       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             INKUT006.                                        
       AUTHOR.                 MURALITHARAN V.                                  
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           02/08/2011.                                      
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * REMARKS                                                        *        
      * -------                                                        *        
      *                                                                *        
      * TITLE        - INVENTORY DURING CLOSE - INCC098 CONTROL CARD   *        
      *                                         BUILDER                *        
      *                                                                *        
      * DESCRIPTION  - NEW PROGRAM TO BUILD INCC098 CONTROL CARD TO    *        
      * HAVE ALTERNATE DATES / DATA TO ACCOUNT FOR PHYSICAL INVENTORY  *        
      * BEING TAKE DURING CLOSING WEEK.                                *        
      *                                                                *        
      * THIS PROGRAM WILL BE EXECUTED FROM THE VAKSAT SCHEDULE EVERY   *        
      * SATURDAY.                                                      *        
      *                                                                *        
      * BECAUSE INK885/INKUT163 AND INK407/INKUT166/ AND  VAK885/      *        
      * VAKUT163 AND VAK407/VAKUT166 NEED TO LOOK AHEAD TO THE NEXT    *        
      * WEEK, THE VAKSAT SCHEDULE WILL BE MODIFIED TO RUN INK510 (NO   *        
      * VA VERSION). IT WILL RUN BEFORE VAK885, VAK407 AND AFTER       *        
      * MLKUP317. THE INKSAT SCHEDULES HAS DEPENDENCIES ALREADY IN IT  *        
      * TO WAIT ON PARTS OF THE VAKSAT SCHEDULE.                       *        
      *                                                                *        
      * WHEN INK510/INKUT006 RUNS FROM THE VAKSAT SCHEDULE, IT WILL BE *        
      * GIVEN A  'TODAY + ONE DAY' ESP PARAMETER WHICH WILL CAUSE IT   *        
      * TO PROCESS IN THE NEXT WEEK.                                   *        
      *                                                                         
      * PLEASE SEE INRD001 FOR EXAMPLES OF THE ALTERNATE DATE CARD              
      * AND ITS VALUES                                                          
      *                                                                         
      * RERUN INSTRUCTIONS.                                            *        
      * -------------------                                            *        
      * IF THIS PROGRAM IS TO BE RERUN, OVERRIDE THE ORIGINAL INPUT AND*        
      * OUTPUT CONTROL CARDS WITH RERUN VERSIONS AND RUN FROM PROCLIBE *        
      * SO THAT THE ORIGINAL CONTROL CARDS ARE NOT CHANGED UNLESS THEY *        
      * NEED TO BE CHANGED PERMANENTLY.                                *        
      *                                                                *        
      * CHANGE HISTORY                                                 *        
      ******************************************************************        
      * WR NUMBER     PROGRAMMER       DATE                            *        
      * ---------------------------------------------------------------*        
      * WR34557       MURALITHARAN V   09/13/2011                      *        
      * ORIGINAL VERSION                                               *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PROCESS-DATE-FILE                                             
               ASSIGN                TO INP01.                                  
                                                                                
           SELECT ALTERNATE-DATE-CNTL-FILE                                      
               ASSIGN                TO OUT01.                                  
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PROCESS-DATE-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  PROCESS-DATE-FILE-REC         PIC X(80).                             
                                                                                
       FD  ALTERNATE-DATE-CNTL-FILE                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  ALTERNATE-DATE-CNTL-REC       PIC X(80).                             
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
      *****************************************************************         
      *  DB2 MERCH LDGR CONTROL                                                 
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DB2 DATE ATTRIBUTES                                                    
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * COPYBOOK FOR SQL COMMUNICATION AREA                           *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  COPYBOOK OF WORKING STORAGE FOR SQL ABEND ROUTINE                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      *  COPYBOOK OF OUTPUT ALTERNATE DATE CONTROL CARD RECORD LAYOUT *         
      *****************************************************************         
                                                                                
           COPY INRD001.                                                        
                                                                                
      ******************************************************************        
      *  PC-PROGRAM CONSTANTS                                          *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           03  PC-PGM-NAME               PIC  X(08)  VALUE 'INKUT006'.          
           03  PC-MAXIMUM-RETRY-COUNT    PIC S9(04)  VALUE +2 COMP SYNC.        
                                                                                
      *****************************************************************         
      *  THE LAYOUT FOR INPUT DATE CONTROL CARD                       *         
      *****************************************************************         
       01  PV-CC-CW-INPUT-CONTROL.                                              
           03  PV-CC-CW-INPUT-DTE        PIC  X(10).                            
           03  FILLER                    PIC  X(70)  VALUE SPACES.              
                                                                                
      *****************************************************************         
      *  GENERAL WORKAREA                                             *         
      *****************************************************************         
       01  PV-PROGRAM-VARIABLES.                                                
                                                                                
           03  PV-PARAGRAPH-NAME         PIC  X(30)  VALUE SPACES.              
           03  PV-RETRY-COUNT            PIC  S9(04) VALUE ZERO.                
           03  PV-OPERATION-MSG-1        PIC  X(60)  VALUE SPACES.              
           03  PV-OPERATION-MSG-2        PIC  X(60)  VALUE SPACES.              
           03  PV-DB2-OPER-ATTEMPTED     PIC  X(50)  VALUE SPACES.              
                                                                                
           03  PV-PROCESS-DATE           PIC  X(10)  VALUE SPACES.              
           03  FILLER              REDEFINES PV-PROCESS-DATE.                   
               05  PV-PROCESS-DATE-CCYY  PIC  X(04).                            
               05  PV-PROCESS-DATE-DASH-1                                       
                                         PIC  X(01).                            
                   88  PV-VALID-PROCESS-DATE-DASH-1  VALUE '-'.                 
               05  PV-PROCESS-DATE-MM    PIC  X(02).                            
               05  PV-PROCESS-DATE-DASH-2                                       
                                         PIC  X(01).                            
                   88  PV-VALID-PROCESS-DATE-DASH-2  VALUE '-'.                 
               05  PV-PROCESS-DATE-DD    PIC  X(02).                            
                                                                                
      *****************************************************************         
      *  GENERAL WORKAREA SQL ABEND                                    *        
      *****************************************************************         
       01  ABEND-CODE                    PIC S9(04)  VALUE +0 COMP SYNC.        
           88  ABEND-4003-CTRL-CARD-ERROR            VALUE +4003.               
           88  ABEND-4013-DB2-ERROR                  VALUE +4013.               
           88  ABEND-4015-DATE-ERROR                 VALUE +4015.               
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      * DESCRIPTION     : CONTROL MAIN PROCESSING FLOW                *         
      *****************************************************************         
       0000-MAIN-CONTROL.                                                       
                                                                                
           PERFORM 1000-INIT-OPEN.                                              
                                                                                
           IF  IN001-IN-CLOSE-WK = 'Y'                                          
               PERFORM 2000-DETERMINE-DATES-DATA                                
           END-IF.                                                              
                                                                                
           PERFORM 8000-CLOSE.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-INIT-OPEN.                                                          
      *****************************************************************         
      * DESCRIPTION     : INITIALISE FIELDS AND OPEN FILES USED       *         
      *****************************************************************         
                                                                                
           DISPLAY '*************************************'.                     
           DISPLAY 'INKUT006 STARTING'.                                         
           DISPLAY '*************************************'.                     
                                                                                
           MOVE "*1000-INIT-OPEN*"         TO PV-PARAGRAPH-NAME                 
                                                                                
           OPEN INPUT                         PROCESS-DATE-FILE                 
                OUTPUT                        ALTERNATE-DATE-CNTL-FILE.         
                                                                                
           INITIALIZE IN001-RECORD.                                             
                                                                                
           PERFORM 1020-READ-DATE-FILE.                                         
                                                                                
           DISPLAY 'INPUT DATE IS ' PV-CC-CW-INPUT-DTE.                         
                                                                                
           PERFORM 1040-SELECT-TMLCNTL.                                         
                                                                                
           DISPLAY 'MLCNTL-OPN-FSCL-MN-DTE '                                    
                                     MLCNTL-OPN-FSCL-MN-DTE.                    
           DISPLAY 'MLCNTL-OPN-FSCL-WK-DTE '                                    
                                     MLCNTL-OPN-FSCL-WK-DTE.                    
           DISPLAY 'MLCNTL-OPN-FSCL-MN-NBR '                                    
                                     MLCNTL-OPN-FSCL-MN-NBR.                    
           DISPLAY 'MLCNTL-OPN-FSCL-WK-NBR '                                    
                                     MLCNTL-OPN-FSCL-WK-NBR.                    
                                                                                
           MOVE MLCNTL-OPN-FSCL-MN-DTE     TO IN001-OPN-FSCL-MN-DTE.            
           MOVE MLCNTL-OPN-FSCL-WK-DTE     TO IN001-OPN-FSCL-WK-DTE.            
           MOVE MLCNTL-OPN-FSCL-MN-NBR     TO IN001-OPN-FSCL-MN-NBR.            
                                                                                
                                                                                
           SET IN001-IN-CLOSE-WK-NO        TO TRUE.                             
                                                                                
           PERFORM 1100-DATE-EDIT-VALIDATE.                                     
                                                                                
      ****************************************************************          
      * READ INPUT DATE CONTROL CARD                                            
      ****************************************************************          
       1020-READ-DATE-FILE.                                                     
                                                                                
           READ PROCESS-DATE-FILE         INTO PV-CC-CW-INPUT-CONTROL           
               AT END                                                           
                 SET ABEND-4003-CTRL-CARD-ERROR                                 
                                            TO TRUE                             
                 MOVE '1020-READ-DATE-FILE' TO PV-PARAGRAPH-NAME                
                 MOVE 'READ PROCESS DATE FILE'                                  
                                            TO PV-OPERATION-MSG-1               
                 MOVE 'MISSING INPUT CONTROL DATE'                              
                                            TO PV-OPERATION-MSG-2               
               PERFORM 9200-ABEND                                               
           END-READ.                                                            
                                                                                
       1040-SELECT-TMLCNTL.                                                     
      *****************************************************************         
      * DESCRIPTION     : SELECT ALL ATTRIBUTES FROM TMLCNTL                    
      *****************************************************************         
                                                                                
           PERFORM WITH TEST AFTER                                              
                   VARYING PV-RETRY-COUNT FROM 1 BY 1                           
                     UNTIL (SQLCODE = +0)                                       
                        OR (SQLCODE = +100)                                     
                        OR (PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT)            
                                                                                
               EXEC SQL                                                         
                   SELECT  OPN_FSCL_MN_DTE                                      
                          ,OPN_FSCL_MN_NBR                                      
                          ,OPN_FSCL_WK_DTE                                      
                          ,OPN_FSCL_WK_NBR                                      
                     INTO :MLCNTL-OPN-FSCL-MN-DTE                               
                         ,:MLCNTL-OPN-FSCL-MN-NBR                               
                         ,:MLCNTL-OPN-FSCL-WK-DTE                               
                         ,:MLCNTL-OPN-FSCL-WK-NBR                               
                     FROM TMLCNTL                                               
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE '1040-SELECT-TMLCNTL'                               
                                            TO PV-PARAGRAPH-NAME                
                       MOVE 'SELECT ROW FROM TMLCNTL'                           
                                            TO PV-DB2-OPER-ATTEMPTED            
                       DISPLAY 'TMLCNTL NOT FOUND OR NOT AVAILABLE'             
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               DISPLAY '** MAX RETRIES EXCEEDED ON TMLCNTL **'                  
               MOVE '1040-SELECT-TMLCNTL'   TO PV-PARAGRAPH-NAME                
               MOVE 'SELECT ROW FROM TMLCNTL'                                   
                                            TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ****************************************************************          
      * VALIDATE THE DATE READ FROM PROCESS DATE FILE                           
      ****************************************************************          
       1100-DATE-EDIT-VALIDATE.                                                 
                                                                                
           MOVE PV-CC-CW-INPUT-DTE          TO PV-PROCESS-DATE                  
                                                                                
           IF  PV-PROCESS-DATE-CCYY IS NUMERIC                                  
           AND PV-PROCESS-DATE-MM IS NUMERIC                                    
           AND PV-PROCESS-DATE-DD IS NUMERIC                                    
           AND PV-VALID-PROCESS-DATE-DASH-1                                     
           AND PV-VALID-PROCESS-DATE-DASH-2                                     
               CONTINUE                                                         
           ELSE                                                                 
               SET ABEND-4015-DATE-ERROR    TO TRUE                             
               MOVE '1100-DATE-EDIT-VALIDATE'                                   
                                            TO PV-PARAGRAPH-NAME                
               MOVE 'PV-CC-CW-INPUT-DTE VALIDATION'                             
                                            TO PV-OPERATION-MSG-1               
               DISPLAY 'PV-CC-CW-INPUT-DTE = ' PV-CC-CW-INPUT-DTE               
               MOVE 'DATE IS INVALID'       TO PV-OPERATION-MSG-2               
               PERFORM 9200-ABEND                                               
           END-IF.                                                              
                                                                                
           MOVE PV-CC-CW-INPUT-DTE          TO DTATTR-KY-DTE.                   
                                                                                
           PERFORM 1120-SELECT-TDTATTR.                                         
                                                                                
           MOVE DTATTR-FSCL-WK-END-DTE     TO IN001-OPN-FSCL-WK-DTE.            
           MOVE DTATTR-FSCL-WK-BEG-DTE     TO IN001-OPN-FSCL-WK-BEG-DTE.        
                                                                                
      *--  IF MONTH END DATES ARE EQUAL, INPUT DATE IS IN CURRENT               
      *    MONTH , RETURN CURRENT DATA (ALREADY MOVED TO OUTPUT)                
      *    AND THE IN001-IN-CLOSE-WK IS ALREADY FALSE ('N')                     
                                                                                
      *--  IF TDATTR MONTH END DATE < TMLCNTL MONTH END DATE, NON               
      *    DB2 ABEND 'INPUT DATE IN PRIOR MONTH'                                
                                                                                
                                                                                
           IF  DTATTR-FSCL-MN-END-DTE = MLCNTL-OPN-FSCL-MN-DTE                  
               CONTINUE                                                         
           ELSE                                                                 
               IF  DTATTR-FSCL-MN-END-DTE < MLCNTL-OPN-FSCL-MN-DTE              
                   SET ABEND-4015-DATE-ERROR                                    
                                            TO TRUE                             
                   MOVE '1100-DATE-EDIT-VALIDATE'                               
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'FSCL-MN-END-DTE VALIDATION'                            
                                            TO PV-OPERATION-MSG-1               
                   MOVE 'INPUT DATE IN PRIOR MONTH'                             
                                            TO PV-OPERATION-MSG-2               
                   DISPLAY 'DTATTR-FSCL-MN-END-DTE = '                          
                            DTATTR-FSCL-MN-END-DTE                              
                   DISPLAY 'MLCNTL-OPN-FSCL-MN-DTE = '                          
                            MLCNTL-OPN-FSCL-MN-DTE                              
                   PERFORM 9200-ABEND                                           
               ELSE                                                             
                   PERFORM 1200-CHECK-NEXT-MN                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
       1120-SELECT-TDTATTR.                                                     
      *****************************************************************         
      * DESCRIPTION     : SELECTS ALL ATTRIBUTES FROM TDTATTR                   
      *****************************************************************         
                                                                                
           PERFORM WITH TEST AFTER                                              
                   VARYING PV-RETRY-COUNT FROM 1 BY 1                           
                     UNTIL (SQLCODE = +0)                                       
                        OR (SQLCODE = +100)                                     
                        OR (PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT)            
                                                                                
               EXEC SQL                                                         
                   SELECT  FSCL_MN_NBR                                          
                          ,WK_IN_MN_NBR                                         
                          ,FSCL_MN_END_DTE                                      
                          ,FSCL_WK_BEG_DTE                                      
                          ,FSCL_WK_END_DTE                                      
                          ,PFM_END_DTE                                          
                     INTO :DTATTR-FSCL-MN-NBR                                   
                         ,:DTATTR-WK-IN-MN-NBR                                  
                         ,:DTATTR-FSCL-MN-END-DTE                               
                         ,:DTATTR-FSCL-WK-BEG-DTE                               
                         ,:DTATTR-FSCL-WK-END-DTE                               
                         ,:DTATTR-PFM-END-DTE                                   
                     FROM TDTATTR                                               
                    WHERE KY_DTE = :DTATTR-KY-DTE                               
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE '1120-SELECT-TDTATTR'                               
                                            TO PV-PARAGRAPH-NAME                
                       MOVE 'SELECT ROW FROM TDTATTR'                           
                                            TO PV-DB2-OPER-ATTEMPTED            
                       DISPLAY 'TDTATTR NOT FOUND OR NOT AVAILABLE'             
                       DISPLAY 'KY_DTE = '     DTATTR-KY-DTE                    
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               DISPLAY '** MAX RETRIES EXCEEDED ON TDTATTR **'                  
               MOVE '1120-SELECT-TDTATTR'   TO PV-PARAGRAPH-NAME                
               MOVE 'SELECT ROW FROM TDTATTR'                                   
                                            TO PV-DB2-OPER-ATTEMPTED            
               DISPLAY 'KY_DTE = '             DTATTR-KY-DTE                    
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ****************************************************************          
      * CHECK THE OPN-FSCL-MN-DTE                                               
      ****************************************************************          
       1200-CHECK-NEXT-MN.                                                      
                                                                                
      *--  INPUT DATE IS IN A FUTURE MONTH AND TMLCNTL IS IN                    
      *    CURRENT MONTH (MONTH BEING CLOSED)                                   
                                                                                
      *--  IF THE PRIOR FISCAL MONTH FROM THE TDTATTR IS GREATER                
      *    THAN THE CURRENT MONTH END IN TMLCNTL THEN THE INPUT                 
      *    DATE IS TOO FAR IN THE FUTURE, CREATE AND ABEND.                     
                                                                                
      *--  IF THE TDTATTR WEEK IN MONTH IS 01 THEN THE INPUT DATE               
      *    IS IN THE FIRST WEEK OF THE NEW MONTH (CUT OFF WEEK OF               
      *    THE OLD MONTH), SEND THE DATA ABOUT THE NEW MONTH. WE                
      *    ONLY WANT THE NEW DATA SENT WHEN WE ARE TRULY                        
      *    RUNNING IN THE FIRST WEEK OF THE NEW MONTH BUT                       
      *    TMLCNTL IS STILL SITTING AT THE END OF THE OLD MONTH                 
                                                                                
           IF  DTATTR-PFM-END-DTE > MLCNTL-OPN-FSCL-MN-DTE                      
               SET ABEND-4015-DATE-ERROR      TO TRUE                           
               MOVE '1200-CHECK-NEXT-MN'      TO PV-PARAGRAPH-NAME              
               MOVE 'MLCNTL-OPN-FSCL-MN-DTE VALIDATION'                         
                                              TO PV-OPERATION-MSG-1             
               MOVE 'INPUT DATE TOO FAR IN THE FUTURE'                          
                                              TO PV-OPERATION-MSG-2             
               DISPLAY 'DTATTR-PFM-END-DTE = '   DTATTR-PFM-END-DTE             
               DISPLAY 'MLCNTL-OPN-FSCL-MN-DTE= ' MLCNTL-OPN-FSCL-MN-DTE        
               PERFORM 9200-ABEND                                               
           ELSE                                                                 
               IF  DTATTR-WK-IN-MN-NBR = '1'                                    
                   SET IN001-IN-CLOSE-WK-YES  TO TRUE                           
               ELSE                                                             
                   SET ABEND-4015-DATE-ERROR  TO TRUE                           
                   MOVE '1200-CHECK-NEXT-MN'  TO PV-PARAGRAPH-NAME              
                   MOVE 'FSCL-MN-END-DTE VALIDATION'                            
                                              TO PV-OPERATION-MSG-1             
                   MOVE 'INPUT DATE OUT OF SYNCH WITH TMLCNTL'                  
                                              TO PV-OPERATION-MSG-2             
                   DISPLAY 'DTATTR-WK-IN-MN-NBR = ' DTATTR-WK-IN-MN-NBR         
                   PERFORM 9200-ABEND                                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *-----------------------------------------------------------------        
      * LOAD THE OUTPUT FILE WHEN IN001-IN-CLOSE-WK-YES                         
      *-----------------------------------------------------------------        
       2000-DETERMINE-DATES-DATA.                                               
                                                                                
           MOVE '*2000-DETERMINE-DATES-DATA*'                                   
                                            TO PV-PARAGRAPH-NAME                
           MOVE DTATTR-FSCL-MN-END-DTE      TO IN001-OPN-FSCL-MN-DTE.           
           MOVE DTATTR-FSCL-WK-END-DTE      TO IN001-OPN-FSCL-WK-DTE.           
           MOVE DTATTR-FSCL-MN-NBR          TO IN001-OPN-FSCL-MN-NBR.           
                                                                                
      *-----------------------------------------------------------------        
      * 8000-CLOSE - WRITE THE OUTPUT FILE AND CLOSES THE FILES                 
      *-----------------------------------------------------------------        
       8000-CLOSE.                                                              
                                                                                
           WRITE ALTERNATE-DATE-CNTL-REC  FROM IN001-RECORD                     
                                                                                
           CLOSE                               PROCESS-DATE-FILE                
                                               ALTERNATE-DATE-CNTL-FILE.        
                                                                                
           DISPLAY 'IN001-IN-CLOSE-WK '                                         
                               IN001-IN-CLOSE-WK.                               
           DISPLAY 'IN001-OPN-FSCL-MN-DTE '                                     
                               IN001-OPN-FSCL-MN-DTE.                           
           DISPLAY 'IN001-OPN-FSCL-WK-DTE '                                     
                               IN001-OPN-FSCL-WK-DTE.                           
           DISPLAY 'IN001-OPN-FSCL-WK-BEG-DTE '                                 
                               IN001-OPN-FSCL-WK-BEG-DTE.                       
           DISPLAY 'IN001-OPN-FSCL-MN-NBR '                                     
                               IN001-OPN-FSCL-MN-NBR.                           
                                                                                
           DISPLAY '*************************************'.                     
           DISPLAY 'INKUT006 ENDING'.                                           
           DISPLAY '*************************************'.                     
                                                                                
                                                                                
      *****************************************************************         
      * 9200-ABEND.                                                   *         
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO FILE              *         
      *****************************************************************         
       9200-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = '  PC-PGM-NAME.                     
           DISPLAY '** PARAGRAPH       ** = '  PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION       ** = '  PV-OPERATION-MSG-1.              
           DISPLAY '**                 ** = '  PV-OPERATION-MSG-2.              
           PERFORM 9950-ABEND.                                                  
                                                                                
       9900-SQL-ABEND.                                                          
      ******************************************************************        
      * SQL ABEND ROUTINE                                                       
      ******************************************************************        
                                                                                
           SET ABEND-4013-DB2-ERROR         TO TRUE.                            
           DISPLAY '**  ABEND          **'.                                     
           DISPLAY '**  PROGRAM        **  = ' PC-PGM-NAME.                     
           DISPLAY '**  PARAGRAPH      **  = ' PV-PARAGRAPH-NAME.               
           DISPLAY '**  OPERATION      **  = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      * THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                    
      * DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                   
      *                                                                         
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9950-ABEND.                                                  
      *                                                                         
       9950-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
      *****************************************************************         
