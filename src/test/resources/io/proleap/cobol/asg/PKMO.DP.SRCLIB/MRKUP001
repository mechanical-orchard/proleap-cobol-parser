       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MRKUP001.                                                 
       AUTHOR.        PAUL KEBER.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07/02/2008.                                               
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *  MRKUP001 - MDS SESSIONS PURGE UPDATE                                   
      *****************************************************************         
      *                                                                         
      *  THIS PROGRAM WILL UPDATE THE SESSION STATUS TO "D" ON                  
      *  MDS_O_SESSIONS ROWS FOR EACH SESSION ID ON THE INPUT FILE.             
      *  THIS WILL ALLOW THESE OBSOLETE SESSION IDS TO BE DELETED.              
      *  CHECKPOINT RESTART LOGIC IS USED IN THIS PROGRAM.                      
      *                                                                         
      *  TABLES UPDATED ARE:                                                    
      *      MDS_O_SESSIONS - MDS OUTLOOK SESSIONS TABLE                        
      *      TCKRSTCNTL     - DB2 CHECKPOINT RESTART CONTROL                    
      *      TCKRSTINF      - DB2 CHECKPOINT RESTART INFORMATION                
      *****************************************************************         
      * 07/02/08 - PAUL KEBER       -  WR NO. 35040                             
      *    - INITIAL IMPLEMENTATION.                                            
      *****************************************************************         
PK0708* 07/23/08 - PAUL KEBER       -  INC NO. INC000000453753                  
PK0708*    - ADD CHECKPOINT RESTART CHECK FOR NO MORE INPUT RECS ON             
PK0708*      RESTART.                                                           
PK0708*****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT PURGE-EXTRACT-FILE           ASSIGN TO INP01.                 
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
       FILE SECTION.                                                            
                                                                                
       FD  PURGE-EXTRACT-FILE                                                   
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PURGE-EXTRACT-RECORD    PIC  X(88).                                  
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME                PIC  X(08)   VALUE                
                                                           'MRKUP001'.          
           05  PC-JOB-NAME                    PIC  X(08)   VALUE                
                                                           'MRK500'.            
           05  PC-MAXIMUM-RETRY-COUNT         PIC S9(04)   VALUE +3             
                                                           COMP SYNC.           
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-WORK-VARIABLES.                                                   
           05  PV-RETRY-COUNT                 PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.        
           05  PV-COMMIT-TIMESTAMP            PIC  X(26)   VALUE SPACES.        
           05  PV-CURRENT-TIMESTAMP           PIC  X(26)   VALUE SPACES.        
           05  PV-RECS-READ-CNTR              PIC  9(09)   VALUE ZEROES.00830000
           05  PV-SQLCODE-DISPLAY             PIC +(8)9    VALUE ZEROES.00830000
           05  PV-SESSION-ID                  PIC  9(09)   VALUE ZERO.          
                                                                                
      ******************************************************************        
      * CR CHECKPOINT RESTART VARIABLES                                *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                    PIC S9(04) VALUE +45              
                                                         COMP.                  
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-SESSION-ID              PIC  9(09) VALUE ZERO.            
               10  CR-RECS-PROCESSED-CNTR     PIC  9(09) VALUE ZERO.            
               10  CR-RECS-READ-CNTR          PIC  9(09) VALUE ZERO.            
               10  CR-MOS-ROW-UPD-CNTR        PIC  9(09) VALUE ZERO.            
               10  CR-COMMIT-CNTR             PIC  9(09) VALUE ZERO.            
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW              PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-FILE                          VALUE 'Y'.           
           05  PS-FIRST-COMMIT-SW             PIC  X(01)   VALUE 'N'.           
               88  PS-FIRST-COMMIT                         VALUE 'Y'.           
           05  PS-NO-PURGE-RECS-SW            PIC  X(01)   VALUE 'N'.           
               88  PS-NO-PURGE-RECS                        VALUE 'Y'.           
           05  PS-CHECKPOINT-RESTART-SW       PIC  X(01)   VALUE 'N'.           
               88  PS-CHECKPOINT-RESTART                   VALUE 'Y'.           
               88  PS-NOT-CHECKPOINT-RESTART               VALUE 'N'.           
                                                                                
       01  ABEND-CODE                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           88  PV-ABEND-4013                   VALUE +4013.                     
      *        DB2 ABEND                                                        
                                                                                
       01  DISPLAY-EDITS.                                                       
           05  DI-RECS-PROCESSED-CNTR       PIC  Z(08)9  VALUE ZEROS.           
           05  DI-RECS-READ-CNTR            PIC  Z(08)9  VALUE ZEROS.           
           05  DI-MOS-ROW-UPDATE-CNTR       PIC  Z(08)9  VALUE ZEROS.           
           05  DI-COMMIT-CNTR               PIC  Z(08)9  VALUE ZEROS.           
                                                                                
      ******************************************************************00790094
      *  MDS PURGE EXTRACT FILE RECORD LAYOUT                           00800094
      ******************************************************************00810094
           COPY MRRD003.                                                00820094
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      ***************************************************************** 02670199
      *  MDS_O_SESSIONS DCLGEN                                          02672099
      ***************************************************************** 02673099
           EXEC SQL                                                             
               INCLUDE MDS00000                                                 
           END-EXEC.                                                            
                                                                        02670099
      *****************************************************************         
      *    COBOL DECLARATIONS FOR THE CHECKPOINT RESTART CONTROL TABLE          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    COBOL DECL FOR THE CHECKPOINT RESTART INFO TABLE                     
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * 0000-MAIN-MODULE.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-PURGE-UPDATES                                   
             UNTIL PS-END-OF-FILE.                                              
                                                                                
           PERFORM 3000-EOJ-PROCESSING.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                           *        
      *  ==> PROCESSES:                                                *        
      *      - CHECKS FOR RESTART PROCESSING.                          *        
      *      - INITIALIZES VARIABLES.                                  *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           OPEN INPUT PURGE-EXTRACT-FILE.                                       
                                                                                
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                                 
                                                                                
      ******************************************************************        
      *    IF PROGRAM IS IN A RESTART STATUS.  SELECT SAVED DATA FROM  *        
      *    THE RESTART INFO TABLE.                                     *        
      ******************************************************************        
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
               PERFORM 7500-SELECT-RESTART-INFO                                 
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                               
                                   TO CR-CHECKPOINT-RESTART-VAR                 
               MOVE CR-SESSION-ID  TO PV-SESSION-ID                             
               SET PS-CHECKPOINT-RESTART TO TRUE                                
           ELSE                                                                 
               SET PS-FIRST-COMMIT TO TRUE                                      
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                             
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-RECORD                                             
               UNTIL PV-RECS-READ-CNTR > CR-RECS-PROCESSED-CNTR                 
                  OR PS-END-OF-FILE.                                            
                                                                                
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
               DISPLAY '** RESTART RUN **'                              06720800
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD: '             02067000
                        PV-RECS-READ-CNTR                               02068000
               DISPLAY 'SESSION ID = ' MR003-SESSION-ID                 06720900
           END-IF.                                                              
                                                                                
           IF PS-END-OF-FILE                                                    
PK0708       AND TCKRSTCNTL-CKPT-STAT-CODE = '0'                                
               SET PS-NO-PURGE-RECS TO TRUE                                     
               DISPLAY ' '                                                      
               DISPLAY '*****************************************'              
               DISPLAY ' NO MDS SESSIONS PURGE UPDATES FOR TODAY.'              
               DISPLAY '*****************************************'              
               DISPLAY ' '                                                      
           ELSE                                                                 
               PERFORM 7100-GET-COMMIT-TIMESTAMP                                
               PERFORM 7200-SET-CURRENT-TIMESTAMP                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2000-PROCESS-PURGE-UPDATES.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - PROCESSES A PURGE UPDATE RECORD AND READS NEXT PURGE      *        
      *    - UPDATE RECORD                                             *        
      ******************************************************************        
       2000-PROCESS-PURGE-UPDATES.                                              
           MOVE '2000-PROCESS-PURGE-UPDATES' TO PV-PARAGRAPH-NAME.              
                                                                                
           MOVE MR003-SESSION-ID TO MDS00000-SESS-ID.                           
           PERFORM 6000-UPDATE-MDS-O-SESSIONS.                                  
                                                                                
           PERFORM 7000-COMMIT-PROCESSING                                       
           ADD +1 TO CR-RECS-PROCESSED-CNTR.                                    
           SET PS-NOT-CHECKPOINT-RESTART TO TRUE.                               
                                                                                
           PERFORM 4000-READ-RECORD.                                            
                                                                                
      ******************************************************************        
      * 3000-EOJ-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - CLOSE FILES                                               *        
      *    - DISPLAYS PROCESSING TOTALS                                *        
      *    - TAKES A FINAL COMMIT AND UPDATES THE CHECKPOINT STATUS    *        
      *      TO INDICATE SUCESSFULL COMPLETION                         *        
      ******************************************************************        
       3000-EOJ-PROCESSING.                                                     
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.                     
                                                                                
           CLOSE PURGE-EXTRACT-FILE.                                            
                                                                                
           IF PS-NO-PURGE-RECS                                                  
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 7800-COMMIT                                              
               MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE                            
               PERFORM 7900-UPDATE-CHECKPOINT-STATUS                            
           END-IF.                                                              
                                                                                
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
      ******************************************************************        
      * 4000-READ-RECORD.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - READS THE NEXT RECORD IN THE INPUT FILE.                  *        
      ******************************************************************        
       4000-READ-RECORD.                                                        
           MOVE '4000-READ-RECORD' TO PV-PARAGRAPH-NAME.                        
                                                                                
           READ PURGE-EXTRACT-FILE                                              
               INTO MR003-MDS-PURGE-EXTRACT                                     
                   AT END                                                       
                       MOVE 'Y' TO PS-END-OF-FILE-SW                            
                   NOT AT END                                                   
                       ADD +1 TO PV-RECS-READ-CNTR                              
                       MOVE MR003-SESSION-ID   TO PV-SESSION-ID                 
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * 6000-UPDATE-MDS-O-SESSIONS.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - UPDATES MDS_O_SESSIONS                                    *        
      ******************************************************************        
       6000-UPDATE-MDS-O-SESSIONS.                                              
           MOVE '6000-UPDATE-MDS-O-SESSIONS' TO PV-PARAGRAPH-NAME.              
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
               EXEC SQL                                                         
                UPDATE MDS_O_SESSIONS                                           
                   SET   SESS_STATUS = 'D'                                      
                   WHERE SESS_ID     = :MDS00000-SESS-ID                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD SQLERRD(3)  TO CR-MOS-ROW-UPD-CNTR                   
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       ADD +1          TO PV-RETRY-COUNT                        
                   WHEN OTHER                                                   
                        MOVE 'UPDATE MDS_O_SESSIONS'                            
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                        PERFORM 9999-SQL-ABEND                                  
                END-EVALUATE                                                    
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE MDS_O_SESSIONS RETRIES EXCEEDED'                    
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7000-COMMIT-PROCESSING.                                        *        
      *  ==> PROCESSES:                                                *        
      *    - CHECK IF WE NEED TO TAKE A COMMIT.                        *        
      *    - COMMIT THIS UNIT OF WORK.                                 *        
      ******************************************************************        
       7000-COMMIT-PROCESSING.                                                  
                                                                                
           PERFORM 7200-SET-CURRENT-TIMESTAMP.                                  
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                        
               IF PS-FIRST-COMMIT                                               
                   MOVE '9'        TO TCKRSTCNTL-CKPT-STAT-CODE                 
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                        
                   MOVE 'N'        TO PS-FIRST-COMMIT-SW                        
               END-IF                                                           
               PERFORM 7800-COMMIT                                              
               PERFORM 7100-GET-COMMIT-TIMESTAMP                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7100-GET-COMMIT-TIMESTAMP.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT CONTROL      *        
      *      TABLE.                                                    *        
      ******************************************************************        
       7100-GET-COMMIT-TIMESTAMP.                                               
           MOVE '7100-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME.               
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   SELECT                                                       
                     (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)              
                   INTO                                                         
                     :PV-COMMIT-TIMESTAMP                                       
                   FROM TCKRSTCNTL                                              
                  WHERE JOB_NAME  = :PC-JOB-NAME                                
                    AND PLAN_NAME = :PC-PROGRAM-NAME                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'SELECT TCKRSTCNTL'                                 
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TCKRSTCNTL RETRIES EXCEEDED'                        
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7200-SET-CURRENT-TIMESTAMP.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - GET THE CURRENT TIMESTAMP FROM DB2.                       *        
      ******************************************************************        
       7200-SET-CURRENT-TIMESTAMP.                                              
           MOVE '7200-SET-CURRENT-TIMESTAMP' TO PV-PARAGRAPH-NAME.              
                                                                                
           EXEC SQL                                                             
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'ERROR ON SET OF TIMESTAMP '                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 7400-INIT-CHECKPOINT-SELECT.                                   *        
      *  ==> PROCESSES:                                                *        
      *    - SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE TO SEE  *        
      *      IF WE ARE IN A RESTART CONDITION.                         *        
      ******************************************************************        
       7400-INIT-CHECKPOINT-SELECT.                                             
           MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME.             
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                 SELECT                                                         
                      CKPT_STAT_CODE                                            
                   INTO                                                         
                     :TCKRSTCNTL-CKPT-STAT-CODE                                 
                   FROM  TCKRSTCNTL                                             
                  WHERE  JOB_NAME  = :PC-JOB-NAME                               
                    AND  PLAN_NAME = :PC-PROGRAM-NAME                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'SELECT TCKRSTCNTL'                                 
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TCKRSTCNTL RETRIES EXCEEDED'                        
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7500-SELECT-RESTART-INFO.                                      *        
      *  ==> PROCESSES:                                                *        
      *    - SELECTS FROM THE CHECKPOINT RESTART INFORMATION TABLE TO  *        
      *      OBTAIN THE PROGRAMS SAVED VARIABLES.                      *        
      ******************************************************************        
       7500-SELECT-RESTART-INFO.                                                
           MOVE '7500-SELECT-RESTART-INFO' TO PV-PARAGRAPH-NAME.                
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   SELECT                                                       
                      WORK_STRG_DESC                                            
                   INTO                                                         
                     :TCKRSTINF-WORK-STRG-DESC                                  
                   FROM  TCKRSTINF                                              
                  WHERE  JOB_NAME  = :PC-JOB-NAME                               
                    AND  PLAN_NAME = :PC-PROGRAM-NAME                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'SELECT TCKRSTINF'                                  
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TCKRSTINF RETRIES EXCEEDED'                         
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7800-COMMIT.                                                   *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.           *        
      *    - TAKE A COMMIT.                                            *        
      ******************************************************************        
       7800-COMMIT.                                                             
           MOVE '7800-COMMIT'      TO PV-PARAGRAPH-NAME.                        
                                                                                
           ADD +1                  TO CR-COMMIT-CNTR.                           
           MOVE PV-SESSION-ID      TO CR-SESSION-ID.                            
           MOVE PV-RECS-READ-CNTR  TO CR-RECS-READ-CNTR.                        
           MOVE CR-CHECKPOINT-RESTART-AREA                                      
                                   TO TCKRSTINF-WORK-STRG-DESC.                 
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                 UPDATE TCKRSTINF                                               
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC              
                  WHERE JOB_NAME       = :PC-JOB-NAME                           
                    AND PLAN_NAME      = :PC-PROGRAM-NAME                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'UPDATE TCKRSTINF'                                  
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TCKRSTINF RETRIES EXCEEDED'                         
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = 0                                                   
               MOVE 'ERROR ON COMMIT'                                           
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7900-UPDATE-CHECKPOINT-STATUS.                                 *        
      *  ==> PROCESSES:                                                *        
      *    - UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE        *        
      ******************************************************************        
       7900-UPDATE-CHECKPOINT-STATUS.                                           
           MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME.           
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                 UPDATE  TCKRSTCNTL                                             
                    SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE            
                  WHERE  JOB_NAME  = :PC-JOB-NAME                               
                    AND  PLAN_NAME = :PC-PROGRAM-NAME                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'UPDATE TCKRSTCNTL'                                 
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TCKRSTCNTL RETRIES EXCEEDED'                        
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 9990-DISPLAY-CONTROL-INFO.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
      ******************************************************************        
       9990-DISPLAY-CONTROL-INFO.                                               
                                                                                
           MOVE CR-RECS-PROCESSED-CNTR                                          
                                   TO DI-RECS-PROCESSED-CNTR.                   
           MOVE CR-RECS-READ-CNTR  TO DI-RECS-READ-CNTR.                        
           MOVE CR-MOS-ROW-UPD-CNTR                                             
                                   TO DI-MOS-ROW-UPDATE-CNTR.                   
           MOVE CR-COMMIT-CNTR     TO DI-COMMIT-CNTR.                           
                                                                                
           DISPLAY '********************************************'.              
           DISPLAY '**** RUN STATISTICS FOR PROGRAM MRKUP001 ***'.              
           DISPLAY '****'.                                                      
           DISPLAY 'INPUT RECS PROCESSED:        '                              
                                            DI-RECS-PROCESSED-CNTR.             
           DISPLAY 'INPUT RECORDS READ:          ' DI-RECS-READ-CNTR.           
           DISPLAY 'MDS_O_SESSIONS ROWS UPDATED: '                              
                                            DI-MOS-ROW-UPDATE-CNTR.             
           DISPLAY 'COMMITS TAKEN:               ' DI-COMMIT-CNTR.              
           DISPLAY '****'.                                                      
           DISPLAY '**** RUN STATS END FOR PROGRAM MRKUP001  ***'.              
           DISPLAY '********************************************'.              
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND.                                                *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *        
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
                                                                                
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'MRKUP001 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                           
           DISPLAY 'INPUT SESSION ID = ' MR003-SESSION-ID.                      
                                                                                
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           SET PV-ABEND-4013 TO TRUE.                                           
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
