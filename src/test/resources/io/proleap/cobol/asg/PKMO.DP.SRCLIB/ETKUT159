       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT159.                                                 
       AUTHOR.        WADE GROVE.                                               
       DATE-WRITTEN.  12/01/2009.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM SPLITS THE SALES HISTORY DATA INTO MULTIPLE      *        
      *  GDG'S FOR PROCESSING.                                         *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE1                                                   
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT INPUT-FILE2                                                   
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT OUTPUT-FILE1                                                  
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT OUTPUT-FILE2                                                  
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT OUTPUT-FILE3                                                  
               ASSIGN TO OUT03.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE1                                                          
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 50 CHARACTERS                                        
           DATA RECORD IS ET035-RECORD-COUNT-RECORD.                            
                                                                                
           COPY ETRD035.                                                        
                                                                                
       FD  INPUT-FILE2                                                          
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS ET019-PRODUCT-ACTIVITY-RECORD.                        
                                                                                
           COPY ETRD019.                                                        
                                                                                
       FD  OUTPUT-FILE1                                                         
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 50 CHARACTERS                                        
           DATA RECORD IS OUTPUT-COUNT-RECORD.                                  
                                                                                
       01  OUTPUT-COUNT-RECORD.                                                 
           05  OUTPUT-ENT-ID                     PIC 9(10).                     
           05  FILLER                            PIC X(01).                     
           05  OUTPUT-DEPARTMENT                 PIC X(03).                     
           05  FILLER                            PIC X(01).                     
           05  OUTPUT-DEPARTMENT-RECORDS         PIC 9(09).                     
           05  FILLER                            PIC X(01).                     
           05  OUTPUT-ENT-ID-RECORDS             PIC 9(09).                     
           05  FILLER                            PIC X(01).                     
           05  OUTPUT-DEPARTMENTS                PIC 9(09).                     
           05  FILLER                            PIC X(06).                     
                                                                                
                                                                                
       FD  OUTPUT-FILE2                                                         
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS OUTPUT-RECORD2.                                       
                                                                                
       01  OUTPUT-RECORD2                   PIC X(150).                         
                                                                                
                                                                                
       FD  OUTPUT-FILE3                                                         
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS OUTPUT-RECORD3.                                       
                                                                                
       01  OUTPUT-RECORD3                   PIC X(150).                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CURR-FILE-RECORD-COUNT      PIC 9(10)   VALUE 0.                  
       01  WS-PREV-ENT-ID                 PIC 9(10)   VALUE 0.                  
       01  WS-PREV-DEPARTMENT             PIC 9(10)   VALUE 0.                  
       01  WS-LOOK-AHEAD-RECORD-COUNT     PIC 9(09)   VALUE 0.                  
       01  WS-MAX-FILE-RECORD-COUNT       PIC 9(07)   VALUE 1000000.            
       01  WS-VENDORS-LEFT-COUNT          PIC 9(9)    VALUE ZERO.               
       01  WS-ENT-ID-COUNT-AFTER          PIC 9(9)    VALUE ZERO.               
       01  WS-RECORD-COUNT-REMAINDER      PIC 9(9)    VALUE ZERO.               
       01  WS-DEPARTMENT-CORRECTION       PIC 9(9)    VALUE ZERO.               
                                                                                
       01  OUTPUT-FILE-SW                 PIC X(1)    VALUE '1'.                
           88  OUTPUT-FILE1-ACTIVE                    VALUE '1'.                
           88  OUTPUT-FILE2-ACTIVE                    VALUE '2'.                
       01  END-OF-INPUT-FILE-SW1          PIC X(1)    VALUE 'N'.                
           88  END-OF-INPUT-FILE1                     VALUE 'Y'.                
       01  END-OF-INPUT-FILE-SW2          PIC X(1)    VALUE 'N'.                
           88  END-OF-INPUT-FILE2                     VALUE 'Y'.                
       01  RECORDS-SPLIT                  PIC X(1)    VALUE 'N'.                
           88  SPLIT                                  VALUE 'Y'.                
                                                                                
       01  TBL-RECORD-COUNTS.                                                   
           05  TBL-COUNT-ENTRIES OCCURS 2000 TIMES.                             
               10  TBL-ENT-ID             PIC 9(10).                            
               10  TBL-DEPARTMENT         PIC X(03).                            
               10  TBL-DEPARTMENT-RECORDS PIC 9(09).                            
               10  TBL-ENT-ID-RECORDS     PIC 9(09).                            
               10  TBL-DEPARTMENTS        PIC 9(09).                            
               10  TBL-PROCESSED          PIC X(01).                            
                                                                                
                                                                                
       01  X                   PIC 9(9)  VALUE 1.                               
       01  MAX-X               PIC 9(9)  VALUE ZERO.                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  INPUT-FILE1                                              
                       INPUT-FILE2.                                             
                                                                                
           OPEN OUTPUT OUTPUT-FILE1                                             
                       OUTPUT-FILE2                                             
                       OUTPUT-FILE3.                                            
                                                                                
           INITIALIZE TBL-RECORD-COUNTS.                                        
                                                                                
           PERFORM 1000-READ-COUNTS                                             
              THRU 1000-READ-COUNTS-EXIT                                        
             UNTIL END-OF-INPUT-FILE1.                                          
                                                                                
           MOVE X TO MAX-X.                                                     
           DISPLAY "TOTAL VENDOR RECS " MAX-X.                                  
                                                                                
           PERFORM 2000-PROCESS-COUNTS THRU                                     
                   2000-PROCESS-COUNTS-EXIT                                     
           VARYING X FROM 1 BY 1                                                
             UNTIL X > MAX-X OR END-OF-INPUT-FILE2.                             
                                                                                
           PERFORM 5000-WRITE-OUTPUT3 THRU 5000-WRITE-OUTPUT3-EXIT              
           VARYING X FROM 1 BY 1 UNTIL X = MAX-X.                               
                                                                                
WG1105     DISPLAY 'VENDORS LEFT ' WS-VENDORS-LEFT-COUNT.                       
                                                                                
WG1105     IF WS-VENDORS-LEFT-COUNT = 0                                         
WG1105        MOVE 0 TO RETURN-CODE.                                            
                                                                                
           CLOSE INPUT-FILE1                                                    
                 INPUT-FILE2                                                    
                 OUTPUT-FILE1                                                   
                 OUTPUT-FILE2                                                   
                 OUTPUT-FILE3.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-READ-COUNTS.                                                        
                                                                                
           READ INPUT-FILE1                                                     
              AT END                                                            
                 SET END-OF-INPUT-FILE1 TO TRUE                                 
                 GO TO 1000-READ-COUNTS-EXIT.                                   
                                                                                
           MOVE ET035-ENT-ID             TO TBL-ENT-ID (X).                     
           MOVE ET035-DEPARTMENT         TO TBL-DEPARTMENT (X).                 
           MOVE ET035-DEPARTMENT-RECORDS TO TBL-DEPARTMENT-RECORDS (X).         
           MOVE ET035-ENT-ID-RECORDS     TO TBL-ENT-ID-RECORDS (X).             
           MOVE ET035-DEPARTMENTS        TO TBL-DEPARTMENTS (X).                
           MOVE "N"                      TO TBL-PROCESSED (X).                  
                                                                                
      *    DISPLAY "(X) = "                                                     
      *              X.                                                         
      *    DISPLAY "TBL-ENT-ID (X) = "                                          
      *             TBL-ENT-ID (X).                                             
      *    DISPLAY "TBL-DEPARTMENT (X) = "                                      
      *             TBL-DEPARTMENT (X).                                         
      *    DISPLAY "TBL-DEPARTMENT-RECORDS (X) = "                              
      *             TBL-DEPARTMENT-RECORDS (X).                                 
      *    DISPLAY "TBL-ENT-ID-RECORDS (X) = "                                  
      *             TBL-ENT-ID-RECORDS (X).                                     
      *    DISPLAY "TBL-DEPARTMENTS (X) = "                                     
      *             TBL-DEPARTMENTS (X).                                        
                                                                                
           ADD 1 TO X.                                                          
                                                                                
       1000-READ-COUNTS-EXIT.                                                   
           EXIT.                                                                
                                                                                
       2000-PROCESS-COUNTS.                                                     
                                                                                
      *                                                                         
      *    DISPLAY "WS-LOOK-AHEAD-RECORD-COUNT ="                               
      *            WS-LOOK-AHEAD-RECORD-COUNT.                                  
      *    DISPLAY "WS-CURR-FILE-RECORD-COUNT ="                                
      *            WS-CURR-FILE-RECORD-COUNT.                                   
      *    DISPLAY "TBL-DEPARTMENT-RECORDS ="                                   
      *            TBL-DEPARTMENT-RECORDS (X).                                  
      *    DISPLAY " ".                                                         
                                                                                
      *----------------------------------------------------------------         
      *----------------------------------------------ENT-ID > 1,000,000         
      *----------------------------------------------------------------         
           IF TBL-ENT-ID-RECORDS (X) = WS-MAX-FILE-RECORD-COUNT                 
             OR                                                                 
              TBL-ENT-ID-RECORDS (X) > WS-MAX-FILE-RECORD-COUNT                 
              MOVE TBL-ENT-ID-RECORDS (X) TO WS-ENT-ID-COUNT-AFTER              
              IF WS-LOOK-AHEAD-RECORD-COUNT > WS-MAX-FILE-RECORD-COUNT          
                OR                                                              
                 WS-LOOK-AHEAD-RECORD-COUNT = WS-MAX-FILE-RECORD-COUNT          
                 DISPLAY "ENT-ID CHANGE FROM "                                  
                         WS-PREV-ENT-ID " TO " TBL-ENT-ID (X)                   
                 DISPLAY "  MAX RECORDS HIT"                                    
                 MOVE 0001 TO RETURN-CODE                                       
                 PERFORM 4000-PROCESS-FILE THRU                                 
                         4000-PROCESS-FILE-EXIT                                 
                   UNTIL END-OF-INPUT-FILE2                                     
                 GO TO 2000-PROCESS-COUNTS-EXIT                                 
              ELSE                                                              
                 MOVE TBL-DEPARTMENTS (X) TO WS-DEPARTMENT-CORRECTION           
      *          DISPLAY "TBL-ENT-ID-RECORDS = "                                
      *                   TBL-ENT-ID-RECORDS (X)                                
      *          DISPLAY "WS-RECORD-COUNT-SPLIT = "                             
      *                   WS-RECORD-COUNT-SPLIT                                 
                 PERFORM 2500-PROCESS-PART-ENT-ID                               
                    THRU 2500-PROCESS-PART-ENT-ID-EXIT                          
                 VARYING X FROM X BY 1                                          
                   UNTIL SPLIT                                                  
                 PERFORM 2600-CLEANUP-PART-ENT-ID                               
                    THRU 2600-CLEANUP-PART-ENT-ID-EXIT                          
                         WS-DEPARTMENT-CORRECTION TIMES                         
                 MOVE 0001 TO RETURN-CODE                                       
                 PERFORM 4000-PROCESS-FILE THRU                                 
                         4000-PROCESS-FILE-EXIT                                 
                   UNTIL END-OF-INPUT-FILE2                                     
                 GO TO 2000-PROCESS-COUNTS-EXIT.                                
      *----------------------------------------------------------------         
      *---------------------------------------------------ENT-ID CHANGE         
      *----------------------------------------------------------------         
           IF TBL-ENT-ID (X) NOT EQUAL WS-PREV-ENT-ID                           
              DISPLAY "ENT-ID CHANGE FROM "                                     
                      WS-PREV-ENT-ID " TO " TBL-ENT-ID (X)                      
      **---------------------------------------------------------------         
      **FIRST TIME----------------------------------------ENT-ID CHANGE         
      **---------------------------------------------------------------         
              IF WS-PREV-ENT-ID = 0                                             
      *          DISPLAY "  FIRST TIME"                                         
                 PERFORM 3000-PROCESS-FILE THRU                                 
                         3000-PROCESS-FILE-EXIT                                 
                         TBL-DEPARTMENT-RECORDS (X) TIMES                       
                 DISPLAY "DEPT " TBL-DEPARTMENT (X)                             
                         "  RECORDS PROCESSED "                                 
                          TBL-DEPARTMENT-RECORDS (X) " "                        
                          WS-CURR-FILE-RECORD-COUNT                             
                 MOVE TBL-DEPARTMENT (X) TO WS-PREV-DEPARTMENT                  
                 MOVE TBL-ENT-ID     (X) TO WS-PREV-ENT-ID                      
                 MOVE "Y" TO TBL-PROCESSED (X)                                  
                 GO TO 2000-PROCESS-COUNTS-EXIT                                 
              ELSE                                                              
      **---------------------------------------------------------------         
      **NOT FIRST TIME------------------------------------ENT-ID CHANGE         
      **---------------------------------------------------------------         
                 COMPUTE WS-LOOK-AHEAD-RECORD-COUNT =                           
                         WS-CURR-FILE-RECORD-COUNT +                            
                         TBL-ENT-ID-RECORDS (X)                                 
                 DISPLAY "LOOK AHEAD = "                                        
                          WS-LOOK-AHEAD-RECORD-COUNT " "                        
                          WS-CURR-FILE-RECORD-COUNT " "                         
                          TBL-ENT-ID-RECORDS (X)                                
      ***--------------------------------------------------------------         
      ***MAX RECORDS HIT----------------------------------ENT-ID CHANGE         
      ***--------------------------------------------------------------         
                 IF WS-LOOK-AHEAD-RECORD-COUNT >                                
                    WS-MAX-FILE-RECORD-COUNT                                    
                  OR                                                            
                    WS-LOOK-AHEAD-RECORD-COUNT =                                
                    WS-MAX-FILE-RECORD-COUNT                                    
                    DISPLAY "  MAX RECORDS HIT"                                 
                    MOVE 0001 TO RETURN-CODE                                    
                    PERFORM 4000-PROCESS-FILE THRU                              
                            4000-PROCESS-FILE-EXIT                              
                      UNTIL END-OF-INPUT-FILE2                                  
                    GO TO 2000-PROCESS-COUNTS-EXIT                              
                 ELSE                                                           
      ***--------------------------------------------------------------         
      ***MAX RECORDS NOT HIT------------------------------ENT-ID CHANGE         
      ***--------------------------------------------------------------         
      *             DISPLAY "  MAX RECORDS NOT HIT"                             
                    PERFORM 3000-PROCESS-FILE THRU                              
                            3000-PROCESS-FILE-EXIT                              
                            TBL-DEPARTMENT-RECORDS (X) TIMES                    
                    DISPLAY "DEPT " TBL-DEPARTMENT (X)                          
                            "  RECORDS PROCESSED "                              
                             TBL-DEPARTMENT-RECORDS (X) " "                     
                             WS-CURR-FILE-RECORD-COUNT                          
                    MOVE "Y" TO TBL-PROCESSED (X)                               
                    MOVE TBL-ENT-ID     (X) TO WS-PREV-ENT-ID                   
                    MOVE TBL-DEPARTMENT (X) TO WS-PREV-DEPARTMENT               
                    GO TO 2000-PROCESS-COUNTS-EXIT                              
      *----------------------------------------------------------------         
      *-----------------------------------------------NOT ENT-ID CHANGE         
      *----------------------------------------------------------------         
           ELSE                                                                 
      *       DISPLAY "  NOT ENT-ID CHANGE"                                     
              PERFORM 3000-PROCESS-FILE THRU                                    
                      3000-PROCESS-FILE-EXIT                                    
                      TBL-DEPARTMENT-RECORDS (X) TIMES                          
              DISPLAY "DEPT " TBL-DEPARTMENT (X)                                
                      "  RECORDS PROCESSED " TBL-DEPARTMENT-RECORDS (X)         
                      " "                                                       
                       WS-CURR-FILE-RECORD-COUNT                                
              MOVE "Y" TO TBL-PROCESSED (X)                                     
              MOVE TBL-DEPARTMENT (X) TO WS-PREV-DEPARTMENT                     
              GO TO 2000-PROCESS-COUNTS-EXIT.                                   
                                                                                
                                                                                
       2000-PROCESS-COUNTS-EXIT.                                                
           EXIT.                                                                
                                                                                
       2500-PROCESS-PART-ENT-ID.                                                
           PERFORM 3000-PROCESS-FILE                                            
              THRU 3000-PROCESS-FILE-EXIT                                       
                   TBL-DEPARTMENT-RECORDS (X) TIMES.                            
           MOVE "Y" TO TBL-PROCESSED (X).                                       
           COMPUTE WS-DEPARTMENT-CORRECTION =                                   
                   WS-DEPARTMENT-CORRECTION - 1.                                
           COMPUTE WS-ENT-ID-COUNT-AFTER =                                      
                   WS-ENT-ID-COUNT-AFTER -                                      
                   TBL-DEPARTMENT-RECORDS (X).                                  
           IF WS-CURR-FILE-RECORD-COUNT >                                       
              WS-MAX-FILE-RECORD-COUNT                                          
             OR                                                                 
              WS-CURR-FILE-RECORD-COUNT =                                       
              WS-MAX-FILE-RECORD-COUNT                                          
              MOVE 'Y' TO RECORDS-SPLIT                                         
              COMPUTE WS-RECORD-COUNT-REMAINDER =                               
                      TBL-ENT-ID-RECORDS (X)    -                               
                      WS-CURR-FILE-RECORD-COUNT                                 
              DISPLAY "WS-RECORD-COUNT-REMAINDER = "                            
                       WS-RECORD-COUNT-REMAINDER                                
           ELSE                                                                 
              NEXT SENTENCE.                                                    
                                                                                
       2500-PROCESS-PART-ENT-ID-EXIT.                                           
           EXIT.                                                                
                                                                                
       2600-CLEANUP-PART-ENT-ID.                                                
           MOVE WS-DEPARTMENT-CORRECTION TO TBL-DEPARTMENTS (X).                
           MOVE WS-ENT-ID-COUNT-AFTER TO TBL-ENT-ID-RECORDS (X).                
           COMPUTE X = X + 1.                                                   
       2600-CLEANUP-PART-ENT-ID-EXIT.                                           
           EXIT.                                                                
       3000-PROCESS-FILE.                                                       
                                                                                
           READ INPUT-FILE2                                                     
              AT END                                                            
                 SET END-OF-INPUT-FILE2 TO TRUE.                                
                                                                                
           MOVE ET019-PRODUCT-ACTIVITY-RECORD TO OUTPUT-RECORD2.                
           WRITE OUTPUT-RECORD2.                                                
           ADD 1 TO WS-CURR-FILE-RECORD-COUNT.                                  
                                                                                
       3000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
       4000-PROCESS-FILE.                                                       
                                                                                
           READ INPUT-FILE2                                                     
              AT END                                                            
                 SET END-OF-INPUT-FILE2 TO TRUE                                 
                 GO TO 4000-PROCESS-FILE-EXIT.                                  
                                                                                
           MOVE ET019-PRODUCT-ACTIVITY-RECORD TO OUTPUT-RECORD3.                
           WRITE OUTPUT-RECORD3.                                                
                                                                                
       4000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
       5000-WRITE-OUTPUT3.                                                      
                                                                                
           DISPLAY TBL-ENT-ID (X) " "                                           
                   TBL-DEPARTMENT (X) " "                                       
                   TBL-DEPARTMENT-RECORDS (X) " "                               
                   TBL-ENT-ID-RECORDS (X) " "                                   
                   TBL-PROCESSED (X).                                           
                                                                                
                                                                                
           IF TBL-PROCESSED (X) = "N"                                           
              MOVE TBL-ENT-ID (X) TO OUTPUT-ENT-ID                              
              MOVE TBL-DEPARTMENT (X) TO OUTPUT-DEPARTMENT                      
              MOVE TBL-DEPARTMENT-RECORDS (X) TO                                
                   OUTPUT-DEPARTMENT-RECORDS                                    
              MOVE TBL-ENT-ID-RECORDS (X) TO                                    
                   OUTPUT-ENT-ID-RECORDS                                        
              MOVE TBL-DEPARTMENTS (X) TO                                       
                   OUTPUT-DEPARTMENTS                                           
              ADD 1 TO WS-VENDORS-LEFT-COUNT                                    
              WRITE OUTPUT-COUNT-RECORD.                                        
                                                                                
       5000-WRITE-OUTPUT3-EXIT.                                                 
           EXIT.                                                                
                                                                                
