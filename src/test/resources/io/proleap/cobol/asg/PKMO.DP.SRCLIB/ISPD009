000100*****************************************************************         
000200**                                                                        
000300**   Format and DISPLAY an XML message.                                   
000400**   This routine is used to aid in debugging the XML PARSE               
000500**   and XML GENERATE commands in a COBOL program by formatting           
000600**   an XML message into a more readable format.                          
000700**                                                                        
000800**   This copybook is used in conjunction with ISWS009.                   
000900**   Your program must include the following copybooks:                   
001000**       ISWS007                                                          
001100**       ISWS008                                                          
001200**       ISWS009                                                          
001300**       ISPD007                                                          
001400**       ISPD008                                                          
001500*****************************************************************         
001600                                                                          
001700 9554-DISPLAY-FORMATTED-XML.                                              
001800                                                                          
001900     PERFORM 9555-FORMAT-XML.                                             
002000                                                                          
002100                                                                          
002200 9557-XML-ERROR.                                                          
002300                                                                          
002400     DISPLAY ' '.                                                         
002500     DISPLAY '**************************************'.                    
002600     DISPLAY '*** ERROR detected at position ' XML-TXT-LENGTH             
002700             ' ***'.                                                      
002800     DISPLAY '**************************************'.                    
002900     PERFORM 9000-EXPLAIN-XML-RESPONSE                                    
003000     PERFORM  VARYING XML-LINE-IDX FROM 1 BY 1                            
003100                UNTIL XML-LINE-IDX > XML-RESPONSE-LINES                   
003200         DISPLAY XML-MSG(XML-LINE-IDX)                                    
003300     END-PERFORM.                                                         
003400     DISPLAY '**************************************'.                    
003500     DISPLAY ' '.                                                         
003600     IF XML-TXT-LENGTH > 0                                                
003700       DISPLAY '      The "|" below a line indicates the location'        
003800       DISPLAY '      of the error.'                                      
003900       DISPLAY '*** Input XML *** Begin'                                  
004000       PERFORM  VARYING XML-BK FROM 1 BY XML-MAX-LINE-LENGTH              
004100                  UNTIL XML-BK > 999998                                   
004200                     OR XML-UNFORMATTED-MSG(XML-BK:) = SPACES             
004300         DISPLAY XML-UNFORMATTED-MSG(XML-BK:XML-MAX-LINE-LENGTH)          
004400         IF ((XML-TXT-LENGTH >= XML-BK)                                   
004500         AND (XML-TXT-LENGTH <  XML-BK + XML-MAX-LINE-LENGTH))            
004600           DISPLAY XML-SPACES(1:XML-TXT-LENGTH - XML-BK - 1)              
004700                   '|'                                                    
004800         ELSE                                                             
004900           DISPLAY ' '                                                    
005000         END-IF                                                           
005100       END-PERFORM                                                        
005200       DISPLAY '*** Input XML *** End'                                    
005300       DISPLAY ' '                                                        
005400     END-IF.                                                              
005500     MOVE +4018         TO XML-FORMAT-ABEND-CODE.                         
005600     CALL 'ILBOABN0' USING XML-FORMAT-ABEND-CODE.                         
005700                                                                          
005800                                                                          
005900 9558-XML-SUCCESS.                                                        
006000                                                                          
006100     PERFORM  VARYING XML-FMT-LINE FROM 1 BY 1                            
006200                UNTIL XML-FMT-LINE > XML-FORMATTED-LINES                  
006300         DISPLAY XML-FMT-MSG(XML-FMT-LINE)                                
006400     END-PERFORM.                                                         
