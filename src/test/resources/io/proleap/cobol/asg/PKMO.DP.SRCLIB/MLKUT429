000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    MLKUT429.                                                 
000300 AUTHOR.        JOHN SELWITSCHKA.                                         
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  01-18-07.                                                 
000600 DATE-COMPILED.                                                           
000700******************************************************************        
000800*                                                                         
000900*    MLKUT429 -  CREATE DATA INTEGRITY TWKMCLC EXTRACT FILE               
001000*                                                                         
001100* THIS PROGRAM WILL CREATE THE DATA INTEGRITY EXTRACT FILE FOR            
001200* DB2 TABLE TWKMCLC.                                                      
001300*                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
      * W28582  01-18-2007 J SELWITSCHKA                                        
      *                                NEW PROGRAM.                             
001400******************************************************************        
001500*                                                                         
001600 ENVIRONMENT DIVISION.                                                    
001700*                                                                         
001800 CONFIGURATION SECTION.                                                   
001900*                                                                         
002000 SOURCE-COMPUTER.        IBM-3090.                                        
002100 OBJECT-COMPUTER.        IBM-3090.                                        
002200*                                                                         
002300 INPUT-OUTPUT SECTION.                                                    
002400 FILE-CONTROL.                                                            
002500                                                                          
002600     SELECT DATA-INTEGRITY-CTL-FILE                                       
002700         ASSIGN TO INP01.                                                 
002800                                                                          
002900     SELECT DATA-INTEGRITY-EXTR-FILE                                      
003000         ASSIGN TO OUT01.                                                 
003100                                                                          
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700                                                                          
003800 FD  DATA-INTEGRITY-CTL-FILE                                              
003900     LABEL RECORDS ARE STANDARD                                           
004000     RECORDING MODE IS F                                                  
004100     BLOCK CONTAINS 0 RECORDS.                                            
004200 01  DATA-INTEGRITY-CTL-REC      PIC X(80).                               
004300                                                                          
004400 FD  DATA-INTEGRITY-EXTR-FILE                                             
004500     RECORDING MODE IS F                                                  
004600     LABEL RECORDS ARE STANDARD                                           
004700     BLOCK CONTAINS 0 RECORDS.                                            
004800                                                                          
W26603 01  DATA-INTEGRITY-EXTR-REC     PIC X(204).                              
005000                                                                          
005100*                                                                         
005200******************************************************************        
005300 WORKING-STORAGE SECTION.                                                 
005400******************************************************************        
005500                                                                          
005600 01  PS-PROGRAM-SWITCHES.                                                 
005700     05  PS-TWKMCLC-DONE         PIC X       VALUE 'N'.                   
005800         88  PS-TWKMCLC-IS-DONE              VALUE 'Y'.                   
005900         88  PS-TWKMCLC-IS-NOT-DONE          VALUE 'N'.                   
006000                                                                          
006100 01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
006200     05  PA-TWKMCLC-ROWS-FETCHED PIC S9(13)  VALUE ZERO.                  
006300     05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.                  
006400                                                                          
006500 01  PV-PROGRAM-VARIABLES.                                                
006800     05  PV-PREV-FSCL-MN-END-DTE PIC X(10) VALUE SPACE.                   
006900     05  PV-PREV-DEPT-NBR        PIC X(03) VALUE SPACE.                   
           05  PV-STRT-DTE             PIC X(10).                               
           05  PV-END-DTE              PIC X(10).                               
008000                                                                          
008100 01  CD-COUNTER-DISPLAY.                                                  
008200     05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.                   
008300                                                                          
008400 01 ABEND-AREAS.                                                          
008500     05 ABEND-CODE               PIC S9(04)  VALUE ZERO                   
008600                                             COMP SYNC.                   
008700         88  AC-BAD-DATA                     VALUE +4003.                 
008800         88  AC-DB2-ERROR                    VALUE +4013.                 
008900     05  AA-ABEND-LIT            PIC X(40)   VALUE                        
009000           '*****       ABEND'.                                           
009100     05  AA-PROGRAM-LIT          PIC X(40)   VALUE                        
009200             '*****   PROGRAM: MLKUT429'.                                 
009300     05  AA-PARAGRAPH-LIT.                                                
009400         10  FILLER              PIC X(17)   VALUE                        
009500             '***** PARAGRAPH: '.                                         
009600         10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.                
009700     05  AA-MESSAGE-LINE.                                                 
009800         10  FILLER              PIC X(06)   VALUE '*****'.               
009900         10  AA-MESSAGE          PIC X(127)  VALUE SPACES.                
010000     05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                        
010100             '*****    DB2 ERROR'.                                        
010200     05  AA-DB2-OPERATION-LIT.                                            
010300         10  FILLER              PIC X(17)   VALUE                        
010400             '***** OPERATION: '.                                         
010500         10  AA-DB2-OPERATION.                                            
010600             15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.                
010700             15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.                
011200                                                                          
011300 EJECT                                                                    
011400                                                                          
011500*** DATA INTEGRITY CONTROL CARD                                           
011600     COPY MLRD110.                                                        
011700                                                                          
011800                                                                          
011900*** DATA INTEGRITY TWKMCLC EXTRACT FILE                                   
W26603     COPY MLRD141.                                                        
012100                                                                          
012200 EJECT                                                                    
012300                                                                          
012400******************************************************************        
012500*  DB2 ERROR MESSAGES FORMAT AREA.                                        
012600******************************************************************        
012700     COPY DPWS004.                                                        
012800                                                                          
012900******************************************************************        
013000*  DB2 ERRORS                                                             
013100******************************************************************        
013200     COPY SQLCA2.                                                         
013300                                                                          
013400******************************************************************        
013500*  DB2 COMMUNICATION AREA                                                 
013600******************************************************************        
013700     EXEC SQL                                                             
013800          INCLUDE SQLCA                                                   
013900     END-EXEC.                                                            
014000                                                                          
014100******************************************************************        
014200*  DB2 TWKMCLC TABLE LAYOUT                                               
014300******************************************************************        
014400     EXEC SQL                                                             
014500          INCLUDE TWKMCLC                                                 
014600     END-EXEC.                                                            
014700                                                                          
014800******************************************************************        
014900*  DATE ATTRIBUTE TABLE LAYOUT                                            
015000******************************************************************        
015100     EXEC SQL                                                             
015200          INCLUDE TDTATTR                                                 
015300     END-EXEC.                                                            
015400                                                                          
014800******************************************************************        
014900*  MERCHANDISE LEDGER CONTROL TABLE LAYOUT                                
015000******************************************************************        
015100     EXEC SQL                                                             
015200          INCLUDE TMLCNTL                                                 
015300     END-EXEC.                                                            
015400                                                                          
015500******************************************************************        
015600*  DB2 TWKMCLC CURSOR                                                     
015700******************************************************************        
015800     EXEC SQL                                                             
015900         DECLARE TWKMCLC_CURSOR CURSOR FOR                                
016200          SELECT A.FSCL_WK_END_DTE                                        
016400                ,DEPT_NBR                                                 
016400                ,MAJ_CL_NBR                                               
016500                ,SUM(SLS_RTL_AMT)                                         
016600                ,SUM(MKDN_PLU_RTL_AMT)                                    
016700                ,SUM(MKDN_PROMO_RTL_AMT)                                  
016700                ,SUM(MKDN_RGST_RTL_AMT)                                   
016700                ,SUM(MKDN_GLDST_RTL_AMT)                                  
016700                ,SUM(MKDN_UMTCH_RTL_AMT)                                  
016700                ,SUM(MKDN_BYR_RTL_AMT)                                    
016700                ,SUM(MKDN_STR_RTL_AMT)                                    
016700                ,SUM(MKDN_PRPT_RTL_AMT)                                   
016700                ,SUM(MKDN_OTH_RTL_AMT                                     
                         + MKDN_EMP_RTL_AMT)                                    
016700                ,SUM(MKUP_RTL_AMT)                                        
016800                ,SUM(XFER_IN_RTL_AMT)                                     
016900                ,SUM(XFER_OUT_RTL_AMT)                                    
017000                ,SUM(RCPT_RTL_AMT)                                        
017100                ,SUM(RCPT_NET_COST_AMT)                                   
017200                ,SUM(PADJ_RTL_AMT)                                        
017200                ,SUM(PADJ_FRGT_COST_AMT)                                  
017200                ,SUM(PADJ_RTV_COST_AMT)                                   
017200                ,SUM(PADJ_RTV_RTL_AMT)                                    
017200                ,SUM(PADJ_DA_COST_AMT)                                    
017200                ,SUM(PADJ_DA_RTL_AMT)                                     
017300                ,SUM(PADJ_NET_COST_AMT)                                   
016500                ,SUM(PADJ_OTH_COST_AMT                                    
016500                   + PADJ_BROP_COST_AMT                                   
016500                   + LIC_FEE_COST_AMT)                                    
016500                ,SUM(PADJ_OTH_RTL_AMT)                                    
017400                ,SUM(INV_ADJ_RTL_AMT)                                     
017500                ,SUM(SHNK_RTL_AMT)                                        
017600                ,SUM(END_INV_RTL_AMT)                                     
017700            FROM TWKMCLC A                                                
017800                ,TDTATTR B                                                
017900           WHERE (A.FSCL_WK_END_DTE                                       
018000                      BETWEEN :PV-STRT-DTE AND :PV-END-DTE)               
018200             AND A.FSCL_WK_END_DTE = B.KY_DTE                             
018200             AND A.CHNL_CDE IN ('A')                                      
018400        GROUP BY  A.FSCL_WK_END_DTE                                       
018300                 ,DEPT_NBR                                                
018300                 ,MAJ_CL_NBR                                              
W26603           WITH UR                                                        
018500     END-EXEC.                                                            
018600                                                                          
018700 EJECT                                                                    
018800                                                                          
018900******************************************************************        
019000 PROCEDURE DIVISION.                                                      
019100******************************************************************        
019200                                                                          
019300******************************************************************        
019400 0000-MAINLINE-MLKUT429.                                                  
019500******************************************************************        
019600*                                                                         
019700     PERFORM 1000-INITIALIZATION.                                         
019800                                                                          
019900     PERFORM 2000-MAIN-PROCESS.                                           
020000                                                                          
020100     PERFORM 8000-EOJ.                                                    
020200                                                                          
020300     STOP RUN.                                                            
020400                                                                          
020500 EJECT                                                                    
020600                                                                          
020700******************************************************************        
020800 1000-INITIALIZATION.                                                     
020900******************************************************************        
021000                                                                          
021100     OPEN INPUT  DATA-INTEGRITY-CTL-FILE.                                 
021200     OPEN OUTPUT DATA-INTEGRITY-EXTR-FILE.                                
021300                                                                          
021400     PERFORM 4000-READ-CONTROL-CARD.                                      
021500                                                                          
021600     DISPLAY ' '.                                                         
021700     DISPLAY '*** MLKUT429 INPUT CONTROL CARD VALUES ***'                 
021800     DISPLAY ML110-DATA-INTEGRITY-CC-REC.                                 
021900                                                                          
022000     INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
022100     INITIALIZE PV-PROGRAM-VARIABLES.                                     
022200                                                                          
      *    DETERMINE RANGE OF DATES TO BE PROCESSED.                            
      *                                                                         
      *    THE START DATE WILL BE THE FIRST DAY OF THE FISCAL MONTH             
      *    INTO WHICH ML110-STRT-DTE FALLS.                                     
      *                                                                         
      *    THE END DATE WILL BE ML110-END-DTE OR THE MAX POSTING FSCL WK        
      *    DATE FROM TMLCNTL, WHICHEVER IS GREATER.  THE REASON THE END         
      *    DATE CANNOT BE GREATER THAN THE MAX POSTING FSCL WK DATE IS          
      *    THAT END INV RETAIL IS TAKEN FROM THE MOST RECENT WEEK               
      *    RETURNED BY THE TWKMCLC CURSOR. IF THE CURSOR RETURNS THE            
      *    FUTURE WEEK ROWS THAT EXIST FOR ON-ORDER, END INV RETAIL WILL        
      *    BE PICKED UP FROM THOSE ROWS.  END INV RTL WILL BE ZERO ON           
      *    THOSE ROWS BECAUSE WE ONLY RECALCULATE UP THROUGH THE MAX            
      *    POSTING FSCL WK DATE.                                                
                                                                                
           MOVE ML110-STRT-DTE         TO DTATTR-KY-DTE.                        
022300     PERFORM 7000-GET-FSCL-MN-BEG-DTE.                                    
           MOVE DTATTR-FSCL-MN-BEG-DTE TO PV-STRT-DTE.                          
                                                                                
030600     PERFORM 7050-GET-MXPSTG-FSCL-WK-DTE.                                 
           IF ML110-END-DTE > MLCNTL-MXPSTG-FSCL-WK-DTE                         
               MOVE MLCNTL-MXPSTG-FSCL-WK-DTE TO PV-END-DTE                     
           ELSE                                                                 
               MOVE ML110-END-DTE             TO PV-END-DTE                     
           END-IF.                                                              
022400                                                                          
JSTEMP*    MOVE ML110-STRT-DTE TO PV-STRT-DTE                                   
JSTEMP*    MOVE ML110-END-DTE  TO PV-END-DTE                                    
JSTEMP*                           DTATTR-FSCL-MN-END-DTE.                       
           DISPLAY 'START DATE: ' PV-STRT-DTE.                                  
           DISPLAY 'END DATE:   ' PV-END-DTE.                                   
022500 EJECT                                                                    
022600                                                                          
022700******************************************************************        
022800 2000-MAIN-PROCESS.                                                       
022900******************************************************************        
023000*                                                                         
023100     PERFORM 7100-OPEN-CURSOR-TWKMCLC.                                    
023200                                                                          
023300     PERFORM 7200-FETCH-TWKMCLC.                                          
023400                                                                          
023500     PERFORM                                                              
023600       UNTIL PS-TWKMCLC-IS-DONE                                           
023700         PERFORM 5000-WRITE-EXTRACT                                       
023800         PERFORM 7200-FETCH-TWKMCLC                                       
023900     END-PERFORM.                                                         
024000                                                                          
024100     PERFORM 7300-CLOSE-CURSOR-TWKMCLC.                                   
024200                                                                          
024300                                                                          
024400******************************************************************        
024500 4000-READ-CONTROL-CARD.                                                  
024600******************************************************************        
024700*                                                                         
024800     READ DATA-INTEGRITY-CTL-FILE                                         
024900       INTO ML110-DATA-INTEGRITY-CC-REC                                   
025000         AT END                                                           
025100             MOVE '4000-READ-CONTROL-CARD'                                
025200                                 TO AA-PARAGRAPH-NAME                     
025300             MOVE 'CONTROL CARD IS MISSING'                               
025400                                 TO AA-MESSAGE                            
025500             MOVE +4001          TO ABEND-CODE                            
025600             PERFORM 9900-COMMON-ABEND-ROUTINE                            
025700     END-READ.                                                            
025800                                                                          
025900 EJECT                                                                    
026000                                                                          
026100******************************************************************        
026200 5000-WRITE-EXTRACT.                                                      
026300******************************************************************        
026400*                                                                         
026500     MOVE WKMCLC-FSCL-WK-END-DTE TO ML141-FSCL-WK-END-DTE.                
026700     MOVE WKMCLC-DEPT-NBR        TO ML141-DEPT-NBR.                       
026800     MOVE WKMCLC-MAJ-CL-NBR      TO ML141-MAJ-CL-NBR.                     
027100     MOVE WKMCLC-SLS-RTL-AMT     TO ML141-SLS-RTL-AMT.                    
027100     MOVE WKMCLC-MKDN-PLU-RTL-AMT                                         
027100                                 TO ML141-MKDN-PLU-RTL-AMT.               
027100     MOVE WKMCLC-MKDN-PROMO-RTL-AMT                                       
027100                                 TO ML141-MKDN-PROMO-RTL-AMT.             
027100     MOVE WKMCLC-MKDN-RGST-RTL-AMT                                        
027100                                 TO ML141-MKDN-RGST-RTL-AMT.              
027100     MOVE WKMCLC-MKDN-GLDST-RTL-AMT                                       
027100                                 TO ML141-MKDN-GLDST-RTL-AMT.             
027100     MOVE WKMCLC-MKDN-UMTCH-RTL-AMT                                       
027100                                 TO ML141-MKDN-UMTCH-RTL-AMT.             
027100     MOVE WKMCLC-MKDN-BYR-RTL-AMT                                         
027100                                 TO ML141-MKDN-BYR-RTL-AMT.               
027100     MOVE WKMCLC-MKDN-STR-RTL-AMT                                         
027100                                 TO ML141-MKDN-STR-RTL-AMT.               
027100     MOVE WKMCLC-MKDN-PRPT-RTL-AMT                                        
027100                                 TO ML141-MKDN-PRPT-RTL-AMT.              
027100     MOVE WKMCLC-MKDN-OTH-RTL-AMT                                         
027100                                 TO ML141-MKDN-OTH-RTL-AMT.               
027300     MOVE WKMCLC-MKUP-RTL-AMT    TO ML141-MKUP-RTL-AMT.                   
027400     MOVE WKMCLC-XFER-IN-RTL-AMT TO ML141-XFER-IN-RTL-AMT.                
027500     MOVE WKMCLC-XFER-OUT-RTL-AMT                                         
027600                                 TO ML141-XFER-OUT-RTL-AMT.               
027700     MOVE WKMCLC-RCPT-RTL-AMT    TO ML141-RCPT-RTL-AMT.                   
027800     MOVE WKMCLC-RCPT-NET-COST-AMT                                        
027900                                 TO ML141-RCPT-NET-COST-AMT.              
028000     MOVE WKMCLC-PADJ-RTL-AMT    TO ML141-PADJ-RTL-AMT.                   
028100     MOVE WKMCLC-PADJ-FRGT-COST-AMT                                       
028200                                 TO ML141-PADJ-FRGT-COST-AMT.             
028100     MOVE WKMCLC-PADJ-RTV-COST-AMT                                        
028200                                 TO ML141-PADJ-RTV-COST-AMT.              
028100     MOVE WKMCLC-PADJ-RTV-RTL-AMT                                         
028200                                 TO ML141-PADJ-RTV-RTL-AMT.               
028100     MOVE WKMCLC-PADJ-DA-COST-AMT                                         
028200                                 TO ML141-PADJ-DA-COST-AMT.               
028100     MOVE WKMCLC-PADJ-DA-RTL-AMT TO ML141-PADJ-DA-RTL-AMT.                
028100     MOVE WKMCLC-PADJ-NET-COST-AMT                                        
028200                                 TO ML141-PADJ-NET-COST-AMT.              
028100     MOVE WKMCLC-PADJ-OTH-COST-AMT                                        
028200                                 TO ML141-PADJ-OTH-COST-AMT.              
028100     MOVE WKMCLC-PADJ-OTH-RTL-AMT                                         
028200                                 TO ML141-PADJ-OTH-RTL-AMT.               
028300     MOVE WKMCLC-INV-ADJ-RTL-AMT TO ML141-INV-ADJ-RTL-AMT.                
028400     MOVE WKMCLC-SHNK-RTL-AMT    TO ML141-SHNK-RTL-AMT.                   
028500                                                                          
028600*    ENDING INVENTORY IS ONLY FILLED IN ON THE RECORD FOR THE             
028700*    FIRST (MOST RECENT) FISCAL WEEK IN THE FISCAL MONTH.                 
028800*    (THE FISCAL WEEKS ARE PROCESSED IN DESCENDING SEQUENCE.)             
028900*?   IF  DTATTR-FSCL-MN-END-DTE  = PV-PREV-FSCL-MN-END-DTE                
029000*    AND WKMCLC-DEPT-NBR         = PV-PREV-DEPT-NBR                       
028900     IF  ML141-FSCL-WK-END-DTE   = PV-END-DTE                             
029100         MOVE WKMCLC-END-INV-RTL-AMT TO ML141-END-INV-RTL-AMT             
029200     ELSE                                                                 
029300         MOVE WKMCLC-END-INV-RTL-AMT TO ML141-END-INV-RTL-AMT             
029400         MOVE DTATTR-FSCL-MN-END-DTE TO PV-PREV-FSCL-MN-END-DTE           
029500         MOVE WKMCLC-DEPT-NBR        TO PV-PREV-DEPT-NBR                  
029600     END-IF.                                                              
029700                                                                          
029800     WRITE DATA-INTEGRITY-EXTR-REC                                        
029900         FROM ML141-DATA-INTEGRITY-EXTR-REC.                              
030000     ADD 1                       TO PA-OUTPUT-RECS-WRITTEN.               
030100                                                                          
030200                                                                          
030300 EJECT                                                                    
030400                                                                          
030500******************************************************************        
030600 7000-GET-FSCL-MN-BEG-DTE.                                                
030700******************************************************************        
030800                                                                          
030900     EXEC SQL                                                             
031000         SELECT  FSCL_MN_BEG_DTE                                          
031100           INTO :DTATTR-FSCL-MN-BEG-DTE                                   
031200           FROM TDTATTR                                                   
031300          WHERE KY_DTE = :DTATTR-KY-DTE                                   
W26603         WITH UR                                                          
031400     END-EXEC.                                                            
031500                                                                          
031600     EVALUATE TRUE                                                        
031700         WHEN SQLCODE = 0                                                 
031800             CONTINUE                                                     
031900         WHEN OTHER                                                       
032000             MOVE '7000-GET-FSCL-MN-BEG-DTE'                              
032100                                 TO AA-PARAGRAPH-NAME                     
032200             MOVE 'NO TDTATTR MATCH FOUND FOR KY_DTE'                     
032300                                 TO AA-MESSAGE                            
032400             MOVE 'SELECT FROM'  TO AA-DB2-OPERATION                      
032500             PERFORM 9901-SQL-ERROR                                       
032600     END-EVALUATE.                                                        
032700                                                                          
032800                                                                          
030500******************************************************************        
030600 7050-GET-MXPSTG-FSCL-WK-DTE.                                             
030700******************************************************************        
030800*                                                                         
030900     EXEC SQL                                                             
031000         SELECT  MXPSTG_FSCL_WK_DTE                                       
                 INTO :MLCNTL-MXPSTG-FSCL-WK-DTE                                
031200           FROM TMLCNTL                                                   
W26603         WITH UR                                                          
031400     END-EXEC.                                                            
031500                                                                          
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7050-GET-MXPSTG-FSCL-WK-DTE'                           
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TMLCNTL ROW NOT FOUND'                                 
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9901-SQL-ERROR                                       
           END-EVALUATE.                                                        
032700                                                                          
032800                                                                          
032900******************************************************************        
033000 7100-OPEN-CURSOR-TWKMCLC.                                                
033100******************************************************************        
033200*                                                                         
033300     EXEC SQL                                                             
033400         OPEN TWKMCLC_CURSOR                                              
033500     END-EXEC.                                                            
033600                                                                          
033700     EVALUATE TRUE                                                        
033800         WHEN SQLCODE = 0                                                 
033900             CONTINUE                                                     
034000         WHEN OTHER                                                       
034100             MOVE '7100-OPEN-CURSOR-TWKMCLC'                              
034200                                 TO AA-PARAGRAPH-NAME                     
034300             MOVE 'TWKMCLC TABLE ERROR'                                   
034400                                 TO AA-MESSAGE                            
034500             MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION                      
034600             PERFORM 9901-SQL-ERROR                                       
034700     END-EVALUATE.                                                        
034800                                                                          
034900 EJECT                                                                    
035000                                                                          
035100******************************************************************        
035200 7200-FETCH-TWKMCLC.                                                      
035300******************************************************************        
035400*                                                                         
035500     EXEC SQL                                                             
035600         FETCH TWKMCLC_CURSOR                                             
036000          INTO :WKMCLC-FSCL-WK-END-DTE                                    
036100              ,:WKMCLC-DEPT-NBR                                           
036200              ,:WKMCLC-MAJ-CL-NBR                                         
036400              ,:WKMCLC-SLS-RTL-AMT                                        
036500              ,:WKMCLC-MKDN-PLU-RTL-AMT                                   
036600              ,:WKMCLC-MKDN-PROMO-RTL-AMT                                 
036700              ,:WKMCLC-MKDN-RGST-RTL-AMT                                  
036800              ,:WKMCLC-MKDN-GLDST-RTL-AMT                                 
036900              ,:WKMCLC-MKDN-UMTCH-RTL-AMT                                 
037000              ,:WKMCLC-MKDN-BYR-RTL-AMT                                   
037100              ,:WKMCLC-MKDN-STR-RTL-AMT                                   
037200              ,:WKMCLC-MKDN-PRPT-RTL-AMT                                  
037300              ,:WKMCLC-MKDN-OTH-RTL-AMT                                   
037300              ,:WKMCLC-MKUP-RTL-AMT                                       
037300              ,:WKMCLC-XFER-IN-RTL-AMT                                    
037300              ,:WKMCLC-XFER-OUT-RTL-AMT                                   
037300              ,:WKMCLC-RCPT-RTL-AMT                                       
037300              ,:WKMCLC-RCPT-NET-COST-AMT                                  
037300              ,:WKMCLC-PADJ-RTL-AMT                                       
037300              ,:WKMCLC-PADJ-FRGT-COST-AMT                                 
037300              ,:WKMCLC-PADJ-RTV-COST-AMT                                  
037300              ,:WKMCLC-PADJ-RTV-RTL-AMT                                   
037300              ,:WKMCLC-PADJ-DA-COST-AMT                                   
037300              ,:WKMCLC-PADJ-DA-RTL-AMT                                    
037300              ,:WKMCLC-PADJ-NET-COST-AMT                                  
037300              ,:WKMCLC-PADJ-OTH-COST-AMT                                  
037300              ,:WKMCLC-PADJ-OTH-RTL-AMT                                   
037300              ,:WKMCLC-INV-ADJ-RTL-AMT                                    
037300              ,:WKMCLC-SHNK-RTL-AMT                                       
037300              ,:WKMCLC-END-INV-RTL-AMT                                    
037400     END-EXEC.                                                            
037500                                                                          
037600     EVALUATE TRUE                                                        
037700         WHEN SQLCODE = 0                                                 
037800             ADD +1              TO PA-TWKMCLC-ROWS-FETCHED               
037900         WHEN SQLCODE = +100                                              
038000             MOVE 'Y'            TO PS-TWKMCLC-DONE                       
038100         WHEN OTHER                                                       
038200             MOVE '7200-FETCH-TWKMCLC'                                    
038300                                 TO AA-PARAGRAPH-NAME                     
038400             MOVE 'TWKMCLC TABLE ERROR'                                   
038500                                 TO AA-MESSAGE                            
038600             MOVE 'FETCH'        TO AA-DB2-OPERATION                      
038700             PERFORM 9901-SQL-ERROR                                       
038800     END-EVALUATE.                                                        
038900                                                                          
039000 EJECT                                                                    
039100                                                                          
039200******************************************************************        
039300 7300-CLOSE-CURSOR-TWKMCLC.                                               
039400******************************************************************        
039500*                                                                         
039600     EXEC SQL                                                             
039700         CLOSE TWKMCLC_CURSOR                                             
039800     END-EXEC.                                                            
039900                                                                          
040000     EVALUATE TRUE                                                        
040100         WHEN SQLCODE = 0                                                 
040200             CONTINUE                                                     
040300         WHEN OTHER                                                       
040400             MOVE '7300-CLOSE-CURSOR-TWKMCLC'                             
040500                                 TO AA-PARAGRAPH-NAME                     
040600             MOVE 'TWKMCLC TABLE ERROR'                                   
040700                                 TO AA-MESSAGE                            
040800             MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION                      
040900             PERFORM 9901-SQL-ERROR                                       
041000     END-EVALUATE.                                                        
041100                                                                          
041200                                                                          
041300******************************************************************        
041400 8000-EOJ.                                                                
041500******************************************************************        
041600*                                                                         
041700     DISPLAY ' '.                                                         
041800     DISPLAY '  PROGRAM MLKUT429 PROCESSING SUMMARY'.                     
041900     DISPLAY '  -----------------------------------'.                     
042000     DISPLAY ' '.                                                         
042100*                                                                         
042200     MOVE PA-TWKMCLC-ROWS-FETCHED TO CD-COUNT-HOLD1.                      
042300     DISPLAY 'TWKMCLC ROWS FETCHED =      ' CD-COUNT-HOLD1.               
042400*                                                                         
042500     MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.                       
042600     DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.               
042700*                                                                         
042800     DISPLAY ' '.                                                         
042900                                                                          
043000     CLOSE DATA-INTEGRITY-CTL-FILE.                                       
043100     CLOSE DATA-INTEGRITY-EXTR-FILE.                                      
043200                                                                          
043300 EJECT                                                                    
043400                                                                          
043500******************************************************************        
043600 9900-COMMON-ABEND-ROUTINE.                                               
043700******************************************************************        
043800*                                                                         
043900     DISPLAY AA-ABEND-LIT.                                                
044000     DISPLAY AA-PROGRAM-LIT.                                              
044100     DISPLAY AA-PARAGRAPH-LIT.                                            
044200     DISPLAY AA-MESSAGE-LINE.                                             
044300     DISPLAY ABEND-CODE.                                                  
044400                                                                          
044500     CALL 'ILBOABN0' USING ABEND-CODE.                                    
044600                                                                          
044700 EJECT                                                                    
044800                                                                          
044900******************************************************************        
045000 9901-SQL-ERROR.                                                          
045100******************************************************************        
045200*                                                                         
045300     CLOSE DATA-INTEGRITY-CTL-FILE                                        
045400           DATA-INTEGRITY-EXTR-FILE.                                      
045500                                                                          
045600     DISPLAY AA-ABEND-LIT.                                                
045700     DISPLAY AA-DB2-ERROR-LIT.                                            
045800     DISPLAY AA-PROGRAM-LIT.                                              
045900     DISPLAY AA-PARAGRAPH-LIT.                                            
046000     DISPLAY AA-MESSAGE-LINE.                                             
046100     DISPLAY AA-DB2-OPERATION-LIT.                                        
046200     SET AC-DB2-ERROR TO TRUE.                                            
046300     MOVE +4013                  TO ABEND-CODE.                           
046400                                                                          
046500     COPY DPPD004.                                                        
046600     CALL 'ILBOABN0' USING ABEND-CODE.                                    
046700                                                                          
