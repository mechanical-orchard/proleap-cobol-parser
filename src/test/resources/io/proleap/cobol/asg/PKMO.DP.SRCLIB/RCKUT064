000100******************************************************************02/16/96
000200 IDENTIFICATION DIVISION.                                         RCK00185
000300******************************************************************   LV025
000400                                                                  RCK00185
000500 PROGRAM-ID.    RCKUT064.                                            CL**2
000600 AUTHOR.        DAN HINTZ.                                           CL**2
000700 INSTALLATION.  KOHLS.                                            RCK00185
000800 DATE-WRITTEN   MAY, 2008.                                           CL**2
000900 DATE-COMPILED.                                                   RCK00185
001000                                                                  RCK00185
001100******************************************************************RCK00185
001200*  THIS PROGRAM CREATES EXTRACT FILES OF DATA FROM THE           *   CL**2
001300*  DISTRIBUTION HEADER TABLE AND DISTRIBUTION DETAIL TABLE WHICH *   CL**2
001310*  ARE USED IN PACK TO LIGHT VERIFICATION.  THE DRIVING CURSOR   *   CL**2
001320*  WILL BE ALL TDISTHD ROWS THAT WERE CREATED BEFORE A SPECIFIED *   CL**2
001330*  DATE.  FOR EACH UNIQUE ORD_NBR/RCVR_SEQ_NBR FOUND, A CHECK    *   CL**2
001330*  WILL BE DONE TO SEE IF THERE IS AN ACTIVE RECEIVER ON TRECEIV.*   CL**2
001400******************************************************************RCK00185
001500                                                                  RCK00185
001600******************************************************************RCK00185
001700 ENVIRONMENT DIVISION.                                            RCK00185
001800******************************************************************RCK00185
001900                                                                  RCK00185
002000 CONFIGURATION SECTION.                                           RCK00185
002100                                                                  RCK00185
002200 SOURCE-COMPUTER.        IBM-3090.                                RCK00185
002300 OBJECT-COMPUTER.        IBM-3090.                                RCK00185
002400                                                                  RCK00185
002500 INPUT-OUTPUT SECTION.                                            RCK00185
002600                                                                  RCK00185
002700 FILE-CONTROL.                                                    RCK00185
002800                                                                  RCK00185
003210     SELECT DISTRB-HDR-FILE                                          CL**2
003220         ASSIGN TO OUT01.                                            CL*16
003230                                                                     CL*16
003300     SELECT DISTRB-DTL-FILE                                          CL*16
003400         ASSIGN TO OUT02.                                            CL*16
003410                                                                     CL*16
003500******************************************************************RCK00185
003600 DATA DIVISION.                                                   RCK00185
003700******************************************************************RCK00185
003800                                                                  RCK00185
003900 FILE SECTION.                                                    RCK00185
004000                                                                     CL**2
004710 FD  DISTRB-HDR-FILE                                                 CL**2
004720     RECORDING MODE IS F                                             CL**2
004730     LABEL RECORDS ARE STANDARD                                      CL**2
004740     BLOCK CONTAINS 0 RECORDS.                                       CL**2
004750                                                                     CL**2
004761 01  DISTRB-HDR-RECORD          PIC  X(253).                      RCKCL**2
004770                                                                     CL*17
004800 FD  DISTRB-DTL-FILE                                                 CL*17
004900     RECORDING MODE IS F                                             CL*17
005000     LABEL RECORDS ARE STANDARD                                      CL*17
005100     BLOCK CONTAINS 0 RECORDS.                                       CL*17
005200                                                                     CL*17
005310 01  DISTRB-DTL-RECORD          PIC  X(44).                       RCKCL*17
005400*                                                                 RCK00185
005500*                                                                 RCK00185
005600******************************************************************RCK00185
005700 WORKING-STORAGE SECTION.                                         RCK00185
005800******************************************************************RCK00185
005900*                                                                 RCK00185
006000*                                                                 RCK00185
007900******************************************************************RCK00185
008000** DB2 FORMATTED MESSAGE AREA                                   **RCK00185
008100******************************************************************RCK00185
008200     COPY DPWS004.                                                RCK00185
008300*                                                                 RCK00185
008500*                                                                 RCK00185
008600******************************************************************RCK00185
008700** COMMUNICATIONS AREA FOR DB2                                  **RCK00185
008800******************************************************************RCK00185
008900     EXEC SQL                                                     RCK00185
009000          INCLUDE SQLCA                                           RCK00185
009100     END-EXEC.                                                    RCK00185
009200*                                                                 RCK00185
009300*                                                                 RCK00185
009400******************************************************************RCK00185
009500** COBOL DECLARATION AND DCLGEN MEMBER FOR THE DISTRIBUTION     **   CL**3
009600** HEADER TABLE                                                 **RCK00185
009700******************************************************************RCK00185
009800     EXEC SQL                                                     RCK00185
009900          INCLUDE TDISTHD                                            CL**3
010000     END-EXEC.                                                    RCK00185
010010*                                                                 RCK00185
010020*                                                                 RCK00185
010030******************************************************************RCK00185
010040*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE DISTRIBUTION     **   CL**3
010050*  DETAIL TABLE                                                 **RCK00185
010060******************************************************************RCK00185
010070     EXEC SQL                                                     RCK00185
010080          INCLUDE TDISTDT                                            CL**3
010090     END-EXEC.                                                    RCK00185
010091*                                                                 RCK00185
010092*                                                                 RCK00185
010200******************************************************************RCK00185
010300** COBOL DECLARATION AND DCLGEN MEMBER FOR THE RECEIVER TABLE   **   CL**3
010400** THIS IS IN HERE FOR THE COBOL DECLARATION ONLY!!!!!          **   CL**3
010500******************************************************************RCK00185
010600     EXEC SQL                                                     RCK00185
010700          INCLUDE TRECEIV                                            CL**3
010800     END-EXEC.                                                    RCK00185
011000*                                                                 RCK00185
011100*                                                                 RCK00185
010200******************************************************************RCK00185
010300** COBOL DECLARATION AND DCLGEN MEMBER FOR THE DC PARAMETER     **   CL**3
010400** TABLE (TKRPRPM).                                             **   CL**3
010500******************************************************************RCK00185
010600     EXEC SQL                                                     RCK00185
010700          INCLUDE TKRPRPM                                            CL**3
010800     END-EXEC.                                                    RCK00185
011000*                                                                 RCK00185
011100*                                                                 RCK00185
011200 01  ABEND-CODE                      PIC S9(04)    VALUE +0       RCK00185
011300                                                   COMP SYNC.     RCK00185
011400                                                                  RCK00185
011500 01  PS-PROGRAM-SWITCHES.                                         RCK00185
011902     05  PS-EOF-DISTRB-HDR-CSR-SW    PIC  X(01)    VALUE 'N'.        CL*21
011903         88  PS-EOF-DISTRB-HDR-CSR                 VALUE 'Y'.        CL*21
011904         88  PS-NOT-EOF-DISTRB-HDR-CSR             VALUE 'N'.        CL*21
011920     05  PS-EOF-DISTRB-DTL-CSR-SW    PIC  X(01)    VALUE 'N'.        CL*21
011930         88  PS-EOF-DISTRB-DTL-CSR                 VALUE 'Y'.        CL*21
011931         88  PS-NOT-EOF-DISTRB-DTL-CSR             VALUE 'N'.        CL*21
011920     05  PS-WRITE-DISTRB-HDR-DTL-SW  PIC  X(01)    VALUE 'N'.        CL*21
011930         88  PS-WRITE-DISTRB-HDR-DTL               VALUE 'Y'.        CL*21
011931         88  PS-DO-NOT-WRITE-DISTRB-HDR-DTL        VALUE 'N'.        CL*21
012000                                                                  RCK00185
012100                                                                  RCK00185
012200 01  PC-PROGRAM-CONSTANTS.                                        RCK00185
012500     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE          RCK00185
012600         'RCKUT064'.                                                 CL**3
012900     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3       RCK00185
013000                                                   COMP SYNC.     RCK00185
013200                                                                     CL**5
013300 01  PV-PROGRAM-VARIABLES.                                        RCK00185
013500     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.  RCK00185
013600     05  PV-DB2-TABLE-NAME           PIC  X(30)    VALUE SPACES.  RCK00185
013610     05  PV-DB2-OPERATION-MESSAGE    PIC  X(30)    VALUE SPACES.  RCK00185
013700     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO     RCK00185
013800                                                   COMP SYNC.     RCK00185
013900     05  PV-CLOSE-COUNTER            PIC S9(04)    VALUE ZERO     RCK00185
014000                                                   COMP SYNC.     RCK00185
014100     05  PV-FETCH-COUNTER            PIC S9(04)    VALUE ZERO     RCK00185
014200                                                   COMP SYNC.     RCK00185
014201     05  PV-TRECEIV-READ-CNT         PIC S9(07)    VALUE ZERO        CL*22
014210                                                   COMP-3.           CL*22
014201     05  PV-TRECEIV-DEL-STAT-CNT     PIC S9(07)    VALUE ZERO        CL*22
014210                                                   COMP-3.           CL*22
014201     05  PV-TRECEIV-OTH-STAT-CNT     PIC S9(07)    VALUE ZERO        CL*22
014210                                                   COMP-3.           CL*22
014201     05  PV-TRECEIV-DELETED-CNT      PIC S9(07)    VALUE ZERO        CL*22
014210                                                   COMP-3.           CL*22
014300     05  PV-TDISTHD-READ-CNTR        PIC S9(07)    VALUE ZERO        CL*22
014400                                                   COMP-3.           CL*22
014300     05  PV-TDISTHD-WRITE-CNTR       PIC S9(07)    VALUE ZERO        CL*22
014400                                                   COMP-3.           CL*22
014410     05  PV-TDISTDT-WRITE-CNTR       PIC S9(07)    VALUE ZERO        CL*22
014420                                                   COMP-3.           CL*22
014500     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                CL*22
014600                                                                     CL*22
014700     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO     RCK00185
014800                                                   COMP-3.        RCK00185
010700     05  PV-CURR-RECEIVER.                                                
010800         10  PV-CURR-ORD-NBR         PIC  9(08)    VALUE ZEROS.           
010800         10  PV-CURR-RCVR-SEQ-NBR    PIC  9(04)    VALUE ZEROS.           
010700     05  PV-PREV-RECEIVER.                                                
010800         10  PV-PREV-ORD-NBR         PIC  9(08)    VALUE ZEROS.           
010800         10  PV-PREV-RCVR-SEQ-NBR    PIC  9(04)    VALUE ZEROS.           
016400*                                                                 RCK00185
016500*  CURSOR FOR ALL DISTRIBUTION HEADER ROWS WITH A VERIFIED DATE   RCK00185
016600*  OLDER THAN A SPECIFIED NUMBER OF DAYS.                         RCK00185
016700*                                                                 RCK00185
016800                                                                  RCK00185
016801     EXEC SQL                                                     RCK00185
016802       DECLARE DISTRB_HDR_CURSOR CURSOR FOR                       RCK00121
016803         SELECT                                                   RCL*01*8
016804               A.ORD_NBR                                                85
016805              ,A.RCVR_SEQ_NBR                                           85
016806              ,A.ORD_LNE_NBR                                            85
016807              ,A.WRK_ORD_SEQ_NBR                                        85
016808              ,A.OPER_UNT_ID                                            85
016809              ,A.DISTRB_PRC_AREA_ID                                     85
016810              ,A.PRCG_STAT_CDE                                          85
016811              ,A.CRE_TMST                                               85
016812              ,A.RSLTS_RCVD_TMST                                        85
016813              ,A.VRFD_TMST                                              85
016814              ,A.PRC_AREA_MODU_ID                                       85
016815              ,A.CRE_USR_ID                                               
016816              ,A.TOT_CART_CNT                                             
016817              ,A.CART_DWNLD_DTE                                           
016818              ,A.CART_DWNLD_TM                                            
016819              ,A.CART_DWNLD_USR_ID                                        
016820              ,A.FCSE_CART_CNT                                            
016821              ,A.FCSE_LBL_TYP_CDE                                         
016822              ,A.P2L_CART_CNT                                             
016823              ,A.INPK_CART_CNT                                            
016824              ,A.INPK_UNT_QTY                                             
016825              ,A.INPK_CNVYB_IND                                           
016826              ,A.OTPK_UNT_QTY                                             
016827              ,A.SYS_CRE_CDE                                              
016828              ,A.SPC_INSTR_DESC                                           
016829              ,A.FCSE_CNFRMN_TMST                                         
016830              ,A.FCSE_CNFRMN_USR_ID                                       
016831              ,A.FCSE_CNFRMN_PGM_ID                                       
                    ,A.ACTV_IND                                                 
                    ,A.DLT_REQ_SRC_ID                                           
                    ,A.DLT_REQ_TMST                                             
                    ,A.DLT_RSLT_CDE                                             
                    ,A.P2L_SCN_CART_CNT                                         
                    ,A.CRE_LBL_OPT_CDE                                          
                    ,A.FCSE_FCLTY_AREA_ID                                       
                    ,A.ODD_CART_UNT_QTY                                         
                    ,A.PREPK_LNE_PRC_CDE                                        
                    ,A.UCC128_DAT_USG_CDE                                       
                    ,A.CAL_UNT_PRCD_QTY                                         
                    ,A.PHY_LBL_PRC_CNT                                          
                    ,A.VRTL_LBL_PRC_CNT                                         
016832          FROM TDISTHD A                                                *3
016832              ,TKRPRPM B                                                *3
016833         WHERE B.SYS_PRC_FUNC_CDE  = 'HDRPRG'                           *5
016840           AND B.LOC_ID            = 90                                 *5
016840           AND B.INTFC_SYS_CDE     = 'KHL'                              *5
016840           AND B.FUNC_PRC_STAT_CDE = 'AC'                               *5
016840           AND A.CRE_TMST          <                                    *5
016840               (CURRENT TIMESTAMP - B.FUNC_QTY DAYS)                    *5
016804      ORDER BY A.ORD_NBR                                                85
016805              ,A.RCVR_SEQ_NBR                                           85
016806              ,A.ORD_LNE_NBR                                            85
016900     END-EXEC.                                                          85
022300*                                                                 RCK00185
022424*  CURSOR FOR ALL DISTRIBUTION DETAIL ROWS FOR A SPECIFIED LINE   RCK00185
022430*  OF A RECEIVER.                                                 RCK00185
022440*                                                                 RCK00185
022450                                                                  RCK00185
022460     EXEC SQL                                                     RCK00185
022470       DECLARE DISTRB_DTL_CURSOR CURSOR FOR                             21
022480         SELECT                                                    CL*  *8
022491               ORD_NBR                                                    
022492              ,RCVR_SEQ_NBR                                               
022493              ,ORD_LNE_NBR                                                
022494              ,WRK_ORD_SEQ_NBR                                            
022495              ,STR_NBR                                                    
022496              ,EXPTED_ALLOC_QTY                                           
022497              ,ACTL_ALLOC_QTY                                             
022500              ,RCVG_PSTD_STAT_CDE                                         
022501              ,P2L_SPLT_UNT_QTY                                           
022502              ,FCSE_SPLT_UNT_QTY                                          
022503              ,INPK_SPLT_UNT_QTY                                          
022504              ,FCSE_ACTL_UNT_QTY                                          
                    ,ODD_CART_IND                                               
                    ,PREPK_ODD_UNT_QTY                                          
022505          FROM TDISTDT                                                  *3
022506         WHERE ORD_NBR      = :DISTHD-ORD-NBR                           *5
022507           AND RCVR_SEQ_NBR = :DISTHD-RCVR-SEQ-NBR                      *5
022508           AND ORD_LNE_NBR  = :DISTHD-ORD-LNE-NBR                       *5
016900     END-EXEC.                                                          85
022510                                                                  RCK00185
022511                                                                  RCK00185
022520******************************************************************RCK00185
022600 PROCEDURE DIVISION.                                              RCK00185
022700******************************************************************RCK00185
022800                                                                  RCK00185
022810******************************************************************RCK00185
022820* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE     RCK00185
022830*      PROGRAM.                                                   RCK00185
022840******************************************************************RCK00185
022900 0000-PROGRAM-MAINLINE.                                           RCK00185
023400                                                                  RCK00185
023500     PERFORM 1000-INITIALIZE.                                     RCK00185
023501                                                                  RCK00185
026409     PERFORM 7000-OPEN-DISTRB-HDR-CURSOR.                            CL*21
026410                                                                  RCK00185
026411     SET PS-NOT-EOF-DISTRB-HDR-CSR TO TRUE                              85
026412     PERFORM 7100-FETCH-DISTRB-HDR-CURSOR.                           CL*21
026413                                                                        21
023504     PERFORM 2000-PROCESS-TDISTHD-CURSOR                             CL*21
023505         UNTIL PS-EOF-DISTRB-HDR-CSR.                                CL*21
026413                                                                        21
023931     CLOSE DISTRB-HDR-FILE                                           CL**4
023940           DISTRB-DTL-FILE.                                          CL**4
023950                                                                     CL**4
023951     MOVE PV-TDISTHD-READ-CNTR TO PV-DISPLAY-COUNT.                 CL* 22
023952     DISPLAY 'TOTAL TDISTHD RECS READ......: ' PV-DISPLAY-COUNT.        22
023953                                                                     CL**4
023951     MOVE PV-TRECEIV-READ-CNT  TO PV-DISPLAY-COUNT.                 CL* 22
023952     DISPLAY 'TOTAL RECEIVER RECS READ.....: ' PV-DISPLAY-COUNT.        22
023953                                                                     CL**4
023951     MOVE PV-TRECEIV-DEL-STAT-CNT TO PV-DISPLAY-COUNT.              CL* 22
023952     DISPLAY 'TOTAL RECEIVERS DEL STAT.....: ' PV-DISPLAY-COUNT.        22
023953                                                                     CL**4
023951     MOVE PV-TRECEIV-OTH-STAT-CNT TO PV-DISPLAY-COUNT.              CL* 22
023952     DISPLAY 'TOTAL RECEIVERS OTHER STAT...: ' PV-DISPLAY-COUNT.        22
023953                                                                     CL**4
023951     MOVE PV-TRECEIV-DELETED-CNT  TO PV-DISPLAY-COUNT.              CL* 22
023952     DISPLAY 'TOTAL DELETED RECEIVERS......: ' PV-DISPLAY-COUNT.        22
023953                                                                     CL**4
023960     MOVE PV-TDISTHD-WRITE-CNTR TO PV-DISPLAY-COUNT.                 CL*22
023970     DISPLAY 'TOTAL DISTRB HDR RECS WRITTEN: ' PV-DISPLAY-COUNT.        22
023971                                                                     CL**4
023972     MOVE PV-TDISTDT-WRITE-CNTR TO PV-DISPLAY-COUNT.                 CL*22
023973     DISPLAY 'TOTAL DISTRB DTL RECS WRITTEN: ' PV-DISPLAY-COUNT.        22
023980                                                                     CL*22
024000     STOP RUN.                                                    RCK00185
024010*                                                                 RCK00185
024100*0000-EXIT.                                                       RCK00185
024200*                                                                 RCK00185
024300*                                                                 RCK00185
024310******************************************************************RCK00185
024320*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                    RCK00185
024360******************************************************************RCK00185
024400 1000-INITIALIZE.                                                 RCK00185
025100*                                                                 RCK00185
025322     OPEN OUTPUT DISTRB-HDR-FILE                                     CL**3
025330                 DISTRB-DTL-FILE.                                    CL*13
025660*                                                                 RCK00185
025670     INITIALIZE DCLTDISTHD.                                       RCK00185
025672     SET PS-NOT-EOF-DISTRB-HDR-CSR TO TRUE.                             21
025680*                                                                 RCK00185
026300*1000-EXIT.                                                       RCK00185
026400*                                                                 RCK00185
026401*                                                                 RCK00185
030048******************************************************************RCK00185
030049*  PROCESS THE DISTRIBUTION HEADER ROWS THAT ARE FOUND ON THE     RCK00185
030050*  RECEIVERS EXTRACT FILE.  WRITE THIS RECORD TO AN ARCHIVE FILE. RCK00185
030051*  OPEN ANOTHER CURSOR AND PROCESS ALL DISTRIBUTION DETAIL ROWS   RCK00185
030052*  FOR A SPECIFIED LINE.                                          RCK00185
030060******************************************************************RCK00185
030100 2000-PROCESS-TDISTHD-CURSOR.                                        CL**4
030700                                                                  RCK00185
030800     IF  PV-CURR-RECEIVER = PV-PREV-RECEIVER                         CL*21
030800         IF  PS-WRITE-DISTRB-HDR-DTL                                 CL*21
030800             PERFORM 2200-WRITE-TDISTHD-TDISTDT                      CL*21
030800         END-IF                                                      CL*21
030800     ELSE                                                            CL*21
030800         DISPLAY '** CURRENT RECEIVER: ' PV-CURR-RECEIVER ' **'      CL*21
030800         MOVE PV-CURR-RECEIVER              TO PV-PREV-RECEIVER      CL*21
030812         SET PS-DO-NOT-WRITE-DISTRB-HDR-DTL TO TRUE                     85
030812         ADD 1                              TO PV-TRECEIV-READ-CNT     85 
030800         PERFORM 2100-READ-TRECEIV                                   CL*21
030800         IF  PS-WRITE-DISTRB-HDR-DTL                                 CL*21
030800             PERFORM 2200-WRITE-TDISTHD-TDISTDT                      CL*21
030800         END-IF                                                      CL*21
030800     END-IF.                                                         CL*21
030840*                                                                 RCK00185
030900     PERFORM 7100-FETCH-DISTRB-HDR-CURSOR.                           CL*21
030910*                                                                 RCK00185
031000*2000-EXIT.                                                       RCK00185
031010*                                                                 RCK00185
031020*                                                                 RCK00185
031062******************************************************************RCK00185
031063*  READ A ROW FROM THE TRECEIV TABLE                              RCK00185
031070******************************************************************RCK00185
031600 2100-READ-TRECEIV.                                                  CL*21
032000     INITIALIZE DCLTRECEIV.                                       RCK00185
032500                                                                  RCK00185
032000     PERFORM WITH TEST AFTER                                      RCK00185
032100       VARYING PV-OPEN-COUNTER FROM 1 BY 1                        RCK00185
032200         UNTIL (PV-OPEN-COUNTER > PC-MAX-RETRIES                  RCK00185
032300                OR                                                RCK00185
032400                SQLCODE = ZERO                                    RCK00185
032300                OR                                                RCK00185
032400                SQLCODE = +100)                                   RCK00185
032500                                                                  RCK00185
032600         EXEC SQL                                                 RCK00185
036200             SELECT CRE_TMST                                              
036200                   ,STATUS_CDE                                            
010800                   ,REC_TMST                                              
010800                   ,CURR_OPER_UNT_ID                                      
036301               INTO :RECEIV-CRE-TMST                                      
036301                   ,:RECEIV-STATUS-CDE                                    
010800                   ,:RECEIV-REC-TMST                                      
010800                   ,:RECEIV-CURR-OPER-UNT-ID                              
036302               FROM TRECEIV                                               
036303              WHERE ORD_NBR        = :DISTHD-ORD-NBR                      
036303                AND REC_SEQ_NBR    = :DISTHD-RCVR-SEQ-NBR                 
036303                AND VER_STATUS_CDE = 'A'                                  
032800         END-EXEC                                                 RCK00185
032900                                                                  RCK00185
033000         EVALUATE TRUE                                            RCK00185
033100             WHEN SQLCODE = -904                                  RCK00185
033200             WHEN SQLCODE = -911                                  RCK00185
033300                 CONTINUE                                         RCK00185
034200             WHEN SQLCODE = ZERO                                  RCK00185
034300                 IF  RECEIV-STATUS-CDE = 'DE'                     RCK00185
034300                     DISPLAY '** RECEIVER '                       RCK00185
034300                             PV-CURR-RECEIVER                     RCK00185
034300                             ' IS IN DELETED STATUS **'           RCK00185
034300                     DISPLAY '**'                                 RCK00185
034300                     SET PS-WRITE-DISTRB-HDR-DTL TO TRUE          RCK00185
030812                     ADD 1 TO PV-TRECEIV-DEL-STAT-CNT                  85 
034300                 ELSE                                             RCK00185
                           DISPLAY '** RECEIVER '                       0185    
                                   PV-CURR-RECEIVER                     0185    
                                   ' IS IN '                            0185    
                                   RECEIV-STATUS-CDE                    0185    
                                   ' STATUS - DO NOT DELETE TDISTHD **' 0185    
                           DISPLAY '**'                                 0185    
030812                     ADD 1 TO PV-TRECEIV-OTH-STAT-CNT                  85 
034300                 END-IF                                           RCK00185
034200             WHEN SQLCODE = +100                                  RCK00185
034300                 DISPLAY '** RECEIVER '                           RCK00185
034300                         PV-CURR-RECEIVER                         RCK00185
034300                         ' HAS BEEN DELETED **'                   RCK00185
034300                 DISPLAY '**'                                     RCK00185
034300                 SET PS-WRITE-DISTRB-HDR-DTL     TO TRUE          RCK00185
030812                 ADD 1 TO PV-TRECEIV-DELETED-CNT                       85 
033400             WHEN SQLWARN0 NOT = SPACES                           RCK00185
033500             WHEN SQLCODE < ZERO                                  RCK00185
033600             WHEN SQLCODE > ZERO                                  RCK00185
033700                 MOVE '2100-READ-TRECEIV              '              CL*21
033800                                 TO PV-PARAGRAPH-NAME             RCK00185
033900                 MOVE 'READ A ROW FROM TRECEIV       '               CL*21
034000                                 TO PV-DB2-OPERATION-MESSAGE      RCK00185
034010                 MOVE 'TRECEIV'  TO PV-DB2-TABLE-NAME                CL*21
034100                 PERFORM 9100-SQL-ABEND                           RCK00185
034400         END-EVALUATE                                             RCK00185
034500     END-PERFORM.                                                 RCK00185
034600                                                                  RCK00185
034700     IF PV-OPEN-COUNTER > PC-MAX-RETRIES                          RCK00185
034800         MOVE '2100-READ-TRECEIV '                                   CL*21
034900                                 TO PV-PARAGRAPH-NAME             RCK00185
035000         MOVE 'READ MAX RETRIES EXCEEDED     '                    RCK00185
035100                                 TO PV-DB2-OPERATION-MESSAGE      RCK00185
035110         MOVE 'TRECEIV'          TO PV-DB2-TABLE-NAME                CL*21
035200         PERFORM 9100-SQL-ABEND                                   RCK00185
035300     END-IF.                                                      RCK00185
035600*                                                                 RCK00185
035400*2100-EXIT.                                                       RCK00185
035500*                                                                 RCK00185
035600*                                                                 RCK00185
030048******************************************************************RCK00185
030049*  WRITE OUT THE TDISTHD ROW AND ASSOCIATED TDISTDT ROWS.  THESE  RCK00185
030050*  ARE RECORDS THAT ARE ASSOCIATED WITH A RECEIVER THAT EITHER    RCK00185
030051*  IS IN A COMPLETED OR DELETED STATUS, OR NO LONGER EXISTS ON    RCK00185
030052*  THE TRECEIV TABLE.                                             RCK00185
030060******************************************************************RCK00185
030100 2200-WRITE-TDISTHD-TDISTDT.                                         CL**4
030700                                                                  RCK00185
030800     PERFORM 8000-WRITE-DISTRB-HDR-RECORD.                           CL*21
030801*                                                                 RCK00185
030810     PERFORM 7300-OPEN-DISTRB-DTL-CURSOR.                            CL*21
030811*                                                                 RCK00185
030812     SET PS-NOT-EOF-DISTRB-DTL-CSR TO TRUE                              85
030813     PERFORM 7400-FETCH-DISTRB-DTL-CURSOR.                           CL*21
030816*                                                                 RCK00185
030820     PERFORM 3000-PROCESS-TDISTDT-CURSOR                             CL*21
030830         UNTIL PS-EOF-DISTRB-DTL-CSR.                                CL*21
030831*                                                                 RCK00185
030832     PERFORM 7500-CLOSE-DISTRB-DTL-CURSOR.                              21
030910*                                                                 RCK00185
031000*2200-EXIT.                                                       RCK00185
031010*                                                                 RCK00185
031020*                                                                 RCK00185
031021******************************************************************RCK00185
031022*  PROCESS THE DISTRIBUTION DETAIL ROWS FOR A SPECIFIED RECEIVER  RCK00185
031023*  LINE, WRITING EACH ROW TO AN ARCHIVE FILE.                     RCK00185
031029******************************************************************RCK00185
031030 3000-PROCESS-TDISTDT-CURSOR.                                        CL**4
031031                                                                  RCK00185
031032     PERFORM 8100-WRITE-DISTRB-DTL-RECORD.                           CL*21
031033*                                                                 RCK00185
031043     PERFORM 7400-FETCH-DISTRB-DTL-CURSOR.                           CL*21
031044*                                                                 RCK00185
031045*3000-EXIT.                                                       RCK00185
031046*                                                                 RCK00185
031047*                                                                 RCK00185
031062******************************************************************RCK00185
031063*  OPEN THE DISTRIBUTION HEADER CURSOR.                           RCK00185
031070******************************************************************RCK00185
031600 7000-OPEN-DISTRB-HDR-CURSOR.                                        CL*21
032000     PERFORM WITH TEST AFTER                                      RCK00185
032100       VARYING PV-OPEN-COUNTER FROM 1 BY 1                        RCK00185
032200         UNTIL (PV-OPEN-COUNTER > PC-MAX-RETRIES                  RCK00185
032300                OR                                                RCK00185
032400                SQLCODE = ZERO)                                   RCK00185
032500                                                                  RCK00185
032600         EXEC SQL                                                 RCK00185
032700              OPEN DISTRB_HDR_CURSOR                                 CL*21
032800         END-EXEC                                                 RCK00185
032900                                                                  RCK00185
033000         EVALUATE TRUE                                            RCK00185
033100             WHEN SQLCODE = -904                                  RCK00185
033200             WHEN SQLCODE = -911                                  RCK00185
033300                 CONTINUE                                         RCK00185
033400             WHEN SQLWARN0 NOT = SPACES                           RCK00185
033500             WHEN SQLCODE < ZERO                                  RCK00185
033600             WHEN SQLCODE > ZERO                                  RCK00185
033700                 MOVE '7000-OPEN-DISTRB-HDR-CURSOR    '              CL*21
033800                                 TO PV-PARAGRAPH-NAME             RCK00185
033900                 MOVE 'OPEN THE DISTRB_HDR_CURSOR    '               CL*21
034000                                 TO PV-DB2-OPERATION-MESSAGE      RCK00185
034010                 MOVE 'TDISTHD'  TO PV-DB2-TABLE-NAME                CL*21
034100                 PERFORM 9100-SQL-ABEND                           RCK00185
034200             WHEN SQLCODE = ZERO                                  RCK00185
034300                 CONTINUE                                         RCK00185
034400         END-EVALUATE                                             RCK00185
034500     END-PERFORM.                                                 RCK00185
034600                                                                  RCK00185
034700     IF PV-OPEN-COUNTER > PC-MAX-RETRIES                          RCK00185
034800         MOVE '7000-OPEN-DISTRB-HDR-CURSOR   '                       CL*21
034900                                 TO PV-PARAGRAPH-NAME             RCK00185
035000         MOVE 'OPEN MAX RETRIES EXCEEDED     '                    RCK00185
035100                                 TO PV-DB2-OPERATION-MESSAGE      RCK00185
035110         MOVE 'TDISTHD'          TO PV-DB2-TABLE-NAME                CL*21
035200         PERFORM 9100-SQL-ABEND                                   RCK00185
035300     END-IF.                                                      RCK00185
031044*                                                                 RCK00185
035400*7000-EXIT.                                                       RCK00185
035500*                                                                 RCK00185
035600*                                                                 RCK00185
035610******************************************************************RCK00185
035620*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.         RCK00185
035630******************************************************************RCK00185
035700 7100-FETCH-DISTRB-HDR-CURSOR.                                       CL*21
035800                                                                        85
035900     INITIALIZE DCLTDISTHD.                                       RCK00185
036000                                                                        85
036100     EXEC SQL                                                     RCK00185
036200          FETCH DISTRB_HDR_CURSOR                                    CL*21
036301           INTO :DISTHD-ORD-NBR                                           
036302               ,:DISTHD-RCVR-SEQ-NBR                                      
036303               ,:DISTHD-ORD-LNE-NBR                                       
036304               ,:DISTHD-WRK-ORD-SEQ-NBR                                   
036305               ,:DISTHD-OPER-UNT-ID                                       
036306               ,:DISTHD-DISTRB-PRC-AREA-ID                                
036307               ,:DISTHD-PRCG-STAT-CDE                                     
036308               ,:DISTHD-CRE-TMST                                          
036309               ,:DISTHD-RSLTS-RCVD-TMST                                   
036310               ,:DISTHD-VRFD-TMST                                         
036400               ,:DISTHD-PRC-AREA-MODU-ID                                  
036500               ,:DISTHD-CRE-USR-ID                                        
036600               ,:DISTHD-TOT-CART-CNT                                      
036700               ,:DISTHD-CART-DWNLD-DTE                                    
036800               ,:DISTHD-CART-DWNLD-TM                                     
036900               ,:DISTHD-CART-DWNLD-USR-ID                                 
037000               ,:DISTHD-FCSE-CART-CNT                                     
037100               ,:DISTHD-FCSE-LBL-TYP-CDE                                  
037200               ,:DISTHD-P2L-CART-CNT                                      
037300               ,:DISTHD-INPK-CART-CNT                                     
037400               ,:DISTHD-INPK-UNT-QTY                                      
037500               ,:DISTHD-INPK-CNVYB-IND                                    
037600               ,:DISTHD-OTPK-UNT-QTY                                      
037700               ,:DISTHD-SYS-CRE-CDE                                       
037800               ,:DISTHD-SPC-INSTR-DESC                                    
037900               ,:DISTHD-FCSE-CNFRMN-TMST                                  
038000               ,:DISTHD-FCSE-CNFRMN-USR-ID                                
038100               ,:DISTHD-FCSE-CNFRMN-PGM-ID                                
                     ,:DISTHD-ACTV-IND                                          
                     ,:DISTHD-DLT-REQ-SRC-ID                                    
                     ,:DISTHD-DLT-REQ-TMST                                      
                     ,:DISTHD-DLT-RSLT-CDE                                      
                     ,:DISTHD-P2L-SCN-CART-CNT                                  
                     ,:DISTHD-CRE-LBL-OPT-CDE                                   
                     ,:DISTHD-FCSE-FCLTY-AREA-ID                                
                     ,:DISTHD-ODD-CART-UNT-QTY                                  
                     ,:DISTHD-PREPK-LNE-PRC-CDE                                 
                     ,:DISTHD-UCC128-DAT-USG-CDE                                
                     ,:DISTHD-CAL-UNT-PRCD-QTY                                  
                     ,:DISTHD-PHY-LBL-PRC-CNT                                   
                     ,:DISTHD-VRTL-LBL-PRC-CNT                                  
040900     END-EXEC.                                                    RCK00185
041000                                                                  RCK00185
041100     EVALUATE TRUE                                                RCK00185
041200         WHEN SQLCODE = ZERO                                      RCK00185
036301             MOVE DISTHD-ORD-NBR      TO PV-CURR-ORD-NBR                  
036302             MOVE DISTHD-RCVR-SEQ-NBR TO PV-CURR-RCVR-SEQ-NBR             
043700             ADD +1                   TO PV-TDISTHD-READ-CNTR        CL*22
041400         WHEN SQLCODE = +100                                      RCK00185
041500             SET PS-EOF-DISTRB-HDR-CSR                               CL*21
041600                                      TO TRUE                     RCK00185
041700         WHEN SQLWARN0 NOT = SPACES                               RCK00185
041800         WHEN SQLCODE < ZERO                                      RCK00185
041900         WHEN SQLCODE > ZERO                                      RCK00185
042000             MOVE '7100-FETCH-DISTRB-HDR-CURSOR  '                   CL*21
042100                                      TO PV-PARAGRAPH-NAME        RCK00185
042200             MOVE 'FETCH THE DISTRB_HDR_CURSOR   '                   CL*21
042300                                      TO PV-DB2-OPERATION-MESSAGE RCK00185
042301             MOVE 'TDISTHD'           TO PV-DB2-TABLE-NAME           CL*21
042400             PERFORM 9100-SQL-ABEND                               RCK00185
042500     END-EVALUATE.                                                RCK00185
042510*                                                                 RCK00185
042600*7100-EXIT.                                                       RCK00185
042700*                                                                 RCK00185
042800*                                                                 RCK00185
042900******************************************************************RCK00185
042910*  CLOSE THE DISTRIBUTION HEADER CURSOR                              CL**4
042920******************************************************************RCK00185
043000 7200-CLOSE-DISTRB-HDR-CURSOR.                                       CL*21
043001                                                                  RCK00185
043010     PERFORM WITH TEST AFTER                                      RCK00185
043020       VARYING PV-CLOSE-COUNTER FROM 1 BY 1                       RCK00185
043030         UNTIL (PV-CLOSE-COUNTER > PC-MAX-RETRIES                 RCK00185
043040                OR                                                RCK00185
043050                SQLCODE = ZERO)                                   RCK00185
043060                                                                  RCK00185
043070         EXEC SQL                                                 RCK00185
043080              CLOSE DISTRB_HDR_CURSOR                                CL*21
043090         END-EXEC                                                 RCK00185
043091                                                                  RCK00185
043092         EVALUATE TRUE                                            RCK00185
043093             WHEN SQLCODE = -904                                  RCK00185
043094             WHEN SQLCODE = -911                                  RCK00185
043095                 CONTINUE                                         RCK00185
043096             WHEN SQLWARN0 NOT = SPACES                           RCK00185
043097             WHEN SQLCODE < ZERO                                  RCK00185
043098             WHEN SQLCODE > ZERO                                  RCK00185
043099                 MOVE '7200-CLOSE-DISTRB-HDR-CURSOR   '           RCK00185
043100                                 TO PV-PARAGRAPH-NAME             RCK00185
043101                 MOVE 'CLOSE THE DISTRB_HDR_CURSOR    '              CL*21
043102                                 TO PV-DB2-OPERATION-MESSAGE      RCK00185
043103                 MOVE 'TDISTHD'  TO PV-DB2-TABLE-NAME                   21
043104                 PERFORM 9100-SQL-ABEND                           RCK00185
043105             WHEN SQLCODE = ZERO                                  RCK00185
043106                 CONTINUE                                         RCK00185
043107         END-EVALUATE                                             RCK00185
043108     END-PERFORM.                                                 RCK00185
043109                                                                  RCK00185
043110     IF PV-CLOSE-COUNTER > PC-MAX-RETRIES                         RCK00185
043111         MOVE '7200-CLOSE-DISTRB-HDR-CURSOR'                            85
043112                                 TO PV-PARAGRAPH-NAME             RCK00185
043113         MOVE 'CLOSE MAX RETRIES EXCEEDED     '                   RCK00185
043114                                 TO PV-DB2-OPERATION-MESSAGE      RCK00185
043115         MOVE 'TDISTHD'          TO PV-DB2-TABLE-NAME                CL*21
043116         PERFORM 9100-SQL-ABEND                                   RCK00185
043117     END-IF.                                                      RCK00185
043118*                                                                 RCK00185
043119*7200-EXIT.                                                       RCK00185
043120*                                                                 RCK00185
043121*                                                                    CL**4
043122******************************************************************RCK00185
043123*  OPEN THE DISTRIBUTION DETAIL CURSOR.                           RCK00185
043124******************************************************************RCK00185
043125 7300-OPEN-DISTRB-DTL-CURSOR.                                        CL*21
043126     PERFORM WITH TEST AFTER                                      RCK00185
043127       VARYING PV-OPEN-COUNTER FROM 1 BY 1                        RCK00185
043128         UNTIL (PV-OPEN-COUNTER > PC-MAX-RETRIES                  RCK00185
043129                OR                                                RCK00185
043130                SQLCODE = ZERO)                                   RCK00185
043131                                                                  RCK00185
043132         EXEC SQL                                                 RCK00185
043133              OPEN DISTRB_DTL_CURSOR                                 CL*21
043134         END-EXEC                                                 RCK00185
043135                                                                  RCK00185
043136         EVALUATE TRUE                                            RCK00185
043137             WHEN SQLCODE = -904                                  RCK00185
043138             WHEN SQLCODE = -911                                  RCK00185
043139                 CONTINUE                                         RCK00185
043140             WHEN SQLWARN0 NOT = SPACES                           RCK00185
043141             WHEN SQLCODE < ZERO                                  RCK00185
043142             WHEN SQLCODE > ZERO                                  RCK00185
043143                 MOVE '7300-OPEN-DISTRB-DTL-CURSOR    '              CL*21
043144                                 TO PV-PARAGRAPH-NAME             RCK00185
043145                 MOVE 'OPEN THE DISTRB_DTL_CURSOR    '               CL*21
043146                                 TO PV-DB2-OPERATION-MESSAGE      RCK00185
043148                 MOVE 'TDISTDT'  TO PV-DB2-TABLE-NAME             RCK00185
043149                 PERFORM 9100-SQL-ABEND                           RCK00185
043150             WHEN SQLCODE = ZERO                                  RCK00185
043151                 CONTINUE                                         RCK00185
043152         END-EVALUATE                                             RCK00185
043153     END-PERFORM.                                                 RCK00185
043154                                                                  RCK00185
043155     IF PV-OPEN-COUNTER > PC-MAX-RETRIES                          RCK00185
043156         MOVE '7300-OPEN-DISTRB-DTL-CURSOR   '                       CL*21
043157                                 TO PV-PARAGRAPH-NAME             RCK00185
043158         MOVE 'OPEN MAX RETRIES EXCEEDED     '                    RCK00185
043159                                 TO PV-DB2-OPERATION-MESSAGE      RCK00185
043160         MOVE 'TDISTDT'          TO PV-DB2-TABLE-NAME             RCK00185
043161         PERFORM 9100-SQL-ABEND                                   RCK00185
043162     END-IF.                                                      RCK00185
043164*                                                                 RCK00185
043163*7300-EXIT.                                                       RCK00185
043164*                                                                 RCK00185
043165*                                                                 RCK00185
043166******************************************************************RCK00185
043167*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.         RCK00185
043168******************************************************************RCK00185
043169 7400-FETCH-DISTRB-DTL-CURSOR.                                       CL*21
043170                                                                        85
043171     INITIALIZE DCLTDISTDT.                                       RCK00185
043172                                                                        85
043173     EXEC SQL                                                     RCK00185
043174          FETCH DISTRB_DTL_CURSOR                                    CL*21
043175           INTO :DISTDT-ORD-NBR                                           
043176               ,:DISTDT-RCVR-SEQ-NBR                                      
043177               ,:DISTDT-ORD-LNE-NBR                                       
043178               ,:DISTDT-WRK-ORD-SEQ-NBR                                   
043179               ,:DISTDT-STR-NBR                                           
043180               ,:DISTDT-EXPTED-ALLOC-QTY                                  
043181               ,:DISTDT-ACTL-ALLOC-QTY                                    
043182               ,:DISTDT-RCVG-PSTD-STAT-CDE                                
043183               ,:DISTDT-P2L-SPLT-UNT-QTY                                  
043184               ,:DISTDT-FCSE-SPLT-UNT-QTY                                 
043185               ,:DISTDT-INPK-SPLT-UNT-QTY                                 
043186               ,:DISTDT-FCSE-ACTL-UNT-QTY                                 
                     ,:DISTDT-ODD-CART-IND                                      
                     ,:DISTDT-PREPK-ODD-UNT-QTY                                 
043187     END-EXEC.                                                    RCK00185
043188                                                                  RCK00185
043189     EVALUATE TRUE                                                RCK00185
043190         WHEN SQLCODE = ZERO                                      RCK00185
043191             CONTINUE                                             RCK00185
043192         WHEN SQLCODE = +100                                      RCK00185
043193             SET PS-EOF-DISTRB-DTL-CSR                               CL*21
043194                                 TO TRUE                          RCK00185
043195         WHEN SQLWARN0 NOT = SPACES                               RCK00185
043196         WHEN SQLCODE < ZERO                                      RCK00185
043197         WHEN SQLCODE > ZERO                                      RCK00185
043198             MOVE '7400-FETCH-DISTRB-DTL-CURSOR  '                   CL*21
043199                                 TO PV-PARAGRAPH-NAME             RCK00185
043200             MOVE 'FETCH THE DISTRB_DTL-CURSOR   '                   CL*21
043201                                 TO PV-DB2-OPERATION-MESSAGE      RCK00185
043202             MOVE 'TDISTDT'      TO PV-DB2-TABLE-NAME             RCK00185
043203             PERFORM 9100-SQL-ABEND                               RCK00185
043204     END-EVALUATE.                                                RCK00185
043206*                                                                 RCK00185
043205*7400-EXIT.                                                       RCK00185
043206*                                                                 RCK00185
043207*                                                                 RCK00185
043208******************************************************************RCK00185
043209*  CLOSE THE DISTRIBUTION DETAIL CURSOR                              CL**4
043210******************************************************************RCK00185
043211 7500-CLOSE-DISTRB-DTL-CURSOR.                                       CL*21
043212                                                                  RCK00185
043213     PERFORM WITH TEST AFTER                                      RCK00185
043214       VARYING PV-CLOSE-COUNTER FROM 1 BY 1                       RCK00185
043215         UNTIL (PV-CLOSE-COUNTER > PC-MAX-RETRIES                 RCK00185
043216                OR                                                RCK00185
043217                SQLCODE = ZERO)                                   RCK00185
043218                                                                  RCK00185
043219         EXEC SQL                                                 RCK00185
043220              CLOSE DISTRB_DTL_CURSOR                                CL*21
043221         END-EXEC                                                 RCK00185
043222                                                                  RCK00185
043223         EVALUATE TRUE                                            RCK00185
043224             WHEN SQLCODE = -904                                  RCK00185
043225             WHEN SQLCODE = -911                                  RCK00185
043226                 CONTINUE                                         RCK00185
043227             WHEN SQLWARN0 NOT = SPACES                           RCK00185
043228             WHEN SQLCODE < ZERO                                  RCK00185
043229             WHEN SQLCODE > ZERO                                  RCK00185
043230                 MOVE '7500-CLOSE-DISTRB-DTL-CURSOR   '           RCK00185
043231                                 TO PV-PARAGRAPH-NAME             RCK00185
043232                 MOVE 'CLOSE THE DISTRB_DTL_CURSOR    '              CL*21
043233                                 TO PV-DB2-OPERATION-MESSAGE      RCK00185
043234                 MOVE 'TDISTHD'  TO PV-DB2-TABLE-NAME                   21
043235                 PERFORM 9100-SQL-ABEND                           RCK00185
043236             WHEN SQLCODE = ZERO                                  RCK00185
043237                 CONTINUE                                         RCK00185
043238         END-EVALUATE                                             RCK00185
043239     END-PERFORM.                                                 RCK00185
043240                                                                  RCK00185
043241     IF PV-CLOSE-COUNTER > PC-MAX-RETRIES                         RCK00185
043242         MOVE '7500-CLOSE-DISTRB-DTL-CURSOR'                            85
043243                                 TO PV-PARAGRAPH-NAME             RCK00185
043244         MOVE 'CLOSE MAX RETRIES EXCEEDED     '                   RCK00185
043245                                 TO PV-DB2-OPERATION-MESSAGE      RCK00185
043246         MOVE 'TDISTHD'          TO PV-DB2-TABLE-NAME                CL*21
043247         PERFORM 9100-SQL-ABEND                                   RCK00185
043248     END-IF.                                                      RCK00185
043249*                                                                 RCK00185
043250*7500-EXIT.                                                       RCK00185
043251*                                                                 RCK00185
043252*                                                                    CL**4
043253******************************************************************RCK00185
043260*  WRITE THE DISTRIBUTION HEADER RECORD                              CL**4
043300******************************************************************RCK00185
043400 8000-WRITE-DISTRB-HDR-RECORD.                                       CL*21
043500     WRITE DISTRB-HDR-RECORD                                            15
043600         FROM DCLTDISTHD.                                            CL**4
043700     ADD +1 TO PV-TDISTHD-WRITE-CNTR.                                CL*22
043710*                                                                 RCK00185
043800*8000-EXIT.                                                       RCK00185
043900*                                                                 RCK00185
044000*                                                                    CL**4
044200******************************************************************RCK00185
044201*  WRITE THE DISTRIBUTION DETAIL RECORD                              CL**4
044202******************************************************************RCK00185
044203 8100-WRITE-DISTRB-DTL-RECORD.                                       CL*21
044205     WRITE DISTRB-DTL-RECORD                                         CL*15
044206         FROM DCLTDISTDT.                                            CL**4
044207     ADD +1 TO PV-TDISTDT-WRITE-CNTR.                                CL*22
044208*                                                                    CL**4
044209*8100-EXIT.                                                       RCK00185
043900*                                                                 RCK00185
044000*                                                                    CL**4
049800 9100-SQL-ABEND.                                                  RCK00185
049900******************************************************************RCK00185
050000*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL    RCK00185
050100*  ERROR OR WARNING CODE OCCURS.                                  RCK00185
050200******************************************************************RCK00185
050300     DISPLAY '9100-SQL-ABEND'.                                    RCK00185
050400     DISPLAY '** ABEND           **'.                                   85
050500     DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.        85
050701     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        RCKUT021
050702     DISPLAY '** TABLE AFFECTED  ** = ' PV-DB2-TABLE-NAME.        RCKUT021
050705     DISPLAY '** OPERATION       ** = ' PV-DB2-OPERATION-MESSAGE  RCKUT021
050710     DISPLAY '**                 ** = '                           RCKUT021
050800                                                                  RCK00185
050900******************************************************************RCK00185
051000* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    RCK00185
051100* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         RCK00185
051200* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     RCK00185
051300* FORMAT.                                                         RCK00185
051400******************************************************************RCK00185
051500     COPY DPPD004.                                                      85
051600                                                                  RCK00185
051700     MOVE +4013                  TO ABEND-CODE.                   RCK00185
051800     CALL 'ILBOABN0' USING ABEND-CODE.                            RCK00185
051900*9100-EXIT.                                                       RCK00185
