000100 TITLE 'COPY XL.PURCHASE.ORDER.ERROR.Q TO A FLAT FILE'.                   
000200 IDENTIFICATION DIVISION.                                                 
000300* ------------------------------------------------------------- *         
000400 PROGRAM-ID.      XLKUT061.                                               
000500 AUTHOR.          TKMA536.                                                
000600 INSTALLATION.    KOHLS DEPARTMENT STORES.                                
000700 DATE-WRITTEN.    05-02-2003.                                             
000800                                                                          
000900*****************************************************************         
001000*******     COPY PO MSG ERROR QUEUE TO FILE            **********         
001100*****************************************************************         
001200*                                                               *         
001300*                       PROGRAM  LOGIC                          *         
001400*                       --------------                          *         
001500*  MAIN                                                         *         
001600*  ----                                                         *         
001700*                                                               *         
001800*   OPEN THE MESSAGE OUTPUT SEQUENTIAL FILE.                    *         
001900*                                                               *         
002000*   CONNECT TO THE QUEUE MANAGER.                               *         
002100*   IF CONNECTION FAILED THEN                                   *         
002200*            CALL 9000-DISPLAY-ERROR-MESSAGE AND EXIT           *         
002300*                                                               *         
002400*   OPEN THE SPECIFIED MESSAGE QUEUE (MQOPEN).                  *         
002500*   IF OPEN FAILED THEN                                         *         
002600*            DISCONNECT FROM QUEUE MANAGER                      *         
002700*            CALL 9000-DISPLAY-ERROR-MESSAGE AND EXIT           *         
002800*   ENDIF.                                                      *         
002900*                                                               *         
003000*   SET THE GET MESSAGE OPTIONS.                                *         
003100*            GET MESSAGES FROM QUEUE (MQGET)                    *         
003200*            IF GET FAILED                                      *         
003300*                     CALL 9000-DISPLAY-ERROR-MESSAGE           *         
003400*            ENDIF                                              *         
003500*                                                               *         
003600*   WRITE MESSAGE TO OUTPUT SEQUENTIAL FILE                     *         
003700*                                                               *         
003800*   CLOSE THE MESSAGE QUEUE.                                    *         
003900*   IF CLOSE FAILED THEN                                        *         
004000*            CALL 9000-DISPLAY-ERROR-MESSAGE.                   *         
004100*                                                               *         
004200*   DISCONNECT FROM THE QUEUE MANAGER.                          *         
004300*   IF DISCONNECT FAILED THEN                                   *         
004400*            CALL 9000-DISPLAY-ERROR-MESSAGE.                   *         
004500*                                                               *         
004600*   CLOSE THE MESSAGE OUTPUT FILE.                              *         
004700*                                                               *         
004800*   EXIT PROGRAM.                                               *         
004900*                                                               *         
005000*****************************************************************         
005100* ------------------------------------------------------------- *         
005200 ENVIRONMENT DIVISION.                                                    
005300* ------------------------------------------------------------- *         
005400                                                                          
005500 CONFIGURATION SECTION.                                                   
005600                                                                          
005700 SOURCE-COMPUTER.       IBM-3090.                                         
005800 OBJECT-COMPUTER.       IBM-3090.                                         
005900                                                                          
006000 INPUT-OUTPUT SECTION.                                                    
006100                                                                          
006200 FILE-CONTROL.                                                            
006300     SELECT CONTROL-CARD-1                                                
006400            ASSIGN TO INP01.                                              
006500     SELECT CONTROL-CARD-2                                                
006600            ASSIGN TO INP02.                                              
006700     SELECT MSG-EXTRACT                                                   
006800            ASSIGN TO OUT01.                                              
006900                                                                          
007000* ------------------------------------------------------------- *         
007100 DATA DIVISION.                                                           
007200* ------------------------------------------------------------- *         
007300 FILE SECTION.                                                            
007400                                                                          
007500 FD  CONTROL-CARD-1                                                       
007600     RECORDING MODE F                                                     
007700     BLOCK CONTAINS 0 RECORDS                                             
007800     LABEL RECORDS ARE OMITTED.                                           
007900 01  CONTROL-CARD-1-LINE              PIC  X(80).                         
008000                                                                          
008100 FD  CONTROL-CARD-2                                                       
008200     RECORDING MODE F                                                     
008300     BLOCK CONTAINS 0 RECORDS                                             
008400     LABEL RECORDS ARE OMITTED.                                           
008500 01  CONTROL-CARD-2-LINE              PIC  X(80).                         
008600                                                                          
008700 FD  MSG-EXTRACT                                                          
008800     RECORDING MODE F                                                     
008900     BLOCK CONTAINS 0 RECORDS                                             
009000     LABEL RECORDS ARE OMITTED.                                           
009100 01  MSG-EXTRACT-RECORD               PIC X(1026).                        
009200                                                                          
009300* ------------------------------------------------------------- *         
009400 WORKING-STORAGE SECTION.                                                 
009500* ------------------------------------------------------------- *         
009600*                                                                         
009700                                                                          
009800                                                                          
009900 01  WS-MQCONN               PIC X(8)  VALUE 'CSQBCONN'.                  
010000 01  WS-MQOPEN               PIC X(8)  VALUE 'CSQBOPEN'.                  
010100 01  WS-MQGET                PIC X(8)  VALUE 'CSQBGET'.                   
010200 01  WS-MQCLOSE              PIC X(8)  VALUE 'CSQBCLOS'.                  
010300 01  WS-MQDISC               PIC X(8)  VALUE 'CSQBDISC'.                  
010400 01  WS-MQCMIT               PIC X(8)  VALUE 'CSQBCOMM'.                  
010500 01  WS-MQBACK               PIC X(8)  VALUE 'CSQBBACK'.                  
010600*                                                                         
010700*    GENERAL WORK FIELDS                                                  
010800*                                                                         
010900 01  NUMRECS                 PIC 9(9)  VALUE ZEROS.                       
011000 01  NUMGETS                 PIC S9(9) BINARY VALUE 0.                    
011100 01  NUMPUTS                 PIC 9(9)  VALUE ZEROS.                       
011200 01  NUMCMTS                 PIC 9(9)  VALUE ZEROS.                       
011300 01  ERROR-MESSAGE           PIC X(48) VALUE SPACES.                      
011400 01  PV-PARAGRAPH            PIC X(48) VALUE SPACES.                      
011500*                                                                         
011600*   PARAMETER VARIABLES                                                   
011700*                                                                         
011800 01  QM-NAME               PIC X(48).                                     
011900 01  QNAME                 PIC X(48).                                     
012000 01  PADCHAR               PIC X(1) VALUE '*'.                            
012100 01  MSGBUFFER             PIC X(1000000) VALUE SPACES.                   
012200 01  NUMMSGS               PIC S9(9) BINARY VALUE 1.                      
012300 01  MSGLENGTH             PIC S9(9) BINARY VALUE +1000000.               
012400 01  DATA-LENGTH           PIC S9(9) BINARY.                              
012500 01  MQCHECK               PIC X(8) VALUE 'MQR2C'.                        
012600 01  MQ-REASON-TEXT        PIC X(30) VALUE SPACES.                        
012700 01  HOLD-REASON           PIC S9(9) BINARY.                              
012800 01  HOLD-COMPLETION-CODE  PIC S9(9) BINARY.                              
012900*                                                                         
013000*    API FIELDS                                                           
013100*                                                                         
013200 01  HCONN                   PIC S9(9) BINARY VALUE 0.                    
013300 01  GQ-HANDLE               PIC S9(9) BINARY VALUE 0.                    
013400 01  OPEN-OPTIONS            PIC S9(9) BINARY.                            
013500 01  COMPLETION-CODE         PIC S9(9) BINARY.                            
013600 01  REASON                  PIC S9(9) BINARY.                            
013700 01  CON-REASON              PIC S9(9) BINARY.                            
013800                                                                          
013900 01  PV-ABEND-VARIABLES.                                                  
014000     05  PV-ABEND-CODE              PIC S9(04) VALUE +0 COMP SYNC.        
014100     88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
014200     88  ABEND-4002-READ-ERROR                       VALUE +4002.         
014300     88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
014400     88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
014500     88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
014600     88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
014700     88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
014800     88  ABEND-4008-DATA-ERROR                       VALUE +4008.         
014900     88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009.         
015200     88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
015300     88  ABEND-4019-CAL-SUB-ERROR                    VALUE +4019.         
015400     88  ABEND-4020-MQ-ERROR                         VALUE +4020.         
015500     88  ABEND-4444-FORCED-ABEND                     VALUE +4444.         
015600                                                                          
015700                                                                          
015800  1  PE-MQ-ABEND-VARIABLES.                                               
015900     10  PE-MQ-QMANAGER           PIC X(05) VALUE SPACES.                 
016000     10  PE-MQ-QNAME              PIC X(30) VALUE SPACES.                 
016100     10  PE-MQ-COMPLETION-CODE    PIC 9(04) VALUE ZEROS.                  
016200     10  PE-MQ-REASON-CODE        PIC 9(04) VALUE ZEROS.                  
016300                                                                          
016400*                                                                         
016500*    API CONTROL BLOCKS                                                   
016600*                                                                         
016700 01  MQM-OBJECT-DESCRIPTOR.                                               
016800     COPY CMQODV.                                                         
016900 01  MQM-MESSAGE-DESCRIPTOR.                                              
017000     COPY CMQMDV.                                                         
017100 01  MQM-GET-MESSAGE-OPTIONS.                                             
017200     COPY CMQGMOV.                                                        
017300*                                                                         
017400*    MQV CONTAINS CONSTANTS (FOR FILLING IN THE CONTROL BLOCKS)           
017500*    AND RETURN CODES (FOR TESTING THE RESULT OF A CALL)                  
017600*                                                                         
017700 01  MQM-CONSTANTS.                                                       
017800     COPY CMQV.                                                           
017900*                                                                         
018000*----------------------------------------------------------------*        
018100*    CONTROL CARD                                                         
018200*----------------------------------------------------------------*        
018300 01  CC-CONTROL-CARD-1.                                                   
018400     05  CC-QMGR-NAME              PIC X(48).                             
018500     05  FILLER                    PIC X(32).                             
018600                                                                          
018700 01  CC-CONTROL-CARD-2.                                                   
018800     05  CC-STORE-EOD-MSG-QNAME    PIC X(48).                             
018900     05  FILLER                    PIC X(32).                             
019000                                                                          
019100*----------------------------------------------------------------*        
019200*    PROGRAM CONSTANTS                                                    
019300*----------------------------------------------------------------*        
019400     05  PC-PGM-NAME               PIC X(09) VALUE 'XLKUT061 '.           
019500     05  PC-FIFTY-STARS            PIC X(50) VALUE ALL '*'.               
019600                                                                          
019700*----------------------------------------------------------------*        
019800*    PROGRAM SWITCHES                                                     
019900*----------------------------------------------------------------*        
020000 01  PS-PROGRAM-SWITCHES.                                                 
020100     05  PS-CONTROL-CARD-1-SW      PIC X(01) VALUE 'N'.                   
020200         88  PS-CONTROL-CARD-1-END           VALUE 'Y'.                   
020300         88  PS-CONTROL-CARD-1-START         VALUE 'N'.                   
020400     05  PS-CONTROL-CARD-2-SW      PIC X(01) VALUE 'N'.                   
020500         88  PS-CONTROL-CARD-2-END           VALUE 'Y'.                   
020600         88  PS-CONTROL-CARD-2-START         VALUE 'N'.                   
020700                                                                          
020800 LINKAGE SECTION.                                                         
020900 01  RUN-PARMS.                                                           
021000     05  PARM-LENGTH     PIC S9(04) COMP.                                 
021100     05  PARM-USERID     PIC X(07).                                       
021200* ------------------------------------------------------------- *         
021300 PROCEDURE DIVISION USING RUN-PARMS.                                      
021400* ------------------------------------------------------------- *         
021500 1000-MAIN SECTION.                                                       
021600                                                                          
021700* ------------------------------------------------------------- *         
021800*  READ CONTROL CARD TO GET QUEUE MANAGER NAME                  *         
021900* ------------------------------------------------------------- *         
022000      SET PS-CONTROL-CARD-1-START TO TRUE.                                
022100      OPEN INPUT CONTROL-CARD-1.                                          
022200      READ CONTROL-CARD-1 INTO CC-CONTROL-CARD-1                          
022300          AT END                                                          
022400              SET PS-CONTROL-CARD-1-END                                   
022500                                  TO TRUE.                                
022600                                                                          
022700      IF PS-CONTROL-CARD-1-END                                            
022800          DISPLAY PC-PGM-NAME                                             
022900              'CONTROL CARD MISSING - FOR QMGR NAME'                      
023000          GO TO 4000-END-PROGRAM                                          
023100      ELSE                                                                
023200          DISPLAY PC-PGM-NAME                                             
023300                  '- '                                                    
023400                  PC-FIFTY-STARS                                          
023500          DISPLAY PC-PGM-NAME                                             
023600                  '- CONTROL CARD: '                                      
023700                  CC-CONTROL-CARD-1                                       
023800      END-IF.                                                             
023900                                                                          
024000      CLOSE CONTROL-CARD-1.                                               
024100                                                                          
024200* ------------------------------------------------------------- *         
024300*  READ CONTROL CARD TO GET STORE EOD MSG QUEUE NAME            *         
024400* ------------------------------------------------------------- *         
024500                                                                          
024600      SET PS-CONTROL-CARD-2-START TO TRUE.                                
024700      OPEN INPUT CONTROL-CARD-2.                                          
024800      READ CONTROL-CARD-2 INTO CC-CONTROL-CARD-2                          
024900          AT END                                                          
025000              SET PS-CONTROL-CARD-2-END                                   
025100                                  TO TRUE.                                
025200                                                                          
025300      IF PS-CONTROL-CARD-2-END                                            
025400          DISPLAY PC-PGM-NAME                                             
025500             'CONTROL CARD MISSING - STORE EOD MSG QUEUE NAME'            
025600          GO TO 4000-END-PROGRAM                                          
025700      ELSE                                                                
025800          DISPLAY PC-PGM-NAME                                             
025900                  '- '                                                    
026000                  PC-FIFTY-STARS                                          
026100          DISPLAY PC-PGM-NAME                                             
026200                  '- CONTROL CARD: '                                      
026300                  CC-CONTROL-CARD-2                                       
026400      END-IF.                                                             
026500                                                                          
026600      CLOSE CONTROL-CARD-2.                                               
026700                                                                          
026800* ------------------------------------------------------------- *         
026900*  OPEN OUTPUT MESSAGE SEQUENTIAL FILE                          *         
027000* ------------------------------------------------------------- *         
027100                                                                          
027200     OPEN OUTPUT MSG-EXTRACT.                                             
027300     INITIALIZE MSG-EXTRACT-RECORD.                                       
027400                                                                          
027500* ------------------------------------------------------------- *         
027600*                                                                         
027700*    CONNECT TO THE QUEUE MANAGER                                         
027800*                                                                         
027900     MOVE CC-QMGR-NAME TO QM-NAME.                                        
028000     CALL WS-MQCONN USING QM-NAME                                         
028100                         HCONN                                            
028200                         COMPLETION-CODE                                  
028300                         REASON.                                          
028400*                                                                         
028500*    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE                      
028600*    AND EXIT                                                             
028700*                                                                         
028800     MOVE REASON TO CON-REASON.                                           
028900     IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
029000        MOVE 'MQCONN'   TO ERROR-MESSAGE                                  
029100        PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
029200        GO TO 4000-END-PROGRAM                                            
029300     END-IF.                                                              
029400     DISPLAY 'MQCONN SUCCESSFUL TO ', QM-NAME.                            
029500*                                                                         
029600*    OPEN THE QUEUE FOR INPUT                                             
029700*                                                                         
029800     COMPUTE OPEN-OPTIONS = MQOO-INPUT-SHARED +                           
029900                            MQOO-FAIL-IF-QUIESCING.                       
030000     MOVE CC-STORE-EOD-MSG-QNAME   TO MQOD-OBJECTNAME.                    
030100     MOVE QM-NAME TO MQOD-OBJECTQMGRNAME.                                 
030200*                                                                         
030300     CALL WS-MQOPEN USING HCONN                                           
030400                         MQM-OBJECT-DESCRIPTOR                            
030500                         OPEN-OPTIONS                                     
030600                         GQ-HANDLE                                        
030700                         COMPLETION-CODE                                  
030800                         REASON.                                          
030900*                                                                         
031000*    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                            
031100*    AND EXIT.                                                            
031200*                                                                         
031300     IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
031400        MOVE 'MQOPEN'   TO ERROR-MESSAGE                                  
031500        PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
031600        GO TO 3000-DISCONNECT-QMGR                                        
031700     END-IF.                                                              
031800     DISPLAY 'MQOPEN SUCCESSFUL'.                                         
031900*                                                                         
032000     PERFORM 2000-MQGET-EOD-MSG.                                          
032100*                                                                         
032200*    CLOSE THE QUEUE                                                      
032300*                                                                         
032400     CALL WS-MQCLOSE USING HCONN                                          
032500                          GQ-HANDLE                                       
032600                          MQCO-NONE                                       
032700                          COMPLETION-CODE                                 
032800                          REASON.                                         
032900     IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
033000        MOVE 'MQCLOSE'  TO ERROR-MESSAGE                                  
033100        PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
033200     ELSE                                                                 
033300        DISPLAY 'MQCLOSE SUCCESSFUL'                                      
033400     END-IF.                                                              
033500*                                                                         
033600     PERFORM 3000-DISCONNECT-QMGR.                                        
033700     GO TO 4000-END-PROGRAM.                                              
033800*                                                                         
033900* ------------------------------------------------------------- *         
034000* PUT MESSAGE ON LOCAL QUEUE TO END THE DATA COLLECT PROCESS              
034100* ------------------------------------------------------------- *         
034200 2000-MQGET-EOD-MSG.                                                      
034300                                                                          
034400     PERFORM                                                              
034500       UNTIL (COMPLETION-CODE IS EQUAL TO MQCC-FAILED)                    
034600         MOVE MQMI-NONE TO MQMD-MSGID                                     
034700                                                                          
034800         INITIALIZE MQMD-CORRELID                                         
034900         IF PARM-LENGTH = ZERO                                            
035000             MOVE MQCI-NONE TO MQMD-CORRELID                              
035100         ELSE                                                             
035200             MOVE PARM-USERID TO MQMD-CORRELID                            
035300         END-IF                                                           
035400                                                                          
035500*        MOVE MQCI-NONE TO MQMD-CORRELID                                  
035600         MOVE +1000000 TO MSGLENGTH                                       
035700         COMPUTE MQGMO-OPTIONS  = MQGMO-SYNCPOINT +                       
035800                                  MQGMO-CONVERT   +                       
035900                                  MQGMO-WAIT                              
036000                                                                          
036100         CALL WS-MQGET USING HCONN                                        
036200                            GQ-HANDLE                                     
036300                            MQM-MESSAGE-DESCRIPTOR                        
036400                            MQM-GET-MESSAGE-OPTIONS                       
036500                            MSGLENGTH                                     
036600                            MSGBUFFER                                     
036700                            DATA-LENGTH                                   
036800                            COMPLETION-CODE                               
036900                            REASON                                        
037000                                                                          
037100*        IF PUT FAILED THEN DISPLAY ERROR MESSAGE                         
037200*        AND BREAK OUT OF LOOP                                            
037300                                                                          
037400         IF (COMPLETION-CODE NOT = MQCC-OK) THEN                          
037500            MOVE 'MQGET'     TO ERROR-MESSAGE                             
037600            PERFORM 9000-DISPLAY-ERROR-MESSAGE                            
037700         ELSE                                                             
037800            ADD 1 TO NUMRECS                                              
037900                IF NUMRECS > 1000                                         
038000                    PERFORM 3600-MQ-COMMIT                                
038100                    MOVE ZERO TO NUMRECS                                  
038200                END-IF                                                    
038300            ADD 1 TO NUMGETS                                              
038400            PERFORM 8000-WRITE-EXTRACT-RECORD                             
038500         END-IF                                                           
038600     END-PERFORM.                                                         
038700*                                                                         
038800* 2000-END-MQGET-EOD-MSG.                                                 
038900*                                                                         
039000* ------------------------------------------------------------- *         
039100* CLOSE AND DISCONNECT FROM THE MQ QUEUE MANAGER                          
039200* ------------------------------------------------------------- *         
039300 3000-DISCONNECT-QMGR.                                                    
039400*                                                                         
039500*    DISCONNECT FROM THE QUEUE MANAGER                                    
039600*                                                                         
039700     IF CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED                 
039800        CALL WS-MQDISC USING HCONN                                        
039900                            COMPLETION-CODE                               
040000                            REASON                                        
040100        IF (COMPLETION-CODE NOT = MQCC-OK) THEN                           
040200           MOVE 'MQDISC'   TO ERROR-MESSAGE                               
040300           PERFORM 9000-DISPLAY-ERROR-MESSAGE                             
040400        ELSE                                                              
040500           DISPLAY 'MQDISC SUCCESSFUL'                                    
040600        END-IF                                                            
040700     END-IF.                                                              
040800                                                                          
040900*    CLOSE THE MESSAGE EXTRACT OUTPUT FILE.                               
041000                                                                          
041100     CLOSE MSG-EXTRACT.                                                   
041200*                                                                         
041300* 3000-END-DISCONNECT.                                                    
041400******************************************************************        
041500*  COMMIT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT                
041600******************************************************************        
041700 3600-MQ-COMMIT.                                                          
041800                                                                          
041900     CALL WS-MQCMIT USING HCONN                                           
042000                          COMPLETION-CODE                                 
042100                          REASON.                                         
042200                                                                          
042300     IF (COMPLETION-CODE NOT = MQCC-OK)                                   
042400         CALL MQCHECK USING REASON                                        
042500                               MQ-REASON-TEXT                             
042600        MOVE '3600-COMMIT-MQ'   TO PV-PARAGRAPH                           
042700        MOVE 'MQCMIT ERROR'     TO ERROR-MESSAGE                          
042800        MOVE COMPLETION-CODE    TO HOLD-COMPLETION-CODE                   
042900        MOVE REASON             TO HOLD-REASON                            
043000        PERFORM 3700-BACK-OUT-MQ                                          
043100        MOVE HOLD-COMPLETION-CODE TO COMPLETION-CODE                      
043200                                        PE-MQ-COMPLETION-CODE             
043300        MOVE HOLD-REASON          TO REASON                               
043400                                        PE-MQ-REASON-CODE                 
043500        PERFORM 9200-MQ-ABEND                                             
043600     ELSE                                                                 
043700        ADD +1 TO NUMCMTS                                                 
043800     END-IF.                                                              
043900                                                                          
044000******************************************************************        
044100* BACK OUT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT               
044200******************************************************************        
044300 3700-BACK-OUT-MQ.                                                        
044400                                                                          
044500     CALL WS-MQBACK USING HCONN                                           
044600                          COMPLETION-CODE                                 
044700                          REASON.                                         
044800                                                                          
044900     IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
045000         CALL MQCHECK USING REASON                                        
045100                            MQ-REASON-TEXT                                
045200        MOVE '3700-BACK-OUT-MQ'  TO PV-PARAGRAPH                          
045300        MOVE 'MQBACK ERROR'      TO ERROR-MESSAGE                         
045400        PERFORM 9200-MQ-ABEND                                             
045500     END-IF.                                                              
045600******************************************************************        
045700                                                                          
045800* ------------------------------------------------------------- *         
045900* END THE PROGRAM                                                         
046000* ------------------------------------------------------------- *         
046100 4000-END-PROGRAM.                                                        
046200*                                                                         
046300*    DISPLAY THE NUMBER OF MESSAGES SUCCESSFULLY PUT                      
046400*    TO THE QUEUE                                                         
046500*                                                                         
046600     DISPLAY NUMGETS, ' MESSAGES READ FROM QUEUE'.                        
046700     DISPLAY NUMPUTS, ' MESSAGES PUT TO DISK    '.                        
046800     DISPLAY NUMCMTS, ' NO. OF COMMITS TAKEN    '.                        
046900*                                                                         
047000     IF NUMPUTS > 0                                                       
047100        MOVE 4095 TO RETURN-CODE                                          
047200     END-IF.                                                              
047300     STOP RUN.                                                            
047400*                                                                         
047500* ------------------------------------------------------------- *         
047600* WRITE THE MESSAGE QUEUE TO A SEQUENTIAL FILE.                           
047700* ------------------------------------------------------------- *         
047800 8000-WRITE-EXTRACT-RECORD.                                               
047900* ------------------------------------------------------------- *         
048000*                                                                         
048100     INITIALIZE MSG-EXTRACT-RECORD.                                       
048200     MOVE MSGBUFFER TO MSG-EXTRACT-RECORD.                                
048300     WRITE MSG-EXTRACT-RECORD.                                            
048400     ADD +1 TO NUMPUTS.                                                   
048500*                                                                         
048600* ------------------------------------------------------------- *         
048700* DISPLAY THE VALUES OF A FAILED MQ API COMMAND                           
048800* ------------------------------------------------------------- *         
048900 9000-DISPLAY-ERROR-MESSAGE.                                              
049000* ------------------------------------------------------------- *         
049100*                                                                         
049200     DISPLAY '************************************************'.          
049300     DISPLAY '* ', ERROR-MESSAGE.                                         
049400     DISPLAY '* COMPLETION CODE : ', COMPLETION-CODE.                     
049500     DISPLAY '* REASON CODE     : ', REASON.                              
049600     DISPLAY '************************************************'.          
049700*                                                                         
049800* 9000-DISPLAY-ERROR-MSG-END.                                             
049900*                                                                         
050000                                                                          
050100****************************************************************          
050200** PERFORM MQ-SERIES ABEND PROCESSING                                     
050300****************************************************************          
050400 9200-MQ-ABEND.                                                           
050500                                                                          
050600     DISPLAY '**** ABEND *************************************'.          
050700     DISPLAY '** MQSERIES ERROR **'.                                      
050800     DISPLAY '** PROGRAM:    ', PC-PGM-NAME.                              
050900     DISPLAY '** ABEND CODE: ', PV-ABEND-CODE.                            
051000     DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH.                             
051100     DISPLAY '** MESSAGE:    ', ERROR-MESSAGE.                            
051200     DISPLAY '** COMP CODE:  ', PE-MQ-COMPLETION-CODE.                    
051300     DISPLAY '** RSN CODE:   ', PE-MQ-REASON-CODE.                        
051400     DISPLAY '** RSN TEXT:   ', MQ-REASON-TEXT.                           
051500                                                                          
051600     MOVE +4013 TO PV-ABEND-CODE.                                         
051700     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
051800                                                                          
051900******************************************************************        
052000**                      END OF PROGRAM                                    
052100*                                                                         
052200******************************************************************        
