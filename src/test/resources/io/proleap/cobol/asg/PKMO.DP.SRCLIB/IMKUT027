       IDENTIFICATION DIVISION.                                         09/16/94
                                                                           LV020
       PROGRAM-ID.             IMKUT027.                                   CL**1
       AUTHOR.                 PAUL KEBER.                                 CL**1
       INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL**1
       DATE-WRITTEN.           10/01/2002.                                 CL**1
       DATE-COMPILED.                                                      CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      *  IMKUT027 - TSIZE DESCRIPTION UPDATE TRIGGER RPC               *   CL**1
      ******************************************************************   CL**1
      * THIS STORED PROCEDURE PROGRAM WILL BE USED TO FORMAT A NON-VSM     CL**1
      * ITEM MAINTENANCE TSIZE KOHL'S DESCRIPTION CHANGE TRANSACTION       CL**1
      * RECORD AND PUT IT ON THE IM.NONVSM.ITEM.CHANGES.Q MQSERIES QUEUE   CL**1
      * AND TO THE IM.NONVSM.RMS.ITEM.CHANGES.Q MQSERIES QUEUE WHEN THE         
      * SIZE_RNG_CDE FOR THE SIZE HAS A VALUE OF 'I' OR 'R'.                    
      * THE EVENT MESSAGE HAS A BUSINESS EVENT ID OF 005.                       
      ******************************************************************   CL**1
      * Change Log                                                              
      * DATE: 06/06/2007     CHANGE ID: WR32933    WHO: P. KEBER                
      * REASON FOR CHANGE : ADDED LOGIC TO PUT MESSAGES TO THE                  
      * IM.NONVSM.RMS.ITEM.CHANGES.Q MQSERIES QUEUE.                            
      ******************************************************************   CL**1
      * Change Log                                                              
      * DATE: 04/11/2016     CHANGE ID: CHG0139655 WHO: COGNIZANT               
      * REASON FOR CHANGE : REMOVED LOGIC WHICH PUT MESSAGES TO THE             
      * IM.NONVSM.RMS.ITEM.CHANGES.Q MQSERIES QUEUE AS PART OF                  
      * RMS9 RETIREMENT PROJECT                                                 
      ******************************************************************   CL**1
      * Change Log                                                              
      * DATE: 11/30/2018     CHANGE ID: CHG0213345    WHO: Jim Hunter           
213345* MAINFRAME REFACTORING PROJECT.                                     CL**1
213345* BECAUSE THE DB2 STORED PROCEDUES WON'T BE ABLE TO USE MQ           CL**1
213345* AFTER THEY ARE REFACTORED INTO JAVA APPS, THE MQ LOGIC IS          CL**1
213345* BEING REPLACED WITH INSERTS TO THE IMT_EVNT_MSG_BRG TABLE.         CL**1
213345* THE INSERT LOGIC IS BEING COPIED FROM IMKUP002.                    CL**1
213345* IMKCS301 WILL BE CHANGED TO READ THE RECORD WITH A NEW                  
213345* 915 BUSINESS EVENT CODE AND WRITE IT TO THE                             
213345* IM.NONVSM.ITEM.CHANGES.Q WHICH IS WHAT THIS PROGRAM DID BEFORE          
213345* IT WAS REFACTORED.                                                      
      ******************************************************************   CL**1
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************   CL**1
      * PROGRAM CONSTANTS                                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL**1
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME             PIC X(08)      VALUE 'IMKUT027'.         
           05  PC-OUT-MSGLENGTH        PIC S9(9)      VALUE +100 COMP.          
           05  PC-MAX-RETRIES          PIC S9(4)      VALUE +2   COMP.          
                                                                                
      ******************************************************************   CL**1
      * PROGRAM SWITCHES                                              *    CL**1
      ******************************************************************   CL**1
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW             PIC X(01)      VALUE 'N'.           CL**1
               88  PS-ERROR                           VALUE 'Y'.           CL**1
               88  PS-NO-ERROR                        VALUE 'N'.           CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      * PROGRAM VARIABLES                                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL*17
       01  PV-PROGRAM-VARIABLES.                                           CL**1
           05  PV-SAVE-COMPLETION-CODE      PIC S9(9) VALUE +0 COMP.            
           05  PV-SAVE-REASON               PIC S9(9) VALUE +0 COMP.            
           05  PV-RETRY-COUNT               PIC S9(4) VALUE +0 COMP.            
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACEs.             
           05  PV-ERROR-MESSAGE             PIC X(48) VALUE SPACES.             
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACES.             
                                                                                
       01  ABEND-CODE                       PIC S9(4) VALUE +0 COMP.            
           88  AC-DB2-ERROR                           VALUE +4013.              
                                                                                
      *****************************************************************         
      *  NON-VSM ITEM MAINTENANCE INFO COPYBOOK                                 
      ******************************************************************        
           COPY IMRD018.                                                        
                                                                           CL**1
                                                                                
      ******************************************************************        
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                
      ******************************************************************        
                                                                                
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
213345*  ITEM EVENT MESSAGE BRIDGE TABLE (IMT_EVNT_MSG_BRG)                     
      ******************************************************************        
           EXEC SQL                                                             
213345         INCLUDE IMT00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    DB2 COMMUNICATION AREA                                               
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
                                                                           CL**1
       LINKAGE SECTION.                                                         
       01  LS-KOHLS-SIZE-CDE            PIC S9(9)   COMP.                       
       01  LS-KOHLS-DESC-NEW            PIC X(10).                              
       01  LS-SIZE-RNG-CDE-NEW          PIC X(01).                              
                                                                                
       PROCEDURE DIVISION USING  LS-KOHLS-SIZE-CDE                              
                               , LS-KOHLS-DESC-NEW                              
                               , LS-SIZE-RNG-CDE-NEW.                           
                                                                                
213345******************************************************************        
213345*    FORMAT AND INSERT A NEW 905 BUSINESS EVENT CODE RECORD INTO          
213345*    THE IMT_EVNT_MSG_BRG TABLE.                                          
      ******************************************************************        
       1000-MAIN-MODULE.                                                        
                                                                                
           SET PS-NO-ERROR TO TRUE.                                             
                                                                                
           PERFORM 1100-CREATE-SIZE-DESC-CHG-REC.                               
                                                                                
           GOBACK.                                                              
                                                                                
213345******************************************************************   CL**1
213345*    CREATE A SIZE DESCRIPTION CHANGE MESSAGE FOR THE NON-VSM             
213345*    ITEM MAINTENANCE MQSERIES QUEUE TO BE INSERTED TO THE                
213345*    IMT_EVNT_MSG_BRG TABLE.                                              
213345*    INSTEAD OF 005, THIS PROGRAM WILL NOW SET THE BUSINESS               
213345*    EVENT ID TO 905.  IMKCS301 WILL BE CHANGED TO SEND THE               
213345*    RECORD TO THE IM.NONVSM.ITEM.CHANGES.Q WHICH IS WHAT THIS            
213345*    PROGRAM DID BEFORE IT WAS REFACTORED AND DB2 STORED                  
213345*    PROCEDURES WRITTEN IN JAVA COULD NO LONGER WRITE TO MQ.              
213345******************************************************************   CL**1
       1100-CREATE-SIZE-DESC-CHG-REC.                                           
                                                                                
           INITIALIZE IM018-ITEM-ACTION-DATA.                                   
213345     MOVE 905                 TO IM018-BUSINESS-EVENT-ID.                 
           MOVE LS-KOHLS-SIZE-CDE   TO IM018-SIZE-DESC-CHG-SIZE-CDE.            
           MOVE LS-KOHLS-DESC-NEW   TO IM018-SIZE-DESC-CHG-KOHLS-DESC.          
213345     PERFORM 2000-INSERT-IMT-EVNT-MSG.                                    
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
213345 2000-INSERT-IMT-EVNT-MSG.                                        20580025
213345                                                                  20590025
213345     MOVE ZEROES              TO IMT00001-RMS-STYL-ID.                    
213345     MOVE 905                 TO IMT00001-BUS-EVNT-TYP-CDE.               
213345     MOVE IMRD018-RECORD      TO IMT00001-MSG-TXT-TEXT.                   
213345     COMPUTE IMT00001-MSG-TXT-LEN = LENGTH OF IMRD018-RECORD.             
213345                                                                  20620025
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL SQLCODE = +0                                             
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                          
213345*                                                                 20710025
213345           EXEC SQL                                               20720025
213345               INSERT INTO IMT_EVNT_MSG_BRG                               
213345                      ( CRE_TMST                                          
213345                      ,RMS_STYL_ID                                        
213345                      ,BUS_EVNT_TYP_CDE                                   
213345                      ,MSG_TXT )                                          
213345               VALUES                                                     
213345                     ( CURRENT TIMESTAMP                                  
213345                     ,:IMT00001-RMS-STYL-ID                               
213345                     ,:IMT00001-BUS-EVNT-TYP-CDE                          
213345                     ,:IMT00001-MSG-TXT)                                  
213345           END-EXEC                                               20800025
213345*                                                                 20810025
213345         EVALUATE TRUE                                            20820025
213345             WHEN SQLCODE = ZERO                                  20870025
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
213345                 MOVE '2000-INSERT-IMT-EVNT-MSG'                          
                                                    TO PV-PARAGRAPH-NAME        
213345                 MOVE 'IMT_EVNT_MSG_BRG INSERT'                           
213345                                              TO PV-DB2-OPERATION         
                       PERFORM 9800-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
213345         MOVE '2000-INSERT-IMT-EVNT-MSG'   TO PV-PARAGRAPH-NAME           
213345         MOVE 'INSERT RETRIES EXCEEDED'    TO PV-DB2-OPERATION            
               PERFORM 9800-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
213345******************************************************************        
      * 9800-SQL-ABEND - DISPLAY DB2 ERROR INFO AND ROLLBACK THE CHANGES        
213345******************************************************************        
       9800-SQL-ERROR.                                                     CL**1
                                                                           CL*13
           DISPLAY '** IMKUT027: SQLERROR IN PARAGRAPH: '                       
                   PV-PARAGRAPH-NAME.                                           
           DISPLAY '** IMKUT027: OPERATION: ' PV-DB2-OPERATION.                 
                                                                           CL*13
      ******************************************************************   CL**1
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**1
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE            CL**1
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE        CL**1
      * FORMAT.                                                            CL**1
      ******************************************************************   CL**1
           COPY DPPD004.                                                   CL**1
                                                                                
           EXEC SQL                                                             
               ROLLBACK                                                         
           END-EXEC.                                                            
                                                                                
           SET PS-ERROR TO TRUE.                                                
