00001  ID DIVISION.                                                     07/15/93
00002                                                                   SNKUP008
00003  PROGRAM-ID.    SNKUP008.                                            LV002
00004                                                                   SNKUP008
00005  AUTHOR.        CHRISTINE CHARLESWORTH.                           SNKUP008
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP008
00007  DATE-WRITTEN.  AUGUST   , 1992.                                  SNKUP008
00008  DATE-COMPILED.                                                   SNKUP008
00009                                                                   SNKUP008
00010 ******************************************************************SNKUP008
00011 *   THIS PROGRAM WILL WRITE ALL TEST STATUS SHIP NOTICES        **SNKUP008
00012 *   WHICH HAVE BEEN ADDED TO THE PRODUCTION SHIP NOTICE TABLES  **SNKUP008
00013 *   TO A FLAT FILE.  THIS FILE WILL BE READ IN BY THE DELETE    **SNKUP008
00014 *   PROGRAMS, AND THE ROWS DELETED.                             **SNKUP008
00015 ******************************************************************SNKUP008
00016 *                                                               **SNKUP008
00017 *  CHANGE MADE:                                                 **SNKUP008
00018 *                                                               **SNKUP008
00019 ******************************************************************SNKUP008
00020 * CHANGED 10/06/92 BY PAT LEE                                     SNKUP008
00021 *   P921106005  CHANGED DECLARE LOGIC TO FETCH OPERCO_NBR AS WELL SNKUP008
00022 *               THE EDI_VEND_ID AND ASNHDR_NBR.                   SNKUP008
00023 *                                                                 SNKUP008
00024 ******************************************************************SNKUP008
00025 * CHANGED 11/21/92 BY ANNE YEKO                                   SNKUP008
00026 *   1812        INSTEAD OF DOING A DELETE CASCADE IN THIS PROGRAM,SNKUP008
00027 *               THE LOGIC WAS CHANGED TO WRITE THE KEY INFORMATIONSNKUP008
00028 *               TO A FLAT FILE.  PROGRAMS WERE WRITTEN TO DO      SNKUP008
00029 *               INDIVIDUAL DELETES FROM THE ASN TABLES BASED ON   SNKUP008
00030 *               THE KEY INFORMATION ON THIS FLAT FILE.            SNKUP008
00031 ******************************************************************SNKUP008
00032                                                                   SNKUP008
00033      EJECT                                                        SNKUP008
00034  ENVIRONMENT DIVISION.                                            SNKUP008
00035                                                                   SNKUP008
00036  INPUT-OUTPUT SECTION.                                            SNKUP008
00037  FILE-CONTROL.                                                    SNKUP008
00038                                                                   SNKUP008
00039      SELECT ASNHDR-DELETE-FILE                                    SNKUP008
00040             ASSIGN TO OUT01.                                      SNKUP008
00041                                                                   SNKUP008
00042  DATA DIVISION.                                                   SNKUP008
00043                                                                   SNKUP008
00044  FILE SECTION.                                                    SNKUP008
00045                                                                   SNKUP008
00046  FD  ASNHDR-DELETE-FILE                                           SNKUP008
00047      RECORDING MODE IS F                                          SNKUP008
00048      LABEL RECORDS ARE STANDARD                                   SNKUP008
00049      RECORD CONTAINS 5  CHARACTERS                                   CL**2
00050      BLOCK CONTAINS 0 RECORDS.                                    SNKUP008
00051                                                                   SNKUP008
00052  01  ASNHDR-DELETE-RECORD        PIC X(5).                           CL**2
00053                                                                   SNKUP008
00054  EJECT                                                            SNKUP008
00055  WORKING-STORAGE SECTION.                                         SNKUP008
00056 *                                                                 SNKUP008
00057  01  FILLER.                                                      SNKUP008
00058      05  FILLER                    PIC  X(36)  VALUE              SNKUP008
00059          ' ***** WORKING STORAGE PGM=SNKUP008 '.                  SNKUP008
00060      05  PGM-COMPILED              PIC  X(20)  VALUE SPACE.       SNKUP008
00061      05  FILLER                    PIC  X(10)  VALUE              SNKUP008
00062                                                ' LOCATION='.      SNKUP008
00063      05  PGM-LOCATION              PIC  X(04)  VALUE SPACE.       SNKUP008
00064      05  FILLER                    PIC  X(06)  VALUE              SNKUP008
00065                                                ' *****'.          SNKUP008
00066  01  ABEND-CODE                    PIC S9(04)  VALUE ZERO         SNKUP008
00067                                                COMP SYNC.         SNKUP008
00068  01  ABEND-PARAGRAPH               PIC  X(30)  VALUE SPACE.       SNKUP008
00069 *                                                                 SNKUP008
00070  EJECT                                                            SNKUP008
00071  01  PC-PROGRAM-CONSTANTS.                                        SNKUP008
00072      05  PC-TEST                   PIC  X(01)  VALUE 'T'.         SNKUP008
00073      05  PC-Y                      PIC  X(01)  VALUE 'Y'.         SNKUP008
00074      05  PC-N                      PIC  X(01)  VALUE 'N'.         SNKUP008
00075      05  PC-1                      PIC  9(01)  VALUE  1.          SNKUP008
00076 *                                                                 SNKUP008
00077  01  PS-PROGRAM-SWITCHES.                                         SNKUP008
00078      05  PS-TEST-ASN-EOT-SW        PIC  X(01)  VALUE 'N'.         SNKUP008
00079          88 TEST-ASNS-EOT                      VALUE 'Y'.         SNKUP008
00080 *                                                                 SNKUP008
00081  01  PV-PROGRAM-VARIABLES.                                        SNKUP008
00082      05  PV-ASNHDR-WRITTEN         PIC S9(07)  VALUE +0           SNKUP008
00083                                                COMP-3.            SNKUP008
00084  EJECT                                                            SNKUP008
00085 *  ASNHDR DELETE RECORD LAYOUT                                    SNKUP008
00086 *                                                                 SNKUP008
00087      COPY SNRD002O.                                               SNKUP008
00090  EJECT                                                            SNKUP008
00091 ******************************************************************SNKUP008
00092 *    DB2 DATABASE AREA                                            SNKUP008
00093 ******************************************************************SNKUP008
00094  EJECT                                                            SNKUP008
00095 *    DCLGEN ========= ASN HEADER                                  SNKUP008
00096      EXEC SQL                                                     SNKUP008
00097          INCLUDE TASNHDR                                          SNKUP008
00098      END-EXEC.                                                    SNKUP008
00099 *     TASNHDR CURSOR AREA                                         SNKUP008
00100      EXEC SQL                                                     SNKUP008
00101        DECLARE TEST_ASN_CURSOR CURSOR FOR                         SNKUP008
00102          SELECT ASNHDR_ID,                                        SNKUP008
00103                 REC_MTHD_IND                                      SNKUP008
00104            FROM TASNHDR                                           SNKUP008
00105           WHERE REC_MTHD_IND = :PC-TEST                           SNKUP008
00106      END-EXEC.                                                    SNKUP008
00107 *                                                                 SNKUP008
00108  EJECT                                                            SNKUP008
00109 *    SQL COMMUNICATION AREA                                       SNKUP008
00110      EXEC SQL                                                     SNKUP008
00111          INCLUDE SQLCA                                            SNKUP008
00112      END-EXEC.                                                    SNKUP008
00113  EJECT                                                            SNKUP008
00114 *    SQL COMMUNICATION AREA 2                                     SNKUP008
00115      COPY SQLCA2.                                                 SNKUP008
00116  EJECT                                                            SNKUP008
00117 ***************************************************************** SNKUP008
00118  PROCEDURE DIVISION.                                              SNKUP008
00119 *                                                                 SNKUP008
00120  A000-MAIN-DRIVER.                                                SNKUP008
00121 *                                                                 SNKUP008
00122      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP008
00123      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP008
00124      MOVE PC-N                   TO PS-TEST-ASN-EOT-SW.           SNKUP008
00125                                                                   SNKUP008
00126      OPEN OUTPUT ASNHDR-DELETE-FILE.                              SNKUP008
00127 *                                                                 SNKUP008
00128      PERFORM F000-OPEN-TEST-ASN-CURSOR.                           SNKUP008
00129 *                                                                 SNKUP008
00130      PERFORM F100-FETCH-TEST-ASNS                                 SNKUP008
00131        UNTIL TEST-ASNS-EOT.                                       SNKUP008
00132 *                                                                 SNKUP008
00133      DISPLAY '******************************************'.        SNKUP008
00134      DISPLAY 'ASNHDR RECORDS WRITTEN: ' PV-ASNHDR-WRITTEN.        SNKUP008
00135      DISPLAY '******************************************'.        SNKUP008
00136 *                                                                 SNKUP008
00137      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP008
00138 *                                                                 SNKUP008
00139      GOBACK.                                                      SNKUP008
00140 *                                                                 SNKUP008
00141  A000-EXIT.  EXIT.                                                SNKUP008
00142  EJECT                                                            SNKUP008
00143 ***************************************************************** SNKUP008
00144 *    THIS PARAGRAPH OPENS THE TEST ASN CURSROR                    SNKUP008
00145 ***************************************************************** SNKUP008
00146  F000-OPEN-TEST-ASN-CURSOR.                                       SNKUP008
00147 *                                                                 SNKUP008
00148      MOVE 'F000'                 TO PGM-LOCATION.                 SNKUP008
00149      MOVE 'F000-OPEN-TEST-ASN-CURSOR '                            SNKUP008
00150                                  TO ABEND-PARAGRAPH.              SNKUP008
00151 *                                                                 SNKUP008
00152      EXEC SQL                                                     SNKUP008
00153          OPEN TEST_ASN_CURSOR                                     SNKUP008
00154      END-EXEC.                                                    SNKUP008
00155 *                                                                 SNKUP008
00156 *!   DISPLAY 'SQLCODE = ' SQLCODE                                 SNKUP008
00157      IF  SQLCODE < ZERO                                           SNKUP008
00158          PERFORM Z998-SQL-ERROR                                   SNKUP008
00159      END-IF.                                                      SNKUP008
00160 *                                                                 SNKUP008
00161  EJECT                                                            SNKUP008
00162 ***************************************************************** SNKUP008
00163 *    THIS PARAGRAPH FETCHES THE TEST STATUS SHIP NOTICES WHICH    SNKUP008
00164 *    HAVE BEEN ADDED TO THE PRODUCTION SHIP NOTICE TABLES.        SNKUP008
00165 ***************************************************************** SNKUP008
00166  F100-FETCH-TEST-ASNS.                                            SNKUP008
00167 *                                                                 SNKUP008
00168      MOVE 'F100'                 TO PGM-LOCATION.                 SNKUP008
00169      MOVE 'F100-FETCH-TEST-ASNS      '                            SNKUP008
00170                                  TO ABEND-PARAGRAPH.              SNKUP008
00171 *                                                                 SNKUP008
00172      EXEC SQL                                                     SNKUP008
00173          FETCH TEST_ASN_CURSOR                                    SNKUP008
00174           INTO :ASNHDR-ASNHDR-ID,                                 SNKUP008
00175                :ASNHDR-REC-MTHD-IND                               SNKUP008
00176      END-EXEC.                                                    SNKUP008
00177 *                                                                 SNKUP008
00178 *!   DISPLAY 'SQLCODE = ' SQLCODE                                 SNKUP008
00179      IF  SQLCODE = ZERO                                           SNKUP008
00180          PERFORM D000-WRITE-TEST-ASNS                             SNKUP008
00181      ELSE                                                         SNKUP008
00182          IF SQLCODE = 100                                         SNKUP008
00183              MOVE PC-Y TO PS-TEST-ASN-EOT-SW                      SNKUP008
00184              PERFORM F200-CLOSE-TEST-ASN-CURSOR                   SNKUP008
00185          ELSE                                                     SNKUP008
00186              PERFORM Z998-SQL-ERROR                               SNKUP008
00187          END-IF                                                   SNKUP008
00188      END-IF.                                                      SNKUP008
00189 *                                                                 SNKUP008
00190  EJECT                                                            SNKUP008
00191 ***************************************************************** SNKUP008
00192 *    THIS PARAGRAPH CLOSES THE TEST ASN CURSROR                   SNKUP008
00193 ***************************************************************** SNKUP008
00194  F200-CLOSE-TEST-ASN-CURSOR.                                      SNKUP008
00195 *                                                                 SNKUP008
00196      MOVE 'F200'                 TO PGM-LOCATION.                 SNKUP008
00197      MOVE 'F200-CLOSE-TEST-ASN-CURSOR'                            SNKUP008
00198                                  TO ABEND-PARAGRAPH.              SNKUP008
00199 *                                                                 SNKUP008
00200      EXEC SQL                                                     SNKUP008
00201          CLOSE TEST_ASN_CURSOR                                    SNKUP008
00202      END-EXEC.                                                    SNKUP008
00203 *                                                                 SNKUP008
00204 *!   DISPLAY 'SQLCODE = ' SQLCODE                                 SNKUP008
00205      IF  SQLCODE < ZERO                                           SNKUP008
00206          PERFORM Z998-SQL-ERROR                                   SNKUP008
00207      END-IF.                                                      SNKUP008
00208 *                                                                 SNKUP008
00209  EJECT                                                            SNKUP008
00210 ***************************************************************** SNKUP008
00211 *    THIS PARAGRAPH WRITES THE TEST STATUS SHIP NOTICES WHICH     SNKUP008
00212 *    HAVE BEEN ADDED TO THE PRODUCTION SHIP NOTICE TABLES TO      SNKUP008
00213 *    A FLAT FILE.                                                 SNKUP008
00214 ***************************************************************** SNKUP008
00215  D000-WRITE-TEST-ASNS.                                            SNKUP008
00216 *                                                                 SNKUP008
00217      MOVE 'D000'                 TO PGM-LOCATION.                 SNKUP008
00218      MOVE 'D000-WRITE-TEST-ASNS     '                             SNKUP008
00219                                  TO ABEND-PARAGRAPH.              SNKUP008
00220 *                                                                 SNKUP008
00221 *    MOVE ASNHDR-OPERCO-NBR      TO SN002O-OPERCO-NBR.            SNKUP008
00222 *    MOVE ASNHDR-EDI-VEND-ID     TO SN002O-EDI-VEND-ID.           SNKUP008
00223 *    MOVE ASNHDR-ASNHDR-NBR      TO SN002O-ASNHDR-NBR.            SNKUP008
00224      MOVE ASNHDR-ASNHDR-ID       TO SN002O-ASNHDR-ID.             SNKUP008
00225                                                                   SNKUP008
00226      WRITE ASNHDR-DELETE-RECORD                                   SNKUP008
00227          FROM SN002O-ASN-DELETE-RECORD.                           SNKUP008
00228                                                                   SNKUP008
00229      ADD PC-1                    TO PV-ASNHDR-WRITTEN.            SNKUP008
00230 *                                                                 SNKUP008
00231  EJECT                                                            SNKUP008
00232 ***************************************************************** SNKUP008
00233 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP008
00234 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP008
00235 ***************************************************************** SNKUP008
00236  Z998-SQL-ERROR.                                                  SNKUP008
00237      DISPLAY 'Z998'.                                              SNKUP008
00238      MOVE SQLCODE                TO SQLCODE2.                     SNKUP008
00239      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP008
00240      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP008
00241      DISPLAY '** PROGRAM          ** = SNKUP008'.                 SNKUP008
00242      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP008
00243      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP008
00244      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP008
00245      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP008
00246      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP008
00247      MOVE +4013                  TO ABEND-CODE.                   SNKUP008
00248      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP008
