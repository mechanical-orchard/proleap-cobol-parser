       IDENTIFICATION DIVISION.                                         00010021
       PROGRAM-ID.    XSKUP006.                                         00020021
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030021
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040021
       DATE-WRITTEN.  09/29/2002.                                       00050021
       DATE-COMPILED.                                                   00060021
      ******************************************************************00070021
      **       VSM ITEM MESSAGE - NUMBER 010 - STYLE RECLASS            00080021
      ******************************************************************00090021
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO UPDATE DEPT, MARJOR    00100022
      ***     CLASS AND SUB CLASS ON TABLE XST_VND_STYL.                00110022
      ******************************************************************00120021
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00130021
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00140021
      ******************************************************************00150021
       ENVIRONMENT DIVISION.                                            00160021
       CONFIGURATION SECTION.                                           00170021
       SOURCE-COMPUTER.  IBM-3090.                                      00180021
       OBJECT-COMPUTER.  IBM-3090.                                      00190021
       DATA DIVISION.                                                   00200021
                                                                        00210021
       WORKING-STORAGE SECTION.                                         00220021
      ******************************************************************00230021
      **PROGRAM SWITCHES                                                00240021
      ******************************************************************00250021
       01  PS-PROGRAM-SWITCHES.                                         00260021
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00270021
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00280021
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00290021
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00300021
               88  PS-ACTION-REVERSE                  VALUE 01.         00310021
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00320021
               88  PS-ACTION-NOTHING                  VALUE 02.         00330021
      ******************************************************************00340021
      **PROGRAM VARIABLES                                               00350021
      ******************************************************************00360021
       01  PC-PROGRAM-COUNTERS.                                         00370021
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00380021
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00390021
      ******************************************************************00400021
      **PROGRAM VARIABLES                                               00410021
      ******************************************************************00420021
       01  PV-PROGRAM-VARIABLES.                                        00430021
           05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACE.      00440021
      ******************************************************************00450021
      **COPYBOOKS                                                       00460021
      ******************************************************************00470021
      ***VSM ITEM COPYBOOK                                              00480021
           COPY IMRD008.                                                00490023
      ***ABEND COPYBOOK                                                 00500021
           COPY DPWS004.                                                00510023
      ***DB2 MESSAGE AREA                                               00520021
           COPY SQLCA2.                                                 00530023
      ***DATE ROUTINE COPYBOOK                                          00540021
           COPY DPWS500.                                                00550023
      ******************************************************************00560021
      **TABLE INCLUSIONS                                                00570021
      ******************************************************************00580021
      ***DB2 COMMUNICATIONS AREA                                        00590021
           EXEC SQL                                                     00600021
                INCLUDE SQLCA                                           00610021
           END-EXEC.                                                    00620021
      ***XST_VND_STYL TABLE                                             00630021
           EXEC SQL                                                     00640021
               INCLUDE XST00013                                         00650021
           END-EXEC.                                                    00660021
                                                                        00670021
       LINKAGE SECTION.                                                 00680021
      ******************************************************************00690021
      **LINKAGE VARIABLES                                               00700021
      ******************************************************************00710021
       01  LV-IMRD008-RECORD               PIC X(450).                  00720021
       01  LV-RETURN-CODE                  PIC S9(04).                  00730021
       01  LV-ACTION-IND                   PIC  X(02).                  00740021
       01  LV-CURRENT-TIMESTAMP            PIC  X(26).                  00750021
                                                                        00760021
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00770021
                                LV-RETURN-CODE,                         00780021
                                LV-ACTION-IND,                          00790021
                                LV-CURRENT-TIMESTAMP.                   00800021
      ******************************************************************00810021
       0000-MAINLINE.                                                   00820021
                                                                        00830021
            PERFORM 1000-INITIALIZATION.                                00840021
            PERFORM 2000-PROCESS-MESSAGE                                00850021
                    UNTIL PS-PROCESS-COMPLETE.                          00860021
            PERFORM 9999-WRAPUP.                                        00870021
                                                                        00880021
       EJECT.                                                           00890021
      ***************************************************************** 00900021
      **MOVES ALL COPYBOOK FIELDS TO THE DCLGEN FIELDS                  00910021
      ***************************************************************** 00920021
       1000-INITIALIZATION.                                             00930021
                                                                        00940021
           MOVE '1000-INITIALIZATION'          TO PV-PARAGRAPH-NAME.    00950021
                                                                        00960021
           INITIALIZE PS-PROCESS-CONTINUE-SW.                           00970021
           INITIALIZE PC-RETRY-COUNTER.                                 00980021
                                                                        00990021
           MOVE LV-IMRD008-RECORD              TO IMRD008-RECORD.       01000021
           MOVE IM008-ITEM-KEY-RMS-STYL-ID     TO XST00013-STYL-ID.     01010021
                                                                        01020021
           MOVE IM008-STYLE-RECL-DEPT-NEW  TO XST00013-DEPT-NBR.        01030021
           MOVE IM008-STYLE-RECL-CLASS-NEW TO XST00013-MAJ-CL-NBR.      01040021
           MOVE IM008-STYLE-RECL-SUBCL-NEW TO XST00013-SUB-CL-NBR.      01050021
121402     MOVE LV-CURRENT-TIMESTAMP       TO XST00013-LAST-UPD-TMST.   01060021
121402     MOVE IM008-CRE-TMST            TO XST00013-SOR-LAST-MSG-TMST.01070021
                                                                        01080021
       EJECT.                                                           01090021
      ***************************************************************** 01100021
      **UPDATES SPECIFIED STYLE ID ON THE VENDOR STYLE TABLE            01110022
      ***************************************************************** 01120021
       2000-PROCESS-MESSAGE.                                            01130021
                                                                        01140021
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01150021
                                                                        01160021
           EXEC SQL                                                     01170021
              UPDATE XST_VND_STYL                                       01180021
                  SET   DEPT_NBR          = :XST00013-DEPT-NBR          01190021
                      , MAJ_CL_NBR        = :XST00013-MAJ-CL-NBR        01200021
                      , SUB_CL_NBR        = :XST00013-SUB-CL-NBR        01210021
                      , LAST_UPD_TMST     = :XST00013-LAST-UPD-TMST     01220021
121402                , SOR_LAST_MSG_TMST = :XST00013-SOR-LAST-MSG-TMST 01230021
                  WHERE STYL_ID       = :XST00013-STYL-ID               01240021
           END-EXEC.                                                    01250021
                                                                        01260021
           EVALUATE TRUE                                                01270021
      ***SUCCESSFUL PROCESSING                                          01280022
              WHEN SQLCODE = ZERO                                       01290021
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01300021
      ***ROW/TABLE LOCK                                                 01310022
              WHEN SQLCODE = -911                                       01320021
              WHEN SQLCODE = -913                                       01330021
                   ADD +1                      TO PC-RETRY-COUNTER      01340021
                   IF  PC-RETRY-COUNTER = PC-RETRY-MAX                  01350021
                       SET PS-PROCESS-COMPLETE TO TRUE                  01360021
                   ELSE                                                 01370021
                       ADD +1                  TO PC-RETRY-COUNTER      01380021
                   END-IF                                               01390021
              WHEN OTHER                                                01400021
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01410021
           END-EVALUATE.                                                01420021
                                                                        01430021
       EJECT.                                                           01440021
      ******************************************************************01450021
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01460021
      ******************************************************************01470021
       9999-WRAPUP.                                                     01480021
                                                                        01490021
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME              01500021
                                                                        01510021
           MOVE SQLCODE               TO LV-RETURN-CODE.                01520021
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01530021
                                                                        01540021
           EXIT PROGRAM.                                                01550021
                                                                        01560021
       EJECT.                                                           01570021
