000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.             SUKRF506.                                        
000600 AUTHOR.                 LARRY SCHLEICHER.                                
000700 INSTALLATION.           KOHLS.                                           
000800 DATE-WRITTEN.           JUNE, 1996.                                      
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200* THIS PROGRAM DISPLAYS THE CARTON ADD SCREEN ON THE LRT (LASER           
001300* RADIO TERMINAL).  IT WILL ACCEPT THE ENTRY OF THE PO NUMBER AND         
001400* STORE NUMBER FOR THE CARTON.                                            
001500******************************************************************        
002800*---------------------------------------------------------------*         
001500*    CHANGES                                                     *        
      *    MADE BY: CHERI PARMLEY                                      *        
      *    DATE:    09/25/2014                                         *        
      *                                                                *        
      *    CHANGES TO PO # FROM 7 TO 8 DIGITS                          *        
      *    TAGGED WITH *CLP 09/25/2014                                 *        
      *                                                                *        
001600*----------------------------------------------------------------*        
263968* CHANGED 11/16/2021    BY JIM HUNTER      WR: CHG0263968                 
263968* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263968* MAKING THE CALL DYNAMIC VS STATIC.                                      
263968*---------------------------------------------------------------*         
001600                                                                          
001700                                                                          
001800******************************************************************        
001900 ENVIRONMENT DIVISION.                                                    
002000******************************************************************        
002100                                                                          
002200******************************************************************        
002300 DATA DIVISION.                                                           
002400******************************************************************        
002500                                                                          
002600******************************************************************        
002700 WORKING-STORAGE SECTION.                                                 
002800******************************************************************        
002900*                                                                         
003000 01  CA-COMMAREA.                                                         
003100*                                                                         
003200*  COMMAREA LAYOUT FOR THE PACK BY STORE AUDIT PROJECT                    
003300*                                                                         
003400     COPY SUWS001.                                                        
003500     05  PV-PROGRAM-DATA.                                                 
      *CLP 09/25/2014                                                           
003600         10  PV-PO.                                                       
003700             15  PV-PO-N                 PIC  9(08).                      
003800         10  PV-STORE.                                                    
003900             15  PV-STORE-N              PIC  9(04).                      
004000                                                                          
004100*                                                                         
004200 01  PC-PROGRAM-CONSTANTS.                                                
004300     05  PC-TRANID                       PIC  X(04)  VALUE 'S506'.        
004400     05  PC-MAX-RETRIES                  PIC S9(04)  VALUE +3             
004500                                                     COMP SYNC.           
004600     05  PC-ROW-NOT-FOUND                PIC S9(04)  VALUE +100           
004700                                                     COMP SYNC.           
004800*    05  PC-COMMAREA-LENGTH              PIC S9(04)  VALUE +311           
004900     05  PV-COMMAREA-LENGTH              PIC S9(04)  VALUE +0             
005000                                                     COMP SYNC.           
005100     05  PC-49                           PIC  X(02)  VALUE '49'.          
005200     05  PC-50                           PIC  X(02)  VALUE '50'.          
005300     05  PC-95                           PIC  X(02)  VALUE '95'.          
005400     05  PC-UG                           PIC  X(02)  VALUE 'UG'.          
005500     05  PC-CURRENT-PROGRAM-NAME         PIC  X(08)  VALUE                
005600         'SUKRF506'.                                                      
005700     05  PC-TRAILER-STOP-PGM             PIC  X(08)  VALUE                
005800         'SUKRF503'.                                                      
005900     05  PC-NOT-FOUND-PGM                PIC  X(08)  VALUE                
006000         'SUKRF505'.                                                      
006100     05  PC-SU-SHIP-CODE-PGM             PIC  X(08)  VALUE                
006200         'SUKRF502'.                                                      
006300     05  PC-DB2-DATABASE-ERROR-MSG       PIC  X(19)  VALUE                
006400         'DB2 ACCESS ERROR   '.                                           
006500     05  PC-UNRECOGNIZED-PF-KEY-MSG      PIC  X(19)  VALUE                
006600         'UNRECOGNIZED PF KEY'.                                           
006700     05  PC-ENTER-THROUGH-MENU-MSG       PIC  X(19)  VALUE                
006800         'ENTER THROUGH MENU '.                                           
006900     05  PC-TRANS-UNAVAILABLE-MSG        PIC  X(19)  VALUE                
007000         'TRANS NOT AVAILABLE'.                                           
007100     05  PC-PLS-ENT-FLD-MSG              PIC  X(19)  VALUE                
007200         'PLEASE ENTER FIELD '.                                           
007300     05  PC-MUST-BE-NUM-MSG              PIC  X(19)  VALUE                
007400         'MUST BE NUMERIC    '.                                           
007500     05  PC-MUST-BE-GT-0-MSG             PIC  X(19)  VALUE                
007600         'MUST BE > ZERO     '.                                           
007700     05  PC-MUST-BE-PBS-MSG              PIC  X(19)  VALUE                
007800         'RCVR NOT PK-BY-STR '.                                           
007900     05  PC-STR-NOT-XCRIBED-MSG          PIC  X(19)  VALUE                
008000         'STR NOT TRANSCRIBED'.                                           
008100     05  PC-CICS-ERR-MSG                 PIC  X(19)  VALUE                
008200         'CICS ACCESS ERROR  '.                                           
008300     05  PC-INVALID-TRIP                 PIC  X(19)  VALUE                
008400         'INVALID TRIP ID    '.                                           
008500     05  PC-TRIP-NOT-ARRIVED             PIC  X(19)  VALUE                
008600         'TRAILER NOT AR     '.                                           
008700     05  PC-INVALID-STORE                PIC  X(19)  VALUE                
008800         'INVALID STORE NUMBR'.                                           
008900     05  PC-STORE-NOT-NUMERIC            PIC  X(19)  VALUE                
009000         'STORE NOT NUMERIC  '.                                           
009100     05  PC-INVALID-PO                   PIC  X(19)  VALUE                
009200         'INVALID PO NUMBER  '.                                           
009300     05  PC-PO-NOT-NUMERIC               PIC  X(19)  VALUE                
009400         'PO NOT NUMERIC     '.                                           
009500     05  PC-DIFFERENT-OPER-UNT           PIC  X(19)  VALUE                
009600         'WRONG DC F2-CONFIRM'.                                           
009700*                                                                         
009800 01  PS-PROGRAM-SWITCHES.                                                 
009900     05  PS-VALID-INPUT-SW               PIC  X(01)  VALUE 'Y'.           
010000         88  PS-VALID-INPUT                          VALUE 'Y'.           
010100         88  PS-INVALID-INPUT                        VALUE 'N'.           
010200     05  PS-TRIP-SW                      PIC  X(01)  VALUE ' '.           
010300         88  PS-VALID-TRIP-ID                        VALUE 'Y'.           
010400         88  PS-TRIP-NOT-ARRIVED                     VALUE 'A'.           
010500         88  PS-INVALID-TRIP                         VALUE 'N'.           
010600     05  PS-INVALID-STORE-SW             PIC  X(01)  VALUE 'N'.           
010700         88  PS-INVALID-STORE                        VALUE 'Y'.           
010800*                                                                         
010900 01  PV-PROGRAM-VARIABLES.                                                
011000     05  PV-MESSAGE-AREA                 PIC  X(19)  VALUE SPACE.         
011100     05  PV-CICS-RESPONSE                PIC S9(09)  VALUE ZEROS          
011200                                                     COMP SYNC.           
011300     05  PV-TRANSFER-PROGRAM             PIC  X(08)  VALUE SPACE.         
011400     05  PV-RECORD-COUNT                 PIC S9(09)  VALUE ZEROS          
011500                                                     COMP SYNC.           
011600     05  PV-COUNT-IND                    PIC S9(04)  VALUE ZEROS          
011700                                                     COMP SYNC.           
011800     05  PV-SELECT-COUNTER               PIC S9(04)  VALUE ZEROS          
011900                                                     COMP SYNC.           
012000     05  PV-WRITE-LENGTH                 PIC  9(04)  VALUE  80            
012100                                                     COMP SYNC.           
012200     05  PV-RBA                          PIC S9(08)  VALUE +0             
012300                                                     COMP SYNC.           
012400     05  PV-STORE-NBR                    PIC  9(04)  VALUE  0.            
012800     05  PV-PO-NBR                       PIC S9(09)  VALUE +0             
012900                                                     COMP SYNC.           
013000     05  PV-CURRENT-DATE                 PIC  X(10).                      
013100*                                                                         
013200                                                                          
013300*                                                                         
013400*  GENERATED DSECT FOR THE SUKM506 MAPSET                                 
013500*                                                                         
013600     COPY SUKM506.                                                        
013700                                                                          
013800*                                                                         
013900*  SENDING/RECEIVING AREA FOR THE NUMERIC EDIT ROUTINE (DPKUT100)         
014000*                                                                         
014100     COPY DPWS010I.                                                       
014200                                                                          
014300*                                                                         
014400*  LAYOUT FOR THE ABEND MODULE COMMUNICATIONS AREA.                       
014500*                                                                         
014600     COPY DPWS013.                                                        
014700                                                                          
014710*                                                                         
014720*  LAYOUT FOR THE LOGISTICS DOMAIN LOCATION MASTER MODULE                 
014730*                                                                         
014740     COPY XLWS071.                                                        
014750                                                                          
014800*                                                                         
014900*  KOHL'S STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST           
015000*                                                                         
015100     COPY DPWS015.                                                        
015200                                                                          
015300*                                                                         
015400*  KOHL'S STANDARD ATTENTION IDENTIFIER LIST                              
015500*                                                                         
015600     COPY DFHAID.                                                         
015700                                                                          
016600/                                                                         
016700*---------------------------------------------------------------*         
016800*    DB2 AREA FOR THE PURCHASE ORDER TABLE                      *         
016900*---------------------------------------------------------------*         
017000     EXEC SQL                                                             
017100          INCLUDE TPURCHS                                                 
017200     END-EXEC.                                                            
017300                                                                          
017400                                                                          
017500*                                                                         
017600*  COMMUNICATIONS AREA FOR DB2                                            
017700*                                                                         
017800     EXEC SQL                                                             
017900         INCLUDE SQLCA                                                    
018000     END-EXEC.                                                            
018100                                                                          
018200                                                                          
018300*                                                                         
018400*****************************************************************         
018500 LINKAGE SECTION.                                                         
018600******************************************************************        
018700*01  DFHCOMMAREA                   PIC  X(311).                           
018800 01  DFHCOMMAREA.                                                         
018900     05  FILLER                       OCCURS 1 TO 4072 TIMES              
019000                                      DEPENDING ON EIBCALEN.              
019100         10  FILLER                   PIC X.                              
019200                                                                          
019300******************************************************************        
019400 PROCEDURE DIVISION.                                                      
019500******************************************************************        
019600 0000-PROGRAM-MAINLINE.                                                   
019700*LMM                                                                      
019800     INITIALIZE CA-COMMAREA                                               
019900     MOVE DFHCOMMAREA TO CA-COMMAREA.                                     
020000                                                                          
020100     EVALUATE TRUE                                                        
020200         WHEN EIBCALEN = ZERO                                             
020300         WHEN SU001-INITIALIZE-SCREEN                                     
020400         WHEN EIBAID = DFHCLEAR                                           
020500             SET SU001-PROCESS-SCREEN TO TRUE                             
020600             PERFORM 1000-INITIALIZE                                      
020700             PERFORM 8000-SEND-MAP                                        
020800             PERFORM 8200-RETURN-TRANID                                   
020900                                                                          
021000         WHEN EIBAID = DFHENTER                                           
021100         WHEN EIBAID = DFHPF2                                             
021200             SET PS-VALID-INPUT TO TRUE                                   
021300             PERFORM 2000-PROCESS-SCREEN-FIELDS                           
021400             MOVE 'N' TO SU001-MISDIRECT-IND                              
021500                                                                          
021600             IF PS-VALID-INPUT AND                                        
021700                 EIBAID = DFHPF2 AND                                      
021800                (SU001-STORE-OPER-UNT-ID = SU001-OPER-UNT-ID OR           
021900                 SU001-DIFFERENT-OPER-UNT)                                
022000                 SET SU001-ADD-NOT-FOUND TO TRUE                          
022100                 MOVE PC-SU-SHIP-CODE-PGM                                 
022200                                       TO PV-TRANSFER-PROGRAM             
022300                 PERFORM 4000-XCTL-LRT-MENU                               
022400             ELSE                                                         
022500                 IF PS-VALID-INPUT AND                                    
022600                    SU001-STORE-OPER-UNT-ID NOT =                         
022700                          SU001-OPER-UNT-ID                               
022800                    MOVE PC-DIFFERENT-OPER-UNT TO AMSGLNO                 
022900                    SET SU001-DIFFERENT-OPER-UNT TO TRUE                  
023000                 END-IF                                                   
023100                 IF PS-VALID-INPUT                                        
023200                     MOVE -1     TO APOL                                  
023300                 END-IF                                                   
023400                 PERFORM 8100-SEND-DATA-ONLY                              
023500                 PERFORM 8200-RETURN-TRANID                               
023600             END-IF                                                       
023700                                                                          
023800         WHEN EIBAID = DFHPF4                                             
023900             SET SU001-ADD-NOT-FOUND TO TRUE                              
024000             MOVE 'Y' TO SU001-MISDIRECT-IND                              
024100             MOVE PC-SU-SHIP-CODE-PGM                                     
024200                                   TO PV-TRANSFER-PROGRAM                 
024300             PERFORM 4000-XCTL-LRT-MENU                                   
024400                                                                          
024500         WHEN OTHER                                                       
024600             MOVE -1                         TO APOL                      
024700             MOVE PC-UNRECOGNIZED-PF-KEY-MSG TO AMSGLNO                   
024800             PERFORM 8100-SEND-DATA-ONLY                                  
024900             PERFORM 8200-RETURN-TRANID                                   
025000     END-EVALUATE.                                                        
025100                                                                          
025200     GOBACK.                                                              
025300                                                                          
025400*                                                                         
025500 1000-INITIALIZE.                                                         
025600                                                                          
025700     MOVE SU001-CARTON TO ACARTNO.                                        
025800     INITIALIZE PV-PROGRAM-DATA.                                          
025900     MOVE SPACES TO APOO                                                  
026000                    ASTRO.                                                
026100     MOVE -1     TO APOL.                                                 
026200                                                                          
026300*                                                                         
026400 2000-PROCESS-SCREEN-FIELDS.                                              
026500                                                                          
026600     PERFORM 2100-RECEIVE-MAP.                                            
026700     PERFORM 2200-EDIT-INPUT.                                             
026800                                                                          
026900*                                                                         
027000 2100-RECEIVE-MAP.                                                        
027100                                                                          
027200     EXEC CICS                                                            
027300         RECEIVE MAP    ('SU506A')                                        
027400                 MAPSET ('SUKM506')                                       
027500                 RESP   (PV-CICS-RESPONSE)                                
027600     END-EXEC.                                                            
027700                                                                          
027800     EVALUATE TRUE                                                        
027900         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
028000             PERFORM 2110-RECEIVE-SCREEN-FIELDS                           
028100         WHEN PV-CICS-RESPONSE = DFHRESP (MAPFAIL)                        
028200             CONTINUE                                                     
028300         WHEN OTHER                                                       
028400            MOVE '2100-RECEIVE-MAP'      TO DP013-PARAGRAPH               
028500            MOVE 'UNABLE TO RECEIVE MAP' TO DP013-MESSAGE-TEXT (1)        
028600            SET DP013-LOGIC-ABEND        TO TRUE                          
028700            SET DP013-NO-ROLLBACK        TO TRUE                          
028800            PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                        
028900     END-EVALUATE.                                                        
029000                                                                          
029100*                                                                         
029200 2110-RECEIVE-SCREEN-FIELDS.                                              
029300                                                                          
029400     IF  ASTRL > ZERO                                                     
029500         MOVE ASTRI TO PV-STORE                                           
029600     ELSE                                                                 
029700         IF ASTRA = DP015-ERASE-EOF                                       
029800             MOVE SPACES TO PV-STORE                                      
029900         END-IF                                                           
030000     END-IF.                                                              
030100                                                                          
030200     IF  APOL > ZERO                                                      
030300         MOVE APOI TO PV-PO                                               
030400     ELSE                                                                 
030500         IF APOA = DP015-ERASE-EOF                                        
030600             MOVE SPACES TO PV-PO                                         
030700         END-IF                                                           
030800     END-IF.                                                              
030900                                                                          
031000*                                                                         
031100 2200-EDIT-INPUT.                                                         
031200                                                                          
031300     MOVE DP015-UNP-NUM-NOR-OFF  TO ASTRA                                 
031400                                    APOA                                  
031500     MOVE 0                      TO ASTRL                                 
031600                                    APOL                                  
031700     MOVE SPACES                 TO AMSGLNO                               
031800     PERFORM 2230-EDIT-STORE.                                             
031900     PERFORM 2250-EDIT-PO.                                                
032000                                                                          
032100*                                                                         
032200 2230-EDIT-STORE.                                                         
032300     MOVE PV-STORE             TO DP010I-UNEDITED-FIELD.                  
032400     MOVE LENGTH OF PV-STORE   TO DP010I-MAXIMUM-DIGITS.                  
032500     MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS.                
032600     SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE.                             
263968     CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
263968          USING DP010I-NUMERIC-EDIT-AREA.                                 
032800     IF  DP010I-ERROR-DETECTED                                            
032900         SET PS-INVALID-INPUT        TO TRUE                              
033000         MOVE DP015-UNP-ALP-BRT-MDT  TO ASTRA                             
033100         MOVE -1                     TO ASTRL                             
033200         MOVE PC-STORE-NOT-NUMERIC   TO AMSGLNO                           
033300     ELSE                                                                 
033400         MOVE DP010I-NUMERIC-FIELD TO PV-STORE-NBR                        
033500         PERFORM 2240-SELECT-LOCATION                                     
033600         IF PS-INVALID-STORE                                              
033700             SET PS-INVALID-INPUT        TO TRUE                          
033800             MOVE DP015-UNP-ALP-BRT-MDT  TO ASTRA                         
033900             MOVE -1                     TO ASTRL                         
034000             MOVE PC-INVALID-STORE       TO AMSGLNO                       
034100         END-IF                                                           
034200     END-IF.                                                              
034300                                                                          
034400 2240-SELECT-LOCATION.                                                    
034401                                                                          
034410     INITIALIZE XL071-COMMAREA-REC.                                       
034420     MOVE PV-STORE-NBR           TO XL071-LOCATION-NUMBER.                
034430                                                                          
034440     CALL XL071-LOCATION-MODULE USING                                     
034450                                    DFHEIBLK                              
034460                                    XL071-COMMAREA                        
034470                                    XL071-COMMAREA-REC.                   
034480     EVALUATE TRUE                                                        
034490         WHEN XL071-NO-ERRORS                                             
034491              MOVE PV-STORE-NBR TO SU001-STORE                            
034492                                                                          
034493              EXEC SQL                                                    
034494                  SET :PV-CURRENT-DATE = CURRENT DATE                     
034495              END-EXEC                                                    
034496                                                                          
034497              IF  PV-CURRENT-DATE  > XL071-ASGMT-EFF-BEG-DTE              
034498              AND PV-CURRENT-DATE  < XL071-ASGMT-EFF-STP-DTE              
034499                  MOVE XL071-ASGMT-DC-NBR                                 
034500                    TO SU001-STORE-OPER-UNT-ID                            
034502              ELSE                                                        
034508                  SET PS-INVALID-STORE TO TRUE                            
034510              END-IF                                                      
034518         WHEN XL071-LOC-NOT-FOUND                                         
034519              SET PS-INVALID-STORE TO TRUE                                
034520         WHEN XL071-BAD-DB2-RETURN                                        
034521              MOVE '2240-SELECT-LOCATION'                                 
034522                              TO DP013-PARAGRAPH                          
034523              MOVE 'CALL XLKUT071 '                                       
034524                              TO DP013-MESSAGE-TEXT(1)                    
034525              STRING 'XLKUT071 TABLE '    DELIMITED BY SIZE               
034526                     'WAS: '              DELIMITED BY SIZE               
034527                     XL071-DB2-TABLE      DELIMITED BY SIZE               
034528                                     INTO DP013-MESSAGE-TEXT (2)          
034529              STRING 'LOCATION NUMBER '   DELIMITED BY SIZE               
034530                     'WAS: '              DELIMITED BY SIZE               
034531                     PV-STORE             DELIMITED BY SIZE               
034532                                     INTO DP013-MESSAGE-TEXT (3)          
034533              MOVE XL071-SQLCA         TO DP013-SQLCA                     
034535              MOVE 'LOCATION TABLES'   TO DP013-DB2-TABLE-NAME (1)        
034538              SET DP013-DB2-ABEND                                         
034539                              TO TRUE                                     
034540              SET DP013-NO-ROLLBACK                                       
034541                              TO TRUE                                     
034542              SET DP013-XCTL-DISPLAY-RESTART                              
034543                              TO TRUE                                     
034544              PERFORM DP014-0000-PROCESS-ABEND                            
034546     END-EVALUATE.                                                        
041200*                                                                         
041300 2250-EDIT-PO.                                                            
041400     MOVE PV-PO                TO DP010I-UNEDITED-FIELD.                  
041500     MOVE LENGTH OF PV-PO      TO DP010I-MAXIMUM-DIGITS.                  
041600     MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS.                
041700     SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE.                             
263968     CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
263968          USING DP010I-NUMERIC-EDIT-AREA.                                 
041900     IF  DP010I-ERROR-DETECTED                                            
042000         SET PS-INVALID-INPUT        TO TRUE                              
042100         MOVE DP015-UNP-ALP-BRT-MDT  TO APOA                              
042200         MOVE -1                     TO APOL                              
042300         MOVE PC-PO-NOT-NUMERIC      TO AMSGLNO                           
042400     ELSE                                                                 
042500         MOVE DP010I-NUMERIC-FIELD TO PV-PO-NBR                           
042600         PERFORM 2260-SELECT-PO                                           
042700     END-IF.                                                              
042800                                                                          
042900 2260-SELECT-PO.                                                          
043000     EXEC SQL                                                             
043100          SELECT PO_NBR                                                   
043200          INTO   :PURCHS-PO-NBR                                           
043300          FROM  TPURCHS                                                   
043400          WHERE PO_NBR         = :PV-PO-NBR                               
043500            AND VER_STATUS_CDE = 'A'                                      
043600     END-EXEC                                                             
043700                                                                          
043800     EVALUATE TRUE                                                        
043900         WHEN SQLCODE = +0                                                
044000         WHEN SQLCODE = +100                                              
044100             CONTINUE                                                     
044200                                                                          
044300         WHEN SQLWARN0 NOT = SPACES                                       
044400         WHEN SQLCODE NOT EQUAL ZERO                                      
044500             MOVE '2260-SELECT-PO'                                        
044600                             TO DP013-PARAGRAPH                           
044700             MOVE 'CHECK VALID PO'                                        
044800                             TO DP013-MESSAGE-TEXT(1)                     
044900             MOVE PV-STORE                                                
045000                             TO DP013-MESSAGE-TEXT(2)                     
045100             MOVE SQLCA      TO DP013-SQLCA                               
045200             MOVE 'TPURCHS'  TO DP013-DB2-TABLE-NAME(1)                   
045300             SET DP013-DB2-ABEND                                          
045400                             TO TRUE                                      
045500             SET DP013-NO-ROLLBACK                                        
045600                             TO TRUE                                      
045700             SET DP013-XCTL-DISPLAY-RESTART                               
045800                             TO TRUE                                      
045900             PERFORM DP014-0000-PROCESS-ABEND                             
046000                                                                          
046100     END-EVALUATE.                                                        
046200                                                                          
046300     EVALUATE TRUE                                                        
046400         WHEN SQLCODE = +0                                                
046500             MOVE PV-PO-NBR TO SU001-PO                                   
046600         WHEN SQLCODE = +100                                              
046700             SET PS-INVALID-INPUT        TO TRUE                          
046800             MOVE DP015-UNP-ALP-BRT-MDT  TO APOA                          
046900             MOVE -1                     TO APOL                          
047000             MOVE PC-INVALID-PO          TO AMSGLNO                       
047100     END-EVALUATE.                                                        
047200                                                                          
047300                                                                          
047400*                                                                         
047500 4000-XCTL-LRT-MENU.                                                      
047600     SET SU001-INITIALIZE-SCREEN  TO TRUE.                                
047700     MOVE PC-CURRENT-PROGRAM-NAME TO SU001-LAST-PROGRAM.                  
047800*LMM                                                                      
047900     MOVE LENGTH OF CA-COMMAREA    TO PV-COMMAREA-LENGTH                  
048000     EXEC CICS                                                            
048100         XCTL  PROGRAM   (PV-TRANSFER-PROGRAM)                            
048200               COMMAREA  (CA-COMMAREA)                                    
048300*LMM           LENGTH    (PC-COMMAREA-LENGTH)                             
048400               LENGTH    (PV-COMMAREA-LENGTH)                             
048500               RESP      (PV-CICS-RESPONSE)                               
048600     END-EXEC.                                                            
048700                                                                          
048800     EVALUATE TRUE                                                        
048900         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
049000             CONTINUE                                                     
049100         WHEN OTHER                                                       
049200             MOVE '4000-XCTL-TO-LRT-MENU'                                 
049300                 TO DP013-PARAGRAPH                                       
049400             MOVE 'UNABLE TO XCTL TO PROGRAM: '                           
049500                 TO DP013-MESSAGE-TEXT (1)                                
049600             MOVE PV-TRANSFER-PROGRAM                                     
049700                 TO DP013-MESSAGE-TEXT (2)                                
049800             SET DP013-CICS-ABEND  TO TRUE                                
049900             SET DP013-NO-ROLLBACK TO TRUE                                
050000             PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                       
050100             MOVE PC-TRANS-UNAVAILABLE-MSG TO AMSGLNO                     
050200             SET SU001-PROCESS-SCREEN TO TRUE                             
050300             PERFORM 8000-SEND-MAP                                        
050400             PERFORM 8200-RETURN-TRANID                                   
050500     END-EVALUATE.                                                        
050600                                                                          
050700*                                                                         
050800 8000-SEND-MAP.                                                           
050900                                                                          
051000     EXEC CICS                                                            
051100         SEND MAP       ('SU506A')                                        
051200              MAPSET    ('SUKM506')                                       
051300              ERASE                                                       
051400              CURSOR                                                      
051500              RESP      (PV-CICS-RESPONSE)                                
051600     END-EXEC.                                                            
051700                                                                          
051800     EVALUATE TRUE                                                        
051900         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
052000             CONTINUE                                                     
052100         WHEN OTHER                                                       
052200             MOVE '8000-SEND-MAP'      TO DP013-PARAGRAPH                 
052300             MOVE 'UNABLE TO SEND MAP' TO DP013-MESSAGE-TEXT (1)          
052400             SET DP013-CICS-ABEND      TO TRUE                            
052500             SET DP013-NO-ROLLBACK     TO TRUE                            
052600             PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                       
052700     END-EVALUATE.                                                        
052800                                                                          
052900*                                                                         
053000 8100-SEND-DATA-ONLY.                                                     
053100                                                                          
053200     EXEC CICS                                                            
053300         SEND MAP       ('SU506A')                                        
053400              MAPSET    ('SUKM506')                                       
053500              CURSOR                                                      
053600              DATAONLY                                                    
053700              RESP      (PV-CICS-RESPONSE)                                
053800     END-EXEC.                                                            
053900                                                                          
054000     EVALUATE TRUE                                                        
054100         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
054200             CONTINUE                                                     
054300         WHEN OTHER                                                       
054400             MOVE '8100-SEND-DATA-ONLY' TO DP013-PARAGRAPH                
054500             MOVE 'UNABLE TO SEND DATA' TO DP013-MESSAGE-TEXT (1)         
054600             SET DP013-CICS-ABEND       TO TRUE                           
054700             SET DP013-NO-ROLLBACK      TO TRUE                           
054800             PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                       
054900     END-EVALUATE.                                                        
055000                                                                          
055100*                                                                         
055200 8200-RETURN-TRANID.                                                      
055300                                                                          
055400*LMM                                                                      
055500     MOVE LENGTH OF CA-COMMAREA     TO PV-COMMAREA-LENGTH                 
055600     EXEC CICS                                                            
055700         RETURN TRANSID  (PC-TRANID)                                      
055800                COMMAREA (CA-COMMAREA)                                    
055900*LMM            LENGTH   (PC-COMMAREA-LENGTH)                             
056000                LENGTH   (PV-COMMAREA-LENGTH)                             
056100                RESP     (PV-CICS-RESPONSE)                               
056200     END-EXEC.                                                            
056300                                                                          
056400     EVALUATE TRUE                                                        
056500         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
056600             CONTINUE                                                     
056700         WHEN OTHER                                                       
056800             MOVE '8200-RETURN-TRANIND' TO DP013-PARAGRAPH                
056900             MOVE 'UNABLE TO RETURN TO TRANID'                            
057000                 TO DP013-MESSAGE-TEXT (1)                                
057100             SET DP013-CICS-ABEND       TO TRUE                           
057200             SET DP013-NO-ROLLBACK      TO TRUE                           
057300             PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                       
057400     END-EVALUATE.                                                        
057500                                                                          
057600*                                                                         
057700 8300-RETURN.                                                             
057800                                                                          
057900     EXEC CICS                                                            
058000         SEND FROM (PV-MESSAGE-AREA)                                      
058100              LENGTH (19)                                                 
058200              ERASE                                                       
058300     END-EXEC.                                                            
058400                                                                          
058500     EXEC CICS                                                            
058600         RETURN                                                           
058700     END-EXEC.                                                            
058800                                                                          
058900*                                                                         
059000 9999-WRITE-MESSAGE-TO-CICS-LOG.                                          
059100                                                                          
059200     MOVE PC-CURRENT-PROGRAM-NAME  TO DP013-PROGRAM.                      
059300     MOVE DFHEIBLK                 TO DP013-DFHEIBLK.                     
059400     INITIALIZE DP013-SRC-TASK-NUMBER                                     
059500                DP013-STACK.                                              
059600     SET DP013-LINK-RETURN TO TRUE.                                       
059700                                                                          
059800     EXEC CICS                                                            
059900         LINK                                                             
060000             PROGRAM   (DP013-NAME)                                       
060100             COMMAREA  (DP013-PARAMETERS)                                 
060200             LENGTH    (DP013-LENGTH)                                     
060300     END-EXEC.                                                            
060400                                                                          
060500*                                                                         
060600*----------------------------------------------------------------*      25
060700*    ABEND PROCESSOR MODULE                                      *      25
060800*----------------------------------------------------------------*      25
060900                                                                        25
061000     COPY DPPD014.                                                      25
061100                                                                        25
