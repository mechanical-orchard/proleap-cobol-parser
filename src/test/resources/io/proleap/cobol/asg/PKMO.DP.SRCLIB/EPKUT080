      ****************************************************************  00010082
       IDENTIFICATION DIVISION.                                         00020082
      ****************************************************************  00030082
                                                                        00040082
       PROGRAM-ID.         EPKUT080.                                    00050082
                                                                        00060082
       AUTHOR.             BRYAN BUKOWSKI.                              00070082
                                                                        00080082
       INSTALLATION.       KOHLS DEPARTMENT STORES.                     00090082
                                                                        00100082
       DATE-WRITTEN.       OCTOBER 2009.                                00110082
                                                                        00120082
       DATE-COMPILED.                                                   00130082
                                                                        00140082
      ******************************************************************00150082
      *                                                                *00160082
      *     THIS PROGRAM CREATES FOUR DAILY CALL BOX EXTRACT FILE       00170082
      *     SPLIT UP BY TERRITORY NUMBER.  IT WILL ALSO CREATE A        00180082
      *     SUMMARY FILE WITH WEEK TO DATE, MONTH TO DATE AND YEAR      00190082
      *     TO DATE VALUES FOR EACH STORE AREA.                         00200082
      *                                                                *00210082
      *     SELECT EPT_CALL_BOX AND EXTRACT ALL FIELDS.                *00220082
      *                                                                *00230082
      *     THIS NEW FILE WILL BE DOWNLOADED TO THE LAN.               *00240082
      *                                                                *00250082
      *     TO ADD A NEW BUTTON YOU WILL NEED TO ADD THE CODE TO THE   *00260082
      *     EPCC707 CONTROL CARD.  YOU WILL ALSO NEED TO DETERMINE     *00270082
      *     WHAT SECTION THE NEW CODE BELONGS IN, OR IF IT NEEDS       *00280082
      *     A NEW CATEGORY.  SUMMARY TOTALS WILL NEED TO BE UPDATED    *00290082
      *     FOR THE NEW BUTTON.                                        *00300082
      *                                                                *00310082
      *----------------------------------------------------------       00320082
      * HISTORY LOG:                                                    00330082
      *----------------------------------------------------------       00340082
022210* DATE: 2/22/2010                                                 00350082
022210* WR/IR#:INC000001158117                                          00360082
022210* PROGRAMMER: BRYAN BUKOWSKI                                      00370082
022210* DESCRIPTION: INITIALIZE THE MESSAGE CALL-BOX-MSG-LOC-TABLE      00380082
022210*  FIELD CB-TBL-ADDRESS TO ALL 9'S TO ENABLE THE SEARCH TO        00390082
022210*  FIND ENTRIES AT THE END OF THE TABLE.                          00400082
      *----------------------------------------------------------       00410082
FL1095* DATE: 6/02/2010                                                 00420082
FL1095* WR/IR#: WR38172                                                 00430085
FL1095* PROGRAMMER: KAREN GIVENS (USE 'FL1095' FOR ALL CHANGES)         00440082
FL1095* DESCRIPTION: ADD A NEW CALL BOX BUTTON AND MESSAGE FOR A        00450082
FL1095* 'CREDIT' FUNCTION.                                              00460082
      *----------------------------------------------------------       00470082
      ******************************************************************00480082
                                                                        00490082
      ****************************************************************  00500082
       ENVIRONMENT DIVISION.                                            00510082
      ****************************************************************  00520082
                                                                        00530082
       CONFIGURATION SECTION.                                           00540082
                                                                        00550082
       SOURCE-COMPUTER.    IBM-3090.                                    00560082
                                                                        00570082
       OBJECT-COMPUTER.    IBM-3090.                                    00580082
                                                                        00590082
       INPUT-OUTPUT SECTION.                                            00600082
                                                                        00610082
       FILE-CONTROL.                                                    00620082
                                                                        00630082
           SELECT CURRENT-DATE                                          00640082
               ASSIGN TO INP01.                                         00650082
                                                                        00660082
           SELECT MESSAGE-ID                                            00670082
               ASSIGN TO INP02.                                         00680082
                                                                        00690082
           SELECT TERRITORY-1-EXTRACT-FILE                              00700082
               ASSIGN TO OUT01.                                         00710082
                                                                        00720082
           SELECT TERRITORY-2-EXTRACT-FILE                              00730082
               ASSIGN TO OUT02.                                         00740082
                                                                        00750082
           SELECT TERRITORY-3-EXTRACT-FILE                              00760082
               ASSIGN TO OUT03.                                         00770082
                                                                        00780082
           SELECT TERRITORY-4-EXTRACT-FILE                              00790082
               ASSIGN TO OUT04.                                         00800082
                                                                        00810082
           SELECT SUMMARY-FILE                                          00820082
               ASSIGN TO OUT05.                                         00830082
                                                                        00840082
      ****************************************************************  00850082
       DATA DIVISION.                                                   00860082
      ****************************************************************  00870082
                                                                        00880082
       FILE SECTION.                                                    00890082
                                                                        00900082
       FD  CURRENT-DATE                                                 00910082
           RECORDING MODE IS F                                          00920082
           RECORD CONTAINS 80 CHARACTERS                                00930082
           LABEL RECORDS ARE STANDARD                                   00940082
           BLOCK CONTAINS 0 RECORDS.                                    00950082
                                                                        00960082
       01  CURRENT-DATE-INPUT-RECORD  PIC X(80).                        00970082
                                                                        00980082
       FD  MESSAGE-ID                                                   00990082
           RECORDING MODE IS F                                          01000082
           RECORD CONTAINS 80 CHARACTERS                                01010082
           LABEL RECORDS ARE STANDARD                                   01020082
           BLOCK CONTAINS 0 RECORDS.                                    01030082
                                                                        01040082
       01  MESSAGE-ID-INPUT-RECORD    PIC X(80).                        01050082
                                                                        01060082
       FD  TERRITORY-1-EXTRACT-FILE                                     01070082
           RECORDING MODE IS F                                          01080082
           RECORD CONTAINS 100 CHARACTERS                               01090082
           LABEL RECORDS ARE STANDARD                                   01100082
           BLOCK CONTAINS 0  RECORDS.                                   01110082
                                                                        01120082
       01  TERRITORY-1-EXTRACT-RECORD   PIC X(100).                     01130082
                                                                        01140082
       FD  TERRITORY-2-EXTRACT-FILE                                     01150082
           RECORDING MODE IS F                                          01160082
           RECORD CONTAINS 100 CHARACTERS                               01170082
           LABEL RECORDS ARE STANDARD                                   01180082
           BLOCK CONTAINS 0  RECORDS.                                   01190082
                                                                        01200082
       01  TERRITORY-2-EXTRACT-RECORD  PIC X(100).                      01210082
                                                                        01220082
       FD  TERRITORY-3-EXTRACT-FILE                                     01230082
           RECORDING MODE IS F                                          01240082
           RECORD CONTAINS 100 CHARACTERS                               01250082
           LABEL RECORDS ARE STANDARD                                   01260082
           BLOCK CONTAINS 0  RECORDS.                                   01270082
                                                                        01280082
       01  TERRITORY-3-EXTRACT-RECORD  PIC X(100).                      01290082
                                                                        01300082
       FD  TERRITORY-4-EXTRACT-FILE                                     01310082
           RECORDING MODE IS F                                          01320082
           RECORD CONTAINS 100 CHARACTERS                               01330082
           LABEL RECORDS ARE STANDARD                                   01340082
           BLOCK CONTAINS 0  RECORDS.                                   01350082
                                                                        01360082
       01  TERRITORY-4-EXTRACT-RECORD  PIC X(100).                      01370082
                                                                        01380082
       FD  SUMMARY-FILE                                                 01390082
           RECORDING MODE IS F                                          01400082
           RECORD CONTAINS 100 CHARACTERS                               01410082
           LABEL RECORDS ARE STANDARD                                   01420082
           BLOCK CONTAINS 0  RECORDS.                                   01430082
                                                                        01440082
       01  SUMMARY-RECORD              PIC X(100).                      01450082
                                                                        01460082
      ****************************************************************  01470082
       WORKING-STORAGE SECTION.                                         01480082
      ****************************************************************  01490082
                                                                        01500082
       01  AC-ABEND-CODE               PIC S9(04) VALUE +0              01510082
                                                          COMP          01520082
                                                          SYNC.         01530082
                                                                        01540082
           88  AC-INVALID-CONTROL-CARD                    VALUE +4003.  01550082
           88  AC-DB2-ERROR                               VALUE +4013.  01560082
           88  AC-CALENDAR-ROUTINE-ERROR                  VALUE +4019.  01570082
                                                                        01580082
      ****************************************************************  01590082
      *   CALL BOX EXTRACT RECORD LAYOUT - OUTPUT                       01600082
      ****************************************************************  01610082
       01  CALL-BOX-EXTRACT.                                            01620082
           05  CB-EXTRACT-DATE         PIC  X(10)    VALUE SPACES.      01630082
           05  FILLER                  PIC  X(1)     VALUE ','.         01640082
           05  CB-EXTRACT-STORE-NM     PIC  X(4)     VALUE SPACES.      01650082
           05  FILLER                  PIC  X(1)     VALUE ','.         01660082
           05  CB-EXTRACT-DEV-ID       PIC  X(4)     VALUE SPACES.      01670082
           05  FILLER                  PIC  X(1)     VALUE ','.         01680082
           05  CB-EXTRACT-MSG-ID       PIC  X(4)     VALUE SPACES.      01690082
           05  FILLER                  PIC  X(1)     VALUE ','.         01700082
           05  CB-EXTRACT-MSG-LOC      PIC  X(28)    VALUE SPACES.      01710082
           05  FILLER                  PIC  X(1)     VALUE ','.         01720082
           05  CB-EXTRACT-MSG          PIC  X(33)    VALUE SPACES.      01730082
           05  FILLER                  PIC  X(1)     VALUE ','.         01740082
           05  CB-EXTRACT-TIME         PIC  X(8)     VALUE SPACES.      01750082
                                                                        01760082
      ****************************************************************  01770082
      *  LAYOUT FOR THE PROCESS DATE INPUT FILE - INPUT                 01780082
      ****************************************************************  01790082
       01  CALL-BOX-CONTROL-AREA.                                       01800082
           05  PROCESS-CONTROL-DATE.                                    01810082
               10  PROCESS-DATE-CC     PIC XX.                          01820082
               10  PROCESS-DATE-YY     PIC XX.                          01830082
               10  FILLER              PIC X VALUE '-'.                 01840082
               10  PROCESS-DATE-MM     PIC XX.                          01850082
               10  FILLER              PIC X VALUE '-'.                 01860082
               10  PROCESS-DATE-DD     PIC XX.                          01870082
           05  FILLER                  PIC X(70)    VALUE SPACES.       01880082
                                                                        01890082
      ****************************************************************  01900082
      *  LAYOUT FOR THE MESSAGE/LOCATION INFORMATION - INPUT            01910082
      ****************************************************************  01920082
       01  MESSAGE-ID-AREA.                                             01930082
           05  MESSAGE-ID-X            PIC X(4).                        01940082
           05  MESSAGE-LOCATION        PIC X(28).                       01950082
           05  MESSAGE-MESSAGE         PIC X(33).                       01960082
           05  FILLER                  PIC X(15).                       01970082
                                                                        01980082
      ****************************************************************  01990082
      *  SUMMARY FILE FORMAT - OUTPUT                                   02000082
      ****************************************************************  02010082
       01  SUMMARY-VALUES-EXTRACT.                                      02020082
           05  SUMMARY-NAME            PIC X(25).                       02030082
           05  FILLER                  PIC X(1)   VALUE ','.            02040082
           05  SUMMARY-DAY             PIC X(10).                       02050082
           05  FILLER                  PIC X(1)   VALUE ','.            02060082
           05  SUMMARY-WEEK            PIC X(10).                       02070082
           05  FILLER                  PIC X(1)   VALUE ','.            02080082
           05  SUMMARY-MONTH           PIC X(10).                       02090082
           05  FILLER                  PIC X(1)   VALUE ','.            02100082
           05  SUMMARY-YEAR            PIC X(10).                       02110082
           05  FILLER                  PIC X(64) VALUE SPACES.          02120082
                                                                        02130082
       01  PV-PROGRAM-VARIABLES.                                        02140082
           05  PV-CALL-BOX-CURSOR-COUNT PIC S9(7) VALUE +0 COMP-3.      02150082
           05  PV-CALL-BOX-WTD         PIC  X(10)    VALUE SPACES.      02160082
           05  PV-CALL-BOX-MTD         PIC  X(10)    VALUE SPACES.      02170082
           05  PV-CALL-BOX-YTD         PIC  X(10)    VALUE SPACES.      02180082
           05  PV-DATE-EDIT-MDY.                                        02190082
               10  PV-DATE-EDIT-MDY-MM PIC XX.                          02200082
               10  FILLER              PIC X VALUE '-'.                 02210082
               10  PV-DATE-EDIT-MDY-DD PIC XX.                          02220082
               10  FILLER              PIC X VALUE '-'.                 02230082
               10  PV-DATE-EDIT-MDY-CC PIC XX.                          02240082
               10  PV-DATE-EDIT-MDY-YY PIC XX.                          02250082
           05  PV-DATE-EDIT-YMD.                                        02260082
               10  PV-DATE-EDIT-YMD-CC PIC XX.                          02270082
               10  PV-DATE-EDIT-YMD-YY PIC XX.                          02280082
               10  FILLER              PIC X VALUE '-'.                 02290082
               10  PV-DATE-EDIT-YMD-MM PIC XX.                          02300082
               10  FILLER              PIC X VALUE '-'.                 02310082
               10  PV-DATE-EDIT-YMD-DD PIC XX.                          02320082
           05  PV-DB2-OPERATION        PIC X(30)  VALUE SPACES.         02330082
           05  PV-EDIT-MASK            PIC Z,ZZZ,ZZ9.                   02340082
           05  PV-END-DATE             PIC X(10)  VALUE SPACES.         02350082
           05  PV-LAST-STORE           PIC X(04)  VALUE SPACES.         02360082
           05  PV-MSG-ID-COUNT         PIC 999 VALUE 1.                 02370082
           05  PV-MSG-ID-START         PIC S9(4) COMP.                  02380082
           05  PV-MSG-ID-FINISH        PIC S9(4) COMP.                  02390082
           05  PV-MSG-BTN-ID           PIC S9(1) COMP.                  02400082
           05  PV-OPERATION-MSG-1      PIC X(40)  VALUE SPACES.         02410082
           05  PV-OPERATION-MSG-2      PIC X(40)  VALUE SPACES.         02420082
           05  PV-OTHER-TOTALS-DAY     PIC S9(9) COMP.                  02430082
           05  PV-OTHER-TOTALS-WTD     PIC S9(9) COMP.                  02440082
           05  PV-OTHER-TOTALS-MTD     PIC S9(9) COMP.                  02450082
           05  PV-OTHER-TOTALS-YTD     PIC S9(9) COMP.                  02460082
           05  PV-PARAGRAPH-NAME       PIC X(30)  VALUE SPACES.         02470082
           05  PV-POS-SUMM-COUNT       PIC S9(9) COMP.                  02480082
           05  PV-POS-SUMM-BUTTON      PIC S9(1) COMP.                  02490082
           05  PV-RECORDS-WRITTEN      PIC S9(7)  VALUE +0 COMP-3.      02500082
           05  PV-RECORDS-WRITTEN-T1   PIC S9(7)  VALUE +0 COMP-3.      02510082
           05  PV-RECORDS-WRITTEN-T2   PIC S9(7)  VALUE +0 COMP-3.      02520082
           05  PV-RECORDS-WRITTEN-T3   PIC S9(7)  VALUE +0 COMP-3.      02530082
           05  PV-RECORDS-WRITTEN-T4   PIC S9(7)  VALUE +0 COMP-3.      02540082
           05  PV-RUNNING-TOTALS-DAY   PIC S9(9) COMP.                  02550082
           05  PV-RUNNING-TOTALS-WTD   PIC S9(9) COMP.                  02560082
           05  PV-RUNNING-TOTALS-MTD   PIC S9(9) COMP.                  02570082
           05  PV-RUNNING-TOTALS-YTD   PIC S9(9) COMP.                  02580082
           05  PV-SET-POS-INDEX        PIC  9(2) COMP VALUE 1.          02590082
           05  PV-START-DATE           PIC X(10)  VALUE SPACES.         02600082
           05  PV-SUMMARY-DAY          PIC S9(9) COMP.                  02610082
           05  PV-SUMMARY-WTD          PIC S9(9) COMP.                  02620082
           05  PV-SUMMARY-MTD          PIC S9(9) COMP.                  02630082
           05  PV-SUMMARY-YTD          PIC S9(9) COMP.                  02640082
           05  PV-SUMMARY-VALUE        PIC S9(9) COMP.                  02650082
           05  PV-TERR-NBR             PIC S9(4)V COMP-3.               02660082
           05  PV-TERR-NBR-9           PIC 9(2).                        02670082
           05  PV-TERR-NBR-X REDEFINES PV-TERR-NBR-9 PIC X(02).         02680082
           05  PV-WRITE-POS-SUMMARY    PIC S9(9) COMP VALUE 1.          02690082
                                                                        02700082
       01  PC-PROGRAM-CONSTANTS.                                        02710082
           05  PC-DETAIL-HEADER        PIC X(56)  VALUE                 02720082
             'CALL DATE,STORE,DEV ID,LOC ID,LOCATION,MESSAGE,CALL TIME'.02730082
FL1095     05  PC-MAX-POS-SUMMARY      PIC 9(1) COMP VALUE 7.           02740084
           05  PC-SUMMARY-HEADER       PIC X(25)  VALUE                 02750082
                                       'MESSAGE,TODAY,WTD,MTD,YTD'.     02760082
           05  PC-SUMMARY-ADD-CASH     PIC X(25)  VALUE                 02770082
                                       'ADDITIONAL CASHIER'.            02780082
           05  PC-SUMMARY-CARRY-OUT    PIC X(25)  VALUE                 02790082
                                       'CARRY OUT ASSISTANCE'.          02800082
           05  PC-SUMMARY-E3-ASSIST    PIC X(25)  VALUE                 02810082
                                       'E3 ASSISTANCE'.                 02820082
           05  PC-SUMMARY-27-ASSIST    PIC X(25)  VALUE                 02830082
                                       '27 ASSISTANCE'.                 02840082
           05  PC-SUMMARY-15-ASSIST    PIC X(25)  VALUE                 02850082
                                       '15 ASSISTANCE'.                 02860082
FL1095     05  PC-SUMMARY-CREDIT       PIC X(25)  VALUE                 02870083
FL1095                                 'CREDIT'.                        02880083
           05  PC-SUMMARY-ALL-CASH     PIC X(25)  VALUE                 02890082
                                       'ALL AVAILABLE CASHIERS'.        02900082
           05  PC-SUMMARY-PRICE-VER    PIC X(25)  VALUE                 02910082
                                       'PRICE VERIFIER ASSISTANCE'.     02920082
           05  PC-SUMMARY-KIOSK-ASSIST PIC X(25)  VALUE                 02930082
                                       'KIOSK ASSISTANCE'.              02940082
           05  PC-SUMMARY-OTHER        PIC X(25)  VALUE                 02950082
                                       'OTHER'.                         02960082
           05  PC-SUMMARY-TOTALS       PIC X(25)  VALUE                 02970082
                                       'TOTALS'.                        02980082
           05  PC-SUMM-TOT-PV-START    PIC 9(4)   VALUE 0100.           02990082
           05  PC-SUMM-TOT-PV-FINISH   PIC 9(4)   VALUE 0199.           03000082
           05  PC-SUMM-TOT-KSK-START   PIC 9(4)   VALUE 0200.           03010082
           05  PC-SUMM-TOT-KSK-FINISH  PIC 9(4)   VALUE 0299.           03020082
           05  PC-SUMM-TOT-POS-START   PIC S9(4) COMP   VALUE 500.      03030082
           05  PC-SUMM-TOT-POS-FINISH  PIC S9(4) COMP  VALUE 999.       03040082
           05  PC-SUMM-TOT-START       PIC 9(4)   VALUE 0000.           03050082
           05  PC-SUMM-TOT-FINISH      PIC 9(4)   VALUE 0999.           03060082
           05  PC-CURRENT-PROGRAM-NAME PIC X(8)   VALUE 'EPKUT080'.     03070082
           05  PC-ROW-NOT-FOUND        PIC S9(04) VALUE +100            03080082
                                                  COMP SYNC.            03090082
                                                                        03100082
      ***************************************************************** 03110082
      *  TABLE THAT HOLD THE MESSAGE/LOCATION VALUES SO THAT THE        03120082
      *  MESSAGE ID VALUES CAN BE LINK TO THE ACTUAL MESSAGE.           03130082
      ***************************************************************** 03140082
       01  CALL-BOX-MSG-LOC-TABLE.                                      03150082
           05  CB-MSG-LOC-TABLE OCCURS 200 TIMES                        03160082
               ASCENDING KEY IS CB-TBL-ADDRESS                          03170082
               INDEXED BY CB-INDEX.                                     03180082
022210         10  CB-TBL-ADDRESS          PIC X(04) VALUE '9999'.      03190082
               10  CB-TBL-LOCATION         PIC X(28).                   03200082
               10  CB-TBL-MESSAGE          PIC X(33).                   03210082
                                                                        03220082
      ***************************************************************** 03230082
      *  HOLDS THE DAILY TOTAL VALUES FOR EACH OF THE 10 BUTTON         03240082
      *  ID TYPES.                                                      03250082
      ***************************************************************** 03260082
       01  POS-CALL-BOX-DAY-TABLE.                                      03270082
           05  POS-DAY-CB-TABLE OCCURS 10 TIMES                         03280082
               ASCENDING KEY IS POS-DAY-BUTTON                          03290082
               INDEXED BY DAY-INDEX.                                    03300082
               10  POS-DAY-BUTTON          PIC S9(02).                  03310082
               10  POS-DAY-TOTALS          PIC 9(09).                   03320082
                                                                        03330082
      ***************************************************************** 03340082
      *  HOLDS THE WEEKLY TOTAL VALUES FOR EACH OF THE 10 BUTTON        03350082
      *  ID TYPES.                                                      03360082
      ***************************************************************** 03370082
       01  POS-CALL-BOX-WTD-TABLE.                                      03380082
           05  POS-WTD-CB-TABLE OCCURS 10 TIMES                         03390082
               ASCENDING KEY IS POS-WTD-BUTTON                          03400082
               INDEXED BY WTD-INDEX.                                    03410082
               10  POS-WTD-BUTTON          PIC S9(02).                  03420082
               10  POS-WTD-TOTALS          PIC 9(09).                   03430082
                                                                        03440082
      ***************************************************************** 03450082
      *  HOLDS THE MONTHLY TOTAL VALUES FOR EACH OF THE 10 BUTTON       03460082
      *  ID TYPES.                                                      03470082
      ***************************************************************** 03480082
       01  POS-CALL-BOX-MTD-TABLE.                                      03490082
           05  POS-MTD-CB-TABLE OCCURS 10 TIMES                         03500082
               ASCENDING KEY IS POS-MTD-BUTTON                          03510082
               INDEXED BY MTD-INDEX.                                    03520082
               10  POS-MTD-BUTTON          PIC S9(02).                  03530082
               10  POS-MTD-TOTALS          PIC 9(09).                   03540082
                                                                        03550082
      ***************************************************************** 03560082
      *  HOLDS THE YEARLY TOTAL VALUES FOR EACH OF THE 10 BUTTON        03570082
      *  ID TYPES.                                                      03580082
      ***************************************************************** 03590082
       01  POS-CALL-BOX-YTD-TABLE.                                      03600082
           05  POS-YTD-CB-TABLE OCCURS 10 TIMES                         03610082
               ASCENDING KEY IS POS-YTD-BUTTON                          03620082
               INDEXED BY YTD-INDEX.                                    03630082
               10  POS-YTD-BUTTON          PIC S9(02).                  03640082
               10  POS-YTD-TOTALS          PIC 9(09).                   03650082
                                                                        03660082
       01  PS-PROGRAM-SWITCHES.                                         03670082
           05  PS-END-OF-CALL-BOX-CURSOR-SW     PIC X     VALUE 'N'.    03680082
               88 PS-END-OF-CALL-BOX-CURSOR               VALUE 'Y'.    03690082
           05  PS-END-OF-DATE-FILE-SW          PIC X      VALUE 'N'.    03700082
               88 PS-END-OF-DATE-FILE                     VALUE 'Y'.    03710082
           05  PS-END-OF-MSG-FILE-SW           PIC X      VALUE 'N'.    03720082
               88  PS-END-OF-MSG-FILE-Y                   VALUE 'Y'.    03730082
               88  PS-END-OF-MSG-FILE-N                   VALUE 'N'.    03740082
           05  PS-TOTALS-REPORT-SW             PIC X      VALUE 'N'.    03750082
               88  PS-TOTALS-REPORT-Y                     VALUE 'Y'.    03760082
               88  PS-TOTALS-REPORT-N                     VALUE 'N'.    03770082
           05  PS-END-OF-POS-CURSOR-SW         PIC X      VALUE 'N'.    03780082
               88  PS-END-OF-POS-CURSOR-Y                 VALUE 'Y'.    03790082
               88  PS-END-OF-POS-CURSOR-N                 VALUE 'N'.    03800082
           05  PS-ADD-CALL-BOX-COUNT-SW        PIC X      VALUE 'N'.    03810082
               88  PS-ADD-CALL-BOX-COUNT-Y                VALUE 'Y'.    03820082
               88  PS-ADD-CALL-BOX-COUNT-N                VALUE 'N'.    03830082
           05  PS-STORE-TERR-FOUND-SW          PIC X      VALUE 'N'.    03840082
               88  PS-STORE-TERR-FOUND-Y                  VALUE 'Y'.    03850082
               88  PS-STORE-TERR-FOUND-N                  VALUE 'N'.    03860082
           05  PS-SUM-DAILY-VALUE-SW         PIC X        VALUE 'N'.    03870082
               88  PS-SUM-DAILY-VALUE-Y                   VALUE 'Y'.    03880082
               88  PS-SUM-DAILY-VALUE-N                   VALUE 'N'.    03890082
           05  PS-SUM-WEEKLY-VALUE-SW          PIC X      VALUE 'N'.    03900082
               88  PS-SUM-WEEKLY-VALUE-Y                  VALUE 'Y'.    03910082
               88  PS-SUM-WEEKLY-VALUE-N                  VALUE 'N'.    03920082
           05  PS-SUM-MONTHLY-VALUE-SW         PIC X      VALUE 'N'.    03930082
               88  PS-SUM-MONTHLY-VALUE-Y                 VALUE 'Y'.    03940082
               88  PS-SUM-MONTHLY-VALUE-N                 VALUE 'N'.    03950082
           05  PS-SUM-YEARLY-VALUE-SW          PIC X      VALUE 'N'.    03960082
               88  PS-SUM-YEARLY-VALUE-Y                  VALUE 'Y'.    03970082
               88  PS-SUM-YEARLY-VALUE-N                  VALUE 'N'.    03980082
      ******************************************************************03990082
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500) *           04000082
      ******************************************************************04010082
           COPY DPWS500.                                                04020082
                                                                        04030082
      ******************************************************************04040082
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            04050082
      ******************************************************************04060082
           COPY DPWS004.                                                04070082
                                                                        04080082
      ******************************************************************04090082
      *  DB2 COMMUNICATION AREA. *                                      04100082
      ******************************************************************04110082
           EXEC SQL                                                     04120082
                INCLUDE SQLCA                                           04130082
           END-EXEC.                                                    04140082
      ******************************************************************04150082
      *  DB2 TABLE XST_LOC_DIST TABLE *                                 04160082
      ******************************************************************04170082
           EXEC SQL                                                     04180082
                INCLUDE XST00002                                        04190082
           END-EXEC.                                                    04200082
      ******************************************************************04210082
      *  DB2 TABLE XST_DIST_REGN TABLE *                                04220082
      ******************************************************************04230082
           EXEC SQL                                                     04240082
                INCLUDE XST00004                                        04250082
           END-EXEC.                                                    04260082
      ******************************************************************04270082
      *  DB2 TABLE XST_REGN_TERR TABLE *                                04280082
      ******************************************************************04290082
           EXEC SQL                                                     04300082
                INCLUDE XST00006                                        04310082
           END-EXEC.                                                    04320082
      ******************************************************************04330082
      *  DB2 TABLE EPT_CALL_BOX CALL BOX TABLE *                        04340082
      ******************************************************************04350082
           EXEC SQL                                                     04360082
                INCLUDE EPT00062                                        04370082
           END-EXEC.                                                    04380082
      ******************************************************************04390082
      *  CURSOR FOR FETCHING CALL BOX                                  *04400082
      ******************************************************************04410082
           EXEC SQL                                                     04420082
               DECLARE CALL-BOX-CURSOR CURSOR                           04430082
               FOR SELECT D.STR_NBR                                     04440082
                         ,D.DEV_ID                                      04450082
                         ,D.CALL_DTE                                    04460082
                         ,D.CALL_TM                                     04470082
                         ,D.MSG_ID                                      04480082
                         ,D.MSG_BTN_ID                                  04490082
               FROM EPT_CALL_BOX D                                      04500082
               WHERE D.CALL_DTE = :DPG52-LK-DATE-INPUT                  04510082
               ORDER BY D.STR_NBR, D.CALL_TM                            04520082
               WITH UR                                                  04530082
           END-EXEC.                                                    04540082
      ******************************************************************04550082
      *  CURSOR FOR FETCHING CALL BOX SUMMARY REPORT                   *04560082
      ******************************************************************04570082
           EXEC SQL                                                     04580082
               DECLARE CALL-BOX-SUMM-CURSOR CURSOR                      04590082
               FOR SELECT COUNT(*)                                      04600082
                         ,D.MSG_BTN_ID                                  04610082
               FROM EPT_CALL_BOX D                                      04620082
               WHERE D.CALL_DTE BETWEEN :PV-START-DATE                  04630082
                                    AND :PV-END-DATE                    04640082
                 AND D.MSG_ID BETWEEN :PC-SUMM-TOT-POS-START            04650082
                                  AND :PC-SUMM-TOT-POS-FINISH           04660082
               GROUP BY D.MSG_BTN_ID                                    04670082
               ORDER BY D.MSG_BTN_ID                                    04680082
               WITH UR                                                  04690082
           END-EXEC.                                                    04700082
      ******************************************************************04710082
       PROCEDURE DIVISION.                                              04720082
      ******************************************************************04730082
                                                                        04740082
           PERFORM 1000-INITIALIZATION.                                 04750082
                                                                        04760082
           PERFORM 2000-PROCESS-CALL-BOX-FILE                           04770082
               UNTIL PS-END-OF-CALL-BOX-CURSOR                          04780082
           PERFORM 4000-SUMMARY-REPORTS.                                04790082
           PERFORM 8000-END-OF-JOB.                                     04800082
                                                                        04810082
           STOP RUN.                                                    04820082
      ******************************************************************04830082
      *    INITIALIZATION ROUTINE, OPEN THE EXTRACT, ACCEPT THE        *04840082
      *    CARD FROM SYSIN, PERFORM THE ROUTINE TO FIND THE FISCAL     *04850082
      *    PERIOD AND PERFORM THE ROUTIN TO OPEN THE CURSOR.           *04860082
      ******************************************************************04870082
       1000-INITIALIZATION.                                             04880082
                                                                        04890082
           INITIALIZE PV-LAST-STORE                                     04900082
                      PV-MSG-ID-COUNT.                                  04910082
           MOVE 'N' TO PS-TOTALS-REPORT-SW.                             04920082
                                                                        04930082
           OPEN  INPUT  CURRENT-DATE.                                   04940082
           OPEN  INPUT  MESSAGE-ID.                                     04950082
           OPEN  OUTPUT TERRITORY-1-EXTRACT-FILE.                       04960082
           OPEN  OUTPUT TERRITORY-2-EXTRACT-FILE.                       04970082
           OPEN  OUTPUT TERRITORY-3-EXTRACT-FILE.                       04980082
           OPEN  OUTPUT TERRITORY-4-EXTRACT-FILE.                       04990082
           OPEN  OUTPUT SUMMARY-FILE.                                   05000082
                                                                        05010082
           WRITE SUMMARY-RECORD                                         05020082
               FROM PC-SUMMARY-HEADER.                                  05030082
           WRITE TERRITORY-1-EXTRACT-RECORD                             05040082
               FROM PC-DETAIL-HEADER.                                   05050082
           WRITE TERRITORY-2-EXTRACT-RECORD                             05060082
               FROM PC-DETAIL-HEADER.                                   05070082
           WRITE TERRITORY-3-EXTRACT-RECORD                             05080082
               FROM PC-DETAIL-HEADER.                                   05090082
           WRITE TERRITORY-4-EXTRACT-RECORD                             05100082
               FROM PC-DETAIL-HEADER.                                   05110082
                                                                        05120082
           READ CURRENT-DATE                                            05130082
               INTO CALL-BOX-CONTROL-AREA                               05140082
               AT END SET PS-END-OF-DATE-FILE                           05150082
                                         TO TRUE.                       05160082
                                                                        05170082
           IF PS-END-OF-DATE-FILE                                       05180082
               MOVE '1000-INITIALIZATION'                               05190082
                                       TO PV-PARAGRAPH-NAME             05200082
               MOVE 'CURRENT DATE CONTROL CARD IS EMPTY'                05210082
                                       TO PV-OPERATION-MSG-1            05220082
               SET AC-INVALID-CONTROL-CARD                              05230082
                                         TO TRUE                        05240082
               PERFORM 9999-ABEND                                       05250082
           END-IF.                                                      05260082
                                                                        05270082
           MOVE 1 TO PV-MSG-ID-COUNT.                                   05280082
           PERFORM 1100-DATE-ROUTINE                                    05290082
           PERFORM 1300-LOAD-MESSAGE-TABLE                              05300082
                   UNTIL PS-END-OF-MSG-FILE-Y.                          05310082
           PERFORM 1200-OPEN-CALL-BOX-CURSOR.                           05320082
           PERFORM 2100-FETCH-CALL-BOX-CURSOR.                          05330082
                                                                        05340082
           IF PS-ADD-CALL-BOX-COUNT-Y                                   05350082
              ADD 1                     TO PV-CALL-BOX-CURSOR-COUNT     05360082
              SET PS-ADD-CALL-BOX-COUNT-N TO TRUE                       05370082
           END-IF.                                                      05380082
                                                                        05390082
           PERFORM UNTIL PV-SET-POS-INDEX >  10                         05400082
                MOVE PV-SET-POS-INDEX TO                                05410082
                                POS-DAY-BUTTON (PV-SET-POS-INDEX)       05420082
                                POS-WTD-BUTTON (PV-SET-POS-INDEX)       05430082
                                POS-MTD-BUTTON (PV-SET-POS-INDEX)       05440082
                                POS-YTD-BUTTON (PV-SET-POS-INDEX)       05450082
                 ADD +1 TO PV-SET-POS-INDEX                             05460082
           END-PERFORM.                                                 05470082
                                                                        05480082
      ******************************************************************05490082
      *    INITIALIZE THE CALL PARAMETERS, SET THE DATE INDICATORS,    *05500082
      *    MOVE THE YEAR AND PERIOD TO THE INPUT AREA AND CALL THE     *05510082
      *    ROUTINE AND CHECK FOR ERRORS.                               *05520082
      ******************************************************************05530082
       1100-DATE-ROUTINE.                                               05540082
                                                                        05550082
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              05560082
                                                                        05570082
           SET  DPG51-FISCAL-454-CALENDAR    TO TRUE.                   05580082
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   05590082
           SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                   05600082
           MOVE PROCESS-CONTROL-DATE         TO DPG52-LK-DATE-INPUT.    05610082
                                                                        05620082
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    05630082
                                                   DPG54 DPG55 DPG56.   05640082
                                                                        05650082
           EVALUATE TRUE                                                05660082
               WHEN DPG54-SEVERE-ERROR                                  05670082
               WHEN DPG54-DATE-INVALID                                  05680082
                   MOVE '1100-DATE-ROUTINE'                             05690082
                                                 TO PV-PARAGRAPH-NAME   05700082
                   MOVE 'ERROR CONVERTING INPUT CARD DATE'              05710082
                                                 TO PV-OPERATION-MSG-1  05720082
                   SET AC-CALENDAR-ROUTINE-ERROR TO TRUE                05730082
                   MOVE DPG54-ERROR-MESSAGE      TO PV-OPERATION-MSG-2  05740082
                   PERFORM 9999-ABEND                                   05750082
           END-EVALUATE.                                                05760082
                                                                        05770082
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT  TO PV-CALL-BOX-WTD.          05780082
           MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO PV-CALL-BOX-MTD.          05790082
           MOVE DPG56-FIRST-FD-DB2-ISO-FMT TO PV-CALL-BOX-YTD.          05800082
                                                                        05810082
      ******************************************************************05820082
      *  OPEN THE CALL BOX CURSOR TO EXTRACT ALL POS CALL FOR THE DAY  *05830082
      ******************************************************************05840082
       1200-OPEN-CALL-BOX-CURSOR.                                       05850082
                                                                        05860082
           EXEC SQL                                                     05870082
                OPEN CALL-BOX-CURSOR                                    05880082
           END-EXEC.                                                    05890082
                                                                        05900082
           EVALUATE TRUE                                                05910082
               WHEN SQLCODE = ZERO                                      05920082
                    CONTINUE                                            05930082
               WHEN SQLWARN0 NOT EQUAL SPACE                            05940082
               WHEN SQLCODE NOT EQUAL ZERO                              05950082
                   MOVE '1200-OPEN-CALL-BOX-CURSOR'                     05960082
                                       TO PV-PARAGRAPH-NAME             05970082
                   MOVE 'ERROR ON OPEN OF CALL-BOX-CURSOR'              05980082
                                       TO PV-DB2-OPERATION              05990082
                   SET AC-DB2-ERROR    TO TRUE                          06000082
                   PERFORM 9800-SQL-ABEND                               06010082
           END-EVALUATE.                                                06020082
                                                                        06030082
      ******************************************************************06040082
      *  USING A CONTROL CARD, LOADS THE MESSAGE ID TABLE SO THAT THE  *06050082
      *  PROGRAM WILL DISPLAY THE MESSAGE NAME AND LOCATION THAT       *06060082
      *  CORRESPONDS TO THE MESSAGE ID.                                *06070082
      ******************************************************************06080082
       1300-LOAD-MESSAGE-TABLE.                                         06090082
                                                                        06100082
           READ MESSAGE-ID                                              06110082
               INTO MESSAGE-ID-AREA                                     06120082
               AT END SET PS-END-OF-MSG-FILE-Y                          06130082
                                         TO TRUE.                       06140082
                                                                        06150082
           IF PS-END-OF-MSG-FILE-N                                      06160082
              MOVE MESSAGE-ID-X     TO CB-TBL-ADDRESS (PV-MSG-ID-COUNT) 06170082
              MOVE MESSAGE-LOCATION TO CB-TBL-LOCATION (PV-MSG-ID-COUNT)06180082
              MOVE MESSAGE-MESSAGE  TO CB-TBL-MESSAGE (PV-MSG-ID-COUNT) 06190082
                                                                        06200082
              ADD +1 TO PV-MSG-ID-COUNT                                 06210082
           END-IF.                                                      06220082
                                                                        06230082
      ******************************************************************06240082
      *  MOVE THE EXTRACTED FIELDS TO THE OUTPUT RECORD.  GET THE       06250082
      *  CORRECT MESSAGE NAME AND LOCATION FOR THE MESSAGE ID           06260082
      *  AND GET THE STORE TERRITORY NUMBER SO THAT THE RECORD CAN BE   06270082
      *  WRITTEN TO CORRECT OUTPUT FILE.                                06280082
      ******************************************************************06290082
       2000-PROCESS-CALL-BOX-FILE.                                      06300082
                                                                        06310082
           MOVE EPT00062-STR-NBR        TO  CB-EXTRACT-STORE-NM.        06320082
           MOVE EPT00062-MSG-ID         TO  CB-EXTRACT-MSG-ID.          06330082
           MOVE EPT00062-CALL-TM        TO  CB-EXTRACT-TIME.            06340082
           MOVE EPT00062-DEV-ID         TO  CB-EXTRACT-DEV-ID.          06350082
                                                                        06360082
           MOVE EPT00062-CALL-DTE TO PV-DATE-EDIT-YMD.                  06370082
           MOVE PV-DATE-EDIT-YMD-CC   TO PV-DATE-EDIT-MDY-CC.           06380082
           MOVE PV-DATE-EDIT-YMD-YY   TO PV-DATE-EDIT-MDY-YY.           06390082
           MOVE PV-DATE-EDIT-YMD-MM   TO PV-DATE-EDIT-MDY-MM.           06400082
           MOVE PV-DATE-EDIT-YMD-DD   TO PV-DATE-EDIT-MDY-DD.           06410082
           MOVE PV-DATE-EDIT-MDY TO CB-EXTRACT-DATE.                    06420082
                                                                        06430082
           PERFORM 2200-GET-MSG-TEXT.                                   06440082
                                                                        06450082
           IF PV-LAST-STORE NOT EQUAL CB-EXTRACT-STORE-NM               06460082
             PERFORM 3000-SELECT-STR-TERR                               06470082
             IF PS-STORE-TERR-FOUND-Y                                   06480082
               MOVE XST00006-TERR-NBR TO PV-TERR-NBR-9                  06490082
               SET  PS-STORE-TERR-FOUND-N  TO TRUE                      06500082
             ELSE                                                       06510082
               MOVE 4 TO PV-TERR-NBR-9                                  06520082
             END-IF                                                     06530082
             MOVE CB-EXTRACT-STORE-NM TO PV-LAST-STORE                  06540082
           END-IF.                                                      06550082
                                                                        06560082
           PERFORM 9000-WRITE-EXTRACT-RECORDS.                          06570082
                                                                        06580082
           PERFORM 2100-FETCH-CALL-BOX-CURSOR.                          06590082
           IF PS-ADD-CALL-BOX-COUNT-Y                                   06600082
              ADD 1                     TO PV-CALL-BOX-CURSOR-COUNT     06610082
              SET PS-ADD-CALL-BOX-COUNT-N TO TRUE                       06620082
           END-IF.                                                      06630082
                                                                        06640082
      ******************************************************************06650082
      *    FETCH THE CALL BOX CURSOR AND CHECK FOR ERRORS.             *06660082
      ******************************************************************06670082
       2100-FETCH-CALL-BOX-CURSOR.                                      06680082
                                                                        06690082
           EXEC SQL                                                     06700082
                FETCH CALL-BOX-CURSOR                                   06710082
                 INTO :EPT00062-STR-NBR                                 06720082
                     ,:EPT00062-DEV-ID                                  06730082
                     ,:EPT00062-CALL-DTE                                06740082
                     ,:EPT00062-CALL-TM                                 06750082
                     ,:EPT00062-MSG-ID                                  06760082
                     ,:EPT00062-MSG-BTN-ID                              06770082
           END-EXEC.                                                    06780082
                                                                        06790082
           EVALUATE TRUE                                                06800082
               WHEN SQLCODE = ZERO                                      06810082
                   SET PS-ADD-CALL-BOX-COUNT-Y TO TRUE                  06820082
               WHEN SQLCODE = PC-ROW-NOT-FOUND                          06830082
                   SET PS-END-OF-CALL-BOX-CURSOR TO TRUE                06840082
               WHEN SQLWARN0 NOT EQUAL SPACE                            06850082
               WHEN SQLCODE NOT EQUAL ZERO                              06860082
                   MOVE '2100-FETCH-CALL-BOX-CURSOR'                    06870082
                                            TO PV-PARAGRAPH-NAME        06880082
                   MOVE SQLCODE             TO PV-DB2-OPERATION         06890082
                   SET AC-DB2-ERROR         TO TRUE                     06900082
                   PERFORM 9800-SQL-ABEND                               06910082
           END-EVALUATE.                                                06920082
                                                                        06930082
                                                                        06940082
      ******************************************************************06950082
      *  FIND THE CORRECT MESSAGE NAME AND LOCATION FOR A GIVEN MSG ID  06960082
      ******************************************************************06970082
       2200-GET-MSG-TEXT.                                               06980082
                                                                        06990082
           SEARCH ALL CB-MSG-LOC-TABLE                                  07000082
               AT END                                                   07010082
                   MOVE 'INVALID MESSAGE'          TO CB-EXTRACT-MSG-LOC07020082
                   MOVE 'INVALID LOCATION'         TO CB-EXTRACT-MSG    07030082
               WHEN CB-TBL-ADDRESS (CB-INDEX) = CB-EXTRACT-MSG-ID       07040082
                   MOVE CB-TBL-LOCATION (CB-INDEX) TO CB-EXTRACT-MSG-LOC07050082
                   MOVE CB-TBL-MESSAGE (CB-INDEX)  TO CB-EXTRACT-MSG    07060082
           END-SEARCH.                                                  07070082
                                                                        07080082
      ******************************************************************07090082
      *  GETS THE CORRECT TERRITORY FOR EACH STORE SO THAT IT CAN BE   *07100082
      *  PUT IN THE CORRECT SPREADSHEET.                               *07110082
      ******************************************************************07120082
       3000-SELECT-STR-TERR.                                            07130082
                                                                        07140082
           EXEC SQL                                                     07150082
              SELECT T.TERR_NBR                                         07160082
              INTO  :XST00006-TERR-NBR                                  07170082
              FROM  XST_LOC_DIST   D                                    07180082
                   ,XST_DIST_REGN  R                                    07190082
                   ,XST_REGN_TERR  T                                    07200082
              WHERE D.DIST_NBR = R.DIST_NBR                             07210082
              AND   R.REGN_NBR = T.REGN_NBR                             07220082
              AND   D.END_DTE IS NULL                                   07230082
              AND   R.END_DTE IS NULL                                   07240082
              AND   T.END_DTE IS NULL                                   07250082
              AND   D.LOC_NBR = :EPT00062-STR-NBR                       07260082
           END-EXEC.                                                    07270082
                                                                        07280082
           EVALUATE TRUE                                                07290082
               WHEN SQLCODE = ZERO                                      07300082
                   SET  PS-STORE-TERR-FOUND-Y  TO TRUE                  07310082
               WHEN SQLCODE = 100                                       07320082
                   SET  PS-STORE-TERR-FOUND-N  TO TRUE                  07330082
               WHEN SQLWARN0 NOT EQUAL SPACE                            07340082
               WHEN SQLCODE NOT EQUAL ZERO                              07350082
                   DISPLAY 'EPT00062-STR-NBR:' EPT00062-STR-NBR         07360082
                   MOVE '3000-SELECT-STR-TERR'                          07370082
                                           TO PV-PARAGRAPH-NAME         07380082
                   MOVE SQLCODE            TO PV-DB2-OPERATION          07390082
                   PERFORM 9800-SQL-ABEND                               07400082
           END-EVALUATE.                                                07410082
                                                                        07420082
      *3000-EXIT.                                                       07430082
      ******************************************************************07440082
      *  CALL THE PROCESSES TO POPULATE THE SUMMARY TABLES.             07450082
      ******************************************************************07460082
       4000-SUMMARY-REPORTS.                                            07470082
                                                                        07480082
           MOVE  DPG52-LK-DATE-INPUT  TO PV-END-DATE.                   07490082
                                                                        07500082
      * GET THE DAILY VALUES                                            07510082
           MOVE  DPG52-LK-DATE-INPUT  TO PV-START-DATE.                 07520082
           PERFORM 4070-OPEN-CALL-BOX-SUMM-CURSOR.                      07530082
           PERFORM 4080-FTCH-CALL-BOX-SUM-CURSOR                        07540082
           SET PS-END-OF-POS-CURSOR-N TO TRUE.                          07550082
           PERFORM 4010-POPULATE-POS-DAY-TABLE                          07560082
                        UNTIL PS-END-OF-POS-CURSOR-Y.                   07570082
           PERFORM 4090-CLOSE-CALL-BOX-SUM-CURSOR.                      07580082
                                                                        07590082
      * GET THE WEEKLY VALUES                                           07600082
           MOVE  PV-CALL-BOX-WTD      TO PV-START-DATE.                 07610082
           PERFORM 4070-OPEN-CALL-BOX-SUMM-CURSOR.                      07620082
           PERFORM 4080-FTCH-CALL-BOX-SUM-CURSOR                        07630082
           SET PS-END-OF-POS-CURSOR-N TO TRUE.                          07640082
           PERFORM 4020-POPULATE-POS-WTD-TABLE                          07650082
                        UNTIL PS-END-OF-POS-CURSOR-Y.                   07660082
           PERFORM 4090-CLOSE-CALL-BOX-SUM-CURSOR.                      07670082
                                                                        07680082
      * GET THE MONTHLY VALUES                                          07690082
           MOVE  PV-CALL-BOX-MTD      TO PV-START-DATE.                 07700082
           PERFORM 4070-OPEN-CALL-BOX-SUMM-CURSOR.                      07710082
           PERFORM 4080-FTCH-CALL-BOX-SUM-CURSOR                        07720082
           SET PS-END-OF-POS-CURSOR-N TO TRUE.                          07730082
           PERFORM 4030-POPULATE-POS-MTD-TABLE                          07740082
                        UNTIL PS-END-OF-POS-CURSOR-Y.                   07750082
           PERFORM 4090-CLOSE-CALL-BOX-SUM-CURSOR.                      07760082
                                                                        07770082
      * GET THE YEARLY VALUES                                           07780082
           MOVE  PV-CALL-BOX-YTD      TO PV-START-DATE.                 07790082
           PERFORM 4070-OPEN-CALL-BOX-SUMM-CURSOR.                      07800082
           PERFORM 4080-FTCH-CALL-BOX-SUM-CURSOR                        07810082
           SET PS-END-OF-POS-CURSOR-N TO TRUE.                          07820082
           PERFORM 4040-POPULATE-POS-YTD-TABLE                          07830082
                        UNTIL PS-END-OF-POS-CURSOR-Y.                   07840082
           PERFORM 4090-CLOSE-CALL-BOX-SUM-CURSOR.                      07850082
                                                                        07860082
           PERFORM 4055-WRITE-POS-SUMMARY                               07870082
             UNTIL PV-WRITE-POS-SUMMARY > PC-MAX-POS-SUMMARY.           07880082
                                                                        07890082
                                                                        07900082
           MOVE PC-SUMMARY-PRICE-VER  TO SUMMARY-NAME.                  07910082
           MOVE PC-SUMM-TOT-PV-START  TO PV-MSG-ID-START.               07920082
           MOVE PC-SUMM-TOT-PV-FINISH TO PV-MSG-ID-FINISH.              07930082
           PERFORM 4050-SUMMARY-REPORTS-OTHER.                          07940082
                                                                        07950082
           MOVE PC-SUMMARY-KIOSK-ASSIST TO SUMMARY-NAME.                07960082
           MOVE PC-SUMM-TOT-KSK-START   TO PV-MSG-ID-START.             07970082
           MOVE PC-SUMM-TOT-KSK-FINISH  TO PV-MSG-ID-FINISH.            07980082
           PERFORM 4050-SUMMARY-REPORTS-OTHER.                          07990082
                                                                        08000082
                                                                        08010082
           MOVE PC-SUMM-TOT-START  TO PV-MSG-ID-START.                  08020082
           MOVE PC-SUMM-TOT-FINISH TO PV-MSG-ID-FINISH.                 08030082
           SET  PS-TOTALS-REPORT-Y TO TRUE.                             08040082
           PERFORM 4050-SUMMARY-REPORTS-OTHER.                          08050082
                                                                        08060082
      *4000-EXIT.                                                       08070082
      ******************************************************************08080082
      *  LOADS A TABLE WITH THE DAILY COUNT OF ALL THE BUTTONS          08090082
      ******************************************************************08100082
       4010-POPULATE-POS-DAY-TABLE.                                     08110082
                                                                        08120082
           SEARCH ALL POS-DAY-CB-TABLE                                  08130082
               AT END                                                   08140082
                  CONTINUE                                              08150082
      * WE NEED TO ADD 1 TO THE BUTTON SUMMARY BECAUSE THERE IS A       08160082
      * 0 BUTTON, BUT THE TABLE INDEX STARTS AT 1.                      08170082
               WHEN POS-DAY-BUTTON (DAY-INDEX) = PV-POS-SUMM-BUTTON + 1 08180082
                  MOVE PV-POS-SUMM-COUNT                                08190082
                                       TO POS-DAY-TOTALS (DAY-INDEX)    08200082
           END-SEARCH.                                                  08210082
                                                                        08220082
           IF PV-POS-SUMM-BUTTON < PC-MAX-POS-SUMMARY                   08230082
                  ADD PV-POS-SUMM-COUNT TO PV-RUNNING-TOTALS-DAY.       08240082
                                                                        08250082
                                                                        08260082
           PERFORM 4080-FTCH-CALL-BOX-SUM-CURSOR.                       08270082
                                                                        08280082
      ******************************************************************08290082
      * LOADS A TABLE WITH THE WEEKLY COUNT OF ALL THE BUTTONS          08300082
      ******************************************************************08310082
       4020-POPULATE-POS-WTD-TABLE.                                     08320082
                                                                        08330082
           SEARCH ALL POS-WTD-CB-TABLE                                  08340082
               AT END                                                   08350082
                  CONTINUE                                              08360082
      * WE NEED TO ADD 1 TO THE BUTTON SUMMARY BECAUSE THERE IS A       08370082
      * 0 BUTTON, BUT THE TABLE INDEX STARTS AT 1.                      08380082
               WHEN POS-WTD-BUTTON (WTD-INDEX) = PV-POS-SUMM-BUTTON + 1 08390082
                  MOVE PV-POS-SUMM-COUNT                                08400082
                                       TO POS-WTD-TOTALS (WTD-INDEX)    08410082
           END-SEARCH.                                                  08420082
                                                                        08430082
           IF PV-POS-SUMM-BUTTON < PC-MAX-POS-SUMMARY                   08440082
                  ADD PV-POS-SUMM-COUNT TO PV-RUNNING-TOTALS-WTD.       08450082
                                                                        08460082
           PERFORM 4080-FTCH-CALL-BOX-SUM-CURSOR.                       08470082
                                                                        08480082
      ******************************************************************08490082
      * LOADS A TABLE WITH THE MONTHLY COUNT OF ALL THE BUTTONS         08500082
      ******************************************************************08510082
       4030-POPULATE-POS-MTD-TABLE.                                     08520082
                                                                        08530082
           SEARCH ALL POS-MTD-CB-TABLE                                  08540082
               AT END                                                   08550082
                  CONTINUE                                              08560082
      * WE NEED TO ADD 1 TO THE BUTTON SUMMARY BECAUSE THERE IS A       08570082
      * 0 BUTTON, BUT THE TABLE INDEX STARTS AT 1.                      08580082
               WHEN POS-MTD-BUTTON (MTD-INDEX) = PV-POS-SUMM-BUTTON + 1 08590082
                  MOVE PV-POS-SUMM-COUNT                                08600082
                                       TO POS-MTD-TOTALS (MTD-INDEX)    08610082
           END-SEARCH.                                                  08620082
                                                                        08630082
           IF PV-POS-SUMM-BUTTON < PC-MAX-POS-SUMMARY                   08640082
                  ADD PV-POS-SUMM-COUNT TO PV-RUNNING-TOTALS-MTD.       08650082
                                                                        08660082
           PERFORM 4080-FTCH-CALL-BOX-SUM-CURSOR.                       08670082
                                                                        08680082
      ******************************************************************08690082
      * LOADS A TABLE WITH THE YEARLY COUNT OF ALL THE BUTTONS          08700082
      ******************************************************************08710082
       4040-POPULATE-POS-YTD-TABLE.                                     08720082
                                                                        08730082
           SEARCH ALL POS-YTD-CB-TABLE                                  08740082
               AT END                                                   08750082
                  CONTINUE                                              08760082
      * WE NEED TO ADD 1 TO THE BUTTON SUMMARY BECAUSE THERE IS A       08770082
      * 0 BUTTON, BUT THE TABLE INDEX STARTS AT 1.                      08780082
               WHEN POS-YTD-BUTTON (YTD-INDEX) = PV-POS-SUMM-BUTTON + 1 08790082
                  MOVE PV-POS-SUMM-COUNT                                08800082
                                      TO POS-YTD-TOTALS (YTD-INDEX)     08810082
           END-SEARCH.                                                  08820082
                                                                        08830082
           IF PV-POS-SUMM-BUTTON < PC-MAX-POS-SUMMARY                   08840082
                  ADD PV-POS-SUMM-COUNT TO PV-RUNNING-TOTALS-YTD.       08850082
                                                                        08860082
           PERFORM 4080-FTCH-CALL-BOX-SUM-CURSOR.                       08870082
                                                                        08880082
      ******************************************************************08890082
      *  CALL THE PROCESSES TO POPULATE THE SUMMARY TABLES.             08900082
      ******************************************************************08910082
       4050-SUMMARY-REPORTS-OTHER.                                      08920082
                                                                        08930082
           PERFORM 4100-GET-WTD-VALUES.                                 08940082
           IF PS-TOTALS-REPORT-N AND PS-SUM-WEEKLY-VALUE-Y              08950082
              ADD PV-SUMMARY-WTD         TO PV-RUNNING-TOTALS-WTD       08960082
              SET PS-SUM-WEEKLY-VALUE-N  TO TRUE                        08970082
           END-IF                                                       08980082
                                                                        08990082
           PERFORM 4200-GET-MTD-VALUES.                                 09000082
           IF PS-TOTALS-REPORT-N AND PS-SUM-MONTHLY-VALUE-Y             09010082
              ADD PV-SUMMARY-MTD         TO PV-RUNNING-TOTALS-MTD       09020082
              SET PS-SUM-MONTHLY-VALUE-N  TO TRUE                       09030082
           END-IF                                                       09040082
                                                                        09050082
           PERFORM 4300-GET-YTD-VALUES.                                 09060082
           IF PS-TOTALS-REPORT-N AND PS-SUM-YEARLY-VALUE-Y              09070082
              ADD PV-SUMMARY-YTD         TO PV-RUNNING-TOTALS-YTD       09080082
              SET PS-SUM-YEARLY-VALUE-N  TO TRUE                        09090082
           END-IF                                                       09100082
                                                                        09110082
           PERFORM 4400-GET-DAY-VALUES.                                 09120082
           IF PS-TOTALS-REPORT-N AND PS-SUM-DAILY-VALUE-Y               09130082
              ADD PV-SUMMARY-DAY         TO PV-RUNNING-TOTALS-DAY       09140082
              SET PS-SUM-DAILY-VALUE-N  TO TRUE                         09150082
           END-IF                                                       09160082
                                                                        09170082
           IF PS-TOTALS-REPORT-Y                                        09180082
             PERFORM 4060-CALC-OTHER-TOTALS                             09190082
             MOVE PC-SUMMARY-TOTALS  TO SUMMARY-NAME.                   09200082
           PERFORM 9100-WRITE-SUMMARY-RECORDS.                          09210082
                                                                        09220082
      *4050-EXIT.                                                       09230082
      ******************************************************************09240082
      *  CALL THE PROCESSES TO POPULATE THE SUMMARY TABLES.             09250082
      ******************************************************************09260082
       4055-WRITE-POS-SUMMARY.                                          09270082
                                                                        09280082
           MOVE POS-DAY-TOTALS (PV-WRITE-POS-SUMMARY)                   09290082
                                                TO PV-SUMMARY-DAY.      09300082
           MOVE POS-WTD-TOTALS (PV-WRITE-POS-SUMMARY)                   09310082
                                                TO PV-SUMMARY-WTD.      09320082
           MOVE POS-MTD-TOTALS (PV-WRITE-POS-SUMMARY)                   09330082
                                                TO PV-SUMMARY-MTD.      09340082
           MOVE POS-YTD-TOTALS (PV-WRITE-POS-SUMMARY)                   09350082
                                                TO PV-SUMMARY-YTD.      09360082
                                                                        09370082
           EVALUATE PV-WRITE-POS-SUMMARY                                09380082
             WHEN 1                                                     09390082
               MOVE PC-SUMMARY-ADD-CASH   TO SUMMARY-NAME               09400084
             WHEN 2                                                     09410082
               MOVE PC-SUMMARY-ALL-CASH   TO SUMMARY-NAME               09420082
             WHEN 3                                                     09430082
               MOVE PC-SUMMARY-E3-ASSIST  TO SUMMARY-NAME               09440082
             WHEN 4                                                     09450082
               MOVE PC-SUMMARY-27-ASSIST  TO SUMMARY-NAME               09460082
             WHEN 5                                                     09470082
               MOVE PC-SUMMARY-CARRY-OUT  TO SUMMARY-NAME               09480082
             WHEN 6                                                     09490082
               MOVE PC-SUMMARY-15-ASSIST  TO SUMMARY-NAME               09500082
FL1095       WHEN 7                                                     09510083
FL1095         MOVE PC-SUMMARY-CREDIT     TO SUMMARY-NAME               09520083
           END-EVALUATE.                                                09530082
                                                                        09540082
           PERFORM 9100-WRITE-SUMMARY-RECORDS.                          09550082
                                                                        09560082
           ADD +1 TO PV-WRITE-POS-SUMMARY.                              09570082
                                                                        09580082
      *4055-EXIT.                                                       09590082
      ******************************************************************09600082
      *  CALCULATE THE DIFFERENCE BETWEEN THE TOTALS AND THE RUNNING    09610082
      *  TOTALS FOR THE DIFFERENT LOCATIONS.  DIFFERENCES COULD HAPPEN  09620082
      *  BECAUSE OF A NEW OR MISLABELED MESSAGE ID.                     09630082
      ******************************************************************09640082
       4060-CALC-OTHER-TOTALS.                                          09650082
                                                                        09660082
           COMPUTE PV-OTHER-TOTALS-DAY =  PV-SUMMARY-DAY -              09670082
                                          PV-RUNNING-TOTALS-DAY.        09680082
           COMPUTE PV-OTHER-TOTALS-WTD =  PV-SUMMARY-WTD -              09690082
                                          PV-RUNNING-TOTALS-WTD.        09700082
           COMPUTE PV-OTHER-TOTALS-MTD =  PV-SUMMARY-MTD -              09710082
                                          PV-RUNNING-TOTALS-MTD.        09720082
           COMPUTE PV-OTHER-TOTALS-YTD =  PV-SUMMARY-YTD -              09730082
                                          PV-RUNNING-TOTALS-YTD.        09740082
                                                                        09750082
           PERFORM 9110-WRITE-OTH-SUMMARY-RECORDS.                      09760082
                                                                        09770082
      ******************************************************************09780082
      *  OPEN THE CALL BOX CURSOR TO EXTRACT ALL POS CALL FOR THE DAY  *09790082
      ******************************************************************09800082
       4070-OPEN-CALL-BOX-SUMM-CURSOR.                                  09810082
                                                                        09820082
           EXEC SQL                                                     09830082
                OPEN CALL-BOX-SUMM-CURSOR                               09840082
           END-EXEC.                                                    09850082
                                                                        09860082
           EVALUATE TRUE                                                09870082
               WHEN SQLCODE = ZERO                                      09880082
                    CONTINUE                                            09890082
               WHEN SQLWARN0 NOT EQUAL SPACE                            09900082
               WHEN SQLCODE NOT EQUAL ZERO                              09910082
                   MOVE '4070-OPEN-CALL-BOX-SUMM-CURSOR'                09920082
                                       TO PV-PARAGRAPH-NAME             09930082
                   MOVE 'ERROR ON OPEN OF CALL-BOX-CURSOR'              09940082
                                       TO PV-DB2-OPERATION              09950082
                   SET AC-DB2-ERROR    TO TRUE                          09960082
                   PERFORM 9800-SQL-ABEND                               09970082
           END-EVALUATE.                                                09980082
      ******************************************************************09990082
      *    FETCH THE CALL BOX CURSOR AND CHECK FOR ERRORS.             *10000082
      ******************************************************************10010082
       4080-FTCH-CALL-BOX-SUM-CURSOR.                                   10020082
                                                                        10030082
           EXEC SQL                                                     10040082
                FETCH CALL-BOX-SUMM-CURSOR                              10050082
                 INTO :PV-POS-SUMM-COUNT                                10060082
                     ,:PV-POS-SUMM-BUTTON                               10070082
           END-EXEC.                                                    10080082
                                                                        10090082
           EVALUATE TRUE                                                10100082
               WHEN SQLCODE = ZERO                                      10110082
                   CONTINUE                                             10120082
               WHEN SQLCODE = PC-ROW-NOT-FOUND                          10130082
                   SET PS-END-OF-POS-CURSOR-Y TO TRUE                   10140082
               WHEN SQLWARN0 NOT EQUAL SPACE                            10150082
               WHEN SQLCODE NOT EQUAL ZERO                              10160082
                   MOVE '4080-FTCH-CALL-BOX-SUM-CURSOR'                 10170082
                                            TO PV-PARAGRAPH-NAME        10180082
                   MOVE SQLCODE             TO PV-DB2-OPERATION         10190082
                   SET AC-DB2-ERROR         TO TRUE                     10200082
                   PERFORM 9800-SQL-ABEND                               10210082
           END-EVALUATE.                                                10220082
                                                                        10230082
      ******************************************************************10240082
      *    CLOSE THE CALL BOX CURSOR AND CHECK FOR ERRORS.             *10250082
      ******************************************************************10260082
                                                                        10270082
       4090-CLOSE-CALL-BOX-SUM-CURSOR.                                  10280082
                                                                        10290082
           EXEC SQL                                                     10300082
                CLOSE CALL-BOX-SUMM-CURSOR                              10310082
           END-EXEC.                                                    10320082
                                                                        10330082
           EVALUATE TRUE                                                10340082
               WHEN SQLCODE = ZERO                                      10350082
                    CONTINUE                                            10360082
               WHEN SQLWARN0 NOT EQUAL SPACE                            10370082
               WHEN SQLCODE NOT EQUAL ZERO                              10380082
                   MOVE '4090-CLOSE-CALL-BOX-SUM-CURSOR'                10390082
                                       TO PV-PARAGRAPH-NAME             10400082
                   MOVE 'ERROR ON CLOSE OF CALL-BOX-CURSOR'             10410082
                                       TO PV-DB2-OPERATION              10420082
                   SET AC-DB2-ERROR    TO TRUE                          10430082
                   PERFORM 9800-SQL-ABEND                               10440082
           END-EVALUATE.                                                10450082
                                                                        10460082
                                                                        10470082
      ******************************************************************10480082
      *  OBTAINS THE WEEK TO DATE DATE TOTALS FOR A VARIABLE RANGE      10490082
      *  OF MESSAGE IDS.                                                10500082
      ******************************************************************10510082
       4100-GET-WTD-VALUES.                                             10520082
           EXEC SQL                                                     10530082
               SELECT COUNT(*)                                          10540082
               INTO :PV-SUMMARY-WTD                                     10550082
               FROM EPT_CALL_BOX D                                      10560082
               WHERE D.CALL_DTE BETWEEN :PV-CALL-BOX-WTD                10570082
                                    AND :DPG52-LK-DATE-INPUT            10580082
                 AND D.MSG_ID BETWEEN :PV-MSG-ID-START                  10590082
                                  AND :PV-MSG-ID-FINISH                 10600082
               WITH UR                                                  10610082
           END-EXEC.                                                    10620082
                                                                        10630082
           EVALUATE TRUE                                                10640082
              WHEN SQLCODE = ZERO                                       10650082
                 SET PS-SUM-WEEKLY-VALUE-Y  TO TRUE                     10660082
              WHEN OTHER                                                10670082
                 MOVE SQLCODE    TO PV-DB2-OPERATION                    10680082
                 MOVE '4100-GET-WTD-VALUES' TO PV-PARAGRAPH-NAME        10690082
                 DISPLAY 'PV-CALL-BOX-WTD: ' PV-CALL-BOX-WTD            10700082
                 DISPLAY 'DPG52-LK-DATE-INPUT: ' DPG52-LK-DATE-INPUT    10710082
                 DISPLAY 'PV-MSG-ID-START: ' PV-MSG-ID-START            10720082
                 DISPLAY 'PV-MSG-ID-FINISH: ' PV-MSG-ID-FINISH          10730082
                 PERFORM 9800-SQL-ABEND                                 10740082
           END-EVALUATE.                                                10750082
                                                                        10760082
      *4100-EXIT.                                                       10770082
      ******************************************************************10780082
      *  OBTAINS THE MONTH TO DATE DATE TOTALS FOR A VARIABLE RANGE     10790082
      *  OF MESSAGE IDS.                                               *10800082
      ******************************************************************10810082
                                                                        10820082
       4200-GET-MTD-VALUES.                                             10830082
           EXEC SQL                                                     10840082
               SELECT COUNT(*)                                          10850082
               INTO :PV-SUMMARY-MTD                                     10860082
               FROM EPT_CALL_BOX D                                      10870082
               WHERE D.CALL_DTE BETWEEN :PV-CALL-BOX-MTD                10880082
                                    AND :DPG52-LK-DATE-INPUT            10890082
                 AND D.MSG_ID BETWEEN :PV-MSG-ID-START                  10900082
                                  AND :PV-MSG-ID-FINISH                 10910082
               WITH UR                                                  10920082
           END-EXEC.                                                    10930082
           EVALUATE TRUE                                                10940082
              WHEN SQLCODE = ZERO                                       10950082
                 SET PS-SUM-MONTHLY-VALUE-Y  TO TRUE                    10960082
              WHEN OTHER                                                10970082
                 MOVE SQLCODE               TO PV-DB2-OPERATION         10980082
                 MOVE '4200-GET-MTD-VALUES' TO PV-PARAGRAPH-NAME        10990082
                 DISPLAY 'PV-CALL-BOX-WTD: ' PV-CALL-BOX-WTD            11000082
                 DISPLAY 'DPG52-LK-DATE-INPUT: ' DPG52-LK-DATE-INPUT    11010082
                 DISPLAY 'PV-MSG-ID-START: ' PV-MSG-ID-START            11020082
                 DISPLAY 'PV-MSG-ID-FINISH: ' PV-MSG-ID-FINISH          11030082
                 PERFORM 9800-SQL-ABEND                                 11040082
           END-EVALUATE.                                                11050082
                                                                        11060082
      *4200-EXIT.                                                       11070082
      ******************************************************************11080082
      *  OBTAINS THE YEAR TO DATE DATE TOTALS FOR A VARIABLE RANGE      11090082
      *  OF MESSAGE IDS.                                               *11100082
      ******************************************************************11110082
                                                                        11120082
       4300-GET-YTD-VALUES.                                             11130082
           EXEC SQL                                                     11140082
               SELECT COUNT(*)                                          11150082
               INTO :PV-SUMMARY-YTD                                     11160082
               FROM EPT_CALL_BOX D                                      11170082
               WHERE D.CALL_DTE BETWEEN :PV-CALL-BOX-YTD                11180082
                                    AND :DPG52-LK-DATE-INPUT            11190082
                 AND D.MSG_ID BETWEEN :PV-MSG-ID-START                  11200082
                                  AND :PV-MSG-ID-FINISH                 11210082
               WITH UR                                                  11220082
           END-EXEC.                                                    11230082
           EVALUATE TRUE                                                11240082
              WHEN SQLCODE = ZERO                                       11250082
                 SET PS-SUM-YEARLY-VALUE-Y  TO TRUE                     11260082
              WHEN OTHER                                                11270082
                 MOVE SQLCODE    TO PV-DB2-OPERATION                    11280082
                 MOVE '4300-GET-YTD-VALUES' TO PV-PARAGRAPH-NAME        11290082
                 DISPLAY 'PV-CALL-BOX-WTD: ' PV-CALL-BOX-WTD            11300082
                 DISPLAY 'DPG52-LK-DATE-INPUT: ' DPG52-LK-DATE-INPUT    11310082
                 DISPLAY 'PV-MSG-ID-START: ' PV-MSG-ID-START            11320082
                 DISPLAY 'PV-MSG-ID-FINISH: ' PV-MSG-ID-FINISH          11330082
                 PERFORM 9800-SQL-ABEND                                 11340082
           END-EVALUATE.                                                11350082
                                                                        11360082
      *4300-EXIT.                                                       11370082
      ******************************************************************11380082
      *  OBTAINS THE DAILY TOTALS FOR A VARIABLE RANGE                  11390082
      *  OF MESSAGE IDS.                                                11400082
      ******************************************************************11410082
       4400-GET-DAY-VALUES.                                             11420082
           EXEC SQL                                                     11430082
               SELECT COUNT(*)                                          11440082
               INTO :PV-SUMMARY-DAY                                     11450082
               FROM EPT_CALL_BOX D                                      11460082
               WHERE D.CALL_DTE = :DPG52-LK-DATE-INPUT                  11470082
                 AND D.MSG_ID BETWEEN :PV-MSG-ID-START                  11480082
                                  AND :PV-MSG-ID-FINISH                 11490082
               WITH UR                                                  11500082
           END-EXEC.                                                    11510082
           EVALUATE TRUE                                                11520082
              WHEN SQLCODE = ZERO                                       11530082
                 SET PS-SUM-DAILY-VALUE-Y  TO TRUE                      11540082
              WHEN OTHER                                                11550082
                 MOVE SQLCODE    TO PV-DB2-OPERATION                    11560082
                 MOVE '4400-GET-DAY-VALUES' TO PV-PARAGRAPH-NAME        11570082
                 DISPLAY 'PV-CALL-BOX-WTD: ' PV-CALL-BOX-WTD            11580082
                 DISPLAY 'DPG52-LK-DATE-INPUT: ' DPG52-LK-DATE-INPUT    11590082
                 DISPLAY 'PV-MSG-ID-START: ' PV-MSG-ID-START            11600082
                 DISPLAY 'PV-MSG-ID-FINISH: ' PV-MSG-ID-FINISH          11610082
                 PERFORM 9800-SQL-ABEND                                 11620082
           END-EVALUATE.                                                11630082
                                                                        11640082
      *4400-EXIT.                                                       11650082
                                                                        11660082
      ******************************************************************11670082
      *  CLEAN UP FILES                                                 11680082
      ******************************************************************11690082
       8000-END-OF-JOB.                                                 11700082
                                                                        11710082
           CLOSE CURRENT-DATE.                                          11720082
           CLOSE MESSAGE-ID.                                            11730082
           CLOSE TERRITORY-1-EXTRACT-FILE.                              11740082
           CLOSE TERRITORY-2-EXTRACT-FILE.                              11750082
           CLOSE TERRITORY-3-EXTRACT-FILE.                              11760082
           CLOSE TERRITORY-4-EXTRACT-FILE.                              11770082
           CLOSE SUMMARY-FILE.                                          11780082
                                                                        11790082
           PERFORM 8100-CLOSE-CALL-BOX-CURSOR.                          11800082
                                                                        11810082
           DISPLAY '****************************************'           11820082
           DISPLAY '*************** EPKUT080 ***************'           11830082
           DISPLAY '****************************************'           11840082
           MOVE PV-CALL-BOX-CURSOR-COUNT                                11850082
                                       TO PV-EDIT-MASK                  11860082
           INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'          11870082
           DISPLAY 'DEPOSIT ROWS FETCHED...........'                    11880082
               PV-EDIT-MASK                                             11890082
           MOVE PV-RECORDS-WRITTEN     TO PV-EDIT-MASK                  11900082
           INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'          11910082
           DISPLAY 'EXTRACT RECORDS WRITTEN........'                    11920082
               PV-EDIT-MASK                                             11930082
           MOVE PV-RECORDS-WRITTEN-T1  TO PV-EDIT-MASK                  11940082
           INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'          11950082
           DISPLAY 'EXTRACT RECORDS WRITTEN TERR 1.'                    11960082
               PV-EDIT-MASK                                             11970082
           MOVE PV-RECORDS-WRITTEN-T2  TO PV-EDIT-MASK                  11980082
           INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'          11990082
           DISPLAY 'EXTRACT RECORDS WRITTEN TERR 2.'                    12000082
               PV-EDIT-MASK                                             12010082
           MOVE PV-RECORDS-WRITTEN-T3  TO PV-EDIT-MASK                  12020082
           INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'          12030082
           DISPLAY 'EXTRACT RECORDS WRITTEN TERR 3.'                    12040082
               PV-EDIT-MASK                                             12050082
           MOVE PV-RECORDS-WRITTEN-T4  TO PV-EDIT-MASK                  12060082
           INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'          12070082
           DISPLAY 'EXTRACT RECORDS WRITTEN TERR 4.'                    12080082
               PV-EDIT-MASK                                             12090082
           DISPLAY '****************************************'           12100082
           DISPLAY '****************************************'           12110082
           DISPLAY '****************************************'.          12120082
                                                                        12130082
           MOVE ZERO                     TO RETURN-CODE.                12140082
      ******************************************************************12150082
      *    CLOSE THE CALL BOX CURSOR AND CHECK FOR ERRORS.             *12160082
      ******************************************************************12170082
                                                                        12180082
       8100-CLOSE-CALL-BOX-CURSOR.                                      12190082
                                                                        12200082
           EXEC SQL                                                     12210082
                CLOSE CALL-BOX-CURSOR                                   12220082
           END-EXEC.                                                    12230082
                                                                        12240082
           EVALUATE TRUE                                                12250082
               WHEN SQLCODE = ZERO                                      12260082
                    CONTINUE                                            12270082
               WHEN SQLWARN0 NOT EQUAL SPACE                            12280082
               WHEN SQLCODE NOT EQUAL ZERO                              12290082
                   MOVE '8100-CLOSE-CALL-BOX-CURSOR'                    12300082
                                       TO PV-PARAGRAPH-NAME             12310082
                   MOVE 'ERROR ON CLOSE OF CALL-BOX-CURSOR'             12320082
                                       TO PV-DB2-OPERATION              12330082
                   SET AC-DB2-ERROR    TO TRUE                          12340082
                   PERFORM 9800-SQL-ABEND                               12350082
           END-EVALUATE.                                                12360082
                                                                        12370082
      ***************************************************************** 12380082
      * THIS MODULE WRITES THE OUTPUT FILE RECORD.                      12390082
      ***************************************************************** 12400082
       9000-WRITE-EXTRACT-RECORDS.                                      12410082
                                                                        12420082
           EVALUATE PV-TERR-NBR-X                                       12430082
             WHEN '01'                                                  12440082
               ADD 1                  TO PV-RECORDS-WRITTEN-T1          12450082
               WRITE TERRITORY-1-EXTRACT-RECORD                         12460082
                   FROM CALL-BOX-EXTRACT                                12470082
                                                                        12480082
             WHEN '02'                                                  12490082
               ADD 1                  TO PV-RECORDS-WRITTEN-T2          12500082
               WRITE TERRITORY-2-EXTRACT-RECORD                         12510082
                   FROM CALL-BOX-EXTRACT                                12520082
                                                                        12530082
             WHEN '03'                                                  12540082
               ADD 1                  TO PV-RECORDS-WRITTEN-T3          12550082
               WRITE TERRITORY-3-EXTRACT-RECORD                         12560082
                   FROM CALL-BOX-EXTRACT                                12570082
                                                                        12580082
             WHEN OTHER                                                 12590082
               ADD 1                  TO PV-RECORDS-WRITTEN-T4          12600082
               WRITE TERRITORY-4-EXTRACT-RECORD                         12610082
                   FROM CALL-BOX-EXTRACT                                12620082
           END-EVALUATE.                                                12630082
                                                                        12640082
           ADD 1                       TO PV-RECORDS-WRITTEN.           12650082
                                                                        12660082
                                                                        12670082
      ***************************************************************** 12680082
      * THIS MODULE WRITES THE OUTPUT SUMMARY RECORD.                   12690082
      ***************************************************************** 12700082
       9100-WRITE-SUMMARY-RECORDS.                                      12710082
                                                                        12720082
           MOVE PV-SUMMARY-DAY     TO SUMMARY-DAY.                      12730082
           MOVE PV-SUMMARY-WTD     TO SUMMARY-WEEK.                     12740082
           MOVE PV-SUMMARY-MTD     TO SUMMARY-MONTH.                    12750082
           MOVE PV-SUMMARY-YTD     TO SUMMARY-YEAR.                     12760082
                                                                        12770082
           WRITE SUMMARY-RECORD                                         12780082
               FROM SUMMARY-VALUES-EXTRACT.                             12790082
                                                                        12800082
      ***************************************************************** 12810082
      * THIS MODULE WRITES THE OUTPUT SUMMARY RECORD.                   12820082
      ***************************************************************** 12830082
       9110-WRITE-OTH-SUMMARY-RECORDS.                                  12840082
                                                                        12850082
           MOVE PC-SUMMARY-OTHER    TO SUMMARY-NAME.                    12860082
           MOVE PV-OTHER-TOTALS-DAY TO SUMMARY-DAY.                     12870082
           MOVE PV-OTHER-TOTALS-WTD TO SUMMARY-WEEK.                    12880082
           MOVE PV-OTHER-TOTALS-MTD TO SUMMARY-MONTH.                   12890082
           MOVE PV-OTHER-TOTALS-YTD TO SUMMARY-YEAR.                    12900082
                                                                        12910082
           WRITE SUMMARY-RECORD                                         12920082
               FROM SUMMARY-VALUES-EXTRACT.                             12930082
                                                                        12940082
      ******************************************************************12950082
      * 9800-SQL-ABEND.                                                 12960082
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.12970082
      ******************************************************************12980082
       9800-SQL-ABEND.                                                  12990082
           DISPLAY '** ABEND     **'.                                   13000082
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        13010082
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              13020082
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               13030082
                                                                        13040082
           COPY DPPD004.                                                13050082
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         13060082
                                                                        13070082
      *9800-EXIT.                                                       13080082
                                                                        13090082
      ******************************************************************13100082
      * 9999-ABEND                                                      13110082
      *     ABEND PROGRAM WHEN AN ERROR OCCURS.                         13120082
      ******************************************************************13130082
       9999-ABEND.                                                      13140082
           DISPLAY '** ABEND           **'.                             13150082
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  13160082
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        13170082
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       13180082
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       13190082
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         13200082
