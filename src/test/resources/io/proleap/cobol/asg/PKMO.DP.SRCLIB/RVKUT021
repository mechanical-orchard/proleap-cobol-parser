000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.             RVKUT021.                                        
000400 AUTHOR.                 VIRGENE LENNEMAN.                                
000500 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000600 DATE-WRITTEN.           NOVEMBER 2006.                                   
000700 DATE-COMPILED.                                                           
000800                                                                          
000900*                                                                         
001000*  PROGRAM NARRATIVE:                                                     
001100*  THIS PROGRAM WILL CREATE A FILE CONTAINING VENDOR                      
001200*  AGREEMENTS FOR GENCO.                                                  
001300*                                                                         
001400*                                                                         
001500 ENVIRONMENT DIVISION.                                                    
001600                                                                          
001700 CONFIGURATION SECTION.                                                   
001800                                                                          
001900 SOURCE-COMPUTER.                        IBM-3090.                        
002000 OBJECT-COMPUTER.                        IBM-3090.                        
002100                                                                          
002200 INPUT-OUTPUT SECTION.                                                    
002300                                                                          
002400 FILE-CONTROL.                                                            
002500                                                                          
002600     SELECT  RV-VNDR-AGR-FILE                                             
002700         ASSIGN TO OUT01.                                                 
002800                                                                          
002900     SELECT  RV-MARKER-FILE                                               
003000         ASSIGN TO OUT02.                                                 
003100                                                                          
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700*                                                                         
003800 FD  RV-VNDR-AGR-FILE                                                     
003900     RECORDING MODE IS F                                                  
004000     LABEL RECORDS ARE STANDARD                                           
004100     BLOCK CONTAINS 0 RECORDS.                                            
004200 01  RV-VNDR-AGR-RECORD                 PIC  X(53).                       
004300*                                                                         
004400*                                                                         
004500 FD  RV-MARKER-FILE                                                       
004600     RECORDING MODE IS F                                                  
004700     LABEL RECORDS ARE STANDARD                                           
004800     BLOCK CONTAINS 0 RECORDS.                                            
004900 01  RV-MARKER-RECORD                   PIC  X(78).                       
005000*                                                                         
005100*                                                                         
005200 WORKING-STORAGE SECTION.                                                 
000250***************************************************************** 00025015
000260*    INPUT CONTROL CARD                                           00026015
000270***************************************************************** 00027015
000054 01  CC-CONTROL-CARD.                                                     
000055     05  CC-SCHEDULE-DATE.                                                
000056         10  CC-CCYY                 PIC X(04).                           
000057         10  CC-MM                   PIC X(02).                           
000058         10  CC-DD                   PIC X(02).                           
000059                                                                          
005300*****************************************************************         
005400*    DATABASE KEYS                                                        
005500*****************************************************************         
005600 01  DK-KEYS.                                                             
005700     05  DK-ENT-ID                    PIC S9(09)  VALUE +0 COMP.          
005800     05  DK-DEPT-NBR                  PIC  X(03)  VALUE SPACES.           
006000                                                                          
005300*****************************************************************         
005400*    PROGRAM CONSTANTS                                                    
005500*****************************************************************         
005600 01  PC-CONSTANTS.                                                        
005700     05  PC-CURRENT-PGM-RVKUT021      PIC  X(08) VALUE 'RVKUT021'.        
005800     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
005900                                                 COMP SYNC.               
006000                                                                          
006100*****************************************************************         
006200*    PROGRAM SWITCHES                                                     
006300*****************************************************************         
006400 01  PS-SWITCHES.                                                         
006500     05  PS-END-CURSOR-SWITCH         PIC  X(01) VALUE 'N'.               
006600         88  PS-END-CSR                          VALUE 'Y'.               
006700         88  PS-NOT-END-CSR                      VALUE 'N'.               
006500     05  PS-DATA-FOUND-SWITCH         PIC  X(01) VALUE 'Y'.               
006600         88  PS-DATA-FOUND                       VALUE 'Y'.               
006700         88  PS-DATA-NOT-FOUND                   VALUE 'N'.               
006500     05  PS-DATA-FOUND-SWITCH-A1      PIC  X(01) VALUE 'Y'.               
006600         88  PS-DATA-FOUND-A1                    VALUE 'Y'.               
006700         88  PS-DATA-NOT-FOUND-A1                VALUE 'N'.               
006500     05  PS-DATA-FOUND-SWITCH-A3      PIC  X(01) VALUE 'Y'.               
006600         88  PS-DATA-FOUND-A3                    VALUE 'Y'.               
006700         88  PS-DATA-NOT-FOUND-A3                VALUE 'N'.               
006500     05  PS-DATA-FOUND-SWITCH-A5      PIC  X(01) VALUE 'Y'.               
006600         88  PS-DATA-FOUND-A5                    VALUE 'Y'.               
006700         88  PS-DATA-NOT-FOUND-A5                VALUE 'N'.               
006800                                                                          
006900*****************************************************************         
007000*    PROGRAM VARIABLES                                                    
007100*****************************************************************         
007200 01  PV-VARIABLES.                                                        
007300     05  PV-RETRY-COUNT               PIC S9(04) VALUE +0                 
007400                                                 COMP SYNC.               
007500     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
007600                                                 COMP SYNC.               
007700     05  PV-PARAGRAPH                 PIC  X(30) VALUE SPACES.            
007800     05  PV-DB2-FUNCTION              PIC  X(30) VALUE SPACES.            
007900     05  PV-ABEND-STRUCTURE-1         PIC  X(20) VALUE SPACES.            
008000     05  PV-ABEND-STRUCTURE-2         PIC  X(20) VALUE SPACES.            
008000     05  PV-ABEND-STRUCTURE-3         PIC  X(20) VALUE SPACES.            
008100     05  PV-ABEND-MESSAGE             PIC  X(50) VALUE SPACES.            
008200     05  PV-RECS-WRITTEN              PIC  9(09) VALUE ZERO.              
005700     05  PV-ENT-ID                    PIC  9(09) VALUE ZERO.              
009000     05  PV-RUN-DATE-GENCO            PIC  X(08) VALUE SPACES.            
009400     05  PV-RUN-TIME-GENCO            PIC  X(06) VALUE SPACES.            
010500***** FILE NAME CONSISTS OF RUN-DATE-VNDAGRMT.DAT                         
010600*****     EXAMPLE: 20061107_VNDAGRMT.DAT                                  
010700     05  PV-FILE-NAME.                                                    
010800         10  PV-CREATE-DATE           PIC  X(08) VALUE SPACES.            
010900         10  PV-UNDERSCORE            PIC  X(01) VALUE '_'.               
011000         10  PV-VNDAGRMT-DAT          PIC  X(12) VALUE SPACES.            
010700     05  PV-DEPT-NBR.                                                     
010800         10  FILLER                   PIC  X(01) VALUE ZERO.              
010900         10  PV-DEPT                  PIC  X(03) VALUE SPACES.            
011000         10  FILLER                   PIC  X(06) VALUE SPACES.            
011100                                                                          
011200***  MARKER FILE LAYOUT                                                   
011300     COPY RVRD029.                                                        
011400                                                                          
011500***  VENDOR AGREEMENT FILE LAYOUT                                         
011600     COPY RVRD021.                                                        
011700                                                                          
000418***  DATE ROUTINE COPYBOOK                                        00041815
000419     COPY DPWS500.                                                00041915
000420                                                                  00042015
011800***  DB2 ABEND COPYBOOK                                                   
011900     COPY DPWS004.                                                        
012000                                                                          
012100     EXEC SQL                                                             
012200         INCLUDE SQLCA                                                    
012300     END-EXEC.                                                            
012400                                                                          
012500     COPY SQLCA2.                                                         
012600                                                                          
012700***  EXTERNAL ENTITY                                                      
012800     EXEC SQL                                                             
012900         INCLUDE TEXTENT                                                  
013000     END-EXEC.                                                            
013100                                                                          
013200***  ENTITY NAME                                                          
013300     EXEC SQL                                                             
013400         INCLUDE TENTNME                                                  
013500     END-EXEC.                                                            
013600                                                                          
012700***  VENDOR AGREEMENT                                                     
012800     EXEC SQL                                                             
012900         INCLUDE TVNDAGR                                                  
013000     END-EXEC.                                                            
013100                                                                          
012700***  VENDOR DEPARTMENT                                                    
012800     EXEC SQL                                                             
012900         INCLUDE TVNDDPT                                                  
013000     END-EXEC.                                                            
013100                                                                          
013200***  VENDOR FREIGHT TERMS                                                 
013300     EXEC SQL                                                             
013400         INCLUDE TFRGTRM                                                  
013500     END-EXEC.                                                            
013600                                                                          
013700***  EXTRACT CURSOR                                                       
013800     EXEC SQL                                                             
013900         DECLARE EXTRACT-CSR CURSOR FOR                                   
014000         SELECT A.ENT_ID                                                  
014000              , C.DEPT_NBR                                                
014300           FROM                                                           
014400                TEXTENT  A                                                
014500              , TENTNME  B                                                
014500              , TVNDDPT  C                                                
014600           WHERE A.ENT_ID           = B.ENT_ID                            
014700             AND A.MDSE_VENDOR_IND = 'Y'                                  
014900             AND B.TYPE_CDE         = '01'                                
014900             AND A.ENT_ID           = C.ENT_ID                            
015000        ORDER BY A.ENT_ID, C.DEPT_NBR                                     
015000        WITH UR                                                           
015100     END-EXEC.                                                            
015200                                                                          
013500                                                                          
013500                                                                          
015300 LINKAGE SECTION.                                                         
015400                                                                          
015500 PROCEDURE DIVISION.                                                      
015600*****************************************************************         
015700*  MAINLINE LOGIC FOR PROGRAM                                             
015800*****************************************************************         
015900 0000-MAINLINE.                                                           
016000     PERFORM 1000-INITIALIZE.                                             
016100                                                                          
016200     PERFORM 2000-PROCESS                                                 
016300         UNTIL PS-END-CSR.                                                
016400                                                                          
016500     PERFORM 9000-EOJ.                                                    
016600                                                                          
016700     STOP RUN.                                                            
016800                                                                          
016900*****************************************************************         
017000*  INITIALIZATION PARAGRAPH                                               
017100*****************************************************************         
017200 1000-INITIALIZE.                                                         
017300     OPEN OUTPUT RV-VNDR-AGR-FILE                                         
017400                 RV-MARKER-FILE.                                          
017500                                                                          
000126     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
000127                                                                          
000128     DISPLAY 'CONTROL CARD ====>  ' CC-CONTROL-CARD.                      
000129                                                                          
000130     PERFORM 1100-VALIDATE-CONTROL-CARD.                                  
000131                                                                          
000132******* TIME IS CONTAINED CURRENT-DATE IN POSITION 9 FOR 8                
000133     MOVE FUNCTION CURRENT-DATE(9:8) TO PV-RUN-TIME-GENCO.                
000135                                                                          
000136     DISPLAY '******  RUN TIME  => '  PV-RUN-TIME-GENCO.                  
000137                                                                          
019500     PERFORM 7000-OPEN-EXTRACT-CSR.                                       
019600     PERFORM 7100-FETCH-EXTRACT-CSR.                                      
019700                                                                          
000142******************************************************************        
000143**  VALIDATE THE DATE FROM THE SCHEDULE                                   
000144******************************************************************        
000145 1100-VALIDATE-CONTROL-CARD.                                              
000146     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
000147     SET DPG51-FISCAL-454-CALENDAR                                        
000148         DPG51-DO-NOT-INCR-DECR-DATE                                      
000149         DPG52-LK-DTE-ISO           TO TRUE.                              
000150                                                                          
000151     MOVE CC-SCHEDULE-DATE     TO DPG52-LK-DATE-INPUT.                    
000152                                                                          
000153     CALL DP500-KOHLS-CALENDAR-ROUTINE USING                              
000154             DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
000155                                                                          
000156     IF DPG54-NO-ERROR                                                    
000157         MOVE CC-SCHEDULE-DATE     TO PV-RUN-DATE-GENCO                   
000158     ELSE                                                                 
000159         DISPLAY '** ABEND **'                                            
000160         DISPLAY '** PROGRAM = RVKUT021 -- CALLED MODULE '                
000161                  'DPKUT500 **'                                           
000162         DISPLAY '** PARAGRAPH = 1100-VALIDATE-CONTROL-CARD **'           
000163         DISPLAY '** INVALID SCHEDULE DATE: ' CC-SCHEDULE-DATE            
000164         DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '                
000165         DISPLAY DPG54-ERROR-MESSAGE                                      
000166         MOVE +4003 TO PV-ABEND-CODE                                      
000167         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
000168     END-IF.                                                              
000169                                                                          
019800*****************************************************************         
019900* PROCESS DATA FROM CURSOR AND WRITE OUTPUT RECORD                        
020000*****************************************************************         
020100 2000-PROCESS.                                                            
020200     INITIALIZE RV021-VNDAGR-RECORD.                                      
020310     MOVE 'A'                      TO RV021-ACD-INDICATOR.                
020400     MOVE EXTENT-ENT-ID            TO RV021-VENDOR-NUMBER.                
020400     MOVE VNDDPT-DEPT-NBR          TO PV-DEPT.                            
020400     MOVE PV-DEPT-NBR              TO RV021-DEPT-NBR.                     
021100     MOVE 'X'                      TO RV021-ANCHOR-CONSTANT.              
000169                                                                          
000169     PERFORM 7300-GET-AGREEMENT-1.                                        
000169     IF PS-DATA-FOUND-A1                                                  
021000         COMPUTE RV021-KOHLS-FRGT-PCT =                                   
021000                        FRGTRM-KOHLS-FRGT-PCT * 100                       
021000         COMPUTE RV021-VND-FRGT-PCT   =                                   
021000                        FRGTRM-VND-FRGT-PCT * 100                         
000169     ELSE                                                                 
021000         MOVE ZERO                 TO RV021-KOHLS-FRGT-PCT                
021000                                      RV021-VND-FRGT-PCT                  
000169     END-IF.                                                              
000169                                                                          
000169     PERFORM 7400-GET-AGREEMENT-3.                                        
000169     IF PS-DATA-FOUND-A3                                                  
020600         MOVE VNDAGR-RTV-RA-REQD-IND    TO RV021-RTV-RA-REQD-IND          
020600         MOVE VNDAGR-RTV-NO-RA-REQD-IND TO                                
020600                                        RV021-RTV-NO-RA-REQD-IND          
020700         MOVE VNDAGR-DEBIT-DESTROY-IND  TO RV021-DEBIT-DESTROY-IND        
020800         MOVE VNDAGR-MARKOUT-IND        TO RV021-MARKOUT-IND              
020900         MOVE VNDAGR-VND-AUTH-ID        TO RV021-RETURN-AUTH-ID           
000169     ELSE                                                                 
020600         MOVE SPACES                TO RV021-RTV-RA-REQD-IND              
020600                                       RV021-RTV-NO-RA-REQD-IND           
020700                                       RV021-DEBIT-DESTROY-IND            
020800                                       RV021-MARKOUT-IND                  
020900                                       RV021-RETURN-AUTH-ID               
000169     END-IF.                                                              
000169*** NEGOTIATED PCT IS USED INCORRECTLY IN RLOG                            
000169*** DECISION MADE 9/24/2007 -- SEND ZERO IN THIS FIELD                    
000169**   PERFORM 7500-GET-AGREEMENT-5-7.                                      
000169**   IF PS-DATA-FOUND-A5                                                  
020600**       COMPUTE RV021-NEGOTIATED-PCT = VNDAGR-DISCOUNT-PCT * 100         
000169**   ELSE                                                                 
020600         MOVE ZERO                  TO RV021-NEGOTIATED-PCT               
000169**   END-IF.                                                              
000169*** NEGOTIATED PCT IS USED INCORRECTLY IN RLOG                            
000169*** DECISION MADE 9/24/2007 -- SEND ZERO IN THIS FIELD                    
000169                                                                          
021300     PERFORM  8000-WRITE-OUTPUT.                                          
021400                                                                          
021500     PERFORM 7100-FETCH-EXTRACT-CSR.                                      
021600                                                                          
021800******************************************************************        
021900* OPEN CURSOR                                                             
022000******************************************************************        
022100 7000-OPEN-EXTRACT-CSR.                                                   
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0))                                              
022800                                                                          
022900         EXEC SQL                                                         
023000              OPEN EXTRACT-CSR                                            
023100         END-EXEC                                                         
023200                                                                          
023300         EVALUATE TRUE                                                    
023400             WHEN SQLCODE = +0                                            
023500             WHEN SQLCODE = -904                                          
023600             WHEN SQLCODE = -911                                          
023700             WHEN SQLCODE = -913                                          
023800                 CONTINUE                                                 
023900             WHEN SQLWARN0 NOT EQUAL SPACE                                
024000             WHEN SQLCODE NOT EQUAL ZERO                                  
024100                 DISPLAY '*****  ABEND *****'                             
024200                 DISPLAY '** PROGRAM = RVKUT021 '                         
024300                 DISPLAY '** PARAGRAPH = 7000-OPEN-EXTRACT-CSR'           
024400                 MOVE '7000-OPEN-EXTRACT-CSR' TO PV-PARAGRAPH             
024500                 MOVE 'OPEN CURSOR          ' TO PV-DB2-FUNCTION          
024600                 MOVE 'TEXTENT'         TO PV-ABEND-STRUCTURE-1           
024700                 MOVE 'TENTNME'         TO PV-ABEND-STRUCTURE-2           
024800                 PERFORM 9200-SQL-ERROR                                   
024900         END-EVALUATE                                                     
025000     END-PERFORM.                                                         
025100                                                                          
025200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025300         MOVE '7000-OPEN-EXTRACT-CSR'  TO PV-PARAGRAPH                    
025400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
025500         MOVE 'TEXTENT'                TO PV-ABEND-STRUCTURE-1            
025600         MOVE 'TENTNME'                TO PV-ABEND-STRUCTURE-2            
025700         PERFORM 9200-SQL-ERROR                                           
025800     END-IF.                                                              
025900                                                                          
030300******************************************************************        
030400*  FETCH CURSOR                                                           
030500******************************************************************        
030600 7100-FETCH-EXTRACT-CSR.                                                  
030700     PERFORM                                                              
030800         WITH TEST AFTER                                                  
030900         VARYING PV-RETRY-COUNT                                           
031000         FROM 1 BY 1                                                      
031100         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
031200         OR  (SQLCODE = +0) OR (PS-END-CSR))                              
031300                                                                          
031400         EXEC SQL                                                         
031500              FETCH EXTRACT-CSR                                           
031600              INTO :EXTENT-ENT-ID                                         
031600                 , :VNDDPT-DEPT-NBR                                       
031900         END-EXEC                                                         
032000                                                                          
032100         EVALUATE TRUE                                                    
032200             WHEN SQLCODE = ZERO                                          
032300                  MOVE EXTENT-ENT-ID   TO DK-ENT-ID                       
032300                  MOVE VNDDPT-DEPT-NBR TO DK-DEPT-NBR                     
032300             WHEN SQLCODE = -904                                          
032400             WHEN SQLCODE = -911                                          
032500             WHEN SQLCODE = -913                                          
032600                 CONTINUE                                                 
032700             WHEN SQLCODE = +100                                          
032800                 SET PS-END-CSR TO TRUE                                   
032900             WHEN SQLWARN0 NOT EQUAL SPACE                                
033000             WHEN SQLCODE NOT EQUAL ZERO                                  
033100                 DISPLAY '** ABEND **'                                    
033200                 DISPLAY '** PROGRAM = RVKUT021 **'                       
033300                 DISPLAY '** PARAGRAPH = 7100-FETCH-EXTRACT-CSR'          
033300                 MOVE DK-ENT-ID TO PV-ENT-ID                              
033300                 DISPLAY '** ENT-ID    = ' PV-ENT-ID                      
033300                 DISPLAY '** DEPT      = ' DK-DEPT-NBR                    
033400                 MOVE '7100-FETCH-EXTRACT-CSR' TO PV-PARAGRAPH            
033500                 MOVE 'FETCH FROM CURSOR     ' TO PV-DB2-FUNCTION         
033600                 MOVE 'TEXTENT'         TO PV-ABEND-STRUCTURE-1           
033700                 MOVE 'TENTNME'         TO PV-ABEND-STRUCTURE-2           
033700                 MOVE 'TVNDAGR'         TO PV-ABEND-STRUCTURE-3           
033800                 PERFORM 9200-SQL-ERROR                                   
033900         END-EVALUATE                                                     
034000     END-PERFORM.                                                         
034100                                                                          
034200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
034300         MOVE '7100-FETCH-EXTRACT-CSR' TO PV-PARAGRAPH                    
034400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
034500         MOVE 'TEXTENT'                TO PV-ABEND-STRUCTURE-1            
034600         MOVE 'TENTNME'                TO PV-ABEND-STRUCTURE-2            
034700         PERFORM 9200-SQL-ERROR                                           
034800     END-IF.                                                              
034900                                                                          
026100******************************************************************        
026200* CLOSE CURSOR                                                            
026300******************************************************************        
026400 7200-CLOSE-EXTRACT-CSR.                                                  
026500     PERFORM                                                              
026600         WITH TEST AFTER                                                  
026700         VARYING PV-RETRY-COUNT                                           
026800         FROM 1 BY 1                                                      
026900         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
027000         OR  (SQLCODE = +0))                                              
027100                                                                          
027200         EXEC SQL                                                         
027300             CLOSE EXTRACT-CSR                                            
027400         END-EXEC                                                         
027500                                                                          
027600         EVALUATE TRUE                                                    
027700             WHEN SQLCODE = +0                                            
027800             WHEN SQLCODE = -904                                          
027900             WHEN SQLCODE = -911                                          
028000             WHEN SQLCODE = -913                                          
028100                 CONTINUE                                                 
028200             WHEN SQLWARN0 NOT EQUAL SPACE                                
028300             WHEN SQLCODE NOT EQUAL ZERO                                  
028400                 DISPLAY '** ABEND **'                                    
028500                 DISPLAY '** PROGRAM = RVKUT021 **'                       
028600                 DISPLAY '** PARAGRAPH = 7200-CLOSE-EXTRACT-CSR'          
028700                 MOVE '7200-CLOSE-EXTRACT-CSR' TO PV-PARAGRAPH            
028800                 MOVE 'CLOSE CURSOR          ' TO PV-DB2-FUNCTION         
028900                 MOVE 'TEXTENT'         TO PV-ABEND-STRUCTURE-1           
029000                 MOVE 'TENTNME'         TO PV-ABEND-STRUCTURE-2           
029100                 PERFORM 9200-SQL-ERROR                                   
029200         END-EVALUATE                                                     
029300     END-PERFORM.                                                         
029400                                                                          
029500     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
029600         MOVE '7200-CLOSE-EXTRACT-CSR' TO PV-PARAGRAPH                    
029700         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
029800         MOVE 'TEXTENT'                TO PV-ABEND-STRUCTURE-1            
029900         MOVE 'TENTNME'                TO PV-ABEND-STRUCTURE-2            
030000         PERFORM 9200-SQL-ERROR                                           
030100     END-IF.                                                              
030200                                                                          
021800******************************************************************        
021900* GET AGREEMENT 1                                                         
022000******************************************************************        
022100 7300-GET-AGREEMENT-1.                                                    
198300     INITIALIZE DCLTVNDAGR                                                
198300                DCLTFRGTRM.                                               
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0)                                               
022700         OR  (SQLCODE = +100))                                            
022800                                                                          
013800         EXEC SQL                                                         
215500             SELECT COALESCE(FRGTRM.FRGT_TERM_ID,0)                       
215600                  , COALESCE(FRGTRM.KOHLS_FRGT_PCT,0)                     
215700                  , COALESCE(FRGTRM.VND_FRGT_PCT,0)                       
215800               INTO :FRGTRM-FRGT-TERM-ID                                  
215900                  , :FRGTRM-KOHLS-FRGT-PCT                                
216000                  , :FRGTRM-VND-FRGT-PCT                                  
216100               FROM                                                       
216200                   (SELECT A.FRGT_TERM_ID                                 
216300                      FROM TVNDAGR A                                      
216600                     WHERE ENT_ID       = :DK-ENT-ID                      
216600                       AND DEPT_NBR     = :DK-DEPT-NBR                    
216400                       AND AGRMT_TYP_ID = '1'                             
216500                       AND SEQ_NBR =                                      
216800                          (SELECT MAX(SEQ_NBR)                            
216800                             FROM TVNDAGR                                 
216800                            WHERE ENT_ID          = :DK-ENT-ID            
216800                              AND DEPT_NBR        = :DK-DEPT-NBR          
216800                              AND AGRMT_TYP_ID    = '1'))                 
216800                                               AS VT1                     
216800                LEFT OUTER JOIN TFRGTRM FRGTRM                            
216800                     ON FRGTRM.FRGT_TERM_ID = VT1.FRGT_TERM_ID            
015200              WITH UR                                                     
013500         END-EXEC                                                         
021600                                                                          
090400         EVALUATE TRUE                                                    
090500             WHEN SQLCODE = ZERO                                          
090600                 SET PS-DATA-FOUND-A1 TO TRUE                             
090600             WHEN SQLCODE = +100                                          
090600                 SET PS-DATA-NOT-FOUND-A1 TO TRUE                         
090700             WHEN SQLCODE = -904                                          
090800             WHEN SQLCODE = -913                                          
090900                  CONTINUE                                                
091000                                                                          
091100             WHEN SQLWARN0 NOT = SPACES                                   
091200             WHEN SQLCODE NOT = ZERO                                      
028400                 DISPLAY '** ABEND **'                                    
028500                 DISPLAY '** PROGRAM = RVKUT021 **'                       
028600                 DISPLAY '** PARAGRAPH = 7300-GET-AGREEMENT-1'            
033300                 MOVE DK-ENT-ID               TO PV-ENT-ID                
033300                 DISPLAY '** ENT-ID    = '    PV-ENT-ID                   
033300                         '   ** DEPT   = '    DK-DEPT-NBR                 
091300                 MOVE '7300-GET-AGREEMENT-1'  TO PV-PARAGRAPH             
091400                 MOVE 'SELECT SHIPPING TERMS' TO PV-DB2-FUNCTION          
091700                 MOVE 'TVNDAGR'     TO PV-ABEND-STRUCTURE-1               
202900                 MOVE 'TFRGTRM'     TO PV-ABEND-STRUCTURE-2               
092100                 PERFORM 9200-SQL-ERROR                                   
092200         END-EVALUATE                                                     
092300     END-PERFORM.                                                         
092400                                                                          
092500     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
092600         MOVE '7300-GET-AGREEMENT-1' TO PV-PARAGRAPH                      
092700         MOVE 'MAX RETRIES EXCEEDED' TO PV-ABEND-MESSAGE                  
093000         MOVE 'TVNDAGR ' TO PV-ABEND-STRUCTURE-1                          
093000         MOVE 'TFRGTRM ' TO PV-ABEND-STRUCTURE-2                          
093400         PERFORM 9200-SQL-ERROR                                           
093500     END-IF.                                                              
021700                                                                          
021800******************************************************************        
021900*  GET AGREEMENT ID 3 FOR VENDOR/DEPARTMENT -- PURCHASING                 
022000******************************************************************        
021600 7400-GET-AGREEMENT-3.                                                    
198300     INITIALIZE DCLTVNDAGR.                                               
198400                                                                          
198500     PERFORM WITH TEST AFTER                                              
198600         VARYING PV-RETRY-COUNT                                           
198700         FROM 1 BY 1                                                      
198800         UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES                           
198900                 OR SQLCODE = 0                                           
199000                 OR SQLCODE = +100)                                       
199100                                                                          
199200         EXEC SQL                                                         
199300             SELECT COALESCE(RTV_RA_REQD_IND,' ')                         
199400                  , COALESCE(RTV_NO_RA_REQD_IND,' ')                      
199500                  , COALESCE(DEBIT_DESTROY_IND,' ')                       
199600                  , COALESCE(MARKOUT_IND,' ')                             
199700                  , COALESCE(VND_AUTH_ID,' ')                             
199900               INTO :VNDAGR-RTV-RA-REQD-IND                               
200000                  , :VNDAGR-RTV-NO-RA-REQD-IND                            
200100                  , :VNDAGR-DEBIT-DESTROY-IND                             
200200                  , :VNDAGR-MARKOUT-IND                                   
200300                  , :VNDAGR-VND-AUTH-ID                                   
200500               FROM TVNDAGR VNDAGR                                        
200700              WHERE VNDAGR.ENT_ID   = :DK-ENT-ID                          
200800                AND VNDAGR.DEPT_NBR = :DK-DEPT-NBR                        
201100                AND AGRMT_TYP_ID    = '3'                                 
201200                AND (CURRENT_DATE   >= EFFECTIVE_DATE                     
201300                   AND CURRENT_DATE <= EXPIRATION_DATE)                   
201310                AND SEQ_NBR         =                                     
201320                   (SELECT MAX(SEQ_NBR)                                   
201330                      FROM TVNDAGR VNDAGR2                                
201340                     WHERE VNDAGR2.ENT_ID       = VNDAGR.ENT_ID           
201350                       AND VNDAGR2.DEPT_NBR     = VNDAGR.DEPT_NBR         
201360                       AND VNDAGR2.AGRMT_TYP_ID = '3')                    
201400         END-EXEC                                                         
201500                                                                          
201600         EVALUATE TRUE                                                    
201700             WHEN SQLCODE = ZERO                                          
201800                 SET PS-DATA-FOUND-A3   TO TRUE                           
202000             WHEN SQLCODE = +100                                          
202300                 SET PS-DATA-NOT-FOUND-A3 TO TRUE                         
202100             WHEN SQLCODE = -904                                          
202200             WHEN SQLCODE = -913                                          
202300                 CONTINUE                                                 
202400             WHEN SQLWARN0 NOT = SPACES                                   
202500             WHEN SQLCODE NOT EQUAL ZERO                                  
202600                 MOVE '7400-GET-AGREEMENT-3' TO PV-PARAGRAPH              
202700                 MOVE 'SELECT TVNDAGR'       TO PV-DB2-FUNCTION           
202800                 MOVE 'TVNDAGR'         TO PV-ABEND-STRUCTURE-1           
202900                 MOVE SPACES            TO PV-ABEND-STRUCTURE-2           
033300                 MOVE DK-ENT-ID         TO PV-ENT-ID                      
033300                 DISPLAY '** ENT-ID    = ' PV-ENT-ID                      
033300                         '   ** DEPT   = ' DK-DEPT-NBR                    
203100                 PERFORM 9200-SQL-ERROR                                   
203300         END-EVALUATE                                                     
203400     END-PERFORM.                                                         
203500                                                                          
203600     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
024400         MOVE '7400-GET-AGREEMENT-3'       TO PV-PARAGRAPH                
024500         MOVE 'SELECT TVNDAGR - MAX RETRY' TO PV-DB2-FUNCTION             
024600         MOVE 'TVNDAGR'         TO PV-ABEND-STRUCTURE-1                   
024700         MOVE SPACES            TO PV-ABEND-STRUCTURE-2                   
024800         PERFORM 9200-SQL-ERROR                                           
204700     END-IF.                                                              
204800                                                                          
000169*** NEGOTIATED PCT IS USED INCORRECTLY IN RLOG                            
000169*** DECISION MADE 9/24/2007 -- SEND ZERO IN THIS FIELD                    
021800******************************************************************        
021900*  GET AGREEMENT ID 5, TYPE 7 FOR VENDOR/DEPARTMENT -- GET                
021900*         NEGOTIATED PERCENT                                              
022000******************************************************************        
021600*7500-GET-AGREEMENT-5-7.                                                  
198300*    INITIALIZE DCLTVNDAGR.                                               
198400*                                                                         
198500*    PERFORM WITH TEST AFTER                                              
198600*        VARYING PV-RETRY-COUNT                                           
198700*        FROM 1 BY 1                                                      
198800*        UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES                           
198900*                OR SQLCODE = 0                                           
199000*                OR SQLCODE = +100)                                       
199100*                                                                         
199200*        EXEC SQL                                                         
199300*            SELECT COALESCE(DISCOUNT_PCT,0)                              
199900*              INTO :VNDAGR-DISCOUNT-PCT                                  
200500*              FROM TVNDAGR VNDAGR                                        
200700*             WHERE VNDAGR.ENT_ID   = :DK-ENT-ID                          
200800*               AND VNDAGR.DEPT_NBR = :DK-DEPT-NBR                        
201100*               AND AGRMT_TYP_ID    = '5'                                 
201100*               AND TYP_CDE         =  7                                  
201200*               AND (CURRENT_DATE   >= EFFECTIVE_DATE                     
201300*                  AND CURRENT_DATE <= EXPIRATION_DATE)                   
201310*               AND SEQ_NBR         =                                     
201320*                  (SELECT MAX(SEQ_NBR)                                   
201330*                     FROM TVNDAGR VNDAGR2                                
201340*                    WHERE VNDAGR2.ENT_ID       = VNDAGR.ENT_ID           
201350*                      AND VNDAGR2.DEPT_NBR     = VNDAGR.DEPT_NBR         
201360*                      AND VNDAGR2.AGRMT_TYP_ID = '5'                     
201100*                      AND VNDAGR2.TYP_CDE      =  7)                     
201400*        END-EXEC                                                         
201500*                                                                         
201600*        EVALUATE TRUE                                                    
201700*            WHEN SQLCODE = ZERO                                          
201800*                SET PS-DATA-FOUND-A5 TO TRUE                             
202000*            WHEN SQLCODE = +100                                          
202100*            WHEN SQLCODE = -904                                          
202200*            WHEN SQLCODE = -913                                          
202300*                SET PS-DATA-NOT-FOUND-A5 TO TRUE                         
202400*            WHEN SQLWARN0 NOT = SPACES                                   
202500*            WHEN SQLCODE NOT EQUAL ZERO                                  
202600*                MOVE '7500-GET-AGREEMENT-5-7' TO PV-PARAGRAPH            
202700*                MOVE 'SELECT TVNDAGR - NEG %' TO PV-DB2-FUNCTION         
202800*                MOVE 'TVNDAGR'         TO PV-ABEND-STRUCTURE-1           
202900*                MOVE SPACES            TO PV-ABEND-STRUCTURE-2           
033300*                MOVE DK-ENT-ID         TO PV-ENT-ID                      
033300*                DISPLAY '** ENT-ID    = ' PV-ENT-ID                      
033300*                        '   ** DEPT   = ' DK-DEPT-NBR                    
203100*                PERFORM 9200-SQL-ERROR                                   
203300*        END-EVALUATE                                                     
203400*    END-PERFORM.                                                         
203500*                                                                         
203600*    IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
024400*        MOVE '7500-GET-AGREEMENT-5-7'    TO PV-PARAGRAPH                 
024500*        MOVE 'SELECT TVNDAGR - MAX RETRY' TO PV-DB2-FUNCTION             
024600*        MOVE 'TVNDAGR'         TO PV-ABEND-STRUCTURE-1                   
024700*        MOVE SPACES            TO PV-ABEND-STRUCTURE-2                   
024800*        PERFORM 9200-SQL-ERROR                                           
204700*    END-IF.                                                              
000169*** NEGOTIATED PCT IS USED INCORRECTLY IN RLOG                            
000169*** DECISION MADE 9/24/2007 -- SEND ZERO IN THIS FIELD                    
204800                                                                          
021600                                                                          
035000*****************************************************************         
035100*  WRITE OUTPUT FILE                                                      
035200*****************************************************************         
035300 8000-WRITE-OUTPUT.                                                       
035400     WRITE RV-VNDR-AGR-RECORD FROM RV021-VNDAGR-RECORD.                   
035500                                                                          
035600     ADD 1                TO PV-RECS-WRITTEN.                             
035700                                                                          
035800*****************************************************************         
035900*  CREATE MARKER FILE                                                     
036000*****************************************************************         
036100 8500-CREATE-MARKER.                                                      
036200     MOVE PV-RUN-DATE-GENCO TO PV-CREATE-DATE.                            
036300     MOVE 'VNDAGRMT.DAT'    TO PV-VNDAGRMT-DAT.                           
036400     MOVE PV-FILE-NAME    TO RV029-FILE-NAME.                             
036500     MOVE PV-RECS-WRITTEN TO RV029-RECORD-COUNT.                          
036600     MOVE PV-RUN-DATE-GENCO  TO RV029-BUILD-DATE.                         
036700     MOVE PV-RUN-TIME-GENCO  TO RV029-BUILD-TIME.                         
036800     MOVE 'X'             TO RV029-ANCHOR-CONSTANT.                       
036900                                                                          
037000     WRITE RV-MARKER-RECORD FROM RV029-MARKER-RECORD.                     
037100                                                                          
037200*****************************************************************         
037300*  END OF JOB PROCESSING                                                  
037400*****************************************************************         
037500 9000-EOJ.                                                                
037600     DISPLAY 'RECORDS WRITTEN ===>  ' PV-RECS-WRITTEN.                    
037700                                                                          
037800     PERFORM 8500-CREATE-MARKER.                                          
037900                                                                          
038000     PERFORM 7200-CLOSE-EXTRACT-CSR.                                      
038100                                                                          
038200     CLOSE RV-VNDR-AGR-FILE                                               
038300           RV-MARKER-FILE.                                                
038400                                                                          
038500                                                                          
038600*****************************************************************         
038700*  PARAGRAPH EXECUTED WHEN SERIOUS DB2 ERROR OCCURRED                     
038800*****************************************************************         
038900 9200-SQL-ERROR.                                                          
039000     CLOSE RV-VNDR-AGR-FILE                                               
039100                                                                          
039200     MOVE SQLCODE                     TO SQLCODE2.                        
039300     MOVE SQLERRD(3)                  TO SQLERRD3.                        
039400                                                                          
039500     DISPLAY '** ABEND **         ** = SQLERROR'.                         
039600     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PGM-RVKUT021.                
039700     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH.                           
039800     DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                        
039900                                                                          
040000     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
040100     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
040200     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
040300     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
040400                                                                          
040500     COPY DPPD004.                                                        
040600                                                                          
040700     MOVE +4013                       TO PV-ABEND-CODE.                   
040800     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
040900                                                                          
041000*************  END OF RVKUT021  ********************************          
