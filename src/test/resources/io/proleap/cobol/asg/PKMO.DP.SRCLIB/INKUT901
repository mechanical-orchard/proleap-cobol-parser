       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT901.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            02/25/2004.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  COMMON EXTRACT OF INVENTORY PARAMETER TABLE FOR OTHER DOMAINS *        
      *                                                                *        
      *  THIS PROGRAM WILL EXTRACT DATA FROM INVENTORY PARAMETER       *        
      *  TABLE FOR OTHER DOMAINS TO LOAD INTO THEIR VERSION OF THE     *        
      *  TABLE.                                                        *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      *                                                                *        
W33304* WR33304  06-06-08    T.RAMSEYER: HILO - HOLD INVENTORY LEDGER  *        
W33304*                                  OPEN ACROSS PERIODS           *        
W33304*                      ADD TINCNTL TO THE EXTRACT CURSOR. THE    *        
W33304*                      OUTPUT FILE WILL NOW HAVE FIELDS THAT ARE *        
W33304*                      PULLED FROM BOTH TINVPAR AND TINCNTL.     *        
W33304*                      CHANGE 'IND' OF SCHD_PHY_CNT_IND TO 'CDE' *        
W33304*                      SO FIELD IS NOW: SCHD_PHY_CNT_CDE.        *        
W33304*                      ADDED INITS BEFORE FETCHES AND AFTER      *        
W33304*                      WRITES TO PREVENT OVERLAYS.               *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT REQUEST-CNTL-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT INVPAR-COMMON-EXTRACT                                         
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  REQUEST-CNTL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  REQUEST-CNTL-REC                PIC X(80).                           
                                                                                
       FD  INVPAR-COMMON-EXTRACT                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 330 CHARACTERS                                       
           DATA RECORD IS COMMON-EXTRACT-REC.                                   
       01  COMMON-EXTRACT-REC              PIC X(330).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  COMMON EXTRACT DATA LAYOUT                                             
      ******************************************************************        
      *01  IN901-INVPAR-COMMON-EXTRACT.                                         
           COPY INRD901.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-INCOMPLETE-DTE           PIC X(10)       VALUE                
                                                           '9999-09-09'.        
           05  PC-Y                        PIC X           VALUE 'Y'.           
           05  PC-N                        PIC X           VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-REQUEST-CNTL      PIC X           VALUE 'N'.           
               88  END-OF-REQUEST-CNTL                     VALUE 'Y'.           
           05  PS-END-OF-TINVPAR           PIC X           VALUE 'N'.           
               88  END-OF-TINVPAR                          VALUE 'Y'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** REPORT REQUEST CONTROL REC ***'.                                
                                                                                
       01  RQ-CNTL-AREA.                                                        
           05  RQ-COMMENT-IND              PIC X(01)       VALUE SPACE.         
               88  RQ-COMMENTS                             VALUE '*'.           
           05  RQ-TYPE                     PIC X(10)       VALUE SPACES.        
               88  RQ-VALIDS                               VALUES ARE           
                   'ALL       '                                                 
                   'CURRENT   '                                                 
                   'PREVIOUS  '.                                                
               88  RQ-ALL-LOCATIONS                        VALUE                
                   'ALL       '.                                                
               88  RQ-CURR-INVENTORY-LOCATIONS             VALUE                
                   'CURRENT   '.                                                
               88  RQ-PREV-INVENTORY-LOCATIONS             VALUE                
                   'PREVIOUS  '.                                                
           05  FILLER                      PIC X(69)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-RECORD-EXTRACTED         PIC S9(05)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
W33304     05  PV-NULL-INDICATORS.                                              
W33304         10  PV-NULL-IND-CTOFF-IND   PIC S9(4) COMP                       
W33304                                               VALUE 0.                   
W33304         10  PV-NULL-IND-DEL-RSN-TXT PIC S9(4) COMP                       
W33304                                               VALUE 0.                   
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4003                                  VALUE +4003.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
W33304*---------------------------------------------------------------*         
W33304*    TINCNTL - PHYSICAL INVENTORY TABLE (CONTROL)               *         
W33304*---------------------------------------------------------------*         
W33304     EXEC SQL                                                             
W33304         INCLUDE TINCNTL                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
      *---------------------------------------------------------------*         
      *    TINVPAR - PHYSICAL INVENTORY TABLE                         *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TINVPAR - INVENTORY PARAMETER CURSOR FOR ALL LOCATION      *         
      *---------------------------------------------------------------*         
           EXEC SQL   DECLARE TINVPAR-CURSOR CURSOR FOR                         
W33304          SELECT A.INV_ID                                                 
W33304               , A.INV_NM                                                 
W33304               , A.ACTV_IND                                               
W33304               , A.OPN_FSCL_MN_NBR                                        
W33304               , A.OPN_FSCL_MN_DTE                                        
W33304               , B.LOC_NBR                                                
W33304               , B.ACTL_INV_DTE                                           
W33304               , B.PLND_INV_DTE                                           
W33304               , B.SCAN_INV_IND                                           
W33304               , B.ACTL_UNIT_BK_DTE                                       
W33304               , B.ACTL_FIN_BK_DTE                                        
W33304               , B.PLND_UNIT_BK_DTE                                       
W33304               , B.PLND_FIN_BK_DTE                                        
W33304               , B.ACTL_INV_RECD_DTE                                      
W33304               , B.STKRM_DAY_QTY                                          
W33304               , B.CORR_DAY_QTY                                           
W33304               , B.CHG_TMST                                               
W33304               , B.CHG_ID_NBR                                             
W33304               , B.ACTL_LDG_CTOF_TMST                                     
W33304               , B.ACTL_DC_CTOFF_DTE                                      
W33304               , B.ITM_CTOFF_IND                                          
W33304               , B.SHTG_ACTL_INV_DTE                                      
W33304               , B.PHY_CNT_TMST                                           
W33304               , B.SLS_ADJ_CPLD_IND                                       
W33304               , B.SCHD_PHY_CNT_CDE                                       
W33304               , B.XFER_INV_CTOFF_DTE                                     
W33304               , B.SHEET_INV_CPLD_IND                                     
W33304               , B.UNT_BKG_STAT_CDE                                       
W33304               , B.FIL_TOT_BAL_CDE                                        
W33304               , B.STKRM_WDRWL_IND                                        
W33304               , B.MFSTNG_IND                                             
W33304               , B.INV_SRVC_CDE                                           
W33304               , B.JOB_NM                                                 
W33304               , B.SHEET_INV_REQR_IND                                     
W33304               , B.TMST_EDT_STAT_CDE                                      
W33304               , B.INV_CLO_DTE                                            
W33304               , B.FINCL_INV_STAT_CDE                                     
W33304               , B.LOC_INV_STAT_CDE                                       
W33304               , B.LOC_DEL_RSN_TXT                                        
W33304               , B.LOC_DEL_DTE                                            
W33304            FROM TINCNTL A                                                
W33304               , TINVPAR B                                                
W33304           WHERE A.INV_ID = B.INV_ID                                      
W33304        ORDER BY B.ACTL_INV_DTE                                           
W33304               , B.PLND_INV_DTE                                           
W33304               , B.LOC_NBR                                                
W33304        WITH UR                                                           
W33304     END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           IF RQ-VALIDS                                                         
               PERFORM Z100-FETCH-TINVPAR                                       
               PERFORM B200-PROCESS-TINVPAR                                     
                   UNTIL END-OF-TINVPAR                                         
           END-IF.                                                              
                                                                                
           PERFORM B400-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           OPEN INPUT  REQUEST-CNTL-FILE                                        
                OUTPUT INVPAR-COMMON-EXTRACT.                                   
                                                                                
           EXEC SQL                                                             
               OPEN TINVPAR-CURSOR                                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               DISPLAY 'PROCESS DATE ' PV-PROCESSING-DATE                       
               MOVE 'OPEN TINVPAR-CURSOR' TO PV-OPER-ATTEMPTED                  
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           DISPLAY 'EXTRACT REQUESTED:'.                                        
           DISPLAY '  ----+----1----+----2----+----3----+----4'.                
           PERFORM Z300-READ-REQUEST-CNTL.                                      
           DISPLAY '  ' RQ-CNTL-AREA.                                           
                                                                                
           PERFORM                                                              
           UNTIL END-OF-REQUEST-CNTL                                            
               OR NOT RQ-COMMENTS                                               
                                                                                
               PERFORM Z300-READ-REQUEST-CNTL                                   
               DISPLAY '  ' RQ-CNTL-AREA                                        
                                                                                
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      *    PROCESS TINVPAR CURSOR AND WRITE OUT COMMON EXTRACT                  
      ******************************************************************        
       B200-PROCESS-TINVPAR.                                                    
                                                                                
W33304     MOVE INCNTL-INV-ID              TO IN901-INV-ID.                     
W33304     MOVE INCNTL-INV-NM-TEXT         TO IN901-INV-NM-TEXT.                
W33304     MOVE INCNTL-INV-NM-LEN          TO IN901-INV-NM-LEN.                 
W33304     MOVE INCNTL-ACTV-IND            TO IN901-ACTV-IND.                   
W33304     MOVE INCNTL-OPN-FSCL-MN-NBR     TO IN901-OPN-FSCL-MN-NBR.            
W33304     MOVE INCNTL-OPN-FSCL-MN-DTE     TO IN901-OPN-FSCL-MN-DTE.            
           MOVE INVPAR-LOC-NBR             TO IN901-LOC-NBR.                    
           MOVE INVPAR-ACTL-INV-DTE        TO IN901-INV-DTE.                    
           MOVE INVPAR-PLND-INV-DTE        TO IN901-PLND-INV-DTE.               
           MOVE INVPAR-SCAN-INV-IND        TO IN901-SCN-INV-IND.                
           MOVE INVPAR-ACTL-UNIT-BK-DTE    TO IN901-UNIT-BK-DTE.                
           MOVE INVPAR-ACTL-FIN-BK-DTE     TO IN901-FIN-BK-DTE.                 
           MOVE INVPAR-PLND-UNIT-BK-DTE    TO IN901-PLND-UNIT-BK-DTE.           
           MOVE INVPAR-PLND-FIN-BK-DTE     TO IN901-PLND-FIN-BK-DTE.            
           MOVE INVPAR-ACTL-INV-RECD-DTE   TO IN901-INV-RCVD-DTE.               
           MOVE INVPAR-STKRM-DAY-QTY       TO IN901-STKRM-DAY-QTY.              
           MOVE INVPAR-CORR-DAY-QTY        TO IN901-CORR-DAY-QTY.               
           MOVE INVPAR-CHG-TMST            TO IN901-LAST-UPD-TMST.              
           MOVE INVPAR-CHG-ID-NBR          TO IN901-LAST-UPD-BY-USR-ID.         
           MOVE INVPAR-ACTL-LDG-CTOF-TMST  TO IN901-LDGR-CTOFF-TMST.            
           MOVE INVPAR-ACTL-DC-CTOFF-DTE   TO IN901-DC-CTOFF-DTE.               
W33304     MOVE INVPAR-ITM-CTOFF-IND       TO IN901-ITM-CTOFF-IND.              
W33304     MOVE INVPAR-SHTG-ACTL-INV-DTE   TO IN901-SHTG-ACTL-INV-DTE.          
           MOVE INVPAR-PHY-CNT-TMST        TO IN901-PHY-CNT-TMST.               
           MOVE INVPAR-SLS-ADJ-CPLD-IND    TO IN901-SLS-ADJ-CPLD-IND.           
W33304     MOVE INVPAR-SCHD-PHY-CNT-CDE    TO IN901-SCHD-PHY-CNT-CDE.           
           MOVE INVPAR-XFER-INV-CTOFF-DTE  TO IN901-XFER-INV-CTOFF-DTE.         
           MOVE INVPAR-SHEET-INV-CPLD-IND  TO IN901-SHEET-INV-CPLD-IND.         
           MOVE INVPAR-UNT-BKG-STAT-CDE    TO IN901-UNT-BKG-STAT-CDE.           
           MOVE INVPAR-FIL-TOT-BAL-CDE     TO IN901-FIL-TOT-BAL-CDE.            
           MOVE INVPAR-STKRM-WDRWL-IND     TO IN901-STKRM-WDRWL-IND.            
           MOVE INVPAR-MFSTNG-IND          TO IN901-MFSTNG-IND.                 
           MOVE INVPAR-INV-SRVC-CDE        TO IN901-INV-SRVC-CDE.               
W33304     MOVE INVPAR-JOB-NM              TO IN901-LAST-UPD-JOB-NM.            
           MOVE INVPAR-SHEET-INV-REQR-IND  TO IN901-SHEET-INV-REQR-CDE.         
           MOVE INVPAR-TMST-EDT-STAT-CDE   TO IN901-TMST-EDT-STAT-CDE.          
W33304     MOVE INVPAR-INV-CLO-DTE         TO IN901-INV-CLO-DTE.                
W33304     MOVE INVPAR-FINCL-INV-STAT-CDE  TO IN901-FINCL-INV-STAT-CDE.         
W33304     MOVE INVPAR-LOC-INV-STAT-CDE    TO IN901-LOC-INV-STAT-CDE.           
W33304     MOVE INVPAR-LOC-DEL-RSN-TXT     TO IN901-LOC-DEL-RSN-TXT.            
W33304     MOVE INVPAR-LOC-DEL-DTE         TO IN901-LOC-DEL-DTE.                
                                                                                
           PERFORM Z200-WRITE-EXTRACT.                                          
                                                                                
           PERFORM Z100-FETCH-TINVPAR.                                          
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B400-FINALIZE.                                                           
                                                                                
           DISPLAY ' **** RECORDS EXTRACTED: ' PV-RECORD-EXTRACTED.             
                                                                                
           EXEC SQL                                                             
                CLOSE TINVPAR-CURSOR                                            
           END-EXEC                                                             
           IF SQLCODE NOT = +0                                                  
               MOVE 'CLOSE TINVPAR-CURSOR' TO PV-OPER-ATTEMPTED                 
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           CLOSE   REQUEST-CNTL-FILE                                            
                   INVPAR-COMMON-EXTRACT.                                       
                                                                                
      ******************************************************************        
      *    FETCH THE NEXT RECORD FROM TINVPAR CURSOR                            
      ******************************************************************        
       Z100-FETCH-TINVPAR.                                                      
                                                                                
W33304     INITIALIZE DCLTINCNTL                                                
W33304                DCLTINVPAR.                                               
W33304                                                                          
           EXEC SQL FETCH TINVPAR-CURSOR                                        
W33304       INTO  :INCNTL-INV-ID                                               
W33304            ,:INCNTL-INV-NM                                               
W33304            ,:INCNTL-ACTV-IND                                             
W33304            ,:INCNTL-OPN-FSCL-MN-NBR                                      
W33304            ,:INCNTL-OPN-FSCL-MN-DTE                                      
W33304            ,:INVPAR-LOC-NBR                                              
                  ,:INVPAR-ACTL-INV-DTE                                         
                  ,:INVPAR-PLND-INV-DTE                                         
                  ,:INVPAR-SCAN-INV-IND                                         
                  ,:INVPAR-ACTL-UNIT-BK-DTE                                     
                  ,:INVPAR-ACTL-FIN-BK-DTE                                      
                  ,:INVPAR-PLND-UNIT-BK-DTE                                     
                  ,:INVPAR-PLND-FIN-BK-DTE                                      
                  ,:INVPAR-ACTL-INV-RECD-DTE                                    
                  ,:INVPAR-STKRM-DAY-QTY                                        
                  ,:INVPAR-CORR-DAY-QTY                                         
                  ,:INVPAR-CHG-TMST                                             
                  ,:INVPAR-CHG-ID-NBR                                           
                  ,:INVPAR-ACTL-LDG-CTOF-TMST                                   
                  ,:INVPAR-ACTL-DC-CTOFF-DTE                                    
W33304            ,:INVPAR-ITM-CTOFF-IND:PV-NULL-IND-CTOFF-IND                  
W33304            ,:INVPAR-SHTG-ACTL-INV-DTE                                    
                  ,:INVPAR-PHY-CNT-TMST                                         
                  ,:INVPAR-SLS-ADJ-CPLD-IND                                     
W33304            ,:INVPAR-SCHD-PHY-CNT-CDE                                     
                  ,:INVPAR-XFER-INV-CTOFF-DTE                                   
                  ,:INVPAR-SHEET-INV-CPLD-IND                                   
                  ,:INVPAR-UNT-BKG-STAT-CDE                                     
                  ,:INVPAR-FIL-TOT-BAL-CDE                                      
                  ,:INVPAR-STKRM-WDRWL-IND                                      
                  ,:INVPAR-MFSTNG-IND                                           
                  ,:INVPAR-INV-SRVC-CDE                                         
W33304            ,:INVPAR-JOB-NM                                               
                  ,:INVPAR-SHEET-INV-REQR-IND                                   
                  ,:INVPAR-TMST-EDT-STAT-CDE                                    
W33304            ,:INVPAR-INV-CLO-DTE                                          
W33304            ,:INVPAR-FINCL-INV-STAT-CDE                                   
W33304            ,:INVPAR-LOC-INV-STAT-CDE                                     
W33304            ,:INVPAR-LOC-DEL-RSN-TXT:PV-NULL-IND-DEL-RSN-TXT              
W33304            ,:INVPAR-LOC-DEL-DTE                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET END-OF-TINVPAR TO TRUE                                   
               WHEN OTHER                                                       
                   MOVE 'FETCH TINVPAR-CURSOR' TO  PV-OPER-ATTEMPTED            
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    WRITE OUT COMMON EXTRACT                                             
      ******************************************************************        
       Z200-WRITE-EXTRACT.                                                      
                                                                                
           ADD +1 TO PV-RECORD-EXTRACTED.                                       
           WRITE COMMON-EXTRACT-REC FROM IN901-INVPAR-COMMON-EXTRACT.           
                                                                                
W33304     INITIALIZE IN901-INVPAR-COMMON-EXTRACT.                              
                                                                                
      ******************************************************************        
      *    READ CONTROL CARD                                                    
      ******************************************************************        
       Z300-READ-REQUEST-CNTL.                                                  
                                                                                
           READ REQUEST-CNTL-FILE                                               
               INTO RQ-CNTL-AREA                                                
               AT END SET END-OF-REQUEST-CNTL TO TRUE.                          
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUT901'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
