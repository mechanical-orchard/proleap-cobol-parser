       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      PXKRP122.                                       00020001
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    06/15/2012.                                     00050001
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **CREATES THE NEW STORE WHITE TICKET PRICE CHANGE REPORT          00080001
      ******************************************************************00090000
      **DATE: 06/15/2012   MADE BY: DAVE WELLER INITIAL WRITE           00100019
      ******************************************************************00110000
      **DATE: 06/23/2014   MADE BY: L. FRIESS   REMOVE PRICE CHANGE ID  00120019
      ******************************************************************00130019
       ENVIRONMENT DIVISION.                                            00140000
       CONFIGURATION SECTION.                                           00150000
       SOURCE-COMPUTER.        IBM-3090.                                00160000
       OBJECT-COMPUTER.        IBM-3090.                                00170000
       INPUT-OUTPUT SECTION.                                            00180000
       FILE-CONTROL.                                                    00190000
                                                                        00200000
           SELECT INPUT-FILE                                            00210000
                  ASSIGN TO INP01.                                      00220000
           SELECT REPORT-FILE                                           00230000
                  ASSIGN TO OUT01.                                      00240000
                                                                        00250000
       DATA DIVISION.                                                   00260000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  INPUT-FILE                                                   00290000
           RECORDING MODE F                                             00300000
           BLOCK CONTAINS 0 RECORDS                                     00310000
           LABEL RECORDS ARE OMITTED                                    00320000
           DATA RECORD IS INPUT-RECORD.                                 00330000
       01  INPUT-RECORD                    PIC X(250).                  00340001
                                                                        00350000
       FD  REPORT-FILE                                                  00360000
           RECORDING MODE F                                             00370000
           BLOCK CONTAINS 0 RECORDS                                     00380000
           LABEL RECORDS ARE OMITTED                                    00390000
           DATA RECORD IS REPORT-RECORD.                                00400000
       01  REPORT-RECORD                   PIC X(133).                  00410000
                                                                        00420000
       WORKING-STORAGE SECTION.                                         00430000
      ******************************************************************00440000
      **SWITCHES                                                        00450000
      ******************************************************************00460000
       01  PS-PROGRAM-SWITCHES.                                         00470000
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)   VALUE 'N'.   00480000
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.   00490000
               88  PS-NOT-END-OF-INPUT-FILE                VALUE 'N'.   00500000
      ******************************************************************00510000
      **PROGRAM HOLDS                                                   00520000
      ******************************************************************00530000
       01  PH-PROGRAM-HOLDS-1.                                          00540000
           05  PH-STR-NBR                    PIC S9(04) COMP            00550000
                                                        VALUE ZEROS.    00560000
           05  PH-BUS-AREA-ID                PIC S9(02) COMP-3          00570000
                                                        VALUE ZEROS.    00580000
       01  PH-PROGRAM-HOLDS-2.                                          00590000
           05  PH-SUB-CL-NBR                 PIC S9(04) COMP-3          00600000
                                                        VALUE ZEROS.    00610000
           05  PH-DEPT-NBR                   PIC S9(04) COMP-3          00620000
                                                        VALUE ZEROS.    00630000
      ******************************************************************00640000
      **TIMESTAMP VARIABLES                                             00650000
      ******************************************************************00660000
       01 DF-DATE-TIME-FIELDS.                                          00670000
          05 DF-ACCEPT-DATE.                                            00680000
             10  DF-ACCEPT-YR                  PIC 9(02) VALUE ZEROS.   00690000
             10  DF-ACCEPT-MO                  PIC 9(02) VALUE ZEROS.   00700000
             10  DF-ACCEPT-DY                  PIC 9(02) VALUE ZEROS.   00710000
          05 DF-ACCEPT-TIME.                                            00720000
             10  DF-ACCEPT-HR                  PIC 9(02) VALUE ZEROS.   00730000
             10  DF-ACCEPT-MN                  PIC 9(02) VALUE ZEROS.   00740000
             10  DF-ACCEPT-SS                  PIC 9(04) VALUE ZEROS.   00750000
          05 DF-FORMAT-DATE.                                            00760000
             10  DF-FORMAT-DATE-MM             PIC 9(02) VALUE ZEROS.   00770000
             10  FILLER                        PIC X(01) VALUE '/'.     00780000
             10  DF-FORMAT-DATE-DD             PIC 9(02) VALUE ZEROS.   00790000
             10  FILLER                        PIC X(01) VALUE '/'.     00800000
             10  DF-FORMAT-DATE-YY             PIC 9(02) VALUE ZEROS.   00810000
      ******************************************************************00820000
      **PROGRAM CONSTANTS                                               00830000
      ******************************************************************00840000
       01  PC-PROGRAM-CONSTANTS.                                        00850000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                00860000
               VALUE 'PXKRP122'.                                        00870001
      ******************************************************************00880000
      **PROGRAM COUNTERS                                                00890000
      ******************************************************************00900000
       01  PC-PROGRAM-COUNTER.                                          00910000
           05  PC-INPUT-RECORDS-READ           PIC 9(09) VALUE ZEROS.   00920000
           05  PC-REPORT-RECORDS-WRITTEN       PIC 9(09) VALUE ZEROS.   00930000
           05  PC-REPORT-LINE-COUNTER          PIC 9(03) VALUE ZEROS.   00940000
           05  PC-PAGE-COUNTER                 PIC 9(03) VALUE ZEROS.   00950000
      ******************************************************************00960000
      **REPORT HEADINGS                                                 00970000
      ******************************************************************00980000
       01  RH-TITLE-1.                                                  00990000
           05  FILLER                          PIC X(45)  VALUE SPACES. 01000002
           05  FILLER                          PIC X(43)                01010002
                   VALUE 'NEW STORE WHITE TICKET PRICE CHANGE REPORT '. 01020005
           05  FILLER                          PIC X(45)  VALUE SPACES. 01030002
       01  RH-TITLE-2.                                                  01040000
           05  FILLER                          PIC X(55)  VALUE SPACES. 01050002
           05  FILLER                          PIC X(23)                01060002
                   VALUE '  MARKUPS / MARKDOWNS  '.                     01070003
           05  FILLER                          PIC X(55)  VALUE SPACES. 01080002
       01  RH-HEADINGS-1.                                               01090000
           05  FILLER                          PIC X(02)  VALUE SPACES. 01100000
           05  FILLER                          PIC X(14)                01110000
                             VALUE ' STORE NUMBER:'.                    01120000
           05  RH-HEADINGS-1-STR-NBR           PIC Z9999  VALUE ZEROS.  01130000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01140000
           05  RH-HEADING-1-STR-NAME           PIC X(20)  VALUE SPACES. 01150000
           05  FILLER                          PIC X(90)  VALUE SPACES. 01160000
       01  RH-HEADINGS-2.                                               01170000
           05  FILLER                          PIC X(17)                01180000
                             VALUE ' EFFECTIVE DATE: '.                 01190000
           05  RH-HEADINGS-2-EFF-DATE          PIC X(10)  VALUE SPACES. 01200000
       01  RH-HEADINGS-3.                                               01210000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01220000
           05  FILLER                          PIC X(16)                01230000
                             VALUE ' BUSINESS AREA: '.                  01240000
           05  RH-HEADINGS-3-BUS-ID            PIC 99     VALUE ZEROS.  01250000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01260000
           05  RH-HEADINGS-3-BUS-DESC          PIC X(25)  VALUE SPACES. 01270000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01280000
           05  FILLER                          PIC X(45)  VALUE SPACES. 01290005
           05  FILLER                          PIC X(35)  VALUE SPACES. 01300005
           05  FILLER                          PIC X(07)  VALUE SPACES. 01310017
       01  RH-HEADINGS-4.                                               01320000
           05  FILLER                          PIC X(06)  VALUE SPACES. 01330000
           05  FILLER                          PIC X(05)                01340000
               VALUE ' SUB '.                                           01350000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01360000
           05  FILLER                          PIC X(09)                01370015
               VALUE 'BOX'.                                             01380007
           05  FILLER                          PIC X(79)  VALUE SPACES. 01390015
           05  FILLER                          PIC X(03)                01400006
               VALUE 'NEW'.                                             01410006
           05  FILLER                          PIC X(30)  VALUE SPACES. 01420017
       01  RH-HEADINGS-5.                                               01430000
           05  FILLER                          PIC X(05)                01440000
               VALUE 'DEPT '.                                           01450000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01460000
           05  FILLER                          PIC X(05)                01470000
               VALUE 'CLASS'.                                           01480000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01490000
           05  FILLER                          PIC X(09)                01500015
               VALUE ' ID '.                                            01510000
           05  FILLER                          PIC X(04)  VALUE SPACES. 01520015
           05  FILLER                          PIC X(20)                01530000
               VALUE 'STYLE               '.                            01540000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01550000
           05  FILLER                          PIC X(30)                01560000
               VALUE 'STYLE DESCRIPTION            '.                   01570000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01580000
           05  FILLER                          PIC X(20)                01590000
               VALUE 'LABEL NAME          '.                            01600000
           05  FILLER                          PIC X(02)  VALUE SPACES. 01610014
           05  FILLER                          PIC X(11)                01620004
               VALUE ' PRICE  '.                                        01630004
           05  FILLER                          PIC X(23)  VALUE SPACES. 01640017
       01  RH-HEADINGS-6.                                               01650000
           05  FILLER                          PIC X(05)                01660000
               VALUE '-----'.                                           01670000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01680000
           05  FILLER                          PIC X(05)                01690000
               VALUE '-----'.                                           01700000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01710000
           05  FILLER                          PIC X(09)                01720015
               VALUE '---------'.                                       01730015
           05  FILLER                          PIC X(04)  VALUE SPACES. 01740015
           05  FILLER                          PIC X(20)                01750000
               VALUE '--------------------'.                            01760000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01770000
           05  FILLER                          PIC X(30)                01780000
               VALUE '------------------------------'.                  01790000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01800000
           05  FILLER                          PIC X(20)                01810000
               VALUE '--------------------'.                            01820000
           05  FILLER                          PIC X(03)  VALUE SPACES. 01830000
           05  FILLER                          PIC X(11)                01840004
               VALUE '-----------'.                                     01850004
           05  FILLER                          PIC X(22)  VALUE SPACES. 01860017
       01  RH-DETAIL-LINE.                                              01870000
           05  RH-COLUMN-1-DEPT-NBR            PIC ZZZZZ  VALUE ZEROS.  01880000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01890000
           05  RH-COLUMN-1-SUB-CLASS           PIC ZZZZZ  VALUE ZEROS.  01900000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01910000
           05  RH-COLUMN-1-STKR-NBR            PIC ZZZZZZZZZ.           01920015
           05  FILLER                          PIC X(04)  VALUE SPACES. 01930015
           05  RH-COLUMN-1-VS-NBR              PIC X(20)  VALUE ZEROS.  01940000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01950000
           05  RH-COLUMN-1-VS-DESC             PIC X(30)  VALUE SPACES. 01960000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01970000
           05  RH-COLUMN-1-LBL-NM              PIC X(20)  VALUE SPACES. 01980000
           05  FILLER                          PIC X(03)  VALUE SPACES. 01990006
           05  RH-COLUMN-1-RETAIL              PIC $ZZZZZZZ.ZZ.         02000003
           05  FILLER                          PIC X(22)  VALUE SPACES. 02010017
       01  RH-FOOTERS-1.                                                02020000
           05  FILLER                          PIC X(54)  VALUE SPACES. 02030000
           05  FILLER                          PIC X(19)                02040000
               VALUE '***END OF REPORT***'.                             02050000
      ******************************************************************02060000
      **COPYBOOKS                                                       02070000
      ******************************************************************02080000
      ***INPUT FILE FORMAT                                              02090000
           COPY PXRD141.                                                02100003
                                                                        02110018
      ***STANDARD KOHLS HEADING FOR 132 BYTE REPORT                     02120000
           COPY DPWS132O.                                               02130000
      ******************************************************************02140000
      **ERROR HANDLING                                                  02150000
      ******************************************************************02160000
       01  PV-MISC-ABEND-FIELDS.                                        02170000
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   02180000
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   02190000
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   02200000
                                                                        02210000
       PROCEDURE DIVISION.                                              02220000
      ***************************************************************** 02230000
                                                                        02240000
           PERFORM 1000-INITIALIZATION.                                 02250000
           PERFORM 2000-PROCESS-RECORDS                                 02260000
                   UNTIL PS-END-OF-INPUT-FILE.                          02270000
           PERFORM 9999-WRAPUP.                                         02280000
           GOBACK.                                                      02290000
                                                                        02300000
      ******************************************************************02310000
      **INITIALIZATION AREA                                             02320000
      ******************************************************************02330000
       1000-INITIALIZATION.                                             02340000
                                                                        02350000
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             02360000
                                                                        02370000
           OPEN INPUT  INPUT-FILE                                       02380000
                OUTPUT REPORT-FILE.                                     02390000
                                                                        02400000
      **RETRIEVE DATE FROM SYSTEM                                       02410000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             02420000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             02430000
                                                                        02440000
           MOVE DF-ACCEPT-YR          TO DF-FORMAT-DATE-YY.             02450000
           MOVE DF-ACCEPT-MO          TO DF-FORMAT-DATE-MM.             02460000
           MOVE DF-ACCEPT-DY          TO DF-FORMAT-DATE-DD.             02470000
                                                                        02480000
           PERFORM 2999-READ-INPUT-FILE.                                02490000
                                                                        02500000
           IF  PS-NOT-END-OF-INPUT-FILE                                 02510000
               PERFORM 1100-HEADING-PREP                                02520008
               PERFORM 2100-WRITE-HEADINGS                              02530009
               MOVE PXRD141-BUS-AREA-ID                                 02540003
                                      TO PH-BUS-AREA-ID                 02550000
               MOVE PXRD141-STR-NBR   TO PH-STR-NBR                     02560003
           END-IF.                                                      02570000
                                                                        02580000
      ******************************************************************02590000
      **MOVE DATA TO FIELDS IN HEADING FOR REPORT                       02600000
      ******************************************************************02610000
       1100-HEADING-PREP.                                               02620000
                                                                        02630000
           MOVE '1100-HEADING-PREP'  TO PV-PARAGRAPH-NAME.              02640000
                                                                        02650000
      ***PREPS KOHLS STANDARD HEADING                                   02660000
           MOVE PC-CURRENT-PROGRAM-NAME                                 02670000
                                     TO DP132O-PROGRAM-NAME.            02680000
           MOVE 1                                                       02690005
                                     TO DP132O-REPORT-NUMBER.           02700005
           MOVE DF-FORMAT-DATE       TO DP132O-RUN-DATE.                02710000
           MOVE DF-ACCEPT-HR         TO DP132O-RUN-HOUR.                02720000
           MOVE DF-ACCEPT-MN         TO DP132O-RUN-MINUTE.              02730000
                                                                        02740000
      ******************************************************************02750000
      **PROCESSES INPUT FILE                                            02760000
      ******************************************************************02770000
       2000-PROCESS-RECORDS.                                            02780000
                                                                        02790000
           MOVE '2000-PROCESS-RECORDS'  TO PV-PARAGRAPH-NAME.           02800000
                                                                        02810000
           EVALUATE TRUE                                                02820000
              WHEN PXRD141-STR-NBR EQUAL PH-STR-NBR                     02830003
                   IF  PC-REPORT-LINE-COUNTER > 60                      02840000
                    OR PXRD141-BUS-AREA-ID NOT EQUAL PH-BUS-AREA-ID     02850003
                       PERFORM 2100-WRITE-HEADINGS                      02860000
                       INITIALIZE PH-PROGRAM-HOLDS-2                    02870000
                       MOVE PXRD141-BUS-AREA-ID                         02880003
                                        TO PH-BUS-AREA-ID               02890000
                   END-IF                                               02900000
                   PERFORM 2200-WRITE-DETAIL                            02910000
              WHEN PXRD141-STR-NBR NOT EQUAL PH-STR-NBR                 02920003
                   PERFORM 2300-WRITE-FOOTERS                           02930000
                   INITIALIZE PC-PAGE-COUNTER                           02940010
                   PERFORM 2100-WRITE-HEADINGS                          02950000
                   INITIALIZE PH-PROGRAM-HOLDS-2                        02960000
                   PERFORM 2200-WRITE-DETAIL                            02970000
                   MOVE PXRD141-STR-NBR TO PH-STR-NBR                   02980003
                   MOVE PXRD141-BUS-AREA-ID                             02990003
                                        TO PH-BUS-AREA-ID               03000000
           END-EVALUATE.                                                03010000
                                                                        03020000
           PERFORM 2999-READ-INPUT-FILE.                                03030000
                                                                        03040000
      ***************************************************************** 03050000
      **WRITE REPORT HEADERS                                            03060000
      ***************************************************************** 03070000
       2100-WRITE-HEADINGS.                                             03080000
                                                                        03090000
           MOVE '2100-WRITE-HEADINGS'  TO PV-PARAGRAPH-NAME.            03100000
                                                                        03110000
           INITIALIZE PC-REPORT-LINE-COUNTER.                           03120000
                                                                        03130000
           ADD +1                      TO PC-PAGE-COUNTER.              03140000
           MOVE PC-PAGE-COUNTER        TO DP132O-PAGE-NUMBER.           03150000
                                                                        03160000
           MOVE PXRD141-STR-NBR        TO RH-HEADINGS-1-STR-NBR.        03170003
           MOVE PXRD141-LOC-NM         TO RH-HEADING-1-STR-NAME.        03180003
           MOVE PXRD141-BUS-AREA-ID    TO RH-HEADINGS-3-BUS-ID.         03190003
           MOVE PXRD141-BUS-AREA-DESC  TO RH-HEADINGS-3-BUS-DESC.       03200003
           MOVE PXRD141-EFF-DATE       TO RH-HEADINGS-2-EFF-DATE.       03210003
                                                                        03220000
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      03230000
                               AFTER ADVANCING PAGE.                    03240000
                                                                        03250000
           WRITE REPORT-RECORD FROM RH-TITLE-1 AFTER ADVANCING 1 LINE.  03260000
           WRITE REPORT-RECORD FROM RH-TITLE-2 AFTER ADVANCING 1 LINE.  03270000
                                                                        03280000
           WRITE REPORT-RECORD FROM RH-HEADINGS-1                       03290000
                               AFTER ADVANCING 2 LINES.                 03300000
           WRITE REPORT-RECORD FROM RH-HEADINGS-2                       03310000
                               AFTER ADVANCING 1 LINES.                 03320000
           WRITE REPORT-RECORD FROM RH-HEADINGS-3                       03330000
                               AFTER ADVANCING 1 LINES.                 03340000
           WRITE REPORT-RECORD FROM RH-HEADINGS-4                       03350000
                               AFTER ADVANCING 2 LINES.                 03360000
           WRITE REPORT-RECORD FROM RH-HEADINGS-5                       03370000
                               AFTER ADVANCING 1 LINES.                 03380000
           WRITE REPORT-RECORD FROM RH-HEADINGS-6                       03390000
                               AFTER ADVANCING 1 LINES.                 03400000
                                                                        03410000
           ADD +11             TO PC-REPORT-LINE-COUNTER.               03420000
                                                                        03430000
      ***************************************************************** 03440000
      **WRITE DETAIL LINE                                               03450000
      ***************************************************************** 03460000
       2200-WRITE-DETAIL.                                               03470000
                                                                        03480000
           MOVE '2200-WRITE-DETAIL'    TO PV-PARAGRAPH-NAME.            03490000
                                                                        03500000
           INITIALIZE RH-DETAIL-LINE.                                   03510000
                                                                        03520000
           IF  PH-DEPT-NBR NOT EQUAL PXRD141-DEPT-NBR                   03530003
               MOVE PXRD141-DEPT-NBR   TO RH-COLUMN-1-DEPT-NBR          03540003
           END-IF.                                                      03550000
                                                                        03560000
           IF PH-SUB-CL-NBR NOT EQUAL PXRD141-SUB-CL-NBR                03570013
               MOVE PXRD141-SUB-CL-NBR TO RH-COLUMN-1-SUB-CLASS         03580003
           END-IF.                                                      03590000
                                                                        03600000
           MOVE PXRD141-VS-NBR         TO RH-COLUMN-1-VS-NBR.           03610003
           MOVE PXRD141-VS-DESC        TO RH-COLUMN-1-VS-DESC.          03620003
           MOVE PXRD141-LBL-NM         TO RH-COLUMN-1-LBL-NM.           03630003
           MOVE PXRD141-STKR-NBR       TO RH-COLUMN-1-STKR-NBR.         03640003
           MOVE PXRD141-UNT-RTL-AMT    TO RH-COLUMN-1-RETAIL.           03650003
                                                                        03660000
           ADD +1                      TO PC-REPORT-LINE-COUNTER.       03670000
                                                                        03680000
           WRITE REPORT-RECORD FROM RH-DETAIL-LINE.                     03690000
                                                                        03700000
           MOVE PXRD141-DEPT-NBR       TO PH-DEPT-NBR.                  03710003
           MOVE PXRD141-SUB-CL-NBR     TO PH-SUB-CL-NBR.                03720003
                                                                        03730000
           ADD +1                      TO PC-REPORT-RECORDS-WRITTEN.    03740000
                                                                        03750000
      ***************************************************************** 03760000
      **WRITE FOOTERS                                                   03770000
      ***************************************************************** 03780000
       2300-WRITE-FOOTERS.                                              03790000
                                                                        03800000
           MOVE '2300-WRITE-FOOTERS' TO PV-PARAGRAPH-NAME.              03810000
                                                                        03820000
           WRITE REPORT-RECORD FROM RH-FOOTERS-1.                       03830000
                                                                        03840000
      ***************************************************************** 03850000
      **READ INPUT FILE                                                 03860000
      ***************************************************************** 03870000
       2999-READ-INPUT-FILE.                                            03880000
                                                                        03890000
           READ INPUT-FILE INTO PXRD141-WHITE-TICKET-REC                03900003
                AT END SET PS-END-OF-INPUT-FILE TO TRUE                 03910000
           END-READ.                                                    03920000
                                                                        03930000
           IF  PS-NOT-END-OF-INPUT-FILE                                 03940000
               ADD +1 TO PC-INPUT-RECORDS-READ                          03950000
           END-IF.                                                      03960000
                                                                        03970000
      ****************************************************************  03980000
      *END OF PROGRAM ROUTINE                                           03990000
      ****************************************************************  04000000
       9999-WRAPUP.                                                     04010000
                                                                        04020000
           IF  PC-REPORT-RECORDS-WRITTEN > 0                            04030000
               PERFORM 2300-WRITE-FOOTERS                               04040000
           END-IF.                                                      04050000
                                                                        04060000
           CLOSE INPUT-FILE                                             04070000
                 REPORT-FILE.                                           04080000
                                                                        04090000
           PERFORM 9999-DISPLAY-STATISTICS.                             04100000
                                                                        04110000
      ****************************************************************  04120000
      *DISPLAY PROGRAM STATISTICS                                       04130000
      ****************************************************************  04140000
       9999-DISPLAY-STATISTICS.                                         04150000
                                                                        04160000
           DISPLAY '*************************************************'. 04170000
           DISPLAY '* PROGRAM...............: ' PC-CURRENT-PROGRAM-NAME.04180000
           DISPLAY '* DATE..................: ' DF-ACCEPT-DATE.         04190000
           DISPLAY '* TIMESTAMP.............: ' DF-ACCEPT-TIME.         04200000
           DISPLAY '*************************************************'. 04210000
           DISPLAY '* INPUT RECORDS READ.........: '                    04220000
                                          PC-INPUT-RECORDS-READ.        04230000
           DISPLAY '* REPORT RECORDS WRITTEN.....: '                    04240000
                                          PC-REPORT-RECORDS-WRITTEN.    04250000
           DISPLAY '*************************************************'. 04260000
                                                                        04270000
