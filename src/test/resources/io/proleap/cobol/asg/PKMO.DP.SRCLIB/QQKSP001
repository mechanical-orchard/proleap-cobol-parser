      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.      QQKSP001.                                               
       AUTHOR.          SIVA NAGISETTY.                                         
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    FEBRUARY 2007.                                          
       DATE-COMPILED.   FEBRUARY 2007.                                          
                                                                                
      ******************************************************************        
      * THIS DB2 STORED PROCEDURE WILL INSERT/UPDATE VENDOR ADS        *        
      * TABLES QQT_EXENT, QQT_EXENT_ROL_ASSN, QQT_MDSE_VND AND         *        
      *  QQT_EXP_VND.                                                  *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE                    
                                                       'QQKSP001'.              
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-HOLD-CPBL-CDE             PIC  X(3)  VALUE SPACES.            
           05  PV-HOLD-PREV-EXENT-ROL-CDE   PIC  X(2)  VALUE SPACES.            
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.            
           05  PV-CURRENT-TMST              PIC  X(26) VALUE SPACES.            
           05  PV-CURRENT-TMST-DET.                                             
               10 PV-CURRENT-DATE           PIC  X(10) VALUE SPACES.            
               10 PV-CURRENT-TIME           PIC  X(16) VALUE SPACES.            
           05  PV-DB2-OPERATION-ATTEMPTED   PIC  X(30) VALUE SPACES.            
           05  PV-HOLD-SQLCODE              PIC S9(09) VALUE ZEROES             
                                                           COMP.                
                                                                                
                                                                                
           05  PS-ABEND-SW                  PIC  X(01) VALUE 'N'.               
               88  PS-NO-ABEND                         VALUE 'N'.               
               88  PS-ABEND                            VALUE 'Y'.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-SQL-ERROR-SW          PIC X          VALUE 'N'.               
               88  PS-SQL-ERROR                        VALUE 'Y'.               
               88  PS-NO-SQL-ERROR                     VALUE 'N'.               
                                                                                
013500 01  WS-INTEGER-UNPK               PIC S9(10).                            
014200 01  WS-INTEGER-EDIT               PIC ++++++++++9.                       
                                                                                
      ******************************************************************        
      * MSG   ERROR MESSAGE TO BE DISPLAYED IN THE STORED PROCEDURE    *        
      *       ADDRESS SPACE                                            *        
      ******************************************************************        
       01  MSG-ERROR-DISPLAY.                                                   
           05  FILLER                       PIC  X(05) VALUE                    
               'ERROR'.                                                         
           05  FILLER                       PIC  X(01) VALUE SPACES.            
           05  FILLER                       PIC  X(01) VALUE '-'.               
           05  FILLER                       PIC  X(01) VALUE SPACES.            
           05  MSG-STORED-PROCEDURE-NAME    PIC  X(08) VALUE SPACES.            
           05  FILLER                       PIC  X(01) VALUE SPACES.            
           05  MSG-SGN-NBR                  PIC  X(10) VALUE SPACES.            
           05  FILLER                       PIC  X(01) VALUE ':'.               
           05  FILLER                       PIC  X(02) VALUE SPACES.            
                                                                                
      ******************************************************************        
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *        
      ******************************************************************        
                                                                                
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *    QQT_EXENT  - EXTERNAL ENTT TABLE                            *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE QQT00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    QQT_EXENT_ROL_ASSN - EXTERNAL ENTT ROL ASSN.                *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE QQT00005                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    QQT_MDSE_VND - MERCHANDISE VENDOR.                          *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE QQT00014                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    QQT_EXP_VND - EXPENSE VENDOR.                               *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE QQT00006                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    TENTNME - VM TENTNME.                                       *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TENTNME                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    QQT_EXENT_CPBL - VENDOR CAPABILITIES                        *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE QQT00008                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    SQL COMMUNICATIONS AREA DCLGEN                              *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * AC ABEND CODES                                                 *        
      ******************************************************************        
       01  ABEND-CODE                       PIC S9(04) VALUE ZEROES             
                                                       COMP                     
                                                       SYNC.                    
           88  AC-ABEND-4013-DB2-ERROR                 VALUE +4013.             
                                                                                
      ******************************************************************        
       LINKAGE SECTION.                                                         
      ******************************************************************        
       01  LS-ACTION-IND                       PIC  X(1).                       
       01  LS-ENT-ID-PREV                      PIC  S9(9) COMP.                 
       01  LS-ENT-ID-CURR                      PIC  S9(9) COMP.                 
       01  LS-DUN-NBR-PREV                     PIC  X(09).                      
       01  LS-DUN-NBR-CURR                     PIC  X(09).                      
       01  LS-STATUS-CDE-PREV                  PIC  X(1).                       
       01  LS-STATUS-CDE-CURR                  PIC  X(1).                       
       01  LS-CRE-TMST-PREV                    PIC  X(26).                      
       01  LS-CRE-TMST-CURR                    PIC  X(26).                      
       01  LS-LAST-UPD-TMST-PREV               PIC  X(26).                      
       01  LS-LAST-UPD-TMST-CURR               PIC  X(26).                      
       01  LS-PRIM-TYP-CDE-PREV                PIC  X(1).                       
       01  LS-PRIM-TYP-CDE-CURR                PIC  X(1).                       
       01  LS-PRTNR-IND-PREV                   PIC  X(1).                       
       01  LS-PRTNR-IND-CURR                   PIC  X(1).                       
       01  LS-VND-CLSN-CDE-PREV                PIC  X(1).                       
       01  LS-VND-CLSN-CDE-CURR                PIC  X(1).                       
       01  LS-EDI-PO-PRE-PK-CDE-PREV           PIC  X(1).                       
       01  LS-EDI-PO-PRE-PK-CDE-CURR           PIC  X(1).                       
       01  LS-CFY-SLSRNK-NBR-PREV              PIC  S9(4) COMP.                 
       01  LS-CFY-SLSRNK-NBR-CURR              PIC  S9(4) COMP.                 
       01  LS-PFY-SLSRNK-NBR-PREV              PIC  S9(4) COMP.                 
       01  LS-PFY-SLSRNK-NBR-CURR              PIC  S9(4) COMP.                 
       01  LS-P12MN-SLSRNK-NBR-PREV            PIC  S9(4) COMP.                 
       01  LS-P12MN-SLSRNK-NBR-CURR            PIC  S9(4) COMP.                 
       01  LS-SCAC-CDE-PREV                    PIC  X(4).                       
       01  LS-SCAC-CDE-CURR                    PIC  X(4).                       
       01  LS-PRTNR-TYP-CDE-PREV               PIC  X(2).                       
       01  LS-PRTNR-TYP-CDE-CURR               PIC  X(2).                       
                                                                                
       PROCEDURE DIVISION USING   LS-ACTION-IND                                 
                                , LS-ENT-ID-PREV                                
                                , LS-ENT-ID-CURR                                
                                , LS-DUN-NBR-PREV                               
                                , LS-DUN-NBR-CURR                               
                                , LS-STATUS-CDE-PREV                            
                                , LS-STATUS-CDE-CURR                            
                                , LS-CRE-TMST-PREV                              
                                , LS-CRE-TMST-CURR                              
                                , LS-LAST-UPD-TMST-PREV                         
                                , LS-LAST-UPD-TMST-CURR                         
                                , LS-PRIM-TYP-CDE-PREV                          
                                , LS-PRIM-TYP-CDE-CURR                          
                                , LS-PRTNR-IND-PREV                             
                                , LS-PRTNR-IND-CURR                             
                                , LS-VND-CLSN-CDE-PREV                          
                                , LS-VND-CLSN-CDE-CURR                          
                                , LS-EDI-PO-PRE-PK-CDE-PREV                     
                                , LS-EDI-PO-PRE-PK-CDE-CURR                     
                                , LS-CFY-SLSRNK-NBR-PREV                        
                                , LS-CFY-SLSRNK-NBR-CURR                        
                                , LS-PFY-SLSRNK-NBR-PREV                        
                                , LS-PFY-SLSRNK-NBR-CURR                        
                                , LS-P12MN-SLSRNK-NBR-PREV                      
                                , LS-P12MN-SLSRNK-NBR-CURR                      
                                , LS-SCAC-CDE-PREV                              
                                , LS-SCAC-CDE-CURR                              
                                , LS-PRTNR-TYP-CDE-PREV                         
                                , LS-PRTNR-TYP-CDE-CURR.                        
                                                                                
      ******************************************************************        
       A100-MAINLINE.                                                           
      ******************************************************************        
                                                                                
      **   DISPLAY PC-PROGRAM-NAME ':  STARTING PROCEDURE'.                     
                                                                                
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM B200-PROCESS-TRIGGERS.                                       
                                                                                
      **   DISPLAY PC-PROGRAM-NAME ':  ENDING PROCEDURE'.                       
                                                                                
           GOBACK.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
      *     B100-INITIALIZE                                            *        
      * ==> PROCESSES:                                                 *        
      *     - INITIALIZE THE PROGRAM VARIABLES.                        *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       B100-INITIALIZE.                                                         
                                                                                
           MOVE PC-PROGRAM-NAME          TO MSG-STORED-PROCEDURE-NAME.          
                                                                                
           MOVE ZEROES                   TO PV-HOLD-SQLCODE.                    
           MOVE SPACES                   TO PV-PARAGRAPH-NAME.                  
                                                                                
           EXEC SQL                                                             
               SET :PV-CURRENT-TMST = CURRENT TIMESTAMP                         
           END-EXEC.                                                            
                                                                                
      **   DISPLAY 'TIMESTAMP:'  PV-CURRENT-TMST.                               
                                                                                
           MOVE PV-CURRENT-TMST  TO PV-CURRENT-TMST-DET.                        
                                                                                
      ******************************************************************        
      *     B200-PROCESS-TRIGGERS.                                     *        
      * ==> PROCESSES:                                                 *        
      *     - PROCESSES EACH TRANSACTION PASSED THRU TRIGGER           *        
      *     - IF ACTION IND IS 'I' INSERT ROWS INTO QQT_EXENT,         *        
      *       QQT_EXENT_ROL_ASSN, QQT_MDSE_VND, QQT_EXP_VND.           *        
      *     - IF ACTION IND IS 'U' UPDATE ROWS IN QQT_EXENT,           *        
      *       QQT_EXENT_ROL_ASSN, QQT_MDSE_VND, QQT_EXP_VND.           *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       B200-PROCESS-TRIGGERS.                                                   
                                                                                
                                                                                
           EVALUATE LS-ACTION-IND                                               
                                                                                
             WHEN 'I'                                                           
                                                                                
               PERFORM C100-INSERT-EXENT                                        
               PERFORM C200-INSERT-ROL-ASSN                                     
               PERFORM C300-INSERT-MDSE-VND                                     
               PERFORM C400-INSERT-EXP-VND                                      
               PERFORM C500-INSERT-CPBL                                         
                                                                                
             WHEN 'U'                                                           
                                                                                
               PERFORM D100-UPDATE-EXENT                                        
               PERFORM D200-DELETE-INSERT-ROL-ASSN                              
               PERFORM D300-UPDATE-MDSE-VND                                     
               PERFORM D400-DELETE-INSERT-EXP-VND                               
               PERFORM D500-UPDATE-CPBL                                         
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C100-INSERT-EXENT.                                         *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXENT.                              *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       C100-INSERT-EXENT.                                                       
                                                                                
           MOVE  LS-ENT-ID-CURR   TO QQT00001-EXENT-ID.                         
           MOVE  LS-DUN-NBR-CURR  TO QQT00001-EXENT-NBR.                        
                                                                                
      *    PERFORM Y900-GET-EXTL-NM.                                            
           MOVE SPACES TO QQT00001-PRIM-EXENT-NM                                
                          QQT00001-SHRT-EXENT-NM.                               
                                                                                
           EVALUATE LS-STATUS-CDE-CURR                                          
                                                                                
              WHEN 'A'                                                          
                MOVE 'Y' TO QQT00001-ACTV-IND                                   
                                                                                
              WHEN 'I'                                                          
                MOVE 'N' TO QQT00001-ACTV-IND                                   
                                                                                
           END-EVALUATE                                                         
                                                                                
           MOVE LS-CRE-TMST-CURR      TO QQT00001-SOR-CRE-TMST.                 
           MOVE LS-LAST-UPD-TMST-CURR TO                                        
                                    QQT00001-SOR-LAST-UPD-TMST.                 
                                                                                
           PERFORM C150-INSERT-EXENT-ROWS.                                      
                                                                                
      ******************************************************************        
      *     C150-INSERT-EXENT-ROWS                                     *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXENT                               *        
      * ==> PERFORMED FROM: C100-INSERT-EXENT.                         *        
      ******************************************************************        
       C150-INSERT-EXENT-ROWS.                                                  
                                                                                
           EXEC SQL                                                             
               INSERT INTO QQT_EXENT                                            
                  (  EXENT_ID                                                   
                   , EXENT_NBR                                                  
                   , PRIM_EXENT_NM                                              
                   , ACTV_IND                                                   
                   , SHRT_EXENT_NM                                              
                   , SOR_CRE_TMST                                               
                   , SOR_LAST_UPD_TMST)                                         
               VALUES                                                           
                  ( :QQT00001-EXENT-ID                                          
                   ,:QQT00001-EXENT-NBR                                         
                   ,:QQT00001-PRIM-EXENT-NM                                     
                   ,:QQT00001-ACTV-IND                                          
                   ,:QQT00001-SHRT-EXENT-NM                                     
                   ,:QQT00001-SOR-CRE-TMST                                      
                   ,:QQT00001-SOR-LAST-UPD-TMST)                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'C150-INSERT-EXENT      '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'INSERT ERROR ON EXENT      '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C200-INSERT-ROL-ASSN                                       *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXENT_ROL_ASSN.                     *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS AND                  *        
      *                     D200-DELETE-INSERT-ROL-ASSN                *        
      ******************************************************************        
       C200-INSERT-ROL-ASSN.                                                    
                                                                                
           EVALUATE TRUE                                                        
              WHEN LS-PRIM-TYP-CDE-CURR = 'M'                                   
                  MOVE  LS-ENT-ID-CURR   TO                                     
                           QQT00005-EXENT-ID                                    
                  MOVE LS-PRIM-TYP-CDE-CURR TO                                  
                           QQT00005-EXENT-ROL-CDE                               
                  PERFORM  C250-INSERT-ROL-ASSN-ROWS                            
           END-EVALUATE.                                                        
                                                                                
           EVALUATE TRUE                                                        
              WHEN LS-PRIM-TYP-CDE-CURR = 'F'                                   
                  MOVE  LS-ENT-ID-CURR   TO                                     
                           QQT00005-EXENT-ID                                    
                  MOVE LS-PRIM-TYP-CDE-CURR TO                                  
                           QQT00005-EXENT-ROL-CDE                               
                  PERFORM  C250-INSERT-ROL-ASSN-ROWS                            
           END-EVALUATE.                                                        
                                                                                
           EVALUATE TRUE                                                        
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-IND-CURR = 'Y'                                  
                  MOVE  LS-ENT-ID-CURR TO QQT00005-EXENT-ID                     
                  MOVE 'P'             TO QQT00005-EXENT-ROL-CDE                
                  PERFORM  C250-INSERT-ROL-ASSN-ROWS                            
           END-EVALUATE.                                                        
                                                                                
           EVALUATE TRUE                                                        
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-VND-CLSN-CDE-CURR = 'E'                               
                  MOVE  LS-ENT-ID-CURR TO QQT00005-EXENT-ID                     
                  MOVE 'K'             TO QQT00005-EXENT-ROL-CDE                
                  PERFORM  C250-INSERT-ROL-ASSN-ROWS                            
           END-EVALUATE.                                                        
                                                                                
           EVALUATE TRUE                                                        
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-TYP-CDE-CURR = 'AG'                             
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-TYP-CDE-CURR = 'BK'                             
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-TYP-CDE-CURR = 'CO'                             
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-TYP-CDE-CURR = 'CR'                             
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-TYP-CDE-CURR = 'FA'                             
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-TYP-CDE-CURR = 'OP'                             
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-TYP-CDE-CURR = 'S2'                             
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-TYP-CDE-CURR = 'BR'                             
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-TYP-CDE-CURR = 'DE'                             
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-TYP-CDE-CURR = 'FF'                             
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-TYP-CDE-CURR = 'TM'                             
              WHEN LS-PRIM-TYP-CDE-CURR = 'E' AND                               
                       LS-PRTNR-TYP-CDE-CURR = 'S1'                             
                                                                                
                  MOVE LS-ENT-ID-CURR        TO QQT00005-EXENT-ID               
                  MOVE LS-PRTNR-TYP-CDE-CURR TO QQT00005-EXENT-ROL-CDE          
                  PERFORM  C250-INSERT-ROL-ASSN-ROWS                            
                                                                                
           END-EVALUATE.                                                        
                                                                                
           EVALUATE TRUE                                                        
              WHEN LS-PRIM-TYP-CDE-CURR = 'E'                                   
      *           AND LS-PRTNR-TYP-CDE-CURR NOT = ('AG' AND 'BK' AND            
      *                    'CO' AND 'CR' AND 'FA' AND 'OP' AND 'S2' AND         
      *                    'BR' AND 'DE' AND 'FF' AND 'TM' AND 'S1' )           
                  MOVE  LS-ENT-ID-CURR TO QQT00005-EXENT-ID                     
                  MOVE  'E'            TO QQT00005-EXENT-ROL-CDE                
                  PERFORM  C250-INSERT-ROL-ASSN-ROWS                            
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C250-INSERT-ROL-ASSN-ROWS                                  *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXENT_ROL_ASSN                      *        
      * ==> PERFORMED FROM: C200-INSERT-ROL-ASSN.                      *        
      ******************************************************************        
       C250-INSERT-ROL-ASSN-ROWS.                                               
                                                                                
           EXEC SQL                                                             
               INSERT INTO QQT_EXENT_ROL_ASSN                                   
                  ( EXENT_ID                                                    
                   ,EXENT_ROL_CDE)                                              
               VALUES                                                           
                  ( :QQT00005-EXENT-ID                                          
                   ,:QQT00005-EXENT-ROL-CDE)                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'C250-INSERT-ROL-ASSN   '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'INSERT ERROR ON ROL ASSN   '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C300-INSERT-MDSE-VND                                       *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_MDSE_VND.                           *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       C300-INSERT-MDSE-VND.                                                    
                                                                                
           IF LS-PRIM-TYP-CDE-CURR = 'M'                                        
                                                                                
             MOVE  LS-ENT-ID-CURR   TO QQT00014-EXENT-ID                        
             MOVE  LS-CFY-SLSRNK-NBR-CURR                                       
                                    TO QQT00014-CFY-SLS-RNK-NBR                 
             MOVE  LS-PFY-SLSRNK-NBR-CURR                                       
                                    TO QQT00014-PFY-SLSRNK-NBR                  
             MOVE  LS-P12MN-SLSRNK-NBR-CURR                                     
                                    TO QQT00014-P12-MN-SLSRNK-NBR               
                                                                                
             PERFORM C350-INSERT-MDSE-VND-ROWS                                  
                                                                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     C350-INSERT-MDSE-VND-ROWS                                  *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_MDSE_VND                            *        
      * ==> PERFORMED FROM: C300-INSERT-MDSE-VND                       *        
      ******************************************************************        
       C350-INSERT-MDSE-VND-ROWS.                                               
                                                                                
           EXEC SQL                                                             
               INSERT INTO QQT_MDSE_VND                                         
                  ( EXENT_ID                                                    
                   ,CFY_SLS_RNK_NBR                                             
                   ,PFY_SLSRNK_NBR                                              
                   ,P12_MN_SLSRNK_NBR)                                          
               VALUES                                                           
                  ( :QQT00014-EXENT-ID                                          
                   ,:QQT00014-CFY-SLS-RNK-NBR                                   
                   ,:QQT00014-PFY-SLSRNK-NBR                                    
                   ,:QQT00014-P12-MN-SLSRNK-NBR)                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'C350-INSERT-MDSE-VND   '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'INSERT ERROR ON MDSE VND   '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C400-INSERT-EXP-VND                                        *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXP_VND.                            *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       C400-INSERT-EXP-VND.                                                     
                                                                                
            EVALUATE TRUE                                                       
                                                                                
            WHEN  LS-PRIM-TYP-CDE-CURR = 'E' AND                                
                ( LS-PRTNR-TYP-CDE-CURR = 'BR' OR 'CO' OR                       
                                  'CR' OR 'DE' OR 'FF')                         
                                                                                
             MOVE  LS-ENT-ID-CURR   TO QQT00006-EXENT-ID                        
             MOVE  LS-SCAC-CDE-CURR TO QQT00006-SCAC-CDE                        
                                                                                
             PERFORM C450-INSERT-EXP-VND-ROWS                                   
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C450-INSERT-EXP-VND-ROWS                                   *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXP_VND                             *        
      * ==> PERFORMED FROM: C400-INSERT-EXP-VND                        *        
      ******************************************************************        
       C450-INSERT-EXP-VND-ROWS.                                                
                                                                                
           EXEC SQL                                                             
               INSERT INTO QQT_EXP_VND                                          
                  ( EXENT_ID                                                    
                   ,SCAC_CDE)                                                   
               VALUES                                                           
                  ( :QQT00006-EXENT-ID                                          
                   ,:QQT00006-SCAC-CDE)                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'C450-INSERT-EXP-VND    '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'INSERT ERROR ON EXP VND    '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
      ******************************************************************        
      *     C500-INSERT-CPBL                                           *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXENT_CPBL                          *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       C500-INSERT-CPBL.                                                        
                                                                                
            EVALUATE TRUE                                                       
                                                                                
            WHEN LS-EDI-PO-PRE-PK-CDE-CURR = 'Y'                                
                                                                                
             MOVE  LS-ENT-ID-CURR   TO QQT00008-EXENT-ID                        
             MOVE  'PPK'            TO QQT00008-EXENT-CPBL-CDE                  
             MOVE  PV-CURRENT-TMST  TO QQT00008-EFF-BEG-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-CRE-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-LAST-UPD-TMST               
             MOVE  'QQKSP001'       TO QQT00008-SOR-LAST-UPD-USRID              
                                                                                
             PERFORM C550-INSERT-CPBL-ROWS                                      
                                                                                
            WHEN LS-EDI-PO-PRE-PK-CDE-CURR = 'C'                                
                                                                                
             MOVE  LS-ENT-ID-CURR   TO QQT00008-EXENT-ID                        
             MOVE  'CPK'            TO QQT00008-EXENT-CPBL-CDE                  
             MOVE  PV-CURRENT-TMST  TO QQT00008-EFF-BEG-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-CRE-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-LAST-UPD-TMST               
             MOVE  'QQKSP001'       TO QQT00008-SOR-LAST-UPD-USRID              
                                                                                
             PERFORM C550-INSERT-CPBL-ROWS                                      
                                                                                
                                                                                
            WHEN LS-EDI-PO-PRE-PK-CDE-CURR = 'X'                                
                                                                                
             MOVE  LS-ENT-ID-CURR   TO QQT00008-EXENT-ID                        
             MOVE  'XPK'            TO QQT00008-EXENT-CPBL-CDE                  
             MOVE  PV-CURRENT-TMST  TO QQT00008-EFF-BEG-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-CRE-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-LAST-UPD-TMST               
             MOVE  'QQKSP001'       TO QQT00008-SOR-LAST-UPD-USRID              
                                                                                
             PERFORM C550-INSERT-CPBL-ROWS                                      
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C550-INSERT-CPBL-ROWS.                                     *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXENT_CPBL.                         *        
      * ==> PERFORMED FROM: C500-INSERT-CPBL.                          *        
      ******************************************************************        
       C550-INSERT-CPBL-ROWS.                                                   
                                                                                
           EXEC SQL                                                             
               INSERT INTO QQT_EXENT_CPBL                                       
                  (  EXENT_ID                                                   
                   , EXENT_CPBL_CDE                                             
                   , EFF_BEG_DTE                                                
                   , EFF_END_DTE                                                
                   , SOR_CRE_DTE                                                
                   , SOR_LAST_UPD_TMST                                          
                   , SOR_LAST_UPD_USRID)                                        
               VALUES                                                           
                  ( :QQT00008-EXENT-ID                                          
                   ,:QQT00008-EXENT-CPBL-CDE                                    
                   ,:QQT00008-EFF-BEG-DTE                                       
                   , NULL                                                       
                   ,:QQT00008-SOR-CRE-DTE                                       
                   ,:QQT00008-SOR-LAST-UPD-TMST                                 
                   ,:QQT00008-SOR-LAST-UPD-USRID)                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'C550-INSERT-CPBL       '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'INSERT ERROR ON CPBL       '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
      ******************************************************************        
      *     D100-UPDATE-EXENT.                                         *        
      * ==> PROCESSES:                                                 *        
      *     - UPDATE QQT_EXENT.                                        *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       D100-UPDATE-EXENT.                                                       
                                                                                
           EVALUATE TRUE                                                        
                                                                                
            WHEN LS-DUN-NBR-CURR    NOT = LS-DUN-NBR-PREV OR                    
                 LS-STATUS-CDE-CURR NOT = LS-STATUS-CDE-PREV                    
                                                                                
               MOVE  LS-ENT-ID-CURR   TO QQT00001-EXENT-ID                      
               MOVE  LS-DUN-NBR-CURR  TO QQT00001-EXENT-NBR                     
                                                                                
      *        PERFORM Y900-GET-EXTL-NM                                         
                                                                                
              EVALUATE LS-STATUS-CDE-CURR                                       
                                                                                
                 WHEN 'A'                                                       
                    MOVE 'Y' TO QQT00001-ACTV-IND                               
                                                                                
                 WHEN 'I'                                                       
                    MOVE 'N' TO QQT00001-ACTV-IND                               
                                                                                
              END-EVALUATE                                                      
                                                                                
               MOVE LS-CRE-TMST-CURR      TO QQT00001-SOR-CRE-TMST              
               MOVE LS-LAST-UPD-TMST-CURR TO                                    
                                    QQT00001-SOR-LAST-UPD-TMST                  
                                                                                
               PERFORM D150-UPDATE-EXENT-ROWS                                   
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     D150-INSERT-EXENT-ROWS                                     *        
      * ==> PROCESSES:                                                 *        
      *     - UPDATE QQT_EXENT.                                        *        
      * ==> PERFORMED FROM: D100-UPDATE-EXENT.                         *        
      ******************************************************************        
       D150-UPDATE-EXENT-ROWS.                                                  
                                                                                
           EXEC SQL                                                             
               UPDATE QQT_EXENT                                                 
               SET   EXENT_ID          =  :QQT00001-EXENT-ID                    
                   , EXENT_NBR         =  :QQT00001-EXENT-NBR                   
                   , PRIM_EXENT_NM     =  :QQT00001-PRIM-EXENT-NM               
                   , ACTV_IND          =  :QQT00001-ACTV-IND                    
                   , SHRT_EXENT_NM     =  :QQT00001-SHRT-EXENT-NM               
                   , SOR_CRE_TMST      =  :QQT00001-SOR-CRE-TMST                
                   , SOR_LAST_UPD_TMST =  :QQT00001-SOR-LAST-UPD-TMST           
               WHERE                                                            
                     EXENT_ID = :QQT00001-EXENT-ID                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'D150-UPDATE-EXENT      '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'UPDATE ERROR ON EXENT      '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     D200-DELETE-INSERT-ROL-ASSN.                               *        
      * ==> PROCESSES:                                                 *        
      *     - DELETE QQT_EXENT_ROL_ASSN ROWS.                          *        
      *     - INSERT QQT_EXENT_ROL_ASSN.                               *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       D200-DELETE-INSERT-ROL-ASSN.                                             
                                                                                
            MOVE LS-ENT-ID-CURR TO QQT00005-EXENT-ID.                           
                                                                                
            PERFORM D250-DELETE-ROL-ASSN-ROWS.                                  
                                                                                
            PERFORM C200-INSERT-ROL-ASSN.                                       
                                                                                
      ******************************************************************        
      *     D250-DELETE-ROL-ASSN-ROWS                                  *        
      * ==> PROCESSES:                                                 *        
      *     - DELETE QQT_EXENT_ROL_ASSN                                *        
      * ==> PERFORMED FROM: D200-DELETE-INSERT-ROL-ASSN.               *        
      ******************************************************************        
       D250-DELETE-ROL-ASSN-ROWS.                                               
                                                                                
           EXEC SQL                                                             
               DELETE FROM QQT_EXENT_ROL_ASSN                                   
                WHERE EXENT_ID  = :QQT00005-EXENT-ID                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE QQT00005-EXENT-ID TO WS-INTEGER-UNPK                    
                   MOVE WS-INTEGER-UNPK TO WS-INTEGER-EDIT                      
                   DISPLAY 'EXENT ID = ' WS-INTEGER-EDIT                        
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'D250-DELETE-ROL-ASSN   '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'DELETE ERROR ON ROL ASSN   '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     D300-UPDATE-MDSE-VND                                       *        
      * ==> PROCESSES:                                                 *        
      *     - UPDATE / INSERT QQT_MDSE_VND.                            *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       D300-UPDATE-MDSE-VND.                                                    
                                                                                
      * INSERT VENDOR WHEN THE VENDOR BECOMES MERCHANDISE.                      
                                                                                
             IF LS-PRIM-TYP-CDE-CURR = 'M' AND                                  
                LS-PRIM-TYP-CDE-PREV NOT = 'M'                                  
                                                                                
               MOVE  LS-ENT-ID-CURR   TO QQT00014-EXENT-ID                      
               MOVE  LS-CFY-SLSRNK-NBR-CURR                                     
                                      TO QQT00014-CFY-SLS-RNK-NBR               
               MOVE  LS-PFY-SLSRNK-NBR-CURR                                     
                                    TO QQT00014-PFY-SLSRNK-NBR                  
               MOVE  LS-P12MN-SLSRNK-NBR-CURR                                   
                                    TO QQT00014-P12-MN-SLSRNK-NBR               
                                                                                
               PERFORM C350-INSERT-MDSE-VND-ROWS                                
                                                                                
             END-IF.                                                            
                                                                                
      * UPDATE VENDOR WHEN MERCHANDISE VENDOR ATTRIBUTES CHANGE.                
                                                                                
             IF  LS-PRIM-TYP-CDE-CURR = 'M' AND                                 
                 LS-PRIM-TYP-CDE-PREV = 'M'                                     
                                                                                
                 MOVE  LS-ENT-ID-CURR   TO QQT00014-EXENT-ID                    
                 MOVE  LS-CFY-SLSRNK-NBR-CURR                                   
                                        TO QQT00014-CFY-SLS-RNK-NBR             
                 MOVE  LS-PFY-SLSRNK-NBR-CURR                                   
                                        TO QQT00014-PFY-SLSRNK-NBR              
                 MOVE  LS-P12MN-SLSRNK-NBR-CURR                                 
                                        TO QQT00014-P12-MN-SLSRNK-NBR           
                                                                                
                 PERFORM D350-UPDATE-MDSE-VND-ROWS                              
                                                                                
             END-IF.                                                            
      ******************************************************************        
      *     D350-UPDATE-MDSE-VND-ROWS                                  *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_MDSE_VND                            *        
      * ==> PERFORMED FROM: D300-UPDATE-MDSE-VND                       *        
      ******************************************************************        
       D350-UPDATE-MDSE-VND-ROWS.                                               
                                                                                
                                                                                
           EXEC SQL                                                             
               UPDATE  QQT_MDSE_VND                                             
                  SET   EXENT_ID          = :QQT00014-EXENT-ID                  
                      , CFY_SLS_RNK_NBR   = :QQT00014-CFY-SLS-RNK-NBR           
                      , PFY_SLSRNK_NBR    = :QQT00014-PFY-SLSRNK-NBR            
                      , P12_MN_SLSRNK_NBR = :QQT00014-P12-MN-SLSRNK-NBR         
                 WHERE  EXENT_ID         = :LS-ENT-ID-PREV                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'D350-UPDATE-MDSE-VND   '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'UPDATE ERROR ON MDSE VND   '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     D400-DELETE-INSERT-EXP-VND.                                *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXP_VND.                            *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       D400-DELETE-INSERT-EXP-VND.                                              
                                                                                
            EVALUATE TRUE                                                       
                                                                                
      *INSERT ROW WHEN VENDOR BECOMES EXPENSE VENDOR                            
                                                                                
            WHEN  LS-PRIM-TYP-CDE-CURR = 'E' AND                                
                  LS-PRIM-TYP-CDE-PREV NOT = 'E' AND                            
                  ( LS-PRTNR-TYP-CDE-CURR = 'BR' OR 'CO' OR                     
                                    'CR' OR 'DE' OR 'FF')                       
                                                                                
             MOVE  LS-ENT-ID-CURR   TO QQT00006-EXENT-ID                        
             MOVE  LS-SCAC-CDE-CURR TO QQT00006-SCAC-CDE                        
                                                                                
             PERFORM C450-INSERT-EXP-VND-ROWS                                   
                                                                                
      *DELETE AND INSERT SCAC CODE WHEN PRTNR TYPE CODE CHANGES                 
                                                                                
            WHEN  LS-PRIM-TYP-CDE-CURR = 'E' AND                                
                  LS-PRIM-TYP-CDE-PREV = 'E' AND                                
                  ( LS-PRTNR-TYP-CDE-PREV = 'BR' OR 'CO' OR                     
                                    'CR' OR 'DE' OR 'FF')                       
                                                                                
             MOVE  LS-ENT-ID-CURR   TO QQT00006-EXENT-ID                        
             MOVE  LS-SCAC-CDE-CURR TO QQT00006-SCAC-CDE                        
                                                                                
             PERFORM D450-DELETE-EXP-VND-ROWS                                   
             PERFORM C400-INSERT-EXP-VND                                        
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     D450-DELETE-EXP-VND-ROWS                                   *        
      * ==> PROCESSES:                                                 *        
      *     - UPDATE QQT_EXP_VND                                       *        
      * ==> PERFORMED FROM: D400-UPDATE-EXP-VND                        *        
      ******************************************************************        
       D450-DELETE-EXP-VND-ROWS.                                                
                                                                                
           EXEC SQL                                                             
               DELETE  FROM  QQT_EXP_VND                                        
                WHERE   EXENT_ID  = :LS-ENT-ID-PREV                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
               WHEN SQLCODE = +100                                              
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'D450-DELETE-EXP-VND    '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'DELETE ERROR ON EXP VND    '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
      ******************************************************************        
      *     D500-UPDATE-CPBL.                                          *        
      * ==> PROCESSES:                                                 *        
      *     - UPDATE QQT_EXENT_CPBL.                                   *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       D500-UPDATE-CPBL.                                                        
                                                                                
            EVALUATE TRUE                                                       
                                                                                
      *WHEN EDI_PO_PREPK_CDE CHANGES FROM 'Y' TO 'N' - UPDATE CPBL              
                                                                                
            WHEN LS-EDI-PO-PRE-PK-CDE-PREV = 'Y' AND                            
                 LS-EDI-PO-PRE-PK-CDE-CURR  = 'N'                               
                                                                                
             MOVE 'PPK' TO PV-HOLD-CPBL-CDE                                     
             PERFORM C550-UPD-CPBL-EXPIRE-ROW                                   
                                                                                
      *WHEN EDI_PO_PREPK_CDE CHANGES FROM 'C' TO 'N' - UPDATE CPBL              
                                                                                
            WHEN LS-EDI-PO-PRE-PK-CDE-PREV = 'C' AND                            
                 LS-EDI-PO-PRE-PK-CDE-CURR  = 'N'                               
                                                                                
             MOVE 'CPK' TO PV-HOLD-CPBL-CDE                                     
             PERFORM C550-UPD-CPBL-EXPIRE-ROW                                   
                                                                                
      *WHEN EDI_PO_PREPK_CDE CHANGES FROM 'C' TO 'Y' - UPD/INS CPBL             
                                                                                
            WHEN LS-EDI-PO-PRE-PK-CDE-PREV = 'C' AND                            
                 LS-EDI-PO-PRE-PK-CDE-CURR  = 'Y'                               
                                                                                
             MOVE 'CPK' TO PV-HOLD-CPBL-CDE                                     
             PERFORM C550-UPD-CPBL-EXPIRE-ROW                                   
                                                                                
             MOVE  LS-ENT-ID-CURR   TO QQT00008-EXENT-ID                        
             MOVE  'PPK'            TO QQT00008-EXENT-CPBL-CDE                  
             MOVE  PV-CURRENT-TMST  TO QQT00008-EFF-BEG-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-CRE-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-LAST-UPD-TMST               
             MOVE  'QQKSP001'       TO QQT00008-SOR-LAST-UPD-USRID              
                                                                                
             PERFORM C550-INSERT-CPBL-ROWS                                      
                                                                                
      *WHEN EDI_PO_PREPK_CDE CHANGES FROM 'Y' TO 'C' - UPD/INS CPBL             
                                                                                
            WHEN LS-EDI-PO-PRE-PK-CDE-PREV = 'Y' AND                            
                 LS-EDI-PO-PRE-PK-CDE-CURR  = 'C'                               
                                                                                
             MOVE 'PPK' TO PV-HOLD-CPBL-CDE                                     
             PERFORM C550-UPD-CPBL-EXPIRE-ROW                                   
                                                                                
             MOVE  LS-ENT-ID-CURR   TO QQT00008-EXENT-ID                        
             MOVE  'CPK'            TO QQT00008-EXENT-CPBL-CDE                  
             MOVE  PV-CURRENT-TMST  TO QQT00008-EFF-BEG-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-CRE-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-LAST-UPD-TMST               
             MOVE  'QQKSP001'       TO QQT00008-SOR-LAST-UPD-USRID              
                                                                                
             PERFORM C550-INSERT-CPBL-ROWS                                      
                                                                                
      *WHEN EDI_PO_PREPK_CDE CHANGES FROM 'X' TO 'N' - UPDATE                   
                                                                                
            WHEN LS-EDI-PO-PRE-PK-CDE-PREV = 'X' AND                            
                 LS-EDI-PO-PRE-PK-CDE-CURR  = 'N'                               
                                                                                
             MOVE 'XPK' TO PV-HOLD-CPBL-CDE                                     
             PERFORM C550-UPD-CPBL-EXPIRE-ROW                                   
                                                                                
      *WHEN EDI_PO_PREPK_CDE CHANGES FROM 'X' TO 'Y' - UPD/INS CPBL             
                                                                                
            WHEN LS-EDI-PO-PRE-PK-CDE-PREV = 'X' AND                            
                 LS-EDI-PO-PRE-PK-CDE-CURR  = 'Y'                               
                                                                                
             MOVE 'XPK' TO PV-HOLD-CPBL-CDE                                     
             PERFORM C550-UPD-CPBL-EXPIRE-ROW                                   
                                                                                
             MOVE  LS-ENT-ID-CURR   TO QQT00008-EXENT-ID                        
             MOVE  'PPK'            TO QQT00008-EXENT-CPBL-CDE                  
             MOVE  PV-CURRENT-TMST  TO QQT00008-EFF-BEG-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-CRE-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-LAST-UPD-TMST               
             MOVE  'QQKSP001'       TO QQT00008-SOR-LAST-UPD-USRID              
                                                                                
             PERFORM C550-INSERT-CPBL-ROWS                                      
                                                                                
      *WHEN EDI_PO_PREPK_CDE CHANGES FROM 'X' TO 'C' - UPD/INS CPBL             
                                                                                
            WHEN LS-EDI-PO-PRE-PK-CDE-PREV = 'X' AND                            
                 LS-EDI-PO-PRE-PK-CDE-CURR  = 'C'                               
                                                                                
             MOVE 'XPK' TO PV-HOLD-CPBL-CDE                                     
             PERFORM C550-UPD-CPBL-EXPIRE-ROW                                   
                                                                                
             MOVE  LS-ENT-ID-CURR   TO QQT00008-EXENT-ID                        
             MOVE  'CPK'            TO QQT00008-EXENT-CPBL-CDE                  
             MOVE  PV-CURRENT-TMST  TO QQT00008-EFF-BEG-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-CRE-DTE                     
             MOVE  LS-CRE-TMST-CURR TO QQT00008-SOR-LAST-UPD-TMST               
             MOVE  'QQKSP001'       TO QQT00008-SOR-LAST-UPD-USRID              
                                                                                
             PERFORM C550-INSERT-CPBL-ROWS                                      
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C550-UPD-CPBL-EXPIRE-ROW                                   *        
      * ==> PROCESSES:                                                 *        
      *     - UPDATE QQT_EXENT_CPBL                                    *        
      * ==> PERFORMED FROM: D500-UPDATE-CPBL                           *        
      ******************************************************************        
       C550-UPD-CPBL-EXPIRE-ROW.                                                
                                                                                
           EXEC SQL                                                             
               UPDATE  QQT_EXENT_CPBL                                           
                  SET  EFF_END_DTE    = :PV-CURRENT-DATE                        
                WHERE  EXENT_ID       = :LS-ENT-ID-PREV                         
                  AND  EXENT_CPBL_CDE = :PV-HOLD-CPBL-CDE                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
               WHEN SQLCODE = +100                                              
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'D550-EXPIRE-CPBL-ROW   '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'EXPIRE CPBL ROW            '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
      ******************************************************************        
      *     Y900-GET-EXTL-NM..                                         *        
      * ==> PROCESSES:                                                 *        
      *     - GET THE PRIM_EXTL_NM,SHRT_EXTL_NM FROM TENTNME FOR       *        
      *       GIVEN ENT ID.                                            *        
      * ==> PERFORMED FROM: C100-INSERT-EXENT,D100-UPDATE-EXEXNT       *        
      ******************************************************************        
      *Y900-GET-EXTL-NM.                                                        
      *                                                                         
      * GET PRIM-EXENT-NM.                                                      
      *                                                                         
      *    EXEC SQL                                                             
      *        SELECT COALESCE(ENT_NAME_DESC,'')                                
      *          INTO :QQT00001-PRIM-EXENT-NM                                   
      *          FROM TENTNME                                                   
      *         WHERE ENT_ID   = :LS-ENT-ID-CURR                                
      *           AND TYPE_CDE = '01'                                           
      *         WITH UR                                                         
      *    END-EXEC.                                                            
      *                                                                         
      *                                                                         
      *    EVALUATE TRUE                                                        
      *        WHEN SQLCODE = +0                                                
      *            MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
      *                                                                         
      *        WHEN SQLCODE = +100                                              
      *            MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
      *            MOVE SPACES           TO  QQT00001-PRIM-EXENT-NM             
      *                                                                         
      *        WHEN OTHER                                                       
      *            MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
      *            MOVE 'Y900-GET-EXTL-NM       '                               
      *                                  TO PV-PARAGRAPH-NAME                   
      *            MOVE 'SELECT ERR ON TENTNME PRIM NM   '                      
      *                                  TO PV-DB2-OPERATION-ATTEMPTED          
      *            PERFORM 9998-SQL-ERROR                                       
      *    END-EVALUATE.                                                        
      *                                                                         
      * GET SHRT-EXENT-NM.                                                      
      *                                                                         
      *    EXEC SQL                                                             
      *        SELECT COALESCE(ENT_NAME_DESC,'')                                
      *          INTO :QQT00001-SHRT-EXENT-NM                                   
      *          FROM TENTNME                                                   
      *         WHERE ENT_ID   = :LS-ENT-ID-CURR                                
      *           AND TYPE_CDE = '04'                                           
      *         WITH UR                                                         
      *    END-EXEC.                                                            
      *                                                                         
      *    EVALUATE TRUE                                                        
      *        WHEN SQLCODE = +0                                                
      *            MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
      *                                                                         
      *        WHEN SQLCODE = +100                                              
      *            MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
      *            MOVE SPACES           TO QQT00001-SHRT-EXENT-NM              
      *                                                                         
      *        WHEN OTHER                                                       
      *            MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
      *            MOVE 'Y900-GET-EXTL-NM       '                               
      *                                  TO PV-PARAGRAPH-NAME                   
      *            MOVE 'SELECT ERR ON TENTNME SHRT NM   '                      
      *                                  TO PV-DB2-OPERATION-ATTEMPTED          
      *            PERFORM 9998-SQL-ERROR                                       
      *    END-EVALUATE.                                                        
      *                                                                         
                                                                                
      *-----------------------------------------------------------------        
      * DB2 SQL ERROR PROCESSING                                                
      *-----------------------------------------------------------------        
       9998-SQL-ERROR.                                                          
                                                                                
           SET PS-SQL-ERROR TO TRUE.                                            
                                                                                
           DISPLAY '** '                                                        
                   PC-PROGRAM-NAME                                              
                   ' ABEND IN PARAGRAPH '                                       
                   PV-PARAGRAPH-NAME.                                           
           DISPLAY '** '                                                        
                   PC-PROGRAM-NAME                                              
                   ' OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                  
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES MODULE DSNTIAR TO FORMAT             
      *    THE INFO IN SQLCA INTO UNDERSTANDABLE VERBAGE.                       
                                                                                
           COPY DPPD004.                                                        
      *                                                                         
           EXEC SQL                                                             
               ROLLBACK                                                         
           END-EXEC.                                                            
