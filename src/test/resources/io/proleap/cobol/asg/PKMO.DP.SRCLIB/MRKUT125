000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.             MRKUT125.                                        
000300 AUTHOR.                 DONALD TOMLINSON.                                
000400 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000500 DATE-WRITTEN.           08-22-2003.                                      
000600 DATE-COMPILED.                                                           
000700**                                                                        
000800*****************************************************************         
000900*     THE PURPOSE OF THIS PROGRAM IS TO BUILD A CONTROL CARD    *         
001000*     (MRCC144) WHICH WILL BE COPIED TO A GDG AND USED BY WH    *         
001100*     UNIX JOB. EACH WEEK (15 WEEK ROTATION) A NEW GROUP OF     *         
001200*     DEPARTMENTS WILL BE BUILT IN THE CONTROL FILE.            *         
001300*****************************************************************         
001810* CHANGED 09/15/03 BY DONALD TOMLINSON  WR# 23385               *         
001820*    NEW PGM.                                                   *         
001830*****************************************************************         
002000**                                                              *         
002100 ENVIRONMENT DIVISION.                                                    
002200 CONFIGURATION SECTION.                                                   
002300 SOURCE-COMPUTER.        IBM-370.                                         
002400 OBJECT-COMPUTER.        IBM-370.                                         
002500 INPUT-OUTPUT SECTION.                                                    
002600 FILE-CONTROL.                                                            
002700**                                                                        
002800     SELECT MRSI146-CONTROL      ASSIGN        TO INP01.                  
002900**                                                                        
003000     SELECT MRCC146-CONTROL      ASSIGN        TO INP02.                  
003100**                                                                        
003200     SELECT MRCC144-OUT          ASSIGN        TO OUT01.                  
003300**                                                                        
003400     SELECT MRCC146-OUT          ASSIGN        TO OUT02.                  
003500**                                                                        
003600 DATA DIVISION.                                                           
003700 FILE SECTION.                                                            
003800**                                                                        
003900** THIS FILE WILL CONTAIN THE TABLE OF DEPT'S FOR                         
004000** EACH OF THE 15 WEEKS IN THE ROTATION CYCLE.                            
004100 FD  MRSI146-CONTROL                                                      
004200     RECORDING MODE IS F.                                                 
004300 01  MRSI146-CONTROL-RECORD         PIC X(80).                            
004400**                                                                        
004500**                                                                        
004600** THIS RECORD WILL CONTAIN THE LAST WEEK RAN IN ROTATION                 
004700 FD  MRCC146-CONTROL                                                      
004800     RECORDING MODE IS F.                                                 
004900 01  MRCC146-CONTROL-RECORD         PIC X(80).                            
005000**                                                                        
005100**                                                                        
005200** THIS FILE WILL CONTAIN THE DEPTS TO BE PASSED                          
005300** ON TO WAREHOUSE PROCESSING.                                            
005400 FD  MRCC144-OUT                                                          
005500     RECORDING MODE IS F.                                                 
005600 01  MRCC144-OUT-RECORD             PIC X(80).                            
005700**                                                                        
005800**                                                                        
005900** THIS RECORD WILL CONTAIN THE NEW UPDATED LAST WEEK RAN                 
006000 FD  MRCC146-OUT                                                          
006100     RECORDING MODE IS F.                                                 
006200 01  MRCC146-OUT-RECORD             PIC X(80).                            
006300**                                                                        
006400**                                                                        
006500 WORKING-STORAGE SECTION.                                                 
006600** THIS IS THE RECORD LAYOUT FOR THE DEPT RECORDS PASSED TO WARE-         
006700** HOUSE FOR UNIX PROCESSING.                                             
006800 01  WS-MRCC144-OUT-RECORD.                                               
006900     05  MRCC144-DEPT               PIC 9(03).                            
007000     05  FILLER                     PIC X(77)   VALUE SPACES.             
007100**                                                                        
007200** THIS IS THE RECORD LAYOUT FOR THE CONTROL MEMBER MRCC146.              
007300 01  WS-MRCC146-CONTROL-RECORD.                                           
007400     05  FILLER                     PIC X(30).                            
007500     05  MRCC146-LAST-WEEK          PIC 9(02).                            
007510     05  FILLER                     PIC X(48).                            
007600**                                                                        
007700**                                                                        
007800 01  SWITCHES.                                                            
007900     05  MRCC146-END-OF-FILE-SW     PIC X(1)    VALUE 'N'.                
008000         88  MRCC146-END-OF-FILE-YES            VALUE 'Y'.                
008100         88  MRCC146-END-OF-FILE-NO             VALUE 'N'.                
008200     05  MRSI146-END-OF-FILE-SW     PIC X(1)    VALUE 'N'.                
008300         88  MRSI146-END-OF-FILE-YES            VALUE 'Y'.                
008400         88  MRSI146-END-OF-FILE-NO             VALUE 'N'.                
008500**                                                                        
008600**                                                                        
008700 01  VARIABLES.                                                           
008800     05  WS-LAST-WEEK               PIC 9(02)    VALUE 0.                 
008900     05  WS-CURRENT-WEEK            PIC 9(02)    VALUE 0.                 
009000     05  WS-INPUT-DEPTS             PIC 9(02)    VALUE 0.                 
009010     05  WS-OUTPUT-DEPTS            PIC 9(02)    VALUE 0.                 
009100**                                                                        
009200**                                                                        
009300 01  CONSTANTS.                                                           
009400     05  WS-Y                       PIC X(01)    VALUE 'Y'.               
009500     05  WS-1                       PIC 9(01)    VALUE 1.                 
009600     05  WS-15                      PIC 9(02)    VALUE 15.                
009700     05  WS-ZERO                    PIC 9(03)    VALUE ZERO.              
009800**                                                                        
009900*************************************************************             
010000**RECORD LAYOUT FOR DEPT TABLE DATA IN PKMO.CONTROL(MRSI146).             
010010**RECORD USES 'DEPT-INDEX' TO REFERENCE DEPARTMENT FIELD.                 
010020*************************************************************             
010100     COPY MRRD146.                                                        
010110*************************************************************             
010300**                                                                        
010400 PROCEDURE DIVISION.                                                      
010500 0000-MAINLINE.                                                           
010600**                                                                        
010700     PERFORM 0100-INITIALIZATION.                                         
010800**                                                                        
010900     PERFORM 0200-PROCESS-WEEK                                            
011000         UNTIL MRSI146-END-OF-FILE-YES.                                   
011100**                                                                        
011200     PERFORM 0800-WRITE-MRCC146-OUT.                                      
011300**                                                                        
011310     DISPLAY 'INPUT DEPTS FOUND = ' WS-INPUT-DEPTS.                       
011311     DISPLAY 'OUTPUT DEPTS WRITTEN = ' WS-OUTPUT-DEPTS.                   
011330**                                                                        
011400     PERFORM 0900-CLOSE-FILES.                                            
011500**                                                                        
011600     STOP RUN.                                                            
011800**                                                                        
011810****************************************************************          
011820** OPEN ALL FILES AND PERFORM INITIAL READS FOR BOTH INPUTS.              
011830****************************************************************          
011900 0100-INITIALIZATION.                                                     
012000     OPEN INPUT MRCC146-CONTROL                                           
012100                MRSI146-CONTROL                                           
012200         OUTPUT MRCC144-OUT                                               
012300                MRCC146-OUT.                                              
012400**                                                                        
012500     PERFORM 0110-READ-MRCC146.                                           
012600**                                                                        
012700     PERFORM 0120-READ-MRSI146.                                           
012800**                                                                        
012900****************************************************************          
013000** READ TKMA.CONTROL(MRCC146) CONTROL MEMBER WHICH CONTAINS               
013100** THE LAST WEEK PROCESSED.                                               
013110****************************************************************          
013200 0110-READ-MRCC146.                                                       
013210     MOVE SPACES          TO WS-MRCC146-CONTROL-RECORD.                   
013300     READ MRCC146-CONTROL INTO WS-MRCC146-CONTROL-RECORD                  
013400         AT END MOVE WS-Y TO MRCC146-END-OF-FILE-SW.                      
013401**                                                                        
013410     DISPLAY 'MRKUT125 PROGRAM INFORMATION'                               
013420     DISPLAY 'OLD CONTROL MEMBER MRCC146 = '                              
013430         WS-MRCC146-CONTROL-RECORD.                                       
013440**                                                                        
013500     IF MRCC146-END-OF-FILE-NO                                            
013600         MOVE MRCC146-LAST-WEEK TO WS-LAST-WEEK                           
013700         MOVE WS-LAST-WEEK      TO WS-CURRENT-WEEK                        
013800         ADD WS-1 TO WS-CURRENT-WEEK                                      
013820         MOVE WS-CURRENT-WEEK   TO MRCC146-LAST-WEEK                      
013900         IF WS-CURRENT-WEEK > WS-15                                       
014000             MOVE WS-1          TO WS-CURRENT-WEEK                        
014010             MOVE WS-CURRENT-WEEK                                         
014020                                TO MRCC146-LAST-WEEK                      
014030         ELSE                                                             
014040             CONTINUE                                                     
014041         END-IF                                                           
014050     ELSE                                                                 
014060         DISPLAY 'MRCC146 CONTROL RECORD MISSING'                         
014070     END-IF.                                                              
014100**                                                                        
014200****************************************************************          
014300** READ TKMA.CONTROL(MRSI146) CONTROL MEMBER WHICH CONTAINS               
014400** THE 12 ROTATION GROUPS OF DEPARTMENTS.                                 
014410****************************************************************          
014500 0120-READ-MRSI146.                                                       
014600     READ MRSI146-CONTROL INTO MR146-DEPT-RECORD                          
014700         AT END MOVE WS-Y TO MRSI146-END-OF-FILE-SW.                      
014800**                                                                        
014900****************************************************************          
015000** THIS PARAGRAPH WILL MOVE THE DEPARTMENTS FROM THE CONTROL              
015100** MEMBER MRSI146 TO THE OUTPUT TO BE WRITTEN.                            
015110****************************************************************          
015200 0200-PROCESS-WEEK.                                                       
015300     IF MR146-WEEK = WS-CURRENT-WEEK                                      
015400         PERFORM 0210-PROCESS-DEPTS                                       
015500             VARYING DEPT-INDEX FROM +1 BY +1                             
015600             UNTIL DEPT-INDEX > +10                                       
015700         MOVE WS-Y  TO MRSI146-END-OF-FILE-SW                             
015800     ELSE                                                                 
015900         PERFORM 0120-READ-MRSI146.                                       
016000**                                                                        
016100****************************************************************          
016200** THIS PARAGRAPH WILL MOVE THE DEPARTMENTS FROM THE CONTROL              
016300** MEMBER MRSI146 TO THE OUTPUT TO BE WRITTEN.                            
016310****************************************************************          
016400 0210-PROCESS-DEPTS.                                                      
016500     IF MR146-DEPT(DEPT-INDEX) = WS-ZERO                                  
016600         SET DEPT-INDEX TO +10                                            
016700     ELSE                                                                 
016800         MOVE MR146-DEPT(DEPT-INDEX) TO MRCC144-DEPT                      
016810         ADD WS-1  TO WS-INPUT-DEPTS                                      
016900         PERFORM 0700-WRITE-MRCC144-OUT.                                  
017000**                                                                        
017100****************************************************************          
017110** WRITE THE MRCC144 RECORD THAT WILL CONTAIN A DEPT NUMBER. THIS         
017120** FILE WILL THEN BE MOVED TO TKMA.CONTROL(MRCC144) LATER IN JOB.         
017130****************************************************************          
017200 0700-WRITE-MRCC144-OUT.                                                  
017300     WRITE MRCC144-OUT-RECORD FROM WS-MRCC144-OUT-RECORD.                 
017310     ADD WS-1  TO WS-OUTPUT-DEPTS.                                        
017400**                                                                        
017500****************************************************************          
017501** WRITE MRCC146 MEMBER WHICH CONTAINS THE NEW LAST                       
017502** WEEK PROCESSED. THIS FILE WILL THEN BE MOVED TO                        
017503** TKMA.CONTROL(MRCC146) LATER IN THE JOB.                                
017504****************************************************************          
017600 0800-WRITE-MRCC146-OUT.                                                  
017700     WRITE MRCC146-OUT-RECORD     FROM WS-MRCC146-CONTROL-RECORD.         
017710     DISPLAY 'NEW CONTROL MEMBER MRCC146 = '                              
017720         WS-MRCC146-CONTROL-RECORD.                                       
017800**                                                                        
017900****************************************************************          
017910** CLOSE ALL FILES.                                                       
017920****************************************************************          
018000 0900-CLOSE-FILES.                                                        
018100     CLOSE MRSI146-CONTROL                                                
018110           MRCC146-CONTROL.                                               
018200**                                                                        
018300     CLOSE MRCC144-OUT                                                    
018500           MRCC146-OUT.                                                   
018600**                                                                        
018700**                  END OF PROGRAM                                        
