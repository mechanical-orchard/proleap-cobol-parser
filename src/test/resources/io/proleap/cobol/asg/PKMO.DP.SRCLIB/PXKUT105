       IDENTIFICATION DIVISION.                                         00010005
       PROGRAM-ID.      PXKUT105.                                       00020005
       AUTHOR.          DAVID WELLER.                                   00030005
       DATE-WRITTEN.    AUGUST 2010.                                    00040005
      *---------------------------------------------------------------  00050005
      *  THIS PROGRAM DETERMINES IF A CLEARANCE TO ACTIVE (CTA) EVENT   00060005
      *  IS NOT NEEDED FOR A SKU/STORE BECAUSE THE SKU IS ALREADY       00070005
      *  ACTIVE AT THE STORE.  IF THE CTA IS NOT NEEDED, THE SKU/STORE  00080005
      *  WILL BE WRITTEN TO THE EXCLUSION FILE.                         00090005
      *--------------------------------------------------------------*  00100005
      * DATE:  AUGUST 2010                                              00110005
      * WR/IR#: PKE000000004715                                         00120008
      * PROGRAMMER: DAVE WELLER                                         00130005
      * DESCRIPTION: - DETERMINE IF A CTA CHANGE NEEDS TO BE SENT       00140005
      *     FOR A SKU/STORE.  IF THE SKU IS ALREADY ACTIVE AT THE       00150005
      *     STORE, WRITE THE SKU/STORE TO A CTA EXCLUSION RECORD.       00160005
      *--------------------------------------------------------------*  00170005
       ENVIRONMENT DIVISION.                                            00180005
      *----------------------------------------------------------       00190005
       CONFIGURATION SECTION.                                           00200005
       SOURCE-COMPUTER.        IBM-3090.                                00210005
       OBJECT-COMPUTER.        IBM-3090.                                00220005
                                                                        00230005
       INPUT-OUTPUT SECTION.                                            00240005
                                                                        00250005
       FILE-CONTROL.                                                    00260005
                                                                        00270005
           SELECT WHITE-TICKET-EXTR-FILE-IN                             00280005
                  ASSIGN TO INP01.                                      00290005
           SELECT SKU-STR-EXCLSN-FILE-OUT                               00300005
                  ASSIGN TO OUT01.                                      00310005
                                                                        00320005
       DATA DIVISION.                                                   00330005
       FILE SECTION.                                                    00340005
                                                                        00350005
       FD  WHITE-TICKET-EXTR-FILE-IN                                    00360005
           RECORDING MODE F                                             00370005
           BLOCK CONTAINS 0 RECORDS                                     00380005
           LABEL RECORDS ARE OMITTED                                    00390005
           DATA RECORD IS WHITE-TICKET-EXTR-REC-IN.                     00400005
       01  WHITE-TICKET-EXTR-REC-IN        PIC X(213).                  00410005
                                                                        00420005
       FD  SKU-STR-EXCLSN-FILE-OUT                                      00430005
           RECORDING MODE F                                             00440005
           BLOCK CONTAINS 0 RECORDS                                     00450005
           LABEL RECORDS ARE OMITTED                                    00460005
           DATA RECORD IS SKU-STR-EXCLSN-REC-OUT.                       00470005
       01  SKU-STR-EXCLSN-REC-OUT          PIC X(7).                    00480005
                                                                        00490005
       WORKING-STORAGE SECTION.                                         00500005
      *-----------------------------------------------------------      00510005
      * PS- PROGRAM SWITCHES                                            00520005
      *-----------------------------------------------------------      00530005
       01  PS-PROGRAM-SWITCHES.                                         00540005
           05  PS-END-OF-INPUT-FILE-SW        PIC X(01) VALUE 'N'.      00550005
               88  PS-END-OF-INPUT-FILE                 VALUE 'Y'.      00560005
               88  PS-NOT-END-OF-INPUT-FILE             VALUE 'N'.      00570005
           05  PS-COUNT-FOUND-SW              PIC  X(001).              00580005
               88  PS-COUNT-NOT-FOUND                     VALUE 'N'.    00590005
               88  PS-COUNT-FOUND                         VALUE 'Y'.    00600005
                                                                        00610005
      *-----------------------------------------------------------      00620005
      * PC- PROGRAM CONSTANTS                                           00630005
      *-----------------------------------------------------------      00640005
       01  PC-PROGRAM-CONSTANTS.                                        00650005
           05  PC-CURRENT-PROGRAM-NAME   PIC X(08)  VALUE 'PXKUT105'.   00660005
           05  PC-MAX-RETRIES            PIC S9(04) VALUE +5 COMP.      00670005
                                                                        00680005
      *-----------------------------------------------------------      00690005
      * PV- PROGRAM VARIABLES                                           00700005
      *-----------------------------------------------------------      00710005
       01  PV-PROGRAM-VARAIBLES.                                        00720005
                                                                        00730005
           05  PV-DISPLAY-SKU-NBR        PIC 9(10)  VALUE ZERO.         00740005
           05  PV-DISPLAY-LOC-NBR        PIC 9(4)   VALUE ZERO.         00750005
                                                                        00760005
      *-----------------------------------------------------------      00770005
      * PC- PROGRAM COUNTERS                                            00780005
      *-----------------------------------------------------------      00790005
       01  PC-PROGRAM-COUNTERS.                                         00800005
           05  PC-CNT-READ-RECS             PIC 9(09)  VALUE 0.         00810005
           05  PC-CNT-WRITTEN-RECS          PIC 9(09)  VALUE 0.         00820005
           05  PC-CNT-DISPLAY-RECS          PIC 9(09)  VALUE 0.         00830005
           05  PV-ACTIVE-ROW-COUNT          PIC S9(03) COMP-3           00840005
                                                       VALUE +0.        00850005
           05  PV-RETRY-COUNT               PIC S9(04) VALUE ZERO       00860005
                                                       COMP SYNC.       00870005
                                                                        00880005
      *----------------------------------------------------------       00890005
      * WHITE TICKET EXTRACT - INPUT                                    00900005
      *----------------------------------------------------------       00910005
           COPY PXRD040.                                                00920005
                                                                        00930005
      *----------------------------------------------------------       00940005
      * SKU/STORE EXCLUSION EXTRACT - OUTPUT                            00950005
      *----------------------------------------------------------       00960005
           COPY PXRD053.                                                00970005
                                                                        00980005
      *-----------------------------------------------------------      00990005
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         01000005
      *-----------------------------------------------------------      01010005
           COPY DPWS004.                                                01020005
                                                                        01030005
      *-----------------------------------------------------------      01040005
      * DB2 ABEND COPYBOOK                                              01050005
      *-----------------------------------------------------------      01060005
           COPY DPWS900.                                                01070005
                                                                        01080005
      *-----------------------------------------------------------      01090005
      * DB2 MESSAGE AREA                                                01100005
      *-----------------------------------------------------------      01110005
           COPY SQLCA2.                                                 01120005
                                                                        01130005
      *-----------------------------------------------------------      01140005
      * ERROR HANDLING                                                  01150005
      *-----------------------------------------------------------      01160005
       01  PV-MISC-ABEND-FIELDS.                                        01170005
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      01180005
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      01190005
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      01200005
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      01210005
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 01220005
           88  ABEND-4003-CTRL-CARD-ERROR        VALUE +4003.           01230005
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           01240005
           88  ABEND-4019-CAL-SUB-ERROR          VALUE +4019.           01250005
           88  ABEND-4020-MQ-ERROR               VALUE +4020.           01260005
           88  ABEND-4444-FORCED-ABEND           VALUE +4444.           01270005
                                                                        01280005
      *-----------------------------------------------------------      01290005
      * DB2 COMMUNICATIONS AREA                                         01300005
      *-----------------------------------------------------------      01310005
           EXEC SQL                                                     01320005
                INCLUDE SQLCA                                           01330005
           END-EXEC.                                                    01340005
                                                                        01350005
      *--------------------------------------------------               01360005
      *  XST_SKU_LOC TABLE                                              01370005
      *--------------------------------------------------               01380005
           EXEC SQL                                                     01390005
               INCLUDE XST00008                                         01400005
           END-EXEC.                                                    01410005
                                                                        01420005
      *--------------------------------------------------               01430005
      *  XST_SKU TABLE                                                  01440005
      *--------------------------------------------------               01450005
           EXEC SQL                                                     01460005
               INCLUDE XST00010                                         01470005
           END-EXEC.                                                    01480005
                                                                        01490005
       LINKAGE SECTION.                                                 01500005
                                                                        01510005
      *----------------------------------------------------------       01520005
       PROCEDURE DIVISION.                                              01530005
      *----------------------------------------------------------       01540005
       0000-MAINLINE.                                                   01550005
                                                                        01560005
           PERFORM 1000-INITIALIZATION.                                 01570005
                                                                        01580005
           PERFORM 2000-PROCESS-INPUT                                   01590005
               UNTIL PS-END-OF-INPUT-FILE.                              01600005
                                                                        01610005
           PERFORM 2400-CLOSE-FILES.                                    01620005
                                                                        01630005
           PERFORM 9999-WRAPUP.                                         01640005
                                                                        01650005
           GOBACK.                                                      01660005
                                                                        01670005
      *----------------------------------------------------------       01680005
      * 1000-INITIALIZATION.                                            01690005
      *     INITIALIZATION AREA - INITIALIZES ALL VARIABLES, OPENS      01700005
      *     FILES, READS INPUT FILES                                    01710005
      *----------------------------------------------------------       01720005
       1000-INITIALIZATION.                                             01730005
                                                                        01740005
           OPEN INPUT  WHITE-TICKET-EXTR-FILE-IN.                       01750005
           OPEN OUTPUT SKU-STR-EXCLSN-FILE-OUT.                         01760005
                                                                        01770005
           PERFORM 1500-READ-INPUT-FILE.                                01780005
                                                                        01790005
      *----------------------------------------------------------       01800005
      * 1500-READ-INPUT-FILE.                                           01810005
      *----------------------------------------------------------       01820005
       1500-READ-INPUT-FILE.                                            01830005
                                                                        01840005
           READ WHITE-TICKET-EXTR-FILE-IN INTO PXRD040-EXTRACT-RECORD   01850005
               AT END                                                   01860005
               SET PS-END-OF-INPUT-FILE TO TRUE.                        01870005
           IF PS-NOT-END-OF-INPUT-FILE                                  01880005
               ADD +1             TO PC-CNT-READ-RECS                   01890005
           END-IF.                                                      01900005
                                                                        01910005
      *----------------------------------------------------------       01920005
      * 2000-PROCESS-INPUT.                                             01930005
      *     FETCHES THE MERCHANDISE CURSOR AND THEN FETCHES ANY         01940005
      *     STYLE ID'S THAT ARE WITHIN THOSE DEPARTMENT AND SUB         01950005
      *     CLASSES.  CREATES DUMMY SKU RECORDS.                        01960005
      *----------------------------------------------------------       01970005
       2000-PROCESS-INPUT.                                              01980005
                                                                        01990005
      * CREATE AN EXCLUSION ROW FOR CTA ROWS, ONLY                      02000005
           IF PXRD040-EVNT-CTG-CDE = 'ACT'                              02010005
               AND PXRD040-PRCHG-TYP-CDE = 'CTA'                        02020005
               PERFORM 5900-SELECT-XST-SKU-LOC                          02030005
               IF PS-COUNT-FOUND                                        02040005
                  PERFORM 8300-WRITE-EXCLSN-REC                         02050005
               END-IF                                                   02060005
           END-IF.                                                      02070005
                                                                        02080005
           ADD  1 TO PC-CNT-DISPLAY-RECS.                               02090005
           IF PC-CNT-DISPLAY-RECS > 100000                              02100005
               DISPLAY '                       '                        02110005
               DISPLAY '---  INTERIM COUNTS ---'                        02120005
               DISPLAY 'NUMBER OF WHTTKT EXTR RECS WRITTEN: '           02130007
                       PC-CNT-WRITTEN-RECS                              02140005
                                                                        02150005
               MOVE ZEROES TO PC-CNT-DISPLAY-RECS                       02160005
           END-IF.                                                      02170005
                                                                        02180005
           PERFORM 1500-READ-INPUT-FILE.                                02190005
                                                                        02200005
      *-----------------------------------------------------------      02210005
      * 2400-CLOSE-FILES.                                               02220005
      *     CLOSE THE MERCHANDISE CURSOR.                               02230005
      *-----------------------------------------------------------      02240005
       2400-CLOSE-FILES.                                                02250005
                                                                        02260005
           CLOSE       WHITE-TICKET-EXTR-FILE-IN.                       02270005
           CLOSE       SKU-STR-EXCLSN-FILE-OUT.                         02280005
                                                                        02290005
      *----------------------------------------------------------       02300005
      * 5900-SELECT-XST-SKU-LOC.                                        02310005
      *     RETRIEVE XST-SKU-LOC.                                       02320005
      *----------------------------------------------------------       02330005
       5900-SELECT-XST-SKU-LOC.                                         02340005
                                                                        02350005
           MOVE PXRD040-STR-NBR    TO XST00008-LOC-NBR.                 02360005
           MOVE PXRD040-SKU-NBR    TO XST00010-SKU-NBR.                 02370005
           SET PS-COUNT-NOT-FOUND  TO TRUE                              02380005
                                                                        02390005
           PERFORM WITH TEST AFTER                                      02400005
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       02410005
                 UNTIL SQLCODE = +0                                     02420005
                    OR SQLCODE = +100                                   02430005
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  02440005
               EXEC SQL                                                 02450005
                   SELECT COUNT(*)                                      02460005
                     INTO :PV-ACTIVE-ROW-COUNT                          02470005
                     FROM XST_SKU_LOC A                                 02480005
                        , XST_SKU     B                                 02490005
                    WHERE  B.SKU_NBR           =                        02500005
                          :XST00010-SKU-NBR                             02510005
                      AND  A.INTR_UPC_ID       =  B.INTR_UPC_ID         02520005
                      AND  (A.END_TMST         IS NULL                  02530005
                           OR  A.END_TMST      >= CURRENT_TIMESTAMP)    02540005
                      AND  A.LOC_NBR           =  0                     02550005
                      AND  (A.LOC_SKU_STAT_CDE =  '20'                  02560005
                       OR  (A.LOC_SKU_STAT_CDE =  '25'                  02570005
                         AND NOT EXISTS                                 02580005
                         (SELECT C.LOC_NBR                              02590005
                            FROM XST_SKU_LOC C                          02600005
                           WHERE C.INTR_UPC_ID      =  A.INTR_UPC_ID    02610005
                             AND C.LOC_NBR          =                   02620005
                                 :XST00008-LOC-NBR                      02630005
                             AND C.LOC_SKU_STAT_CDE =  '30')))          02640005
                   WITH UR                                              02650005
               END-EXEC                                                 02660005
                                                                        02670005
               EVALUATE TRUE                                            02680005
                   WHEN SQLCODE = -904                                  02690005
                   WHEN SQLCODE = -913                                  02700005
                       CONTINUE                                         02710005
                   WHEN SQLCODE = ZERO                                  02720005
                       SET PS-COUNT-FOUND         TO TRUE               02730005
                   WHEN OTHER                                           02740005
                       MOVE XST00010-SKU-NBR      TO PV-DISPLAY-SKU-NBR 02750005
                       MOVE XST00008-LOC-NBR      TO PV-DISPLAY-LOC-NBR 02760005
                       DISPLAY '** '                                    02770005
                               PC-CURRENT-PROGRAM-NAME                  02780005
                               ' - SKU NBR: '                           02790005
                               PV-DISPLAY-SKU-NBR                       02800005
                       DISPLAY '** '                                    02810005
                               PC-CURRENT-PROGRAM-NAME                  02820005
                               ' - LOC NBR: '                           02830005
                               PV-DISPLAY-LOC-NBR                       02840005
                       MOVE '5900-SELECT-XST-SKU-LOC'                   02850005
                                                  TO PV-PARAGRAPH-NAME  02860005
                       MOVE 'SELECT FROM SKU LOC' TO PV-DB2-OPERATION   02870005
                       PERFORM 9900-SQL-ABEND-ROUTINE                   02880005
               END-EVALUATE                                             02890005
           END-PERFORM.                                                 02900005
                                                                        02910005
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          02920005
               MOVE XST00010-SKU-NBR              TO PV-DISPLAY-SKU-NBR 02930005
               MOVE XST00008-LOC-NBR              TO PV-DISPLAY-LOC-NBR 02940005
               DISPLAY '** '                                            02950005
                       PC-CURRENT-PROGRAM-NAME                          02960005
                       ' - SKU NBR: '                                   02970005
                       PV-DISPLAY-SKU-NBR                               02980005
               DISPLAY '** '                                            02990005
                       PC-CURRENT-PROGRAM-NAME                          03000005
                       ' - LOC NBR: '                                   03010005
                       PV-DISPLAY-LOC-NBR                               03020005
               MOVE '5900-SELECT-XST-SKU-LOC'     TO PV-PARAGRAPH-NAME  03030005
               MOVE 'SELECT FROM SKU LOC'         TO PV-DB2-OPERATION   03040005
               PERFORM 9900-SQL-ABEND-ROUTINE                           03050005
           END-IF.                                                      03060005
                                                                        03070005
      *----------------------------------------------------------       03080005
      * 8300-WRITE-EXCLSN-REC.                                          03090005
      *     WRITE A CTA EXCLUSION RECORD IF AN ACTIVE STATUS            03100005
      *     ROW IS FOUND FOR THE SKU AND LOC.                           03110005
      *----------------------------------------------------------       03120005
       8300-WRITE-EXCLSN-REC.                                           03130005
                                                                        03140005
           IF PV-ACTIVE-ROW-COUNT > 0                                   03150005
                                                                        03160005
               MOVE XST00008-LOC-NBR TO PX053-STR-NBR                   03170005
               MOVE XST00010-SKU-NBR TO PX053-SKU-NBR                   03180005
               WRITE SKU-STR-EXCLSN-REC-OUT                             03190005
                   FROM PX053-SKU-STR-EXCLSN-REC                        03200005
               ADD 1 TO PC-CNT-WRITTEN-RECS                             03210005
                                                                        03220005
           END-IF.                                                      03230005
                                                                        03240005
      *----------------------------------------------------------       03250005
      * 9800-ABEND.                                                     03260005
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  03270005
      *----------------------------------------------------------       03280005
       9800-ABEND.                                                      03290005
                                                                        03300005
           DISPLAY '** ABEND           **'.                             03310005
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  03320005
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        03330005
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       03340005
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       03350005
           CALL 'ILBOABN0' USING ABEND-CODE.                            03360005
                                                                        03370005
      *----------------------------------------------------------       03380005
      * 9900-SQL-ABEND-ROUTINE.                                         03390005
      *     SQL ABEND ROUTINE.                                          03400005
      *----------------------------------------------------------       03410005
       9900-SQL-ABEND-ROUTINE.                                          03420005
                                                                        03430005
           SET ABEND-4013-DB2-ERROR TO TRUE.                            03440005
           DISPLAY '** ABEND     **'.                                   03450005
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        03460005
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              03470005
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               03480005
                                                                        03490005
           COPY DPPD004.                                                03500005
           CALL 'ILBOABN0' USING ABEND-CODE.                            03510005
                                                                        03520005
      *9900-EXIT.                                                       03530005
                                                                        03540005
      *----------------------------------------------------------       03550005
      * 9999-WRAPUP.                                                    03560005
      *     END OF PROGRAM ROUTINE.  CLOSES ALL FILES AND DISPLAYS      03570005
      *     ANY DATA FROM THE PROGRAM.                                  03580005
      *----------------------------------------------------------       03590005
       9999-WRAPUP.                                                     03600005
                                                                        03610005
           DISPLAY ' '.                                                 03620005
           DISPLAY '*********************************************'.     03630005
           DISPLAY 'PROGRAM NAME: ' PC-CURRENT-PROGRAM-NAME.            03640005
           DISPLAY '*********************************************'.     03650005
           DISPLAY ' '.                                                 03660005
           DISPLAY '              NUMBER WHTTKT EXTR RECS READ:    '    03670006
                    PC-CNT-READ-RECS.                                   03680005
           DISPLAY '              NUMBER WHTTKT EXTR RECS WRITTEN: '    03690006
                    PC-CNT-WRITTEN-RECS.                                03700005
           DISPLAY ' '.                                                 03710005
           DISPLAY '*********************************************'.     03720005
                                                                        03730005
