       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT164.                                       
       AUTHOR.                  JULIE SEIDLER.                                  
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            06/09/98.                                       
       DATE-COMPILED.                                                           
                                                                                
W34517******************************************************************        
W34517*     IMPORTANT                                                  *        
W34517*     ---------                                                  *        
W34517* CLONE FROM PROD HAVING ALL OCCURANCES OF TINSCLS BEING REPLACED*        
W34517* BY THE NEW TABLE, INT_SUB_CL_INV_LDGR                          *        
      ******************************************************************        
      *     SUB-CLASS LEVEL INVENTORY ADJUSTMENT EXTRACT               *        
      *                                                                *        
      * THIS PROGRAM REPORTS THE BOOK VALUE OF INVENTORY AT THE TIME   *        
      * THE SNAPSHOT WAS TAKEN, THE COMPONENTS OF THE ADJUSTMENTS THAT *        
      * HAVE BEEN MADE TO THE SNAPSHOT, AND THE ADJUSTED BOOK VALUE.   *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
      * WR20362  11/01/99    CHANGE PROGRAM TO NOT DRIVE OFF                    
      *                      OF MBS, DRIVE OFF OF THE DATA IN                   
      *                      TINSCLS ORDERED BY DEPARTMENT                      
      * WR21015  06/05/00    REMOVED ALL LOGIC THAT LOOKED AT                   
      *                      THE STORES INVENTORY DATES.                        
      * 21591    03-01-2002  MANIFEST PROJECT CHANGE FOR IN-TRANSIT             
      *                      REVERSAL FROM INVENTORY LEDGER                     
W26600* W26600  04-05-2004 ROD JANSSEN INV LEDGER STORE EXPANSION               
W27808* W27808  05-25-2005  S.JACKSON NEW MANIFESTING/STORE-EXPECTED CHG        
      * W28545  11-01-2005  V.L. YANG ESTIMATED DAILY INVENTORY LEDGER          
108746*         11-01-2005  INC000000108746 TO HELP THIS RUN FASTER             
W34517* W34517  10-14-2008  INFOSYS    INT_SUB_CL_INV_LDGR IS ACCESSED          
W34517*                                INSTEAD OF TINSCLS                       
W33304* W33304  09-17-2008  VIGNESH SUBRAMANIAN/INFOSYS                         
W33304*                     HOLDING INVENTORY LEDGER OPEN (HILO).               
F33304* F33304  09-15-2009  BRUCE GRAHAM/KFORCE                                 
F33304*                     RUN TIME ISSUE                                      
F33304*                     FIX DB2 DECLARE CURSOR HILO CHANGE                  
RONNA1* CHG0305858                                                              
RONNA1*         02-05-2024 RONNA MCDONALD                                       
RONNA1*                    REMOVE LOGIC FOR ESTIMATED INVENTORY.                
RONNA1*                    THIS IS OBSOLETE.                                    
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INVENTORY-EXTRACT-FILE                                        
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INVENTORY-EXTRACT-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
W33304 01  INVENTORY-ADJ-EXTR-RECORD         PIC X(144).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR INVENTORY ADJUSTMENT EXTRACT RECORD                   
      ****************************************************************          
W28545*01  IN013-INVENTORY-ADJ-EXTR-REC.                                        
W28545     COPY INRD013.                                                        
                                                                                
      ****************************************************************          
      *  ERROR MESSAGE AREA FOR DB2                                             
      ****************************************************************          
                                                                                
           COPY DPWS004.                                                        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
108746*    05  PC-OPERCO-NBR                PIC X(02)  VALUE '03'.              
           05  PC-MAX-RETRIES               PIC S9(04) COMP SYNC                
                                                       VALUE +3.                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-OPEN-FSCL-MN-SW    PIC X      VALUE 'N'.               
               88  PS-END-OF-OPEN-FSCL-MN              VALUE 'Y'.               
           05  PS-END-OF-TINSCLS-CSR-SW     PIC X      VALUE 'N'.               
               88  PS-END-OF-TINSCLS-CSR               VALUE 'Y'.               
               88  PS-NOT-END-OF-TINSCLS-CSR           VALUE 'N'.               
     *     05  PS-MBS-INFO-FOUND-SW         PIC X      VALUE 'N'.               
     *         88  PS-MBS-INFO-FOUND                   VALUE 'Y'.               
     *         88  PS-MBS-INFO-NOT-FOUND               VALUE 'N'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-FSCL-MN-NBR               PIC 9(02)  VALUE ZERO.              
                                                                                
           05  PV-DB2-OPER-ATTEMPTED        PIC X(30).                          
           05  PV-PARAGRAPH-NAME            PIC X(30).                          
           05  PV-RETRY-COUNT               PIC S9(04) COMP SYNC                
                                                       VALUE ZERO.              
           05  PV-DTATTR-KEY-DTE            PIC X(10)  VALUE SPACES.            
           05  PV-SUBCL-3.                                                      
               10 FILLER                    PIC X(01)  VALUE SPACES.            
               10 PV-SUBCL-2                PIC X(02)  VALUE SPACES.            
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP                     
                                                       VALUE ZERO.              
                                                                                
                                                                                
      ****************************************************************          
W34517* INVENTORY SUB-CLASS CUTOFF TABLE, INT_SUB_CL_INV_LDGR                   
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
21591 *        INCLUDE TINSCLS                                                  
21591          INCLUDE INT00007                                                 
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * DATE ATTRIBUTE TABLE                                                    
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * MBS MERCHANDISE AREA TABLE                                              
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * MBS APPLICATION AVAILABILITY TABLE                                      
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TAPLAVL                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *  COMMUNICATION AREAS FOR DB2                                            
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
W33304****************************************************************          
W33304* INVENTORY PARM TABLE                                                    
W33304****************************************************************          
W33304                                                                          
W33304     EXEC SQL                                                             
W33304         INCLUDE TINVPAR                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
W33304****************************************************************          
W33304* INVENTORY CONTROL TABLE                                                 
W33304****************************************************************          
W33304                                                                          
W33304     EXEC SQL                                                             
W33304         INCLUDE TINCNTL                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
                                                                                
      ****************************************************************          
      *  INVENTORY SUB-CLASS CUTOFF TABLE CURSOR                                
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
              DECLARE TINSCLS-CSR CURSOR FOR                                    
                  SELECT                                                        
W33304                A.FSCL_MN_NBR                                             
W33304               ,A.FSCL_MN_END_DTE                                         
W33304               ,A.DEPT_NBR                                                
W33304               ,A.SUB_CL_NBR                                              
W33304               ,A.STR_NBR                                                 
W33304               ,A.SLS_RTL_AMT                                             
W33304               ,A.MKDN_RTL_AMT                                            
W33304               ,A.MKUP_RTL_AMT                                            
W33304               ,A.XFER_IN_RTL_AMT                                         
W33304               ,A.XFER_OUT_RTL_AMT                                        
W33304               ,A.RCPT_RTL_AMT                                            
W33304               ,A.PADJ_RTL_AMT                                            
W33304               ,A.SHNK_RTL_AMT                                            
W33304               ,A.END_INV_RTL_AMT                                         
W33304               ,A.CTOFF_END_INV_AMT                                       
OCFSFS               ,A.STR_PARL_SHIP_RTL_AMT                                   
W33304               ,A.STR_EXPTED_RTL_AMT                                      
W33304               ,B.INV_ID                                                  
W33304               ,B.FINCL_INV_STAT_CDE                                      
W33304               ,C.OPN_FSCL_MN_DTE                                         
                  FROM                                                          
W34517*               TINSCLS                                                   
W33304                INT_SUB_CL_INV_LDGR A                                     
W33304               ,TINVPAR B                                                 
W33304               ,TINCNTL C                                                 
F33304            WHERE A.FSCL_MN_NBR                                           
F33304                              = C.OPN_FSCL_MN_NBR                         
F33304              AND A.FSCL_MN_END_DTE                                       
W33304                              = C.OPN_FSCL_MN_DTE                         
W33304              AND A.STR_NBR = B.LOC_NBR                                   
W33304              AND B.ACTL_FIN_BK_DTE  = '9999-09-09'                       
W33304              AND B.LOC_INV_STAT_CDE = 'IN'                               
RONNA1***           AND B.SCHD_PHY_CNT_CDE <> 'EI'                              
W33304              AND B.INV_ID           = C.INV_ID                           
W33304              AND C.ACTV_IND         = 'Y'                                
108746            WITH UR                                                       
           END-EXEC.                                                            
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
       1000-MAIN-MODULE.                                                        
                                                                                
           OPEN OUTPUT INVENTORY-EXTRACT-FILE.                                  
                                                                                
           PERFORM 2000-MBS-SUBCLASS-DATA.                                      
                                                                                
           CLOSE INVENTORY-EXTRACT-FILE.                                        
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       2000-MBS-SUBCLASS-DATA.                                                  
                                                                                
           SET PS-NOT-END-OF-TINSCLS-CSR TO TRUE.                               
                                                                                
           PERFORM 8500-OPEN-TINSCLS-CSR.                                       
           PERFORM 3000-EXTR-TINSCLS-FOR-SCL                                    
               UNTIL PS-END-OF-TINSCLS-CSR.                                     
           PERFORM 8700-CLOSE-TINSCLS-CSR.                                      
                                                                                
                                                                                
       3000-EXTR-TINSCLS-FOR-SCL.                                               
                                                                                
           PERFORM 8600-FETCH-TINSCLS-CSR.                                      
                                                                                
           IF PS-END-OF-TINSCLS-CSR                                             
               CONTINUE                                                         
           ELSE                                                                 
W34517         MOVE INT00007-DEPT-NBR     TO MDSEAR-DEPT-NBR                    
               INITIALIZE  DCLTMDSEAR                                           
               PERFORM 8400-GET-EFFECTIVE-MBS-INFO                              
                                                                                
               INITIALIZE IN013-INVENTORY-ADJ-EXTR-REC                          
                                                                                
W33304         MOVE INVPAR-INV-ID     TO IN013-INV-ID                           
W33304         MOVE INVPAR-FINCL-INV-STAT-CDE                                   
W33304                                TO IN013-FINCL-INV-STAT-CDE               
W33304         MOVE INCNTL-OPN-FSCL-MN-DTE                                      
W33304                                TO IN013-OPN-FSCL-MN-DTE                  
W34517         MOVE INT00007-DEPT-NBR TO IN013-DEPT-NBR                         
W34517         MOVE INT00007-SUB-CL-NBR TO IN013-SUB-CL-NBR                     
               MOVE MDSEAR-GMA-NBR    TO IN013-GMA-NBR                          
               MOVE MDSEAR-DMA-NBR    TO IN013-DMA-NBR                          
               MOVE MDSEAR-BYR-NBR    TO IN013-BYR-NBR                          
               PERFORM 3100-WRITE-EXTR-RECORD                                   
           END-IF.                                                              
                                                                                
       3100-WRITE-EXTR-RECORD.                                                  
W34517         MOVE INT00007-STR-NBR         TO IN013-STR-NBR.                  
W34517         MOVE INT00007-CTOFF-END-INV-AMT TO IN013-BEG-INV-RTL-AMT.        
W34517         MOVE INT00007-SLS-RTL-AMT     TO IN013-SLS-RTL-AMT.              
W34517         MOVE INT00007-MKDN-RTL-AMT    TO IN013-MKDN-RTL-AMT.             
W34517         MOVE INT00007-MKUP-RTL-AMT    TO IN013-MKUP-RTL-AMT.             
W34517         MOVE INT00007-XFER-IN-RTL-AMT TO IN013-XFER-IN-RTL-AMT.          
W34517         MOVE INT00007-XFER-OUT-RTL-AMT TO IN013-XFER-OUT-RTL-AMT.        
W34517         MOVE INT00007-RCPT-RTL-AMT    TO IN013-RCPT-RTL-AMT.             
W34517         MOVE INT00007-PADJ-RTL-AMT    TO IN013-PADJ-RTL-AMT.             
W34517         MOVE INT00007-SHNK-RTL-AMT    TO IN013-SHNK-RTL-AMT.             
W34517         MOVE INT00007-END-INV-RTL-AMT TO IN013-END-INV-RTL-AMT.          
OCFSFS         MOVE INT00007-STR-PARL-SHIP-RTL-AMT                              
OCFSFS                                   TO IN013-STR-PARL-SHIP-RTL-AMT.        
W27808         MOVE INT00007-STR-EXPTED-RTL-AMT                                 
W27808             TO IN013-STR-EXPTED-RTL-AMT.                                 
W28545         MOVE ZEROES                   TO IN013-PHY-INV-RTL-AMT.          
                                                                                
           PERFORM 7100-WRITE-INVENTORY-EXTRACT.                                
                                                                                
       7100-WRITE-INVENTORY-EXTRACT.                                            
                                                                                
           WRITE INVENTORY-ADJ-EXTR-RECORD                                      
               FROM IN013-INVENTORY-ADJ-EXTR-REC.                               
                                                                                
                                                                                
       8400-GET-EFFECTIVE-MBS-INFO.                                             
                                                                                
      ******************************************************************        
      * GET THE MBS INFO FROM THE EFFECTIVE ROW FOR THE DEPARTMENT.             
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    GMA_NBR                                                     
                   ,DMA_NBR                                                     
                   ,BYR_NBR                                                     
                INTO                                                            
                    :MDSEAR-GMA-NBR                                             
                   ,:MDSEAR-DMA-NBR                                             
                   ,:MDSEAR-BYR-NBR                                             
                FROM                                                            
                    TMDSEAR A                                                   
108746*         WHERE A.OPERCO_NBR  = :PC-OPERCO-NBR                            
108746          WHERE A.MDSELV_CDE  = 'DPT'                                     
W34517            AND A.DEPT_NBR    = :INT00007-DEPT-NBR                        
W33304            AND :INCNTL-OPN-FSCL-MN-DTE                                   
                       BETWEEN A.STRT_DTE                                       
                           AND A.END_DTE                                        
108746          WITH UR                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
      *            SET PS-MBS-INFO-FOUND TO TRUE                                
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
      *            SET PS-MBS-INFO-NOT-FOUND TO TRUE                            
               WHEN OTHER                                                       
                   MOVE '8400-GET-EFFECTIVE-MBS-INFO'                           
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT ROW FROM TMDSEAR'                               
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8500-OPEN-TINSCLS-CSR.                                                   
                                                                                
           EXEC SQL                                                             
                OPEN TINSCLS-CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8500-OPEN-TINSCLS-CSR'                                 
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'OPEN TINSCLS-CSR'                                      
                                            TO PV-DB2-OPER-ATTEMPTED            
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8600-FETCH-TINSCLS-CSR.                                                  
      *****************************************************************         
      * RETRY LOGIC IS NOT INCLUDED IN THIS PARAGRAPH BECAUSE A -911            
      * CAUSES THE CURSOR TO BE CLOSED.                                         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               FETCH                                                            
                   TINSCLS-CSR                                                  
               INTO                                                             
W34517            :INT00007-FSCL-MN-NBR                                         
W34517           ,:INT00007-FSCL-MN-END-DTE                                     
W34517           ,:INT00007-DEPT-NBR                                            
W34517           ,:INT00007-SUB-CL-NBR                                          
W34517           ,:INT00007-STR-NBR                                             
W34517           ,:INT00007-SLS-RTL-AMT                                         
W34517           ,:INT00007-MKDN-RTL-AMT                                        
W34517           ,:INT00007-MKUP-RTL-AMT                                        
W34517           ,:INT00007-XFER-IN-RTL-AMT                                     
W34517           ,:INT00007-XFER-OUT-RTL-AMT                                    
W34517           ,:INT00007-RCPT-RTL-AMT                                        
W34517           ,:INT00007-PADJ-RTL-AMT                                        
W34517           ,:INT00007-SHNK-RTL-AMT                                        
W34517           ,:INT00007-END-INV-RTL-AMT                                     
W34517           ,:INT00007-CTOFF-END-INV-AMT                                   
OCFSFS           ,:INT00007-STR-PARL-SHIP-RTL-AMT                               
W27808           ,:INT00007-STR-EXPTED-RTL-AMT                                  
W33304           ,:INVPAR-INV-ID                                                
W33304           ,:INVPAR-FINCL-INV-STAT-CDE                                    
W33304           ,:INCNTL-OPN-FSCL-MN-DTE                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-TINSCLS-CSR TO TRUE                            
               WHEN OTHER                                                       
                   MOVE '8600-FETCH-TINSCLS-CSR'                                
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'FETCH TINSCLS-CSR'     TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8700-CLOSE-TINSCLS-CSR.                                                  
                                                                                
           EXEC SQL                                                             
               CLOSE TINSCLS-CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8700-CLOSE-TINSCLS-CSR'                                
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'CLOSE TINSCLS-CSR'     TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = INKUT164'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
