       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    INKRP149.                                                 
       AUTHOR.        JOHN PRICE.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07/05/00.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *     INKRP149 - MISSING AREA NUMBER REPORT                               
      *                                                                         
      * THIS PROGRAM READS A SORTED EXTRACT FILE FROM STEP 260 OF INK300        
      * (SORTED BY STORE/AREA NUMBER)                                           
      *                                                                         
      * MISSING AREA NUMBERS ARE REPORTED FOR ALL STORES                        
      ******************************************************************        
W21732* 02/10/03 - SUKUMAR, IBM GLOBAL SERVICES    WR #21732          *         
W21732*    - NI-SKU PROJECT : COMPILED THE PROGRAM WITH CHANGED        *        
W21732*      COPYBOOK INRD135.                                         *        
W26600*                                                                         
W26600*  W26600  06-  -2004  PHYSICAL INVENTORY STORE EXPANSION                 
W26600*                      M JOHNSON                                          
W26600*                                                                         
W21732******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT STORE-EXTRACT-FILE                                            
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT MISSING-AREA-FILE                                             
               ASSIGN RPT01.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  STORE-EXTRACT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  STORE-EXTRACT-RECORD               PIC  X(30).                       
                                                                                
       FD  MISSING-AREA-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  MISSING-AREA-RECORD                PIC  X(132).                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-LINE-CNT                PIC 9(02)  VALUE 65.              
           05  PC-999999                      PIC 9(06)  VALUE 999999.          
           05  PC-THRU                        PIC X(04)  VALUE 'THRU'.          
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-LINE-CNT                    PIC 9(02)  VALUE 99.              
           05  PA-AREA-CNTR                   PIC 9(09)  VALUE ZERO.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
W26600     05  PV-HOLD-STORE                  PIC X(04)  VALUE SPACE.           
           05  PV-HOLD-AREA                   PIC 9(09)  VALUE ZERO.            
           05  PV-PAGE-CNT                    PIC S9(09) VALUE ZERO.            
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-STORE-EXTRACT-SW     PIC X(01)  VALUE 'N'.             
               88 PS-NOT-END-OF-STORE-EXTRACT            VALUE 'N'.             
               88 PS-END-OF-STORE-EXTRACT                VALUE 'Y'.             
                                                                                
       01  TD-TODAYS-DATE.                                                      
           05  TD-YEAR                        PIC  9(02) VALUE ZERO.            
           05  TD-MONTH                       PIC  9(02) VALUE ZERO.            
           05  TD-DAY                         PIC  9(02) VALUE ZERO.            
                                                                                
       01  TT-TODAYS-TIME.                                                      
           05  TT-HOUR                        PIC  9(02) VALUE ZERO.            
           05  TT-MINUTE                      PIC  9(02) VALUE ZERO.            
           05  FILLER                         PIC  9(04) VALUE ZERO.            
                                                                                
      ******************************************************************        
      *  KOHL'S STANDARD FIRST HEADING LINE 132 BYTE WIDE LAYOUT                
      ******************************************************************        
           COPY DPWS132O.                                                       
                                                                                
       01  HL2-HEADING-LINE-2.                                                  
           05  FILLER                         PIC X(56)  VALUE SPACE.           
           05  FILLER                         PIC X(19)  VALUE                  
               'MISSING AREA NUMBER'.                                           
           05  FILLER                         PIC X(58)  VALUE SPACE.           
                                                                                
       01  HL3-HEADING-LINE-3.                                                  
           05  FILLER                         PIC X(06) VALUE 'STORE:'.         
           05  FILLER                         PIC X(02)  VALUE SPACE.           
W26600     05  HL3-STORE-NBR                  PIC X(04)  VALUE SPACE.           
W26600     05  FILLER                         PIC X(120) VALUE SPACE.           
                                                                                
       01  HL4-HEADING-LINE-4.                                                  
           05  FILLER                         PIC X(03)  VALUE SPACE.           
           05  FILLER                         PIC X(16)  VALUE                  
               'SKIP AREA NUMBER'.                                              
           05  FILLER                         PIC X(05)  VALUE SPACE.           
           05  FILLER                         PIC X(15)  VALUE                  
               'END AREA NUMBER'.                                               
           05  FILLER                         PIC X(93)  VALUE SPACE.           
                                                                                
       01  DL-DETAIL-LINE.                                                      
           05  FILLER                         PIC X(03)  VALUE SPACE.           
           05  DL-AREA-1-NUMERIC              PIC 9(09).                        
           05  DL-AREA-1 REDEFINES DL-AREA-1-NUMERIC     PIC ZZZZZZZZ9.         
           05  FILLER                         PIC X(05)  VALUE SPACE.           
           05  DL-THRU                        PIC X(04)  VALUE SPACE.           
           05  FILLER                         PIC X(03)  VALUE SPACE.           
           05  DL-AREA-2-NUMERIC              PIC 9(09).                        
           05  DL-AREA-2 REDEFINES DL-AREA-2-NUMERIC     PIC ZZZZZZZZ9.         
           05  DL-AREA-2-ALPHA REDEFINES DL-AREA-2       PIC X(09).             
           05  FILLER                         PIC X(99)  VALUE SPACE.           
                                                                                
      *****************************************************************         
      * RECORD LAYOUT FOR STORE EXTRACT FILE                                    
      *****************************************************************         
                                                                                
           COPY INRD135.                                                        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-INKRP149.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-EXTRACT                                         
               UNTIL PS-END-OF-STORE-EXTRACT.                                   
                                                                                
           PERFORM 8000-EOJ-PROCESS.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
           PERFORM 1010-OPEN-PROGRAM-FILES.                                     
                                                                                
           PERFORM 4000-READ-STORE-EXTRACT-FILE.                                
                                                                                
           IF  PS-NOT-END-OF-STORE-EXTRACT                                      
               MOVE IN135-STORE               TO PV-HOLD-STORE                  
               PERFORM 1020-BUILD-REPORT-HEADINGS                               
           ELSE                                                                 
               DISPLAY ' '                                                      
               DISPLAY '@@@@@@@@@@                 @@@@@@@@@@'                  
               DISPLAY 'STORE EXTRACT (INPUT) RECORD IS EMPTY'                  
               DISPLAY '@@@@@@@@@@                 @@@@@@@@@@'                  
               DISPLAY ' '                                                      
           END-IF.                                                              
                                                                                
       1010-OPEN-PROGRAM-FILES.                                                 
                                                                                
           OPEN INPUT  STORE-EXTRACT-FILE                                       
                OUTPUT MISSING-AREA-FILE.                                       
                                                                                
       1020-BUILD-REPORT-HEADINGS.                                              
                                                                                
           ACCEPT TD-TODAYS-DATE            FROM DATE.                          
           MOVE TD-YEAR                       TO DP132O-RUN-YEAR.               
           MOVE TD-MONTH                      TO DP132O-RUN-MONTH.              
           MOVE TD-DAY                        TO DP132O-RUN-DAY.                
                                                                                
           ACCEPT TT-TODAYS-TIME            FROM TIME.                          
           MOVE TT-HOUR                       TO DP132O-RUN-HOUR.               
           MOVE TT-MINUTE                     TO DP132O-RUN-MINUTE.             
                                                                                
           MOVE 'INKRP149'                    TO DP132O-PROGRAM-NAME.           
           MOVE 1                             TO DP132O-REPORT-NUMBER.          
                                                                                
       2000-PROCESS-EXTRACT.                                                    
                                                                                
           IF  IN135-STORE = PV-HOLD-STORE                                      
               PERFORM 2010-PROCESS-AREA                                        
           ELSE                                                                 
               PERFORM 2012-PROCESS-STORE-BREAK                                 
               MOVE IN135-STORE               TO PV-HOLD-STORE                  
               MOVE ZEROS                     TO PA-AREA-CNTR                   
               PERFORM 6000-PRINT-PAGE-HEADINGS                                 
               PERFORM 2010-PROCESS-AREA                                        
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-STORE-EXTRACT-FILE.                                
                                                                                
       2010-PROCESS-AREA.                                                       
                                                                                
           ADD +1                             TO PA-AREA-CNTR.                  
           MOVE IN135-AREA                    TO PV-HOLD-AREA.                  
                                                                                
           IF  PV-HOLD-AREA = PA-AREA-CNTR                                      
               NEXT SENTENCE                                                    
           ELSE                                                                 
               MOVE ZEROS                     TO DL-AREA-1-NUMERIC              
                                                 DL-AREA-2-NUMERIC              
               PERFORM 2011-AREAS-NO-MATCH                                      
                   UNTIL PV-HOLD-AREA = PA-AREA-CNTR                            
               PERFORM 6500-PRINT-DETAIL-LINE                                   
           END-IF.                                                              
                                                                                
       2011-AREAS-NO-MATCH.                                                     
                                                                                
           IF  DL-AREA-1-NUMERIC = ZERO                                         
               MOVE SPACES                    TO DL-THRU                        
               MOVE PA-AREA-CNTR              TO DL-AREA-1                      
               MOVE SPACES                    TO DL-AREA-2-ALPHA                
           ELSE                                                                 
               MOVE PC-THRU                   TO DL-THRU                        
               MOVE PA-AREA-CNTR              TO DL-AREA-2                      
           END-IF.                                                              
                                                                                
           ADD +1                             TO PA-AREA-CNTR.                  
                                                                                
       2012-PROCESS-STORE-BREAK.                                                
                                                                                
           IF  PV-HOLD-AREA < PC-999999                                         
               IF  PV-HOLD-AREA = PA-AREA-CNTR                                  
                   ADD +1                     TO PA-AREA-CNTR                   
               END-IF                                                           
                                                                                
               MOVE PA-AREA-CNTR              TO DL-AREA-1                      
                                                                                
               IF  PA-AREA-CNTR < PC-999999                                     
                   MOVE PC-THRU               TO DL-THRU                        
                   MOVE PC-999999             TO DL-AREA-2                      
               ELSE                                                             
                   MOVE SPACES                TO DL-THRU                        
                                                 DL-AREA-2-ALPHA                
               END-IF                                                           
                                                                                
               PERFORM 6500-PRINT-DETAIL-LINE                                   
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * READ STORE FILE.                                                        
      *                                                                         
      ******************************************************************        
                                                                                
       4000-READ-STORE-EXTRACT-FILE.                                            
                                                                                
           READ STORE-EXTRACT-FILE INTO INRD135-PIECE-EXTR-REC                  
               AT END SET PS-END-OF-STORE-EXTRACT TO TRUE.                      
                                                                                
      ******************************************************************        
      * PRINT THE PAGE HEADINGS.                                                
      ******************************************************************        
       6000-PRINT-PAGE-HEADINGS.                                                
                                                                                
           ADD  1                             TO PV-PAGE-CNT.                   
           MOVE PV-PAGE-CNT                   TO DP132O-PAGE-NUMBER.            
                                                                                
           WRITE MISSING-AREA-RECORD                                            
               FROM DP132O-STANDRD-HEADER-132-COLS                              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE MISSING-AREA-RECORD                                            
               FROM HL2-HEADING-LINE-2                                          
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE PV-HOLD-STORE                 TO HL3-STORE-NBR.                 
           WRITE MISSING-AREA-RECORD                                            
               FROM HL3-HEADING-LINE-3                                          
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE MISSING-AREA-RECORD                                            
               FROM HL4-HEADING-LINE-4                                          
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE 4                             TO PA-LINE-CNT.                   
                                                                                
       6500-PRINT-DETAIL-LINE.                                                  
                                                                                
           IF  PA-LINE-CNT > PC-MAX-LINE-CNT                                    
               PERFORM 6000-PRINT-PAGE-HEADINGS                                 
           END-IF.                                                              
                                                                                
           WRITE MISSING-AREA-RECORD                                            
               FROM DL-DETAIL-LINE                                              
               AFTER ADVANCING 1 LINE.                                          
           ADD 1                              TO PA-LINE-CNT.                   
           MOVE ZEROS                         TO DL-AREA-1-NUMERIC              
                                                 DL-AREA-2-NUMERIC.             
           MOVE SPACES                        TO DL-DETAIL-LINE.                
                                                                                
       8000-EOJ-PROCESS.                                                        
                                                                                
           PERFORM 2012-PROCESS-STORE-BREAK.                                    
                                                                                
           CLOSE STORE-EXTRACT-FILE                                             
                 MISSING-AREA-FILE.                                             
                                                                                
