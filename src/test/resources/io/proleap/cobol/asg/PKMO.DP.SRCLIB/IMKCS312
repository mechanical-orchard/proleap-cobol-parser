      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    IMKCS312.                                                 
       AUTHOR.        JACK KRAMER.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  NOVEMBER 2005.                                            
       DATE-COMPILED.                                                           
                                                                                
      *---------------------------------------------------------------*         
      *    I312 - BRAND VENDOR - BRAND LOOKUP.                        *         
      *                                                               *         
      *    THIS PROGRAM GENERATES A LIST OF BRANDS THAT ARE SETUP FOR *         
      *    A SPECIFIED DEPARTMENT. A BRAND MAY BE SELECTED TO BE      *         
      *    PASSED BACK TO THE CALLING PROGRAM - EITHER FOR 'ADD' OR   *         
      *    'UPDATE' IN THE IMT_DEPT_BRND_VND TABLE.                   *         
      *                                                               *         
      *---------------------------------------------------------------*         
      *    CHANGE LOG:                                                 *        
      *    DATE  10/25/2021                                            *        
      *               - CHANGE CALL OF 'DPKUT100' TO                   *        
      *                 CALL DP010I-NUMERIC-EDIT-ROUTINE               *        
      *                 MAKING THE CALL DYNAMIC VS STATIC              *        
      *        MADE BY: GERMAN BANDA            WR/IR #: CHG0262978    *        
      *    DATE  01/16/2023                                            *        
      *               - CHANGE CALL OF 'DPKCS030' TO MAKE THE CALL     *        
      *                 DYNAMIC VS STATIC                              *        
      *        MADE BY: JEAN KURK               WR/IR #: CHG0276953    *        
      *----------------------------------------------------------------*        
       EJECT                                                                    
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DK-DB2-KEYS.                                                         
           15  DK-BRAND-TEXT                  PIC  X(20).                       
           15  DK-BRAND-BYTE                  REDEFINES DK-BRAND-TEXT           
                                              OCCURS 20 TIMES                   
                                              PIC  X(01).                       
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
                                                          'IM312A  '.           
           05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
                                                          'IMKM312 '.           
           05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
                                                          'IMKCS312'.           
           05  PC-OPERCO-NBR                  PIC  X(02)  VALUE '03'.           
           05  PC-DPT-LITERAL                 PIC  X(03)  VALUE 'DPT'.          
           05  PC-CHARTC-LITERAL              PIC S9(04)  VALUE +03             
                                                          COMP SYNC.            
           05  PC-UNKNOWN-BRAND-ID            PIC S9(9)   VALUE +2230           
                                                     COMP SYNC.                 
           05  PC-PERCENT                     PIC  X(01)  VALUE '%'.            
           05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
           05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +15             
                                                          COMP SYNC.            
           05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
                                                          COMP SYNC.            
           05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
                                                          +1000000              
                                                          COMP-3.               
           05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
                                                          COMP SYNC.            
           05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
                                                          COMP-3.               
      *                                                                         
           05  PC-TSYMSG-NUMBERS.                                               
               10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
               10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
               10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
               10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
               10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
               10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
               10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
               10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
               10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
               10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.          
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
               88  PS-NO-ERROR                            VALUE 'Y'.            
               88  PS-ERROR                               VALUE 'N'.            
           05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
               88  PS-COMMAREA-VALID                      VALUE 'Y'.            
               88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
           05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.            
               88  PS-DONE                                VALUE 'Y'.            
               88  PS-NOT-DONE                            VALUE 'N'.            
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SUB                         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-ITEM                        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
      ******************************************************************        
      **  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
      **  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
      **  APPLICATION-SPECIFIC COMMAREA.                              **        
      ******************************************************************        
       01  LQW-LIST-QUEUE-WORK-AREA.                                            
           05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
           05  LQW-ITEM-ENTRIES               OCCURS 15 TIMES                   
                                              INDEXED BY LQW-IDX.               
               10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
               10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
               10  LQW-SELECTED-SW            PIC  X(01).                       
                   88  LQW-SELECT                         VALUE 'S'.            
                   88  LQW-DESELECT                       VALUE ' '.            
               10  LQW-BRAND                  PIC  X(20).                       
               10  LQW-STAT                   PIC  X(01).                       
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *  RECORD LAYOUT FOR THE B/V SELECTION TEMP STORAGE QUEUE.      *         
      *---------------------------------------------------------------*         
                                                                                
           COPY IMWS009.                                                        
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    MAP LAYOUT                                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY IMKM312.                                                        
      *                                                                         
       01  MR-OCCURS-LAYOUT                   REDEFINES  IM312AO.               
           05  FILLER                         PIC X(80).                        
           05  MR-SCROLL-AREA                 OCCURS 15 TIMES.                  
               10  MR-SELECTL                 PIC S9(04) COMP.                  
               10  MR-SELECTA                 PIC  X(01).                       
               10  MR-SELECTC                 PIC  X(01).                       
               10  MR-SELECTH                 PIC  X(01).                       
               10  MR-SELECTI                 PIC  X(01).                       
                                                                                
               10  MR-BRANDL                  PIC S9(04) COMP.                  
               10  MR-BRANDA                  PIC  X(01).                       
               10  MR-BRANDC                  PIC  X(01).                       
               10  MR-BRANDH                  PIC  X(01).                       
               10  MR-BRAND                   PIC  X(20).                       
                                                                                
               10  MR-STATL                   PIC S9(04) COMP.                  
               10  MR-STATA                   PIC  X(01).                       
               10  MR-STATC                   PIC  X(01).                       
               10  MR-STATH                   PIC  X(01).                       
               10  MR-STAT                    PIC  X(01).                       
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ATTRIBUTE SETTINGS COPYBOOK.                               *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    FUNCTION KEYS COPYBOOK.                                    *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    NUMERIC EDIT LAYOUT.                                       *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS010I.                                                       
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    PARAMETERS FOR CALLING CICS ARCHITECTURE API               *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    STANDARD COMMAREA.                                         *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *---------------------------------------------------------------*         
      * INTER-APPLICATION COMMAREA.                                   *         
      *   IMWS008 - IS THE INPUT LAYOUT WHEN COMING FROM THE CALLING  *         
      *             SCREEN - ALSO RETURNS THE SELECTED BRAND.         *         
      *---------------------------------------------------------------*         
                                                                                
               10  INTER-APPL-COMM-AREA       PIC X(200).                       
           COPY IMWS008.                                                        
      *---------------------------------------------------------------*         
      *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
      *---------------------------------------------------------------*         
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-LIST-KEYS.                                           
                       20  ASC-KEY-DEPT                    PIC  X(03).          
                       20  ASC-KEY-BRAND                   PIC  X(20).          
                   15  ASC-DEPT-DESC                       PIC  X(25).          
                   15  ASC-CURRENT-DATE                    PIC  X(10).          
                   15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
                       20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
                           88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
                           88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
                                                                                
                       20  ASC-ITEM-ARRAY.                                      
                           25  ASC-ITEM-ENTRIES OCCURS 15 TIMES.                
                               30  ASC-LIST-ITEM-NUMBER                         
                                                           PIC S9(09)           
                                                           COMP-3.              
                               30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
                                   88  ASC-VALID-SELECT    VALUE ' '            
                                                                 'S'.           
                                   88  ASC-TEMP-NO-SELECT  VALUE ' '.           
                                   88  ASC-TEMP-SELECT     VALUE 'S'.           
                               30  ASC-SELECTED-SW         PIC  X(01).          
                                   88  ASC-NO-SELECT       VALUE ' '.           
                                   88  ASC-SELECT          VALUE 'S'.           
                               30  ASC-BRAND               PIC  X(20).          
                               30  ASC-STAT                PIC  X(01).          
                                                                                
                   15  ASC-BLK-SUB                         PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-ITEM-SUB                        PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
                                                                COMP-3.         
                   15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
                       88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
                       88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
                   15  ASC-LIST-QUEUE-NAME.                                     
                       20  ASC-LIST-TERM-ID                PIC  X(04).          
                       20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-LIST-SEQ                    PIC  9(01).          
                   15  ASC-START-ITEM-REQUEST.                                  
                       20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ABEND PROCESSING COPYBOOK.                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS013.                                                        
                                                                                
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR COMMUNICATIONS                                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MERCHANDISE AREA TABLE - TMDSEAR                                     
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR - TVSCHVL                                               
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE TVSCHVL                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR - TDCVSVL                                               
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE TDCVSVL                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *   BRAND CURSOR                                                *         
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
                DECLARE BRAND-CSR CURSOR                                        
                FOR SELECT C.CHARTC_VAL_DESC                                    
                          ,C.STAT_CDE                                           
                      FROM TMDSEAR A                                            
                          ,TDCVSVL B                                            
                          ,TVSCHVL C                                            
                     WHERE A.OPERCO_NBR      = :PC-OPERCO-NBR                   
                       AND A.MDSELV_CDE      = :PC-DPT-LITERAL                  
                       AND A.END_DTE         > :ASC-CURRENT-DATE                
                       AND A.MDSEAR_DKEY     = B.MDSEAR_ID                      
                       AND C.CHARTC_CDE      = :PC-CHARTC-LITERAL               
                       AND B.CHARTC_CDE      = C.CHARTC_CDE                     
                       AND A.DEPT_NBR        = :ASC-KEY-DEPT                    
                       AND B.CHARTC_SEQ_NBR  = C.CHARTC_SEQ_NBR                 
                       AND C.CHARTC_VAL_DESC LIKE :DK-BRAND-TEXT                
                       AND C.CHARTC_SEQ_NBR <> :PC-UNKNOWN-BRAND-ID             
                     ORDER BY                                                   
                           C.CHARTC_VAL_DESC                                    
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
      **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
      **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
           MOVE LENGTH OF IM312AI        TO DP030-MAP-LENGTH (1).               
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
      *                                                                         
           PERFORM 1000-CONTROL-PROCESSING                                      
      *                                                                         
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
      ******************************************************************        
      **  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
      **  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
      ******************************************************************        
       0001-CALL-CICS-ARCH-API.                                                 
      *    CALL 'DPKCS030' USING DFHEIBLK                                       
           CALL DP930-CICS-ARCH-API                                             
                           USING DFHEIBLK                                       
                                 DFHCOMMAREA                                    
                                 DP030-CICS-API-FIELDS                          
                                 DP020-STANDARD-COMMAREA                        
                                 IM312AI.                                       
      *                                                                         
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK                                            
                   DP013-XCTL-DISPLAY-RESTART                                   
                   DP013-CICS-ABEND      TO TRUE                                
               MOVE '0001-CALL-CICS-ARCH-API'                                   
                                         TO DP013-PARAGRAPH                     
      *        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEX        
      *             'T LINE'             TO DP013-MESSAGE-TEXT (2)              
               MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
      -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
      * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
      *                                                                *        
      * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
      *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
      *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
      *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
                   MOVE DP020-SRC-TASK-NUMBER                                   
                                         TO ASC-LIST-TASK-NUMBER                
                   MOVE PC-LIST-QUEUE-SEQ                                       
                                         TO ASC-LIST-SEQ                        
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                         
      ***-----$TEMPLATE NOTE$-------------------------------------------        
      *** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
      *** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
      *** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
      ***---------------------------------------------------------------        
                   IF  PS-COMMAREA-NOT-VALID                                    
      ***              MOVE PC-TSYMSG-99901                                     
      ***                                TO DP020-MSG-NUMBER                    
      ***              SET DP020-MSG-FATAL                                      
      ***                                TO TRUE                                
                       SET DP020-NEXT-ACT-APPL-ERROR                            
                                         TO TRUE                                
                   ELSE                                                         
                       PERFORM 4000-BUILD-INITIAL-PANEL                         
                   END-IF                                                       
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
                                         TO PV-TOP-ITEM                         
                   PERFORM 2000-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
                                         TO PV-TOP-ITEM                         
                   MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   SET DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
                                         TO DP013-MESSAGE-TEXT(1)               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
      * IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
      *----------------------------------------------------------------*        
                                                                                
       1100-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           SET PS-COMMAREA-VALID         TO TRUE.                               
           MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
           MOVE IM008-DEPT               TO ASC-KEY-DEPT.                       
           MOVE IM008-DEPT-DESC          TO ASC-DEPT-DESC.                      
           MOVE IM008-BRAND              TO ASC-KEY-BRAND.                      
           EXEC SQL                                                             
               SET  :ASC-CURRENT-DATE = CURRENT DATE                            
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
      * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
      * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
      * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
      * FUNCTION KEYS CAN GET THIS FAR.                                *        
      *----------------------------------------------------------------*        
                                                                                
       2000-PROCESS-PANEL.                                                      
           EVALUATE TRUE                                                        
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
                   PERFORM 2050-DELETE-LIST-QUEUE                               
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                                                                                
               WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
                   PERFORM 2140-DESELECT-ALL-ITEMS                              
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   IF  PS-NO-ERROR                                              
                       SET DP030-CONTINUE-GO                                    
                                       TO TRUE                                  
                       PERFORM 2100-CHECK-FUNCTION-KEY                          
                   ELSE                                                         
                       SET DP030-OVERRIDE-GO                                    
                                       TO TRUE                                  
                   END-IF                                                       
           END-EVALUATE.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
      *----------------------------------------------------------------*        
       2050-DELETE-LIST-QUEUE.                                                  
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
              (PV-CICS-RESP = DFHRESP(QIDERR))                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
                                                                                
               MOVE '2050-DELETE-LIST-QUEUE'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
      * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
      * CICS ARCHITECTURE API.                                         *        
      *----------------------------------------------------------------*        
       2100-CHECK-FUNCTION-KEY.                                                 
           EVALUATE TRUE                                                        
               WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
                   IF  ASC-NUMBER-OF-SELECTED-ITEMS = 1                         
                       INITIALIZE IM008-INTER-APPL-COMMAREA                     
                       PERFORM 5000-PREPARE-SEL-QUEUE                           
                   ELSE                                                         
                       IF  ASC-NUMBER-OF-SELECTED-ITEMS > 1                     
                           MOVE PC-TSYMSG-00047   TO DP020-MSG-NUMBER           
                       ELSE                                                     
                           MOVE PC-TSYMSG-00057   TO DP020-MSG-NUMBER           
                       END-IF                                                   
                       SET DP030-OVERRIDE-GO  TO TRUE                           
                       SET DP020-MSG-FATAL    TO TRUE                           
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
                   MOVE +1               TO PV-TOP-ITEM                         
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
                   PERFORM 2110-BROWSE-BACKWARD                                 
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
                   PERFORM 2120-BROWSE-FORWARD                                  
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
                   IF  PS-ERROR                                                 
                       CONTINUE                                                 
                   ELSE                                                         
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                   END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                       DP013-CICS-ABEND  TO TRUE                                
                   MOVE '2100-CHECK-FUNCTION-KEY'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
      *                                                                         
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
      *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
      *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
      *    MESSAGE.                                                    *        
      *----------------------------------------------------------------*        
                                                                                
       2110-BROWSE-BACKWARD.                                                    
           SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
               GIVING PV-TOP-ITEM.                                              
      *                                                                         
           IF  ((PV-TOP-ITEM < +1)                                              
             OR (PV-TOP-ITEM = +1))                                             
               MOVE +1                   TO PV-TOP-ITEM                         
      *        -- TOP OF LIST --                                                
               MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
      *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
      *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
      *----------------------------------------------------------------*        
                                                                                
       2120-BROWSE-FORWARD.                                                     
                                                                                
           ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
               GIVING PV-TOP-ITEM.                                              
           IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
                                         TO PV-TOP-ITEM                         
      *        -- BOTTOM OF LIST --                                             
               MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-INFORMATIONAL                                      
                                         TO TRUE                                
           ELSE                                                                 
               COMPUTE PV-BOTTOM-ITEM =                                         
                   PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
               IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
      *            -- BOTTOM OF LIST --                                         
                   MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
      *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
      *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
      *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
      *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
      *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
      *----------------------------------------------------------------*        
                                                                                
       2130-CHECK-FOR-JUMP-BROWSE.                                              
           SET PS-NO-ERROR               TO TRUE.                               
           IF (ASC-START-ITEM-REQUEST = SPACE)                                  
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
                                         TO PV-TOP-ITEM                         
           ELSE                                                                 
               MOVE ASC-START-ITEM-REQUEST                                      
                                         TO DP010I-UNEDITED-FIELD               
               MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
               MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
               SET DP010I-NEGATIVE-NOT-ALLOWED                                  
                                         TO TRUE                                
               CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
                         USING DP010I-NUMERIC-EDIT-AREA                         
                                                                                
               IF  NOT DP010I-ERROR-DETECTED                                    
                   MOVE DP010I-NUMERIC-FIELD                                    
                                         TO  ASC-START-ITEM-REQUEST-N           
                   IF  ASC-START-ITEM-REQUEST-N > ZERO                          
                       IF  ASC-START-ITEM-REQUEST-N >                           
                                                ASC-NUMBER-OF-ITEMS-READ        
                           MOVE SPACE TO ASC-START-ITEM-REQUEST                 
                           COMPUTE PV-TOP-ITEM =                                
                                   ASC-NUMBER-OF-ITEMS-READ                     
                                   - PC-ITEMS-PER-PANEL + 1                     
                           IF  PV-TOP-ITEM < +1                                 
                               MOVE +1   TO PV-TOP-ITEM                         
                               MOVE PC-TSYMSG-00070                             
                                         TO DP020-MSG-NUMBER                    
                               SET DP020-MSG-INFORMATIONAL                      
                                         TO TRUE                                
                           END-IF                                               
                       ELSE                                                     
                           MOVE ASC-START-ITEM-REQUEST-N                        
                                         TO PV-TOP-ITEM                         
                           MOVE SPACE    TO ASC-START-ITEM-REQUEST              
                           COMPUTE PV-BOTTOM-ITEM =                             
                               PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
                           IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
      *                        -- BOTTOM OF LIST --                             
                               MOVE PC-TSYMSG-00070                             
                                         TO DP020-MSG-NUMBER                    
                               SET DP020-MSG-INFORMATIONAL                      
                                         TO TRUE                                
                           END-IF                                               
                       END-IF                                                   
                   ELSE                                                         
      *                --- VALUE OUT OF RANGE ----                              
                       MOVE PC-TSYMSG-00101                                     
                                         TO DP020-MSG-NUMBER                    
                       MOVE -1           TO ASTRTITL                            
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       MOVE DP015-REVERSE                                       
                                         TO ASTRTITH                            
                       MOVE DP015-RED    TO ASTRTITC                            
                       MOVE DP015-UNP-NUM-BRT-MDT                               
                                         TO ASTRTITA                            
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
                                                                                
               ELSE                                                             
                   MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO ASTRTITL                            
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE DP015-REVERSE    TO ASTRTITH                            
                   MOVE DP015-RED        TO ASTRTITC                            
                   MOVE DP015-UNP-NUM-BRT-MDT                                   
                                         TO ASTRTITA                            
                   SET DP020-MSG-FATAL   TO TRUE                                
                   SET PS-ERROR          TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
      *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
      *----------------------------------------------------------------*        
       2140-DESELECT-ALL-ITEMS.                                                 
           MOVE +1                       TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2141-CLEAR-SELECTED-INDS                                     
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           ADD +1                        TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2141-CLEAR-SELECTED-INDS                                     
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
                                         TO PV-LIST-ITEM-NUMBER.                
                                                                                
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
              (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
               MOVE +1                   TO ASC-BLK-SUB                         
                                                                                
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE +1                   TO PV-BLOCK-NUMBER                     
                                                                                
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2141-CLEAR-SELECTED-INDS                             
                       VARYING ASC-ITEM-SUB FROM 1 BY 1                         
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-BLOCK-NUMBER                     
                                                                                
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
                                                                                
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         ASC-HIGHEST-BLOCK-WRITTEN                              
                                                                                
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER                     
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2141-CLEAR-SELECTED-INDS                             
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
                                                                                
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER                     
                                                                                
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
                                                                                
           MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
                                                                                
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
      *----------------------------------------------------------------*        
                                                                                
       2141-CLEAR-SELECTED-INDS.                                                
                                                                                
           IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
                   NOT EQUAL ZERO                                               
               IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
                   SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
                       ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
                       ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
                                         TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
      *  FOR ALL ITEMS.                                                *        
      *----------------------------------------------------------------*        
                                                                                
       2150-SELECT-ALL-ITEMS.                                                   
                                                                                
           MOVE +1                       TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2151-SET-SELECTED-INDS                                       
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           ADD +1                        TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2151-SET-SELECTED-INDS                                       
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
                                         TO PV-LIST-ITEM-NUMBER.                
                                                                                
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
              (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
               MOVE +1                   TO ASC-BLK-SUB                         
                                                                                
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE +1                   TO PV-BLOCK-NUMBER                     
                                                                                
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2151-SET-SELECTED-INDS                               
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-BLOCK-NUMBER                     
                                                                                
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
                                                                                
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         ASC-HIGHEST-BLOCK-WRITTEN                              
                                                                                
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER                     
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2151-SET-SELECTED-INDS                               
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
                                                                                
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER                     
                                                                                
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
                                                                                
           MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
                             ASC-NUMBER-OF-SELECTED-ITEMS.                      
                                                                                
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MARK EACH ITEM AS BEING SELECTED.                           *        
      *----------------------------------------------------------------*        
                                                                                
       2151-SET-SELECTED-INDS.                                                  
                                                                                
           IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
                   NOT EQUAL ZERO                                               
               IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
                   SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
                       ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
                       ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
                                     TO  TRUE                                   
               END-IF                                                           
           END-IF.                                                              
      ******************************************************************        
      ** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
      ** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
      ******************************************************************        
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
                                                                                
           MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
           MOVE +1                       TO PV-SUB.                             
      *                                                                         
           PERFORM 3100-SET-SUBSCRIPTS.                                         
           IF  ASTRTITL > ZERO                                                  
               MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
           ELSE                                                                 
               IF  ASTRTITF = DP015-ERASE-EOF                                   
                   INITIALIZE ASC-START-ITEM-REQUEST                            
               END-IF                                                           
           END-IF.                                                              
           PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
      ******************************************************************        
      ** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
      ******************************************************************        
       2210-MOVE-LINES-TO-COMMAREA.                                             
           IF  MR-SELECTL (PV-SUB) > ZERO                                       
               SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
               MOVE MR-SELECTI (PV-SUB)                                         
                                         TO ASC-TEMP-SELECTED-SW                
                                       (ASC-BLK-SUB, ASC-ITEM-SUB)              
           ELSE                                                                 
               IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
                       SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
                       MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
                                       (ASC-BLK-SUB, ASC-ITEM-SUB)              
               END-IF                                                           
           END-IF.                                                              
           ADD +1                        TO ASC-ITEM-SUB.                       
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
               ADD +1                    TO ASC-BLK-SUB                         
               MOVE +1                   TO ASC-ITEM-SUB                        
           END-IF.                                                              
      ******************************************************************        
      ** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
      ** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
      ******************************************************************        
       3000-EDIT-DATA-IN-COMMAREA.                                              
           SET PS-NO-ERROR               TO TRUE.                               
           PERFORM 3200-RESET-ATTRIBUTES.                                       
           MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
      **                                                                        
      * CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
      * EDITTING IS DONE FROM THE BOTTOM UP.                                    
      **                                                                        
           COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
               ASC-ITEMS-DISPLAYED - 1.                                         
           PERFORM 3100-SET-SUBSCRIPTS.                                         
           PERFORM 3300-EDIT-SELECTIONS                                         
               VARYING PV-SUB                                                   
               FROM PV-SUB BY -1                                                
               UNTIL PV-SUB < +1.                                               
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
      *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
      *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
      *                                                                *        
      *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
      *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
      *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
      *    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
      *----------------------------------------------------------------*        
                                                                                
       3100-SET-SUBSCRIPTS.                                                     
                                                                                
           COMPUTE ASC-ITEM-SUB =                                               
               (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
                                                                                
           IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
                                                                                
               MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
           ELSE                                                                 
               MOVE +1                     TO ASC-BLK-SUB                       
           END-IF.                                                              
      ******************************************************************        
      ** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
      ******************************************************************        
                                                                                
       3200-RESET-ATTRIBUTES.                                                   
           MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
           MOVE DP015-GREEN              TO ASTRTITC.                           
           MOVE DP015-UNDERLINE          TO ASTRTITH.                           
      *                                                                         
           PERFORM                                                              
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                         TO MR-SELECTA (PV-SUB)                 
               MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
               MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
           END-PERFORM.                                                         
      ******************************************************************        
      ** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
      ** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
      ** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
      ** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
      ******************************************************************        
       3300-EDIT-SELECTIONS.                                                    
           EVALUATE TRUE                                                        
               WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
                   = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
                       CONTINUE                                                 
               WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
                   IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
                       ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
                       MOVE ASC-TEMP-SELECTED-SW                                
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                                      TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   END-IF                                                       
               WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
                       = SPACE                                                  
                   IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                           SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
                           MOVE SPACE TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
                   SET DP020-MSG-FATAL    TO TRUE                               
                   MOVE -1                TO MR-SELECTL (PV-SUB)                
                   SET DP030-SET-CURSOR-APPL-1                                  
                                          TO TRUE                               
                   MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
                   MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
                   SET PS-ERROR TO TRUE                                         
                   IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
                       MOVE SPACE         TO ASC-SELECTED-SW                    
                                              (ASC-BLK-SUB ASC-ITEM-SUB)        
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
           SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
           SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
           IF  ASC-ITEM-SUB < 1                                                 
               SUBTRACT +1 FROM ASC-BLK-SUB                                     
               MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
      *    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
      *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
      *    DISPLAY THE PANEL TO USER.                                  *        
      *----------------------------------------------------------------*        
                                                                                
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
                                       ASC-BLOCK-ENTRIES (2).                   
           MOVE +1                 TO ASC-BLK-SUB.                              
           SET ASC-ITEMS-LEFT-ON-DB                                             
                                   TO TRUE.                                     
                                                                                
           MOVE +0                 TO ASC-ITEM-SUB                              
                                      ASC-NUMBER-OF-ITEMS-READ                  
                                      ASC-NUMBER-OF-SELECTED-ITEMS              
                                      ASC-HIGHEST-BLOCK-WRITTEN                 
                                      ASC-ITEM-AT-TOP-OF-PANEL.                 
           PERFORM 4100-PREPARE-CURSOR                                          
                                                                                
           PERFORM 4010-OPEN-CURSOR.                                            
                                                                                
           PERFORM 4300-READ-ITEMS-FROM-DB.                                     
                                                                                
           PERFORM 4020-CLOSE-CURSOR.                                           
                                                                                
           MOVE +1                   TO  PV-TOP-ITEM.                           
           IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
               SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
               SET DP020-MSG-FATAL   TO  TRUE                                   
               MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
           ELSE                                                                 
               MOVE -1               TO MR-SELECTL (1)                          
               SET DP030-SET-CURSOR-APPL-1                                      
                                     TO TRUE                                    
               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * OPEN THE TMESSAGE CURSOR.                                      *        
      *   IN THIS PARTICULAR APPLICATION ONE OF 3 DIFFERENT CURSORS    *        
      *   WILL BE OPENED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *        
      *   USER.                                                        *        
      *----------------------------------------------------------------*        
                                                                                
       4010-OPEN-CURSOR.                                                        
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
               EXEC SQL                                                         
                    OPEN BRAND-CSR                                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '4010-OPEN-CURSOR'                                  
                                         TO DP013-PARAGRAPH                     
                       MOVE 'OPEN BRAND CURSOR - '                              
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TMDSEAR '   TO DP013-DB2-TABLE-NAME (1)            
                       MOVE 'TDCVSVL '   TO DP013-DB2-TABLE-NAME (2)            
                       MOVE 'TVSCHVL '   TO DP013-DB2-TABLE-NAME (3)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '4010-OPEN-CURSOR'   TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'           
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TMDSEAR '           TO DP013-DB2-TABLE-NAME (1)            
               MOVE 'TDCVSVL '           TO DP013-DB2-TABLE-NAME (2)            
               MOVE 'TVSCHVL '           TO DP013-DB2-TABLE-NAME (3)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * CLOSE THE TMESSAGE CURSOR.                                     *        
      *   IN THIS PARTICULAR APPLICATION ONE OF 3 DIFFERENT CURSORS    *        
      *   WILL BE CLOSED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *        
      *   USER.                                                        *        
      *----------------------------------------------------------------*        
                                                                                
       4020-CLOSE-CURSOR.                                                       
                                                                                
           EXEC SQL                                                             
               CLOSE BRAND-CSR                                                  
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '4020-CLOSE-CURSOR'                                     
                                     TO DP013-PARAGRAPH                         
                   MOVE 'CLOSE BRAND CURSOR '                                   
                                     TO DP013-MESSAGE-TEXT (1)                  
                   MOVE SQLCA        TO DP013-SQLCA                             
                   MOVE 'TMDSEAR '   TO DP013-DB2-TABLE-NAME (1)                
                   MOVE 'TDCVSVL '   TO DP013-DB2-TABLE-NAME (2)                
                   MOVE 'TVSCHVL '   TO DP013-DB2-TABLE-NAME (3)                
                   SET DP013-DB2-ABEND                                          
                       DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    SET UP THE HOST VARIABLES.                                  *        
      *----------------------------------------------------------------*        
                                                                                
       4100-PREPARE-CURSOR.                                                     
                                                                                
           MOVE ASC-KEY-BRAND        TO DK-BRAND-TEXT.                          
           IF  DK-BRAND-TEXT GREATER THAN SPACES                                
               IF  DK-BRAND-BYTE (1) EQUAL '*'                                  
                   MOVE PC-PERCENT                                              
                                 TO DK-BRAND-BYTE (1)                           
                   INSPECT DK-BRAND-TEXT                                        
                       REPLACING FIRST '*' BY PC-PERCENT                        
               END-IF                                                           
                                                                                
               SET PS-NOT-DONE   TO TRUE                                        
                                                                                
               PERFORM VARYING PV-SUB                                           
                   FROM LENGTH OF DK-BRAND-TEXT BY -1                           
                   UNTIL PV-SUB EQUAL 1                                         
                     OR  PS-DONE                                                
                                                                                
                       IF  DK-BRAND-BYTE (PV-SUB) = '*'                         
                           MOVE PC-PERCENT                                      
                                 TO DK-BRAND-BYTE (PV-SUB)                      
                           SET PS-DONE                                          
                                 TO TRUE                                        
                       END-IF                                                   
                                                                                
                       IF  DK-BRAND-BYTE (PV-SUB) > SPACES                      
                           SET PS-DONE                                          
                                 TO TRUE                                        
                       ELSE                                                     
                           MOVE PC-PERCENT                                      
                                 TO DK-BRAND-BYTE (PV-SUB)                      
                       END-IF                                                   
                                                                                
               END-PERFORM                                                      
                                                                                
           ELSE                                                                 
               MOVE ALL '%'      TO DK-BRAND-TEXT                               
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
      *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
      *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
      *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
      *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
      *                                                                *        
      *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
      *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
      *    UPON INITIALIZATION.                                        *        
      *----------------------------------------------------------------*        
                                                                                
       4300-READ-ITEMS-FROM-DB.                                                 
                                                                                
           PERFORM                                                              
               VARYING PV-READ-COUNT                                            
               FROM 1 BY 1                                                      
               UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
                  OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
                                                                                
               PERFORM 4310-FETCH                                               
                                                                                
               IF  ASC-ITEMS-LEFT-ON-DB                                         
                   IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
                       ADD +1            TO ASC-ITEM-SUB                        
                   ELSE                                                         
                       ADD +1            TO ASC-BLK-SUB                         
                       MOVE +1           TO ASC-ITEM-SUB                        
                                                                                
                       IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
                           MOVE +1       TO ASC-BLK-SUB                         
                           SET ASC-BLOCK-MODIFIED (1)                           
                                         TO TRUE                                
                           PERFORM 5100-WRITE-TEMP-STORAGE                      
                           MOVE ASC-BLOCK-ENTRIES                               
                                     (PC-MAX-BLOCKS-IN-ARRAY)                   
                                      TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
                           ADD +1        TO ASC-BLK-SUB                         
                           INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
                           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
                                         TO TRUE                                
                       END-IF                                                   
                   END-IF                                                       
                   ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
                   MOVE ASC-NUMBER-OF-ITEMS-READ                                
                                         TO ASC-LIST-ITEM-NUMBER                
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   MOVE VSCHVL-CHARTC-VAL-DESC  TO ASC-BRAND                    
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE VSCHVL-STAT-CDE         TO ASC-STAT                     
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
               MOVE +1               TO ASC-BLK-SUB                             
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
               ADD +1                TO ASC-BLK-SUB                             
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE 1                TO ASC-BLK-SUB                             
                                        PV-BLOCK-NUMBER                         
               PERFORM 4410-READ-TEMP-STORAGE                                   
               IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
                   MOVE 2            TO ASC-BLK-SUB                             
                                        PV-BLOCK-NUMBER                         
                   PERFORM 4410-READ-TEMP-STORAGE                               
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FETCH THE MESSAGE CURSOR.                                   *        
      *----------------------------------------------------------------*        
                                                                                
       4310-FETCH.                                                              
                                                                                
                EXEC SQL                                                        
                     FETCH BRAND-CSR                                            
                     INTO  :VSCHVL-CHARTC-VAL-DESC                              
                          ,:VSCHVL-STAT-CDE                                     
                END-EXEC                                                        
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE NOT = ZERO                                          
                    MOVE '4310-FETCH-CURSOR'                                    
                                     TO DP013-PARAGRAPH                         
                    MOVE 'FETCH BRAND CURSOR'                                   
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE SQLCA       TO DP013-SQLCA                             
                    SET DP013-DB2-ABEND                                         
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
           IF  SQLCODE EQUAL +100                                               
               SET ASC-NO-ITEMS-LEFT-ON-DB                                      
                                         TO TRUE                                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
      *----------------------------------------------------------------*        
                                                                                
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
           MOVE ASC-KEY-DEPT             TO ADEPTO.                             
           MOVE ASC-DEPT-DESC            TO ADESCO.                             
           IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
               AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
               OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
                   MOVE +1    TO ASC-BLK-SUB                                    
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1     TO ASC-BLK-SUB                                    
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
                       GIVING PV-BLOCK-NUMBER                                   
                       REMAINDER PV-REMAINDER                                   
                   IF  PV-REMAINDER > ZERO                                      
                       ADD +1 TO PV-BLOCK-NUMBER                                
                   END-IF                                                       
                   MOVE +1    TO ASC-BLK-SUB                                    
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   ADD +1     TO ASC-BLK-SUB                                    
                                 PV-BLOCK-NUMBER                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
           END-IF.                                                              
           MOVE +1            TO ASC-BLK-SUB.                                   
           COMPUTE ASC-ITEM-SUB =                                               
               ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
               + PC-ITEMS-PER-PANEL).                                           
      *                                                                         
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
               ADD +1         TO ASC-BLK-SUB                                    
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
           END-IF.                                                              
      *                                                                         
           COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
               ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
           PERFORM 4420-FORMAT-LIST-LINES                                       
               VARYING PV-SUB                                                   
               FROM PC-ITEMS-PER-PANEL BY -1                                    
               UNTIL PV-SUB < +1.                                               
      *                                                                         
           COMPUTE ASC-ITEMS-DISPLAYED =                                        
               ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
           MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
                                            ASTRTITO.                           
           MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
           MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
           MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
                                         TO ASELITO.                            
      *                                                                         
           IF  DP020-MSG-NUMBER-X = SPACE                                       
               IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
      *            --- MORE ITEMS TO DISPLAY ----                               
                   MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL TO TRUE                          
               END-IF                                                           
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
      *----------------------------------------------------------------*        
                                                                                
       4410-READ-TEMP-STORAGE.                                                  
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
             OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND     TO TRUE                                 
               SET DP013-NO-ROLLBACK    TO TRUE                                 
               MOVE '4410-READ-TEMP-STORAGE'                                    
                                        TO DP013-PARAGRAPH                      
               MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
                                        TO DP013-MESSAGE-TEXT(1)                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
      *                                                                         
           IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
               INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
      *----------------------------------------------------------------*        
                                                                                
       4420-FORMAT-LIST-LINES.                                                  
           IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
               SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
               MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
               MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
               MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
                                          MR-BRAND (PV-SUB)                     
                                          MR-STAT (PV-SUB)                      
           ELSE                                                                 
               IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
                                                         > SPACE                
                   MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
                                       TO MR-SELECTI (PV-SUB)                   
               ELSE                                                             
                   MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
                                       TO MR-SELECTI (PV-SUB)                   
               END-IF                                                           
      *                                                                         
               IF  DP030-SET-CURSOR-TRAILER                                     
                   IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
                       > SPACE)                                                 
                     OR (PV-SUB = 1))                                           
                      SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
                      MOVE -1          TO MR-SELECTL (PV-SUB)                   
                   END-IF                                                       
               END-IF                                                           
      *                                                                         
               IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
                   MOVE ASC-BRAND (ASC-BLK-SUB ASC-ITEM-SUB)                    
                                       TO MR-BRAND (PV-SUB)                     
                   MOVE ASC-STAT (ASC-BLK-SUB ASC-ITEM-SUB)                     
                                       TO MR-STAT (PV-SUB)                      
               END-IF                                                           
           END-IF.                                                              
           SUBTRACT +1 FROM ASC-ITEM-SUB                                        
           IF  ASC-ITEM-SUB < + 1                                               
               MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
               MOVE +1                 TO ASC-BLK-SUB                           
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
      * ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
      * FOR THE SELECTED ITEMS QUEUE.                                  *        
      *                                                                *        
      * FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
      * QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
      * THE ARRAY IS FULL.                                             *        
      *----------------------------------------------------------------*        
                                                                                
       5000-PREPARE-SEL-QUEUE.                                                  
                                                                                
           MOVE +1                   TO  ASC-BLK-SUB.                           
           PERFORM 5100-WRITE-TEMP-STORAGE.                                     
           ADD +1                    TO  ASC-BLK-SUB.                           
           PERFORM 5100-WRITE-TEMP-STORAGE.                                     
                                                                                
           PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
                   VARYING PV-ITEM                                              
                   FROM 1 BY 1                                                  
                   UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
      *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
      *----------------------------------------------------------------*        
                                                                                
       5100-WRITE-TEMP-STORAGE.                                                 
           MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
                                         TO PV-LIST-ITEM-NUMBER.                
           COMPUTE PV-BLOCK-NUMBER =                                            
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
           OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
               IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
                   PERFORM 5105-REWRITE-ASC-LISTQ                               
               END-IF                                                           
           ELSE                                                                 
               COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
               ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
               PERFORM 5110-WRITE-ASC-LISTQ                                     
               ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
           END-IF.                                                              
                                                                                
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
      ******************************************************************        
      ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
       5105-REWRITE-ASC-LISTQ.                                                  
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND            TO TRUE                          
               SET DP013-NO-ROLLBACK           TO TRUE                          
               MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
               MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
                                               TO DP013-MESSAGE-TEXT(1)         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
       5110-WRITE-ASC-LISTQ.                                                    
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
           IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '5110-WRITE-ASC-LISTQ'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
      * TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
      *----------------------------------------------------------------*        
                                                                                
       5200-FILL-SEL-QUEUE-RECORD.                                              
                                                                                
           PERFORM 6112-READ-LIST-QUEUE.                                        
                                                                                
           IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
               PERFORM                                                          
               VARYING LQW-IDX                                                  
               FROM 1 BY 1                                                      
               UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
                 OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
                                                                                
                   IF  LQW-SELECT (LQW-IDX)                                     
                       MOVE LQW-BRAND (LQW-IDX) TO IM008-RETURN-DATA            
                       SET IM008-RETURN-BRAND   TO TRUE                         
                   END-IF                                                       
               END-PERFORM                                                      
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
      *----------------------------------------------------------------*        
                                                                                
       6112-READ-LIST-QUEUE.                                                    
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
                   ITEM  (PV-ITEM)                                              
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND        TO TRUE                              
               SET DP013-NO-ROLLBACK       TO TRUE                              
               MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
               MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
                                           TO DP013-MESSAGE-TEXT(1)             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
