       IDENTIFICATION DIVISION.                                                 
00002 *****************************************************************         
00003  PROGRAM-ID.    MLKUP018.                                                 
00004  AUTHOR.        D. PHELPS.                                                
00005  INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
00006  DATE-WRITTEN.  03/06/93.                                                 
00007  DATE-COMPILED.                                                           
00008                                                                           
00009 ******************************************************************        
00011 *    MLKUP018. CONVERSION PROGRAM OF MARKDOWN STOCK TO           *        
00012 *    MRKDOWN STOCK FOR RESALE.                                   *        
      *CHANGE HISTORY:                                                          
      * WR/PROJ   DATE        DESCRIPTION OF CHANGES                            
      * -------   ----------  ----------------------------------------          
487868*PM00487868 08-15-2002  ABEND IN MLK720 - SKU NOT FOUND.                  
487868*                       - ROD JANSSEN                                     
W26603* W26603    03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION           
      * W29145    08-15-2005 KEYMASTER UPGRADE FOR STORE NUMBER                 
      *                       EXPANSION.  - V. LEE YANG                         
S29145* S29145    11-20-2005 S. JACKSON - SAS MKDN OPTIMZ-MLRD017 EXPAND        
S29145*                       INCREASE OUTPUT LRECL = 200                       
SR3403* SR3403    02-27-2012 TEJA - LARGE RETAIL AMOUNT FIELD EXPANSION         
00014 ******************************************************************        
00015                                                                           
00016  ENVIRONMENT DIVISION.                                                    
00017                                                                           
00018  INPUT-OUTPUT SECTION.                                                    
00019  FILE-CONTROL.                                                            
00020                                                                           
00021       SELECT EXTRACT-FILE                                                 
00022              ASSIGN TO INP1.                                              
00023                                                                           
00024       SELECT FORMAT-FILE                                                  
00025              ASSIGN TO OUTP1.                                             
00026                                                                           
00027  DATA DIVISION.                                                           
00028  FILE SECTION.                                                            
00029                                                                           
00030  FD  EXTRACT-FILE                                                         
NEWSKU     RECORDING  MODE IS F                                                 
00031      LABEL RECORDS ARE STANDARD.                                          
W26603 01  INPUT-REC                     PIC X(120).                            
00033                                                                           
00034  FD  FORMAT-FILE                                                          
NEWSKU     RECORDING  MODE IS F                                                 
00035      LABEL RECORDS ARE STANDARD.                                          
W26603*01  OUTPUT-REC                    PIC X(120).                            
S29145 01  OUTPUT-REC                    PIC X(200).                            
00037                                                                           
00038  WORKING-STORAGE SECTION.                                                 
00039                                                                           
W29145     COPY MLRD012I.                                                       
NEWSKU                                                                          
W26603     COPY MLRD017.                                                        
00041                                                                           
W29145 01  PS-PROGRAM-SWITCHES.                                                 
W29145     05  PS-VALID-UPC-SW                 PIC X(01)   VALUE 'N'.           
W29145         88  PS-VALID-UPC                            VALUE 'Y'.           
W29145         88  PS-INVALID-UPC                          VALUE 'N'.           
W29145     05  PS-EOF-SW                       PIC X(01)   VALUE 'N'.           
W29145         88  PS-EOF                                  VALUE 'Y'.           
W29145         88  PS-NOT-EOF                              VALUE 'Y'.           
                                                                                
W29145 01  PS-PROGRAM-CONSTANTS.                                                
SR3403     05  PC-MAX-RTL                  PIC S9(05)V99 VALUE +99999.99        
W29145                         COMP-3.                                          
W29145     05  PC-MAX-RETRIES              PIC S9(04)      VALUE +3             
W29145                         COMP SYNC.                                       
00044                                                                           
NEWSKU 01  PV-HOLD-VARIABLES.                                                   
W29145     05  PV-EXTENDED-RTL             PIC S9(06)V99   VALUE ZEROES         
W29145                         COMP.                                            
W29145     05  PV-RECORD-IN                PIC S9(07)      VALUE ZEROES         
W29145                         COMP-3.                                          
W29145     05  PV-SPLIT-RECORD             PIC S9(07)      VALUE ZEROES         
W29145                         COMP-3.                                          
W29145     05  PV-RECORD-OUT               PIC S9(07)      VALUE ZEROES         
W29145                         COMP-3.                                          
W29145     05  PV-WORK-UPC                 PIC 9(13).                           
W29145     05  PV-WORK-UPC-X  REDEFINES  PV-WORK-UPC.                           
W29145         10  FILLER                  PIC X(4).                            
W29145             88  PV-KOHLS-UPC                        VALUE '0400'.        
W29145         10  PV-UPC-SKU-9            PIC 9(8).                            
W29145         10  PV-UPC-CHK-DIG          PIC 9.                               
W29145     05  PV-PREV-UPC-X               PIC X(15)       VALUE SPACES.        
W29145     05  PV-PREV-SKU                 PIC X(08)       VALUE SPACES.        
W29145     05  PV-PREV-DEPT-NBR            PIC 9(03)       VALUE ZEROES.        
W29145     05  PV-PREV-CLASS-NBR           PIC 9(02)       VALUE ZEROES.        
W29145     05  PV-TEMP-COUNT               PIC S9(04)      VALUE ZEROES         
W29145                         COMP SYNC.                                       
W29145     05  PV-SKU-X-RIGHT              PIC X(08)       VALUE SPACES         
W29145                                                     JUST RIGHT.          
W29145     05  PV-SKU-9-RIGHT REDEFINES PV-SKU-X-RIGHT                          
W29145                                     PIC 9(08).                           
W29145     05  PV-RETRY-COUNT              PIC S9(04)      VALUE ZEROES         
W29145                         COMP.                                            
W29145     05  PV-PARAGRAPH-NAME           PIC  X(30)      VALUE SPACES.        
W29145     05  PV-DB2-OPER-ATTEMPTED       PIC  X(30)      VALUE SPACES.        
00045      EJECT                                                                
00046                                                                           
NEWSKU*-----------------------------------------------------------------        
NEWSKU*  SKU CROSS-REFERENCE TABLE                                              
NEWSKU*-----------------------------------------------------------------        
NEWSKU                                                                          
NEWSKU     EXEC SQL                                                             
NEWSKU         INCLUDE TSKXREF                                                  
NEWSKU     END-EXEC.                                                            
NEWSKU                                                                          
W29145*-----------------------------------------------------------------        
W29145*  UPC SKU TABLE                                                          
W29145*-----------------------------------------------------------------        
W29145                                                                          
W29145     EXEC SQL                                                             
W29145         INCLUDE TUPCSKU                                                  
W29145     END-EXEC.                                                            
                                                                                
NEWSKU*-----------------------------------------------------------------        
NEWSKU*  SQL COMMUNICATIONS AREA                                                
NEWSKU*-----------------------------------------------------------------        
NEWSKU                                                                          
NEWSKU     EXEC SQL                                                             
NEWSKU         INCLUDE SQLCA                                                    
NEWSKU     END-EXEC.                                                            
NEWSKU                                                                          
NEWSKU*****************************************************************         
NEWSKU*  ABEND ROUTINE WORKING STORAGE                                          
NEWSKU*****************************************************************         
NEWSKU                                                                          
NEWSKU     COPY DPWS004.                                                        
NEWSKU                                                                          
NEWSKU 01  ABEND-CODE                          PIC S9(04)  VALUE +0             
NEWSKU                                                     COMP SYNC.           
NEWSKU     88  ABEND-4003                                  VALUE +4003.         
NEWSKU                                                                          
00047                                                                           
00048  EJECT                                                                    
00049  PROCEDURE DIVISION.                                                      
00050 *                                                                         
00051 *----------------------------------------------------------------*        
00052 *    PROGRAM A100-MAINLINE.                                      *        
00053 *----------------------------------------------------------------*        
00054                                                                           
00055                                                                           
00056      PERFORM B100-OPEN-ROUTINE.                                           
00057                                                                           
W29145     PERFORM C100-READ-INPUT.                                             
W29145     PERFORM B200-PROCESS-INPUT                                           
W29145         UNTIL PS-EOF.                                                    
W29145                                                                          
00060      PERFORM B300-CLOSE-FILES.                                            
00061                                                                           
W29145     DISPLAY ' '.                                                         
W29145     DISPLAY ' RECORD IN:        ' PV-RECORD-IN.                          
W29145     DISPLAY ' SPLIT RECORD:     ' PV-SPLIT-RECORD.                       
W29145     DISPLAY ' RECORD OUT:       ' PV-RECORD-OUT.                         
W29145     DISPLAY ' '.                                                         
W29145                                                                          
W29145     IF PV-SPLIT-RECORD > ZEROES                                          
W29145         DISPLAY ' *** THERE WERE TRANSACTIONS WITH RETAIL'               
W29145                 ' EXCEEDING ' PC-MAX-RTL                                 
W29145         DISPLAY ' *** SPLIT TRANSACTIONS CREATED!!!'                     
W29145         MOVE +4059  TO RETURN-CODE                                       
W29145     END-IF.                                                              
                                                                                
00062      GOBACK.                                                              
00063                                                                           
00064 *----------------------------------------------------------------*        
00065 *    B100-OPEN-ROUTINE.                                          *        
00066 *----------------------------------------------------------------*        
00067  B100-OPEN-ROUTINE.                                                       
00068                                                                           
00069      OPEN INPUT EXTRACT-FILE                                              
00070           OUTPUT FORMAT-FILE.                                             
00071                                                                           
00072 *----------------------------------------------------------------*        
00073 *    B200-PROCESS-INPUT.                                         *        
00074 *----------------------------------------------------------------*        
00075  B200-PROCESS-INPUT.                                                      
00076                                                                           
W29145     ADD +1                     TO PV-RECORD-IN.                          
W29145                                                                          
W29145     MOVE ML012I-RECORD-CODE    TO ML017-PC-CDE.                          
W29145     MOVE ML012I-TYPE-CODE      TO ML017-PC-TYP-CDE.                      
W29145     MOVE ML012I-5-DIGIT-DOCUMENT-NMBR                                    
W29145         TO ML017-PC-DOC-5-NBR.                                           
W29145     MOVE ML012I-MONTH          TO ML017-PC-DTE-MM.                       
W29145     MOVE ML012I-DAY            TO ML017-PC-DTE-DD.                       
W29145     MOVE ML012I-KOHLS-MONTH    TO ML017-PC-KOHLS-MM.                     
W29145                                                                          
W29145     IF ML012I-SKU-UPC-X   = PV-PREV-UPC-X                                
W29145         MOVE PV-PREV-SKU       TO ML017-PC-SKU                           
W29145         MOVE PV-PREV-DEPT-NBR  TO ML017-PC-DEPT-NBR                      
W29145         MOVE PV-PREV-CLASS-NBR TO ML017-PC-CLASS-NBR                     
W29145     ELSE                                                                 
W29145         IF ML012I-SKU-UPC-X    NUMERIC                                   
W29145             OR ML012I-UPC-13-X NUMERIC                                   
W29145             OR ML012I-UPC-12-X NUMERIC                                   
W29145             PERFORM C200-CONVERT-UPC-TO-SKU                              
W29145         ELSE                                                             
W29145             MOVE ZEROES              TO PV-TEMP-COUNT                    
W29145             INSPECT ML012I-SKU-X                                         
W29145                 TALLYING PV-TEMP-COUNT                                   
W29145                 FOR ALL ' '                                              
W29145* UNSTRING/SPLIT THE STRING AT FIRST SPACE                                
W29145             IF PV-TEMP-COUNT = 1                                         
W29145                 MOVE +1              TO PV-TEMP-COUNT                    
W29145                 UNSTRING ML012I-SKU-X                                    
W29145                     DELIMITED BY ' '                                     
W29145                     INTO PV-SKU-X-RIGHT                                  
W29145                     POINTER PV-TEMP-COUNT                                
W29145             END-IF                                                       
W29145* DO A PLAIN MOVE OF THE FIELD, IF EITHER MORE THAN 1 SPACE               
W29145* OR LOCATION OF THE ' ' IS BEFORE POSITION 8                             
W29145             IF PV-TEMP-COUNT < 9                                         
W29145                 MOVE ML012I-SKU-X    TO PV-SKU-X-RIGHT                   
W29145             END-IF                                                       
W29145             MOVE PV-SKU-9-RIGHT      TO ML017-PC-SKU                     
W29145         END-IF                                                           
W29145                                                                          
W29145         IF ML017-PC-SKU NOT EQUAL ZEROES                                 
W29145             IF ML017-PC-SEQ-NBR EQUAL ZEROES                             
W29145                 MOVE ML017-PC-SKU(1:3)    TO ML017-PC-DEPT-NBR           
W29145                 MOVE ML017-PC-DEPT-NBR    TO SKXREF-DEPT                 
W29145                 MOVE ML017-PC-SKU(4:2)    TO ML017-PC-CLASS-NBR          
W29145                 MOVE ML017-PC-CLASS-NBR   TO SKXREF-SUBCLASS             
W29145             ELSE                                                         
W29145                 MOVE ML017-PC-SKU         TO SKXREF-SKU                  
W29145                 PERFORM D200-RETRIEVE-SKU-ATTR                           
W29145             END-IF                                                       
W29145         ELSE                                                             
W29145             MOVE ZEROES TO SKXREF-DEPT                                   
W29145                            SKXREF-SUBCLASS                               
W29145         END-IF                                                           
W29145         MOVE SKXREF-DEPT      TO ML017-PC-DEPT-NBR                       
W29145                                  PV-PREV-DEPT-NBR                        
W29145         MOVE SKXREF-SUBCLASS  TO ML017-PC-CLASS-NBR                      
W29145                                  PV-PREV-CLASS-NBR                       
W29145         MOVE ML017-PC-SKU     TO PV-PREV-SKU                             
W29145         MOVE ML012I-SKU-UPC-X TO PV-PREV-UPC-X                           
W29145                                                                          
W29145     END-IF.                                                              
                                                                                
NEWSKU     IF ML012I-REASON-CODE = '7'                                          
00081          MOVE '4' TO ML017-PC-RSN-CDE                                     
W29145     ELSE                                                                 
W29145         MOVE ML012I-REASON-CODE TO ML017-PC-RSN-CDE                      
00082      END-IF.                                                              
00083                                                                           
W29145     MOVE ML012I-SKU-QUANTITY   TO ML017-PC-QTY.                          
W29145     MOVE ML012I-BATCH-NMBR     TO ML017-PC-BATCH-NBR.                    
W29145     MOVE ML012I-MKDN-MKUP-RSN-CDE TO ML017-PC-MKDN-MKUP-RSN-CDE.         
W29145     MOVE ML012I-DOCUMENT-QUANTITY TO ML017-PC-TOT-DOC-QTY.               
W29145     MOVE ML012I-STORE-NMBR     TO ML017-PC-STORE-NBR.                    
W29145     MOVE ML012I-MKDN-MKON-CATEGORY TO ML017-PC-CATEG-CDE.                
W29145     MOVE ML012I-7-DIGIT-DOCUMENT-NMBR TO ML017-PC-DOC-7-NBR.             
W29145     MOVE ML012I-SLS-CORR-CDE   TO ML017-SLS-CORR-CDE.                    
W29145     MOVE ML012I-LINE-NMBR      TO ML017-PC-LINE-NBR.                     
S29145                                                                          
S29145     MOVE SPACES                TO ML017-ORIG-CLR-DTE                     
S29145                                   ML017-MKDN-CDE                         
S29145                                   ML017-PC-DA.                           
S29145     MOVE ZEROES                TO ML017-ORIG-CLR-NONGRP-RTL.             
S29145                                                                          
W29145     IF ML012I-NO-ENT-ID                                                  
W29145         MOVE ML012I-VENDOR-INHSE-NMBR                                    
W29145             TO ML017-PC-VENDOR-INHSE-NMBR                                
W29145         MOVE ZEROES            TO ML017-PC-ENT-ID                        
W29145     ELSE                                                                 
W29145         MOVE ML012I-ENT-ID                                               
W29145             TO ML017-PC-ENT-ID                                           
W29145         MOVE ZEROES            TO ML017-PC-VENDOR-INHSE-NMBR             
W29145     END-IF.                                                              
                                                                                
W29145     MOVE ZEROES                   TO ML017-RGST-ID                       
W29145                                      ML017-TRN-NBR                       
W29145                                      ML017-LNE-ITM-SEQ-NBR.              
W29145     MOVE SPACES                   TO ML017-GMM                           
W29145                                      ML017-DMM                           
W29145                                      ML017-BUYER.                        
                                                                                
W29145* SPLIT PRICE CHANGES, IF EITHER RETAIL EXCEEDED THE MAX                  
W29145     PERFORM                                                              
W29145         UNTIL ML012I-ORIGINAL-RETAIL <= PC-MAX-RTL                       
W29145             AND ML012I-CURRENT-RETAIL <= PC-MAX-RTL                      
W29145         ADD +1                     TO PV-SPLIT-RECORD                    
W29145                                                                          
W29145* SPLIT OLD/ORIGINAL RETAIL                                               
W29145         IF ML012I-ORIGINAL-RETAIL > PC-MAX-RTL                           
W29145             MOVE PC-MAX-RTL    TO ML017-PC-OLD-PR-AMT                    
W29145             COMPUTE ML012I-ORIGINAL-RETAIL                               
W29145                     = ML012I-ORIGINAL-RETAIL                             
W29145                     - PC-MAX-RTL                                         
W29145         ELSE                                                             
W29145             MOVE ML012I-ORIGINAL-RETAIL                                  
W29145                 TO ML017-PC-OLD-PR-AMT                                   
W29145             MOVE ZEROES        TO ML012I-ORIGINAL-RETAIL                 
W29145         END-IF                                                           
W29145* SPLIT NEW/CURRENT RETAIL                                                
W29145         IF ML012I-CURRENT-RETAIL  > PC-MAX-RTL                           
W29145             MOVE PC-MAX-RTL    TO ML017-PC-NEW-PR-AMT                    
W29145             COMPUTE ML012I-CURRENT-RETAIL                                
W29145                     = ML012I-CURRENT-RETAIL                              
W29145                     - PC-MAX-RTL                                         
W29145         ELSE                                                             
W29145             MOVE ML012I-CURRENT-RETAIL                                   
W29145                 TO ML017-PC-NEW-PR-AMT                                   
W29145             MOVE ZEROES        TO ML012I-CURRENT-RETAIL                  
W29145         END-IF                                                           
W29145         COMPUTE ML017-PC-DIFFER-AMT = (ML017-PC-OLD-PR-AMT               
W29145                 - ML017-PC-NEW-PR-AMT)                                   
W29145         IF ML017-PC-DIFFER-AMT < ZEROES                                  
W29145             COMPUTE ML017-PC-DIFFER-AMT = ML017-PC-DIFFER-AMT            
W29145                     * -1                                                 
W29145         END-IF                                                           
W29145         COMPUTE PV-EXTENDED-RTL = ML017-PC-DIFFER-AMT                    
W29145                 * ML017-PC-QTY                                           
W29145                 ON SIZE ERROR                                            
W29145                 MOVE 1000000.00 TO PV-EXTENDED-RTL                       
W29145         END-COMPUTE                                                      
W29145         IF PV-EXTENDED-RTL > 999999.99                                   
W29145             DISPLAY ' *** EXTENDED RETAIL IS TOO BIG.'                   
W29145             DISPLAY ' *** QTY: ' ML017-PC-QTY                            
W29145                     ' PRICE DIFF: ' ML017-PC-DIFFER-AMT                  
W29145             PERFORM Z999-ABEND                                           
W29145         END-IF                                                           
W29145         PERFORM C300-WRITE-EXTRACT                                       
W29145* CALCULATED REMAINING DIFFERENCES                                        
W29145         COMPUTE ML012I-KEYED-DIFFERENCE                                  
W29145                 = (ML012I-ORIGINAL-RETAIL                                
W29145                 - ML012I-CURRENT-RETAIL)                                 
W29145         IF ML012I-KEYED-DIFFERENCE < ZEROES                              
W29145             COMPUTE ML012I-KEYED-DIFFERENCE                              
W29145                     = ML012I-KEYED-DIFFERENCE                            
W29145                     * -1                                                 
W29145         END-IF                                                           
W29145     END-PERFORM.                                                         
W29145                                                                          
W29145* IF SPLIT LOGIC IS EXECUTED, CREATE THE FINAL/REMAINING                  
W29145* OTHERWISE CREATE THE FIRST                                              
W29145     MOVE ML012I-KEYED-DIFFERENCE TO ML017-PC-DIFFER-AMT.                 
W29145     MOVE ML012I-ORIGINAL-RETAIL TO ML017-PC-OLD-PR-AMT.                  
W29145     MOVE ML012I-CURRENT-RETAIL  TO ML017-PC-NEW-PR-AMT.                  
W29145                                                                          
W29145* OLD-PRICE AND NEW-PRICE CAN BE THE SAME, IF THE USERS ENTERED           
W29145* THEM, OR THE SPLIT LOGIC CAUSE A REMAINING OF ZEROES IN BOTH.           
W29145     IF ML017-PC-OLD-PR-AMT NOT EQUAL ML017-PC-NEW-PR-AMT                 
W29145         COMPUTE PV-EXTENDED-RTL = ML017-PC-DIFFER-AMT                    
W29145                 * ML017-PC-QTY                                           
W29145                 ON SIZE ERROR                                            
W29145                 MOVE 1000000.00 TO PV-EXTENDED-RTL                       
W29145         END-COMPUTE                                                      
W29145         IF PV-EXTENDED-RTL > 999999.99                                   
W29145             DISPLAY ' *** EXTENDED RETAIL IS TOO BIG.'                   
W29145             DISPLAY ' *** QTY: ' ML017-PC-QTY                            
W29145                     ' PRICE DIFF: ' ML017-PC-DIFFER-AMT                  
W29145             PERFORM Z999-ABEND                                           
W29145         END-IF                                                           
W29145         PERFORM C300-WRITE-EXTRACT                                       
W29145     END-IF.                                                              
W29145                                                                          
W29145     PERFORM C100-READ-INPUT.                                             
00087                                                                           
00088 *----------------------------------------------------------------*        
00089 *    B300-CLOSE-FILES                                            *        
00090 *----------------------------------------------------------------*        
00091  B300-CLOSE-FILES.                                                        
00092                                                                           
00093      CLOSE EXTRACT-FILE                                                   
00094            FORMAT-FILE.                                                   
00095                                                                           
                                                                                
      *----------------------------------------------------------------*        
      * C100-READ-INPUT                                                         
      * READ THE NEXT RECORD                                                    
      *----------------------------------------------------------------*        
W29145 C100-READ-INPUT.                                                         
W29145                                                                          
W29145     READ EXTRACT-FILE INTO ML012I-MARKDOWN-MARKON-RECORD                 
W29145        AT END SET PS-EOF TO TRUE.                                        
                                                                                
      ******************************************************************        
      *    CONVERT THE UPC CODE TO A SKU BY READING THE TSK TABLE.              
      ******************************************************************        
W29145 C200-CONVERT-UPC-TO-SKU.                                                 
W29145     EVALUATE TRUE                                                        
W29145         WHEN ML012I-IS-UPC-12                                            
W29145             MOVE ML012I-UPC-12  TO TUPCSKU-UPC-NBR                       
W29145                                    PV-WORK-UPC                           
W29145         WHEN ML012I-IS-UPC-13                                            
W29145             MOVE ML012I-UPC-13  TO TUPCSKU-UPC-NBR                       
W29145                                    PV-WORK-UPC                           
W29145         WHEN OTHER                                                       
W29145             MOVE ML012I-SKU-UPC TO TUPCSKU-UPC-NBR                       
W29145                                    PV-WORK-UPC                           
W29145     END-EVALUATE.                                                        
W29145     PERFORM D100-READ-TUPSKU.                                            
W29145     IF PS-VALID-UPC                                                      
W29145          MOVE SKXREF-SKU           TO ML017-PC-SKU                       
W29145     ELSE                                                                 
W29145         IF PV-KOHLS-UPC                                                  
W29145             MOVE PV-UPC-SKU-9      TO ML017-PC-SKU                       
W29145         ELSE                                                             
W29145             MOVE ZEROES            TO ML017-PC-SKU                       
W29145             DISPLAY 'INVALID VENDOR UPC ' PV-WORK-UPC                    
W29145         END-IF                                                           
W29145     END-IF.                                                              
W29145                                                                          
                                                                                
      *----------------------------------------------------------------*        
      * C300-WRITE-EXTRACT                                                      
      * WRITE THE REFORMATED EXTRACT                                            
      *----------------------------------------------------------------*        
W29145 C300-WRITE-EXTRACT.                                                      
W29145                                                                          
W29145     ADD +1                     TO PV-RECORD-OUT.                         
W29145                                                                          
W29145     WRITE OUTPUT-REC FROM ML017-PRICE-CHANGE-RECORD.                     
                                                                                
      ******************************************************************        
      *   READS TUPCSKU, CONVERTS UPC CODE TO SKU                               
      ******************************************************************        
W29145 D100-READ-TUPSKU.                                                        
W29145                                                                          
W29145     PERFORM                                                              
W29145         WITH TEST AFTER                                                  
W29145         VARYING PV-RETRY-COUNT FROM 1 BY 1                               
W29145         UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                          
W29145             OR (SQLCODE = 0)                                             
W29145             OR (SQLCODE = +100)                                          
W29145         EXEC SQL                                                         
W29145             SELECT  SKU                                                  
W29145                    ,INTR_UPC_ID                                          
W29145                    ,SKU_STAT_CDE                                         
W29145             INTO  :SKXREF-SKU                                            
W29145                  ,:SKXREF-INTR-UPC-ID                                    
W29145                  ,:SKXREF-SKU-STAT-CDE                                   
W29145             FROM   TUPCSKU A                                             
W29145                   ,TSKXREF B                                             
W29145             WHERE A.ITEM_NBR = B.ITM_NBR                                 
W29145               AND UPC_NBR = :TUPCSKU-UPC-NBR                             
W29145             WITH UR                                                      
W29145         END-EXEC                                                         
W29145         EVALUATE TRUE                                                    
W29145             WHEN SQLCODE = 0                                             
W29145                 SET PS-VALID-UPC TO TRUE                                 
W29145             WHEN SQLCODE = -904                                          
W29145             WHEN SQLCODE = -911                                          
W29145                 CONTINUE                                                 
W29145             WHEN SQLCODE = +100                                          
W29145                 SET PS-INVALID-UPC  TO TRUE                              
W29145             WHEN OTHER                                                   
W29145                 DISPLAY ' UPC NUMBER: ' TUPCSKU-UPC-NBR                  
W29145                 MOVE 'D100-READ-TUPSKU' TO  PV-PARAGRAPH-NAME            
W29145                 MOVE 'READING SKU/UPC TABLE'                             
W29145                     TO PV-DB2-OPER-ATTEMPTED                             
W29145                 PERFORM Z900-SQL-ABEND                                   
W29145         END-EVALUATE                                                     
W29145     END-PERFORM.                                                         
W29145                                                                          
W29145     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
W29145         MOVE 'MAX RETRIES EXCEEDED'                                      
W29145             TO PV-DB2-OPER-ATTEMPTED                                     
W29145         MOVE 'D100-READ-TUPSKU' TO PV-PARAGRAPH-NAME                     
W29145         PERFORM Z900-SQL-ABEND                                           
W29145     END-IF.                                                              
                                                                                
NEWSKU*----------------------------------------------------------------*        
  |   *    D200-RETRIEVE-SKU-ATTRIBUTES FROM TSKXREF                   *        
  |   *----------------------------------------------------------------*        
  |    D200-RETRIEVE-SKU-ATTR.                                                  
  |                                                                             
  |        EXEC SQL                                                             
W29145         SELECT DEPT                                                      
W29145               ,CLASS                                                     
W29145               ,SUBCLASS                                                  
W29145         INTO  :SKXREF-DEPT                                               
W29145              ,:SKXREF-CLASS                                              
W29145              ,:SKXREF-SUBCLASS                                           
W29145         FROM   TSKXREF                                                   
W29145         WHERE  SKU = :SKXREF-SKU                                         
               WITH UR                                                          
  |        END-EXEC                                                             
  |                                                                             
  |        EVALUATE TRUE                                                        
  |            WHEN SQLCODE = 0                                                 
NEWSKU             CONTINUE                                                     
487868         WHEN SQLCODE = +100                                              
487868            DISPLAY ' MLKUP018 - SKU '                                    
487868                    SKXREF-SKU ' NOT FOUND'                               
487868            MOVE ZEROES    TO SKXREF-DEPT                                 
487868                              SKXREF-CLASS                                
487868                              SKXREF-SUBCLASS                             
NEWSKU         WHEN OTHER                                                       
  |                DISPLAY '**                                   **'            
  |                DISPLAY '**  SKXREF-SKU = ' SKXREF-SKU                       
  |                DISPLAY '**                                   **'            
                   MOVE 'D200-RETRIEVE-SKU-ATTR' TO PV-PARAGRAPH-NAME           
                   MOVE 'READING SKU CROSS-REF' TO PV-DB2-OPER-ATTEMPTED        
  |                PERFORM Z900-SQL-ABEND                                       
  |        END-EVALUATE.                                                        
  |                                                                             
  |   *----------------------------------------------------------------*        
  |   * Z900-SQL-ABEND                                                          
  |   * EXECUTED FOR FAILED SQL CALLS                                           
  |   *----------------------------------------------------------------*        
  |    Z900-SQL-ABEND.                                                          
  |                                                                             
  |        DISPLAY '**  ABEND     **'.                                          
  |        DISPLAY '**  PROGRAM   **  = MLKUP018'.                              
  |        DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
  |        DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
  |                                                                             
  |   *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
  |   *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
  |                                                                             
  |        COPY DPPD004.                                                        
W29145                                                                          
W29145     PERFORM Z999-ABEND.                                                  
  |                                                                             
  |   *----------------------------------------------------------------*        
  |   * Z999-ABEND                                                              
  |   * EXECUTED CALL TO ABEND                                                  
  |   *----------------------------------------------------------------*        
W29145 Z999-ABEND.                                                              
W29145                                                                          
W29145     MOVE +4013                 TO ABEND-CODE.                            
W29145     CALL 'ILBOABN0'             USING ABEND-CODE.                        
                                                                                
