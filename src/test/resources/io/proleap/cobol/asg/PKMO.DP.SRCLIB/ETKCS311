00001  IDENTIFICATION DIVISION.                                         06/22/94
00002  PROGRAM-ID.    ETKCS311.                                         ETKCS311
00003  AUTHOR.        MARC BLAZICH - G&R.                                  LV014
00004  INSTALLATION.  KOHLS DEPARTMENT STORES.                          ETKCS311
00005  DATE-WRITTEN.  02/94.                                            ETKCS311
00006  DATE-COMPILED.                                                   ETKCS311
00007                                                                   ETKCS311
00008 *---------------------------------------------------------------* ETKCS311
00009 *    TRANID      - E311                                         * ETKCS311
00010 *    TYPE        - CICS ARCHITECTURE INQUIRY - DB2              * ETKCS311
00011 *    DESCRIPTION - PURCHASE ORDER ITEM INQUIRY                  * ETKCS311
00012 *    MAPSET      - ETKM311                                      * ETKCS311
00013 *                                                               * ETKCS311
00014 *    THIS PROGRAM ALLOWS ONE TO VIEW TP FUNCTIONAL GROUPS FOR   * ETKCS311
00015 *    A GIVEN DKEY, FUNC_CDE, AND DIR_IND PASSED IN COMMAREA OR  *    CL*10
00016 *    TEMP STORAGE.                                              *    CL*10
00017 *                                                               * ETKCS311
00024 *    CHANGES:                                                   * ETKCS306
00024 *     03/20/97 TOM MEDREK - FIXED APPLICATION SPECIFIC SECTION. * ETKCS306
00024 *       ADDED NRF INDICATOR WHICH WAS ADDED TO ETWS300 &        * ETKCS306
00024 *       ETWS301 EARLIER.                                        * ETKCS306
00018 *---------------------------------------------------------------* ETKCS311
CICS  *  CHANGED 10/25/2022    BY BARB SCHULTZ    WR: CHG0277210      * ETKCS306
CICS  *                                                               * ETKCS306
CICS  *  ADDED COPYBOOK DPWS930 AND MADE THE CICS ARCHITECTURE CALL   * ETKCS306
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.    * ETKCS306
00018 *---------------------------------------------------------------* ETKCS311
00019                                                                   ETKCS311
00020  ENVIRONMENT DIVISION.                                            ETKCS311
00021  DATA DIVISION.                                                   ETKCS311
00022 /                                                                 ETKCS311
00023  WORKING-STORAGE SECTION.                                         ETKCS311
00024                                                                   ETKCS311
00025  01  DK-DB2-KEYS.                                                 ETKCS311
00026      05  DK-TP-DKEY                   PIC S9(09) VALUE +0         ETKCS311
00027                                                  COMP.            ETKCS311
00028      05  DK-FUNC-CDE                  PIC  X(02).                 ETKCS311
00029      05  DK-DIR-IND                   PIC  X(01).                 ETKCS311
00030                                                                   ETKCS311
00031  01  PC-PROGRAM-CONSTANTS.                                        ETKCS311
00032      05  PC-TRANS-ID                  PIC X(04)  VALUE 'E311'.    ETKCS311
00033      05  PC-CURRENT-PROGRAM-NAME      PIC X(08)  VALUE 'ETKCS311'.ETKCS311
00034      05  PC-CURRENT-MAP-NAME          PIC X(08)  VALUE 'ET311A  '.ETKCS311
00035      05  PC-CURRENT-MAPSET-NAME       PIC X(08)  VALUE 'ETKM311 '.ETKCS311
00036      05  PC-APPL-MAP                  PIC S9(04) VALUE  +1        ETKCS311
00037                                                  COMP.            ETKCS311
00038      05  PC-MAX-RETRIES               PIC S9(04) VALUE  +3        ETKCS311
00039                                                  COMP.            ETKCS311
00040      05  PC-NULL-DATE                 PIC X(10)  VALUE               CL*13
00041                                                  '09/09/9999'.       CL*13
00042      05  PC-1                         PIC S9(04) VALUE  +1        ETKCS311
00043                                                  COMP.            ETKCS311
00044      05  PC-INTER-APPL-CODES.                                     ETKCS311
00045          10  PC-INTER-APPL-CRIT-SPEC  PIC X(01)  VALUE ' '.       ETKCS311
00046          10  PC-INTER-APPL-LIST       PIC X(01)  VALUE '5'.       ETKCS311
00047                                                                   ETKCS311
00048      05  PC-TSYMSG-NUMBERS.                                       ETKCS311
00049          10  PC-TSYMSG-00004      PIC  9(05)  VALUE  00004.       ETKCS311
00050          10  PC-TSYMSG-00005      PIC  9(05)  VALUE  00005.       ETKCS311
00051          10  PC-TSYMSG-00007      PIC  9(05)  VALUE  00007.       ETKCS311
00052          10  PC-TSYMSG-00008      PIC  9(05)  VALUE  00008.       ETKCS311
00053          10  PC-TSYMSG-00009      PIC  9(05)  VALUE  00009.       ETKCS311
00054          10  PC-TSYMSG-00010      PIC  9(05)  VALUE  00010.       ETKCS311
00055          10  PC-TSYMSG-00057      PIC  9(05)  VALUE  00057.       ETKCS311
00056          10  PC-TSYMSG-00059      PIC  9(05)  VALUE  00059.       ETKCS311
00057          10  PC-TSYMSG-00069      PIC  9(05)  VALUE  00069.       ETKCS311
00058          10  PC-TSYMSG-00070      PIC  9(05)  VALUE  00070.       ETKCS311
00059          10  PC-TSYMSG-00095      PIC  9(05)  VALUE  00095.       ETKCS311
00060          10  PC-TSYMSG-00115      PIC  9(05)  VALUE  00115.       ETKCS311
00061          10  PC-TSYMSG-00116      PIC  9(05)  VALUE  00116.       ETKCS311
00062          10  PC-TSYMSG-00118      PIC  9(05)  VALUE  00118.       ETKCS311
00063          10  PC-TSYMSG-00148      PIC  9(05)  VALUE  00148.       ETKCS311
00064 *                                                                 ETKCS311
00065  01  PS-PROGRAM-SWITCHES.                                         ETKCS311
00066      05  PS-ERROR-SW              PIC  X(01)  VALUE  'N'.         ETKCS311
00067          88  PS-ERROR                         VALUE  'Y'.         ETKCS311
00068          88  PS-NO-ERROR                      VALUE  'N'.         ETKCS311
00069      05  PS-COMMAREA-SW           PIC  X(01)  VALUE 'N'.          ETKCS311
00070          88  PS-COMMAREA-VALID                VALUE 'Y'.          ETKCS311
00071          88  PS-COMMAREA-NOT-VALID            VALUE 'N'.          ETKCS311
00072      05  PS-LIST-SW               PIC  X(01)  VALUE  'N'.         ETKCS311
00073          88  PS-LIST-END                      VALUE  'Y'.         ETKCS311
00074          88  PS-LIST-NOT-END                  VALUE  'N'.         ETKCS311
00075 *                                                                 ETKCS311
00076  01  PV-PROGRAM-VARIABLES.                                        ETKCS311
00077      05  PV-CICS-RESPONSE         PIC S9(04)  VALUE +0            ETKCS311
00078                                               COMP SYNC.          ETKCS311
00079      05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0            ETKCS311
00080                                               COMP SYNC.          ETKCS311
00081      05  PV-MSG-VAR-CNT           PIC S9(04)  VALUE +0            ETKCS311
00082                                               COMP SYNC.          ETKCS311
00083      05  PV-SLASH-DATE.                                           ETKCS311
00084          10  PV-SL-DATE.                                          ETKCS311
00085              15  PV-SL-DATE-MM     PIC X(02).                     ETKCS311
00086              15  FILLER            PIC X(01)    VALUE '/'.        ETKCS311
00087              15  PV-SL-DATE-DD     PIC X(02).                     ETKCS311
00088              15  FILLER            PIC X(01)    VALUE '/'.        ETKCS311
00089              15  PV-SL-DATE-YY     PIC X(02).                     ETKCS311
00090          10  PV-SL-DATE-CC         PIC X(02).                     ETKCS311
00091                                                                   ETKCS311
00092 /                                                                 ETKCS311
00093 **--------------------------------------------------------------**ETKCS311
00094 ** THE LAYOUT FOR THE TEMPORARY STORAGE QUEUE CONTAINING        **ETKCS311
00095 ** SELECTED LIST ITEMS TO BE PROCESSED BY THIS PROGRAM.         **ETKCS311
00096 **--------------------------------------------------------------**ETKCS311
00097      COPY ETWS301.                                                   CL*14
00098                                                                   ETKCS311
00099 /                                                                 ETKCS311
00100 *----------------------------------------------------------------*ETKCS311
00101 *    MAP LAYOUT                                                  *ETKCS311
00102 *----------------------------------------------------------------*ETKCS311
00103      COPY ETKM311.                                                ETKCS311
00104                                                                   ETKCS311
00105 /                                                                 ETKCS311
00106 *----------------------------------------------------------------*ETKCS311
00107 *    ATTRIBUTE SETTINGS COPYBOOK.                                *ETKCS311
00108 *----------------------------------------------------------------*ETKCS311
00109      COPY DPWS015.                                                ETKCS311
00110                                                                   ETKCS311
00111 /                                                                 ETKCS311
00112 *----------------------------------------------------------------*ETKCS311
00113 *    FUNCTION KEYS COPYBOOK                                      *ETKCS311
00114 *----------------------------------------------------------------*ETKCS311
00115      COPY DPWS016.                                                ETKCS311
00116                                                                   ETKCS311
00117 /                                                                 ETKCS311
00118 **--------------------------------------------------------------**ETKCS311
CICS  *  PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.            **ETKCS311
00121 **--------------------------------------------------------------**ETKCS311
00122      COPY DPWS030.                                                ETKCS311
CICS       COPY DPWS930.                                                ETKCS311
00124 /                                                                 ETKCS311
00125 **--------------------------------------------------------------**ETKCS311
00126 *    STANDARD COMMAREA.                                           ETKCS311
00127 **--------------------------------------------------------------**ETKCS311
00128                                                                   ETKCS311
00129      COPY DPWS020.                                                ETKCS311
00130      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                ETKCS311
00131                                                                   ETKCS311
00132 ***************************************************************** ETKCS311
00133 *    INTER-APPLICATION COMMAREA.                                  ETKCS311
00134 ***************************************************************** ETKCS311
00135                                                                   ETKCS311
00136          10  INTER-APPL-COMM-AREA PIC X(200).                     ETKCS311
00137      COPY ETWS300.                                                   CL*14
00138 ***************************************************************** ETKCS311
00139 *    SPECIFIC COMMMAREA FOR POKCS035.                             ETKCS311
00140 ***************************************************************** ETKCS311
00141          10  ASC-SPECIFIC-COMMAREA.                               ETKCS311
00142              15  ASC-INT-APPL-FORMAT-IND   PIC  X(01).            ETKCS311
00143              15  ASC-USE-SELECTION-QUEUE-IND                      ETKCS311
00144                                            PIC  X(01).            ETKCS311
00145                  88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'. ETKCS311
00146              15  ASC-SEL-QUEUE-NAME        PIC  X(08).            ETKCS311
00147              15  ASC-NUMBER-OF-TS-ITEMS    PIC S9(05)  COMP-3.    ETKCS311
00148              15  ASC-NUMBER-OF-SELECTED-ITEMS                     ETKCS311
00149                                            PIC S9(09)  COMP-3.    ETKCS311
00150              15  ASC-SEL-SUB               PIC S9(04)  COMP.      ETKCS311
00151              15  ASC-SEL-ITEM              PIC S9(04)  COMP.      ETKCS311
00152              15  ASC-KEY-FIELDS.                                  ETKCS311
00153                  20  ASC-KEY-TP-DKEY       PIC S9(09)  COMP.      ETKCS311
00154                  20  ASC-KEY-FUNC-CDE      PIC X(02).             ETKCS311
00155                  20  ASC-KEY-DIR-IND       PIC X(01).             ETKCS311
00156              15  ASC-SCREEN-FIELDS.                               ETKCS311
00157                  20  ASC-ENT-ID                    PIC  9(09).       CL**6
00158                  20  ASC-VENDOR-NAME               PIC  X(30).       CL**6
00159                  20  ASC-FUNC-NME                  PIC  X(25).       CL**6
00160                  20  ASC-VEND-INCHG-QUAL           PIC  X(02).       CL**2
00161                  20  ASC-VEND-INCHG-ID             PIC  X(15).    ETKCS311
00162                  20  ASC-NETWK-ID                  PIC  X(01).    ETKCS311
TM0320                 20  ASC-NRFC-ID                   PIC  X(01).       CL**3
00163                  20  ASC-NETWORK-NAME              PIC  X(15).    ETKCS311
00164                  20  ASC-LAST-CNTL-NBR             PIC  9(09).    ETKCS311
00165                  20  ASC-CNTL-NBR-METH-CDE         PIC  X(01).    ETKCS311
00166                  20  ASC-TRK-IND                   PIC  X(01).    ETKCS311
00167                  20  ASC-OVRRD-CNTL-NBR            PIC  9(09).    ETKCS311
00168                  20  ASC-OVRRD-CNTL-NBR-IND        PIC  X(01).    ETKCS311
00169                  20  ASC-LAST-CHG-DTE              PIC  X(10).       CL**6
00170                  20  ASC-LAST-CHG-UID              PIC  X(08).       CL**6
00171 /                                                                 ETKCS311
00172 *----------------------------------------------------------------*ETKCS311
00173 *    ABEND PROCESSING COPYBOOK.                                  *ETKCS311
00174 *----------------------------------------------------------------*ETKCS311
00175      COPY DPWS013.                                                ETKCS311
00176                                                                   ETKCS311
00177 /                                                                 ETKCS311
00178                                                                   ETKCS311
00179 *    DB2 AREA FOR FUNCTIONAL GROUPS                               ETKCS311
00180                                                                   ETKCS311
00181      EXEC SQL                                                     ETKCS311
00182           INCLUDE TTRDFNC                                         ETKCS311
00183      END-EXEC.                                                    ETKCS311
00184                                                                   ETKCS311
00185 *    DB2 AREA FOR FUNC_CDE DESCRIPTIONS                           ETKCS311
00186                                                                   ETKCS311
00187      EXEC SQL                                                     ETKCS311
00188           INCLUDE TFCNGRP                                            CL**2
00189      END-EXEC.                                                    ETKCS311
00190 *    DB2 AREA FOR COMMUNICATIONS                                  ETKCS311
00191                                                                   ETKCS311
00192      EXEC SQL                                                     ETKCS311
00193           INCLUDE SQLCA                                           ETKCS311
00194      END-EXEC.                                                    ETKCS311
00195                                                                   ETKCS311
00196 /                                                                 ETKCS311
00197  LINKAGE SECTION.                                                 ETKCS311
00198                                                                   ETKCS311
00199  01  DFHCOMMAREA.                                                 ETKCS311
00200      05  FILLER                         OCCURS  1 TO 4072 TIMES   ETKCS311
00201                                         DEPENDING ON EIBCALEN.    ETKCS311
00202          10  FILLER                     PIC X.                    ETKCS311
00203                                                                   ETKCS311
00204 /                                                                 ETKCS311
00205  PROCEDURE DIVISION.                                              ETKCS311
00206 ***************************************************************** ETKCS311
00207 ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. ** ETKCS311
00208 ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  ** ETKCS311
00209 ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            ** ETKCS311
00210 **                                                             ** ETKCS311
00211 ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  ** ETKCS311
00212 ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       ** ETKCS311
00213 ***************************************************************** ETKCS311
00214  0000-MAIN-MODULE.                                                ETKCS311
00215      INITIALIZE DP030-CICS-API-FIELDS.                            ETKCS311
00216      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       ETKCS311
00217      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          ETKCS311
00218      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         ETKCS311
00219      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       ETKCS311
00220      MOVE LENGTH OF ET311AI        TO DP030-MAP-LENGTH (1).       ETKCS311
00221      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     ETKCS311
00222      PERFORM 0001-CALL-CICS-ARCH-API.                             ETKCS311
00223 *                                                                 ETKCS311
00224      PERFORM 1000-CONTROL-PROCESSING                              ETKCS311
00225 *                                                                 ETKCS311
00226      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     ETKCS311
00227      PERFORM 0001-CALL-CICS-ARCH-API.                             ETKCS311
00228                                                                   ETKCS311
00229 /                                                                 ETKCS311
00230  0001-CALL-CICS-ARCH-API.                                         ETKCS311
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                      ETKCS311
00232                                     DFHCOMMAREA                   ETKCS311
00233                                     DP030-CICS-API-FIELDS         ETKCS311
00234                                     DP020-STANDARD-COMMAREA       ETKCS311
00235                                     ET311AI.                      ETKCS311
00236 *                                                                 ETKCS311
00237      IF  DP030-RC-CALL-SUCCESSFUL                                 ETKCS311
00238          CONTINUE                                                 ETKCS311
00239      ELSE                                                         ETKCS311
00240          SET DP013-NO-ROLLBACK                                    ETKCS311
00241              DP013-XCTL-DISPLAY-RESTART                           ETKCS311
00242              DP013-CICS-ABEND      TO TRUE                        ETKCS311
00243          MOVE 'BEFORE 0000-MAIN-MODULE'                           ETKCS311
00244                                    TO DP013-PARAGRAPH             ETKCS311
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE OETKCS311
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)      ETKCS311
00247          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)      ETKCS311
00248          PERFORM DP013-0000-PROCESS-ABEND                         ETKCS311
00249      END-IF.                                                      ETKCS311
00250                                                                   ETKCS311
00251 /                                                                 ETKCS311
00252 *----------------------------------------------------------------*ETKCS311
00253 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *ETKCS311
00254 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *ETKCS311
00255 *----------------------------------------------------------------*ETKCS311
00256                                                                   ETKCS311
00257  1000-CONTROL-PROCESSING.                                         ETKCS311
00258                                                                   ETKCS311
00259      EVALUATE TRUE                                                ETKCS311
00260          WHEN DP020-NEXT-ACT-INITIAL                              ETKCS311
00261               PERFORM 1100-PROCESS-INTER-APPL-COMM                ETKCS311
00262               IF  PS-COMMAREA-NOT-VALID                           ETKCS311
00263                   CONTINUE                                        ETKCS311
00264               ELSE                                                ETKCS311
00265                   PERFORM 4000-BUILD-INITIAL-PANEL                ETKCS311
00266               END-IF                                              ETKCS311
00267                                                                   ETKCS311
00268          WHEN DP020-NEXT-ACT-READ-MAP                             ETKCS311
00269              PERFORM 2000-PROCESS-PANEL                           ETKCS311
00270                                                                   ETKCS311
00271          WHEN DP020-NEXT-ACT-RETURN                               ETKCS311
00272              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 ETKCS311
00273                                                                   ETKCS311
00274          WHEN OTHER                                               ETKCS311
00275              SET DP013-LOGIC-ABEND                                ETKCS311
00276                  DP013-NO-ROLLBACK TO TRUE                        ETKCS311
00277              MOVE '1000-CONTROL-PROCESSING'                       ETKCS311
00278                                    TO DP013-PARAGRAPH             ETKCS311
00279              MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   ETKCS311
00280                                    TO DP013-MESSAGE-TEXT(1)       ETKCS311
00281              MOVE DP020-NEXT-APPL-ACTIVITY                        ETKCS311
00282                                    TO DP013-MESSAGE-TEXT(2)       ETKCS311
00283              PERFORM DP013-0000-PROCESS-ABEND                     ETKCS311
00284      END-EVALUATE.                                                ETKCS311
00285                                                                   ETKCS311
00286 /                                                                 ETKCS311
00287 *----------------------------------------------------------------*ETKCS311
00288 *    DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE  *ETKCS311
00289 *    INTER-APPLICATION FORMAT CODE.                              *ETKCS311
00290 *----------------------------------------------------------------*ETKCS311
00291  1100-PROCESS-INTER-APPL-COMM.                                    ETKCS311
00292                                                                   ETKCS311
00293      SET PS-COMMAREA-VALID            TO TRUE.                    ETKCS311
00294                                                                   ETKCS311
00295      INITIALIZE ASC-SPECIFIC-COMMAREA.                            ETKCS311
00296                                                                   ETKCS311
00297      EVALUATE DP020-INT-APPL-FORMAT-IND                           ETKCS311
00298          WHEN PC-INTER-APPL-CRIT-SPEC                             ETKCS311
00299              MOVE ET300-TP-DKEY           TO                      ETKCS311
00300                   ASC-KEY-TP-DKEY                                 ETKCS311
00301              MOVE ET300-FUNC-CDE          TO                      ETKCS311
00302                   ASC-KEY-FUNC-CDE                                ETKCS311
00303              MOVE ET300-DIR-IND           TO                      ETKCS311
00304                   ASC-KEY-DIR-IND                                 ETKCS311
00305              MOVE ET300-ENT-ID            TO                      ETKCS311
00306                   ASC-ENT-ID                                      ETKCS311
00307              MOVE ET300-VENDOR-NAME       TO                      ETKCS311
00308                   ASC-VENDOR-NAME                                 ETKCS311
00309              MOVE ET300-VENDOR-NAME       TO                      ETKCS311
00310                   ASC-VENDOR-NAME                                 ETKCS311
00311              MOVE ET300-VEND-INCHG-QUAL   TO                      ETKCS311
00312                   ASC-VEND-INCHG-QUAL                             ETKCS311
00313              MOVE ET300-VEND-INCHG-ID     TO                      ETKCS311
00314                   ASC-VEND-INCHG-ID                               ETKCS311
00315              MOVE ET300-NETWK-ID          TO                      ETKCS311
00316                   ASC-NETWK-ID                                    ETKCS311
00317              MOVE ET300-NETWORK-NAME      TO                      ETKCS311
00318                   ASC-NETWORK-NAME                                   CL**6
00319          WHEN PC-INTER-APPL-LIST                                  ETKCS311
00320              SET ASC-USE-SELECTION-QUEUE                          ETKCS311
00321                                  TO TRUE                          ETKCS311
00322              MOVE ET300-NBR-SELECTED-ITEMS                        ETKCS311
00323                                   TO ASC-NUMBER-OF-SELECTED-ITEMS ETKCS311
00324              MOVE ET300-NUMBER-OF-TS-ITEMS                        ETKCS311
00325                                   TO ASC-NUMBER-OF-TS-ITEMS       ETKCS311
00326              MOVE ET300-SEL-QUEUE-NAME                            ETKCS311
00327                                   TO ASC-SEL-QUEUE-NAME              CL**6
00328              PERFORM 1120-PREPARE-LIST-ENTRY                      ETKCS311
00329                                                                   ETKCS311
00330          WHEN OTHER                                               ETKCS311
00331              MOVE PC-TSYMSG-00148 TO DP020-MSG-NUMBER             ETKCS311
00332              SET PS-ERROR                                         ETKCS311
00333                  PS-COMMAREA-NOT-VALID                            ETKCS311
00334                  DP020-MSG-FATAL                                  ETKCS311
00335                  DP020-NEXT-ACT-APPL-ERROR                        ETKCS311
00336                                  TO TRUE                          ETKCS311
00337      END-EVALUATE.                                                ETKCS311
00338                                                                   ETKCS311
00339 /                                                                 ETKCS311
00340 *----------------------------------------------------------------*ETKCS311
00341 * ENTRY IS FROM A LIST.  GET THE PO LIENENUMBER FROM THE TEMP    *ETKCS311
00342 * STORAGE QUEUE PASSED FROM THE LIST.                            *ETKCS311
00343 *----------------------------------------------------------------*ETKCS311
00344  1120-PREPARE-LIST-ENTRY.                                         ETKCS311
00345      MOVE ET301-MAX-ITEMS        TO ASC-SEL-SUB.                     CL**8
00346      MOVE ZERO                   TO ASC-SEL-ITEM.                 ETKCS311
00347      PERFORM 2110-GET-NEXT-REC.                                   ETKCS311
00348 /                                                                 ETKCS311
00349 *----------------------------------------------------------------*ETKCS311
00350 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *ETKCS311
00351 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *ETKCS311
00352 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *ETKCS311
00353 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *ETKCS311
00354 * FUNCTION KEYS CAN GET THIS FAR.                                *ETKCS311
00355 *----------------------------------------------------------------*ETKCS311
00356                                                                   ETKCS311
00357  2000-PROCESS-PANEL.                                              ETKCS311
00358                                                                   ETKCS311
00359      EVALUATE TRUE                                                ETKCS311
00360                                                                   ETKCS311
00361          WHEN DP020-SRC-AID = DP016-CLEAR                         ETKCS311
00362              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 ETKCS311
00363                                                                   ETKCS311
00364          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     ETKCS311
00365              SET DP030-DONT-ERASE                                 ETKCS311
00366                  DP030-SEND-DATAONLY (PC-APPL-MAP)                ETKCS311
00367                                    TO TRUE                        ETKCS311
00368              PERFORM 4000-BUILD-INITIAL-PANEL                     ETKCS311
00369                                                                   ETKCS311
00370          WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)             ETKCS311
00371              IF  ASC-USE-SELECTION-QUEUE                          ETKCS311
00372                  PERFORM 2110-GET-NEXT-REC                        ETKCS311
00373                  IF PS-LIST-END                                   ETKCS311
00374                     SET PS-LIST-NOT-END TO TRUE                   ETKCS311
00375                  ELSE                                             ETKCS311
00376                     PERFORM 4000-BUILD-INITIAL-PANEL              ETKCS311
00377                  END-IF                                           ETKCS311
00378              ELSE                                                 ETKCS311
00379 *                --- BOTTOM OF LIST ---                           ETKCS311
00380                  MOVE PC-TSYMSG-00070                             ETKCS311
00381                                    TO DP020-MSG-NUMBER            ETKCS311
00382                  SET DP020-MSG-INFORMATIONAL                      ETKCS311
00383                      PS-LIST-END   TO TRUE                        ETKCS311
00384              END-IF                                               ETKCS311
00385                                                                   ETKCS311
00386          WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)             ETKCS311
00387              IF  ASC-USE-SELECTION-QUEUE                          ETKCS311
00388                  PERFORM 2120-GET-PREV-REC                        ETKCS311
00389                  IF PS-LIST-END                                   ETKCS311
00390                     SET PS-LIST-NOT-END TO TRUE                   ETKCS311
00391                  ELSE                                             ETKCS311
00392                     PERFORM 4000-BUILD-INITIAL-PANEL              ETKCS311
00393                END-IF                                             ETKCS311
00394              ELSE                                                 ETKCS311
00395 *                --- TOP OF LIST ---                              ETKCS311
00396                  MOVE PC-TSYMSG-00069                             ETKCS311
00397                                    TO DP020-MSG-NUMBER            ETKCS311
00398                  SET DP020-MSG-INFORMATIONAL                      ETKCS311
00399                      PS-LIST-END   TO TRUE                        ETKCS311
00400              END-IF                                               ETKCS311
00401                                                                   ETKCS311
00402          WHEN OTHER                                               ETKCS311
00403              PERFORM 2100-CHECK-FUNCTION-KEY                      ETKCS311
00404                                                                   ETKCS311
00405      END-EVALUATE.                                                ETKCS311
00406                                                                   ETKCS311
00407 /                                                                 ETKCS311
00408 *----------------------------------------------------------------*ETKCS311
00409 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED       *ETKCS311
00410 * FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED   *ETKCS311
00411 * FROM THE CICS ARCHITECTURE API.                                *ETKCS311
00412 *----------------------------------------------------------------*ETKCS311
00413                                                                   ETKCS311
00414  2100-CHECK-FUNCTION-KEY.                                         ETKCS311
00415                                                                   ETKCS311
00416      EVALUATE TRUE                                                ETKCS311
00417          WHEN DP020-SRC-AID = DP016-ENTER                         ETKCS311
00418              CONTINUE                                             ETKCS311
00419                                                                   ETKCS311
00420          WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                  ETKCS311
00421              INITIALIZE ET300-INTER-APPL-COMMAREA                 ETKCS311
00422              MOVE ASC-KEY-TP-DKEY     TO ET300-TP-DKEY            ETKCS311
00423              MOVE ASC-KEY-FUNC-CDE    TO ET300-FUNC-CDE           ETKCS311
00424              MOVE ASC-KEY-DIR-IND     TO ET300-DIR-IND            ETKCS311
00425              MOVE ASC-ENT-ID          TO ET300-ENT-ID             ETKCS311
00426              MOVE ASC-VENDOR-NAME     TO ET300-VENDOR-NAME        ETKCS311
00427              MOVE ASC-VEND-INCHG-ID   TO ET300-VEND-INCHG-ID      ETKCS311
00428              MOVE ASC-VEND-INCHG-QUAL TO ET300-VEND-INCHG-QUAL       CL**2
00429              MOVE ASC-NETWK-ID        TO ET300-NETWK-ID           ETKCS311
00430              MOVE ASC-NETWORK-NAME    TO ET300-NETWORK-NAME          CL**6
00431                                                                   ETKCS311
00432          WHEN OTHER                                               ETKCS311
00433              SET DP013-NO-ROLLBACK                                ETKCS311
00434                  DP013-XCTL-DISPLAY-RESTART                       ETKCS311
00435                  DP013-CICS-ABEND  TO TRUE                        ETKCS311
00436              MOVE '2000-PROCESS-PANEL'                            ETKCS311
00437                                    TO DP013-PARAGRAPH             ETKCS311
00438              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      ETKCS311
00439                                    TO DP013-MESSAGE-TEXT (1)      ETKCS311
00440              PERFORM DP013-0000-PROCESS-ABEND                     ETKCS311
00441                                                                   ETKCS311
00442      END-EVALUATE.                                                ETKCS311
00443                                                                   ETKCS311
00444 /                                                                 ETKCS311
00445  2110-GET-NEXT-REC.                                               ETKCS311
00446      ADD +1                        TO ASC-SEL-SUB.                ETKCS311
00447      IF  ASC-SEL-SUB > ET301-MAX-ITEMS                               CL**8
00448          ADD +1                    TO ASC-SEL-ITEM                ETKCS311
00449          MOVE +1                   TO ASC-SEL-SUB                 ETKCS311
00450      END-IF.                                                      ETKCS311
00451      IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                ETKCS311
00452          PERFORM 9100-READ-SEL-QUEUE                              ETKCS311
00453          IF  ET301-ITEM (ASC-SEL-SUB) > ZERO                         CL**8
00454              INITIALIZE ASC-SCREEN-FIELDS                         ETKCS311
00455              MOVE ET301-TP-DKEY     (ASC-SEL-SUB)                    CL**8
00456                                    TO ASC-KEY-TP-DKEY             ETKCS311
00457              MOVE ET301-FUNC-CDE    (ASC-SEL-SUB)                    CL**8
00458                                    TO ASC-KEY-FUNC-CDE            ETKCS311
00459              MOVE ET301-DIR-IND     (ASC-SEL-SUB)                    CL**8
00460                                    TO ASC-KEY-DIR-IND             ETKCS311
00461              MOVE ET301-ENT-ID   (ASC-SEL-SUB)                       CL**8
00462                                    TO ASC-ENT-ID                  ETKCS311
00463              MOVE ET301-VENDOR-NAME   (ASC-SEL-SUB)                  CL**8
00464                                    TO ASC-VENDOR-NAME             ETKCS311
00465              MOVE ET301-VEND-INCHG-ID (ASC-SEL-SUB)                  CL**8
00466                                    TO ASC-VEND-INCHG-ID           ETKCS311
00467              MOVE ET301-VEND-INCHG-QUAL (ASC-SEL-SUB)                CL**8
00468                                    TO ASC-VEND-INCHG-QUAL            CL**2
00469              MOVE ET301-NETWK-ID        (ASC-SEL-SUB)                CL**8
00470                                    TO ASC-NETWK-ID                ETKCS311
00471              MOVE ET301-NETWK-NAME      (ASC-SEL-SUB)                CL**8
00472                                    TO ASC-NETWORK-NAME               CL**6
00473              SET ET301-INQ (ASC-SEL-SUB)                             CL**8
00474                                    TO TRUE                        ETKCS311
00475              PERFORM 9200-REWRITE-LIST-QUEUE                      ETKCS311
00476          ELSE                                                     ETKCS311
00477              SUBTRACT +1 FROM ASC-SEL-SUB                         ETKCS311
00478 *            --- BOTTOM OF LIST ---                               ETKCS311
00479              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER            ETKCS311
00480              SET DP020-MSG-INFORMATIONAL                          ETKCS311
00481                  PS-LIST-END       TO TRUE                        ETKCS311
00482          END-IF                                                   ETKCS311
00483      ELSE                                                         ETKCS311
00484          SUBTRACT +1 FROM ASC-SEL-ITEM                            ETKCS311
00485          MOVE ET301-MAX-ITEMS      TO ASC-SEL-SUB                    CL**8
00486          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER            ETKCS311
00487          SET DP020-MSG-INFORMATIONAL                              ETKCS311
00488              PS-LIST-END           TO TRUE                        ETKCS311
00489      END-IF.                                                      ETKCS311
00490                                                                   ETKCS311
00491 /                                                                 ETKCS311
00492  2120-GET-PREV-REC.                                               ETKCS311
00493      SUBTRACT +1 FROM ASC-SEL-SUB.                                ETKCS311
00494      IF  ASC-SEL-SUB < +1                                         ETKCS311
00495          SUBTRACT +1 FROM ASC-SEL-ITEM                            ETKCS311
00496          MOVE ET301-MAX-ITEMS      TO ASC-SEL-SUB                    CL**8
00497      END-IF.                                                      ETKCS311
00498 *                                                                 ETKCS311
00499      IF  ASC-SEL-ITEM > ZERO                                      ETKCS311
00500          PERFORM 9100-READ-SEL-QUEUE                              ETKCS311
00501          INITIALIZE ASC-SCREEN-FIELDS                             ETKCS311
00502          MOVE ET301-TP-DKEY     (ASC-SEL-SUB)                        CL**8
00503                                TO ASC-KEY-TP-DKEY                 ETKCS311
00504          MOVE ET301-FUNC-CDE    (ASC-SEL-SUB)                        CL**8
00505                                TO ASC-KEY-FUNC-CDE                ETKCS311
00506          MOVE ET301-DIR-IND     (ASC-SEL-SUB)                        CL**8
00507                                TO ASC-KEY-DIR-IND                 ETKCS311
00508          MOVE ET301-ENT-ID   (ASC-SEL-SUB)                           CL**8
00509                                TO ASC-ENT-ID                      ETKCS311
00510          MOVE ET301-VENDOR-NAME   (ASC-SEL-SUB)                      CL**8
00511                                TO ASC-VENDOR-NAME                 ETKCS311
00512          MOVE ET301-VEND-INCHG-ID (ASC-SEL-SUB)                      CL**8
00513                                TO ASC-VEND-INCHG-ID               ETKCS311
00514          MOVE ET301-VEND-INCHG-QUAL (ASC-SEL-SUB)                    CL**8
00515                                TO ASC-VEND-INCHG-QUAL                CL**2
00516          MOVE ET301-NETWK-ID        (ASC-SEL-SUB)                    CL**8
00517                                TO ASC-NETWK-ID                    ETKCS311
00518          MOVE ET301-NETWK-NAME      (ASC-SEL-SUB)                    CL**8
00519                                TO ASC-NETWORK-NAME                   CL**6
00520          SET ET301-INQ (ASC-SEL-SUB)                                 CL**8
00521                                TO TRUE                            ETKCS311
00522      ELSE                                                         ETKCS311
00523          MOVE +1                   TO ASC-SEL-ITEM                ETKCS311
00524                                       ASC-SEL-SUB                 ETKCS311
00525 *        --- TOP OF LIST ---                                      ETKCS311
00526          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            ETKCS311
00527          SET DP020-MSG-INFORMATIONAL                              ETKCS311
00528              PS-LIST-END           TO TRUE                        ETKCS311
00529      END-IF.                                                      ETKCS311
00530                                                                   ETKCS311
00531 /                                                                 ETKCS311
00532 *----------------------------------------------------------------*ETKCS311
00533 *    VALIDATE THE QUEUE WAS PASSED AND DISPLAY FOR THE FIRST     *ETKCS311
00534 *    ENTRY.                                                      *ETKCS311
00535 *----------------------------------------------------------------*ETKCS311
00536                                                                   ETKCS311
00537  4000-BUILD-INITIAL-PANEL.                                        ETKCS311
00538                                                                   ETKCS311
00539      SET DP030-SET-CURSOR-TRAILER                                 ETKCS311
00540          PS-NO-ERROR             TO TRUE.                         ETKCS311
00541                                                                   ETKCS311
00542      MOVE ASC-KEY-TP-DKEY        TO DK-TP-DKEY                    ETKCS311
00543      MOVE ASC-KEY-FUNC-CDE       TO DK-FUNC-CDE                   ETKCS311
00544      MOVE ASC-KEY-DIR-IND        TO DK-DIR-IND                    ETKCS311
00545                                                                      CL**2
00546      PERFORM 4010-GET-TTRDFNC.                                       CL**2
00547                                                                   ETKCS311
00548 /                                                                 ETKCS311
00549 *----------------------------------------------------------------*ETKCS311
00550 *  RETRIEVE THE ROW ASSOCIATED W/ THE DK- FIELDS                     CL**2
00551 *----------------------------------------------------------------*ETKCS311
00552                                                                   ETKCS311
00553  4010-GET-TTRDFNC.                                                   CL**2
00554                                                                   ETKCS311
00555      PERFORM                                                      ETKCS311
00556          WITH TEST AFTER                                          ETKCS311
00557          VARYING PV-RETRY-COUNTER                                 ETKCS311
00558          FROM 1 BY 1                                              ETKCS311
00559          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               ETKCS311
00560              OR (SQLCODE = +0)                                       CL**9
00561              OR (SQLCODE = +100))                                    CL**9
00562                                                                   ETKCS311
00563          EXEC SQL                                                 ETKCS311
00564               SELECT  LAST_CNTL_NBR                                  CL**2
00565                      ,CNTL_NBR_METH_CDE                              CL**2
00566                      ,OVRRD_CNTL_NBR                                 CL**2
00567                      ,OVRRD_CNTL_NBR_IND                             CL**2
00568                      ,TRK_IND                                        CL**2
00569                      ,CHAR(LAST_CHG_DTE, USA)                        CL**2
00570                      ,LAST_CHG_UID                                   CL**2
00571                 INTO  :TRDFNC-LAST-CNTL-NBR                          CL**3
00572                      ,:TRDFNC-CNTL-NBR-METH-CDE                      CL**2
00573                      ,:TRDFNC-OVRRD-CNTL-NBR                         CL**2
00574                      ,:TRDFNC-OVRRD-CNTL-NBR-IND                     CL**2
00575                      ,:TRDFNC-TRK-IND                                CL**2
00576                      ,:TRDFNC-LAST-CHG-DTE                           CL**2
00577                      ,:TRDFNC-LAST-CHG-UID                           CL**2
00578                 FROM TTRDFNC                                         CL**4
00579                WHERE TP_DKEY  = :DK-TP-DKEY                          CL**2
00580                  AND FUNC_CDE = :DK-FUNC-CDE                         CL**2
00581                  AND DIR_IND  = :DK-DIR-IND                          CL**2
00582          END-EXEC                                                 ETKCS311
00583                                                                   ETKCS311
00584          EVALUATE TRUE                                            ETKCS311
00585              WHEN SQLCODE = +0                                    ETKCS311
00586              WHEN SQLCODE = +100                                     CL**9
00587              WHEN SQLCODE = -904                                  ETKCS311
00588              WHEN SQLCODE = -913                                  ETKCS311
00589                   CONTINUE                                        ETKCS311
00590                                                                   ETKCS311
00591              WHEN SQLWARN0 NOT = SPACES                           ETKCS311
00592              WHEN SQLCODE < +0                                    ETKCS311
00593              WHEN SQLCODE > +0                                    ETKCS311
00594                   MOVE '4010-GET-TRDFNC'                             CL**2
00595                                   TO DP013-PARAGRAPH              ETKCS311
00596                   MOVE 'UNSUCCESSFUL SELECT TRDFNC'                  CL**2
00597                                   TO DP013-MESSAGE-TEXT (1)       ETKCS311
00598                   MOVE SQLCA      TO DP013-SQLCA                  ETKCS311
00599                   MOVE 'TRDFNC  ' TO DP013-DB2-TABLE-NAME (1)        CL**2
00600                   SET DP013-DB2-ABEND                             ETKCS311
00601                       DP013-XCTL-DISPLAY-RESTART                  ETKCS311
00602                                   TO TRUE                         ETKCS311
00603                   PERFORM DP013-0000-PROCESS-ABEND                ETKCS311
00604          END-EVALUATE                                             ETKCS311
00605      END-PERFORM.                                                 ETKCS311
00606                                                                   ETKCS311
00607      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        ETKCS311
00608          MOVE '4010-GET-TRDFNC'   TO DP013-PARAGRAPH                 CL**2
00609          MOVE 'MAX RETRIES EXCEEDED ON SELECT'                       CL**2
00610                                   TO DP013-MESSAGE-TEXT (1)       ETKCS311
00611          MOVE SQLCA               TO DP013-SQLCA                  ETKCS311
00612          MOVE 'TTRDFNC '          TO DP013-DB2-TABLE-NAME (1)        CL**2
00613          SET DP013-DB2-ABEND                                      ETKCS311
00614              DP013-XCTL-DISPLAY-RESTART                           ETKCS311
00615                                   TO TRUE                         ETKCS311
00616          PERFORM DP013-0000-PROCESS-ABEND                         ETKCS311
00617      END-IF.                                                      ETKCS311
00618                                                                   ETKCS311
00619      IF SQLCODE = +0                                                 CL**9
00620          PERFORM 4020-GET-FUNC-DESC                                  CL**9
00621                                                                      CL**9
00622          MOVE TRDFNC-LAST-CNTL-NBR      TO ASC-LAST-CNTL-NBR         CL**9
00623          MOVE TRDFNC-CNTL-NBR-METH-CDE  TO ASC-CNTL-NBR-METH-CDE     CL**9
00624          MOVE TRDFNC-OVRRD-CNTL-NBR     TO ASC-OVRRD-CNTL-NBR        CL**9
00625          MOVE TRDFNC-OVRRD-CNTL-NBR-IND TO ASC-OVRRD-CNTL-NBR-IND    CL**9
00626          MOVE TRDFNC-TRK-IND            TO ASC-TRK-IND               CL**9
00627          MOVE TRDFNC-LAST-CHG-DTE       TO ASC-LAST-CHG-DTE          CL**9
00628          MOVE TRDFNC-LAST-CHG-UID       TO ASC-LAST-CHG-UID          CL**9
00629          MOVE FCNGRP-FUNC-NME           TO ASC-FUNC-NME              CL**9
00630          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                        CL**9
00631      ELSE                                                            CL**9
00632          MOVE PC-TSYMSG-00148           TO DP020-MSG-NUMBER          CL**9
00633          SET PS-ERROR                                                CL**9
00634              DP020-MSG-FATAL                                         CL**9
00635              DP020-NEXT-ACT-APPL-ERROR                               CL**9
00636                              TO TRUE                                 CL**9
00637      END-IF.                                                         CL**9
00638 /                                                                 ETKCS311
00639 *----------------------------------------------------------------*ETKCS311
00640 * SELECT A ROW FROM THE DB2 TPURITM TABLE.                       *ETKCS311
00641 *----------------------------------------------------------------*ETKCS311
00642  4020-GET-FUNC-DESC.                                                 CL**2
00643                                                                      CL**2
00644      PERFORM                                                         CL**2
00645          WITH TEST AFTER                                             CL**2
00646          VARYING PV-RETRY-COUNTER                                    CL**2
00647          FROM 1 BY 1                                                 CL**2
00648          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                  CL**2
00649              OR (SQLCODE = +0))                                      CL**2
00650                                                                      CL**2
00651          EXEC SQL                                                    CL**2
00652               SELECT   FUNC_NME                                      CL**2
00653                 INTO  :FCNGRP-FUNC-NME                               CL**3
00654                 FROM TFCNGRP                                         CL**4
00655                WHERE FUNC_CDE = :DK-FUNC-CDE                         CL**2
00656          END-EXEC                                                    CL**2
00657                                                                      CL**2
00658          EVALUATE TRUE                                               CL**2
00659              WHEN SQLCODE = +0                                       CL**2
00660              WHEN SQLCODE = -904                                     CL**2
00661              WHEN SQLCODE = -913                                     CL**2
00662                   CONTINUE                                           CL**2
00663                                                                      CL**2
00664              WHEN SQLWARN0 NOT = SPACES                              CL**2
00665              WHEN SQLCODE < +0                                       CL**2
00666              WHEN SQLCODE > +0                                       CL**2
00667                   MOVE '4020-GET-FCNGRP'                             CL**4
00668                                   TO DP013-PARAGRAPH                 CL**2
00669                   MOVE 'UNSUCCESSFUL SELECT'                         CL**2
00670                                   TO DP013-MESSAGE-TEXT (1)          CL**2
00671                   MOVE SQLCA      TO DP013-SQLCA                     CL**2
00672                   MOVE 'TRDFNC  ' TO DP013-DB2-TABLE-NAME (1)        CL**2
00673                   SET DP013-DB2-ABEND                                CL**2
00674                       DP013-XCTL-DISPLAY-RESTART                     CL**2
00675                                   TO TRUE                            CL**2
00676                   PERFORM DP013-0000-PROCESS-ABEND                   CL**2
00677          END-EVALUATE                                                CL**2
00678      END-PERFORM.                                                    CL**2
00679                                                                      CL**2
00680      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                           CL**2
00681          MOVE '4020-GET-TFCNGRP'  TO DP013-PARAGRAPH                 CL**2
00682          MOVE 'MAX RETRIES EXCEEDED ON SELECT'                       CL**2
00683                                   TO DP013-MESSAGE-TEXT (1)          CL**2
00684          MOVE SQLCA               TO DP013-SQLCA                     CL**2
00685          MOVE 'TFCNGRP '          TO DP013-DB2-TABLE-NAME (1)        CL**2
00686          SET DP013-DB2-ABEND                                         CL**2
00687              DP013-XCTL-DISPLAY-RESTART                              CL**2
00688                                   TO TRUE                            CL**2
00689          PERFORM DP013-0000-PROCESS-ABEND                            CL**2
00690      END-IF.                                                         CL**2
00691                                                                      CL**2
00692 /                                                                 ETKCS311
00693 *----------------------------------------------------------------*ETKCS311
00694 *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *ETKCS311
00695 *----------------------------------------------------------------*ETKCS311
00696                                                                   ETKCS311
00697  4400-MOVE-COMMAREA-TO-SCREEN.                                    ETKCS311
00698                                                                   ETKCS311
00699      MOVE ASC-VENDOR-NAME                     TO VNDRNMEO            CL**2
00700      MOVE ASC-VEND-INCHG-QUAL                 TO VTPIDQO             CL**2
00701      MOVE ASC-VEND-INCHG-ID                   TO VTPIDO              CL**2
00702      MOVE ASC-NETWK-ID                        TO NTWKCDEO            CL**2
00703      MOVE ASC-NETWORK-NAME                    TO NTWKNMEO            CL**6
00704      MOVE ASC-KEY-FUNC-CDE                    TO FNCCDEO             CL**2
00705      MOVE ASC-KEY-DIR-IND                     TO DIRO                CL**2
00706      MOVE ASC-LAST-CNTL-NBR                   TO CTLNBRO             CL**2
00707      MOVE ASC-CNTL-NBR-METH-CDE               TO MTHDO               CL**2
00708      MOVE ASC-OVRRD-CNTL-NBR                  TO OVRDNBRO            CL**2
00709      MOVE ASC-OVRRD-CNTL-NBR-IND              TO OVRDINDO            CL**2
00710      MOVE ASC-TRK-IND                         TO DOCO                CL**2
00711      IF ASC-LAST-CHG-DTE = PC-NULL-DATE                              CL*13
00712          MOVE SPACE                           TO LCHGDTEO            CL*13
00713      ELSE                                                            CL*13
00714          MOVE ASC-LAST-CHG-DTE                TO LCHGDTEO            CL*13
00715      END-IF.                                                         CL*13
00716      MOVE ASC-LAST-CHG-UID                    TO LCHGUIDO            CL**2
00717      MOVE ASC-FUNC-NME                        TO FNCNMEO             CL**2
00718                                                                   ETKCS311
00719      EVALUATE ASC-CNTL-NBR-METH-CDE                                  CL**2
00720          WHEN '0'                                                    CL**2
00721              MOVE 'NOTRK'                     TO MTHDTXTO            CL**2
00722          WHEN '1'                                                    CL**2
00723              MOVE 'TRACK'                     TO MTHDTXTO            CL**2
00724      END-EVALUATE.                                                   CL**2
00725                                                                      CL**2
00726      EVALUATE TRUE                                                   CL**2
00727          WHEN ASC-KEY-DIR-IND = 'S'                                  CL**2
00728              IF ASC-TRK-IND = '1'                                    CL**2
00729                  MOVE 'TRACK'                 TO DOCTXTO             CL**2
00730              ELSE                                                    CL**2
00731                  MOVE 'NOTRK'                 TO DOCTXTO             CL**2
00732              END-IF                                                  CL**2
00733          WHEN ASC-KEY-DIR-IND = 'R'                                  CL**2
00734              EVALUATE TRUE                                           CL**7
00735                  WHEN ASC-TRK-IND = '0'                              CL**7
00736                      MOVE 'NOACK'             TO DOCTXTO             CL**7
00737                  WHEN ASC-TRK-IND = 'R'                              CL**7
00738                      MOVE 'ACKREJ'            TO DOCTXTO             CL**7
00739                  WHEN ASC-TRK-IND = 'E'                              CL**7
00740                      MOVE 'ACKERR'            TO DOCTXTO             CL**7
00741              END-EVALUATE                                            CL**7
00742      END-EVALUATE.                                                   CL**2
00743 /                                                                 ETKCS311
00744 *----------------------------------------------------------------*ETKCS311
00745 * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *ETKCS311
00746 *----------------------------------------------------------------*ETKCS311
00747                                                                   ETKCS311
00748  9100-READ-SEL-QUEUE.                                             ETKCS311
00749      EXEC CICS                                                    ETKCS311
00750          READQ TS                                                 ETKCS311
00751              QUEUE (ASC-SEL-QUEUE-NAME)                           ETKCS311
00752              INTO  (ET301-SELECTED-VNDRS)                            CL**8
00753              ITEM  (ASC-SEL-ITEM)                                 ETKCS311
00754              RESP  (PV-CICS-RESPONSE)                             ETKCS311
00755      END-EXEC.                                                    ETKCS311
00756                                                                   ETKCS311
00757      IF  PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                   ETKCS311
00758          CONTINUE                                                 ETKCS311
00759      ELSE                                                         ETKCS311
00760         IF  PV-CICS-RESPONSE EQUAL DFHRESP(QIDERR)                ETKCS311
00761             MOVE PC-TSYMSG-00057 TO DP020-MSG-NUMBER              ETKCS311
00762             SET DP020-MSG-WARNING TO TRUE                         ETKCS311
00763             SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                 ETKCS311
00764         ELSE                                                      ETKCS311
00765          SET DP013-LOGIC-ABEND                                    ETKCS311
00766              DP013-NO-ROLLBACK     TO TRUE                        ETKCS311
00767          MOVE '9100-READ-SEL-QUEUE'                               ETKCS311
00768                                    TO DP013-PARAGRAPH             ETKCS311
00769          MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'   ETKCS311
00770                                    TO DP013-MESSAGE-TEXT (1)      ETKCS311
00771          PERFORM DP013-0000-PROCESS-ABEND                         ETKCS311
00772         END-IF                                                    ETKCS311
00773      END-IF.                                                      ETKCS311
00774                                                                   ETKCS311
00775 /                                                                 ETKCS311
00776 *----------------------------------------------------------------*ETKCS311
00777 * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *ETKCS311
00778 *----------------------------------------------------------------*ETKCS311
00779  9200-REWRITE-LIST-QUEUE.                                         ETKCS311
00780                                                                   ETKCS311
00781      EXEC CICS                                                    ETKCS311
00782           WRITEQ TS                                               ETKCS311
00783               QUEUE  (ASC-SEL-QUEUE-NAME)                         ETKCS311
00784               FROM (ET301-SELECTED-VNDRS)                            CL**8
00785               ITEM   (ASC-SEL-ITEM)                               ETKCS311
00786               RESP   (PV-CICS-RESPONSE)                           ETKCS311
00787               REWRITE                                             ETKCS311
00788      END-EXEC.                                                    ETKCS311
00789                                                                   ETKCS311
00790      IF  PV-CICS-RESPONSE NOT EQUAL DFHRESP(NORMAL)               ETKCS311
00791          SET DP013-CICS-ABEND                                     ETKCS311
00792              DP013-NO-ROLLBACK     TO TRUE                        ETKCS311
00793          MOVE '9200-REWRITE-LIST-QUEUE'                           ETKCS311
00794                                    TO DP013-PARAGRAPH             ETKCS311
00795          MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'   ETKCS311
00796                                    TO DP013-MESSAGE-TEXT (1)      ETKCS311
00797          PERFORM DP013-0000-PROCESS-ABEND                         ETKCS311
00798      END-IF.                                                      ETKCS311
00799                                                                   ETKCS311
00800 *----------------------------------------------------------------*ETKCS311
00801 *    ABEND PROCESSOR MODULE                                      *ETKCS311
00802 *----------------------------------------------------------------*ETKCS311
00803                                                                   ETKCS311
00804      COPY DPPD013.                                                ETKCS311
