       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUT055.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            12/06/2001.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  STORE-TO-STORE TRANSFER SKU RECEIPT CREATION                  *        
      *                                                                *        
      *       THIS PROGRAM IS A REWRITE OF A DYL280 PROGRAM, FSKUT055, *        
      *  ORIGINALLY WRITTEN BY WILLIAM B. BURT ON 12-14-1988.          *        
      *                                                                *        
      *       THIS PROGRAM SEQUENTIALLY PROCESSES STORE TO STORE       *        
      *  TRANSFER RECORDS.  A SKU RECEIPT MAINTENANCE RECORD IS        *        
      *  GENERATED FOR EACH THE SENDING AND RECEIVING STORE FOR THE    *        
      *  TRANSFER.  THE ONLY DIFFERENCE BETWEEN THE TWO IS THAT THE    *        
      *  'FROM' (SENDING) STORE HAS AN ACTION CODE OF '-' AND THE      *        
      *  'TO' (RECEIVING) STORE HAS A '+' AND NATURALLY THE STORE      *        
      *  NUMBERS ARE DIFFERENT.  CONTROL TOTALS ARE PRODUCED FOR       *        
      *  RECORDS READ AND GENERATED PER BATCH NUMBER, ALONG WITH FINAL *        
      *  TOTALS FOR BOTH.                                              *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * 23747    07-12-2002  DYL280 CONVERSION TO COBOL                *        
      * 21731    04-28-2003  FS/ML NONINTELLIGENT SKU                  *        
PM3403*PM3403    04-20-2012  LARGE RETAIL PROJECT MLRD019 RECOMPILE             
      *                                                                *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT VALID-TRANSFER-FILE                                           
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT SKU-RECEIPT-MAINT-FILE                                        
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT SKU-RECEIPT-REPORT                                            
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  VALID-TRANSFER-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VALID-TRANSFER-REC              PIC X(80).                           
                                                                                
       FD  SKU-RECEIPT-MAINT-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
21731      RECORD CONTAINS 200 CHARACTERS                                       
           DATA RECORD IS SKU-RECEIPT-MAINT-REC.                                
21731  01  SKU-RECEIPT-MAINT-REC           PIC X(200).                          
                                                                                
       FD  SKU-RECEIPT-REPORT                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE                     PIC X(132).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  HEADER LINE                                                            
      ******************************************************************        
      *01  DP132O-STANDRD-HEADER-132-COLS.                                      
      *01  01  DP132O-CNFDL-HDR-132-COLS.                                       
           COPY DPWS132O.                                                       
                                                                                
       01  RP-REPORT-TITLE-1.                                                   
           05  FILLER                      PIC X(48)       VALUE SPACES.        
           05  RP-REPORT-TITLE             PIC X(34)       VALUE                
               'SKU MAINTENANCE RECEIPTS GENERATED'.                            
                                                                                
       01  RP-REPORT-HEADER-1.                                                  
           05  FILLER                      PIC X(40)       VALUE SPACES.        
           05  FILLER                      PIC X(06)       VALUE                
               'BATCH '.                                                        
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(08)       VALUE                
               ' RECORD '.                                                      
           05  FILLER                      PIC X(07)       VALUE SPACES.        
           05  FILLER                      PIC X(08)       VALUE                
               ' RECORD '.                                                      
           05  FILLER                      PIC X(06)       VALUE SPACES.        
           05  FILLER                      PIC X(09)       VALUE                
               'TRANSFER '.                                                     
           05  FILLER                      PIC X(39)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-2.                                                  
           05  FILLER                      PIC X(40)       VALUE SPACES.        
           05  FILLER                      PIC X(06)       VALUE                
               'NUMBER'.                                                        
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(08)       VALUE                
               '   READ '.                                                      
           05  FILLER                      PIC X(05)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
               'GENERATED '.                                                    
           05  FILLER                      PIC X(07)       VALUE SPACES.        
           05  FILLER                      PIC X(08)       VALUE                
               '  UNITS '.                                                      
           05  FILLER                      PIC X(39)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-3.                                                  
           05  FILLER                      PIC X(40)       VALUE SPACES.        
           05  FILLER                      PIC X(06)       VALUE                
                                                           ALL '-'.             
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(08)       VALUE                
                                                           ALL '-'.             
           05  FILLER                      PIC X(05)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
                                                           ALL '-'.             
           05  FILLER                      PIC X(07)       VALUE SPACES.        
           05  FILLER                      PIC X(08)       VALUE                
                                                           ALL '-'.             
           05  FILLER                      PIC X(39)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-DETAIL.                                                    
           05  FILLER                      PIC X(40)       VALUE SPACES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-BATCH-NUMBER             PIC 9(04)       VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  RP-RECORD-READ              PIC ZZZ,ZZ9-    VALUE ZEROES.        
           05  FILLER                      PIC X(07)       VALUE SPACES.        
           05  RP-RECORD-GENERATED         PIC ZZZ,ZZ9-    VALUE ZEROES.        
           05  FILLER                      PIC X(07)       VALUE SPACES.        
           05  RP-UNITS-TRANSFERED         PIC ZZZ,ZZ9-    VALUE ZEROES.        
           05  FILLER                      PIC X(39)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-TOTAL.                                                     
           05  FILLER                      PIC X(40)       VALUE SPACES.        
           05  FILLER                      PIC X(12)       VALUE                
               'FINAL TOTAL '.                                                  
           05  RP-TOTAL-READ               PIC ZZ,ZZZ,ZZ9- VALUE ZEROES.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  RP-TOTAL-GENERATED          PIC ZZ,ZZZ,ZZ9- VALUE ZEROES.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  RP-TOTAL-UNITS-TRANSFERED   PIC ZZ,ZZZ,ZZ9- VALUE ZEROES.        
           05  FILLER                      PIC X(39)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-END.                                                       
           05  FILLER                      PIC X(46)       VALUE SPACES.        
           05  FILLER                      PIC X(39)       VALUE                
               '*****  E N D   O F   R E P O R T  *****'.                       
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-PRINT-LINE-MAX           PIC S9(04)      VALUE +55            
                               COMP.                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-VALID-TRANSFER    PIC X           VALUE 'N'.           
               88  END-OF-VALID-TRANSFER                   VALUE 'Y'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4003                                  VALUE +4003.         
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-SKU-X                    PIC X(08)       VALUE SPACES.        
           05  PV-SKU-9 REDEFINES PV-SKU-X PIC 9(08).                           
           05  PV-RECORD-READ              PIC S9(06)      VALUE ZEROES         
                               COMP.                                            
           05  PV-RECORD-GENERATED         PIC S9(06)      VALUE ZEROES         
                               COMP.                                            
           05  PV-UNITS-TRANSFERED         PIC S9(06)      VALUE ZEROES         
                               COMP.                                            
           05  PV-TOTAL-READ               PIC S9(08)      VALUE ZEROES         
                               COMP.                                            
           05  PV-TOTAL-GENERATED          PIC S9(08)      VALUE ZEROES         
                               COMP.                                            
           05  PV-TOTAL-UNITS-TRANSFERED   PIC S9(08)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PAGE-COUNT               PIC S9(05)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PRINT-LINE-COUNT         PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ADVANCE-LINE             PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '***  RECORD LAYOUT AREA BEGIN  ***'.                                
                                                                                
      ******************************************************************        
      *  VALID STORE-TO-STORE TRANSFER LAYOUT                                   
      ******************************************************************        
21731 *01  ML019-TRANSFER-RECORD.                                               
21731      COPY MLRD019.                                                        
                                                                                
      ******************************************************************        
      *  SKU MAINTENANCE RECORD LAYOUT                                          
      ******************************************************************        
21731 *01  FS035-ADJUSTMENT-RECORD.                                             
21731      COPY FSRD035.                                                        
                                                                                
      ******************************************************************        
      *  DB2 COMMUNICATION AREA                                                 
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  DB2 SKU CROSS-REFERENCE LAYOUT                                         
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE TSKXREF                                                 
           END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN RPOCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM Z100-READ-TRANSFER.                                          
21731      MOVE ML019-TRANSFER-BATCH-NBR-9 TO RP-BATCH-NUMBER.                  
           PERFORM B200-PROCESS-TRANSFER                                        
               UNTIL END-OF-VALID-TRANSFER.                                     
                                                                                
           PERFORM B400-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           MOVE PV-TODAYS-DATE-MM  TO DP132O-RUN-MONTH.                         
           MOVE PV-TODAYS-DATE-DD  TO DP132O-RUN-DAY.                           
           MOVE PV-TODAYS-DATE-YY  TO DP132O-RUN-YEAR.                          
           MOVE PV-TODAYS-TIME-HR  TO DP132O-RUN-HOUR.                          
           MOVE PV-TODAYS-TIME-MIN TO DP132O-RUN-MINUTE.                        
           MOVE 'MLKUT055'         TO DP132O-PROGRAM-NAME.                      
                                                                                
21731      MOVE SPACES TO FS035-ADJUSTMENT-RECORD.                              
                                                                                
           OPEN INPUT  VALID-TRANSFER-FILE                                      
                OUTPUT SKU-RECEIPT-MAINT-FILE                                   
                       SKU-RECEIPT-REPORT.                                      
                                                                                
      ******************************************************************        
      *    PROCESS VALID TRANSFER RECORDS                                       
      ******************************************************************        
       B200-PROCESS-TRANSFER.                                                   
                                                                                
           PERFORM Z300-SELECT-TSKXREF.                                         
                                                                                
           ADD 1                     TO PV-RECORD-READ.                         
21731      ADD ML019-TRANSFER-QTY    TO PV-UNITS-TRANSFERED.                    
                                                                                
21731      MOVE 'MLKUT055'                   TO FS035-ADJ-SRC-PGM-NM.           
21731      MOVE SKXREF-ITM-NBR               TO FS035-ITM-NBR.                  
21731      SET  FS035-RCPT-ADJ               TO TRUE.                           
21731      MOVE RP-BATCH-NUMBER              TO FS035-BTCH-NBR.                 
21731                                                                           
21731      MOVE ML019-TRANSFER-FROM-STORE-9  TO FS035-LOC-NBR.                  
21731      SET  FS035-ACTN-NEG               TO TRUE.                           
21731      MOVE ML019-TRANSFER-QTY           TO FS035-ADJ-QTY.                  
                                                                                
           PERFORM Z200-WRITE-SKU-RECEIPT-MAINT.                                
                                                                                
21731      MOVE ML019-TRANSFER-TO-STORE-9    TO FS035-LOC-NBR.                  
21731      SET  FS035-ACTN-PLS               TO TRUE.                           
21731      MOVE ML019-TRANSFER-QTY           TO FS035-ADJ-QTY.                  
                                                                                
           PERFORM Z200-WRITE-SKU-RECEIPT-MAINT.                                
                                                                                
           PERFORM Z100-READ-TRANSFER.                                          
                                                                                
21731      IF RP-BATCH-NUMBER NOT = ML019-TRANSFER-BATCH-NBR-9                  
               PERFORM B300-PRODUCE-REPORT                                      
21731          MOVE ML019-TRANSFER-BATCH-NBR-9 TO RP-BATCH-NUMBER               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    PRODUCE THE SKU MAINTENANCE RECEIPTS GENERATED REPORT                
      ******************************************************************        
       B300-PRODUCE-REPORT.                                                     
                                                                                
           MOVE PV-RECORD-READ           TO RP-RECORD-READ.                     
           MOVE PV-RECORD-GENERATED      TO RP-RECORD-GENERATED.                
           MOVE PV-UNITS-TRANSFERED      TO RP-UNITS-TRANSFERED.                
                                                                                
           PERFORM Z500-PRINT-DETAILS.                                          
                                                                                
           ADD PV-RECORD-READ            TO PV-TOTAL-READ.                      
           ADD PV-RECORD-GENERATED       TO PV-TOTAL-GENERATED.                 
           ADD PV-UNITS-TRANSFERED       TO PV-TOTAL-UNITS-TRANSFERED.          
                                                                                
           MOVE ZEROES                   TO PV-RECORD-READ                      
                                            PV-RECORD-GENERATED                 
                                            PV-UNITS-TRANSFERED.                
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B400-FINALIZE.                                                           
                                                                                
           IF PV-RECORD-READ > 0 OR PV-RECORD-GENERATED > 0                     
               PERFORM B300-PRODUCE-REPORT                                      
           END-IF.                                                              
                                                                                
           MOVE PV-TOTAL-READ             TO RP-TOTAL-READ.                     
           MOVE PV-TOTAL-GENERATED        TO RP-TOTAL-GENERATED.                
           MOVE PV-TOTAL-UNITS-TRANSFERED TO RP-TOTAL-UNITS-TRANSFERED.         
                                                                                
           MOVE 2               TO PV-ADVANCE-LINE.                             
           MOVE RP-REPORT-TOTAL TO RP-REPORT-DETAIL.                            
           PERFORM Z500-PRINT-DETAILS.                                          
                                                                                
           MOVE 3               TO PV-ADVANCE-LINE.                             
           MOVE RP-REPORT-END   TO RP-REPORT-DETAIL.                            
           PERFORM Z500-PRINT-DETAILS.                                          
                                                                                
           CLOSE   VALID-TRANSFER-FILE                                          
                   SKU-RECEIPT-MAINT-FILE                                       
                   SKU-RECEIPT-REPORT.                                          
                                                                                
      ******************************************************************        
      *    READ THE NEXT RECORD FROM VALID TRANSFER FILE                        
      ******************************************************************        
       Z100-READ-TRANSFER.                                                      
                                                                                
21731      READ VALID-TRANSFER-FILE INTO ML019-TRANSFER-RECORD                  
               AT END SET END-OF-VALID-TRANSFER TO TRUE.                        
                                                                                
      ******************************************************************        
      *    WRITE SKU RECEIPT MAINTENANCE RECORD                                 
      ******************************************************************        
       Z200-WRITE-SKU-RECEIPT-MAINT.                                            
                                                                                
21731      WRITE SKU-RECEIPT-MAINT-REC FROM FS035-ADJUSTMENT-RECORD.            
                                                                                
           ADD 1 TO PV-RECORD-GENERATED.                                        
                                                                                
      ******************************************************************        
      *    GET SKU ATTRIBUTES FROM SKU CROSS REFERENCE                          
      ******************************************************************        
       Z300-SELECT-TSKXREF.                                                     
                                                                                
21731      MOVE ML019-SKU  TO PV-SKU-X.                                         
           MOVE PV-SKU-9   TO SKXREF-SKU.                                       
                                                                                
           EXEC SQL                                                             
21731          SELECT                                                           
21731                 ITM_NBR                                                   
21731            INTO                                                           
21731                 :SKXREF-ITM-NBR                                           
                 FROM TSKXREF                                                   
                WHERE SKU    = :SKXREF-SKU                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   DISPLAY ' XREF MISSING FOR SKU: ' PV-SKU-9                   
21731              MOVE ZEROES TO SKXREF-ITM-NBR                                
               WHEN OTHER                                                       
                   MOVE 'SELECT TSKXREF'  TO PV-OPER-ATTEMPTED                  
                   PERFORM Z999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    PRINT THE REPORT DETAILS                                             
      ******************************************************************        
       Z500-PRINT-DETAILS.                                                      
                                                                                
           IF PV-PRINT-LINE-COUNT > PC-PRINT-LINE-MAX                           
               OR PV-PRINT-LINE-COUNT = ZEROES                                  
               PERFORM Z600-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-DETAIL                              
               AFTER ADVANCING PV-ADVANCE-LINE LINES.                           
                                                                                
           ADD  PV-ADVANCE-LINE TO PV-PRINT-LINE-COUNT.                         
           MOVE 1               TO PV-ADVANCE-LINE.                             
           MOVE SPACES TO RP-REPORT-DETAIL.                                     
                                                                                
      ******************************************************************        
      *    PRINT THE REPORT HEADINGS                                            
      ******************************************************************        
       Z600-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1 TO PV-PAGE-COUNT.                                              
           MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                            
           MOVE 1             TO DP132O-REPORT-NUMBER.                          
           WRITE REPORT-LINE FROM DP132O-STANDRD-HEADER-132-COLS                
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-TITLE-1.                            
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-HEADER-1                            
               AFTER ADVANCING 2 LINES.                                         
           WRITE REPORT-LINE FROM RP-REPORT-HEADER-2.                           
           WRITE REPORT-LINE FROM RP-REPORT-HEADER-3.                           
                                                                                
           MOVE 7 TO PV-PRINT-LINE-COUNT.                                       
           MOVE 2 TO PV-ADVANCE-LINE.                                           
                                                                                
      ******************************************************************        
       Z999-SQL-ERROR.                                                          
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  MLKUT055'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
