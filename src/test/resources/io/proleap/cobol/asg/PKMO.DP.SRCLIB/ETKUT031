00001 *                                                                 02/03/95
00002 *                                                                 ETKUT031
00003 *                                                                    LV016
00004  IDENTIFICATION DIVISION.                                         ETKUT031
00005                                                                   ETKUT031
00006  PROGRAM-ID.              ETKUT031.                               ETKUT031
00007  AUTHOR.                  KIMBERLY WRIGHT.                           CL**4
00008  INSTALLATION.            KOHLS DEPARTMENT STORES.                ETKUT031
00009  DATE-WRITTEN.            01/26/95.                               ETKUT031
00010  DATE-COMPILED.                                                   ETKUT031
00011                                                                   ETKUT031
00012 ******************************************************************ETKUT031
00013 *     ETKUT031  -  HALLMARK ASN RECYCLE PROGRAM                   ETKUT031
00014 *                                                                 ETKUT031
00015 *     READ THE HALLMARK STATUS FILE AND COMPARE IT TO THE ASN        CL*11
00016 *     FILE TO DETERMINE IF THE ASN SHOULD BE RECYCLED OR PASSED      CL*11
00017 *     ON TO THE ASN PROCESSES.  IF THE ASN HAS BEEN RECYCLED FOR     CL*11
00018 *     MORE THAN 5 DAYS, THE ASN WILL BE SENT ON TO THE ASN TEAM      CL*11
00019 *     TO BE PROCESSED.  AN ASN WILL BE WRITTEN ONLY TO THE ASN       CL*11
00020 *     FILE OR THE HALLMARK RECYCLE ASN FILE (NOT BOTH).              CL*11
00020 *                                                                    CL*11
00020 *     7/26/99 - MRD - CHANGED ASN RECYCLE FOR HALLMARK FROM 3 TO     CL*11
00020 *               5 DAYS.                                              CL*11
00020 *                                                                    CL*11
      *     6/11/04 - VF  - CHANGED ASN RECYCLE FOR HALLMARK FROM 5 TO          
      *               12 DAYS.                                                  
      *                                                                         
00021 ******************************************************************ETKUT031
00022 *                                                                 ETKUT031
00023 *                                                                 ETKUT031
00024  ENVIRONMENT DIVISION.                                            ETKUT031
00025 *                                                                 ETKUT031
00026 *                                                                 ETKUT031
00027  CONFIGURATION SECTION.                                           ETKUT031
00028                                                                   ETKUT031
00029  SOURCE-COMPUTER. IBM-3090.                                       ETKUT031
00030  OBJECT-COMPUTER. IBM-3090.                                       ETKUT031
00031 *                                                                 ETKUT031
00032 *                                                                 ETKUT031
00033  INPUT-OUTPUT SECTION.                                            ETKUT031
00034                                                                   ETKUT031
00035  FILE-CONTROL.                                                    ETKUT031
00036                                                                   ETKUT031
00037      SELECT ET-ASN-DUNS-FILE                                      ETKUT031
00038          ASSIGN TO IN01.                                          ETKUT031
00039                                                                   ETKUT031
00040      SELECT ET-ASN-HALLMARK-STATUS-FILE                              CL**9
00041          ASSIGN TO IN02.                                          ETKUT031
00042                                                                   ETKUT031
00043      SELECT ET-ASN-DUNS-UPD-FILE                                  ETKUT031
00044          ASSIGN TO OUT01.                                         ETKUT031
00045                                                                   ETKUT031
00046      SELECT ET-ASN-HALLMARK-FILE                                  ETKUT031
00047          ASSIGN TO OUT02.                                         ETKUT031
00048 *                                                                 ETKUT031
00049 *                                                                 ETKUT031
00050  DATA DIVISION.                                                   ETKUT031
00051 *                                                                 ETKUT031
00052 *                                                                 ETKUT031
00053  FILE SECTION.                                                    ETKUT031
00054                                                                   ETKUT031
00055  FD  ET-ASN-DUNS-FILE                                             ETKUT031
00056      RECORDING MODE IS F                                          ETKUT031
00057      LABEL RECORDS ARE STANDARD                                   ETKUT031
00058      BLOCK CONTAINS 0 RECORDS                                     ETKUT031
00059      RECORD CONTAINS 350 CHARACTERS                               ETKUT031
00060      DATA RECORD IS ET-ASN-DUNS-RECORD.                           ETKUT031
00061                                                                   ETKUT031
00062  01  ET-ASN-DUNS-RECORD                 PIC X(350).               ETKUT031
00063                                                                   ETKUT031
00064  FD  ET-ASN-HALLMARK-STATUS-FILE                                     CL**8
00065      RECORDING MODE IS F                                          ETKUT031
00066      LABEL RECORDS ARE STANDARD                                   ETKUT031
00067      BLOCK CONTAINS 0 RECORDS                                     ETKUT031
00068      RECORD CONTAINS 57 CHARACTERS                                   CL**2
00069      DATA RECORD IS ET-ASN-HALLMARK-STATUS-REC.                      CL**8
00070                                                                   ETKUT031
00071  01  ET-ASN-HALLMARK-STATUS-REC         PIC X(57).                   CL**8
00072                                                                   ETKUT031
00073  FD  ET-ASN-DUNS-UPD-FILE                                            CL**2
00074      RECORDING MODE IS F                                          ETKUT031
00075      LABEL RECORDS ARE STANDARD                                   ETKUT031
00076      BLOCK CONTAINS 0 RECORDS                                     ETKUT031
00077      RECORD CONTAINS 350 CHARACTERS                                  CL**2
00078      DATA RECORD IS ET-ASN-DUNS-UPD-RECORD.                          CL**2
00079                                                                   ETKUT031
00080  01  ET-ASN-DUNS-UPD-RECORD             PIC X(350).                  CL**2
00081                                                                   ETKUT031
00082  FD  ET-ASN-HALLMARK-FILE                                            CL**2
00083      RECORDING MODE IS F                                          ETKUT031
00084      LABEL RECORDS ARE STANDARD                                   ETKUT031
00085      BLOCK CONTAINS 0 RECORDS                                     ETKUT031
00086      RECORD CONTAINS 350 CHARACTERS                                  CL**2
00087      DATA RECORD IS ET-ASN-HALLMARK-RECORD.                          CL**2
00088                                                                   ETKUT031
00089  01  ET-ASN-HALLMARK-RECORD             PIC X(350).                  CL**2
00090                                                                   ETKUT031
00091                                                                   ETKUT031
00092  EJECT                                                            ETKUT031
00093 *                                                                 ETKUT031
00094 *                                                                 ETKUT031
00095  WORKING-STORAGE SECTION.                                         ETKUT031
00096                                                                   ETKUT031
00097                                                                   ETKUT031
00098 ******************************************************************ETKUT031
00099 *                                                                    CL*13
00100 ******************************************************************ETKUT031
00101                                                                      CL*13
00102                                                                      CL*13
00103                                                                   ETKUT031
00104  01  WS-TODAYS-DATE.                                                 CL**4
00105      05  WS-YY                           PIC 9(02)   VALUE ZEROES.   CL**4
00106      05  WS-MO                           PIC 9(02)   VALUE ZEROES.   CL**4
00107      05  WS-DD                           PIC 9(02)   VALUE ZEROES.   CL**4
00108                                                                      CL**8
00109  01  WS-TODAYS-DATE-FORMAT.                                          CL*14
00110      05  WS-TODAYS-DATE-CC               PIC 9(02)   VALUE ZEROES.   CL*14
00111      05  WS-TODAYS-DATE-NUM              PIC 9(06)   VALUE ZEROES.   CL*14
00112                                                                      CL*14
00113  01  WS-RECYCLE-DATE-FORMAT.                                         CL*14
00114      05  WS-RECYCLE-DATE-CC              PIC 9(02)   VALUE ZEROES.   CL*14
00115      05  WS-RECYCLE-DATE                 PIC 9(06)   VALUE ZEROES.   CL*14
00116                                                                      CL**4
00117  01  WS-PO-NUM-DIG                       PIC S9(09)  COMP.        ETKUT031
00118                                                                   ETKUT031
00119  01  PS-PROGRAM-SWITCHES.                                         ETKUT031
00120      05  PS-END-OF-HALLMARK-SW           PIC X(01).                  CL**4
00121          88  PS-END-OF-HALLMARK-FILE        VALUE 'Y'.               CL**4
00122          88  PS-NOT-END-OF-HALLMARK-FILE    VALUE 'N'.               CL**4
00123      05  PS-END-OF-DUNS-SW               PIC X(01).                  CL**4
00124          88  PS-END-OF-DUNS-FILE            VALUE 'Y'.               CL**4
00125          88  PS-NOT-END-OF-DUNS-FILE        VALUE 'N'.               CL**4
00126      05  PS-HALLMARK-RECORD-SW           PIC X(01).                  CL**4
00127          88  PS-HALLMARK-ASN                VALUE 'Y'.               CL**4
00128          88  PS-NON-HALLMARK-ASN            VALUE 'N'.               CL**4
00129      05  PS-HALLMARK-RECYCLE-SW          PIC X(01).                  CL**4
00130          88  PS-GOOD-HALLMARK-ASN           VALUE 'Y'.               CL**4
00131          88  PS-RECYCLE-HALLMARK-ASN        VALUE 'N'.               CL**4
00132                                                                   ETKUT031
00133  01  PC-PROGRAM-CONSTANTS.                                        ETKUT031
00134      05  PC-IDENTIFICATION-SEGMENT       PIC X(03) VALUE 'ID '.   ETKUT031
00135      05  PC-HALLMARK-DUNS-NBR            PIC X(09)                   CL**4
00136                                             VALUE '003949435'.       CL**4
SP0409     05  PC-BEAUTY-BANK-DUNS-NBR         PIC X(09)                   CL**4
SP0409                                            VALUE '000882225'.       CL**4
00137      05  PC-CALENDAR-ROUTINE             PIC X(08)                   CL*13
00138                                             VALUE 'DPKUT500'.        CL*13
00139                                                                   ETKUT031
00140  01  ABEND-CODE                          PIC S9(04)  VALUE +0     ETKUT031
00141                                                      COMP SYNC.   ETKUT031
00142      88 ABEND-4013                                   VALUE +4013. ETKUT031
00143                                                                   ETKUT031
00144 ***************************************************************** ETKUT031
00145 *    CALENDAR ROUTINE WORKAREA                                       CL*13
00146 ***************************************************************** ETKUT031
00147      COPY DPWS500.                                                   CL*13
00148                                                                   ETKUT031
00149 *****************************************************************    CL*13
00150 *    ASN INPUT RECORD LAYOUT                                         CL*13
00151 *****************************************************************    CL*13
00152      COPY ETRD012.                                                   CL*16
00153                                                                      CL*13
00154 ***************************************************************** ETKUT031
00155 *    HALLMARK ASN STATUS RECORD LAYOUT                            ETKUT031
00156 ***************************************************************** ETKUT031
00157      COPY ETRD013.                                                   CL*16
00158                                                                   ETKUT031
00159 ***************************************************************** ETKUT031
00160 *    ASN DETAIL OUTPUT RECORD LAYOUT                              ETKUT031
00161 ***************************************************************** ETKUT031
00162      COPY SNRD001I.                                                  CL*16
00163                                                                   ETKUT031
00164 ***************************************************************** ETKUT031
00165 *  COPY BOOK OF WORKING STORAGE FOR SQL ABEND ROUTINE             ETKUT031
00166 ***************************************************************** ETKUT031
00167      COPY DPWS004.                                                ETKUT031
00168                                                                   ETKUT031
00169  EJECT                                                            ETKUT031
00170 *                                                                 ETKUT031
00171 *                                                                 ETKUT031
00172  LINKAGE SECTION.                                                 ETKUT031
00173 *                                                                 ETKUT031
00174 *                                                                 ETKUT031
00175  PROCEDURE DIVISION.                                              ETKUT031
00176                                                                   ETKUT031
00177  A100-MAIN-MODULE.                                                ETKUT031
00178 ******************************************************************ETKUT031
00179 *  OPEN FILES                                                     ETKUT031
00180 *  READ FIRST RECORD IN THE DUNNS FILE                               CL**3
00181 *  PROCESS EACH RECORD IN THE DUNNS FILE                             CL**3
00182 *  CLOSE THE FILES                                                ETKUT031
00183 ******************************************************************ETKUT031
00184 *    DISPLAY 'A100-MAIN-MODULE'.                                  ETKUT031
00185                                                                   ETKUT031
00186      OPEN INPUT  ET-ASN-DUNS-FILE                                    CL**2
00187                  ET-ASN-HALLMARK-STATUS-FILE                         CL**6
00188           OUTPUT ET-ASN-DUNS-UPD-FILE                                CL**2
00189                  ET-ASN-HALLMARK-FILE.                               CL**2
00190                                                                   ETKUT031
00191      ACCEPT WS-TODAYS-DATE                                           CL**4
00192          FROM DATE.                                                  CL**4
00193                                                                      CL**4
00194      PERFORM Z100-READ-ASN-DUNS-FILE.                                CL**3
00195                                                                   ETKUT031
00196      PERFORM B100-PROCESS-ASN-DUNS-FILE                              CL**3
00197          UNTIL PS-END-OF-DUNS-FILE.                                  CL**4
00198                                                                   ETKUT031
00199      CLOSE       ET-ASN-DUNS-FILE                                    CL**5
00200                  ET-ASN-HALLMARK-STATUS-FILE                         CL**6
00201                  ET-ASN-DUNS-UPD-FILE                                CL**5
00202                  ET-ASN-HALLMARK-FILE.                               CL**5
00203                                                                   ETKUT031
00204      STOP RUN.                                                    ETKUT031
00205                                                                   ETKUT031
00206  EJECT                                                            ETKUT031
00207  B100-PROCESS-ASN-DUNS-FILE.                                         CL**3
00208 ******************************************************************ETKUT031
00209 *  PROCESS THE IDENTIFICATION RECORD OF EACH ASN.  'GOOD' HALLMARK   CL*11
00210 *  ASNS ARE WRITTEN TO THE UPDATED DUNS FILE ALONG WITH NON-HALLMA   CL*11
00211 *  ASNS.  'RECYCLE' HALLMARK ASNS ARE WRITTEN TO A SEPARATE FILE     CL*11
00212 *  WITH THE VENDOR ID RE-INSERTED INTO THE ID RECORD. (TO BE RE-     CL*11
00213 *  PROCESSED IN ETKUT018).                                           CL*11
00214 *                                                                 ETKUT031
00215 *  PERFORMED BY A100-MAIN-MODULE.                                 ETKUT031
00216 ******************************************************************ETKUT031
00217 *    DISPLAY 'B100-PROCESS-ASN-DUNS-FILE'.                           CL**3
00218                                                                   ETKUT031
00219      IF ID-IDENTIFICATION-REC                                        CL**3
00220         PERFORM C100-PROCESS-ID-REC                                  CL**9
00221      END-IF.                                                         CL**3
00222                                                                      CL**3
00223      IF PS-HALLMARK-ASN                                              CL**3
00224                                                                      CL**3
00225         IF PS-GOOD-HALLMARK-ASN                                      CL**3
00226 *          ***  RECYCLE FLAG NOT SET OR HAS BEEN RECYCLED > 5 DAYS   CL*11
00227            PERFORM Z200-WRITE-DUNS-UPD-FILE                          CL**3
00228         ELSE                                                         CL**3
00229            MOVE ADVANCE-SHIP-NOTICE-DTL-RECORD TO                    CL**3
00230                 ET012-ADV-SHIP-NOTICE                                CL**3
00231            PERFORM Z300-WRITE-ASN-HALLMARK-FILE                      CL**3
00232         END-IF                                                       CL**3
00233                                                                      CL**3
00234      ELSE                                                            CL**3
00235         PERFORM Z200-WRITE-DUNS-UPD-FILE                             CL**3
00236                                                                      CL**3
00237      END-IF.                                                         CL**3
00238                                                                      CL**3
00239      PERFORM Z100-READ-ASN-DUNS-FILE.                                CL**3
00240                                                                   ETKUT031
00241  EJECT                                                            ETKUT031
00242  C100-PROCESS-ID-REC.                                                CL**9
00243 ******************************************************************ETKUT031
00244 *  READ THE HALLMARK ASN STATUS RECORD. IF THE ASN HAS BEEN          CL*11
00245 *  RECYCLED FOR < 5 DAYS, FLAG IT TO BE WRITTEN TO THE HALLMARK      CL*11
00246 *  RECYCLE FILE AND RECYCLED.  OTHERWISE, THE ASN WILL BE WRITTEN    CL*11
00247 *  TO THE FILE PASSED TO ASN PROCESSING.                             CL*11
00248 *                                                                 ETKUT031
00249 *  PERFORMED BY B100-PROCESS-ASN-DUNS-FILE.                          CL**3
00250 ******************************************************************ETKUT031
00251                                                                      CL**3
00252      IF SN001I-VEND-DUNNS-NBR = PC-HALLMARK-DUNS-NBR                 CL**3
SP0409     OR SN001I-VEND-DUNNS-NBR = PC-BEAUTY-BANK-DUNS-NBR              CL**3
00253                                                                      CL**3
00254         SET PS-HALLMARK-ASN TO TRUE                                  CL**3
00255         SET PS-GOOD-HALLMARK-ASN TO TRUE                             CL**3
00256                                                                      CL**3
00257         PERFORM Z400-READ-HALLMARK-STATUS-FILE                       CL**8
00258                                                                      CL**3
00259 *   *****  CALENDAR ROUTINE   *****                                  CL*13
00260         PERFORM D100-CALENDAR-ROUTINE                                CL*13
00261                                                                      CL**8
00262         IF ET013-ASN-RECYCLE AND                                     CL**3
00263            DPG55-DAYS-DIFFERENCE < 12                                CL*14
00264                                                                      CL**3
00265            SET PS-RECYCLE-HALLMARK-ASN TO TRUE                       CL**3
00266            MOVE ET013-EDI-VENDOR-ID TO SN001I-EDI-VEND-ID            CL**3
00267                                                                      CL**3
00268         END-IF                                                       CL**3
00269                                                                      CL**3
00270      ELSE                                                            CL**3
00271                                                                      CL**3
00272         SET PS-NON-HALLMARK-ASN TO TRUE                              CL**3
00273                                                                      CL**3
00274      END-IF.                                                         CL**3
00275                                                                      CL**3
00276                                                                      CL**3
00277  EJECT                                                               CL**3
00278  D100-CALENDAR-ROUTINE.                                              CL*13
00279 ******************************************************************   CL**3
00280 *                                                                    CL*13
00281 *                                                                    CL**3
00282 *  PERFORMED BY C100-                                                CL*13
00283 ******************************************************************   CL**3
00284 *    DISPLAY 'D100-CALENDAR-ROUTINE'.                                CL*13
00285      MOVE WS-TODAYS-DATE      TO WS-TODAYS-DATE-NUM.                 CL*14
           IF WS-YY > 80                                                        
              MOVE 19 TO WS-TODAYS-DATE-CC                                      
           ELSE                                                                 
              MOVE 20 TO WS-TODAYS-DATE-CC                                      
           END-IF.                                                              
           IF SN001I-RECYCLE-YY > 80                                            
              MOVE 19 TO WS-RECYCLE-DATE-CC                                     
           ELSE                                                                 
               MOVE 20 TO WS-RECYCLE-DATE-CC                                    
           END-IF.                                                              
00286      MOVE SN001I-RECYCLE-DATE TO WS-RECYCLE-DATE.                    CL*14
00287                                                                      CL*13
00288      INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                 CL*13
00289                                                                      CL*13
00290      ACCEPT DPG51-SYSTEM-DATE FROM DATE.                             CL*13
00291                                                                      CL*13
00292      SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                         CL*13
00293      SET DPG51-DO-NOT-INCR-DECR-DATE TO TRUE.                        CL*13
00294      MOVE ZEROES TO DPG51-INCR-DECR-DAYS-9                           CL*13
00295                     DPG51-INCR-DECR-BUS-DAYS-9.                      CL*13
00296      SET DPG52-LK-DTE-ISO TO TRUE.                                   CL*13
00297      MOVE WS-TODAYS-DATE-FORMAT  TO DPG52-LK-DATE-INPUT.             CL*14
00298      SET DPG53-LK-CMPR-DTE-ISO TO TRUE.                              CL*13
00299      MOVE WS-RECYCLE-DATE-FORMAT  TO DPG53-LK-CMPR-DATE-INPUT.       CL*14
00300                                                                      CL*13
00301      CALL PC-CALENDAR-ROUTINE                                        CL*13
00302          USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                  CL*13
00303                                                                      CL*13
00304      EVALUATE TRUE                                                   CL*13
00305          WHEN DPG54-SEVERE-ERROR                                     CL*13
00306               DISPLAY '** ABEND **'                                  CL*13
00307               DISPLAY '** PGM = ETKUT031'                            CL*13
00308               DISPLAY                                                CL*13
00309                 '** INVALID RECYCLE DATE **'                         CL*13
00310               DISPLAY '** RECYCLE DATE = ' WS-RECYCLE-DATE           CL*15
00311               DISPLAY '** ERROR MESSAGE = '                          CL*13
00312                   DPG54-ERROR-MESSAGE                                CL*13
00313               DISPLAY '** DPG51 = ' DPG51                            CL*13
00314               DISPLAY '** DPG52 = ' DPG52                            CL*13
00315               DISPLAY '** DPG53 = ' DPG53                            CL*13
00316               DISPLAY '** DPG54 = ' DPG54                            CL*13
00317               MOVE +4003 TO ABEND-CODE                               CL*13
00318               CALL 'ILBOABN0' USING ABEND-CODE                       CL*13
00319           WHEN OTHER                                                 CL*13
00320               CONTINUE                                               CL*13
00321       END-EVALUATE.                                                  CL*14
00322                                                                      CL*13
00323                                                                      CL*13
00324                                                                      CL*13
00325  EJECT                                                               CL*13
00326  Z100-READ-ASN-DUNS-FILE.                                            CL*13
00327 ******************************************************************   CL*13
00328 *  READ THE ASN DUNS FILE.                                           CL*13
00329 *                                                                    CL*13
00330 *  PERFORMED BY A100-MAIN-MODULE.                                    CL*13
00331 *               B100-PROCESS-ASN-DUNS-FILE.                          CL*13
00332 ******************************************************************   CL*13
00333 *    DISPLAY 'Z100-READ-ASN-DUNS-FILE'.                              CL*13
00334                                                                   ETKUT031
00335      READ ET-ASN-DUNS-FILE                                           CL**3
00336          INTO ADVANCE-SHIP-NOTICE-DTL-RECORD                         CL**3
00337          AT END                                                   ETKUT031
00338              SET PS-END-OF-DUNS-FILE TO TRUE.                        CL**3
00339                                                                   ETKUT031
00340                                                                   ETKUT031
00341  EJECT                                                            ETKUT031
00342  Z200-WRITE-DUNS-UPD-FILE.                                           CL**3
00343 ******************************************************************ETKUT031
00344 *  RE-WRITE THE VALID ASN TO THE DUNS FILE.                          CL**3
00345 *                                                                 ETKUT031
00346 ******************************************************************ETKUT031
00347 *    DISPLAY 'Z200-WRITE-DUNS-UPD-FILE'.                             CL**3
00348                                                                   ETKUT031
00349      WRITE ET-ASN-DUNS-UPD-RECORD                                    CL**5
00350          FROM ADVANCE-SHIP-NOTICE-DTL-RECORD.                     ETKUT031
00351                                                                   ETKUT031
00352  EJECT                                                            ETKUT031
00353  Z300-WRITE-ASN-HALLMARK-FILE.                                       CL**6
00354 ******************************************************************ETKUT031
00355 *  WRITE THE HALLMARK ASN TO THE RECYCLE FILE.                       CL**3
00356 *                                                                 ETKUT031
00357 ******************************************************************ETKUT031
00358 *    DISPLAY 'Z300-WRITE-ASN-HALLMARK-FILE'.                         CL**3
00359                                                                   ETKUT031
00360      WRITE ET-ASN-HALLMARK-RECORD                                    CL**3
00361          FROM ET012-ADV-SHIP-NOTICE.                                 CL**3
00362                                                                   ETKUT031
00363  EJECT                                                            ETKUT031
00364  Z400-READ-HALLMARK-STATUS-FILE.                                     CL**8
00365 ******************************************************************   CL**3
00366 *  READ THE HALLMARK ASN FILE.                                       CL**3
00367 *                                                                    CL**3
00368 ******************************************************************   CL**3
00369 *    DISPLAY 'Z400-READ-HALLMARK-STATUS-FILE'.                       CL**8
00370                                                                      CL**3
00371      READ ET-ASN-HALLMARK-STATUS-FILE                                CL**8
00372          INTO ET013-HALLMARK-ASN-STATUS                              CL**3
00373          AT END                                                      CL**3
00374              SET PS-END-OF-HALLMARK-FILE TO TRUE.                    CL**3
00375                                                                      CL**3
00376  EJECT                                                               CL**3
