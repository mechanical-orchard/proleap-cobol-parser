00001  IDENTIFICATION     DIVISION.                                     07/20/94
00002                                                                   DBKUT906
00003  PROGRAM-ID.        DBKUT906.                                        LV007
00004  AUTHOR.            SIDNEY CHAN.                                     CL**5
00005  INSTALLATION.      KOHLS DEPARTMENT STORES.                         CL**5
00006  DATE-WRITTEN.      08-05-1993.                                      CL**7
00007 ******************************************************************   CL**7
00008 ***                 THIS PROGRAM IS WRITTEN TO PICK OFF              CL**7
00009 ***                 TEST DB2 PACKAGES BOUND IN PRODUCTION            CL**7
00010 ***                 AFTER SO MANY DAYS AND TO GENERATE THE           CL**7
00011 ***                 THE FREE PACKAGE COMMANDS TO CLEAN THEM UP.      CL**7
00012 ******************************************************************   CL**7
00009 ***  CHANGE:  01/14/97                                               CL**7
00010 ***     REMOVE THE DB2 CATALOG "CLONE" TABLE INCLUDE & REPLACE       CL**7
00011 ***     WITH "REAL" TABLE INCLUDE FOR SYSIBM.SYSPACKAGE.             CL**7
00011 ***     MADE BY:  DAVE ABT, COMPUWARE                                CL**7
00009 ***  CHANGE:  12/10/99                                               CL**7
00010 ***     INCLUDE CLEANUP OF PEOPLESOFT (DB3P) TEST DB2 PACKAGES.      CL**7
00011 ***     MADE BY:  TOM PETERSON                                       CL**7
00009 ***  CHANGE:  01/09/06                                               CL**7
00010 ***     REMOVED OWNER PREDICATE IN SYSPAK_CSR.  THE OWNERS BEING     CL**7
00010 ***     SPECIFIED ARE EITHER INCOMPLETE OR OBSOLETE.  IT SHOULD      CL**7
00010 ***     BE SUFFICIENT TO JUST SEARCH ON TEST COLLECTION IDS.         CL**7
00010 ***     ALSO ADDED 'WITH UR' TO CURSOR.                              CL**7
00011 ***     MADE BY:  BRAD DORN                                          CL**7
00012 ******************************************************************   CL**7
00013  DATE-COMPILED.                                                   DBKUT906
00014                                                                   DBKUT906
00015  ENVIRONMENT DIVISION.                                            DBKUT906
00016                                                                   DBKUT906
00017  CONFIGURATION SECTION.                                           DBKUT906
00018                                                                   DBKUT906
00019  SOURCE-COMPUTER.    IBM.                                         DBKUT906
00020  OBJECT-COMPUTER.    IBM.                                         DBKUT906
00021                                                                   DBKUT906
00022  INPUT-OUTPUT SECTION.                                            DBKUT906
00023                                                                   DBKUT906
00024                                                                   DBKUT906
00025  FILE-CONTROL.                                                    DBKUT906
00026                                                                   DBKUT906
00027      SELECT CONTROL-FILE                                          DBKUT906
00028          ASSIGN TO OUT01.                                         DBKUT906
00029                                                                   DBKUT906
00030  DATA DIVISION.                                                   DBKUT906
00031                                                                   DBKUT906
00032  FILE SECTION.                                                    DBKUT906
00033  FD  CONTROL-FILE                                                 DBKUT906
00034      RECORDING MODE IS F                                          DBKUT906
00035      BLOCK CONTAINS 0 RECORDS.                                    DBKUT906
00036                                                                   DBKUT906
00037  01  CONTROL-RECORD    PIC X(080).                                DBKUT906
00038                                                                   DBKUT906
00039  WORKING-STORAGE SECTION.                                         DBKUT906
00040                                                                   DBKUT906
00041  01  ABEND-CODE                    PIC S9(4)  VALUE ZERO COMP.    DBKUT906
00042                                                                   DBKUT906
00043                                                                   DBKUT906
00044  01  PACKAGE-COUNT                 PIC S9(3)  VALUE ZERO COMP-3.  DBKUT906
00045                                                                   DBKUT906
00046  01  WS-HOST-VARIABLES.                                           DBKUT906
00049      05  WS-CURRENT-DATE           PIC X(10).                        CL**3
00050      05  WS-CURRENT-TIMESTAMP      PIC X(26).                        CL**4
00051                                                                   DBKUT906
00052  01  WS-LINES.                                                    DBKUT906
00053      05  WS-LINE1.                                                DBKUT906
00054          10  FILLER                PIC X(13)                      DBKUT906
00055                      VALUE '  DSN SYSTEM('.                       DBKUT906
00055          10  WS-LINE1-SYSTEM-ID    PIC X(04)   VALUE SPACES.      DBKUT906
00055          10  FILLER                PIC X(63)   VALUE ')  '.       DBKUT906
00057      05  WS-LINE2                  PIC X(80)   VALUE SPACES.      DBKUT906
00073  01  DP004-DB2-ERROR-FIELDS.                                      DBKUT906
00074      05  DP004-ASTERISK-LINE       PIC  X(80)  VALUE ALL '*'.     DBKUT906
00075      05  DP004-MAX-MESSAGE-LINES   PIC S9(04)  VALUE +12          DBKUT906
00076                                                COMP SYNC.         DBKUT906
00077      05  DP004-ERROR-LINE-LENGTH   PIC S9(09)  VALUE +75          DBKUT906
00078                                                COMP SYNC.         DBKUT906
00079      05  DP004-FORMATTED-MESSAGE.                                 DBKUT906
00080          10  FILLER                PIC S9(04)  VALUE +900         DBKUT906
00081                                                COMP SYNC.         DBKUT906
00082          10  DP004-MESSAGE-LINE    OCCURS 12 TIMES                DBKUT906
00083                                    INDEXED BY DP004-MSG-IDX       DBKUT906
00084                                    PIC  X(75).                    DBKUT906
00085                                                                   DBKUT906
00086      EXEC SQL                                                     DBKUT906
00087          INCLUDE SYSPKG                                           DBKUT906
00088      END-EXEC.                                                    DBKUT906
00089                                                                   DBKUT906
00090      EXEC SQL                                                     DBKUT906
00091          DECLARE SYSPAK_CSR CURSOR FOR                            DBKUT906
               SELECT DISTINCT                                                  
                      'FREE PACKAGE (' || RTRIM(COLLID) || '.'                  
                      || RTRIM(NAME) ||                                         
                      '.(' || RTRIM(VERSION) || '))'                            
00094          FROM SYSIBM.SYSPACKAGE                                   DBKUT906
00095           WHERE ( COLLID LIKE '___TEST_COLL%'                     DBKUT906
00097             AND   BINDTIME  < (CURRENT TIMESTAMP - 5 DAYS)  )        CL**5
               WITH UR                                                          
00098      END-EXEC.                                                    DBKUT906
00099                                                                   DBKUT906
00100      EXEC SQL                                                     DBKUT906
00101          INCLUDE SQLCA                                            DBKUT906
00102      END-EXEC.                                                    DBKUT906
00103                                                                   DBKUT906
00103  LINKAGE SECTION.                                                 DBKUT906
00103                                                                   DBKUT906
00103  01  LINK-FIELDS.                                                 DBKUT906
00103      05  LINK-LEN                  PIC 9(4)   COMP.               DBKUT906
00103      05  LINK-OWNER                PIC X(4).                      DBKUT906
00103                                                                   DBKUT906
00104  PROCEDURE DIVISION USING LINK-FIELDS.                            DBKUT906
00105                                                                   DBKUT906
00106      OPEN OUTPUT CONTROL-FILE.                                    DBKUT906
00107                                                                      CL**3
00108      EXEC SQL                                                        CL**3
00109          SET :WS-CURRENT-DATE = CURRENT DATE                         CL**3
00110      END-EXEC.                                                       CL**3
00111      IF  SQLCODE = ZERO                                              CL**3
00112          DISPLAY '** CURRENT DATE = ' WS-CURRENT-DATE                CL**3
00113      ELSE                                                            CL**3
00114          DISPLAY '** ABEND **         **'                            CL**3
00115          DISPLAY '** PROGRAM          ** = DBKUT906'                 CL**3
00116          DISPLAY '** PARAGRAPH NAME   ** = MAIN-LINE'                CL**3
00117          DISPLAY '** DB2 OPERATION    ** = SET CURRENT DATE'         CL**3
00118          PERFORM 9000-SQL-ABEND.                                     CL**3
00119                                                                      CL**3
00120      EXEC SQL                                                        CL**4
00121          SET :WS-CURRENT-TIMESTAMP = CURRENT TIMESTAMP               CL**4
00122      END-EXEC.                                                       CL**4
00123      IF  SQLCODE = ZERO                                              CL**4
00124          DISPLAY '** CURRENT TIMESTAMP = ' WS-CURRENT-TIMESTAMP      CL**4
00125      ELSE                                                            CL**4
00126          DISPLAY '** ABEND **        **'                             CL**4
00127          DISPLAY '** PROGRAM         ** = DBKUT906'                  CL**4
00128          DISPLAY '** PARAGRAPH NAME  ** = MAIN-LINE'                 CL**4
00129          DISPLAY '** DB2 OPERATION   ** = SET CURRENT TIMESTAMP'     CL**4
00130          PERFORM 9000-SQL-ABEND.                                     CL**4
00131                                                                      CL**4
00131      MOVE 'DB2P' TO WS-LINE1-SYSTEM-ID.                              CL**4
00132                                                                   DBKUT906
00133      EXEC SQL                                                     DBKUT906
00134          OPEN SYSPAK_CSR                                          DBKUT906
00135      END-EXEC.                                                    DBKUT906
00136      IF  SQLCODE NOT = ZERO                                       DBKUT906
00137          DISPLAY '** ABEND **         **'                         DBKUT906
00138          DISPLAY '** PROGRAM          ** = DBKUT906'              DBKUT906
00139          DISPLAY '** PARAGRAPH NAME   ** = MAIN-LINE'             DBKUT906
00140          DISPLAY '** DB2 OPERATION    ** = OPEN SYSPAK_CSR'       DBKUT906
00141          PERFORM 9000-SQL-ABEND.                                  DBKUT906
00142                                                                   DBKUT906
00143      MOVE WS-LINE1 TO CONTROL-RECORD.                             DBKUT906
00144      WRITE CONTROL-RECORD.                                        DBKUT906
00147                                                                   DBKUT906
00148      PERFORM 1000-FETCH-SYSPAC THRU 1000-EXIT                     DBKUT906
00149          UNTIL SQLCODE = +100.                                    DBKUT906
00158                                                                   DBKUT906
00159      EXEC SQL                                                     DBKUT906
00160          CLOSE SYSPAK_CSR                                         DBKUT906
00161      END-EXEC.                                                    DBKUT906
00162      IF  SQLCODE = ZERO                                           DBKUT906
00163          CONTINUE                                                 DBKUT906
00164      ELSE                                                         DBKUT906
00165          DISPLAY '** ABEND **         **'                         DBKUT906
00166          DISPLAY '** PROGRAM          ** = DBKUT906'              DBKUT906
00167          DISPLAY '** PARAGRAPH NAME   ** = MAIN-LINE'             DBKUT906
00168          DISPLAY '** DB2 OPERATION    ** = CLOSE SYSPAK_CSR'      DBKUT906
00169          PERFORM 9000-SQL-ABEND.                                  DBKUT906
00170                                                                   DBKUT906
00171      CLOSE CONTROL-FILE.                                          DBKUT906
00172                                                                   DBKUT906
00151      IF PACKAGE-COUNT = ZERO                                      DBKUT906
00152          MOVE 4    TO RETURN-CODE.                                DBKUT906
00153                                                                   DBKUT906
00173      GOBACK.                                                      DBKUT906
00174                                                                   DBKUT906
00175  1000-FETCH-SYSPAC.                                               DBKUT906
00176                                                                   DBKUT906
00179      EXEC SQL                                                     DBKUT906
00180          FETCH SYSPAK_CSR                                         DBKUT906
00181          INTO  :WS-LINE2                                          DBKUT906
00183      END-EXEC.                                                    DBKUT906
00184                                                                   DBKUT906
00185      IF SQLCODE = +100                                            DBKUT906
00186          GO TO 1000-EXIT.                                         DBKUT906
00187                                                                   DBKUT906
00188      IF SQLCODE NOT = ZERO                                        DBKUT906
00189          DISPLAY '** ABEND **         **'                         DBKUT906
00190          DISPLAY '** PROGRAM          ** = DBKUT906'              DBKUT906
00191          DISPLAY                                                  DBKUT906
00192          '** PARAGRAPH NAME   ** = 1000-FETCH-SYSPAK'             DBKUT906
00193          DISPLAY '** DB2 OPERATION    ** = FETCH SYSPAK_CSR'      DBKUT906
00194          PERFORM 9000-SQL-ABEND.                                  DBKUT906
00195                                                                   DBKUT906
00196      ADD 1 TO PACKAGE-COUNT.                                      DBKUT906
00213      MOVE WS-LINE2 TO CONTROL-RECORD.                             DBKUT906
00214      WRITE CONTROL-RECORD.                                        DBKUT906
00215                                                                   DBKUT906
00216  1000-EXIT.                                                       DBKUT906
00217       EXIT.                                                       DBKUT906
00218                                                                   DBKUT906
00219  9000-SQL-ABEND.                                                  DBKUT906
00220                                                                   DBKUT906
00221      CALL 'DSNTIAR' USING SQLCA                                   DBKUT906
00222                           DP004-FORMATTED-MESSAGE                 DBKUT906
00223                           DP004-ERROR-LINE-LENGTH.                DBKUT906
00224                                                                   DBKUT906
00225      DISPLAY DP004-ASTERISK-LINE.                                 DBKUT906
00226      PERFORM                                                      DBKUT906
00227          VARYING DP004-MSG-IDX                                    DBKUT906
00228          FROM 1 BY 1                                              DBKUT906
00229          UNTIL DP004-MSG-IDX > DP004-MAX-MESSAGE-LINES            DBKUT906
00230              DISPLAY '**'                                         DBKUT906
00231                      DP004-MESSAGE-LINE (DP004-MSG-IDX)           DBKUT906
00232                      ' **'                                        DBKUT906
00233      END-PERFORM.                                                 DBKUT906
00234      DISPLAY DP004-ASTERISK-LINE.                                 DBKUT906
00235                                                                   DBKUT906
00236      EXEC SQL                                                     DBKUT906
00237          ROLLBACK                                                 DBKUT906
00238      END-EXEC.                                                    DBKUT906
00239                                                                   DBKUT906
00240      MOVE +4013 TO ABEND-CODE.                                    DBKUT906
00241      CALL 'ILBOABN0' USING ABEND-CODE.                            DBKUT906
00242                                                                   DBKUT906
