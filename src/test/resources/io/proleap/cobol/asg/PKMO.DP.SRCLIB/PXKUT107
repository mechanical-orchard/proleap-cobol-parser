      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
      *                                                                 00040000
       PROGRAM-ID.   PXKUT107.                                          00050000
       AUTHOR.       RICH KELLEN.                                       00060000
       INSTALLATION. KOHLS DEPARTMENT STORES.                           00070000
       DATE-WRITTEN. JUNE 2010.                                         00080000
       DATE-COMPILED.                                                   00090000
      *                                                                 00100000
      ***************************************************************** 00110000
      * WR36056 - SKU/STORE EVENT DATA LOOKUP.                        * 00120007
      * THIS PROGRAM READS OF FILE OF SKUS/STORES AND LOOKS UP EVENT  * 00130007
      * DATA. IT THEN WRITES RECORDS IN THE PXRD051 FORMAT. THESE     * 00140007
      * RECORDS ARE MERGED WITH ANOTHER EVENT FILE INTO PGM PXKUT042  * 00150007
      * THE PURPOSE OF THIS IS TO CREATE RECORDS TO BE                * 00160007
      * LOADED TO THE COMPLIANCE TABLES FOR SKUS THAT HAVE A FASHION  * 00161007
      * SALES ON HAND BALANCE <0, BUT, FOR WHICH SCANS HAVE BEEN DONE * 00162007
      * IN A PREVIOUS EVENT.                                          * 00163007
      ***************************************************************** 00165000
      ******************** HISTORY OF CHANGES **************************00166000
      * CHG ID/                                                        *00167000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *00168000
      * -------  ----------  ----------------------------------------  *00169000
      * WR36056  10/15/2010  INITIAL IMPLEMENTATION                    *00170007
      ******************************************************************00180000
      ******************************************************************00190000
       ENVIRONMENT DIVISION.                                            00200000
      ******************************************************************00210000
      *                                                                 00220000
       CONFIGURATION SECTION.                                           00230000
       SOURCE-COMPUTER.   IBM-3090.                                     00240000
       OBJECT-COMPUTER.   IBM-3090.                                     00250000
      *                                                                 00260000
       INPUT-OUTPUT SECTION.                                            00270000
       FILE-CONTROL.                                                    00280000
           SELECT MISSING-SKU-UNLOAD-FILE                               00290000
               ASSIGN                       TO INP01.                   00300000
                                                                        00310000
            SELECT SKU-ADD-FILE                                         00320000
              ASSIGN                        TO OUT01.                   00330000
                                                                        00340000
      ******************************************************************00392000
       DATA DIVISION.                                                   00393000
      ******************************************************************00394000
      *                                                                 00395000
       FILE SECTION.                                                    00396000
       FD  MISSING-SKU-UNLOAD-FILE                                      00397000
           RECORDING MODE F                                             00398000
           BLOCK CONTAINS 0 RECORDS                                     00399000
           RECORD CONTAINS 026 CHARACTERS                               00400002
           LABEL RECORDS ARE STANDARD                                   00410000
           DATA RECORD IS MISSING-SKU-UNLOAD-REC.                       00420000
       01  MISSING-SKU-UNLOAD-REC                 PIC X(026).           00430002
      *                                                                 00440000
       FD SKU-ADD-FILE                                                  00450000
           RECORDING MODE IS F                                          00460000
           BLOCK CONTAINS 0 RECORDS                                     00470000
           RECORD CONTAINS 080 CHARACTERS                               00480001
           LABEL RECORDS ARE STANDARD                                   00490000
           DATA RECORD IS SKU-ADD-RECORD.                               00500000
      *                                                                 00510000
       01 SKU-ADD-RECORD                         PIC X(080).            00520001
      *                                                                 00530000
       WORKING-STORAGE SECTION.                                         00599800
                                                                        00599900
       01 ABEND-CODE                      PIC S9(04)    VALUE ZERO      00600000
                                                        COMP SYNC.      00610000
           88  ABEND-4013-DB2-ERROR                        VALUE +4013. 00620000
      *                                                                 00630000
                                                                        00640000
       01 PC-PROGRAM-CONSTANTS.                                         00650000
          05  PC-PXKUT107                 PIC X(10)      VALUE          00660000
              'PXKUT107  '.                                             00670000
           05  PC-CURRENT-PROGRAM-NAME    PIC X(08)                     00680000
               VALUE 'PXKUT107'.                                        00690000
          05  PC-PROGRAM-NAME             PIC X(08)  VALUE              00700000
              'PXKUT107'.                                               00710000
           05  PC-MAX-RETRIES            PIC S9(04) VALUE +5 COMP.      00720000
       01 PV-PROGRAM-VARIABLES.                                         00730000
          05  PV-SQLCODE                  PIC +9(04).                   00740000
          05  PV-OPERATION-MSG            PIC X(60)  VALUE SPACES.      00770000
          05  PV-PRCHG-ID                 PIC X(10)  VALUE SPACES.      00780000
          05  PV-UNT-RTL-AMT              PIC X(09)  VALUE SPACES.      00790000
          05  PV-UNT-RTL-AMT-N REDEFINES PV-UNT-RTL-AMT                 00800000
                                          PIC 9(07)V99.                 00810000
          05  PV-GP-RTL-QTY               PIC X(10)  VALUE SPACES.      00820000
          05  PV-GP-RTL-AMT               PIC X(10)  VALUE SPACES.      00830000
          05  PV-WRITE-CNT                PIC 9(7)   VALUE ZERO         00840000
                                                     COMP-3.            00850000
          05  PV-READ-CNT                 PIC 9(7)      VALUE ZERO      00860000
                                                        COMP-3.         00870000
          05  PV-EVNT-NOT-FOUND-CNT       PIC 9(7)      VALUE ZERO      00880000
                                                        COMP-3.         00890000
          05  PV-RETRY-COUNT              PIC S9(04)    VALUE +0        00900000
                                                        COMP SYNC.      00910000
          05  PV-ABEND-CODE        PIC S9(04)     VALUE +0              00920000
                                                  COMP SYNC.            00930000
          05  PV-PARA-NAME         PIC X(35).                           00940000
          05  PV-DISPLAY-SKU-NBR        PIC 9(10)  VALUE ZERO.          00950000
          05  PV-DISPLAY-LOC-NBR        PIC 9(4)   VALUE ZERO.          00960000
          05  PV-OH-ADJ-QTY             PIC S9(9)   VALUE ZERO.         00961004
                                                                        00970000
      *                                                                 00980000
       01 PS-PROGRAM-SWITCHES.                                          00990000
          05  END-OF-MSSG-SKU-UNLD-FILE-SW PIC X(1) VALUE 'N'.          01000000
              88  PS-END-OF-MSSG-SKU-UNLD-FILE VALUE 'Y'.               01010000
          05  PS-INPUT-FILE-EMPTY-SW       PIC X(1) VALUE 'N'.          01020000
              88  PS-INPUT-FILE-EMPTY               VALUE 'Y'.          01030000
              88  PS-INPUT-FILE-NOT-EMPTY           VALUE 'N'.          01040000
          05  PS-EVENT-FOUND-SW            PIC X(1) VALUE 'N'.          01050000
              88  PS-EVENT-FOUND                    VALUE 'Y'.          01060000
              88  PS-EVENT-NOT-FOUND                VALUE 'N'.          01070000
      *                                                                 01080000
      ******************************************************************01180000
      * COPY MEMBER FOR THE INPUT FILE                                  01190007
      ******************************************************************01200000
      *                                                                 01210000
           COPY PXRD124.                                                01220002
      *                                                                 01230000
      ******************************************************************01240000
      * COPY MEMBER FOR THE OUTPUT FILE                                 01250007
      ******************************************************************01260000
      *                                                                 01270000
           COPY PXRD050.                                                01280001
                                                                        01290000
      *---------------------------------------------                    01300000
      *  MB2 COMMUNICATION AREA.                                        01310000
      *---------------------------------------------                    01320000
           EXEC SQL                                                     01330000
                INCLUDE SQLCA                                           01340000
           END-EXEC.                                                    01350000
                                                                        01360000
           COPY SQLCA2.                                                 01370000
      ******************************************************************01380000
      *  DB2 TABLE XST00001 (XST_LOC)                                   01390000
      ******************************************************************01400000
           EXEC SQL                                                     01410000
               INCLUDE XST00001                                         01420000
           END-EXEC.                                                    01430000
      ******************************************************************01440000
      *  DB2 TABLE XST00010 (XST_SKU)                                   01450000
      ******************************************************************01460000
           EXEC SQL                                                     01470000
               INCLUDE XST00010                                         01480000
           END-EXEC.                                                    01490000
      ******************************************************************01500000
      *  DB2 TABLE XST00011 (XST_SKU_ATTR)                              01510000
      ******************************************************************01520000
           EXEC SQL                                                     01530000
               INCLUDE XST00011                                         01540000
           END-EXEC.                                                    01550000
      ******************************************************************01560000
      *  DB2 TABLE XST00013 (XST_VND_STYL)                              01570000
      ******************************************************************01580000
           EXEC SQL                                                     01590000
               INCLUDE XST00013                                         01600000
           END-EXEC.                                                    01610000
      ******************************************************************01620000
      *  DB2 TABLE XST00015 (XST_DEPT)                                  01630000
      ******************************************************************01640000
           EXEC SQL                                                     01650000
               INCLUDE XST00015                                         01660000
           END-EXEC.                                                    01670000
      ******************************************************************01680000
      *  DB2 TABLE XST00016 (XST_BUS_GP)                                01690000
      ******************************************************************01700000
           EXEC SQL                                                     01710000
               INCLUDE XST00016                                         01720000
           END-EXEC.                                                    01730000
      ******************************************************************01740000
      *  DB2 TABLE XST00023 (XST_STR_GP_MBSHP)                          01750000
      ******************************************************************01760000
           EXEC SQL                                                     01770000
               INCLUDE XST00023                                         01780000
           END-EXEC.                                                    01790000
      ******************************************************************01800000
      *  DB2 TABLE XST00063 (XST_PRCHG)                                 01810000
      ******************************************************************01820000
           EXEC SQL                                                     01830000
               INCLUDE XST00063                                         01840000
           END-EXEC.                                                    01850000
      ******************************************************************01860000
      *  DB2 TABLE XST00064 (XST_STR_GP_MBSHP)                          01870000
      ******************************************************************01880000
           EXEC SQL                                                     01890000
               INCLUDE XST00064                                         01900000
           END-EXEC.                                                    01910000
      ******************************************************************01920000
      *  DB2 TABLE XST00065 (XST_PRCHG_MDSE)                            01930000
      ******************************************************************01940000
           EXEC SQL                                                     01950000
               INCLUDE XST00065                                         01960000
           END-EXEC.                                                    01970000
      ******************************************************************01980000
       PROCEDURE DIVISION.                                              01990000
      ******************************************************************02000000
      *                                                                 02010000
          EJECT                                                         02020000
       0000-MAINLINE-PROCESSING.                                        02030000
      ******************************************************************02040000
      * MAIN PROCESSING ROUTINE                                       * 02050000
      ******************************************************************02060000
                                                                        02070000
            PERFORM 1000-INIT.                                          02080000
            PERFORM 2000-PROCESS-MSSG-SKU-LOOKUP                        02090000
              UNTIL PS-END-OF-MSSG-SKU-UNLD-FILE.                       02100000
                                                                        02160000
            PERFORM 9000-END-OF-JOB.                                    02170000
                                                                        02180000
            GOBACK.                                                     02190000
                                                                        02200000
      ******************************************************************02210000
      * OPEN FILES                                                     *02220007
      ******************************************************************02230000
      *                                                                 02240000
       1000-INIT.                                                       02250000
           MOVE '*1000-INIT.              *' TO PV-PARA-NAME.           02260007
           OPEN INPUT  MISSING-SKU-UNLOAD-FILE                          02270007
                OUTPUT SKU-ADD-FILE.                                    02280007
           PERFORM 7000-READ-MSSG-SKU-UNLD-FILE.                        02310000
           IF PS-END-OF-MSSG-SKU-UNLD-FILE                              02320000
             MOVE 'Y' TO PS-INPUT-FILE-EMPTY-SW.                        02330000
                                                                        02370000
      ******************************************************************02380000
      * PROCESS MISSING SKU PRICE CHANGE LOOKUP.                        02390000
      ******************************************************************02400000
       2000-PROCESS-MSSG-SKU-LOOKUP.                                    02410000
           MOVE '*2000-PROCESS-MSSG-SKU-LOOKUP*' TO PV-PARA-NAME.       02420007
           MOVE PXRD124-STR-NBR           TO XST00023-STR-NBR           02540002
                                             PXRD050-STR-NBR.           02550001
           MOVE PXRD124-SKU-NBR           TO XST00065-SKU-NBR           02560002
                                             PXRD050-SKU-NBR.           02570001
           PERFORM 8000-MSSG-SKU-LOOKUP-SELECT.                         02580000
           IF PS-EVENT-FOUND                                            02590000
      *** ONLY SKU/STORES WITH A MATCHING CURRENT EVENT FOUND WILL HAVE 02600000
      *** A RECORD WRITTEN. ALL OTHERS WILL BE DISCARDED.               02610000
             MOVE PV-PRCHG-ID            TO PXRD050-PRCHG-ID            02620001
             MOVE SPACE                  TO PXRD050-EVNT-CDE            02621003
             MOVE XST00063-EVNT-CTG-CDE  TO PXRD050-EVNT-CTG-CDE        02630001
             MOVE XST00063-PRCHG-TYP-CDE TO PXRD050-PRCHG-TYP-CDE       02640001
             MOVE XST00064-PRCHG-EFF-DTE TO PXRD050-EVNT-DTE            02650001
             MOVE PXRD124-OH-ADJ-QTY     TO PV-OH-ADJ-QTY               02701004
             COMPUTE PXRD050-STR-EXPTED-QTY  = PV-OH-ADJ-QTY * -1       02710003
             MOVE PV-UNT-RTL-AMT-N       TO PXRD050-NEW-UNT-RTL-AMT     02720001
             MOVE PV-GP-RTL-QTY          TO PXRD050-NW-GP-RTL-QTY       02730001
             MOVE PV-GP-RTL-AMT          TO PXRD050-NW-GP-RTL-AMT       02740001
             PERFORM 7100-WRITE-SKU-ADD-FILE.                           02741015
           PERFORM 7000-READ-MSSG-SKU-UNLD-FILE.                        02760000
                                                                        02770000
                                                                        02780000
      ******************************************************************02790000
      *  READ INPUT FILE                                               *02800000
      ******************************************************************02810000
       7000-READ-MSSG-SKU-UNLD-FILE.                                    02820000
           MOVE '*7000-READ-MSSG-SKU-UNLD-FILE*' TO PV-PARA-NAME.       02821007
           ADD 1 TO PV-READ-CNT.                                        02830000
           READ MISSING-SKU-UNLOAD-FILE INTO PXRD124-OH-ADJ-RECORD      02840002
             AT END                                                     02850000
               SET PS-END-OF-MSSG-SKU-UNLD-FILE TO TRUE.                02860000
                                                                        02870000
      ******************************************************************02880000
      *  WRITE OUTPUT FILE                                             *02890000
      ******************************************************************02900000
       7100-WRITE-SKU-ADD-FILE.                                         02910000
           MOVE '*7100-WRITE-SKU-ADD-FILE     *' TO PV-PARA-NAME.       02911007
           WRITE SKU-ADD-RECORD FROM                                    02920000
             PXRD050-DATA-RECORD.                                       02930001
           ADD 1 TO PV-WRITE-CNT.                                       02940000
                                                                        02950000
      *------------------------------------------------------           02960000
      * 8000-MSS-SKU-LOOKUP-SELECT.                                     02970000
      *     LOOKS UP DATA NEEDED TO CREATE PRICE CHANGE EVENT           02980007
      *     DATA TO LOAD THE PXT_EVNT_STR_MDSE TABLE.                   02990007
      *------------------------------------------------------           03000000
                                                                        03010000
       8000-MSSG-SKU-LOOKUP-SELECT.                                     03020000
           MOVE '*8000-MSSG-SKU-LOOKUP-SELECT *' TO PV-PARA-NAME.       03021007
                                                                        03030000
           PERFORM WITH TEST AFTER                                      03040000
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       03050000
                 UNTIL SQLCODE = +0                                     03060000
                    OR SQLCODE = +100                                   03070000
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  03080000
               EXEC SQL                                                 03090000
                  SELECT DIGITS(A.PRCHG_ID) PRCHG_ID                    03100000
                         ,B.PRCHG_EFF_DTE                               03110000
                         ,DIGITS(E.UNT_RTL_AMT) UNT_RTL_AMT             03120000
                         ,A.EVNT_CTG_CDE                                03130000
                         ,A.PRCHG_TYP_CDE                               03140000
                         ,DIGITS(COALESCE(E.GP_RTL_QTY,0))              03150000
                         ,DIGITS(COALESCE(E.GP_RTL_AMT,0))              03160000
                  INTO    :PV-PRCHG-ID                                  03170000
                         ,:XST00064-PRCHG-EFF-DTE                       03180000
                         ,:PV-UNT-RTL-AMT                               03190000
                         ,:XST00063-EVNT-CTG-CDE                        03200000
                         ,:XST00063-PRCHG-TYP-CDE                       03210000
                         ,:PV-GP-RTL-QTY                                03220000
                         ,:PV-GP-RTL-AMT                                03230000
                   FROM   XST_PRCHG         A                           03240000
                         ,XST_STR_GP_PRCHG  B                           03250000
                         ,XST_STR_GP_MBSHP  C                           03260000
                         ,XST_LOC           D                           03270000
                         ,XST_PRCHG_MDSE    E                           03280000
                         ,XST_SKU           F                           03290000
                         ,XST_VND_STYL      G                           03300000
                         ,XST_DEPT          H                           03310000
                         ,XST_BUS_GP        I                           03320000
                   WHERE A.PRCHG_ID       = B.PRCHG_ID                  03330000
                     AND B.STR_GP_TYP_CDE = C.STR_GP_TYP_CDE            03340000
                     AND B.STR_GP_ID      = C.STR_GP_ID                 03350000
                     AND C.STR_NBR        = D.LOC_NBR                   03360000
                     AND B.STR_GP_TYP_CDE = E.STR_GP_TYP_CDE            03370000
                     AND B.STR_GP_ID      = E.STR_GP_ID                 03380000
                     AND B.PRCHG_ID       = E.PRCHG_ID                  03390000
                     AND E.SKU_NBR        = F.SKU_NBR                   03400000
                     AND F.STYL_ID        = G.STYL_ID                   03410000
                     AND G.DEPT_NBR       = H.DEPT_NBR                  03420000
                     AND H.BUS_GP_NBR     = I.BUS_GP_NBR                03430000
                     AND (C.EFF_END_DTE   = B.PRCHG_EFF_DTE             03440010
                       OR C.EFF_END_DTE   IS NULL)                      03450003
                     AND C.EFF_BEG_DTE    <= B.PRCHG_EFF_DTE            03460010
                     AND B.PRCHG_EFF_DTE  = CURRENT DATE                03470016
                     AND A.PRCHG_TYP_CDE  IN                            03480000
                      ('RCP', 'MK1','MK2', 'FNL', 'CTA', 'MUA', 'MDA')  03490000
                     AND A.EVNT_CTG_CDE   IN ('CLR','RCP', 'ACT')       03500000
                     AND A.PRCHG_ST_CDE   = 'APR'                       03510000
                     AND A.LAST_ACTN_CDE  <> 'D'                        03520000
                     AND B.LAST_ACTN_CDE  <> 'D'                        03530000
                     AND E.LAST_ACTN_CDE  <> 'D'                        03540000
                     AND E.SKU_NBR        = :XST00065-SKU-NBR           03550000
                     AND C.STR_NBR        = :XST00023-STR-NBR           03560000
                   FETCH FIRST ROW ONLY                                 03630000
                   WITH UR                                              03640000
               END-EXEC                                                 03650000
                                                                        03660000
               EVALUATE TRUE                                            03670000
                  WHEN SQLCODE = -904                                   03680000
                  WHEN SQLCODE = -913                                   03690000
                    CONTINUE                                            03700000
                  WHEN SQLCODE = +0                                     03710000
                   SET PS-EVENT-FOUND          TO TRUE                  03720000
                  WHEN SQLCODE = +100                                   03730000
                       SET PS-EVENT-NOT-FOUND  TO TRUE                  03740000
                       ADD 1 TO PV-EVNT-NOT-FOUND-CNT                   03750000
                  WHEN OTHER                                            03760000
                    DISPLAY 'SELECT FAILED IN PARA 8000'                03770000
                    DISPLAY 'SKU-NBR:     ' PXRD124-SKU-NBR             03780002
                    DISPLAY 'LOC-NBR:     ' PXRD124-STR-NBR             03790002
                    PERFORM 9980-SQL-ERROR                              03820000
               END-EVALUATE                                             03830000
           END-PERFORM.                                                 03840000
                                                                        03850000
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          03860000
               MOVE XST00065-SKU-NBR              TO PV-DISPLAY-SKU-NBR 03870000
               MOVE XST00023-STR-NBR              TO PV-DISPLAY-LOC-NBR 03880000
               DISPLAY '** '                                            03890000
                       PC-CURRENT-PROGRAM-NAME                          03900000
                       ' - SKU NBR: '                                   03910000
                       PV-DISPLAY-SKU-NBR                               03920000
               DISPLAY '** '                                            03930000
                       PC-CURRENT-PROGRAM-NAME                          03940000
                       ' - LOC NBR: '                                   03941000
                       PV-DISPLAY-LOC-NBR                               03942000
               MOVE '8000-MSSG-SKU-LOOKUP-SELECT' TO PV-PARA-NAME       03943000
               MOVE 'SELECT EVENT DATA'           TO PV-OPERATION-MSG   03944000
               PERFORM 9980-SQL-ERROR                                   03945000
           END-IF.                                                      03946000
                                                                        03947000
      ******************************************************************03948000
      *  WRITE COUNTER FILES, CLOSE FILES, DISPLAY COUNTERS            *03949000
      ******************************************************************03950000
       9000-END-OF-JOB.                                                 03960000
           MOVE '*9000-END-OF-JOB             *' TO PV-PARA-NAME.       03970007
           DISPLAY '*****PXKUT107*****'                                 04010012
           SUBTRACT 1 FROM PV-READ-CNT.                                 04010115
           DISPLAY 'NBR OF INPUT RECORDS    -   '                       04011015
                   PV-READ-CNT.                                         04020015
           DISPLAY 'NBR OF EVENTS NOT FOUND -   '                       04030015
                   PV-EVNT-NOT-FOUND-CNT.                               04040015
           DISPLAY 'NBR OF RECORDS WRITTEN  -   '                       04050015
                   PV-WRITE-CNT.                                        04051015
           CLOSE MISSING-SKU-UNLOAD-FILE                                04052000
                 SKU-ADD-FILE.                                          04053007
                                                                        04056000
      *----------------------------------------------------------       04057000
       9980-SQL-ERROR.                                                  04058000
      *----------------------------------------------------------       04059000
           MOVE '*9980-SQL-ERROR              *' TO PV-PARA-NAME.       04059107
                                                                        04060000
           MOVE SQLCODE    TO SQLCODE2.                                 04070000
           MOVE SQLERRD(3) TO SQLERRD3.                                 04080000
           DISPLAY '** PROGRAM        ** = PXKUT107'.                   04090000
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  04100000
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  04110000
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   04120000
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  04130000
                                                                        04140000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04150000
           CALL 'ILBOABN0' USING ABEND-CODE.                            04160000
                                                                        04170000
      *                                                                 04180000
