       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP026.                                         00020000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  10/04/2002.                                       00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **       MERCHANDISE HIERARCHY MESSAGE FOR TABLE XST_BUS_AREA     00080001
      ******************************************************************00090000
      **THIS IS A CALLED MODULE FOR BUS AREA MERCH HIER TABLES.  THIS   00100001
      **     PROGRAM WILL PERFORM UPDATES/INSERTS/DELETES FOR THE       00110003
      **     TABLE.  IT DOES THE FOLLOWING WITH THESE RETURN CODES:     00120000
      **     0000 - END PROGRAM                                         00130000
      **    -0530 - END PROGRAM                                         00140000
      **    -0803 - COMPARE TIMESTAMPS AND UPDATE IF MESSAGE IS NEWER   00150000
      **    -0911 - RETRY INSERT UP TO 5 TIMES AND THEN END PROGRAM     00160000
      ******************************************************************00170000
      **10/04/2002 CHANGED BY: BEN BENTLEY         PROGRAM WRITE        00180000
010103**01/03/2003 CHANGED BY: JULIE SMITH  TIMESTAMP ADDITION          00190010
      ******************************************************************00200000
       ENVIRONMENT DIVISION.                                            00210000
       CONFIGURATION SECTION.                                           00220000
       SOURCE-COMPUTER.  IBM-3090.                                      00230000
       OBJECT-COMPUTER.  IBM-3090.                                      00240000
       DATA DIVISION.                                                   00250000
                                                                        00260000
       WORKING-STORAGE SECTION.                                         00270000
      ******************************************************************00280000
      **PROGRAM SWITCHES                                                00290000
      ******************************************************************00300000
       01  PS-PROGRAM-SWITCHES.                                         00310000
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00320000
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00330000
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00340000
      ******************************************************************00350000
      **PROGRAM COUNTERS                                                00360000
      ******************************************************************00370000
       01  PC-PROGRAM-COUNTERS.                                         00380000
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00390000
           05  PC-RETRY-COUNTER-2          PIC 9(01)  VALUE ZEROS.      00400000
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00410000
      ******************************************************************00420000
      **PROGRAM VARIABLES                                               00430000
      ******************************************************************00440000
       01  PV-PROGRAM-VARIABLES.                                        00450000
           05  PV-PARAGRAPH-NAME               PIC X(20)  VALUE SPACES. 00460000
           05  PV-TIME-EVALUATE-INSERT.                                 00470000
               10  PV-TIME-HOUR-IN             PIC  9(02) VALUE ZEROS.  00480000
               10  PV-TIME-MINUTE-IN           PIC  9(02) VALUE ZEROS.  00490000
               10  PV-TIME-SECOND-IN           PIC  9(02) VALUE ZEROS.  00500000
               10  PV-TIME-MICROSEC-IN         PIC  9(06) VALUE ZEROS.  00510000
           05  PV-TIME-EVALUATE-UPDATE.                                 00520000
               10  PV-TIME-HOUR-UP             PIC  9(02) VALUE ZEROS.  00530000
               10  PV-TIME-MINUTE-UP           PIC  9(02) VALUE ZEROS.  00540000
               10  PV-TIME-SECOND-UP           PIC  9(02) VALUE ZEROS.  00550000
               10  PV-TIME-MICROSEC-UP         PIC  9(06) VALUE ZEROS.  00560000
      ******************************************************************00570000
      **TIMESTAMP VARIABLES                                             00580000
      ******************************************************************00590000
       01  SD-TIMESTAMP.                                                00600012
           05  SD-CURR-DTE                     PIC  X(10).              00610000
           05  FILLER                          PIC  X(01) VALUE '-'.    00620012
           05  SD-HOUR                         PIC  9(02).              00630012
           05  FILLER                          PIC  X(01) VALUE '.'.    00640012
           05  SD-MINUTE                       PIC  9(02).              00650012
           05  FILLER                          PIC  X(01) VALUE '.'.    00660012
           05  SD-SECOND                       PIC  9(02).              00670012
           05  FILLER                          PIC  X(01) VALUE '.'.    00680012
           05  SD-MICROSEC                     PIC  9(06).              00690012
      ******************************************************************00700000
      **COPYBOOKS                                                       00710000
      ******************************************************************00720000
      ***DB2 MESSAGE AREA                                               00730000
           COPY SQLCA2.                                                 00740013
      ***MESSAGE COPYBOOK                                               00750000
           COPY HMRD001.                                                00760013
      ***DATE ROUTINE COPYBOOK                                          00770000
           COPY DPWS500.                                                00780013
      ******************************************************************00790000
      **TABLE INCLUSIONS                                                00800000
      ******************************************************************00810000
      ***DB2 COMMUNICATIONS AREA                                        00820000
           EXEC SQL                                                     00830000
                INCLUDE SQLCA                                           00840000
           END-EXEC.                                                    00850000
      ***XST_BUS_AREA TABLE                                             00860001
           EXEC SQL                                                     00870000
                INCLUDE XST00024                                        00880001
           END-EXEC.                                                    00890000
                                                                        00900000
       LINKAGE SECTION.                                                 00910000
      ******************************************************************00920000
      **LINKAGE VARIABLES                                               00930000
      ******************************************************************00940000
       01  LV-HMRD001-RECORD               PIC X(500).                  00950000
       01  LV-RETURN-CODE                  PIC S9(04).                  00960000
010103 01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00970010
                                                                        00980000
       PROCEDURE DIVISION USING LV-HMRD001-RECORD,                      00990000
                                LV-RETURN-CODE,                         01000010
010103                          LV-CURRENT-TIMESTAMP.                   01010010
      ******************************************************************01020000
       0000-MAINLINE.                                                   01030000
                                                                        01040000
            PERFORM 1000-INITIALIZATION.                                01050000
            PERFORM 2000-PROCESS-MESSAGE                                01060000
                    UNTIL PS-PROCESS-COMPLETE.                          01070000
            PERFORM 9999-WRAPUP.                                        01080000
                                                                        01090000
       EJECT.                                                           01100004
      ***************************************************************** 01110000
      **INITIALIZATION AREA - ALL MOVES TO UPDATE/INSERT                01120000
      ***************************************************************** 01130000
       1000-INITIALIZATION.                                             01140000
                                                                        01150000
           MOVE '1000-INITIALIZATION'      TO PV-PARAGRAPH-NAME.        01160000
                                                                        01170000
           SET PS-PROCESS-CONTINUE         TO TRUE.                     01180000
           INITIALIZE PC-RETRY-COUNTER                                  01190008
                      HMRD001-RECORD                                    01200011
                      SD-TIMESTAMP.                                     01210011
                                                                        01220000
           MOVE LV-HMRD001-RECORD          TO HMRD001-RECORD.           01230000
           MOVE HM001-BUS-AREA-ID          TO XST00024-BUS-AREA-ID.     01240001
           MOVE HM001-BUS-AREA-DESC        TO XST00024-BUS-AREA-DESC.   01250001
                                                                        01260011
010103     MOVE HM001-CRE-TMST             TO SD-CURR-DTE.              01270012
010103     MOVE SD-TIMESTAMP              TO XST00024-SOR-LAST-MSG-TMST.01280011
           INITIALIZE SD-TIMESTAMP.                                     01290011
                                                                        01300000
       EJECT.                                                           01310004
      ***************************************************************** 01320000
      **MAIN PROGRAM LOGIC - READS ACTION TYPE ON HMRD001 RECORD        01330000
      ***************************************************************** 01340000
       2000-PROCESS-MESSAGE.                                            01350000
                                                                        01360000
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01370000
                                                                        01380000
           EVALUATE TRUE                                                01390000
              WHEN HM001-ACTN-TYP-INSERT                                01400000
                   PERFORM 2100-INSERT-DATA                             01410000
              WHEN HM001-ACTN-TYP-UPDATE                                01420005
                   PERFORM 2200-UPDATE-DATA                             01430005
              WHEN HM001-ACTN-TYP-DELETE                                01440000
                   PERFORM 2300-DELETE-DATA                             01450000
           END-EVALUATE.                                                01460000
                                                                        01470000
       EJECT.                                                           01480000
      ***************************************************************** 01490000
      **INSERT ACTION TYPE                                              01500000
      ***************************************************************** 01510000
       2100-INSERT-DATA.                                                01520000
                                                                        01530000
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01540000
                                                                        01550000
           EXEC SQL                                                     01560000
                INSERT INTO XST_BUS_AREA (BUS_AREA_ID                   01570001
                                        , BUS_AREA_DESC                 01580010
010103                                  , SOR_LAST_MSG_TMST)            01590010
                VALUES (:XST00024-BUS-AREA-ID                           01600001
                      , :XST00024-BUS-AREA-DESC                         01610010
010103                , :XST00024-SOR-LAST-MSG-TMST)                    01620010
           END-EXEC.                                                    01630000
                                                                        01640000
           EVALUATE TRUE                                                01650000
      ***SUCCESSFUL PROCESSING                                          01660010
              WHEN SQLCODE = ZEROS                                      01670000
                   SET PS-PROCESS-COMPLETE   TO TRUE                    01680000
      ***REFERENTIAL INTEGRITY ISSUES                                   01690010
              WHEN SQLCODE = -530                                       01700000
              WHEN SQLCODE = -532                                       01710010
                   SET PS-PROCESS-COMPLETE   TO TRUE                    01720000
      ***ROW ALREADY EXISTS                                             01730010
              WHEN SQLCODE = -803                                       01740005
                   PERFORM 2200-UPDATE-DATA                             01750005
      ***ROW/TABLE LOCK                                                 01760010
              WHEN SQLCODE = -913                                       01770000
              WHEN SQLCODE = -911                                       01780000
                   IF  PC-RETRY-COUNTER >= PC-RETRY-MAX                 01790000
                       SET PS-PROCESS-COMPLETE TO TRUE                  01800000
                   ELSE                                                 01810000
                       ADD +1                  TO PC-RETRY-COUNTER      01820000
                   END-IF                                               01830000
              WHEN OTHER                                                01840000
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01850000
           END-EVALUATE.                                                01860000
                                                                        01870000
       EJECT.                                                           01880004
      ******************************************************************01890005
      **UPDATE ACTION TYPE                                              01900005
      ******************************************************************01910005
       2200-UPDATE-DATA.                                                01920005
                                                                        01930005
           MOVE '2200-UPDATE-DATA' TO PV-PARAGRAPH-NAME                 01940005
                                                                        01950005
           EXEC SQL                                                     01960005
              UPDATE XST_BUS_AREA                                       01970005
                  SET   BUS_AREA_DESC = :XST00024-BUS-AREA-DESC         01980005
010103                , SOR_LAST_MSG_TMST = :XST00024-SOR-LAST-MSG-TMST 01990010
                  WHERE BUS_AREA_ID   = :XST00024-BUS-AREA-ID           02000005
           END-EXEC.                                                    02010005
                                                                        02020005
           EVALUATE TRUE                                                02030005
      ***SUCCESSFUL PROCESSING                                          02040010
              WHEN SQLCODE = ZEROS                                      02050005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02060005
      ***ROW/TABLE LOCK                                                 02070010
              WHEN SQLCODE = -911                                       02080005
              WHEN SQLCODE = -913                                       02090009
                   IF  PC-RETRY-COUNTER-2 >= PC-RETRY-MAX               02100005
                       SET PS-PROCESS-COMPLETE TO TRUE                  02110005
                   ELSE                                                 02120005
                       ADD +1                  TO PC-RETRY-COUNTER-2    02130005
                   END-IF                                               02140005
              WHEN OTHER                                                02150005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02160005
           END-EVALUATE.                                                02170005
                                                                        02180005
       EJECT.                                                           02190005
      ******************************************************************02200000
      **DELETE TYPE ACTION                                              02210000
      ******************************************************************02220000
       2300-DELETE-DATA.                                                02230000
                                                                        02240000
           MOVE '2300-DELETE-DATA' TO PV-PARAGRAPH-NAME                 02250000
                                                                        02260000
           EXEC SQL                                                     02270000
                DELETE FROM XST_BUS_AREA                                02280001
                WHERE BUS_AREA_ID = :XST00024-BUS-AREA-ID               02290001
           END-EXEC.                                                    02300000
                                                                        02310000
           EVALUATE TRUE                                                02320000
      ***SUCCESSFUL PROCESSING                                          02330010
              WHEN SQLCODE = ZEROS                                      02340000
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02350000
      ***NO ROW TO DELETE                                               02360010
              WHEN SQLCODE = +100                                       02370000
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02380000
      ***ROW/TABLE LOCK                                                 02390010
              WHEN SQLCODE = -911                                       02400000
              WHEN SQLCODE = -913                                       02410009
                   IF  PC-RETRY-COUNTER-2 >= PC-RETRY-MAX               02420000
                       SET PS-PROCESS-COMPLETE TO TRUE                  02430000
                   ELSE                                                 02440000
                       ADD +1                  TO PC-RETRY-COUNTER-2    02450000
                   END-IF                                               02460000
              WHEN OTHER                                                02470000
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02480000
           END-EVALUATE.                                                02490000
                                                                        02500000
       EJECT.                                                           02510004
                                                                        02520001
      ******************************************************************02530000
      **PASS SQLCODE TO CALLING AND EXIT PROGRAM                        02540000
      ******************************************************************02550000
       9999-WRAPUP.                                                     02560000
                                                                        02570000
           MOVE '9999-WRAPUP' TO PV-PARAGRAPH-NAME                      02580000
                                                                        02590000
           MOVE SQLCODE       TO LV-RETURN-CODE.                        02600000
           DISPLAY 'XSKUP026 - BUSINESS AREA ID: ' XST00024-BUS-AREA-ID 02610004
                            ' WITH RETURN-CODE ' LV-RETURN-CODE.        02620004
           EXIT PROGRAM.                                                02630001
                                                                        02640000
       EJECT.                                                           02650004
