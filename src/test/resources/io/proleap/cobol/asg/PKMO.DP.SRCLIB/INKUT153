       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT153.                                       
       AUTHOR.                  DAN GRUBER.                                     
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/23/97.                                       
       DATE-COMPILED.                                                           
      *                                                                         
      ******************************************************************        
      *  THIS PROGRAM WILL DO THE BALANCING OF THE TOTALS EXPECTED              
      *  FROM THE STORE AND WHAT WAS ACTUALLY TRANSMITTED TO THE                
      *  STORES VIA FTP.                                                        
      *                                                                         
      *  03/09/99 - MARIA KLAMIK                                                
      *             UPDATED INWS018 AND INWS18O COPYBOOKS WITH NEW              
      *             TIMESTAMP FIELDS.                                           
      *  05/26/99 - AMY LAWSON                                                  
      *             ADDED RETURN CODE IF BALANCING DOESN'T WORK.                
      *  04/03/00 - JULIE SEIDLER                                               
      *             ADDED LOGIC TO SUPPORT A FILE-BALANCE-CODE ON               
      *             TINVPAR.  ALSO, A FILE WILL BE WRITTEN OUT                  
      *             WHEN TOTALS DO NOT BALANCE                                  
JP0700*  07/17/00 - JOHN PRICE                                                  
JP0700*             ADDED LOGIC TO NOT ABEND IF NO TOTALS ENTERED ON THE        
JP0700*             TINVLAP TABLE, BUT RATHER PUT OUT AN ERROR MESSAGE          
JP0700*             (ON EMAIL) ALONG WITH THE CALCULATED TOTALS FROM            
JP0700*             THE INPUT RECORD.                                           
W21732* DATE  01/20/2003                                                        
W21732*     MADE BY: SUKUMAR,IGSI            WR/IR #: WR21732                   
W21732*     ADDED NON INTELLIGENT SKU IN PLACE OF DEPT,CLASS,SEQ.       INKUT120
25248 * 25248    10-06-2003  UPC CAPABILITIES & STORE NUMBER EXPANSION          
25248 *                      -ROD JANSSEN                                       
W26600*                                                                         
W26600*  W26600  06-  -2004  PHYSICAL INVENTORY STORE EXPANSION                 
W26600*                      M JOHNSON                                          
W26600*                                                                         
W28545*                                                                         
W28545*          04-  -2006  CHANGE PROGRAM FOR DATA AND KEY CHANGES TO         
W28545*                      TINVPAR AND TINVLAP, UPDATE STATUS ON              
W28545*                      TINVLAP AND ADD CONTROL FILE FOR STORE             
W28545*                      NUMBER (LOC) AND PLND INV DTE                      
W28545*                      B. GRAHAM - STRATAGEM                              
P28545* W28545 03-24-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE SETUP        
P28545*                               (TINCNTL/TINVPAR MODIFICATIONS)           
      *  10/10/07 - TODD C. MICHALEK      WR/IR #: INC000000064980              
      *             - ADDED PROCESSING (PS-TINVLAP-TOTALS-PROCESSED)            
      *               TO BYPASS A STORE THAT HAS ALREADY BEEN                   
      *               PROCESSED (INVPAR-FIL-TOT-BAL-CDE = 'Y').  CICS           
      *               IS SOME HOW ALLOWING THIS INVALID CONDITION TO            
      *               TAKE PLACE.                                               
      *             - ADDED AN INFORMATIONAL EMAIL TO IS-IN-ALERT TO            
      *               NOTE WHEN THE ATTEMPT TO PROCESS A STORE FOR A            
      *               SECOND TIME TAKES PLACE                                   
      *             - REMOVED ALL OTHER EMAIL MESSAGES (NOT IN BALANCE          
      *               EMAILS).  THESE BECAME UNNECESSARY WITH THE               
      *               ADDITION OF THE PHYSICAL COUNT TOTALS - OPS CICS          
      *               SCREEN.                                                   
      *  01/23/08 - TODD C. MICHALEK      WR/IR #: INC000000233552              
      *             - IN 9000-END-OF-JOB, ADD BACK A RETURN CODE OF             
      *               4060 IF THE TOTALS ON A LOCATION'S TRANSMISSION           
      *               FILE DO NOT BALANCE TO THE CONTROL TOTALS ENTERED         
      *               BY A STORE.  THIS RETURN CODE WILL KEEP INK908            
      *               FROM CLEANING UP FILES, IN THIS OUT OF BALANCE            
      *               CONDITION, ALLOWING INK918 TO BE RUN.  INK918             
      *               CHECKS TO SEE IF THE FILE/CONTROL TOTALS ENTERED          
      *               OUT OF BALANCE CONDITION HAS BEEN RESOLVED.               
      * W33304   05/01/2008  JILL LIN                                           
      *                      HOLDING INVENTORY LEDGER OPEN ACROSS               
      *                      PERIODS INVENTORY SHORTAGE REPORTING               
      ******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT IN-INV-DATA-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
W28545     SELECT INP-CONTROL-FILE1                                             
W28545         ASSIGN TO INP02.                                                 
W28545                                                                          
           SELECT OUT-INV-VALID-DATA-FILE                                       
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT OUT-EMAIL-MESSAGE-FILE                                        
               ASSIGN TO OUT02.                                                 
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  IN-INV-DATA-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IN-INV-DATA-REC                PIC X(58).                            
W28545                                                                          
W28545 FD  INP-CONTROL-FILE1                                                    
W28545     LABEL RECORDS ARE STANDARD                                           
W28545     RECORDING MODE IS F                                                  
W28545     BLOCK  CONTAINS 0 RECORDS                                            
W28545     RECORD CONTAINS 80 CHARACTERS                                        
W28545     DATA RECORD IS INP-CONTROL-RECORD1.                                  
W28545                                                                          
W28545 01  INP-CONTROL-RECORD1            PIC X(80).                            
      *                                                                         
      *                                                                         
       FD  OUT-INV-VALID-DATA-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  OUT-INV-VALID-DATA-REC         PIC X(58).                            
      *                                                                         
       FD  OUT-EMAIL-MESSAGE-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  OUT-EMAIL-MESSAGE-REC          PIC X(80).                            
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES             PIC S9(03)       VALUE +3             
                                                           COMP SYNC.           
JS0400     05  PC-PROGRAM-ID              PIC X(08)        VALUE                
               'INKUT153'.                                                      
JS0400     05  PC-EMPTY-DATE              PIC X(10)        VALUE                
               '9999-09-09'.                                                    
W28545     05  PC-LPTP-ST-KEY             PIC X(02)        VALUE 'ST'.          
W28545     05  PC-LPTP-FI-KEY             PIC X(02)        VALUE 'FI'.          
W33304     05  PC-STAT-CDE-SUCCESS        PIC  X(02)  VALUE 'BL'.               
W33304     05  PC-STAT-CDE-FAILURE        PIC  X(02)  VALUE 'NB'.               
W28545*    05  PC-INV-TYP-CDE-SUCCESS     PIC  X(02)  VALUE '14'.               
W28545*    05  PC-INV-TYP-CDE-FAILURE     PIC  X(02)  VALUE '15'.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
JS0400     05  PS-FIL-TOT-BAL-CDE              PIC X(01)   VALUE 'X'.           
JS0400         88  PS-FIL-TOT-BAL                          VALUE 'Y'.           
JS0400         88  PS-FIL-TOT-NOT-BAL                      VALUE 'N'.           
           05  PS-END-OF-INV-FILE-SW           PIC X(01)   VALUE 'N'.           
               88  PS-END-OF-INV-FILE                      VALUE 'Y'.           
               88  PS-NOT-END-OF-INV-FILE                  VALUE 'N'.           
           05  PS-TINVLAP-TOTALS-FOUND-SW      PIC X(01)   VALUE 'N'.           
               88  PS-TINVLAP-TOTALS-FOUND                 VALUE 'Y'.           
               88  PS-TINVLAP-TOTALS-NOT-FOUND             VALUE 'N'.           
               88  PS-TINVLAP-TOTALS-PROCESSED             VALUE 'D'.           
           05  PS-DUP-DATA-EXISTS-SW           PIC X(01)   VALUE 'N'.           
               88  PS-DUP-DATA-EXISTS                      VALUE 'Y'.           
               88  PS-NO-DUP-DATA-EXISTS                   VALUE 'N'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PROGRAM-NAME            PIC X(08)   VALUE 'INKUT153'.         
           05  PV-PARAGRAPH-NAME          PIC X(30)   VALUE SPACE.              
           05  PV-ABEND-MESSAGE           PIC X(75)   VALUE SPACE.              
           05  PV-DB2-OPERATION-ATTEMPTED PIC X(30)   VALUE SPACE.              
           05  PV-RETRY-COUNT             PIC S9(04)  VALUE +0                  
                                                      COMP.                     
DGTEMP     05  PV-LINE-PRICE              PIC 9(12)V99                          
DGTEMP                                                VALUE ZERO.               
                                                                                
25248      05  PV-TOTAL-DUP-STORE-NBR     PIC X(04)   VALUE SPACE.              
           05  PV-TOTAL-DUP-PRICE         PIC 9(12)V99                          
                                                      VALUE ZERO.               
           05  PV-TOTAL-DUP-QTY           PIC 9(12)   VALUE ZERO.               
           05  PV-TOTAL-DUP-RECORDS       PIC 9(12)   VALUE ZERO.               
           05  PV-TOTAL-DUP-FMT-RECORDS   PIC ZZZ,ZZZ,ZZZ,ZZ9                   
                                                      VALUE SPACE.              
                                                                                
           05  PV-TOTAL-DUP-FMT-PRICE     PIC $$$,$$$,$$$,$$9.99                
                                                      VALUE SPACE.              
           05  PV-TOTAL-DUP-FMT-QTY       PIC ZZZ,ZZZ,ZZZ,ZZ9                   
                                                      VALUE SPACE.              
                                                                                
25248      05  PV-TOTAL-INV-STORE-NBR     PIC X(04)   VALUE SPACE.              
           05  PV-TOTAL-INV-PRICE         PIC 9(12)V99                          
                                                      VALUE ZERO.               
           05  PV-TOTAL-INV-QTY           PIC 9(12)   VALUE ZERO.               
           05  PV-TOTAL-INV-RECORDS       PIC 9(12)   VALUE ZERO.               
           05  PV-TOTAL-INV-FMT-RECORDS   PIC ZZZ,ZZZ,ZZZ,ZZ9                   
                                                      VALUE SPACE.              
                                                                                
           05  PV-TOTAL-INV-FMT-PRICE     PIC $$$,$$$,$$$,$$9.99                
                                                      VALUE SPACE.              
           05  PV-TOTAL-INV-FMT-QTY       PIC ZZZ,ZZZ,ZZZ,ZZ9                   
                                                      VALUE SPACE.              
                                                                                
           05  PV-TINVLAP-TOTAL-PRICE     PIC 9(12)V99                          
                                                      VALUE ZERO.               
           05  PV-TINVLAP-TOTAL-QTY       PIC 9(12)   VALUE ZERO.               
                                                                                
           05  PV-TINVLAP-TOTAL-FMT-PRICE PIC $$$,$$$,$$$,$$9.99                
                                                      VALUE SPACE.              
           05  PV-TINVLAP-TOTAL-FMT-QTY   PIC ZZZ,ZZZ,ZZZ,ZZ9                   
                                                      VALUE SPACE.              
                                                                                
                                                                                
           05  PV-PREV-INV-REC            PIC X(48)   VALUE SPACE.              
                                                                                
W28545     05  PV-INV-STR-LOC-COMP        PIC  S9(04) VALUE ZEROS               
W28545                                                COMP SYNC.                
W28545     05  PV-TINVLAP-STR-TRSM-IND    PIC   X(01) VALUE SPACE.              
                                                                                
       01  ABEND-CODE                     PIC S9(04)  COMP SYNC                 
                                                      VALUE ZERO.               
                                                                                
       01  DL-EMAIL-MESSAGE-II.                                                 
           05  FILLER                     PIC X(40)   VALUE                     
           'NO ADDITIONAL TOTAL PROCESSING FOR ALREA'.                          
           05  FILLER                     PIC X(22)   VALUE                     
           'DY PROCESSED -- STORE '.                                            
25248      05  DL-EMAIL-STORE-II          PIC X(04).                            
                                                                                
       01  DL-EMAIL-MESSAGE-FILLER.                                             
           05  FILLER                     PIC X(80)   VALUE SPACES.             
W28545                                                                          
W28545***************************************************************** 00120000
W28545*  CONTROL FILE ONE LAYOUT - INPUT.                               00130000
W28545***************************************************************** 00140000
W28545                                                                          
W28545 01  CR-CONTROL-RECORD1.                                                  
W28545     05  CR1-INSTR                     PIC X(04)   VALUE SPACES.          
W28545     05  FILLER                        PIC X(03)   VALUE SPACES.          
W28545     05  CR1-INJOB                     PIC X(02)   VALUE SPACES.          
W28545     05  FILLER                        PIC X(01)   VALUE SPACES.          
W28545     05  CR1-PLND-INV-DTE              PIC X(10)   VALUE SPACES.          
W28545     05  FILLER                        PIC X(60)   VALUE SPACES.          
W28545                                                                          
      ******************************************************************        
      * WORKING STORAGE AREA FOR THE INVENTORY FILES                            
      ******************************************************************        
                                                                                
           COPY INWS018.                                                        
                                                                                
      ******************************************************************        
      * WORKING STORAGE AREA FOR THE OUTPUT INVENTORY FILE                      
      ******************************************************************        
                                                                                
25248      COPY INWS018 REPLACING IN018- BY IN018O-.                            
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * DB2 TABLE DCLGENS NEEDED FOR THIS PROGRAM.                              
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVLAP                                                  
           END-EXEC.                                                            
                                                                                
P28545******************************************************************        
P28545* TINCNTL - INVENTORY MASTER CONTROL TABLE                                
P28545******************************************************************        
P28545     EXEC SQL                                                             
P28545         INCLUDE TINCNTL                                                  
P28545     END-EXEC.                                                            
P28545                                                                          
      ******************************************************************        
      * TINVPAR - INVENTORY PARAMETERS INFORMATION                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * SQLCA   - DB2 SQL COMMUNICATIONS AREA                                   
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
            EJECT.                                                              
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
      *                                                                         
      ******************************************************************        
      * MAIN MODULE                                                             
      ******************************************************************        
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
JP0700*    IF PS-TINVLAP-TOTALS-FOUND                                           
           PERFORM 2000-PROCESS-INV-FILE                                        
               UNTIL PS-END-OF-INV-FILE                                         
JP0700*    END-IF.                                                              
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           GOBACK.                                                              
      *                                                                         
      ******************************************************************        
      * OPEN THE FILES USED IN THE PROGRAM AND PERFORM SOME INITIAL             
      * FILE CHECKING.                                                          
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  IN-INV-DATA-FILE                                         
W28545                 INP-CONTROL-FILE1                                        
                OUTPUT OUT-INV-VALID-DATA-FILE                                  
JS0400          OUTPUT OUT-EMAIL-MESSAGE-FILE.                                  
W28545                                                                          
W28545     READ INP-CONTROL-FILE1 INTO CR-CONTROL-RECORD1                       
W28545         AT END                                                           
W28545           MOVE 'CONTROL RECORD IS MISSING' TO PV-ABEND-MESSAGE           
W28545           DISPLAY 'CONTROL RECORD IS MISSING'                            
W28545           MOVE +4080 TO ABEND-CODE                                       
W28545           PERFORM 9999-ABEND.                                            
W28545                                                                          
W28545     CLOSE INP-CONTROL-FILE1.                                             
W28545                                                                          
W28545     DISPLAY 'CONTROL REC ' CR-CONTROL-RECORD1.                           
W28545                                                                          
           INITIALIZE IN018-INVENTORY-COUNT-RECORD.                             
           INITIALIZE IN018O-INVENTORY-COUNT-RECORD.                            
                                                                                
           PERFORM 7000-READ-INV-DATA-FILE.                                     
                                                                                
25248 *    IF IN018-TTL-RECORD                                                  
  |   *        DISPLAY ' '                                                      
  |   *        DISPLAY '*****************************************'              
  |   *        DISPLAY '*** TTL RECORD IGNORED                ***'              
  |   *        DISPLAY '*****************************************'              
  |   *        DISPLAY ' '                                                      
  |   *        PERFORM 7000-READ-INV-DATA-FILE                                  
  |   *    END-IF.                                                              
25248 *                                                                         
           IF  PS-END-OF-INV-FILE                                               
               DISPLAY ' '                                                      
               DISPLAY '*****************************************'              
               DISPLAY '*** EMPTY INV DATA FILE (INP01)       ***'              
               DISPLAY '*****************************************'              
               DISPLAY ' '                                                      
               MOVE 'EMPTY INV DATA FILE (INP01)'                               
                                       TO PV-ABEND-MESSAGE                      
               MOVE '1000-INITIALIZE'  TO PV-PARAGRAPH-NAME                     
               MOVE +4003              TO ABEND-CODE                            
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM 6000-GET-TOTALS-FROM-TINVLAP.                                
                                                                                
           EVALUATE TRUE                                                        
           WHEN PS-TINVLAP-TOTALS-NOT-FOUND                                     
               DISPLAY ' '                                                      
               DISPLAY '*****************************************'              
               DISPLAY '*** TOTALS ROW NOT FOUND ON TINVLAP   ***'              
               DISPLAY '*****************************************'              
               DISPLAY '***'                                                    
               DISPLAY '*** STORE NUMBER: ' IN018-STORE-NUMBER-X                
               DISPLAY '***'                                                    
               DISPLAY ' '                                                      
           WHEN PS-TINVLAP-TOTALS-PROCESSED                                     
               DISPLAY ' '                                                      
               DISPLAY '*****************************************'              
               DISPLAY '** TOTALS ALREADY PROCESSED ON TINVLAP **'              
               DISPLAY '*****************************************'              
               DISPLAY '***'                                                    
               DISPLAY '*** STORE NUMBER: ' IN018-STORE-NUMBER-X                
               DISPLAY '***'                                                    
               DISPLAY ' '                                                      
           WHEN OTHER                                                           
               PERFORM 8000-WRITE-HEADER-RECORD                                 
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      ******************************************************************        
      * PROCESS THE FILE OF INV RECORDS                                         
      ******************************************************************        
       2000-PROCESS-INV-FILE.                                                   
                                                                                
           IF PV-PREV-INV-REC EQUAL IN018-INVENTORY-COUNT-RECORD                
               SET PS-DUP-DATA-EXISTS  TO TRUE                                  
               ADD 1                   TO PV-TOTAL-DUP-RECORDS                  
               MOVE IN018-STORE-NUMBER-X                                        
                                       TO PV-TOTAL-DUP-STORE-NBR                
                                                                                
DGTEMP         COMPUTE PV-LINE-PRICE = IN018-PRICE * IN018-QUANTITY             
                                                                                
DGTEMP         ADD  PV-LINE-PRICE      TO PV-TOTAL-DUP-PRICE                    
DGTEMP*        ADD  IN018-PRICE        TO PV-TOTAL-DUP-PRICE                    
               ADD  IN018-QUANTITY     TO PV-TOTAL-DUP-QTY                      
           END-IF.                                                              
                                                                                
           MOVE IN018-STORE-NUMBER-X   TO PV-TOTAL-INV-STORE-NBR.               
                                                                                
DGTEMP     COMPUTE PV-LINE-PRICE = IN018-PRICE * IN018-QUANTITY                 
                                                                                
DGTEMP     ADD  PV-LINE-PRICE          TO PV-TOTAL-INV-PRICE.                   
DGTEMP*    ADD  IN018-PRICE            TO PV-TOTAL-INV-PRICE.                   
           ADD  IN018-QUANTITY         TO PV-TOTAL-INV-QTY.                     
           ADD  1                      TO PV-TOTAL-INV-RECORDS.                 
                                                                                
DGTEMP*    INITIALIZE IN018O-INVENTORY-COUNT-RECORD.                            
DGTEMP*    MOVE SPACES TO IN018O-INVENTORY-COUNT-RECORD.                        
           MOVE SPACES TO IN018O-STORE-NUMBER-X.                                
           MOVE SPACES TO IN018O-AREA-NUMBER-X.                                 
           MOVE SPACES TO IN018O-LINE-NUMBER-X.                                 
W21732     MOVE SPACES TO IN018O-SKU-X.                                         
           MOVE SPACES TO IN018O-QUANTITY-X.                                    
           MOVE SPACES TO IN018O-PRICE-X.                                       
           MOVE SPACES TO IN018O-UPC-CODE-X.                                    
           MOVE SPACES TO IN018O-INVALID-INDICATOR.                             
           MOVE SPACES TO IN018O-BLANK-SHEET-INDICATOR.                         
           MOVE ZERO   TO IN018O-SCAN-DTE.                                      
           MOVE ZERO   TO IN018O-SCAN-TIMESTAMP.                                
                                                                                
           MOVE IN018-INVENTORY-COUNT-RECORD                                    
                                      TO IN018O-INVENTORY-COUNT-RECORD.         
JP0700                                                                          
JP0700     IF  PS-TINVLAP-TOTALS-FOUND                                          
JP0700         PERFORM 8100-WRITE-VALID-INV-DATA-FILE                           
JP0700     END-IF.                                                              
                                                                                
           MOVE IN018-INVENTORY-COUNT-RECORD                                    
                                      TO PV-PREV-INV-REC.                       
                                                                                
           PERFORM 7000-READ-INV-DATA-FILE.                                     
      *                                                                         
      ******************************************************************        
      * DISPLAY THE DUPLICATE DATA INFORMATION                                  
      ******************************************************************        
       2100-DISPLAY-DUP-INFO.                                                   
                                                                                
           IF PS-DUP-DATA-EXISTS                                                
               MOVE PV-TOTAL-DUP-PRICE    TO PV-TOTAL-DUP-FMT-PRICE             
               MOVE PV-TOTAL-DUP-QTY      TO PV-TOTAL-DUP-FMT-QTY               
               MOVE PV-TOTAL-DUP-RECORDS  TO PV-TOTAL-DUP-FMT-RECORDS           
               DISPLAY ' '                                                      
               DISPLAY '**********************************************'         
               DISPLAY '*********** DUPLICATE RECORD INFO ************'         
               DISPLAY '**********************************************'         
               DISPLAY 'STORE NUMBER ----------->                '              
                                             PV-TOTAL-DUP-STORE-NBR             
               DISPLAY 'TOTAL DUPLICATE PRICE---> '                             
                                             PV-TOTAL-DUP-FMT-PRICE             
               DISPLAY 'TOTAL DUPLICATE QTY ---->    '                          
                                             PV-TOTAL-DUP-FMT-QTY               
               DISPLAY 'NUMBER OF DUP RECORDS--->    '                          
                                             PV-TOTAL-DUP-FMT-RECORDS           
               DISPLAY '**********************************************'         
               DISPLAY ' '                                                      
           ELSE                                                                 
               DISPLAY '*********************************'                      
               DISPLAY '*** NO DUPLICATE DATA EXISTS  ***'                      
               DISPLAY '*********************************'                      
           END-IF.                                                              
                                                                                
      *                                                                         
      ******************************************************************        
      *  GET THE TOTAL PRICE AND QUANTITY EXPECTED TO BE TRANSMITTED   *        
      *  FROM THE STORE TO THE MAINFRAME.  THESE TOTALS WERE ENTERED   *        
      *  FROM THE STORE MANAGERS ON THE PHYSICAL COUNT TOTALS CICS     *        
      *  SCREEN (INKCS044).                                            *        
      ******************************************************************        
       6000-GET-TOTALS-FROM-TINVLAP.                                            
                                                                                
           SET  PS-TINVLAP-TOTALS-NOT-FOUND  TO TRUE.                           
           MOVE CR1-INSTR TO PV-INV-STR-LOC-COMP.                               
                                                                                
           INITIALIZE PV-RETRY-COUNT                                            
W28545                INVLAP-INV-ID                                             
                      INVLAP-LPTP-EXT-AMT                                       
                      INVLAP-LPTP-QTY                                           
W28545*               INVLAP-INV-TYP-CDE                                        
W28545                INVLAP-STR-TRSM-IND                                       
                      PV-TINVLAP-TOTAL-PRICE                                    
                      PV-TINVLAP-TOTAL-QTY.                                     
                                                                                
DELETE*    MOVE '2008-05-19' TO CR1-PLND-INV-DTE.                               
           PERFORM WITH TEST AFTER                                              
                   UNTIL ((SQLCODE = ZERO)                                      
                                OR                                              
                          (SQLCODE = +100)                                      
                                OR                                              
                          (PV-RETRY-COUNT > PC-MAX-RETRIES))                    
             EXEC SQL                                                           
W28545           SELECT LAP.INV_ID,                                             
W28545                  LAP.LPTP_EXT_AMT,                                       
W28545                  LAP.LPTP_QTY,                                           
W28545*                 LAP.INV_TYP_CDE,                                        
W28545                  LAP.STR_TRSM_IND,                                       
                        PAR.FIL_TOT_BAL_CDE                                     
W28545           INTO :INVLAP-INV-ID                                            
W28545               ,:INVLAP-LPTP-EXT-AMT                                      
W28545               ,:INVLAP-LPTP-QTY                                          
W28545*              ,:INVLAP-INV-TYP-CDE                                       
W28545               ,:INVLAP-STR-TRSM-IND                                      
                     ,:INVPAR-FIL-TOT-BAL-CDE                                   
W28545           FROM TINVPAR PAR                                               
W28545               ,TINVLAP LAP                                               
P28545               ,TINCNTL T                                                 
P28545           WHERE T.ACTV_IND = 'Y'                                         
P28545             AND T.INV_ID = PAR.INV_ID                                    
P28545             AND PAR.LOC_NBR = :PV-INV-STR-LOC-COMP                       
W28545             AND PAR.PLND_INV_DTE = :CR1-PLND-INV-DTE                     
W28545             AND PAR.INV_ID  = LAP.INV_ID                                 
W28545             AND PAR.LOC_NBR = LAP.LOC_NBR                                
W28545             AND LAP.LPTP_NBR = :PC-LPTP-ST-KEY                           
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = 0                                               
                      IF INVPAR-FIL-TOT-BAL-CDE = 'Y'                           
                          SET PS-TINVLAP-TOTALS-PROCESSED TO TRUE               
                      ELSE                                                      
                          SET PS-TINVLAP-TOTALS-FOUND TO TRUE                   
                      END-IF                                                    
                      MOVE INVLAP-LPTP-EXT-AMT                                  
                                     TO PV-TINVLAP-TOTAL-PRICE                  
                      MOVE INVLAP-LPTP-QTY                                      
                                     TO PV-TINVLAP-TOTAL-QTY                    
W28545                MOVE INVLAP-STR-TRSM-IND                                  
W28545                               TO PV-TINVLAP-STR-TRSM-IND                 
                 WHEN SQLCODE = +100                                            
                      SET PS-TINVLAP-TOTALS-NOT-FOUND TO TRUE                   
                 WHEN SQLCODE = -911                                            
                 WHEN SQLCODE = -913                                            
                      ADD +1 TO PV-RETRY-COUNT                                  
                 WHEN SQLWARN0 NOT = SPACES                                     
                 WHEN SQLCODE < ZERO                                            
                 WHEN SQLCODE > ZERO                                            
                      MOVE '6000-GET-TOTALS-FROM-TINVLAP'                       
                                     TO PV-PARAGRAPH-NAME                       
                      MOVE 'SELECT TOTALS FROM TINVLAP'                         
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                      PERFORM 9999-SQL-ABEND                                    
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF  SQLCODE = -911                                                   
            OR SQLCODE = -913                                                   
               MOVE '6000-GET-TOTALS-FROM-TINVLAP'                              
                                     TO PV-PARAGRAPH-NAME                       
               MOVE 'MAX RETRIES EXCEEDED'                                      
                                     TO PV-DB2-OPERATION-ATTEMPTED              
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *                                                                         
      ******************************************************************        
      *  READ THE INV DATA FILE.                                       *        
      ******************************************************************        
       7000-READ-INV-DATA-FILE.                                                 
                                                                                
           READ IN-INV-DATA-FILE                                                
               INTO IN018-INVENTORY-COUNT-RECORD                                
               AT END                                                           
                   SET PS-END-OF-INV-FILE TO TRUE                               
           END-READ.                                                            
                                                                                
      *                                                                         
      ******************************************************************        
      *  WRITE THE HEADER RECORD ONTO THE OUTPUT FILE.  THIS RECORD    *        
      *  WILL CONTAIN THE STORE NUMBER BEING INVENTORIED, THE TOTAL    *        
      *  PRICE AND TOTAL QUANTITY.                                     *        
      ******************************************************************        
       8000-WRITE-HEADER-RECORD.                                                
                                                                                
DGTEMP*    INITIALIZE IN018O-INVENTORY-COUNT-RECORD.                            
DGTEMP     MOVE SPACES TO IN018O-INVENTORY-COUNT-RECORD.                        
                                                                                
           SET  IN018O-HDR-RECORD          TO TRUE.                             
                                                                                
           MOVE IN018-STORE-NUMBER-X       TO IN018O-HDR-STORE.                 
           MOVE PV-TINVLAP-TOTAL-PRICE     TO IN018O-HDR-RETAIL.                
           MOVE PV-TINVLAP-TOTAL-QTY       TO IN018O-HDR-QTY.                   
                                                                                
           PERFORM 8100-WRITE-VALID-INV-DATA-FILE.                              
                                                                                
      *                                                                         
      ******************************************************************        
      *  WRITE A RECORD TO THE OUTPUT FILE.                            *        
      ******************************************************************        
       8100-WRITE-VALID-INV-DATA-FILE.                                          
                                                                                
           WRITE OUT-INV-VALID-DATA-REC                                         
               FROM IN018O-INVENTORY-COUNT-RECORD.                              
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       8155-WRITE-EMAIL-MESSAGE-II.                                             
           WRITE OUT-EMAIL-MESSAGE-REC                                          
               FROM DL-EMAIL-MESSAGE-FILLER.                                    
                                                                                
W28545     MOVE CR1-INSTR              TO DL-EMAIL-STORE-II.                    
           WRITE OUT-EMAIL-MESSAGE-REC                                          
               FROM DL-EMAIL-MESSAGE-II.                                        
      ******************************************************************        
      *                                                                         
      ******************************************************************        
      *                                                                         
       8200-UPDATE-TINVPAR.                                                     
W28545                                                                          
W28545* INVLAP INV ID BELOW WAS POPULATED IN PARA 6000                          
W28545* BY THE JOIN OF TINVPAR AND TINVLAP                                      
                                                                                
           EXEC SQL                                                             
               UPDATE TINVPAR                                                   
JS0400             SET FIL_TOT_BAL_CDE  = :PS-FIL-TOT-BAL-CDE,                  
JS0400                 CHG_ID_NBR       = :PC-PROGRAM-ID,                       
JS0400                 CHG_TMST         = CURRENT TIMESTAMP                     
W28545            WHERE INV_ID       = :INVLAP-INV-ID                           
W28545              AND LOC_NBR      = :PV-INV-STR-LOC-COMP                     
W28545              AND PLND_INV_DTE = :CR1-PLND-INV-DTE                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
                 WHEN SQLCODE = 0                                               
                      DISPLAY '****************'                                
                      DISPLAY 'TINVPAR ROW UPDATED'                             
                      DISPLAY '****************'                                
                 WHEN SQLCODE = +100                                            
                      DISPLAY '****************'                                
                      DISPLAY 'TINVPAR ROW NOT FOUND FOR STORE '                
W28545                        PV-INV-STR-LOC-COMP                               
W28545                        ' WITH PLND-INV-DTE '                             
W28545                        CR1-PLND-INV-DTE                                  
                      DISPLAY '****************'                                
                      MOVE '8200-UPDATE-TINVPAR'                                
                                     TO PV-PARAGRAPH-NAME                       
                      MOVE 'UPD FIL-TOT-IND ON TINVLAP'                         
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                      PERFORM 9999-SQL-ABEND                                    
                 WHEN OTHER                                                     
                      MOVE '8200-UPDATE-TINVPAR'                                
                                     TO PV-PARAGRAPH-NAME                       
                      MOVE 'UPD FIL-TOT-IND ON TINVLAP'                         
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                      PERFORM 9999-SQL-ABEND                                    
             END-EVALUATE.                                                      
W28545*                                                                         
W28545*  8300 UPDATE THE STORE TOTAL ROW WITH BALANCE INDICATOR                 
W28545*                                                                         
W28545 8300-UPDATE-TINVLAP.                                                     
W28545*                                                                         
W28545* INVLAP INV ID BELOW WAS POPULATED IN PARA 6000                          
W28545* BY THE JOIN OF TINVPAR AND TINVLAP                                      
W28545                                                                          
W28545     IF PS-FIL-TOT-BAL                                                    
W28545         MOVE PC-STAT-CDE-SUCCESS     TO                                  
W28545                          INVLAP-INV-STAT-CDE                             
W28545*        MOVE PC-INV-TYP-CDE-SUCCESS  TO                                  
W28545*                         INVLAP-INV-TYP-CDE                              
W28545     ELSE                                                                 
W28545         MOVE PC-STAT-CDE-FAILURE TO                                      
W28545                          INVLAP-INV-STAT-CDE                             
W28545*        MOVE PC-INV-TYP-CDE-FAILURE  TO                                  
W28545*                         INVLAP-INV-TYP-CDE                              
W28545     END-IF.                                                              
W28545                                                                          
W28545     EXEC SQL                                                             
W28545         UPDATE TINVLAP                                                   
W33304             SET INV_STAT_CDE  = :INVLAP-INV-STAT-CDE                     
W28545*               ,INV_TYP_CDE   = :INVLAP-INV-TYP-CDE                      
W28545            WHERE INV_ID       = :INVLAP-INV-ID                           
W28545              AND LOC_NBR      = :PV-INV-STR-LOC-COMP                     
W28545              AND LPTP_NBR     = :PC-LPTP-ST-KEY                          
W28545     END-EXEC.                                                            
W28545                                                                          
W28545     EVALUATE TRUE                                                        
W28545           WHEN SQLCODE = 0                                               
W28545                DISPLAY '****************'                                
W28545                DISPLAY 'TINVLAP ROW ST UPDATED'                          
W28545                DISPLAY '****************'                                
W28545                PERFORM 8350-TINVLAP-INSERT-FI-ROW                        
W28545           WHEN SQLCODE = +100                                            
W28545                DISPLAY '****************'                                
W28545                DISPLAY 'TINVLAP ROW ST NOT FOUND FOR STORE '             
W28545                        PV-INV-STR-LOC-COMP                               
W28545                        ' WITH INV -ID OF ' INVLAP-INV-ID                 
W28545                DISPLAY '****************'                                
W28545                MOVE '8300-UPDATE-TINVLAP'                                
W28545                               TO PV-PARAGRAPH-NAME                       
W28545                MOVE 'UPD INV STAT CDE ON TINVLAP'                        
W28545                               TO PV-DB2-OPERATION-ATTEMPTED              
W28545                PERFORM 9999-SQL-ABEND                                    
W28545           WHEN OTHER                                                     
W28545                MOVE '8300-UPDATE-TINVPAR'                                
W28545                               TO PV-PARAGRAPH-NAME                       
W28545                MOVE 'UPD INV STAT CDE ON TINVLAP'                        
W28545                               TO PV-DB2-OPERATION-ATTEMPTED              
W28545                PERFORM 9999-SQL-ABEND                                    
W28545       END-EVALUATE.                                                      
W28545*                                                                         
W28545*                                                                         
W28545*  8350 INSERT THE FILE TOTAL ROW WITH BALANCE INDICATOR                  
W28545*                                                                         
W28545 8350-TINVLAP-INSERT-FI-ROW.                                              
W28545                                                                          
W28545* INVLAP INV-ID BELOW WAS POPULATED IN PARA 6000                          
W28545* BY THE JOIN OF TINVPAR AND TINVLAP                                      
W28545* INVLAP-INV-STAT-CDE ALREADY SET UP IN 8300                              
W28545                                                                          
W28545     MOVE PV-INV-STR-LOC-COMP TO INVLAP-LOC-NBR.                          
W28545     MOVE PC-LPTP-FI-KEY      TO INVLAP-LPTP-NBR.                         
W28545     MOVE PV-TOTAL-INV-PRICE  TO INVLAP-LPTP-EXT-AMT.                     
W28545     MOVE PV-TOTAL-INV-QTY    TO INVLAP-LPTP-QTY.                         
W28545     MOVE PV-TINVLAP-STR-TRSM-IND                                         
W28545                              TO INVLAP-STR-TRSM-IND.                     
W28545                                                                          
W28545                                                                          
W28545     EXEC SQL                                                             
W28545         INSERT                                                           
W28545           INTO TINVLAP                                                   
W28545             (INV_ID                                                      
W28545             ,LOC_NBR                                                     
W28545             ,LPTP_NBR                                                    
W28545             ,LPTP_EXT_AMT                                                
W28545             ,LPTP_QTY                                                    
W28545*            ,INV_TYP_CDE                                                 
W28545             ,INV_STAT_CDE                                                
W28545             ,CRE_TMST                                                    
W28545             ,STR_TRSM_IND)                                               
W28545           VALUES                                                         
W28545             (:INVLAP-INV-ID                                              
W28545             ,:INVLAP-LOC-NBR                                             
W28545             ,:INVLAP-LPTP-NBR                                            
W28545             ,:INVLAP-LPTP-EXT-AMT                                        
W28545             ,:INVLAP-LPTP-QTY                                            
W28545*            ,:INVLAP-INV-TYP-CDE                                         
W28545             ,:INVLAP-INV-STAT-CDE                                        
W28545             , CURRENT TIMESTAMP                                          
W28545             ,:INVLAP-STR-TRSM-IND)                                       
W28545                                                                          
W28545     END-EXEC.                                                            
W28545                                                                          
W28545     EVALUATE TRUE                                                        
W28545           WHEN SQLCODE = 0                                               
W28545                DISPLAY '****************'                                
W28545                DISPLAY 'TINVLAP ROW FI INSERTED '                        
W28545                DISPLAY '****************'                                
W28545           WHEN SQLCODE = -803                                            
W28545                DISPLAY '****************'                                
W28545                DISPLAY 'TINVLAP ROW FI FOUND FOR STORE '                 
W28545                         INVLAP-LOC-NBR                                   
W28545                        ' WITH INV -ID OF ' INVLAP-INV-ID                 
W28545                DISPLAY '****************'                                
W28545                PERFORM 8375-TINVLAP-UPDATE-FI-ROW                        
W28545           WHEN OTHER                                                     
W28545                MOVE '8350-TINVPAR-INSERT-FI-ROW '                        
W28545                               TO PV-PARAGRAPH-NAME                       
W28545                MOVE 'UPD INV STAT CDE ON TINVLAP'                        
W28545                               TO PV-DB2-OPERATION-ATTEMPTED              
W28545                PERFORM 9999-SQL-ABEND                                    
W28545       END-EVALUATE.                                                      
W28545                                                                          
W28545*                                                                         
W28545*                                                                         
W28545*  8375 UPDATE THE FILE TOTAL ROW WITH BALANCE INDICATOR                  
W28545*       AND THE QTY AND DOLLAR AMOUNT                                     
W28545*                                                                         
W28545 8375-TINVLAP-UPDATE-FI-ROW.                                              
W28545*                                                                         
W28545* INVLAP INV ID BELOW WAS POPULATED IN PARA 6000                          
W28545* BY THE JOIN OF TINVPAR AND TINVLAP                                      
W28545* OTHER ATTRIBUTES ALREADY SET UP IN 8300 AND 8350.                       
W28545                                                                          
W28545     EXEC SQL                                                             
W28545         UPDATE TINVLAP                                                   
W28545             SET LPTP_EXT_AMT  = :INVLAP-LPTP-EXT-AMT,                    
W28545                 LPTP_QTY      = :INVLAP-LPTP-QTY,                        
W28545*                INV_TYP_CDE   = :INVLAP-INV-TYP-CDE,                     
W28545                 INV_STAT_CDE  = :INVLAP-INV-STAT-CDE,                    
W28545                 STR_TRSM_IND  = :INVLAP-STR-TRSM-IND                     
W28545            WHERE INV_ID       = :INVLAP-INV-ID                           
W28545              AND LOC_NBR      = :PV-INV-STR-LOC-COMP                     
W28545              AND LPTP_NBR     = :PC-LPTP-FI-KEY                          
W28545     END-EXEC.                                                            
W28545                                                                          
W28545     EVALUATE TRUE                                                        
W28545           WHEN SQLCODE = 0                                               
W28545                DISPLAY '****************'                                
W28545                DISPLAY 'TINVLAP ROW FI UPDATED'                          
W28545                DISPLAY '****************'                                
W28545           WHEN SQLCODE = +100                                            
W28545                DISPLAY '****************'                                
W28545                DISPLAY 'TINVLAP ROW FI NOT FOUND FOR STORE '             
W28545                        PV-INV-STR-LOC-COMP                               
W28545                        ' WITH INV -ID OF ' INVLAP-INV-ID                 
W28545                DISPLAY '****************'                                
W28545                MOVE '8375-TINVLAP-UPDATE-FI-ROW '                        
W28545                               TO PV-PARAGRAPH-NAME                       
W28545                MOVE 'UPD INV STAT CDE ON TINVLAP'                        
W28545                               TO PV-DB2-OPERATION-ATTEMPTED              
W28545                PERFORM 9999-SQL-ABEND                                    
W28545           WHEN OTHER                                                     
W28545                MOVE '8375-TINVPAR-UPDATE-FI-ROW '                        
W28545                               TO PV-PARAGRAPH-NAME                       
W28545                MOVE 'UPD INV STAT CDE ON TINVLAP'                        
W28545                               TO PV-DB2-OPERATION-ATTEMPTED              
W28545                PERFORM 9999-SQL-ABEND                                    
W28545       END-EVALUATE.                                                      
W28545*                                                                         
W28545                                                                          
      ******************************************************************        
      * BALANCE THE TOTALS FROM THE TINVLAP TABLE WITH THE TOTALS               
      * SENT ON THE FILE.  UPDATE TINVPAR AND TINVLAP NOTING THE                
      * BALANCE/NOT BALANCE OUTCOME.                                            
      * IF AN ATTEMPT WAS MADE TO PROCESS A STORE FILE A SECOND TIME,           
      * PROCESSING OF THIS SECOND ATTEMPT WAS NOT DONE AND AN EMAIL             
      * MESSAGE IS SENT TO IS-IN-ALERT NOTING THAT THIS CONDITION               
      * OCCURED.                                                                
      ******************************************************************        
       9000-END-OF-JOB.                                                         
           EVALUATE TRUE                                                        
           WHEN PS-TINVLAP-TOTALS-PROCESSED                                     
               MOVE 4060 TO RETURN-CODE                                         
               PERFORM 8155-WRITE-EMAIL-MESSAGE-II                              
           WHEN PS-TINVLAP-TOTALS-NOT-FOUND                                     
               SET PS-FIL-TOT-NOT-BAL TO TRUE                                   
               PERFORM 8200-UPDATE-TINVPAR                                      
               PERFORM 8300-UPDATE-TINVLAP                                      
           WHEN PS-TINVLAP-TOTALS-FOUND                                         
               PERFORM 2100-DISPLAY-DUP-INFO                                    
               MOVE PV-TOTAL-INV-PRICE      TO PV-TOTAL-INV-FMT-PRICE           
               MOVE PV-TOTAL-INV-QTY        TO PV-TOTAL-INV-FMT-QTY             
               MOVE PV-TOTAL-INV-RECORDS   TO PV-TOTAL-INV-FMT-RECORDS          
               MOVE PV-TINVLAP-TOTAL-PRICE                                      
                   TO PV-TINVLAP-TOTAL-FMT-PRICE                                
               MOVE PV-TINVLAP-TOTAL-QTY    TO PV-TINVLAP-TOTAL-FMT-QTY         
                                                                                
               IF (PV-TOTAL-INV-PRICE NOT EQUAL PV-TINVLAP-TOTAL-PRICE)         
               OR (PV-TOTAL-INV-QTY NOT EQUAL PV-TINVLAP-TOTAL-QTY)             
JS0400             SET PS-FIL-TOT-NOT-BAL TO TRUE                               
JS0400             PERFORM 8200-UPDATE-TINVPAR                                  
W28545             PERFORM 8300-UPDATE-TINVLAP                                  
                   MOVE 4060 TO RETURN-CODE                                     
                   DISPLAY ' '                                                  
                   DISPLAY '*****************************************'          
                   DISPLAY '*** TOTALS DO NOT BALANCE !!!!!!!!!!!    '          
                   DISPLAY '*** '                                               
                   DISPLAY '*** STORE NUMBER:                 '                 
                                 PV-TOTAL-INV-STORE-NBR                         
                   DISPLAY '*** #RECS IN FILE:    '                             
                                 PV-TOTAL-INV-FMT-RECORDS                       
                   DISPLAY '*** '                                               
                   DISPLAY '*** FILE PRICE:    ' PV-TOTAL-INV-FMT-PRICE         
                   DISPLAY '*** TINVLAP PRICE: '                                
                       PV-TINVLAP-TOTAL-FMT-PRICE                               
                   DISPLAY '*** '                                               
                   DISPLAY '*** FILE QTY:         '                             
                       PV-TOTAL-INV-FMT-QTY                                     
                   DISPLAY '*** TINVLAP QTY:      '                             
                       PV-TINVLAP-TOTAL-FMT-QTY                                 
                   DISPLAY '*****************************************'          
                   DISPLAY ' '                                                  
               ELSE                                                             
JS0400             SET PS-FIL-TOT-BAL TO TRUE                                   
JS0400             PERFORM 8200-UPDATE-TINVPAR                                  
W28545             PERFORM 8300-UPDATE-TINVLAP                                  
                   DISPLAY ' '                                                  
                   DISPLAY '*****************************************'          
                   DISPLAY '*** TOTALS BALANCED !!!!!!!!!!!!!!!!!    '          
                   DISPLAY '*** '                                               
                   DISPLAY '*** STORE NUMBER:  ' PV-TOTAL-INV-STORE-NBR         
                   DISPLAY '*** #RECS IN FILE: '                                
                       PV-TOTAL-INV-FMT-RECORDS                                 
                   DISPLAY '*** '                                               
                   DISPLAY '*** FILE PRICE:    '                                
                       PV-TOTAL-INV-FMT-PRICE                                   
                   DISPLAY '*** TINVLAP PRICE: '                                
                       PV-TINVLAP-TOTAL-FMT-PRICE                               
                   DISPLAY '*** '                                               
                   DISPLAY '*** FILE QTY:         '                             
                       PV-TOTAL-INV-FMT-QTY                                     
                   DISPLAY '*** TINVLAP QTY:      '                             
                       PV-TINVLAP-TOTAL-FMT-QTY                                 
                   DISPLAY '*****************************************'          
                   DISPLAY ' '                                                  
               END-IF                                                           
           END-EVALUATE.                                                        
                                                                                
           CLOSE IN-INV-DATA-FILE                                               
                 OUT-INV-VALID-DATA-FILE                                        
JS0400           OUT-EMAIL-MESSAGE-FILE.                                        
      *                                                                         
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  ' PV-ABEND-MESSAGE.                                     
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *                                                                         
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
