      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT027.                                                
       AUTHOR.         MARIBETH ZAMORA                                          
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   10/01/08.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  SPLIT RMS FILE TO CREATE A VENDOR STYLE EXTRACT (VS-BASE) AND          
      *  VENDOR STYLE ASSOCIATION EXTRACT (VS-ASSN)                             
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT VS-BASE-FILE                                                  
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT VS-ASSN-FILE                                                  
               ASSIGN TO OUT02.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INPUT-REC                          PIC X(250).                       
                                                                                
       FD  VS-BASE-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VS-BASE-RECORD                     PIC X(80).                        
                                                                                
       FD  VS-ASSN-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VS-ASSN-RECORD                     PIC X(09).                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'CWKUT027'.            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9) VALUE ZERO.                  
           05  PA-OUT-VS-BASE-RECORDS    PIC  9(9) VALUE ZERO.                  
           05  PA-OUT-VS-ASSN-RECORDS    PIC  9(9) VALUE ZERO.                  
                                                                                
       01  SWITCHES.                                                            
           05  SW-INPUT-FILE           PIC X(01)   VALUE 'N'.                   
               88  END-INPUT                       VALUE 'Y'.                   
               88  NOT-END-INPUT                   VALUE 'N'.                   
                                                                                
       01  WS-WORKING-STORAGE.                                                  
           05  WS-RMS-STYL-ID            PIC  S9(08)V COMP-3 VALUE 0.           
                                                                                
                                                                                
      ******************************************************************        
      * INPUT FILE LAYOUT (VENDOR STYLE COMMON EXTRACT)                         
      ******************************************************************        
           COPY IMRD067.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT (VS-BASE LAYOUT)                                     
      ******************************************************************        
           COPY CWRD027.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT (VS-ASSN LAYOUT)                                     
      ******************************************************************        
           COPY CWRD026.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
      *  MAINLINE                                                               
      ******************************************************************        
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
                   UNTIL  END-INPUT                                             
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  INPUT-FILE.                                              
           OPEN OUTPUT VS-BASE-FILE                                             
                       VS-ASSN-FILE.                                            
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 6000-READ-INPUT.                                             
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
                                                                                
           PERFORM 4000-FORMAT-VS-BASE-RECORD.                                  
           PERFORM 7000-WRITE-VS-BASE-RECORD.                                   
                                                                                
           PERFORM 5000-PROCESS-VS-ASSN-RECORDS.                                
                                                                                
           PERFORM 6000-READ-INPUT.                                             
                                                                                
                                                                                
      ******************************************************************        
      * FORMAT VENDOR STYLE BASE RECORD.                                        
      ******************************************************************        
       4000-FORMAT-VS-BASE-RECORD.                                              
                                                                                
           INITIALIZE CW027-RECORD.                                             
                                                                                
           MOVE  IM067-RMS-STYL-ID      TO  CW027-RMS-STYL-ID                   
           MOVE  IM067-DEPT-NBR         TO  CW027-DEPT-NBR                      
           MOVE  IM067-MAJ-CL-NBR       TO  CW027-MAJ-CL-NBR                    
           MOVE  IM067-SUB-CL-NBR       TO  CW027-SUB-CL-NBR                    
           MOVE  IM067-VS-ID            TO  CW027-VS-ID                         
           MOVE  IM067-ENT-ID           TO  CW027-ENT-ID                        
           MOVE  IM067-VS-NBR           TO  CW027-VS-NBR                        
           MOVE  IM067-STKR-NBR         TO  CW027-STKR-NBR                      
           MOVE  IM067-VS-DESC          TO  CW027-VS-DESC                       
           MOVE  IM067-MAP-UNT-RTL-AMT  TO  CW027-MAP-UNT-RTL-AMT               
           MOVE  IM067-LBL-TYP-CDE      TO  CW027-LBL-TYP-CDE.                  
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS VENDOR STYLE ASSOCIATION RECORDS                                
      ******************************************************************        
       5000-PROCESS-VS-ASSN-RECORDS.                                            
                                                                                
           INITIALIZE CW026-RECORD.                                             
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-LBL-CHARTC-CDE       TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-LBL-CHARTC-SEQ       TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-TKT-TYP-CHARTC-CDE   TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-TKT-TYP-CHARTC-SEQ   TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-HNG-FOLD-CHARTC-CDE  TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-HNG-FOLD-CHARTC-SEQ  TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-KY-CLSN-CHARTC-CDE   TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-KY-CLSN-CHARTC-SEQ   TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-FBRCN-CHARTC-CDE     TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-FBRCN-CHARTC-SEQ     TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-FNSH-CHARTC-CDE      TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-FNSH-CHARTC-SEQ      TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-RTL-CLSN-CHARTC-CDE  TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-RTL-CLSN-CHARTC-SEQ  TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-ESL-CHARTC-CDE       TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-ESL-CHARTC-SEQ       TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-RPT-HIER-CHARTC-CDE  TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-RPT-HIER-CHARTC-SEQ  TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-BDY-STYL-CHARTC-CDE  TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-BDY-STYL-CHARTC-SEQ  TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-CONST-CHARTC-CDE     TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-CONST-CHARTC-SEQ     TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-FUNC-CHARTC-CDE      TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-FUNC-CHARTC-SEQ      TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-GNDR-CHARTC-CDE      TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-GNDR-CHARTC-SEQ      TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-PATRN-CHARTC-CDE     TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-PATRN-CHARTC-SEQ     TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-SLHTT-CHARTC-CDE     TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-SLHTT-CHARTC-SEQ     TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-CLLCTN-CHARTC-CDE    TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-CLLCTN-CHARTC-SEQ    TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-PRIC-TIER-CHARTC-CDE TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-PRIC-TIER-CHARTC-SEQ TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD                                    
                                                                                
           MOVE IM067-RMS-STYL-ID          TO  CW026-RMS-STYL-ID                
           MOVE IM067-LFSTYL-CHARTC-CDE    TO  CW026-VS-CHARTC-CDE              
           MOVE IM067-LFSTYL-CHARTC-SEQ    TO  CW026-VS-CHARTC-VAL-CDE          
           PERFORM 8000-WRITE-VS-ASSN-RECORD.                                   
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE INPUT FILE                                           *        
      ******************************************************************        
       6000-READ-INPUT.                                                         
                                                                                
           READ INPUT-FILE INTO IM067-ITEM-STYLE-EDW-REC                        
               AT END                                                           
                  SET END-INPUT  TO TRUE                                        
           END-READ.                                                            
                                                                                
           IF NOT-END-INPUT                                                     
              ADD +1 TO PA-INPUT-RECORDS                                        
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE THE VENDOR STYLE BASE RECORD                                     
      ******************************************************************        
       7000-WRITE-VS-BASE-RECORD.                                               
                                                                                
           WRITE  VS-BASE-RECORD FROM CW027-RECORD.                             
           ADD +1            TO PA-OUT-VS-BASE-RECORDS.                         
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE THE VENDOR STYLE ASSOCIATION RECORD                              
      ******************************************************************        
       8000-WRITE-VS-ASSN-RECORD.                                               
                                                                                
           WRITE  VS-ASSN-RECORD FROM CW026-RECORD.                             
           ADD +1            TO PA-OUT-VS-ASSN-RECORDS.                         
                                                                                
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORD COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.            
           DISPLAY '   '.                                                       
                                                                                
           DISPLAY 'INPUT RECS READ    : ' PA-INPUT-RECORDS.                    
           DISPLAY 'VS BASE RECS WRITTEN: ' PA-OUT-VS-BASE-RECORDS              
           DISPLAY 'VS ASSN RECS WRITTEN: ' PA-OUT-VS-ASSN-RECORDS.             
                                                                                
           CLOSE INPUT-FILE                                                     
                 VS-BASE-FILE                                                   
                 VS-ASSN-FILE.                                                  
                                                                                
      ******************************************************************        
      ******************************************************************        
