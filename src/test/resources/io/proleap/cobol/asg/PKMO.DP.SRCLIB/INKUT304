       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    INKUT304.                                         00020000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030000
       DATE-WRITTEN.  09-01-2004.                                       00040000
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      *PURPOSE: AT A VENDOR LEVEL, THE PROGRAM PRORATES THE SALES BASE  00080000
      *         FILE SHRINK, SALES, AND ANY EST INV RETAIL DOLLARS WHICH00081000
      *         MIGHT RESIDE ON THE SALES BASE CUM FILE.                00082000
      *                                                                 00083000
      *         THE CURRENT TWKSHNK TABLE DOES NOT BREAK SALES/SHRINK AT00084000
      *         A VENDOR LEVEL. WE NEED TO PRORATE THE DOLLARS ACROSS   00085000
      *         THE DEPT, SUB CLASS, STORE, AND BRAND VENDOR KEY.       00086000
      *                                                                 00087000
      *         SALES/SHRINK TO BE PRORATED IS REPRESENTED BY AN BRAND  00088000
      *         VENDOR KEY EQUAL TO ZEROS.                              00089000
      *                                                                 00090000
W27710*         SALES/SHRINK WHERE TINSCLS OR MLT_MNLY_SUM TABLE ENTRIES00091000
W27710*         DO NOT EXIST HAVE BEEN SORT OUT IN STEP 120 OF INK410   00092000
W27710*         TO BE SORTED BACK IN AFTER THIS STEP. (INSR191 - VEND 0)00093000
W27710*                                                                 00094000
      *CHANGE HISTORY:                                                  00095000
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00096000
      * ------- ---------- ----------- -------------------------------- 00097000
W20445* W20445  09-01-2004 ROD JANSSEN DETAILED INVENTORY ADJUSTMENT    00098000
W20445*                                (CREATION...)                    00099000
W27710* W27710  01-29-2005 ROD JANSSEN MISSING ESTIMATED STORE SALES &  00100000
W27710*                                SHRINK DOLLARS FOR SALES BASE    00110000
W27710*                                REPORTING. WHY?                  00120000
W28582* W28582  01-24-2006 CLARK SCHWENN VENDOR/BRAND CHANGES - REPLACE 00130000
W28582* W28582  01-24-2006             ENT ID W/ LWST ID (DATALESS KEY) 00140000
W33304* W33304  04-29-2009 AMANDA      COMMENT OUT LINE WHERE SAVE AND  00141000
W33304*                    JEFFORDS    SHNK VARIABLE ARE SET EQUAL.     00142000
      ******************************************************************00143000
                                                                        00144000
       ENVIRONMENT DIVISION.                                            00145000
       CONFIGURATION SECTION.                                           00146000
       SOURCE-COMPUTER.        IBM-3090.                                00147000
       OBJECT-COMPUTER.        IBM-3090.                                00148000
       INPUT-OUTPUT SECTION.                                            00149000
       FILE-CONTROL.                                                    00150000
                                                                        00160000
           SELECT SLS-CUM-FILE-BVKEY-IN                                 00170000
               ASSIGN TO INP01.                                         00180000
                                                                        00190000
           SELECT SLS-CUM-FILE-SHNK-IN                                  00200000
               ASSIGN TO INP02.                                         00210000
                                                                        00220000
           SELECT SLS-CUM-FILE-PRORATE-OUT                              00230000
               ASSIGN TO OUT01.                                         00240000
                                                                        00250000
       DATA DIVISION.                                                   00260000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  SLS-CUM-FILE-BVKEY-IN                                        00290000
           LABEL RECORDS ARE OMITTED                                    00300000
           RECORDING MODE IS F                                          00310000
           BLOCK CONTAINS 0 RECORDS.                                    00320000
       01  SLS-CUM-REC-BVKEY-IN     PIC X(80).                          00330000
                                                                        00340000
       FD  SLS-CUM-FILE-SHNK-IN                                         00350000
           LABEL RECORDS ARE OMITTED                                    00360000
           RECORDING MODE IS F                                          00370000
           BLOCK CONTAINS 0 RECORDS.                                    00380000
       01  SLS-CUM-REC-SHNK-IN      PIC X(80).                          00390000
                                                                        00400000
       FD  SLS-CUM-FILE-PRORATE-OUT                                     00410000
           LABEL RECORDS ARE OMITTED                                    00420000
           RECORDING MODE IS F                                          00430000
           BLOCK CONTAINS 0 RECORDS.                                    00440000
       01  SLS-CUM-REC-PRORATE-OUT  PIC X(80).                          00450000
                                                                        00460000
      /*****************************************************************00470000
       WORKING-STORAGE SECTION.                                         00480000
      ******************************************************************00490000
                                                                        00500000
       01  ABEND-CODE               PIC S9(04) VALUE ZERO COMP SYNC.    00510000
                                                                        00520000
       01  PC-PROGRAM-CONSTANTS.                                        00530000
           05  PC-PROGRAM-NAME         PIC X(20)  VALUE 'INKUT304'.     00540000
           05  PC-TABLE-MAX            PIC S9(15) COMP-3 VALUE 9999.    00550000
                                                                        00560000
       01  PV-PROGRAM-VARIABLES.                                        00570000
           05  PV-BVKEY-NDX            PIC S9(15) COMP-3 VALUE ZEROS.   00580000
           05  PV-CUM-FILE-BVKEY-READ  PIC S9(15) COMP-3 VALUE ZEROS.   00590000
           05  PV-CUM-FILE-SHNK-READ   PIC S9(15) COMP-3 VALUE ZEROS.   00600000
           05  PV-WRITE-RECS           PIC S9(15) COMP-3 VALUE ZEROS.   00610000
           05  PV-PRORATE-WRITTEN      PIC S9(15) COMP-3 VALUE ZEROS.   00620000
           05  PV-DISPLAY-AMOUNT    PIC ZZZZZZZZZZZZZZ9  VALUE ZEROS.   00630000
           05  PV-SAVE-KEY.                                             00640000
               10  PV-DEPT-NBR-SAVE    PIC X(03)  VALUE ZEROS.          00650000
               10  PV-SUB-CL-NBR-SAVE  PIC X(02)  VALUE ZEROS.          00660000
               10  PV-STR-NBR-SAVE     PIC S9(04) COMP VALUE ZEROS.     00670000
W27710     05  PV-SHNK-KEY.                                             00680000
W27710         10  PV-DEPT-NBR-SHNK    PIC X(03)  VALUE ZEROS.          00690000
W27710         10  PV-SUB-CL-NBR-SHNK  PIC X(02)  VALUE ZEROS.          00700000
W27710         10  PV-STR-NBR-SHNK     PIC S9(04) COMP VALUE ZEROS.     00710000
           05  PV-OPN-SLS-RTL-AMT      PIC S9(11)V99 COMP-3 VALUE ZERO. 00720000
           05  PV-OPN-SHNK-RTL-AMT     PIC S9(11)V99 COMP-3 VALUE ZERO. 00730000
           05  PV-CUM-SLS-RTL-AMT      PIC S9(11)V99 COMP-3 VALUE ZERO. 00740000
           05  PV-CUM-SHNK-RTL-AMT     PIC S9(11)V99 COMP-3 VALUE ZERO. 00750000
           05  PV-NXT-SLS-RTL-AMT      PIC S9(11)V99 COMP-3 VALUE ZERO. 00760000
           05  PV-NXT-SHNK-RTL-AMT     PIC S9(11)V99 COMP-3 VALUE ZERO. 00770000
           05  PV-ESTIMATED-INV-ADJ-AMT PIC S9(11)V99 COMP-3 VALUE ZERO.00780000
           05  PV-OPN-SLS-RTL-AMT-ACC  PIC S9(11)V99 COMP-3 VALUE ZERO. 00790000
           05  PV-OPN-SHNK-RTL-AMT-ACC PIC S9(11)V99 COMP-3 VALUE ZERO. 00791000
           05  PV-CUM-SLS-RTL-AMT-ACC  PIC S9(11)V99 COMP-3 VALUE ZERO. 00792000
           05  PV-CUM-SHNK-RTL-AMT-ACC PIC S9(11)V99 COMP-3 VALUE ZERO. 00793000
           05  PV-NXT-SLS-RTL-AMT-ACC  PIC S9(11)V99 COMP-3 VALUE ZERO. 00794000
           05  PV-NXT-SHNK-RTL-AMT-ACC PIC S9(11)V99 COMP-3 VALUE ZERO. 00795000
           05  PV-EST-INV-ADJ-AMT-ACC  PIC S9(11)V99 COMP-3 VALUE ZERO. 00796000
           05  PV-WORK-AMOUNT          PIC S9(11)V99 COMP-3 VALUE ZERO. 00797000
           05  PV-BVKEY-PERCENT        PIC S9(04)V9(07)                 00797100
                                                     COMP-3 VALUE ZERO. 00797200
                                                                        00797300
       01  PS-PROGRAM-SWITCHES.                                         00797400
           05  PS-SLS-CUM-SHNK-DONE-SW   PIC X(01)   VALUE 'N'.         00797500
               88  PS-SLS-CUM-SHNK-DONE              VALUE 'Y'.         00797600
           05  PS-SLS-CUM-BVKEY-DONE-SW  PIC X(01)   VALUE 'N'.         00797700
               88  PS-SLS-CUM-BVKEY-DONE             VALUE 'Y'.         00797800
           05  PS-SHRINK-FOUND-SW        PIC X(01)   VALUE 'N'.         00797900
               88  PS-SHRINK-FOUND                   VALUE 'Y'.         00798000
           05  PS-WRITE-ADJUST-SW        PIC X(01)   VALUE 'N'.         00799000
               88  PS-WRITE-ADJUSTMENT               VALUE 'Y'.         00800000
           05  PS-FIRST-TIME-SW          PIC X(01)   VALUE 'N'.         00810000
               88  FIRST-TIME-THRU                   VALUE 'N'.         00820000
               88  CONTINUED-PROCESSING              VALUE 'Y'.         00830000
                                                                        00840000
       01  TB-BVKEY-TABLE.                                              00850000
           05 TB-BVKEY-ELEMENTS       OCCURS 9999 TIMES.                00860000
              10 TB-DEPT-NBR          PIC X(03) VALUE SPACES.           00870000
              10 TB-SUB-CL-NBR        PIC X(02) VALUE SPACES.           00880000
              10 TB-ML-LWST-ID        PIC S9(9) COMP VALUE ZEROS.       00890000
              10 TB-STR-NBR           PIC S9(4) COMP VALUE ZEROS.       00900000
              10 TB-OPN-SLS-RTL-AMT   PIC S9(11)V99 COMP-3 VALUE ZEROS. 00910000
              10 TB-OPN-SHNK-RTL-AMT  PIC S9(11)V99 COMP-3 VALUE ZEROS. 00920000
              10 TB-CUM-SLS-RTL-AMT   PIC S9(11)V99 COMP-3 VALUE ZEROS. 00930000
              10 TB-CUM-SHNK-RTL-AMT  PIC S9(11)V99 COMP-3 VALUE ZEROS. 00940000
              10 TB-NXT-SLS-RTL-AMT   PIC S9(11)V99 COMP-3 VALUE ZEROS. 00950000
              10 TB-NXT-SHNK-RTL-AMT  PIC S9(11)V99 COMP-3 VALUE ZEROS. 00960000
              10 TB-ESTIMATED-INV-ADJ-AMT PIC S9(11)V99                 00970000
                                                    COMP-3 VALUE ZEROS. 00980000
                                                                        00990000
      /**************************************************************** 01000000
      * COPYBOOK - SALES BASE CUM FILE RECORD DESCRIPTION               01010000
      ******************************************************************01020000
           COPY INRD023.                                                01021000
                                                                        01022000
      /**************************************************************** 01023000
      * COPYBOOK - SHRINK LEVEL SALES BASE CUM FILE RECORD DESCRIPTION  01024000
      ***************************************************************** 01025000
           COPY INRD023 REPLACING IN023 BY INSHK.                       01026000
                                                                        01027000
      /**************************************************************** 01028000
      * COPYBOOK - PRORATED SALES BASE CUM FILE RECORD DESCRIPTION      01029000
      ***************************************************************** 01030000
           COPY INRD023 REPLACING IN023 BY PRRTE.                       01040000
                                                                        01050000
      /*****************************************************************01060000
      *FLOW:    THE INK410 JOB CREATES THE SALES BASE WORK FILES USING  01070000
      *         TABLES MLT_MNLY_SUM, TINSCLS, AND TWKSHNK. THE          01080000
      *         MLT_MNLY_SUM AND TINSCLS TABLES BREAK SALES AND SHRINK  01090000
      *         DOWN TO THE STORE/VENDOR LEVEL. TABLE TWKSHNK BREAKS    01100000
      *         SALES AND SHRINK DOWN TO THE SUB CLASS/STORE LEVEL.     01110000
      *                                                                 01120000
      *         SHRINK TABLE DOLLARS WILL HAVE ZEROS FOR AN B/V KEY.    01130000
      *         THESE ROWS WILL BE PRORATED AT A DEPT, SUB CLASS, STORE 01140000
      *         AND B/V KEY LEVEL USING THE SALES AND SHRINK DOLLARS    01150000
      *         FOUND IN THE SALES BASE VENDOR LEVEL FILE.              01160000
      *                                                                 01170000
      *         FORMULA:                                                01180000
      *        *TOTAL UP SALES AND SHRINK AT THE DEPT, SUB CLASS, STORE,01190000
      *         AND BRAND VENDOR KEY.                                   01200000
      *        *BREAK TIME, DEVELOP THE B/V KEY LEVEL PERCENT BASED ON  01210000
      *         THE B/V KEY SALES AND SHRINK DOLLARS DIVIDED BY THE     01220000
      *         TOTAL SALES AND SHRINK DOLLARS.                         01230000
      *        *APPLY THE PERCENT TO THE SHRINK TABLE DOLLARS FOR THE   01240000
      *         DEPT, SUB CLASS, AND STORE.                             01250000
      *        *ACCUMULATE THE ADJUSTED DOLLARS. ANY REMAINDER IS ADDED 01260000
      *         TO THE LAST B/V KEY FOR THE DEPT, SUB CLASS, STORE, AND 01270000
      *         BRAND VENDOR KEY.                                       01280000
      *                                                                 01290000
      *         INPUTS:                                                 01300000
      *         INP01 - CUM SALES BASE AT A VENDOR/BRAND LEVEL.         01310000
      *         INP02 - CUM SALES BASE AT THE SHRINK LEVEL (B/V KEY= 0) 01320000
      *         OUT01 - ALL VENDOR/BRAND LEVEL RECORDS AND ANY PRORATED 01330000
      *                 DOLLAR RECORDS.                                 01340000
      *                                                                 01350000
      ******************************************************************01360000
      ******************************************************************01370000
       PROCEDURE DIVISION.                                              01380000
      ******************************************************************01390000
                                                                        01400000
       0000-MAINLINE-INKUT304.                                          01410000
                                                                        01420000
           PERFORM 1000-INITIALIZATION.                                 01430000
                                                                        01440000
           PERFORM 2000-PRORATION-MAIN-LOOP                             01450000
               UNTIL PS-SLS-CUM-BVKEY-DONE.                             01460000
                                                                        01470000
           PERFORM 9000-END-OF-JOB-TASKS.                               01480000
                                                                        01490000
       9999-EXIT-PROGRAM.                                               01500000
           STOP RUN.                                                    01510000
                                                                        01520000
      /*****************************************************************01530000
      * 1000-INITIALIZATION.                                            01540000
      ******************************************************************01550000
       1000-INITIALIZATION.                                             01560000
                                                                        01570000
           DISPLAY '******************************************'.        01580000
           DISPLAY '*  START OF INKUT304                      '.        01590000
           DISPLAY '******************************************'.        01600000
           DISPLAY ' '.                                                 01610000
                                                                        01620000
           OPEN INPUT  SLS-CUM-FILE-BVKEY-IN                            01630000
                INPUT  SLS-CUM-FILE-SHNK-IN                             01640000
           OPEN OUTPUT SLS-CUM-FILE-PRORATE-OUT.                        01650000
                                                                        01660000
           INITIALIZE TB-BVKEY-TABLE.                                   01670000
W27710     MOVE ZEROS               TO PV-BVKEY-NDX.                    01680000
           MOVE ZEROS               TO PV-OPN-SLS-RTL-AMT.              01690000
           MOVE ZEROS               TO PV-OPN-SHNK-RTL-AMT.             01700000
           MOVE ZEROS               TO PV-CUM-SLS-RTL-AMT.              01710000
           MOVE ZEROS               TO PV-CUM-SHNK-RTL-AMT.             01720000
           MOVE ZEROS               TO PV-NXT-SLS-RTL-AMT.              01730000
           MOVE ZEROS               TO PV-NXT-SHNK-RTL-AMT.             01740000
           MOVE ZEROS               TO PV-ESTIMATED-INV-ADJ-AMT.        01750000
           MOVE 'N'                 TO PS-SLS-CUM-SHNK-DONE-SW.         01760000
           MOVE 'N'                 TO PS-SLS-CUM-BVKEY-DONE-SW.        01760100
           MOVE 'N'                 TO PS-SHRINK-FOUND-SW.              01760200
           MOVE 'N'                 TO PS-FIRST-TIME-SW.                01760300
                                                                        01760400
           PERFORM 7000-READ-CUM-FILE-BVKEY.                            01760500
                                                                        01760600
           PERFORM 7100-READ-CUM-FILE-SHNK.                             01760700
                                                                        01760800
      /*****************************************************************01760900
      * 2000-PRORATION-MAIN-LOOP.                                       01761000
      *     LOOP THROUGH THE VEN/BR LEVEL SALES BASE FILE TO ACCUMULATE 01762000
      *   TOTAL SALES RETAIL, TOTAL SHRINK RETAIL, AND ESTIMATED        01763000
      *   INVENTORY RETAIL AMOUNTS AT THE DEPARTMENT, SUB CLASS, STORE, 01764000
      *   AND VEN/BR LEVEL.                                             01765000
      *     ADD THE VEN/BR LEVEL SALES BASE DOLLARS TO A TABLE TO BE    01766000
      *   USED IN THE PRORATION PROCESS.                                01767000
      *     AT A BREAK, PRORATE THE SHRINK RETAIL FOUND ON THE SHRINK   01768000
      *   LEVEL SALES BASE FILE TO THE VEN/BRS FOR THE DEPARTMENT, SUB  01769000
      *   CLASS, STORE, AND VEN/BR.                                     01770000
      *     RESET FOR THE NEXT BREAK.                                   01780000
      *     WRITE ANY VEN/BR LEVEL SALES BASE TO THE OUTPUT FILE.       01790000
      *     READ THE NEXT VEN/BR LEVEL SALES BASE RECORD.               01800000
      ******************************************************************01810000
       2000-PRORATION-MAIN-LOOP.                                        01820000
                                                                        01830000
           IF FIRST-TIME-THRU                                           01840000
               MOVE IN023-DEPT-NBR   TO PV-DEPT-NBR-SAVE                01850000
               MOVE IN023-SUB-CL-NBR TO PV-SUB-CL-NBR-SAVE              01860000
               MOVE IN023-STR-NBR    TO PV-STR-NBR-SAVE                 01870000
W33304*        COMMENTED LINE BELOW                                     01880000
W27710*        MOVE PV-SAVE-KEY      TO PV-SHNK-KEY                     01890000
               MOVE 'Y'              TO PS-FIRST-TIME-SW                01900000
           END-IF.                                                      01910000
                                                                        01920000
           IF IN023-DEPT-NBR EQUAL PV-DEPT-NBR-SAVE     AND             01930000
              IN023-SUB-CL-NBR EQUAL PV-SUB-CL-NBR-SAVE AND             01940000
              IN023-STR-NBR EQUAL PV-STR-NBR-SAVE                       01950000
               PERFORM 3000-ACCUM-SLS-CUM-BVKEY                         01960000
                                                                        01970000
               MOVE IN023-SALES-BASE-CUM-REC                            01971000
                                   TO PRRTE-SALES-BASE-CUM-REC          01972000
               PERFORM 7200-WRITE-EXTRACT-REC                           01973000
               ADD 1               TO PV-WRITE-RECS                     01974000
                                                                        01975000
           ELSE                                                         01976000
               MOVE 'N'            TO PS-SHRINK-FOUND-SW                01977000
               PERFORM 3100-PRORATE-SHRINK                              01978000
                   UNTIL PS-SHRINK-FOUND                                01979000
                                                                        01980000
               INITIALIZE TB-BVKEY-TABLE                                01990000
W27710         MOVE ZEROS          TO PV-BVKEY-NDX                      02000000
               MOVE ZEROS          TO PV-OPN-SLS-RTL-AMT                02010000
               MOVE ZEROS          TO PV-OPN-SHNK-RTL-AMT               02020000
               MOVE ZEROS          TO PV-CUM-SLS-RTL-AMT                02030000
               MOVE ZEROS          TO PV-CUM-SHNK-RTL-AMT               02030100
               MOVE ZEROS          TO PV-NXT-SLS-RTL-AMT                02030200
               MOVE ZEROS          TO PV-NXT-SHNK-RTL-AMT               02030300
               MOVE ZEROS          TO PV-ESTIMATED-INV-ADJ-AMT          02030400
                                                                        02030500
               PERFORM 3000-ACCUM-SLS-CUM-BVKEY                         02030600
                                                                        02030700
               MOVE IN023-SALES-BASE-CUM-REC                            02030800
                                   TO PRRTE-SALES-BASE-CUM-REC          02030900
               PERFORM 7200-WRITE-EXTRACT-REC                           02031000
               ADD 1               TO PV-WRITE-RECS                     02031100
                                                                        02031200
               MOVE IN023-DEPT-NBR   TO PV-DEPT-NBR-SAVE                02031300
               MOVE IN023-SUB-CL-NBR TO PV-SUB-CL-NBR-SAVE              02031400
               MOVE IN023-STR-NBR    TO PV-STR-NBR-SAVE                 02031500
                                                                        02031600
           END-IF.                                                      02031700
                                                                        02031800
           PERFORM 7000-READ-CUM-FILE-BVKEY.                            02031900
                                                                        02032000
      /**************************************************************** 02033000
      * 3000-ACCUM-SLS-CUM-BVKEY.                                       02034000
      *     ACCUMULATE TOTAL SALES RETAIL, TOTAL SHRINK RETAIL, AND     02035000
      *   ESTIMATED INVENTORY RETAIL AMOUNTS AT THE DEPARTMENT,         02036000
      *   STORE, AND VEN/BR LEVEL.                                      02037000
      *     MOVE ALL VEN/BR LEVEL SALES BASE INFORMATION TO A WORKING   02038000
      *   STORAGE VEN/BR TABLE. THE VEN/BR TABLE WILL BE USED IN THE    02039000
      *   PRORATION PROCESS.                                            02040000
      ***************************************************************** 02050000
       3000-ACCUM-SLS-CUM-BVKEY.                                        02060000
                                                                        02070000
W27710**** MOVE ZEROS                  TO PV-BVKEY-NDX.                 02080000
           PERFORM 4100-SAVE-VENDOR-INFORMATION.                        02090000
                                                                        02100000
           ADD IN023-OPN-SLS-RTL-AMT   TO PV-OPN-SLS-RTL-AMT.           02110000
           ADD IN023-OPN-SHNK-RTL-AMT  TO PV-OPN-SHNK-RTL-AMT.          02120000
           ADD IN023-CUM-SLS-RTL-AMT   TO PV-CUM-SLS-RTL-AMT.           02130000
           ADD IN023-CUM-SHNK-RTL-AMT  TO PV-CUM-SHNK-RTL-AMT.          02140000
           ADD IN023-NXT-SLS-RTL-AMT   TO PV-NXT-SLS-RTL-AMT.           02150000
           ADD IN023-NXT-SHNK-RTL-AMT  TO PV-NXT-SHNK-RTL-AMT.          02160000
           ADD IN023-ESTIMATED-INV-ADJ-AMT                              02170000
                                       TO PV-ESTIMATED-INV-ADJ-AMT.     02180000
                                                                        02190000
      ***************************************************************** 02200000
      * 3100-PRORATE-SHRINK.                                            02210000
      *     USE THE WORKING STORAGE VENDOR TABLE WHICH CONTAINS THE     02220000
      * VENDOR LEVEL SALES BASE DOLLARS FOR THE DEPARTMENT, SUB CLASS,  02230000
      * STORE, AND VENDOR TO DEVELOP THE PRORATED SHRINK LEVEL SALES    02240000
      * BASE DOLLARS.                                                   02250000
      *     READ THE NEXT SHRINK LEVEL SALES BASE RECORD.               02260000
      *                                                                 02270000
      * ** NOTE - WE ONLY PROCESS SHRINK LEVEL SALES BASE IN THIS       02270100
      *    MODULE WHERE A MATCH EXISTS AT A DEPARTMENT, SUB CLASS, AND  02270200
      *    STORE LEVEL. WE HAVE STRIPPED OUT THE NON-MATCH SHRINK LEVEL 02270300
      *    SALES BASE USING "SORT" OUTSIDE THIS COBOL PROGRAM. THE      02270400
      *    STRIPPED OUT DOLLARS ARE INCLUDED IN THE NEXT STEP OF THE    02270500
      *    EXECUTING JOB.                                               02270600
      ***************************************************************** 02270700
       3100-PRORATE-SHRINK.                                             02270800
                                                                        02270900
      *** PRORATE ANY SALES AND SHRINK DOLLARS....                      02271000
W27710     IF PV-SHNK-KEY IS EQUAL TO PV-SAVE-KEY                       02271100
               MOVE ZEROS         TO PV-BVKEY-NDX                       02271200
               MOVE ZEROS         TO PV-OPN-SLS-RTL-AMT-ACC             02271300
               MOVE ZEROS         TO PV-OPN-SHNK-RTL-AMT-ACC            02271400
               MOVE ZEROS         TO PV-CUM-SLS-RTL-AMT-ACC             02271500
               MOVE ZEROS         TO PV-CUM-SHNK-RTL-AMT-ACC            02271600
               MOVE ZEROS         TO PV-NXT-SLS-RTL-AMT-ACC             02271700
               MOVE ZEROS         TO PV-NXT-SHNK-RTL-AMT-ACC            02271800
               MOVE ZEROS         TO PV-EST-INV-ADJ-AMT-ACC             02271900
               PERFORM 4000-CALC-SALES-BASE                             02272000
                 VARYING PV-BVKEY-NDX FROM 1 BY 1                       02273000
                   UNTIL PV-BVKEY-NDX GREATER PC-TABLE-MAX              02274000
               PERFORM 5100-APPLY-ANY-REMAINDERS                        02275000
               MOVE 'Y'           TO PS-SHRINK-FOUND-SW                 02276000
W27710     ELSE                                                         02277000
W27710*** A CUM FILE SHRINK RECORD DOES NOT EXIST FOR THE SAVE KEY      02278000
W27710     IF PV-SHNK-KEY IS GREATER THAN PV-SAVE-KEY                   02279000
W27710         MOVE 'Y'           TO PS-SHRINK-FOUND-SW                 02280000
W27710     ELSE                                                         02281000
W27710*** END OF FILE - CUM FILE SHRINK...                              02282000
W27710     IF PS-SLS-CUM-SHNK-DONE                                      02283000
W27710         MOVE 'Y'           TO PS-SHRINK-FOUND-SW                 02284000
W27710     ELSE                                                         02285000
W27710*** READ THE CUM FILE SHRINK FOR THE NEXT RECORD.                 02286000
W27710     IF PV-SHNK-KEY IS LESS THAN PV-SAVE-KEY                      02286100
               PERFORM 7100-READ-CUM-FILE-SHNK                          02286200
               MOVE INSHK-DEPT-NBR   TO PV-DEPT-NBR-SHNK                02286300
               MOVE INSHK-SUB-CL-NBR TO PV-SUB-CL-NBR-SHNK              02286400
               MOVE INSHK-STR-NBR    TO PV-STR-NBR-SHNK                 02286500
           END-IF.                                                      02286600
                                                                        02286700
      /**************************************************************** 02286800
      * 4000-CALC-SALES-BASE.                                           02286900
      ***************************************************************** 02287000
       4000-CALC-SALES-BASE.                                            02287100
                                                                        02287200
           IF TB-DEPT-NBR (PV-BVKEY-NDX) NOT EQUAL SPACES   AND         02287300
              TB-SUB-CL-NBR (PV-BVKEY-NDX) NOT EQUAL SPACES AND         02287400
              TB-ML-LWST-ID (PV-BVKEY-NDX) NOT EQUAL ZEROS  AND         02287500
              TB-STR-NBR (PV-BVKEY-NDX) NOT EQUAL ZEROS                 02287600
               PERFORM 5000-CALC-SHRINK-SLS-BASE                        02287700
           ELSE                                                         02287800
               MOVE 999999          TO PV-BVKEY-NDX                     02287900
           END-IF.                                                      02288000
                                                                        02289000
      ***************************************************************** 02290000
      * 4100-SAVE-VENDOR-INFORMATION.                                   02300000
      ***************************************************************** 02310000
       4100-SAVE-VENDOR-INFORMATION.                                    02320000
                                                                        02330000
           ADD 1                    TO PV-BVKEY-NDX.                    02340000
                                                                        02350000
      *** MAXIMUM ENTIES IN THE TABLE? ABEND THE PROGRAM....            02360000
           IF PV-BVKEY-NDX IS GREATER THAN PC-TABLE-MAX                 02370000
               MOVE +4013           TO ABEND-CODE                       02380000
               DISPLAY '** ABEND **'                                    02390000
               DISPLAY '** PROGRAM   =  INKUT304'                       02400000
               DISPLAY '** PARAGRAPH = 4100-SAVE-VENDOR-INFO'           02410000
               DISPLAY '** OPERATION = WORK STOR TABLE LOAD'            02420000
               CALL 'ILBOABN0'      USING ABEND-CODE                    02420100
           END-IF.                                                      02420200
                                                                        02420300
      *** LOOK FOR AN EMPTY ROW IN THE TABLE....                        02420400
           IF TB-DEPT-NBR (PV-BVKEY-NDX) NOT EQUAL SPACES   AND         02420500
              TB-SUB-CL-NBR (PV-BVKEY-NDX) NOT EQUAL SPACES AND         02420600
              TB-ML-LWST-ID (PV-BVKEY-NDX) NOT EQUAL ZEROS  AND         02420700
              TB-STR-NBR (PV-BVKEY-NDX) NOT EQUAL ZEROS                 02420800
               GO TO 4100-SAVE-VENDOR-INFORMATION.                      02420900
                                                                        02421000
      *** ADD THE RECORD TO THE TABLE....                               02422000
           MOVE IN023-DEPT-NBR   TO TB-DEPT-NBR (PV-BVKEY-NDX).         02422100
           MOVE IN023-SUB-CL-NBR TO TB-SUB-CL-NBR (PV-BVKEY-NDX).       02422200
           MOVE IN023-ML-LWST-ID TO TB-ML-LWST-ID (PV-BVKEY-NDX).       02422300
           MOVE IN023-STR-NBR    TO TB-STR-NBR (PV-BVKEY-NDX).          02422400
           MOVE IN023-OPN-SLS-RTL-AMT                                   02422500
                           TO TB-OPN-SLS-RTL-AMT (PV-BVKEY-NDX).        02422600
           MOVE IN023-OPN-SHNK-RTL-AMT                                  02422700
                           TO TB-OPN-SHNK-RTL-AMT (PV-BVKEY-NDX).       02422800
           MOVE IN023-CUM-SLS-RTL-AMT                                   02422900
                           TO TB-CUM-SLS-RTL-AMT (PV-BVKEY-NDX).        02423000
           MOVE IN023-CUM-SHNK-RTL-AMT                                  02424000
                           TO TB-CUM-SHNK-RTL-AMT (PV-BVKEY-NDX).       02425000
           MOVE IN023-NXT-SLS-RTL-AMT                                   02426000
                           TO TB-NXT-SLS-RTL-AMT (PV-BVKEY-NDX).        02427000
           MOVE IN023-NXT-SHNK-RTL-AMT                                  02428000
                           TO TB-NXT-SHNK-RTL-AMT (PV-BVKEY-NDX).       02429000
           MOVE IN023-ESTIMATED-INV-ADJ-AMT                             02430000
                        TO TB-ESTIMATED-INV-ADJ-AMT (PV-BVKEY-NDX).     02440000
                                                                        02450000
      /**************************************************************** 02460000
      * 5000-CALC-SHRINK-SLS-BASE.                                      02470000
      ***************************************************************** 02480000
       5000-CALC-SHRINK-SLS-BASE.                                       02490000
                                                                        02500000
           INITIALIZE PRRTE-SALES-BASE-CUM-REC.                         02510000
                                                                        02520000
           MOVE TB-DEPT-NBR (PV-BVKEY-NDX)  TO PRRTE-DEPT-NBR.          02530000
           MOVE TB-SUB-CL-NBR (PV-BVKEY-NDX)TO PRRTE-SUB-CL-NBR.        02540000
           MOVE TB-ML-LWST-ID (PV-BVKEY-NDX) TO PRRTE-ML-LWST-ID.       02550000
           MOVE TB-STR-NBR (PV-BVKEY-NDX)   TO PRRTE-STR-NBR.           02560000
                                                                        02570000
      *** PRORATE OPEN SALES RETAIL AMOUNT.....                         02580000
           IF TB-OPN-SLS-RTL-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR          02590000
              PV-OPN-SLS-RTL-AMT EQUAL ZEROS OR                         02600000
W27710        INSHK-OPN-SLS-RTL-AMT EQUAL ZEROS                         02610000
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02620000
           ELSE                                                         02630000
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02640000
                                  = TB-OPN-SLS-RTL-AMT (PV-BVKEY-NDX)   02650000
                                  / PV-OPN-SLS-RTL-AMT                  02660000
           END-IF.                                                      02670000
                                                                        02680000
           COMPUTE PV-WORK-AMOUNT ROUNDED = PV-BVKEY-PERCENT            02690000
                                          * INSHK-OPN-SLS-RTL-AMT.      02700000
           ADD PV-WORK-AMOUNT     TO PV-OPN-SLS-RTL-AMT-ACC.            02710000
           MOVE PV-WORK-AMOUNT    TO PRRTE-OPN-SLS-RTL-AMT.             02720000
                                                                        02730000
      *** PRORATE OPEN SHRINK RETAIL AMOUNT.....                        02740000
           IF TB-OPN-SHNK-RTL-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR         02750000
              PV-OPN-SHNK-RTL-AMT EQUAL ZEROS  OR                       02760000
W27710        INSHK-OPN-SHNK-RTL-AMT EQUAL ZEROS                        02770000
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02780000
           ELSE                                                         02790000
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02800000
                                  = TB-OPN-SHNK-RTL-AMT (PV-BVKEY-NDX)  02810000
                                  / PV-OPN-SHNK-RTL-AMT                 02820000
           END-IF.                                                      02830000
                                                                        02840000
           COMPUTE PV-WORK-AMOUNT ROUNDED = PV-BVKEY-PERCENT            02850000
                                          * INSHK-OPN-SHNK-RTL-AMT.     02860000
           ADD PV-WORK-AMOUNT     TO PV-OPN-SHNK-RTL-AMT-ACC.           02861000
           MOVE PV-WORK-AMOUNT    TO PRRTE-OPN-SHNK-RTL-AMT.            02862000
                                                                        02863000
      *** PRORATE CUMM SALES RETAIL AMOUNT.....                         02864000
           IF TB-CUM-SLS-RTL-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR          02865000
              PV-CUM-SLS-RTL-AMT EQUAL ZEROS OR                         02866000
W27710        INSHK-CUM-SLS-RTL-AMT EQUAL ZEROS                         02867000
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02868000
           ELSE                                                         02869000
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02870000
                                  = TB-CUM-SLS-RTL-AMT (PV-BVKEY-NDX)   02870100
                                  / PV-CUM-SLS-RTL-AMT                  02870200
           END-IF.                                                      02870300
                                                                        02870400
           COMPUTE PV-WORK-AMOUNT ROUNDED = PV-BVKEY-PERCENT            02870500
                                          * INSHK-CUM-SLS-RTL-AMT.      02870600
           ADD PV-WORK-AMOUNT     TO PV-CUM-SLS-RTL-AMT-ACC.            02870700
           MOVE PV-WORK-AMOUNT    TO PRRTE-CUM-SLS-RTL-AMT.             02870800
                                                                        02870900
      *** PRORATE CUMM SHRINK RETAIL AMOUNT.....                        02871000
           IF TB-CUM-SHNK-RTL-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR         02871100
              PV-CUM-SHNK-RTL-AMT EQUAL ZEROS OR                        02871200
W27710        INSHK-CUM-SHNK-RTL-AMT EQUAL ZEROS                        02871300
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02871400
           ELSE                                                         02871500
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02871600
                                  = TB-CUM-SHNK-RTL-AMT (PV-BVKEY-NDX)  02871700
                                  / PV-CUM-SHNK-RTL-AMT                 02871800
           END-IF.                                                      02871900
                                                                        02872000
           COMPUTE PV-WORK-AMOUNT ROUNDED = PV-BVKEY-PERCENT            02872100
                                          * INSHK-CUM-SHNK-RTL-AMT.     02872200
           ADD PV-WORK-AMOUNT     TO PV-CUM-SHNK-RTL-AMT-ACC.           02872300
           MOVE PV-WORK-AMOUNT    TO PRRTE-CUM-SHNK-RTL-AMT.            02872400
                                                                        02872500
      *** PRORATE NEXT SALES RETAIL AMOUNT.....                         02872600
           IF TB-NXT-SLS-RTL-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR          02872700
              PV-NXT-SLS-RTL-AMT EQUAL ZEROS OR                         02872800
W27710        INSHK-NXT-SLS-RTL-AMT EQUAL ZEROS                         02872900
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02873000
           ELSE                                                         02873100
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02873200
                                  = TB-NXT-SLS-RTL-AMT (PV-BVKEY-NDX)   02873300
                                  / PV-NXT-SLS-RTL-AMT                  02873400
           END-IF.                                                      02873500
                                                                        02873600
           COMPUTE PV-WORK-AMOUNT ROUNDED = PV-BVKEY-PERCENT            02873700
                                          * INSHK-NXT-SLS-RTL-AMT.      02873800
           ADD PV-WORK-AMOUNT     TO PV-NXT-SLS-RTL-AMT-ACC.            02873900
           MOVE PV-WORK-AMOUNT    TO PRRTE-NXT-SLS-RTL-AMT.             02874000
                                                                        02874100
      *** PRORATE NEXT SHRINK RETAIL AMOUNT.....                        02874200
           IF TB-NXT-SHNK-RTL-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR         02874300
              PV-NXT-SHNK-RTL-AMT EQUAL ZEROS OR                        02874400
W27710        INSHK-NXT-SHNK-RTL-AMT EQUAL ZEROS                        02874500
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02874600
           ELSE                                                         02874700
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02874800
                                  = TB-NXT-SHNK-RTL-AMT (PV-BVKEY-NDX)  02874900
                                  / PV-NXT-SHNK-RTL-AMT                 02875000
           END-IF.                                                      02875100
                                                                        02875200
           COMPUTE PV-WORK-AMOUNT ROUNDED = PV-BVKEY-PERCENT            02875300
                                          * INSHK-NXT-SHNK-RTL-AMT.     02875400
           ADD PV-WORK-AMOUNT     TO PV-NXT-SHNK-RTL-AMT-ACC.           02875500
           MOVE PV-WORK-AMOUNT    TO PRRTE-NXT-SHNK-RTL-AMT.            02875600
                                                                        02875700
      *** PRORATE ESTIMATED INVENTORY ADJUSTMENT.....                   02875800
           IF TB-ESTIMATED-INV-ADJ-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR    02875900
              PV-ESTIMATED-INV-ADJ-AMT EQUAL ZEROS OR                   02876000
W27710        INSHK-ESTIMATED-INV-ADJ-AMT EQUAL ZEROS                   02876100
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02876200
           ELSE                                                         02876300
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02876400
                             = TB-ESTIMATED-INV-ADJ-AMT (PV-BVKEY-NDX)  02876500
                             / PV-ESTIMATED-INV-ADJ-AMT                 02876600
           END-IF.                                                      02876700
                                                                        02876800
           COMPUTE PV-WORK-AMOUNT ROUNDED                               02876900
                                  = PV-BVKEY-PERCENT                    02877000
                                  * INSHK-ESTIMATED-INV-ADJ-AMT.        02877100
           ADD PV-WORK-AMOUNT     TO PV-EST-INV-ADJ-AMT-ACC             02877200
           MOVE PV-WORK-AMOUNT    TO PRRTE-ESTIMATED-INV-ADJ-AMT.       02877300
                                                                        02877400
      *** WRITE THE PRORATE SALES/SHRINK RECORD...                      02877500
           PERFORM 7200-WRITE-EXTRACT-REC.                              02877600
           ADD 1                    TO PV-WRITE-RECS.                   02877700
           ADD 1                    TO PV-PRORATE-WRITTEN.              02877800
                                                                        02877900
      ***************************************************************** 02878000
      * 5100-APPLY-ANY-REMAINDERS.                                      02878100
      ***************************************************************** 02878200
       5100-APPLY-ANY-REMAINDERS.                                       02878300
                                                                        02878400
           MOVE 'N'               TO PS-WRITE-ADJUST-SW.                02878500
                                                                        02878600
      *** APPLY ANY REMAINING OPEN SALES RETAIL AMOUNT.....             02878700
           IF PV-OPN-SLS-RTL-AMT-ACC EQUAL INSHK-OPN-SLS-RTL-AMT        02878800
               MOVE ZEROS             TO PRRTE-OPN-SLS-RTL-AMT          02878900
           ELSE                                                         02879000
               COMPUTE PV-WORK-AMOUNT = INSHK-OPN-SLS-RTL-AMT           02879100
                                      - PV-OPN-SLS-RTL-AMT-ACC          02879200
W27710*        COMPUTE PV-WORK-AMOUNT = PV-OPN-SLS-RTL-AMT-ACC          02879300
W27710*                               + PV-WORK-AMOUNT                  02879400
               MOVE PV-WORK-AMOUNT    TO PRRTE-OPN-SLS-RTL-AMT          02879500
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02879600
           END-IF.                                                      02879700
                                                                        02879800
      *** APPLY ANY REMAINING OPEN SHRINK RETAIL AMOUNT.....            02879900
           IF PV-OPN-SHNK-RTL-AMT-ACC EQUAL INSHK-OPN-SHNK-RTL-AMT      02880000
               MOVE ZEROS             TO PRRTE-OPN-SHNK-RTL-AMT         02881000
           ELSE                                                         02882000
               COMPUTE PV-WORK-AMOUNT = INSHK-OPN-SHNK-RTL-AMT          02883000
                                      - PV-OPN-SHNK-RTL-AMT-ACC         02884000
W27710*        COMPUTE PV-WORK-AMOUNT = PV-OPN-SHNK-RTL-AMT-ACC         02885000
W27710*                               + PV-WORK-AMOUNT                  02885100
               MOVE PV-WORK-AMOUNT    TO PRRTE-OPN-SHNK-RTL-AMT         02885200
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02885300
           END-IF.                                                      02885400
                                                                        02885500
      *** APPLY ANY REMAINING CUMM SALES RETAIL AMOUNT.....             02885600
           IF PV-CUM-SLS-RTL-AMT-ACC EQUAL INSHK-CUM-SLS-RTL-AMT        02885700
               MOVE ZEROS             TO PRRTE-CUM-SLS-RTL-AMT          02885800
           ELSE                                                         02885900
               COMPUTE PV-WORK-AMOUNT = INSHK-CUM-SLS-RTL-AMT           02886000
                                      - PV-CUM-SLS-RTL-AMT-ACC          02886100
W27710*        COMPUTE PV-WORK-AMOUNT = PV-CUM-SLS-RTL-AMT-ACC          02886200
W27710*                               + PV-WORK-AMOUNT                  02886300
               MOVE PV-WORK-AMOUNT    TO PRRTE-CUM-SLS-RTL-AMT          02886400
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02886500
           END-IF.                                                      02886600
                                                                        02886700
      *** APPLY ANY REMAINING CUMM SHRINK RETAIL AMOUNT.....            02886800
           IF PV-CUM-SHNK-RTL-AMT-ACC EQUAL INSHK-CUM-SHNK-RTL-AMT      02886900
               MOVE ZEROS             TO PRRTE-CUM-SHNK-RTL-AMT         02887000
           ELSE                                                         02887100
               COMPUTE PV-WORK-AMOUNT = INSHK-CUM-SHNK-RTL-AMT          02887200
                                      - PV-CUM-SHNK-RTL-AMT-ACC         02887300
W27710*        COMPUTE PV-WORK-AMOUNT = PV-CUM-SHNK-RTL-AMT-ACC         02887400
W27710*                               + PV-WORK-AMOUNT                  02887500
               MOVE PV-WORK-AMOUNT    TO PRRTE-CUM-SHNK-RTL-AMT         02887600
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02887700
           END-IF.                                                      02887800
                                                                        02887900
      *** APPLY ANY REMAINING NEXT SALES RETAIL AMOUNT.....             02888000
           IF PV-NXT-SLS-RTL-AMT-ACC EQUAL INSHK-NXT-SLS-RTL-AMT        02889000
               MOVE ZEROS             TO PRRTE-NXT-SLS-RTL-AMT          02890000
           ELSE                                                         02890100
               COMPUTE PV-WORK-AMOUNT = INSHK-NXT-SLS-RTL-AMT           02890200
                                      - PV-NXT-SLS-RTL-AMT-ACC          02890300
W27710*        COMPUTE PV-WORK-AMOUNT = PV-NXT-SLS-RTL-AMT-ACC          02890400
W27710*                               + PV-WORK-AMOUNT                  02890500
               MOVE PV-WORK-AMOUNT    TO PRRTE-NXT-SLS-RTL-AMT          02890600
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02890700
           END-IF.                                                      02890800
                                                                        02890900
      *** APPLY ANY REMAINING NEXT SHRINK RETAIL AMOUNT.....            02891000
           IF PV-NXT-SHNK-RTL-AMT-ACC EQUAL INSHK-NXT-SHNK-RTL-AMT      02892000
               MOVE ZEROS             TO PRRTE-NXT-SHNK-RTL-AMT         02892100
           ELSE                                                         02892200
               COMPUTE PV-WORK-AMOUNT = INSHK-NXT-SHNK-RTL-AMT          02892300
                                      - PV-NXT-SHNK-RTL-AMT-ACC         02892400
W27710*        COMPUTE PV-WORK-AMOUNT = PV-NXT-SHNK-RTL-AMT-ACC         02892500
W27710*                               + PV-WORK-AMOUNT                  02892600
               MOVE PV-WORK-AMOUNT    TO PRRTE-NXT-SHNK-RTL-AMT         02892700
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02892800
           END-IF.                                                      02892900
                                                                        02893000
      *** APPLY ANY REMAINING ESTIMATED INVENTORY ADJUSTMENTS....       02894000
           IF PV-EST-INV-ADJ-AMT-ACC EQUAL INSHK-ESTIMATED-INV-ADJ-AMT  02895000
               MOVE ZEROS             TO PRRTE-ESTIMATED-INV-ADJ-AMT    02895100
           ELSE                                                         02895200
               COMPUTE PV-WORK-AMOUNT = INSHK-ESTIMATED-INV-ADJ-AMT     02895300
                                      - PV-EST-INV-ADJ-AMT-ACC          02895400
W27710*        COMPUTE PV-WORK-AMOUNT = PV-EST-INV-ADJ-AMT-ACC          02895500
W27710*                               + PV-WORK-AMOUNT                  02895600
               MOVE PV-WORK-AMOUNT    TO PRRTE-ESTIMATED-INV-ADJ-AMT    02895700
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02895800
           END-IF.                                                      02895900
                                                                        02896000
      *** WRITE THE PRORATE SALES/SHRINK RECORD...                      02897000
           IF PS-WRITE-ADJUSTMENT                                       02897100
               PERFORM 7200-WRITE-EXTRACT-REC                           02897200
               ADD 1                TO PV-WRITE-RECS                    02897300
               ADD 1                TO PV-PRORATE-WRITTEN               02897400
           END-IF.                                                      02897500
                                                                        02897600
      /**************************************************************** 02897700
      * 7000-READ-CUM-FILE-BVKEY. READ THE VENDOR LVL SALES CUM FILE.   02897800
      ***************************************************************** 02897900
       7000-READ-CUM-FILE-BVKEY.                                        02898000
                                                                        02899000
           READ SLS-CUM-FILE-BVKEY-IN NEXT                              02900000
             INTO IN023-SALES-BASE-CUM-REC                              02900100
               AT END  MOVE 'Y' TO PS-SLS-CUM-BVKEY-DONE-SW             02900200
               NOT AT END  ADD 1 TO PV-CUM-FILE-BVKEY-READ              02900300
           END-READ.                                                    02900400
                                                                        02900500
      ***************************************************************** 02900600
      * 7100-READ-CUM-FILE-SHNK. READ THE SHRINK LVL SALES CUM FILE.    02900700
      ***************************************************************** 02900800
       7100-READ-CUM-FILE-SHNK.                                         02900900
                                                                        02901000
           READ SLS-CUM-FILE-SHNK-IN NEXT                               02901100
             INTO INSHK-SALES-BASE-CUM-REC                              02901200
W27710         AT END  INITIALIZE INSHK-SALES-BASE-CUM-REC              02901300
                       MOVE 'Y' TO PS-SLS-CUM-SHNK-DONE-SW              02901400
               NOT AT END  ADD 1 TO PV-CUM-FILE-SHNK-READ               02901500
           END-READ.                                                    02901600
                                                                        02901700
      /*****************************************************************02901800
      * 7200-WRITE-EXTRACT-REC.                                         02901900
      ******************************************************************02902000
       7200-WRITE-EXTRACT-REC.                                          02902100
                                                                        02902200
           WRITE SLS-CUM-REC-PRORATE-OUT FROM PRRTE-SALES-BASE-CUM-REC. 02902300
                                                                        02902400
      /*****************************************************************02902500
      * 9000-END-OF-JOB-TASKS. FINAL PROCESSING FOR THE PROGRAM.        02902600
      ******************************************************************02902700
       9000-END-OF-JOB-TASKS.                                           02902800
                                                                        02902900
           IF PV-CUM-FILE-BVKEY-READ IS GREATER THAN ZEROS              02903000
               MOVE 'N'              TO PS-SHRINK-FOUND-SW              02904000
               PERFORM 3100-PRORATE-SHRINK                              02905000
                   UNTIL PS-SHRINK-FOUND                                02906000
           END-IF.                                                      02907000
                                                                        02908000
           DISPLAY '******************************************'.        02909000
           DISPLAY '** RECORD COUNT SUMMARY **'.                        02910000
                                                                        02920000
           MOVE PV-CUM-FILE-BVKEY-READ  TO PV-DISPLAY-AMOUNT.           02930000
           DISPLAY '* READ SALES BASE - VENDOR LVL: '                   02940000
                                           PV-DISPLAY-AMOUNT.           02950000
           MOVE PV-CUM-FILE-SHNK-READ   TO PV-DISPLAY-AMOUNT.           02960000
           DISPLAY '*      SALES BASE - SHRINK LVL: '                   02970000
                                           PV-DISPLAY-AMOUNT.           02980000
           MOVE PV-WRITE-RECS           TO PV-DISPLAY-AMOUNT.           02990000
           DISPLAY '* WRITE TOTAL RECORDS         : '                   03000000
                                           PV-DISPLAY-AMOUNT.           03010000
           MOVE PV-PRORATE-WRITTEN      TO PV-DISPLAY-AMOUNT.           03020000
           DISPLAY '*       PRORTED SLS BASE DOLRS: '                   03030000
                                           PV-DISPLAY-AMOUNT.           03040000
                                                                        03050000
           DISPLAY ' '.                                                 03060000
           DISPLAY '******************************************'.        03070000
           DISPLAY '*  END OF INKUT304                        '.        03080000
           DISPLAY '******************************************'.        03090000
                                                                        03100000
           CLOSE SLS-CUM-FILE-BVKEY-IN                                  03110000
                 SLS-CUM-FILE-SHNK-IN                                   03120000
                 SLS-CUM-FILE-PRORATE-OUT.                              03130000
                                                                        03140000
