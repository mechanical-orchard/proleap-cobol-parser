       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKUT160.                                            
       AUTHOR.             JULIE SEIDLER.                                       
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       05-98.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *                                                                         
RB0401*  THERE ARE TWO INPUT FILES, ONE IS DATA EXTRACTED FROM THE              
RB0401*  INTRANSIT TABLE AND THE OTHER IS DATA EXTRACTED FROM THE               
W26600*  CTOFF TABLE.  THIS PROGRAM WILL MERGE THE TWO FILES AND                
RB0401*  MAP DATA FROM THE INPUT FILE AND THE TSK TABLE INTO ONE                
RB0401*  OUTPUT FILE.                                                           
RB0401*                                                                         
RB0401******************************************************************        
RB0401******************************************************************        
RB0401*    CHANGE HISTORY                                                       
RB0401*-----------------------------------------------------------------        
      * DATE  03/05/99                                                          
      *   ADD ONLINE INDICATOR.                                                 
      *     MADE BY: SCOTT SWESSEL           WR/IR #:                           
RB0401*-----------------------------------------------------------------        
      * DATE  10/15/99                                                          
W26600*   OBTAIN UNIT PRICE FROM TUPCPLS INSTEAD OF CTOFF.                      
      *     MADE BY: ROLLIN KRING            WR/IR #:20244                      
RB0401*-----------------------------------------------------------------        
JF0900* DATE  09/06/00                      CHANGE ID=JF0900                    
JF0900*   REMOVED THE GWP-CONTROL-CARD AND ALL REFERENCES AND LOGIC             
JF0900*   THAT DEAL WITH PV-YN-IND. ALSO REMOVED THE REFERENCE TO               
JF0900*   TSK-REGN-PRIC-IND AND ANY TUPCPLS PRICE LOOK-UP LOGIC IN THE          
W26600*   B300-CTOFF-UNLOAD-FILE ROUTINE.                                       
JF0900*     MADE BY: JON FIEDLER            WR/IR #:99999                       
RB0401*-----------------------------------------------------------------        
RB1000* DATE  10/05/00                  CHANGE ID=RB1000                        
RB1000*   INP02 DECREASED IN SIZE.                                              
RB1000*   COMMENTED OUT ALL REFERENCE TO INV_CTOF_UN_PR_AMT, BECAUSE            
RB1000*   THIS FIELD NO LONGER EXISTS, AND IS NOT IN THE INPUT FILE.            
RB1000*     MADE BY: RONNA BUTZ             WR/IR #:99999                       
RB0401*-----------------------------------------------------------------        
W04061* DATE  04/06/2001                   CHANGE ID=W04061                     
W04061*   INK365 - UNIT BOOKING PROCESS EFFICIENCIES (REMOVE JF0900 MOD)        
W04061*   MADE BY: ROD JANSSEN            WR/IR #:99999                         
W04061*                                                                         
RB0401*-----------------------------------------------------------------        
RB0401*  CHANGES:                                                               
RB0401*     04/30/01 WR# 21591 RONNA BUTZ (LOOK FOR RB0401 TO IDENTIFY          
RB0401*                                                                         
RB0401*     CHANGES ARE FOR MANIFESTING.                                        
RB0401*     COPYBOOK INWS016 INCREASED IN SIZE FROM 60 TO 70.                   
RB0401*     THE NEW FIELD ADDED AT THE END IS FROM THE INTRASIT                 
RB0401*     TABLE.                                                              
RB0401*                                                                         
RB0401*     ADDED AN INPUT FILE THAT CONTAINS DATA FROM THE INTRANSIT           
RB0401*     TABLE.  THE TWO INPUT FILES MUST BE SORTED IN ORDER BY              
W26600*     SKU, STORE.  IT IS POSSIBLE TO HAVE A CTOFF RECORD AND NOT          
RB0401*     AN INTRANSIT RECORD, BUT AN INTRASIT RECORD MUST HAVE A             
W26600*     CTOFF RECORD.                                                       
RB0401*                                                                         
W21732******************************************************************        
W21732* CHANGE LOG                                                              
W21732* DATE       WHO     CHANGE DESC                                          
W21732* ---------- ------- -----------------------------------------            
W21732* 12-05-2002 ASHOK,  NON-INTELLIGENT SKU CONVERSION.                      
W21732*            IGSI    INWS016 WAS INCREASED IN SIZE TO 80 BYTES.           
W21732*                    REFERENCES TO TSK TABLE IS REPLACED WITH             
W21732*                    TSKXREF, TVNDSTL AND IMV_SKU.                        
W21732******************************************************************        
C21732* 05-02-2003 S.JACKSON  FIX ISSUE WHERE RECORDS WERE BYPASSED             
C21732*                    IF ENT-ID COULD NOT BE FOUND DURING SKU              
C21732*                    VALIDATION. MOVE ZEROES TO THE ENT_ID IF THE         
C21732*                    VS_ID OR ENT_ID COULD BE FOUND.                      
W26600* W26600  06-29-2004 ROD JANSSEN INV STORE EXPANSION                      
W27710* W27710  01-20-2005 ROD JANSSEN UNIT BOOKING ISSUE WITH MISSING          
W27710*                                CUTOFF/INTRANSIT/POST FLUSH.             
W27808* W27808  05-03-2005 ERIN SEARL  MANIFESTING PROJECT.                     
W28545* W28545  07-03-2006 ADD LOGIC FOR DEPARTMENT LEVEL                       
W28545*                    INVENTORY PROCESS.                                   
W28545*                    J SELWITSCHKA                                        
W33304* W33304  06-03-2008 HOLDING INVENTORY LEDGER OPEN ACROSS                 
W33304*                    PERIODS INVENTORY SHORTAGE REPORTING                 
W33304*                    JILL LIN                                             
W27808******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT STORE-CONTROL-FILE           ASSIGN TO INP01.                 
                                                                                
W26600     SELECT IN-CTOFF-UNLOAD-FILE         ASSIGN TO INP02.                 
                                                                                
           SELECT OUT-FS-CUTOFFS-FILE          ASSIGN TO OUT01.                 
RB0401                                                                          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  STORE-CONTROL-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-CONTROL-RECORD        PIC  X(80).                              
                                                                                
W26600 FD  IN-CTOFF-UNLOAD-FILE                                                 
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W27808 01  IN-CTOFF-UNLOAD-RECORD      PIC  X(23).                              
                                                                                
       FD  OUT-FS-CUTOFFS-FILE                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W21732 01  OUT-FS-CUTOFFS-RECORD        PIC  X(80).                             
RB0401                                                                          
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                                                                         
RB0401 01  WS-CUTOFF-INTRANSIT-QTY  PIC S9(09) COMP VALUE ZERO.                 
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-EMPTY-DATE        PIC X(10)   VALUE '9999-09-09'.             
           05  PC-TEST-YR           PIC X(10)   VALUE '1998-01-01'.             
           05  PC-MAX-RETRIES       PIC 9(03)   VALUE 3.                        
           05  PC-OPER-CO           PIC X(02)   VALUE '03'.                     
           05  PC-ADLOC-ID          PIC X(03)   VALUE SPACES.                   
W28545     05  PC-NINETY-NINE       PIC 9(03)   VALUE 099.                      
W28545     05  PC-FOUR-THOUSAND     PIC 9(04)   VALUE 4000.                     
                                                                                
       01  SA-SAVE-AREA.                                                        
W26600     05  SA-SKU-NBR           PIC 9(08)   VALUE ZERO.                     
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
W26600     05  PV-STR-SUB            PIC S9(04) COMP VALUE ZERO.                
           05  PV-SQL-SUB            PIC 9(03)  VALUE ZERO.                     
           05  PV-PARAGRAPH-NAME     PIC X(30)  VALUE SPACES.                   
           05  PV-DB2-OPER-ATTEMPTED PIC X(30)  VALUE SPACES.                   
W26600     05  PV-STORE              PIC 9(04)  VALUE ZERO.                     
           05  PV-CUR-RTL                 PIC S9(7)V9(2) VALUE ZERO             
                                                     COMP-3.                    
           05  PV-PLND-INV-TMST           PIC X(26) VALUE SPACES.               
           05  PV-PLND-INV-TMST-R REDEFINES PV-PLND-INV-TMST.                   
               10  PV-PLN-INV-DATE        PIC X(10).                            
               10  PV-PLN-TMST            PIC X(16).                            
           05  PV-EDIT-LOC-NBR       PIC 9(05)  VALUE ZEROS.                    
W28545     05  PV-HOLD-STORE-NBR     PIC 9(04)  VALUE ZERO.                     
W28545     05  PV-HOLD-DEPT-NBR      PIC 9(04)  VALUE ZERO.                     
W28545     05  PV-HOLD-MCL-NBR       PIC 9(04)  VALUE ZERO.                     
W28545     05  PV-PREV-STORE-NBR     PIC 9(04)  VALUE ZERO.                     
W28545     05  PV-STORE-0000-COUNT   PIC S9(09) VALUE +0  COMP SYNC.            
W28545     05  PV-STORE-XXXX-COUNT   PIC S9(09) VALUE +0  COMP SYNC.            
W28545     05  PV-II-TBL-SUB         PIC S9(03) VALUE ZERO COMP-3.              
W28545     05  PV-IIPM-TBL-SUB       PIC S9(04) VALUE ZERO COMP-3.              
      *                                                                         
       01  PV-STORE-CONTROL-TABLE.                                              
W26600     05  PV-STR-TABLE OCCURS 9999 TIMES.                                  
W26600         10  PV-TBLE-STORE     PIC X(04).                                 
W26600         10  PV-TBLE-STORE-NUM REDEFINES                                  
W26600             PV-TBLE-STORE     PIC 9(04).                                 
               10  PV-TBLE-INV-DATE  PIC X(10).                                 
               10  PV-TBLE-SCAN-IND  PIC X(01).                                 
W28545         10  PV-TBLE-INV-ID-SUB PIC 9(03) VALUE ZEROS.                    
W28545                                                                          
W28545 01  WS-INT-INV-PARM-MDSE-TABLE.                                          
W28545     05  WS-PV-IIPM-OCC-CTR   PIC S9(04) VALUE ZEROS COMP-3.              
W28545     05  WS-IIPM-TABLE-ELEMENTS      OCCURS 4000 TIMES.                   
W28545         10  WS-IIPM-DEPT-NBR PIC 9(04)  VALUE ZEROS.                     
W28545         10  WS-IIPM-MCL-NBR  PIC 9(04)  VALUE ZEROS.                     
W28545                                                                          
W28545 01  WS-INV-ID-TABLE.                                                     
W28545     05  WS-PV-INV-ID-OCC-CTR PIC S9(03) VALUE ZEROS COMP-3.              
W28545     05  WS-INV-ID-TABLE-ELEMENTS    OCCURS 99 TIMES.                     
W28545         10  WS-INV-ID        PIC S9(09) VALUE ZEROS COMP.                
W28545         10  WS-INV-TYPE      PIC X(01)  VALUE SPACE.                     
W28545         88  WS-INV-TYPE-STORE           VALUE 'S'.                       
W28545         88  WS-INV-TYPE-DEPT            VALUE 'D'.                       
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-STR-NBR-SW    PIC X   VALUE 'N'.                       
               88  PS-EOF-STR-NBR-FILE         VALUE 'Y'.                       
           05  PS-EMPTY-STR-NBR-SW     PIC X   VALUE 'N'.                       
               88  PS-EMPTY-STR-NBR-FILE       VALUE 'Y'.                       
           05  PS-FOUND-STORE-FOR-ITEM-SW PIC X   VALUE 'N'.                    
               88 PV-NOT-FOUND-STORE-FOR-ITEM  VALUE 'N'.                       
               88 PV-FOUND-STORE-FOR-ITEM      VALUE 'Y'.                       
           05  PS-END-OF-CTOFF-UNLD-SW PIC X   VALUE 'N'.                       
               88  PS-EOF-CTOFF-UNLD-FILE      VALUE 'Y'.                       
RB0401     05  PS-END-OF-TRNST-UNLD-SW PIC X   VALUE 'N'.                       
RB0401         88  PS-EOF-TRNST-UNLD-FILE      VALUE 'Y'.                       
W26600     05  PS-NO-CTOFF-ROWS-SW     PIC X   VALUE 'Y'.                       
W26600         88  PS-MORE-CTOFF-ROWS          VALUE 'Y'.                       
W26600         88  PS-NO-CTOFF-ROWS            VALUE 'N'.                       
RB0401     05  PS-CONTINUE-MATCH-SW    PIC X   VALUE 'Y'.                       
RB0401         88  PS-CONTINUE-MATCH           VALUE 'Y'.                       
RB0401         88  PS-DONT-CONTINUE-MATCH      VALUE 'N'.                       
W26600     05  PS-FIRST-CTOFF-UNLD-READ-SW PIC X VALUE 'Y'.                     
W26600         88 PS-NOT-FIRST-CTOFF-UNLD-READ VALUE 'N'.                       
W26600         88 PS-FIRST-CTOFF-UNLD-READ     VALUE 'Y'.                       
           05  PS-BYPASS-ITEM-SW           PIC X VALUE 'N'.                     
               88 PS-DONT-BYPASS-ITEM          VALUE 'N'.                       
               88 PS-BYPASS-ITEM               VALUE 'Y'.                       
W28545     05  PS-INV-ID-FOUND-SW          PIC X(01) VALUE 'N'.                 
W28545         88  PS-INV-ID-FOUND-NO          VALUE 'N'.                       
W28545         88  PS-INV-ID-FOUND-YES         VALUE 'Y'.                       
W28545     05  PS-IIPM-LOADED-SW           PIC X(01) VALUE 'N'.                 
W28545         88  PS-IIPM-LOADED-NO           VALUE 'N'.                       
W28545         88  PS-IIPM-LOADED-YES          VALUE 'Y'.                       
W28545     05  PS-QUALIFIES-FOR-INVENTORY-SW                                    
W28545                                     PIC X(01) VALUE 'N'.                 
W28545         88  PS-QUALIFIES-FOR-INVENTORY-NO  VALUE 'N'.                    
W28545         88  PS-QUALIFIES-FOR-INVENTORY-YES VALUE 'Y'.                    
      *                                                                         
      *****************************************************************         
      * INPUT BOOKING STORE CONTROL FILE                                        
      *****************************************************************         
       01  WS-IN-STR-RECORD.                                                    
W26600     05  FILLER            PIC X(04)   VALUE SPACES.                      
W26600     05  WS-MORE-STORES    PIC X(01)   VALUE SPACES.                      
W26600     05  WS-IN-STR-NBR     PIC X(04)   VALUE SPACES.                      
W26600     05  WS-IN-STR-NBR-NUM REDEFINES                                      
W26600         WS-IN-STR-NBR     PIC 9(04).                                     
W26600     05  FILLER            PIC X(71)   VALUE SPACES.                      
                                                                                
      *****************************************************************         
W26600* INPUT CTOFF UNLOAD LAYOUT                                               
      *****************************************************************         
W26600     COPY INRD139.                                                        
W26600*                                                                         
      *****************************************************************         
      * OUTPUT FILE RECORD LAYOUT                                               
      *****************************************************************         
W26600     COPY INWS016.                                                        
      *                                                                         
      *****************************************************************         
      * ERROR MESSAGE AREA FOR DB2                                              
      *****************************************************************         
           COPY DPWS004.                                                        
      *                                                                         
      *****************************************************************         
      * TINVPAR DCLGEN                                                          
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              INCLUDE TINVPAR                                                   
           END-EXEC.                                                            
                                                                                
W26600*****************************************************************         
W21732* TSKXREF DCLGEN                                                          
W21732*****************************************************************         
W21732                                                                          
W21732     EXEC SQL                                                             
W21732         INCLUDE TSKXREF                                                  
W21732     END-EXEC.                                                            
W28545                                                                          
W28545*****************************************************************         
W28545* TINCNTL DCLGEN                                                          
W28545*****************************************************************         
W28545                                                                          
W28545     EXEC SQL                                                             
W28545        INCLUDE TINCNTL                                                   
W28545     END-EXEC.                                                            
W28545                                                                          
W28545*****************************************************************         
W28545* INT_INV_PARM_MDSE (INT00004) DCLGEN                                     
W28545*****************************************************************         
W28545                                                                          
W28545     EXEC SQL                                                             
W28545        INCLUDE INT00004                                                  
W28545     END-EXEC.                                                            
W28545                                                                          
W28545*****************************************************************         
W28545* DEPARTMENT CURSOR                                                       
W28545*****************************************************************         
W28545                                                                          
W28545     EXEC SQL                                                             
W28545       DECLARE INT00004_DEPT_CSR CURSOR FOR                               
W28545         SELECT DEPT_NBR                                                  
W28545               ,MAJ_CL_NBR                                                
W28545         FROM INT_INV_PARM_MDSE                                           
W28545              WHERE INV_ID  = :INT00004-INV-ID                            
W28545                AND LOC_NBR = :INT00004-LOC-NBR                           
W28545         FOR FETCH ONLY                                                   
W28545     END-EXEC.                                                            
W21732*                                                                         
       01  ABEND-CODE               PIC S9(04)  VALUE ZERO                      
                                                COMP SYNC.                      
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * A100-MAINLINE-PROCESSING.                                               
      ******************************************************************        
       A100-MAINLINE-PROCESSING.                                                
                                                                                
W26600*** INITIALIZATION...                                                     
           PERFORM Z010-OPEN-FILES.                                             
                                                                                
W26600*** BUILD THE STORE CONTROL TABLE FOR THIS RUN...                         
           PERFORM Z100-READ-STORE-CONTROL.                                     
           IF PS-EOF-STR-NBR-FILE                                               
               SET PS-EMPTY-STR-NBR-FILE TO TRUE.                               
                                                                                
           PERFORM B200-PROCESS-STR-CNTL-FILE                                   
               UNTIL PS-EOF-STR-NBR-FILE.                                       
                                                                                
W26600*** PROCESS THE INVENTORY CUTOFF UNLOAD FILE...                           
W26600     PERFORM Z200-READ-CTOFF-UNLOAD-FILE.                                 
                                                                                
W26600     IF PS-FIRST-CTOFF-UNLD-READ                                          
W26600         IF PS-EOF-CTOFF-UNLD-FILE                                        
                  IF PS-EMPTY-STR-NBR-FILE                                      
                      CONTINUE                                                  
                  ELSE                                                          
                      DISPLAY '* * * * * * * * * * * * * * * * * * *'           
                      DISPLAY 'INKUT160 - '                                     
                      DISPLAY 'FS ADJUST FILE IS EMPTY'                         
                      DISPLAY '* * * * * * * * * * * * * * * * * * *'           
                      MOVE +4019                  TO ABEND-CODE                 
                      CALL 'ILBOABN0'  USING ABEND-CODE                         
                  END-IF                                                        
               ELSE                                                             
                  IF PS-EMPTY-STR-NBR-FILE                                      
                      DISPLAY '* * * * * * * * * * * * * * * * * * *'           
                      DISPLAY 'INKUT160 - '                                     
                      DISPLAY 'STORE CONTROL FILE IS EMPTY'                     
                      DISPLAY '* * * * * * * * * * * * * * * * * * *'           
                      MOVE +4019                  TO ABEND-CODE                 
                      CALL 'ILBOABN0'  USING ABEND-CODE.                        
                                                                                
W26600     SET PS-NOT-FIRST-CTOFF-UNLD-READ TO TRUE.                            
                                                                                
W26600     PERFORM B300-CTOFF-UNLOAD-FILE                                       
W26600         UNTIL PS-EOF-CTOFF-UNLD-FILE.                                    
                                                                                
W26600*** END OF JOB TASKS...                                                   
           PERFORM Z020-CLOSE-FILES.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      /*****************************************************************        
      * B200-PROCESS-STR-CNTL-FILE.                                             
      ******************************************************************        
       B200-PROCESS-STR-CNTL-FILE.                                              
                                                                                
           PERFORM Z300-READ-TINVPAR-TABLE.                                     
W26600     MOVE WS-IN-STR-NBR-NUM TO PV-STR-SUB.                                
                                                                                
           MOVE WS-IN-STR-NBR TO PV-TBLE-STORE(PV-STR-SUB).                     
           MOVE INVPAR-PLND-INV-DTE TO PV-TBLE-INV-DATE(PV-STR-SUB).            
           MOVE INVPAR-SCAN-INV-IND TO PV-TBLE-SCAN-IND(PV-STR-SUB).            
                                                                                
W28545     SET PS-INV-ID-FOUND-NO TO TRUE.                                      
W28545                                                                          
W28545     PERFORM                                                              
W28545        VARYING PV-II-TBL-SUB FROM 1 BY 1                                 
W28545        UNTIL   PV-II-TBL-SUB > WS-PV-INV-ID-OCC-CTR OR                   
W28545                PS-INV-ID-FOUND-YES                                       
W28545            IF INVPAR-INV-ID = WS-INV-ID (PV-II-TBL-SUB)                  
W28545                MOVE PV-II-TBL-SUB TO PV-TBLE-INV-ID-SUB                  
W28545                                        (PV-STR-SUB)                      
W28545                SET PS-INV-ID-FOUND-YES TO TRUE                           
W28545            END-IF                                                        
W28545     END-PERFORM.                                                         
W28545                                                                          
W28545     IF PS-INV-ID-FOUND-NO                                                
W28545         ADD 1 TO WS-PV-INV-ID-OCC-CTR                                    
W28545         IF WS-PV-INV-ID-OCC-CTR > PC-NINETY-NINE                         
W28545             PERFORM B210-OUT-OF-ROOM                                     
W28545         ELSE                                                             
W28545             MOVE INVPAR-INV-ID    TO WS-INV-ID                           
W28545                                        (WS-PV-INV-ID-OCC-CTR)            
W28545             MOVE WS-PV-INV-ID-OCC-CTR TO PV-TBLE-INV-ID-SUB              
W28545                                        (PV-STR-SUB)                      
W28545             PERFORM B220-COUNT-DEFAULT-ROWS                              
W28545         END-IF                                                           
W28545     END-IF.                                                              
W28545                                                                          
           PERFORM Z100-READ-STORE-CONTROL.                                     
                                                                                
W28545 B210-OUT-OF-ROOM.                                                        
W28545                                                                          
W28545     MOVE 'B210-OUT-OF-ROOM           '                                   
W28545                                  TO PV-PARAGRAPH-NAME.                   
W28545     MOVE 'MAX ENTRIES EXCEEDED'  TO PV-DB2-OPER-ATTEMPTED.               
W28545     DISPLAY 'WS-INV-ID-TABLE EXCEEDED '.                                 
W28545     DISPLAY 'WS-PV-INV-ID-OCC-CTR > PC-NINETY-NINE '                     
W28545                                       WS-PV-INV-ID-OCC-CTR.              
W28545     DISPLAY 'INV STORE: ', WS-IN-STR-NBR-NUM.                            
W28545     PERFORM Z900-SQL-ABEND.                                              
W28545                                                                          
W28545                                                                          
W28545 B220-COUNT-DEFAULT-ROWS.                                                 
W28545                                                                          
W28545     MOVE INVPAR-INV-ID TO INT00004-INV-ID.                               
W28545     MOVE ZEROS         TO INT00004-LOC-NBR                               
W28545                           PV-STORE-0000-COUNT.                           
W28545                                                                          
W28545     EXEC SQL                                                             
W28545         SELECT COUNT (DISTINCT DEPT_NBR)                                 
W28545              INTO :PV-STORE-0000-COUNT                                   
W28545              FROM INT_INV_PARM_MDSE                                      
W28545              WHERE INV_ID   = :INT00004-INV-ID                           
W28545                AND LOC_NBR  = :INT00004-LOC-NBR                          
W28545     END-EXEC.                                                            
W28545                                                                          
W28545     EVALUATE TRUE                                                        
W28545         WHEN SQLCODE = ZERO                                              
W28545         WHEN SQLCODE = +100                                              
W28545             PERFORM B230-SET-INV-TYPE                                    
W28545         WHEN SQLWARN IS NOT EQUAL TO SPACES                              
W28545         WHEN SQLCODE IS NOT EQUAL TO ZERO                                
W28545         MOVE 'B220-COUNT-DEFAULT-ROWS' TO PV-PARAGRAPH-NAME              
W28545         MOVE 'COUNT     '        TO PV-DB2-OPER-ATTEMPTED                
W28545         PERFORM Z900-SQL-ABEND                                           
W28545     END-EVALUATE.                                                        
W28545                                                                          
W28545 B230-SET-INV-TYPE.                                                       
W28545                                                                          
W28545     IF PV-STORE-0000-COUNT = ZEROS                                       
W28545         SET WS-INV-TYPE-STORE (WS-PV-INV-ID-OCC-CTR) TO TRUE             
W28545     ELSE                                                                 
W28545         SET WS-INV-TYPE-DEPT  (WS-PV-INV-ID-OCC-CTR) TO TRUE             
W28545     END-IF.                                                              
W28545                                                                          
      /***************************************************************          
      * B300-CTOFF-UNLOAD-FILE.                                                 
      ****************************************************************          
W26600 B300-CTOFF-UNLOAD-FILE.                                                  
                                                                                
W21732     PERFORM Z400-READ-TSKXREF.                                           
W26600     MOVE IN139-SKU       TO SA-SKU-NBR.                                  
                                                                                
W26600     PERFORM B310-PROCESS-CTOFF-UNLD-FILE                                 
W26600       UNTIL IN139-SKU NOT = SA-SKU-NBR                                   
W26600          OR PS-EOF-CTOFF-UNLD-FILE.                                      
                                                                                
      ****************************************************************          
      * B310-PROCESS-CTOFF-UNLD-FILE.                                           
      ****************************************************************          
W26600 B310-PROCESS-CTOFF-UNLD-FILE.                                            
                                                                                
           IF PS-BYPASS-ITEM                                                    
               CONTINUE                                                         
           ELSE                                                                 
W28545       SET PS-QUALIFIES-FOR-INVENTORY-NO TO TRUE                          
W28545       MOVE IN139-LOC-NBR      TO PV-HOLD-STORE-NBR                       
W28545       MOVE SKXREF-DEPT        TO PV-HOLD-DEPT-NBR                        
W28545       MOVE SKXREF-SUBCLASS    TO PV-HOLD-MCL-NBR                         
W28545       PERFORM C000-CHECK-BY-INV-TYPE                                     
W28545       IF PS-QUALIFIES-FOR-INVENTORY-YES                                  
               INITIALIZE IN016-PHYSICAL-COUNT-RECORD                           
                                                                                
W26600         MOVE IN139-LOC-NBR            TO PV-STR-SUB                      
W21732         MOVE SKXREF-DEPT              TO IN016-DEPT                      
W21732         MOVE SKXREF-SUBCLASS          TO IN016-CLASS                     
W21732         MOVE IN139-SKU                TO IN016-SKU                       
W21732         MOVE SKXREF-SKU-STAT-CDE      TO IN016-STAT-CDE                  
               MOVE PV-TBLE-INV-DATE(PV-STR-SUB) TO IN016-INV-DATE              
               MOVE PV-TBLE-SCAN-IND(PV-STR-SUB) TO IN016-SCAN-IND              
W26600         MOVE IN139-LOC-NBR            TO PV-EDIT-LOC-NBR                 
W26600         MOVE PV-EDIT-LOC-NBR          TO IN016-STORE-9                   
W26600         MOVE IN139-CTOFF-OH-QTY       TO IN016-OLD-ONHAND-QTY            
W26600         MOVE IN139-POST-FSH-RCPT-QTY  TO IN016-AFTER-RECEIPTS            
W26600         MOVE SKXREF-ITM-NBR           TO IN016-ITEM-NUMBER               
W28545*        IF IN139-IN-TRNST-CTOFF-QTY NOT EQUAL ZEROES                     
W28545*            MOVE ZEROES               TO IN016-CUTOFF-INTRANSIT          
W28545*        ELSE                                                             
W28545*            MOVE IN139-IN-TRNST-CTOFF-QTY                                
W28545*                                      TO IN016-CUTOFF-INTRANSIT          
W28545*        END-IF.                                                          
W28545*        MOVE IN139-STR-EXPTED-CTOFF-QTY                                  
W28545*                                      TO IN016-STR-EXP-OH-QTY.           
W28545*        PERFORM Z800-WRITE-OUTPUT-RECORD.                                
W28545         MOVE ZEROES                   TO IN016-CUTOFF-INTRANSIT          
W28545         MOVE IN139-STR-EXPTED-CTOFF-QTY                                  
W28545                                       TO IN016-STR-EXP-OH-QTY            
W28545         PERFORM Z800-WRITE-OUTPUT-RECORD                                 
W28545       END-IF                                                             
W28545     END-IF.                                                              
                                                                                
W26600     PERFORM Z200-READ-CTOFF-UNLOAD-FILE.                                 
                                                                                
W28545******************************************************************        
W28545*C000-CHECK-BY-INV-TYPE.                                                  
W28545* THE C000 SERIES LOGIC WILL DETERMINE IF THE INVENTORY IS BEING          
W28545* DONE FOR THE WHOLE STORE OR SPECIFIC CLASSES IN ONE OR MORE             
W28545* DEPARTMENTS. FOR A STORE WIDE INVENTORY, ALL RECORDS ARE                
W28545* INCLUDED. FOR A DEPARTMENT INVENTORY, ONLY THOSE DEPARTMENT             
W28545* SKUS WILL BE INCLUDED. THE INFORMATION FOR THIS DECISION                
W28545* IS CARRIED IN INT_INV_PARM_MDSE. THIS INFORMATION WILL BE               
W28545* LOADED INTO MEMORY ONCE PER STORE.                                      
W28545******************************************************************        
W28545                                                                          
W28545 C000-CHECK-BY-INV-TYPE.                                                  
W28545                                                                          
W28545     MOVE PV-TBLE-INV-ID-SUB (PV-HOLD-STORE-NBR)                          
W28545       TO PV-II-TBL-SUB.                                                  
W28545                                                                          
W28545     IF PV-TBLE-INV-ID-SUB (PV-HOLD-STORE-NBR) = ZEROS                    
W28545         CONTINUE                                                         
W28545     ELSE                                                                 
W28545         IF WS-INV-TYPE-STORE (PV-II-TBL-SUB)                             
W28545             SET PS-QUALIFIES-FOR-INVENTORY-YES TO TRUE                   
W28545             MOVE PV-HOLD-STORE-NBR TO PV-PREV-STORE-NBR                  
W28545     ELSE                                                                 
W28545         PERFORM C005-CHECK-BY-DEPT-CLS                                   
W28545     END-IF.                                                              
W28545                                                                          
W28545                                                                          
W28545 C005-CHECK-BY-DEPT-CLS.                                                  
W28545                                                                          
W28545* WHEN STORE NUMBERS ARE NOT EQUAL, A NEW STORE IS BEING                  
W28545* PROCESSED AND THE DEPT DATA MUST BE LOADED INTO WS FROM DB2             
W28545                                                                          
W28545     IF PV-HOLD-STORE-NBR NOT EQUAL PV-PREV-STORE-NBR                     
W28545         MOVE ZEROS TO PV-STORE-XXXX-COUNT                                
W28545         PERFORM C010-COUNT-STORE-SPECIFIC                                
W28545             IF PV-STORE-XXXX-COUNT > ZEROS                               
W28545                 PERFORM C025-LOAD-STORE-SPECIFIC                         
W28545             ELSE                                                         
W28545                 PERFORM C075-LOAD-DEFAULTS                               
W28545             END-IF                                                       
W28545         MOVE PV-HOLD-STORE-NBR TO PV-PREV-STORE-NBR                      
W28545     END-IF.                                                              
W28545                                                                          
W28545     PERFORM                                                              
W28545         VARYING PV-IIPM-TBL-SUB FROM 1 BY 1                              
W28545         UNTIL   PV-IIPM-TBL-SUB > WS-PV-IIPM-OCC-CTR OR                  
W28545                 PS-QUALIFIES-FOR-INVENTORY-YES                           
W28545                                                                  INKUT102
W28545             IF PV-HOLD-DEPT-NBR = WS-IIPM-DEPT-NBR               INKUT102
W28545                                           (PV-IIPM-TBL-SUB)      INKUT102
W28545*                                          (PV-IIPM-TBL-SUB) AND  INKUT102
W28545*               PV-HOLD-MCL-NBR = WS-IIPM-MCL-NBR                 INKUT102
W28545*                                          (PV-IIPM-TBL-SUB)      INKUT102
W28545                 SET PS-QUALIFIES-FOR-INVENTORY-YES TO TRUE       INKUT102
W28545             END-IF                                                       
W28545     END-PERFORM.                                                         
W28545                                                                          
W28545                                                                          
W28545 C010-COUNT-STORE-SPECIFIC.                                               
W28545                                                                          
W28545     MOVE WS-INV-ID (PV-II-TBL-SUB) TO INT00004-INV-ID.                   
W28545     MOVE PV-HOLD-STORE-NBR         TO INT00004-LOC-NBR.                  
W28545                                                                          
W28545     EXEC SQL                                                             
W28545         SELECT COUNT (DISTINCT DEPT_NBR)                                 
W28545              INTO :PV-STORE-XXXX-COUNT                                   
W28545              FROM INT_INV_PARM_MDSE                                      
W28545              WHERE INV_ID   = :INT00004-INV-ID                           
W28545                AND LOC_NBR  = :INT00004-LOC-NBR                          
W28545     END-EXEC.                                                            
W28545                                                                          
W28545     EVALUATE TRUE                                                        
W28545         WHEN SQLCODE = ZERO                                              
W28545         WHEN SQLCODE = +100                                              
W28545             CONTINUE                                                     
W28545         WHEN SQLWARN IS NOT EQUAL TO SPACES                              
W28545         WHEN SQLCODE IS NOT EQUAL TO ZERO                                
W28545         MOVE 'C010-COUNT-STORE-SPECIFIC' TO PV-PARAGRAPH-NAME            
W28545         MOVE 'COUNT     '        TO PV-DB2-OPER-ATTEMPTED                
W28545         PERFORM Z900-SQL-ABEND                                           
W28545     END-EVALUATE.                                                        
W28545                                                                          
W28545                                                                          
W28545 C025-LOAD-STORE-SPECIFIC.                                                
W28545                                                                          
W28545     INITIALIZE WS-INT-INV-PARM-MDSE-TABLE.                               
W28545                                                                          
W28545     PERFORM C100-OPEN-CUR.                                               
W28545                                                                          
W28545     SET PS-IIPM-LOADED-NO TO TRUE.                                       
W28545                                                                          
W28545     PERFORM C130-LOAD-ENTRIES                                            
W28545         UNTIL PS-IIPM-LOADED-YES.                                        
W28545                                                                          
W28545     PERFORM C110-CLOSE-CUR.                                              
W28545                                                                          
W28545                                                                          
W28545 C075-LOAD-DEFAULTS.                                                      
W28545                                                                          
W28545     MOVE WS-INV-ID (PV-II-TBL-SUB) TO INT00004-INV-ID.                   
W28545     MOVE ZEROS                     TO INT00004-LOC-NBR.                  
W28545                                                                          
W28545     INITIALIZE WS-INT-INV-PARM-MDSE-TABLE.                               
W28545                                                                          
W28545     PERFORM C100-OPEN-CUR.                                               
W28545                                                                          
W28545     SET PS-IIPM-LOADED-NO TO TRUE.                                       
W28545                                                                          
W28545     PERFORM C130-LOAD-ENTRIES                                            
W28545         UNTIL PS-IIPM-LOADED-YES.                                        
W28545                                                                          
W28545     PERFORM C110-CLOSE-CUR.                                              
W28545                                                                          
W28545                                                                          
W28545 C100-OPEN-CUR.                                                           
W28545*                                                                         
W28545     EXEC SQL                                                             
W28545         OPEN INT00004_DEPT_CSR                                           
W28545     END-EXEC.                                                            
W28545                                                                          
W28545     EVALUATE TRUE                                                        
W28545       WHEN SQLCODE IS EQUAL TO ZERO                                      
W28545         CONTINUE                                                         
W28545       WHEN SQLWARN IS NOT EQUAL TO SPACES                                
W28545       WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
W28545        MOVE 'C100-OPEN-CUR    ' TO PV-PARAGRAPH-NAME                     
W28545        MOVE 'OPEN CURSOR'       TO PV-DB2-OPER-ATTEMPTED                 
W28545        PERFORM Z900-SQL-ABEND                                            
W28545     END-EVALUATE.                                                        
W28545                                                                          
W28545                                                                          
W28545 C110-CLOSE-CUR.                                                          
W28545*                                                                         
W28545     EXEC SQL                                                             
W28545         CLOSE INT00004_DEPT_CSR                                          
W28545     END-EXEC.                                                            
W28545*                                                                         
W28545     EVALUATE TRUE                                                        
W28545       WHEN SQLCODE IS EQUAL TO ZERO                                      
W28545         CONTINUE                                                         
W28545       WHEN SQLWARN IS NOT EQUAL TO SPACES                                
W28545       WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
W28545        MOVE 'C110-CLOSE-CUR    ' TO PV-PARAGRAPH-NAME                    
W28545        MOVE 'CLOSE CURSOR'       TO PV-DB2-OPER-ATTEMPTED                
W28545        PERFORM Z900-SQL-ABEND                                            
W28545     END-EVALUATE.                                                        
W28545                                                                          
W28545 C130-LOAD-ENTRIES.                                                       
W28545                                                                          
W28545     PERFORM C140-FETCH-INT-INV-CUR.                                      
W28545                                                                          
W28545     IF PS-IIPM-LOADED-NO                                                 
W28545         PERFORM C145-LOAD-IIPM-TO-TBL                                    
W28545     END-IF.                                                              
W28545                                                                          
W28545                                                                          
W28545 C140-FETCH-INT-INV-CUR.                                                  
W28545*                                                                         
W28545     EXEC SQL                                                             
W28545         FETCH INT00004_DEPT_CSR                                          
W28545         INTO :INT00004-DEPT-NBR,                                         
W28545              :INT00004-MAJ-CL-NBR                                        
W28545     END-EXEC.                                                            
W28545     EVALUATE TRUE                                                        
W28545       WHEN SQLCODE IS EQUAL TO ZERO                                      
W28545           CONTINUE                                                       
W28545       WHEN SQLCODE IS EQUAL TO +100                                      
W28545         SET  PS-IIPM-LOADED-YES TO TRUE                                  
W28545       WHEN SQLWARN IS NOT EQUAL TO SPACES                                
W28545       WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
W28545         MOVE 'C140-FETCH-INT-INV-CUR' TO PV-PARAGRAPH-NAME               
W28545         MOVE 'LOAD TABLE'        TO PV-DB2-OPER-ATTEMPTED                
W28545         PERFORM Z900-SQL-ABEND                                           
W28545     END-EVALUATE.                                                        
W28545                                                                          
W28545                                                                          
W28545 C145-LOAD-IIPM-TO-TBL.                                                   
W28545                                                                          
W28545     ADD 1 TO WS-PV-IIPM-OCC-CTR.                                         
W28545                                                                          
W28545     IF WS-PV-IIPM-OCC-CTR > PC-FOUR-THOUSAND                             
W28545         MOVE 'C145-LOAD-IIPM-TO-TBL      '                               
W28545                                  TO PV-PARAGRAPH-NAME                    
W28545         MOVE 'MAX ENTRIES EXCEEDED'  TO PV-DB2-OPER-ATTEMPTED            
W28545         DISPLAY 'WS-INT-INV-PARM-MDSE-TABLE '                            
W28545         DISPLAY 'WS-PV-IIPM-OCC-CTR > PC-FOUR-THOUSAND '                 
W28545                                       WS-PV-IIPM-OCC-CTR                 
W28545         DISPLAY 'INV STORE: ', WS-IN-STR-NBR-NUM                         
W28545         PERFORM Z900-SQL-ABEND                                           
W28545     ELSE                                                                 
W28545         MOVE INT00004-DEPT-NBR TO WS-IIPM-DEPT-NBR                       
W28545                                    (WS-PV-IIPM-OCC-CTR)                  
W28545         MOVE INT00004-MAJ-CL-NBR TO WS-IIPM-MCL-NBR                      
W28545                                    (WS-PV-IIPM-OCC-CTR)                  
W28545     END-IF.                                                              
W28545                                                                          
W28545                                                                          
      ******************************************************************        
      *    OPEN ALL FILES.                                                      
      ******************************************************************        
       Z010-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  STORE-CONTROL-FILE                                       
W26600                 IN-CTOFF-UNLOAD-FILE                                     
W26600          OUTPUT OUT-FS-CUTOFFS-FILE.                                     
      *                                                                         
      ******************************************************************        
      *   CLOSE ALL FILES.                                                      
      ******************************************************************        
       Z020-CLOSE-FILES.                                                        
                                                                                
           CLOSE STORE-CONTROL-FILE                                             
W26600           IN-CTOFF-UNLOAD-FILE                                           
W26600           OUT-FS-CUTOFFS-FILE.                                           
      *                                                                         
      ****************************************************************          
      *  READS THE INPUT FILE OF STORES TO HAVE UNITS BOOKED.                   
      ****************************************************************          
      *                                                                         
       Z100-READ-STORE-CONTROL.                                                 
                                                                                
           READ STORE-CONTROL-FILE INTO WS-IN-STR-RECORD                        
                 AT END                                                         
                   SET PS-EOF-STR-NBR-FILE TO TRUE.                             
                                                                                
W26600     IF WS-IN-STR-NBR IS EQUAL TO '0000'                                  
W26600         SET PS-EOF-STR-NBR-FILE     TO TRUE                              
W26600     END-IF.                                                              
W26600                                                                          
      ****************************************************************          
W26600* Z200-READ-CTOFF-UNLOAD-FILE.                                            
      ****************************************************************          
W26600 Z200-READ-CTOFF-UNLOAD-FILE.                                             
                                                                                
W26600     READ IN-CTOFF-UNLOAD-FILE INTO INRD139-CUTOFF-REC                    
                 AT END                                                         
W26600             SET PS-EOF-CTOFF-UNLD-FILE TO TRUE.                          
                                                                                
      ******************************************************************        
      * Z300-READ-TINVPAR-TABLE.                                                
      ******************************************************************        
       Z300-READ-TINVPAR-TABLE.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
                                                                                
W26600         MOVE WS-IN-STR-NBR-NUM TO INVPAR-LOC-NBR                         
W26600                                                                          
               EXEC SQL                                                         
                  SELECT PLND_INV_DTE                                           
                        ,SCAN_INV_IND                                           
W28545                  ,A.INV_ID                                               
                  INTO :INVPAR-PLND-INV-DTE,                                    
                       :INVPAR-SCAN-INV-IND                                     
W28545                ,:INVPAR-INV-ID                                           
W28545            FROM TINVPAR A                                                
W28545                ,TINCNTL B                                                
W26600            WHERE (LOC_NBR = :INVPAR-LOC-NBR) AND                         
                        (ACTL_UNIT_BK_DTE = :PC-EMPTY-DATE)                     
W28545              AND B.ACTV_IND        = 'Y'                                 
W28545              AND A.INV_ID          = B.INV_ID                            
W33304              AND A.LOC_INV_STAT_CDE = 'IN'                               
W26600            WITH UR                                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       DISPLAY 'ERROR FOR STORE: ' WS-IN-STR-NBR                
                       DISPLAY 'UNITS MAY HAVE ALREADY BEEN BOOKED'             
                       DISPLAY 'OR STORE NUMBER IS INVALID'                     
                       MOVE 'Z300-READ-TINVPAR-TABLE'                           
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'STORE NOT FOUND READING TINVPAR'                   
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z300-READ-TINVPAR-TABLE'                           
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'READING TINVPAR TABLE'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z300-READ-TINVPAR-TABLE'                                   
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'READ RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ****************************************************************          
      * Z400-READ-TSKXREF.                                                      
      ****************************************************************          
W21732 Z400-READ-TSKXREF.                                                       
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
W26600         MOVE IN139-SKU       TO SKXREF-SKU                               
W26600                                                                          
W21732         EXEC SQL                                                         
W21732           SELECT X.DEPT,                                                 
W21732                  X.SUBCLASS,                                             
W21732                  X.SKU,                                                  
W21732                  X.SKU_STAT_CDE,                                 INKUT116
W21732                  X.ITM_NBR                                       INKUT116
W21732           INTO :SKXREF-DEPT,                                     INKUT116
W21732                :SKXREF-SUBCLASS,                                         
W21732                :SKXREF-SKU,                                              
W21732                :SKXREF-SKU-STAT-CDE,                                     
W21732                :SKXREF-ITM-NBR                                           
W21732           FROM TSKXREF X                                         INKUT116
W26600           WHERE X.SKU = :SKXREF-SKU                              INKUT116
W21732         END-EXEC                                                         
W26600                                                                          
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
W27710                 SET PS-DONT-BYPASS-ITEM TO TRUE                          
                   WHEN SQLCODE = +100                                          
W26600                 MOVE IN139-LOC-NBR TO PV-EDIT-LOC-NBR                    
                       DISPLAY '* * * * * * * * * * * * * * * * * * * *'        
W21732                 DISPLAY 'ITM NBR NOT FOUND ON XREF-BYPASS RECORD'        
                       DISPLAY '* * * * * * * * * * * * * * * * * * * *'        
W26600                 DISPLAY 'UNLOAD STORE: ' PV-EDIT-LOC-NBR                 
                       DISPLAY 'UNLOAD SKU: ' IN139-SKU                         
                       DISPLAY '* * * * * * * * * * * * * * * * * * * *'        
                       SET PS-BYPASS-ITEM TO TRUE                               
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
W21732                 MOVE  'Z400-READ-TSKXREF'                                
                                     TO  PV-PARAGRAPH-NAME                      
W21732                 MOVE 'READING TSKXREF TABLE'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
W21732         MOVE  'Z400-READ-TSKXREF'                                        
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'READ RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * Z800-WRITE-OUTPUT-RECORD.                                               
      ******************************************************************        
       Z800-WRITE-OUTPUT-RECORD.                                                
                                                                                
           WRITE OUT-FS-CUTOFFS-RECORD                                          
               FROM IN016-PHYSICAL-COUNT-RECORD.                                
                                                                                
      ****************************************************************          
      *    THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.        *          
      ****************************************************************          
       Z900-SQL-ABEND.                                                          
      *                                                                         
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUT160'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
      *                                                                         
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
      *                                                                         
