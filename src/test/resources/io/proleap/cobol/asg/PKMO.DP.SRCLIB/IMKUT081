       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              IMKUT081.                                       
       AUTHOR.                  BOB FORD.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            09/01/04.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *             REGIONAL CLEARANCE PRICED SKUS                     *        
      *                   SUMMARY EXTRACT                              *        
      *                                                                *        
      *   THIS PROGRAM READS AN INPUT FILE OF RCP SKU/STORE RECORDS    *        
      *   BUILT BY IMKUT080.  THE FILE CONTAINS ALL SKUS WITH A        *        
      *   REGN-PRIC-IND = 'Y'.  THERE IS A 'CORP' RECORD (STORE = 0)   *        
      *   FOR EACH SKU, AND ANY STORE-SPECIFIC RECORDS FOUND ON THE    *        
      *   ITEM-LOC-DOMAIN FILE (WHICH BUILDS TUPCPLS).                 *        
      *                                                                *        
      *   A CURSOR IS READ TO RETRIEVE ALL OPEN STORES WHERE THE TYPE  *        
      *   = 'CS' OR 'DS' AND THE LOCS ARE STORED IN AN INTERNAL TABLE. *        
      *   THE STORE 0 RECORD IS READ FOR THE ITEM, THEN THE NEXT       *        
      *   RECORD IS READ.  THE STORE NUMBER IS COMPARED TO THE INTERNAL*        
      *   STORE TABLE.  FOR EACH STORE > 0 A RECORD IS WRITTEN TO      *        
      *   THE OUTPUT FILE USING THE DATA ON THE INPUT.  FOR EACH VALID *        
      *   STORE THAT IS NOT ON THE INPUT FILE A RECORD IS WRITTEN WITH *        
      *   THE STORE 0 DATA.                                            *        
      *                                                                *        
      ******************************************************************        
      *  CHANGE LOG:                                                            
      *  09/07/04 - BOB FORD                                                    
      *             (WR #27494) NEW PROGRAM -                                   
      ******************************************************************        
      *  CHANGE LOG:                                                            
      *  09/14/04 - BOB FORD      (WR #27494)                                   
      *             CHANGED LOGIC IN 2400- TO ACCOUNT FOR RECS WHERE            
      *             THE INPUT LOC-NBR IS NOT ON THE INTERNAL LOC-TABLE.         
      *             CHANGED OUTPUT FILE TO LRECL 40.                            
      ******************************************************************        
      *  CHANGE LOG:                                                            
      *  10/04/04 - BOB FORD      (WR #27494)                                   
      *             ADDED LOGIC TO WRITE THE FINAL RECORD COUNT WRITTEN         
      *             TO A PDS CONTROL CARD, USED IN FSK013.                      
      ******************************************************************        
      *  10/26/04 - BOB FORD      (WR #27494)                                   
      *             ADDED LOGIC TO FILL NEW FIELDS STAT-CHG-DTE AND             
      *             UNT-RTL-CHG-DTE, CHANGED LRECLS                             
      ******************************************************************        
      *  09/20/05 - D. THEEL      WR #29434                                     
      *             INCREASED INPUT AND OUT01 LRECLS.                           
      *             ADDED NEW COLS ORIG-CLR-DTE AND MKDN-CDE TO                 
      *             PV-CORP, IMRD092, AND IMRD093 LOGIC.                        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT ITEM-LOC-FILE                                                 
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT RCP-SUMMARY-FILE-OUT                                          
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT RCP-RECORD-COUNT-OUT                                          
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  ITEM-LOC-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 64 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ITEM-LOC-REC                    PIC  X(64).                          
                                                                                
       FD  RCP-SUMMARY-FILE-OUT                                                 
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RCP-SUMMARY-REC        PIC X(71).                                    
                                                                                
       FD  RCP-RECORD-COUNT-OUT                                                 
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RCP-RECORD-COUNT-REC   PIC X(80).                                    
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CD-CURRENT-DATE.                                                     
           05  CD-CURRENT-YEAR         PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-MONTH        PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-DAY          PIC 9(2)     VALUE ZERO.                 
                                                                                
       01  CD-CURRENT-DATE-ISO.                                                 
           05  CD-CURRENT-CC-ISO       PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-YEAR-ISO     PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-MONTH-ISO    PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-DAY-ISO      PIC 9(2)     VALUE ZERO.                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-20                   PIC X(02)   VALUE '20'.                  
           05  PC-25                   PIC X(02)   VALUE '25'.                  
           05  PC-30                   PIC X(02)   VALUE '30'.                  
           05  PC-R                    PIC X(01)   VALUE 'R'.                   
           05  PC-Y                    PIC X(01)   VALUE 'Y'.                   
           05  PC-W                    PIC X(01)   VALUE 'W'.                   
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PROGRAM-NAME         PIC X(8)        VALUE 'IMKUT081'.        
           05  PV-PARAGRAPH-NAME       PIC X(25)       VALUE SPACE.             
           05  PV-ABEND-MESSAGE        PIC X(50)       VALUE SPACE.             
           05  PV-OPER-ATTEMPTED       PIC X(30)       VALUE SPACES.            
           05  PV-INTR-UPC-ID          PIC S9(9)       VALUE +0                 
                                                       COMP.                    
           05  PV-STATUS-CODE          PIC X(02)      VALUE SPACES.             
           05  PV-SKUS-READ-CNT        PIC 9(11)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-SKUS-STAT-ERR-CNT    PIC 9(7)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-STAT-20-CNT-A        PIC 9(11)      VALUE ZERO                
                                                       COMP-3.                  
           05  PV-STAT-30-CNT-A        PIC 9(11)      VALUE ZERO                
                                                       COMP-3.                  
           05  PV-TOT-OUT-CNT          PIC 9(11)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-WRK-OUT-CNT          PIC 9(7)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-TOT-OUT-MAX          PIC 9(8)   VALUE 4999999                 
                                                       COMP-3.                  
           05  PV-LOC-NBR              PIC S9(04)     VALUE +0 COMP.            
           05  PV-9-12                 PIC 9(12)     VALUE ZERO.                
       01  PV-CORP-REC.                                                         
           05 PV-CORP-SKU             PIC S9(08)V COMP-3 VALUE +0.              
           05 PV-CORP-ITM-NBR         PIC S9(15)V COMP-3 VALUE +0.              
           05 PV-CORP-INTR-UPC-ID     PIC S9(09) COMP    VALUE +0.              
           05 PV-CORP-STAT-CDE        PIC  X(02)         VALUE SPACE.           
           05 PV-CORP-UNT-RTL-AMT     PIC S9(07)V9(02) COMP-3 VALUE +0.         
           05 PV-CORP-STR-NBR         PIC S9(04) COMP    VALUE +0.              
           05 PV-CORP-TICKET-COLOR    PIC X              VALUE SPACE.           
           05 PV-CORP-UNT-RTL-CHG-DTE PIC  X(10)         VALUE SPACE.           
           05 PV-CORP-STAT-CHG-DTE    PIC  X(10)         VALUE SPACE.           
           05 PV-CORP-ORIG-CLR-DTE    PIC  X(10)         VALUE SPACE.           
           05 PV-CORP-MKDN-CDE        PIC  X             VALUE SPACE.           
           05 FILLER                  PIC X(6)           VALUE SPACE.           
       01  PV-OUTPUT-REC.                                                       
           05 PV-OR-RECORD-COUNT      PIC 9(09).                                
           05 FILLER                  PIC X(71)   VALUE SPACES.                 
                                                                                
       01  PS-SWITCHES.                                                         
           05  PS-END-OF-LOC-CSR-SW        PIC X       VALUE 'N'.               
               88  PS-END-OF-LOC-CSR                   VALUE 'Y'.               
           05  PS-ITEM-LOC-EOF-SW          PIC X       VALUE 'N'.               
               88  PS-ITEM-LOC-EOF                     VALUE 'Y'.               
               88  PS-NOT-ITEM-LOC-EOF                 VALUE 'N'.               
           05  PS-END-OF-ITEM-SW           PIC X       VALUE 'N'.               
               88  PS-END-OF-ITEM                      VALUE 'Y'.               
               88  PS-NOT-END-OF-ITEM                  VALUE 'N'.               
                                                                                
                                                                                
       01  ABEND-CODE                          PIC S9(04)  VALUE +4003          
                                                           COMP SYNC.           
      *****************************************************************         
      *     VALID LOCATION TABLE                                      *         
      *****************************************************************         
       01  TABLE-MAX.                                                           
           10  LOC-TABLE-MAX         PIC 9(5)       VALUE 32767.                
                                                                                
       01  LOCATION-TABLE.                                                      
           05  LOCATION-TBL-SIZE     PIC 9(5)       VALUE ZEROES.               
           05  LOCATION-ENTRY        OCCURS 32767 TIMES                         
                                         INDEXED BY LOC-INDEX.                  
               10  LOC-NBR           PIC 9(05).                                 
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE SKU/LOC EXTRACT FILE                               
      ******************************************************************        
           COPY IMRD092.                                                        
      ******************************************************************        
      *  COPY MEMBER FOR THE RCP SKU LIST FILE                                  
      ******************************************************************        
           COPY IMRD093.                                                        
                                                                                
      *****************************************************************         
      * DB2 TABLE                                                               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 STANDARD COPYBOOK                                                   
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING                   
      *  SQL CODES FOR DISPLAY                                                  
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
      *  DECLARE SQL STATEMENT FOR RETRIEVING STORES FROM THE                   
      *  LOCATION TABLE.                                                        
      ******************************************************************        
           EXEC SQL                                                             
               DECLARE LOC_CSR CURSOR FOR                                       
                SELECT A.LOC_NBR                                                
                  FROM XMT_LOC A                                                
                WHERE A.LOC_TYP_CDE IN ('DS', 'CS')                             
                  AND ((A.OPN_DTE < ((CURRENT DATE) + 6 MONTHS)                 
                  AND  A.CLO_DTE > CURRENT DATE)                                
                  OR  (A.OPN_DTE > CURRENT DATE)                                
                  AND OPN_DTE < ((CURRENT DATE) + 6 MONTHS))                    
                ORDER BY A.LOC_NBR                                              
                WITH UR                                                         
           END-EXEC.                                                            
      *                                                                         
                                                                                
           EJECT                                                                
       LINKAGE SECTION.                                                         
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* START OF IMKUT081               '.                        
           DISPLAY '**********************************'.                        
                                                                                
           PERFORM 1000-HOUSEKEEPING.                                           
                                                                                
           SET PS-NOT-ITEM-LOC-EOF TO TRUE                                      
                                                                                
           PERFORM 2000-READ-INPUT-FILE.                                        
           IF PS-ITEM-LOC-EOF                                                   
              DISPLAY '***********************************************'         
              DISPLAY '*** NO REGIONAL CLEARANCE PRICED SKUS FOUND ***'         
              DISPLAY '***********************************************'         
           ELSE                                                                 
              PERFORM 2200-PROCESS-RCP-SKUS                                     
                  UNTIL PS-ITEM-LOC-EOF                                         
           END-IF.                                                              
                                                                                
           MOVE PV-TOT-OUT-CNT          TO PV-OR-RECORD-COUNT                   
           WRITE RCP-RECORD-COUNT-REC FROM PV-OUTPUT-REC.                       
                                                                                
           PERFORM 1900-EOJ.                                                    
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* IMKUT081 PROGRAM STATISTICS:    '.                        
           DISPLAY '**********************************'.                        
           DISPLAY '* TOTAL SKUS READ: ' PV-SKUS-READ-CNT.                      
           DISPLAY '* TOTAL SKUS READ WITH INVALID STATUS: '                    
                                         PV-SKUS-STAT-ERR-CNT.                  
           DISPLAY '* TOTAL STORE SUMMARY/STATUS 20 RECORDS: '                  
                                         PV-STAT-20-CNT-A.                      
           DISPLAY '* TOTAL STORE SUMMARY/STATUS 30 RECORDS: '                  
                                         PV-STAT-30-CNT-A.                      
           DISPLAY '**********************************'.                        
           DISPLAY '**********************************'.                        
           DISPLAY '* END OF IMKUT081                 '.                        
           DISPLAY '**********************************'.                        
                                                                                
           STOP RUN.                                                            
      ******************************************************************        
      *    OPEN FILES & DETERMINE CURRENT DATE CCYY-MM-DD.                      
      ******************************************************************        
       1000-HOUSEKEEPING.                                                       
                                                                                
           OPEN INPUT  ITEM-LOC-FILE                                            
                OUTPUT RCP-SUMMARY-FILE-OUT                                     
                       RCP-RECORD-COUNT-OUT.                                    
                                                                                
           ACCEPT CD-CURRENT-DATE FROM DATE.                                    
      **** DETERMINE CENTURY ****                                               
           IF CD-CURRENT-YEAR > '80'                                            
               MOVE '19' TO CD-CURRENT-CC-ISO                                   
           ELSE                                                                 
               MOVE '20' TO CD-CURRENT-CC-ISO                                   
           END-IF.                                                              
           MOVE CD-CURRENT-YEAR   TO CD-CURRENT-YEAR-ISO.                       
           MOVE CD-CURRENT-MONTH  TO CD-CURRENT-MONTH-ISO.                      
           MOVE CD-CURRENT-DAY    TO CD-CURRENT-DAY-ISO.                        
                                                                                
           PERFORM 1200-LOAD-LOCATION-TBL.                                      
           DISPLAY 'NUMBER OF LOCS FOUND = ' LOCATION-TBL-SIZE.                 
                                                                                
      ******************************************************************        
      *  LOAD WORKING-STORAGE LOCATION TABLE WITH VALID LOCATIONS.              
      ******************************************************************        
       1200-LOAD-LOCATION-TBL.                                                  
                                                                                
           MOVE '1200-LOAD-LOCATION-TBL' TO PV-PARAGRAPH-NAME.                  
           EXEC SQL                                                             
               OPEN LOC_CSR                                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                CONTINUE                                                        
              WHEN OTHER                                                        
                MOVE 'OPEN LOCATION CURSOR' TO                                  
                    PV-OPER-ATTEMPTED                                           
                PERFORM 9998-DB2-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
           MOVE ZERO TO LOCATION-TBL-SIZE.                                      
           MOVE 'N' TO PS-END-OF-LOC-CSR-SW.                                    
           PERFORM 1201-FETCH-LOC-CSR.                                          
                                                                                
           PERFORM VARYING LOC-INDEX FROM 1 BY 1                                
                             UNTIL LOC-INDEX > LOC-TABLE-MAX                    
                                OR PS-END-OF-LOC-CSR                            
             MOVE XMT00001-LOC-NBR      TO LOC-NBR(LOC-INDEX)                   
             ADD 1 TO LOCATION-TBL-SIZE                                         
             PERFORM 1201-FETCH-LOC-CSR                                         
           END-PERFORM.                                                         
                                                                                
           IF LOC-INDEX >  LOC-TABLE-MAX                                        
             DISPLAY '*********** ABEND ************************'               
             DISPLAY ' TABLE OVERFLOW ON LOCATION-TABLE         '               
             DISPLAY ' INCREASE LOC-TABLE-MAX LIMIT             '               
             DISPLAY ' CURRENT LIMIT = ' LOC-TABLE-MAX                          
             DISPLAY '******************************************'               
             DISPLAY '  '                                                       
             MOVE +4004 TO ABEND-CODE                                           
             CALL 'ILBOABN0' USING ABEND-CODE                                   
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               CLOSE LOC_CSR                                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                CONTINUE                                                        
              WHEN OTHER                                                        
                MOVE 'CLOSE LOCATION CURSOR' TO                                 
                    PV-OPER-ATTEMPTED                                           
                PERFORM 9998-DB2-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  FETCH ALL LOCATIONS.                                                   
      ******************************************************************        
       1201-FETCH-LOC-CSR.                                                      
                                                                                
           MOVE '1201-FETCH-LOC-CSR' TO PV-PARAGRAPH-NAME.                      
                                                                                
           EXEC SQL                                                             
               FETCH LOC_CSR                                                    
               INTO :XMT00001-LOC-NBR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN +0                                                            
                   CONTINUE                                                     
             WHEN +100                                                          
                   SET PS-END-OF-LOC-CSR TO TRUE                                
             WHEN OTHER                                                         
                   MOVE 'FETCH LOCATION CURSOR' TO                              
                        PV-OPER-ATTEMPTED                                       
                   PERFORM 9998-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
       1900-EOJ.                                                                
                                                                                
           CLOSE ITEM-LOC-FILE                                                  
                 RCP-SUMMARY-FILE-OUT                                           
                 RCP-RECORD-COUNT-OUT.                                          
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE INPUT FILE.                                 
      ******************************************************************        
       2000-READ-INPUT-FILE.                                                    
                                                                                
           READ ITEM-LOC-FILE                                                   
               INTO IM092-RCP-SKU-REC                                           
               AT END                                                           
                   SET PS-ITEM-LOC-EOF TO TRUE                                  
               NOT AT END                                                       
                   ADD +1 TO PV-SKUS-READ-CNT.                                  
                                                                                
      ******************************************************************        
      *    PROCESS ALL REGIONAL CLEARANCE PRICED SKUS.                          
      ******************************************************************        
       2200-PROCESS-RCP-SKUS.                                                   
                                                                                
           MOVE IM092-RCP-SKU-REC        TO PV-CORP-REC                         
           PERFORM 2000-READ-INPUT-FILE.                                        
           PERFORM 2400-PROCESS-ALL-LOCS                                        
             VARYING LOC-INDEX FROM 1 BY 1                                      
               UNTIL LOC-INDEX > LOCATION-TBL-SIZE.                             
      *                                                                         
      *    PERFORM 2000-READ-INPUT-FILE.                                        
      *                                                                         
      ******************************************************************        
      *    PROCESSES THE ITEM FOR ALL VALID LOCS                                
      ******************************************************************        
       2400-PROCESS-ALL-LOCS.                                                   
                                                                                
           INITIALIZE IM093-RCP-SKU-LIST-REC.                                   
           MOVE PV-CORP-SKU            TO IM093-SKU.                            
           MOVE PV-CORP-ITM-NBR        TO IM093-ITM-NBR.                        
           MOVE PV-CORP-INTR-UPC-ID    TO IM093-INTR-UPC-ID.                    
           MOVE 'A'                    TO IM093-SUM-LVL-CDE.                    
           MOVE PV-CORP-STAT-CDE       TO IM093-SKU-LVL-STAT-CDE                
           IF (IM092-STR-NBR = LOC-NBR(LOC-INDEX) AND                           
               IM092-INTR-UPC-ID = PV-CORP-INTR-UPC-ID)                         
                 MOVE IM092-UNT-RTL-CHG-DTE TO IM093-UNT-RTL-CHG-DTE            
                 MOVE IM092-STAT-CHG-DTE    TO IM093-STAT-CHG-DTE               
                 MOVE IM092-STAT-CDE     TO IM093-STR-LVL-STAT-CDE              
                 MOVE IM092-UNT-RTL-AMT  TO IM093-UNT-RTL-AMT                   
                 MOVE IM092-STR-NBR      TO IM093-STR-NBR                       
                 MOVE IM092-TICKET-COLOR TO IM093-TICKET-COLOR                  
                 MOVE IM092-ORIG-CLR-DTE TO IM093-ORIG-CLR-DTE                  
                 MOVE IM092-MKDN-CDE     TO IM093-MKDN-CDE                      
                 PERFORM 5000-WRITE-OUTPUT-REC                                  
                 PERFORM 2000-READ-INPUT-FILE                                   
           ELSE                                                                 
               IF (IM092-STR-NBR < LOC-NBR(LOC-INDEX) AND                       
                   IM092-INTR-UPC-ID = PV-CORP-INTR-UPC-ID AND                  
                   PS-NOT-ITEM-LOC-EOF)                                         
                     SET LOC-INDEX DOWN BY 1                                    
                     PERFORM 2000-READ-INPUT-FILE                               
               ELSE                                                             
                  MOVE PV-CORP-UNT-RTL-CHG-DTE TO IM093-UNT-RTL-CHG-DTE         
                   MOVE PV-CORP-STAT-CHG-DTE   TO IM093-STAT-CHG-DTE            
                   MOVE PV-CORP-UNT-RTL-AMT  TO IM093-UNT-RTL-AMT               
                   MOVE PV-CORP-ORIG-CLR-DTE TO IM093-ORIG-CLR-DTE              
                   MOVE PV-CORP-MKDN-CDE     TO IM093-MKDN-CDE                  
                   MOVE LOC-NBR(LOC-INDEX)   TO IM093-STR-NBR                   
                   IF PV-CORP-STAT-CDE = PC-25                                  
                       MOVE PC-20            TO IM093-STR-LVL-STAT-CDE          
                       MOVE PC-W             TO IM093-TICKET-COLOR              
                   ELSE                                                         
                       MOVE PC-30            TO IM093-STR-LVL-STAT-CDE          
                           MOVE PV-CORP-TICKET-COLOR                            
                             TO IM093-TICKET-COLOR                              
                   END-IF                                                       
                   PERFORM 5000-WRITE-OUTPUT-REC                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    WRITE SUMMARY RECORDS - ONE RECORD FOR ALL STORES                    
      ******************************************************************        
       5000-WRITE-OUTPUT-REC.                                                   
                                                                                
           WRITE RCP-SUMMARY-REC FROM IM093-RCP-SKU-LIST-REC.                   
           IF IM093-STR-LVL-STAT-CDE = '20'                                     
              ADD +1 TO PV-STAT-20-CNT-A                                        
           ELSE                                                                 
              ADD +1 TO PV-STAT-30-CNT-A                                        
           END-IF.                                                              
           ADD +1 TO PV-TOT-OUT-CNT                                             
                     PV-WRK-OUT-CNT.                                            
      *     IF PV-WRK-OUT-CNT > PV-TOT-OUT-MAX                                  
      *         DISPLAY 'TOTAL OUTPUT RECS = ' PV-TOT-OUT-CNT                   
      *         MOVE  IM093-INTR-UPC-ID TO PV-9-12                              
      *         DISPLAY 'UPC = ' PV-9-12                                        
      *         DISPLAY 'LOC = ' IM093-STR-NBR                                  
      *         MOVE ZERO TO PV-WRK-OUT-CNT.                                    
      *                                                                         
      ******************************************************************        
      *  FORMAT A DB2 ERROR MESSAGE & ABEND THE PROGRAM                *        
      ******************************************************************        
       9998-DB2-ABEND.                                                          
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '** ABEND **'.                                               
           DISPLAY '** PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                     
           DISPLAY '** OPERATION **  = ' PV-OPER-ATTEMPTED.                     
           DISPLAY '** IMKUT081 - SQL ERROR'.                                   
           MOVE SQLCODE TO SQLCODE2.                                            
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           DISPLAY '**       SQLCODE = ' SQLCODE2.                              
           DISPLAY '**      SQLWARN0 = ' SQLWARN0.                              
           DISPLAY '**      SQLWARN1 = ' SQLWARN1.                              
           DISPLAY '**      SQLWARN2 = ' SQLWARN2.                              
           DISPLAY '**      SQLWARN3 = ' SQLWARN3.                              
           DISPLAY '**      SQLWARN4 = ' SQLWARN4.                              
           DISPLAY '**      SQLWARN5 = ' SQLWARN5.                              
           DISPLAY '**      SQLWARN6 = ' SQLWARN6.                              
           DISPLAY '**      SQLWARN7 = ' SQLWARN7.                              
           DISPLAY '**********************************'.                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPER-ATTEMPTED.                    
           DISPLAY '**********************************'.                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
