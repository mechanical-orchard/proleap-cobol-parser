       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUT902.                                                 
       AUTHOR.        RYAN STAUFFACHER.                                         
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  08-28-08.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                         
      * INKUT902 -                                                              
      *                                                                         
      * THIS PROGRAM WILL COMPARE TWO DATA INTEGRITY EXTRACT FILES AND          
      * CREATE A REPORT FILE OF THE DIFFERENCES.                                
      *                                                                         
      * THE SAME CONTROL CARD WILL BE READ BY THE REPORT PROGRAM SO THAT        
      * IT CAN PUT THE APPROPRIATE TABLE NAMES ON THE REPORT.                   
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT IN-DATA-INTEGRITY-EXTR-FILE                                   
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT VA-DATA-INTEGRITY-EXTR-FILE                                   
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT DATA-INTEGRITY-DIFF-FILE                                      
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  IN-DATA-INTEGRITY-EXTR-FILE                                          
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  IN-DATA-INTEGRITY-EXTR-REC  PIC X(80).                               
                                                                                
       FD  VA-DATA-INTEGRITY-EXTR-FILE                                          
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VA-DATA-INTEGRITY-EXTR-REC  PIC X(80).                               
                                                                                
       FD  DATA-INTEGRITY-DIFF-FILE                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  DATA-INTEGRITY-DIFF-REC     PIC X(80).                               
                                                                                
      *                                                                         
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  PC-CONSTANTS.                                                        
           05  PC-DPKUT500                  PIC X(08) VALUE 'DPKUT500'.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MORE-IN-EXTR-RECS-SW PIC X       VALUE 'Y'.                   
               88  PS-MORE-IN-EXTR-RECS            VALUE 'Y'.                   
               88  PS-NO-MORE-IN-EXTR-RECS         VALUE 'N'.                   
           05  PS-MORE-VA-EXTR-RECS-SW PIC X       VALUE 'Y'.                   
               88  PS-MORE-VA-EXTR-RECS            VALUE 'Y'.                   
               88  PS-NO-MORE-VA-EXTR-RECS         VALUE 'N'.                   
           05  PS-BYPASS-0-DOLLARS-SW  PIC X       VALUE 'Y'.                   
               88  PS-BYPASS-0-DOLLARS             VALUE 'Y'.                   
               88  PS-NO-BYPASS-DOLLARS            VALUE 'N'.                   
                                                                                
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
           05  PA-IN-RECS-READ         PIC S9(13)  VALUE ZERO.                  
           05  PA-VA-RECS-READ         PIC S9(13)  VALUE ZERO.                  
           05  PA-KEY-MATCHES          PIC S9(13)  VALUE ZERO.                  
           05  PA-REPORT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.                  
                                                                                
       01  CD-COUNTER-DISPLAY.                                                  
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.                   
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-IN-FILE-KEY.                                                  
               10  PV-IN-ADJ-DEPT-CLASS.                                        
                   15  PV-IN-ADJ-DEPT-NBR      PIC  X(3).                       
                   15  PV-IN-ADJ-CLASS-NBR     PIC  X(2).                       
               10  PV-IN-ADJ-KOHLS-MM  PIC X(02).                               
               10  PV-IN-ADJ-DTE       PIC X(06).                               
               10  PV-IN-STORE-NBR     PIC X(05).                               
           05  PV-VA-FILE-KEY.                                                  
               10  PV-VA-ADJ-DEPT-CLASS.                                        
                   15  PV-VA-ADJ-DEPT-NBR      PIC  X(3).                       
                   15  PV-VA-ADJ-CLASS-NBR     PIC  X(2).                       
               10  PV-VA-ADJ-KOHLS-MM  PIC X(02).                               
               10  PV-VA-ADJ-DTE       PIC X(06).                               
               10  PV-VA-STORE-NBR     PIC X(05).                               
           05  PV-WORK-ADJ-DTE.                                                 
               10  PV-WORK-ADJ-MM      PIC  X(2) VALUE '00'.                    
               10  PV-WORK-ADJ-DD      PIC  X(2) VALUE '00'.                    
               10  PV-WORK-ADJ-YY      PIC  X(2) VALUE '00'.                    
           05  PV-WORK-CLASS.                                                   
               10  PV-WORK-CLASS1      PIC  X(1) VALUE '0'.                     
               10  FILLER              PIC  X(1) VALUE '0'.                     
           05  PV-WRITE-MAJOR-CLASS.                                            
               10  PV-WRITE-MAJ-CLASS1 PIC  X(1) VALUE '0'.                     
               10  FILLER              PIC  X(1) VALUE '0'.                     
                                                                                
       01 ABEND-AREAS.                                                          
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO                   
                                                   COMP SYNC.                   
               88  AC-BAD-DATA                     VALUE +4003.                 
               88  AC-DB2-ERROR                    VALUE +4013.                 
           05  AA-ABEND-LIT            PIC X(40)   VALUE                        
                 '*****       ABEND'.                                           
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                        
                   '*****   PROGRAM: INKUT902'.                                 
           05  AA-PARAGRAPH-LIT.                                                
               10  FILLER              PIC X(17)   VALUE                        
                   '***** PARAGRAPH: '.                                         
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.                
           05  AA-MESSAGE-LINE.                                                 
               10  FILLER              PIC X(06)   VALUE '*****'.               
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.                
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                        
                   '*****    DB2 ERROR'.                                        
           05  AA-DB2-OPERATION-LIT.                                            
               10  FILLER              PIC X(17)   VALUE                        
                   '***** OPERATION: '.                                         
               10  AA-DB2-OPERATION.                                            
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.                
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.                
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.                
                                                                                
       EJECT                                                                    
                                                                                
      *** IN DATA INTEGRITY EXTRACT FILE                                        
           COPY MLRD018 REPLACING ML018 BY IN018.                               
                                                                                
      *** VA DATA INTEGRITY EXTRACT FILE                                        
           COPY MLRD018 REPLACING ML018 BY VA018.                               
                                                                                
      *** DATA INTEGRITY DIFFERENCE REPORT FILE                                 
           COPY INRD900.                                                        
      *                                                                         
      ******************************************************************        
      *    CALENDAR ROUTINE                                                     
      ******************************************************************        
           COPY DPWS500.                                                        
       EJECT                                                                    
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAINLINE-INKUT902.                                                  
      ******************************************************************        
      *                                                                         
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-PROCESS.                                           
                                                                                
           PERFORM 8000-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       1000-INITIALIZATION.                                                     
      ******************************************************************        
      *                                                                         
           OPEN INPUT  IN-DATA-INTEGRITY-EXTR-FILE                              
                       VA-DATA-INTEGRITY-EXTR-FILE.                             
           OPEN OUTPUT DATA-INTEGRITY-DIFF-FILE.                                
                                                                                
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
           INITIALIZE PV-PROGRAM-VARIABLES.                                     
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
      *                                                                         
           PERFORM 4100-READ-IN-EXTRACT-FILE.                                   
                                                                                
           IF PS-NO-MORE-IN-EXTR-RECS                                           
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY IN EXTRACT FILE'                                     
                                       TO AA-MESSAGE                            
               MOVE +4002              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM 4200-READ-VA-EXTRACT-FILE.                                   
                                                                                
           IF PS-NO-MORE-VA-EXTR-RECS                                           
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY VA EXTRACT FILE'                                     
                                       TO AA-MESSAGE                            
               MOVE +4003              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM                                                              
             UNTIL PS-NO-MORE-IN-EXTR-RECS                                      
               AND PS-NO-MORE-VA-EXTR-RECS                                      
               PERFORM 2100-COMPARE-FILES                                       
           END-PERFORM.                                                         
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2100-COMPARE-FILES.                                                      
      ******************************************************************        
      *                                                                         
           PERFORM 3000-BUILD-FILE-KEYS.                                        
                                                                                
           IF PV-IN-FILE-KEY = PV-VA-FILE-KEY                                   
               ADD 1                   TO PA-KEY-MATCHES                        
               PERFORM 5100-COMPARE-BOTH-FILE-AMTS                              
               PERFORM 4100-READ-IN-EXTRACT-FILE                                
               PERFORM 4200-READ-VA-EXTRACT-FILE                                
           ELSE                                                                 
               IF PV-IN-FILE-KEY < PV-VA-FILE-KEY                               
                   PERFORM 5200-PROCESS-ONLY-IN-FILE                            
                   PERFORM 4100-READ-IN-EXTRACT-FILE                            
               ELSE                                                             
                   PERFORM 5300-PROCESS-ONLY-VA-FILE                            
                   PERFORM 4200-READ-VA-EXTRACT-FILE                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       3000-BUILD-FILE-KEYS.                                                    
      ******************************************************************        
      *                                                                         
           INITIALIZE PV-IN-FILE-KEY.                                           
           INITIALIZE PV-VA-FILE-KEY.                                           
                                                                                
           IF IN018-ADJUSTMENT-RECORD = HIGH-VALUES                             
               MOVE HIGH-VALUES        TO PV-IN-FILE-KEY                        
           ELSE                                                                 
               MOVE IN018-ADJ-DEPT-CLASS TO PV-IN-ADJ-DEPT-CLASS                
               MOVE IN018-ADJ-KOHLS-MM   TO PV-IN-ADJ-KOHLS-MM                  
               MOVE IN018-ADJ-DTE        TO PV-IN-ADJ-DTE                       
               MOVE IN018-ADJ-STORE-NBR  TO PV-IN-STORE-NBR                     
           END-IF.                                                              
           IF VA018-ADJUSTMENT-RECORD = HIGH-VALUES                             
               MOVE HIGH-VALUES        TO PV-VA-FILE-KEY                        
           ELSE                                                                 
               MOVE VA018-ADJ-DEPT-CLASS TO PV-VA-ADJ-DEPT-CLASS                
               MOVE VA018-ADJ-KOHLS-MM   TO PV-VA-ADJ-KOHLS-MM                  
               MOVE VA018-ADJ-DTE        TO PV-VA-ADJ-DTE                       
               MOVE VA018-ADJ-STORE-NBR  TO PV-VA-STORE-NBR                     
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       4100-READ-IN-EXTRACT-FILE.                                               
      ******************************************************************        
      *                                                                         
           IF PS-MORE-IN-EXTR-RECS                                              
               READ IN-DATA-INTEGRITY-EXTR-FILE                                 
                 INTO IN018-ADJUSTMENT-RECORD                                   
                   AT END                                                       
                       SET PS-NO-MORE-IN-EXTR-RECS                              
                                       TO TRUE                                  
                       MOVE HIGH-VALUES                                         
                                       TO IN018-ADJUSTMENT-RECORD               
               END-READ                                                         
           END-IF.                                                              
                                                                                
           IF PS-MORE-IN-EXTR-RECS                                              
               ADD 1                   TO PA-IN-RECS-READ                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       4200-READ-VA-EXTRACT-FILE.                                               
      ******************************************************************        
      *                                                                         
           IF PS-MORE-VA-EXTR-RECS                                              
               READ VA-DATA-INTEGRITY-EXTR-FILE                                 
                 INTO VA018-ADJUSTMENT-RECORD                                   
                   AT END                                                       
                       SET PS-NO-MORE-VA-EXTR-RECS                              
                                       TO TRUE                                  
                       MOVE HIGH-VALUES                                         
                                       TO VA018-ADJUSTMENT-RECORD               
               END-READ                                                         
           END-IF.                                                              
                                                                                
           IF PS-MORE-VA-EXTR-RECS                                              
               ADD 1                   TO PA-VA-RECS-READ                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5100-COMPARE-BOTH-FILE-AMTS.                                             
      ******************************************************************        
      *                                                                         
           IF VA018-ADJ-DTE NOT EQUAL PV-WORK-ADJ-DTE                           
              MOVE VA018-ADJ-DTE TO PV-WORK-ADJ-DTE                             
              PERFORM 5400-GET-FISCAL-MN-END-DATE                               
           END-IF.                                                              
           MOVE DPG56-LAST-FP-DB2-ISO-FMT TO IN900-FSCL-MN-END-DTE.             
           MOVE VA018-ADJ-KOHLS-MM     TO IN900-FSCL-MN-NBR.                    
           MOVE VA018-ADJ-DEPT-NBR     TO IN900-DEPT-NBR.                       
           MOVE VA018-ADJ-CLASS-NBR    TO PV-WORK-CLASS.                        
           MOVE PV-WORK-CLASS1         TO PV-WRITE-MAJ-CLASS1                   
           MOVE PV-WRITE-MAJOR-CLASS   TO IN900-MAJ-CL-NBR.                     
           MOVE VA018-ADJ-CLASS-NBR    TO IN900-SUB-CL-NBR.                     
           MOVE VA018-ADJ-STORE-NBR    TO IN900-STR-NBR.                        
           MOVE SPACES                 TO IN900-DKEY-NBR.                       
           IF IN018-ADJ-AT-RTL-AMT NOT = VA018-ADJ-AT-RTL-AMT                   
               MOVE IN018-ADJ-AT-RTL-AMT TO IN900-IN-AMT                        
               MOVE VA018-ADJ-AT-RTL-AMT TO IN900-VA-AMT                        
               MOVE 3                  TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
                                                                                
           IF IN018-ADJ-AT-COST-AMT NOT = VA018-ADJ-AT-COST-AMT                 
               MOVE IN018-ADJ-AT-COST-AMT TO IN900-IN-AMT                       
               MOVE VA018-ADJ-AT-COST-AMT TO IN900-VA-AMT                       
               MOVE 4                  TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5200-PROCESS-ONLY-IN-FILE.                                               
      ******************************************************************        
      *                                                                         
           PERFORM 5210-BYPASS-IN-ADJ-0-DLRS-CHK.                               
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 5220-PROCESS-NO-VA-RECD-ERR                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5210-BYPASS-IN-ADJ-0-DLRS-CHK.                                           
      ******************************************************************        
      *                                                                         
           IF (IN018-ADJ-AT-RTL-AMT = 0)                                        
             AND (IN018-ADJ-AT-COST-AMT = 0)                                    
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5220-PROCESS-NO-VA-RECD-ERR.                                             
      ******************************************************************        
      *                                                                         
           IF IN018-ADJ-DTE NOT EQUAL PV-WORK-ADJ-DTE                           
              MOVE IN018-ADJ-DTE TO PV-WORK-ADJ-DTE                             
              PERFORM 5400-GET-FISCAL-MN-END-DATE                               
           END-IF.                                                              
           MOVE DPG56-LAST-FP-DB2-ISO-FMT TO IN900-FSCL-MN-END-DTE.             
           MOVE IN018-ADJ-KOHLS-MM     TO IN900-FSCL-MN-NBR.                    
           MOVE IN018-ADJ-DEPT-NBR     TO IN900-DEPT-NBR.                       
           MOVE IN018-ADJ-CLASS-NBR    TO IN900-SUB-CL-NBR                      
           MOVE IN018-ADJ-CLASS-NBR    TO PV-WORK-CLASS.                        
           MOVE PV-WORK-CLASS1         TO PV-WRITE-MAJ-CLASS1                   
           MOVE PV-WRITE-MAJOR-CLASS   TO IN900-MAJ-CL-NBR.                     
           MOVE IN018-ADJ-STORE-NBR    TO IN900-STR-NBR                         
           MOVE SPACES                 TO IN900-DKEY-NBR                        
                                                                                
           MOVE 0                      TO IN900-IN-AMT                          
                                          IN900-VA-AMT.                         
           MOVE 1                      TO IN900-ERR-MSG-NBR.                    
                                                                                
           WRITE DATA-INTEGRITY-DIFF-REC                                        
               FROM IN900-DATA-INTEGRITY-DIFF-REC.                              
                                                                                
           ADD 1                       TO PA-REPORT-RECS-WRITTEN.               
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5300-PROCESS-ONLY-VA-FILE.                                               
      ******************************************************************        
      *                                                                         
           PERFORM 5310-BYPASS-VA-ADJ-0-DLRS-CHK.                               
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 5320-PROCESS-NO-IN-RECD-ERR                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5310-BYPASS-VA-ADJ-0-DLRS-CHK.                                           
      ******************************************************************        
      *                                                                         
           IF (VA018-ADJ-AT-RTL-AMT = 0)                                        
             AND (VA018-ADJ-AT-COST-AMT = 0)                                    
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5320-PROCESS-NO-IN-RECD-ERR.                                             
      ******************************************************************        
      *                                                                         
           IF VA018-ADJ-DTE NOT EQUAL PV-WORK-ADJ-DTE                           
              MOVE VA018-ADJ-DTE TO PV-WORK-ADJ-DTE                             
              PERFORM 5400-GET-FISCAL-MN-END-DATE                               
           END-IF.                                                              
           MOVE DPG56-LAST-FP-DB2-ISO-FMT TO IN900-FSCL-MN-END-DTE.             
           MOVE VA018-ADJ-KOHLS-MM     TO IN900-FSCL-MN-NBR.                    
           MOVE VA018-ADJ-DEPT-NBR     TO IN900-DEPT-NBR.                       
           MOVE VA018-ADJ-CLASS-NBR    TO PV-WORK-CLASS.                        
           MOVE PV-WORK-CLASS1         TO PV-WRITE-MAJ-CLASS1                   
           MOVE PV-WRITE-MAJOR-CLASS   TO IN900-MAJ-CL-NBR.                     
           MOVE VA018-ADJ-CLASS-NBR    TO IN900-SUB-CL-NBR.                     
           MOVE VA018-ADJ-STORE-NBR    TO IN900-STR-NBR.                        
           MOVE 0                      TO IN900-IN-AMT                          
                                          IN900-VA-AMT.                         
           MOVE 2                      TO IN900-ERR-MSG-NBR.                    
                                                                                
           WRITE DATA-INTEGRITY-DIFF-REC                                        
               FROM IN900-DATA-INTEGRITY-DIFF-REC.                              
                                                                                
           ADD 1                       TO PA-REPORT-RECS-WRITTEN.               
                                                                                
      ******************************************************************        
       5400-GET-FISCAL-MN-END-DATE.                                             
      ******************************************************************        
      *                                                                         
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
                                                                                
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
           SET DPG52-LK-DTE-GREG         TO TRUE.                               
           MOVE PV-WORK-ADJ-MM TO DPG52-GREG-DATE-MO.                           
           MOVE PV-WORK-ADJ-DD TO DPG52-GREG-DATE-DY.                           
           MOVE PV-WORK-ADJ-YY TO DPG52-GREG-DATE-YR.                           
                                                                                
           CALL PC-DPKUT500 USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.          
                                                                                
           IF DPG54-NO-ERROR                                                    
               MOVE DPG56-LAST-FP-DB2-ISO-FMT TO IN900-FSCL-MN-END-DTE          
           END-IF.                                                              
      ******************************************************************        
       8000-EOJ.                                                                
      ******************************************************************        
      *                                                                         
           DISPLAY ' '.                                                         
           DISPLAY '  PROGRAM INKUT902 PROCESSING SUMMARY'.                     
           DISPLAY '  -----------------------------------'.                     
           DISPLAY ' '.                                                         
      *                                                                         
           MOVE PA-IN-RECS-READ        TO CD-COUNT-HOLD1.                       
           DISPLAY 'IN RECORDS READ =        ' CD-COUNT-HOLD1.                  
      *                                                                         
           MOVE PA-VA-RECS-READ        TO CD-COUNT-HOLD1.                       
           DISPLAY 'VA RECORDS READ =        ' CD-COUNT-HOLD1.                  
      *                                                                         
           MOVE PA-KEY-MATCHES         TO CD-COUNT-HOLD1.                       
           DISPLAY 'KEY MATCHES =            ' CD-COUNT-HOLD1.                  
      *                                                                         
           MOVE PA-REPORT-RECS-WRITTEN TO CD-COUNT-HOLD1.                       
           DISPLAY 'REPORT RECORDS WRITTEN = ' CD-COUNT-HOLD1.                  
      *                                                                         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE IN-DATA-INTEGRITY-EXTR-FILE.                                   
           CLOSE VA-DATA-INTEGRITY-EXTR-FILE.                                   
           CLOSE DATA-INTEGRITY-DIFF-FILE.                                      
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       9900-COMMON-ABEND-ROUTINE.                                               
      ******************************************************************        
      *                                                                         
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY ABEND-CODE.                                                  
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
