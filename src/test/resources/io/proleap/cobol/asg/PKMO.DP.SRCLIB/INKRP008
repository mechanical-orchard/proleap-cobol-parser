       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKRP008                                                  
       AUTHOR.        DORSEY JACKSON                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  06-20-2002.                                               
       DATE-COMPILED.                                                           
      ******************************************************************        
      * DESCRIPTION:  THIS PROGRAM LISTS THE 10 HIGHEST SHRINKAGE DEPTS,00000600
      * BASED ON SHRINK PERCENT, FOR THE COMPANY.                       00000700
      ******************************************************************        
      ******************************************************************        
      ******************************************************************        
      ******************************************************************        
      *                        CHANGE LOG                              *        
      *  CHANGE ID    CHANGE DATE            REASON FOR CHANGE         *        
W26600*  ---------    -----------            -----------------         *        
W26600*  W26600       04-  -2004             STORE EXPANSION           *        
W26600*                                      M JOHNSON                 *        
W26600*                                                                *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT INP-STORE-SHORTAGE-FILE                                       
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT INP-CONTROL-FILE                                              
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT OUT-REPORT-FILE                                               
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INP-STORE-SHORTAGE-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS   0 RECORDS                                          
W26600*    RECORD CONTAINS 97 CHARACTERS                                        
           DATA RECORD IS INP-STORE-SHORTAGE-RECORD.                            
                                                                                
W26600 01  INP-STORE-SHORTAGE-RECORD    PIC X(120).                             
                                                                                
       FD  INP-CONTROL-FILE                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS INP-CONTROL-RECORD.                                   
                                                                                
       01  INP-CONTROL-RECORD             PIC X(80).                            
                                                                                
       FD  OUT-REPORT-FILE                                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS OUT-REPORT-RECORD.                                    
                                                                                
       01  OUT-REPORT-RECORD              PIC X(132).                           
                                                                                
       EJECT                                                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      *****************************************************************         
      ***************************************************************** 00120000
      *  STORE SHORTAGE RECORD LAYOUT - INPUT.                          00130000
      ***************************************************************** 00140000
           COPY INRD002I.                                               00000400
                                                                                
       01  CR-CONTROL-RECORD.                                                   
           05  CR-TYPE-RUN-IND               PIC X(01)   VALUE SPACE.           
           05  FILLER                        PIC X(79)   VALUE SPACES.          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-LINE-MAX                   PIC S9(4)   VALUE +60              
                                                         COMP SYNC.             
           05  PC-PROGRAM-NAME               PIC X(08) VALUE 'INKRP008'.        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-INPUT-EOF-SW               PIC X(1)     VALUE 'N'.            
               88  PS-INPUT-EOF                           VALUE 'Y'.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PAGE-COUNTER            PIC S9(4)   VALUE ZEROES              
                                                         COMP SYNC.             
           05  PV-LINE-COUNTER            PIC S9(4)   VALUE 99                  
                                                         COMP SYNC.             
           05  PV-IN-COUNTER              PIC S9(4)   VALUE ZEROES              
                                                         COMP SYNC.             
W26600*    05  PV-SAVE-STORE              PIC 9(03)    VALUE ZEROES.            
                                                                                
      ***************************************************************** 00120000
      *   STANDARD REPORT HEADER FOR A 132 COLUMN REPORT.               00220000
      ***************************************************************** 00230000
           COPY DPWS132O.                                                       
                                                                                
       01  HL-HEADING-LINE1.                                                    
           05  FILLER                        PIC X(45)   VALUE SPACES.          
           05  FILLER                        PIC X(06)   VALUE                  
                                             'TOP 10'.                          
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  FILLER                        PIC X(08)   VALUE                  
                                             'SHORTAGE'.                        
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  FILLER                        PIC X(11)   VALUE                  
                                             'DEPARTMENTS'.                     
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  FILLER                        PIC X(14)   VALUE                  
                                             'BY PERCENTAGE'.                   
       01  HL-HEADING-LINE2.                                                    
           05  FILLER                        PIC X(54)   VALUE SPACES.          
           05  HL-HEADING-LINE2-RPT-DESC     PIC X(24)   VALUE SPACES.          
                                                                                
       01  HL-HEADING-LINE3.                                                    
           05  FILLER                        PIC X(05)   VALUE SPACES.          
           05  FILLER                        PIC X(04)   VALUE                  
                                             'DEPT'.                            
           05  FILLER                        PIC X(13)   VALUE SPACES.          
           05  FILLER                        PIC X(04)   VALUE                  
                                             'NAME'.                            
           05  FILLER                        PIC X(19)   VALUE SPACES.          
           05  FILLER                        PIC X(04)   VALUE 'BOOK'.          
           05  FILLER                        PIC X(15)   VALUE SPACES.          
           05  FILLER                        PIC X(08)   VALUE                  
                                             'PHYSICAL'.                        
           05  FILLER                        PIC X(10)   VALUE SPACES.          
           05  FILLER                        PIC X(09)   VALUE                  
                                             'ALLOWANCE'.                       
           05  FILLER                        PIC X(09)   VALUE SPACES.          
           05  FILLER                        PIC X(06)   VALUE                  
                                             'RETAIL'.                          
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  FILLER                        PIC X(05)   VALUE                  
                                             'SHORT'.                           
           05  FILLER                        PIC X(06)   VALUE SPACES.          
           05  FILLER                        PIC X(08)   VALUE                  
                                             'SHRINK %'.                        
                                                                                
       01  SL-SPACE-LINE                     PIC X(132)  VALUE SPACES.          
                                                                                
       01  DL-DETAIL-LINE.                                                      
           05  FILLER                        PIC X(05)   VALUE SPACE.           
           05  DL-DEPT-NUMBER                PIC Z(03)   VALUE ZEROES.          
           05  FILLER                        PIC X(06)   VALUE SPACES.          
           05  DL-DEPT-NAME                  PIC X(20)   VALUE SPACES.          
           05  FILLER                        PIC X(08)   VALUE SPACES.          
           05  DL-BOOK                       PIC ZZZ,ZZZ,ZZZ.99-                
                                                         VALUE ZEROS.           
           05  FILLER                        PIC X(04)   VALUE SPACES.          
           05  DL-PHY-INV-RETAIL             PIC ZZZ,ZZZ,ZZZ.99-                
                                                         VALUE ZEROS.           
           05  FILLER                        PIC X(04)   VALUE SPACES.          
           05  DL-ALLOWANCE                  PIC ZZZ,ZZZ,ZZZ.99-                
                                                         VALUE ZEROS.           
           05  FILLER                        PIC X(04)   VALUE SPACES.          
           05  DL-SHORT-RETAIL               PIC ZZZ,ZZZ,ZZZ.99-                
                                                         VALUE ZEROS.           
           05  FILLER                        PIC X(05)   VALUE SPACES.          
           05  DL-PERCENT                    PIC ZZZ.99- VALUE ZEROES.          
                                                                                
       01  FC1-FINANCE-CORP01-HEADING.                                          
           05  FILLER                        PIC X(54) VALUE SPACES.            
           05  FILLER                        PIC X(24) VALUE                    
               'FINANCIAL CORPORATION 01'.                                      
           05  FILLER                        PIC X(54) VALUE SPACES.            
       01  FC2-FINANCE-CORP02-HEADING.                                          
           05  FILLER                        PIC X(54) VALUE SPACES.            
           05  FC2-FINANCE-CORP2-TITLE       PIC X(24) VALUE                    
               'FINANCIAL CORPORATION 02'.                                      
           05  FILLER                        PIC X(54) VALUE SPACES.            
       01  CH-COMPANY-HEADING.                                                  
           05  FILLER                        PIC X(54) VALUE SPACES.            
           05  CH-COMPANY-TITLE              PIC X(24) VALUE                    
               '     FOR THE COMPANY    '.                                      
           05  FILLER                        PIC X(54) VALUE SPACES.            
                                                                                
       01  TT-TOTAL-TITLES.                                                     
           05  FC1-FINANCE-CORP01-TITLE      PIC X(22) VALUE                    
               'FINANCE CORP 01 TOTALS'.                                        
           05  FC2-FINANCE-CORP02-TITLE      PIC X(22) VALUE                    
               'FINANCE CORP 02 TOTALS'.                                        
           05  CT-COMPANY-TITLE              PIC X(22) VALUE                    
               '    COMPANY TOTALS    '.                                        
                                                                                
       01  RUN-TIME.                                                            
           05  R-HR                          PIC 99.                            
           05  R-MIN                         PIC 99.                            
           05  R-SEC                         PIC 99.                            
           05  R-TH                          PIC 99.                            
                                                                                
       01  DATE-IN.                                                             
           05 DATE-IN-YY                     PIC XX.                            
           05 DATE-IN-MM                     PIC XX.                            
           05 DATE-IN-DD                     PIC XX.                            
                                                                                
       EJECT                                                                    
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-INKRP008.                                                           
                                                                                
      ****************************************************************          
      *    OPEN ALL FILES.                                                      
      *    INITIALIZE PAGE HEADINGS                                             
      *    PROCESS AND PRINT RECORDS ON IN003I-STORE-SHORTAGE-RECORD            
      *    AT END OF FILE CLOSE ALL FILES.                                      
      ****************************************************************          
           OPEN INPUT  INP-STORE-SHORTAGE-FILE                                  
                       INP-CONTROL-FILE                                         
                OUTPUT OUT-REPORT-FILE.                                         
                                                                                
           PERFORM 1000-INITIALIZE-HEADER.                                      
                                                                                
           PERFORM 2000-PROCESS                                                 
               UNTIL PS-INPUT-EOF OR                                            
               PV-IN-COUNTER = +10.                                             
                                                                                
           CLOSE INP-STORE-SHORTAGE-FILE                                        
                 INP-CONTROL-FILE                                               
                 OUT-REPORT-FILE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-INITIALIZE-HEADER.                                                  
                                                                                
      ****************************************************************          
      *    SET UP THE STANDARD PAGE HEADER.                                     
      *    ACCEPT THE RUN DATE AND RUN TIME FROM THE SYSTEM.                    
      *    READ FIRST STORE RECORD.                                             
      *    PERFORMED BY 0000-INKRP008.                                          
      ****************************************************************          
           ACCEPT DATE-IN  FROM DATE.                                           
           ACCEPT RUN-TIME FROM TIME.                                           
           MOVE DATE-IN-MM TO DP132O-RUN-MONTH.                                 
           MOVE DATE-IN-DD TO DP132O-RUN-DAY.                                   
           MOVE DATE-IN-YY TO DP132O-RUN-YEAR.                                  
           MOVE R-HR       TO DP132O-RUN-HOUR.                                  
           MOVE R-MIN      TO DP132O-RUN-MINUTE.                                
           MOVE PC-PROGRAM-NAME TO DP132O-PROGRAM-NAME.                         
           MOVE 1               TO DP132O-REPORT-NUMBER.                        
           PERFORM 3000-READ-CNTL-DATA.                                         
           IF CR-TYPE-RUN-IND = '1'                                             
               MOVE FC1-FINANCE-CORP01-HEADING                                  
                   TO HL-HEADING-LINE2-RPT-DESC                                 
           END-IF                                                               
           IF CR-TYPE-RUN-IND = '2'                                             
               MOVE FC2-FINANCE-CORP02-HEADING                                  
                   TO HL-HEADING-LINE2-RPT-DESC                                 
           END-IF                                                               
           IF CR-TYPE-RUN-IND = 'C'                                             
               MOVE CH-COMPANY-TITLE                                            
                   TO HL-HEADING-LINE2-RPT-DESC                                 
           END-IF                                                               
           PERFORM 4000-READ-STORE-DATA.                                        
                                                                                
       EJECT                                                                    
                                                                                
       2000-PROCESS.                                                            
                                                                                
      ****************************************************************          
      *    PRINT DETAIL FOR THE CURRENT STORE SHRINK RECORD AND                 
      *    READ THE NEXT INPUT RECORD.                                          
      *                                                                         
      *    PERFORMED BY 0000-INKRP008.                                          
      ****************************************************************          
                                                                                
           MOVE IN002I-DEPARTMENT-NUMB TO DL-DEPT-NUMBER.                       
           MOVE IN002I-DEPARTMENT-NAME TO DL-DEPT-NAME.                         
           MOVE IN002I-BOOK-INV         TO DL-BOOK.                             
           MOVE IN002I-PHY-INV-RETAIL   TO DL-PHY-INV-RETAIL.                   
           MOVE IN002I-SHRINK-ALLOWANCE TO DL-ALLOWANCE.                        
           MOVE IN002I-SHORT-RETAIL     TO DL-SHORT-RETAIL.                     
           MOVE IN002I-SHRINK-PER       TO DL-PERCENT.                          
           ADD +1                       TO PV-IN-COUNTER.                       
           PERFORM Z100-PRINT-DETAIL-LINE.                                      
           PERFORM 4000-READ-STORE-DATA.                                        
                                                                                
       3000-READ-CNTL-DATA.                                                     
      ****************************************************************          
      *    READ THE CNTL FILE                                                   
      *                                                                         
      *    PERFORMED BY 1000-INITIALIZE-HEADER.                                 
      ****************************************************************          
           READ INP-CONTROL-FILE INTO CR-CONTROL-RECORD                         
               AT END                                                           
                   SET PS-INPUT-EOF TO TRUE                                     
                   PERFORM Z300-DISPLAY-CNTL-MESSAGE.                           
                                                                                
       4000-READ-STORE-DATA.                                                    
      ****************************************************************          
      *    READ THE STORE SHRINK FILE                                           
      *                                                                         
      *    PERFORMED BY 1000-INITIALIZE-HEADER.                                 
      *    PERFORMED BY 2000-PROCESS.                                           
      ****************************************************************          
           READ INP-STORE-SHORTAGE-FILE INTO IN002I-STORE-SHORTAGE-REC          
               AT END                                                           
                   SET PS-INPUT-EOF TO TRUE.                                    
                                                                                
       6000-PRINT-HEADERS.                                                      
                                                                                
      ****************************************************************          
      *    PRINT THE PAGE HEADERS.                                              
      *                                                                         
      *    PERFORMED BY Z100-DETAIL-PRINT-LINE                                  
      *    PERFORMED BY Z200-PRINT-COMPANY-TOTALS                               
      ****************************************************************          
                                                                                
           ADD  1               TO PV-PAGE-COUNTER.                             
                                                                                
           MOVE PV-PAGE-COUNTER TO DP132O-PAGE-NUMBER.                          
                                                                                
           WRITE OUT-REPORT-RECORD                                              
               FROM DP132O-STANDRD-HEADER-132-COLS                              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE OUT-REPORT-RECORD                                              
               FROM HL-HEADING-LINE1                                            
               AFTER ADVANCING 2 LINE.                                          
                                                                                
           WRITE OUT-REPORT-RECORD                                              
               FROM HL-HEADING-LINE2                                            
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE OUT-REPORT-RECORD                                              
               FROM HL-HEADING-LINE3                                            
               AFTER ADVANCING 2 LINE.                                          
                                                                                
           WRITE OUT-REPORT-RECORD                                              
               FROM SL-SPACE-LINE                                               
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE 06 TO PV-LINE-COUNTER.                                          
                                                                                
           EJECT                                                                
                                                                                
       Z100-PRINT-DETAIL-LINE.                                                  
      ****************************************************************          
      *    PRINT DETAIL LINE.                                                   
      *                                                                         
      *    PERFORMED BY 2000-PROCESS.                                           
      ****************************************************************          
                                                                                
           IF PV-LINE-COUNTER >= PC-LINE-MAX                                    
              PERFORM 6000-PRINT-HEADERS.                                       
           WRITE OUT-REPORT-RECORD                                              
               FROM DL-DETAIL-LINE                                              
               AFTER ADVANCING 1 LINE.                                          
           ADD 1 TO PV-LINE-COUNTER.                                            
                                                                                
           EJECT                                                                
                                                                                
       Z300-DISPLAY-CNTL-MESSAGE.                                               
      ****************************************************************          
      *    DISPLAY ERROR MESSAGE                                                
      *    CONTROL RECORD IS MISSING                                            
      *                                                                         
      *    PERFORMED BY 3000-READ-CNTL-DATA                                     
      ****************************************************************          
                                                                                
           DISPLAY 'CONTROL RECORD IS MISSING'.                                 
