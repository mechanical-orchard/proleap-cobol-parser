      *************************                                         00010003
       IDENTIFICATION DIVISION.                                         00020003
      *************************                                         00030003
       PROGRAM-ID.             AUKRP008.                                00040003
       AUTHOR.                 GOPINATH - COGNIZANT                     00050003
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00060003
       DATE-WRITTEN            MAY 2014.                                00070003
       DATE-COMPILED.                                                   00080003
                                                                        00090003
      ***************************************************************** 00100003
      *                                                                 00110003
      *  THIS PROGRAM WILL CREATE THE DAILY HARD DECLINED CARDS REPORT. 00120003
      *  THIS PROGRAM USES THE SORTED INPUT CARD FILE.                  00130003
      *                                                                 00140003
      *   INPUT : INP01 -  HARD DECLINED GIFT CARD FILE                 00150003
      *                                                                 00160003
      *   OUTPUT: RPT01 -  DAILY HARD DECLINED GIFT CARD REPORT         00170003
      *                                                                 00180003
      *CHANGE HISTORY:                                                  00190003
      * WR/PBI   DATE        DESCRIPTION OF CHANGES                     00200003
      * -------  ----------  ----------------------------------------   00210003
      *  *****   05-22-2014  INITIAL CREATION - COGNIZANT               00220003
      ******************************************************************00230003
                                                                        00240003
       ENVIRONMENT DIVISION.                                            00250003
       CONFIGURATION SECTION.                                           00260003
                                                                        00270003
       SOURCE-COMPUTER. IBM-3090.                                       00280003
       OBJECT-COMPUTER. IBM-3090.                                       00290003
                                                                        00300003
       INPUT-OUTPUT SECTION.                                            00310003
       FILE-CONTROL.                                                    00320003
                                                                        00330003
           SELECT HRD-DCL-INPUT                                         00340003
               ASSIGN TO INP01.                                         00350003
                                                                        00360003
           SELECT DLY-HRDDCL-REPORT                                     00370003
               ASSIGN TO RPT01.                                         00380003
                                                                        00390003
       DATA DIVISION.                                                   00400003
                                                                        00410003
       FILE SECTION.                                                    00420003
                                                                        00430003
       FD  HRD-DCL-INPUT                                                00440003
           RECORDING MODE IS F                                          00450003
           BLOCK CONTAINS 0 RECORDS                                     00460003
           LABEL RECORDS ARE STANDARD.                                  00470003
                                                                        00480003
       01  HRD-DCL-INPUT-REC                     PIC X(100).            00490003
                                                                        00500003
       FD  DLY-HRDDCL-REPORT                                            00510003
           RECORDING MODE IS F                                          00520003
           BLOCK CONTAINS 0 RECORDS                                     00530003
           LABEL RECORDS ARE STANDARD.                                  00540003
                                                                        00550003
       01  DLY-HRDDCL-REPORT-REC                 PIC  X(132).           00560003
                                                                        00570003
      ******************************************************************00580003
       WORKING-STORAGE SECTION.                                         00590003
      ******************************************************************00600003
                                                                        00610003
      ******************************************************************00620003
      *          P R O G R A M     A B E N D     C O D E               *00630003
      ******************************************************************00640003
                                                                        00650003
       01  ABEND-CODE                            PIC S9(04) VALUE ZERO  00660003
                                                            COMP SYNC.  00670003
           88  AC-CALENDAR-ROUTINE-ERROR                    VALUE +4019.00680003
                                                                        00690003
      ******************************************************************00700003
      *              P R O G R A M     C O N S T A N T S               *00710003
      ******************************************************************00720003
       01  PC-PROGRAM-CONSTANTS.                                        00730003
           05  PC-PROGRAM-ABEND-MESSAGE          PIC  X(11) VALUE       00740003
                                                     '** ABEND **'.     00750003
           05  PC-LINE-LIMIT                     PIC S9(04) VALUE +59   00760003
                                                           COMP-3.      00770003
           05  PC-REPORT-NBR-1                   PIC  9(01) VALUE 1.    00780003
           05  PC-PROGRAM-NAME                   PIC  X(08) VALUE       00790003
                                                           'AUKRP008'.  00800003
           05  PC-ONE                            PIC  9(01) VALUE 1.    00810003
           05  PC-MINUS-ONE                      PIC S9(01) VALUE -1.   00820003
                                                                        00830003
      ******************************************************************00840003
      *               P R O G R A M     C O U N T E R S                *00850003
      ******************************************************************00860003
       01  PC-PROGRAM-COUNTERS.                                         00870003
           05  PV-PAGE-CNT                       PIC S9(05) VALUE 0.    00880003
           05  PC-CRD-INPUT-REC-CNT              PIC  9(05) VALUE 0.    00890003
           05  PC-CRD-OUT-REC-CNT                PIC  9(05) VALUE 0.    00900003
                                                                        00910003
      ******************************************************************00920003
      *               P R O G R A M     D I S P L A Y S                *00930003
      ******************************************************************00940003
       01  PC-PROGRAM-DISPLAYS.                                         00950003
           05  PC-CRD-IN-DIS                     PIC  Z(04)9(01).       00960003
           05  PC-CRD-OUT-DIS                    PIC  Z(04)9(01).       00970003
                                                                        00980003
      ******************************************************************00990003
      *          P R O G R A M     E R R O R     M E S S A G E         *01000003
      ******************************************************************01010003
                                                                        01020003
      ******************************************************************01030003
      *              P R O G R A M     L A Y O U T S                   *01040003
      ******************************************************************01050003
       01  PL-PROGRAM-LAYOUTS.                                          01060003
           05  PL-INPUT-CRD-REC.                                        01070003
               10  PL-CRD-ACCT-NBR               PIC  X(19).            01080003
               10  PL-VND-AUTH-RSPN-CDE          PIC  X(03).            01090003
               10  PL-CRD-CRE-TRAN-TMST          PIC  X(26).            01100003
               10  PL-CRD-UPC-NBR                PIC  9(15).            01110003
               10  PL-UPC-DESC                   PIC  X(26).            01120003
           05  FILLER                            PIC  X(11).            01130003
      *----------------------------------------------------------       01140003
      * HEADING LINE 2                                                  01150003
      *----------------------------------------------------------       01160003
       01  HL-HEADING-CRD-LINE-2.                                       01170003
           05  FILLER                            PIC  X(46) VALUE       01180003
                                                            SPACES.     01190003
           05  FILLER                            PIC  X(35) VALUE       01200003
                      ' DAILY HARD DECLINED CARD DETAILS  '.            01210003
           05  FILLER                            PIC  X(51) VALUE       01220003
                                                            SPACES.     01230003
                                                                        01240003
      *----------------------------------------------------------       01250003
      * HEADING LINE 3                                                  01260003
      *----------------------------------------------------------       01270003
       01  HL-HEADING-CRD-LN-3.                                         01280003
           05  FILLER                            PIC  X(19) VALUE       01290003
                                              '___________________'.    01300003
           05  FILLER                            PIC  X(05) VALUE       01310003
                                                            SPACES.     01320003
           05  FILLER                            PIC  X(13) VALUE       01330003
                                                   '_____________'.     01340003
           05  FILLER                            PIC  X(05) VALUE       01350003
                                                            SPACES.     01360003
           05  FILLER                            PIC  X(26) VALUE       01370003
                                       '__________________________'.    01380003
           05  FILLER                            PIC  X(05) VALUE       01390003
                                                            SPACES.     01400003
           05  FILLER                            PIC  X(15) VALUE       01410003
                                                '_______________'.      01420003
           05  FILLER                            PIC  X(05) VALUE       01430003
                                                            SPACES.     01440003
           05  FILLER                            PIC  X(26) VALUE       01450003
                                       '__________________________'.    01460003
           05  FILLER                            PIC  X(14) VALUE       01470003
                                                            SPACES.     01480003
                                                                        01490003
      *----------------------------------------------------------       01500003
      * HEADING LINE 4                                                  01510003
      *----------------------------------------------------------       01520003
       01  HL-HEADING-CRD-LN-4.                                         01530003
           05  FILLER                            PIC  X(19) VALUE       01540003
                                              '    CARD NUMBER    '.    01550003
           05  FILLER                            PIC  X(05) VALUE       01560003
                                                            SPACES.     01570003
           05  FILLER                            PIC  X(13) VALUE       01580003
                                                   'CARD ORG STAT'.     01590003
           05  FILLER                            PIC  X(05) VALUE       01600003
                                                            SPACES.     01610003
           05  FILLER                            PIC  X(26) VALUE       01620003
                                       '  CARD CREATE TIMESTAMP   '.    01630003
           05  FILLER                            PIC  X(05) VALUE       01640003
                                                            SPACES.     01650003
           05  FILLER                            PIC  X(15) VALUE       01660003
                                                '   UPC NUMBER  '.      01670003
           05  FILLER                            PIC  X(05) VALUE       01680003
                                                            SPACES.     01690003
           05  FILLER                            PIC  X(26) VALUE       01700003
                                       '     CARD DESCRIPTION     '.    01710003
           05  FILLER                            PIC  X(14) VALUE       01720003
                                                            SPACES.     01730003
                                                                        01740003
      *----------------------------------------------------------       01750003
      * DETAIL LINE                                                     01760003
      *----------------------------------------------------------       01770003
       01  DL-DETAIL-CRD-LINE.                                          01780003
           05  DL-CRD-ACCT-NBR                   PIC  X(19) VALUE       01790003
                                                            SPACES.     01800003
           05  FILLER                            PIC  X(10) VALUE       01810003
                                                            SPACES.     01820003
           05  DL-VND-AUTH-RSPN-CDE              PIC  X(03) VALUE       01830003
                                                            SPACES.     01840003
           05  FILLER                            PIC  X(10) VALUE       01850003
                                                            SPACES.     01860003
           05  DL-CRD-CRE-TRAN-TMST              PIC  X(26) VALUE       01870003
                                                            SPACES.     01880003
           05  FILLER                            PIC  X(05) VALUE       01890003
                                                            SPACES.     01900003
           05  DL-CRD-UPC-NBR                    PIC  X(15) VALUE       01910003
                                                            SPACES.     01920003
           05  FILLER                            PIC  X(05) VALUE       01930003
                                                            SPACES.     01940003
           05  DL-UPC-DESC                       PIC  X(26) VALUE       01950003
                                                            SPACES.     01960003
           05  FILLER                            PIC  X(14) VALUE       01970003
                                                            SPACES.     01980003
                                                                        01990003
      ******************************************************************02000003
      *              P R O G R A M     S W I T C H E S                 *02010003
      ******************************************************************02020003
       01  PS-PROGRAM-SWITCHES.                                         02030003
           05  PS-END-CRD-OF-FILE-SW             PIC  X(01) VALUE 'N'.  02040003
               88  PS-NOT-END-CRD-OF-FILE                  VALUE 'N'.   02050003
               88  PS-END-CRD-OF-FILE                      VALUE 'Y'.   02060003
                                                                        02070003
      ******************************************************************02080003
      *              P R O G R A M     V A R I A B L E S               *02090003
      ******************************************************************02100003
       01  PV-PROGRAM-VARIABLES.                                        02110003
           05  PV-PARAGRAPH-NAME                 PIC  X(30) VALUE       02120003
                                                           SPACES.      02130003
           05  PV-LINE-CNT                       PIC S9(03) VALUE +65   02140003
                                                      COMP-3.           02150003
           05  PV-CURR-DATE-CCYYMMDD.                                   02160003
               10  PV-CURR-DATE-CCYY             PIC  9(04).            02170003
               10  PV-CURR-DATE-MM               PIC  9(02).            02180003
               10  PV-CURR-DATE-DD               PIC  9(02).            02190003
           05  PV-CURR-TIME.                                            02200003
               10  PV-CURR-TIME-HH               PIC  9(02) VALUE ZERO. 02210003
               10  PV-CURR-TIME-MM               PIC  9(02) VALUE ZERO. 02220003
               10  PV-CURR-TIME-SSHH             PIC  9(04) VALUE ZERO. 02230003
      *----------------------------------------------------------       02240003
      * STANDARD HEADING FOR 132 CHARACTER REPORT                       02250003
      *----------------------------------------------------------       02260003
           COPY DPWS132.                                                02270003
                                                                        02280003
      *---------------------------------------------------------------* 02290003
       PROCEDURE DIVISION.                                              02300003
      *---------------------------------------------------------------* 02310003
                                                                        02320003
       0000-MAINLINE-PARAGRAPH.                                         02330003
                                                                        02340003
           PERFORM 1000-INITIALIZE.                                     02350003
                                                                        02360003
           PERFORM 2000-PROCESS-STR-INFO                                02370003
                   UNTIL PS-END-CRD-OF-FILE.                            02380003
                                                                        02390003
           PERFORM 9000-END-OF-JOB.                                     02400003
                                                                        02410003
           GOBACK.                                                      02420003
      ***************************************************************** 02430003
      * 1000-INITIALIZE.                                                02440003
      *                                                                 02450003
      * INITALIZE COUNTER, SWITCHES AND LAYOUTS. OPEN THE FILES         02460003
      * AND MAKE IT READY FOR FURTHER PROCESSING.                       02470003
      *                                                                 02480003
      *                                                                 02490003
      * PARAGRAPH CALLED FROM :- 0000-MAINLINE-PARAGRAPH                02500003
      * PARAGRAPH CALLED TO   :- 8000-READ-CRD-INPUT                    02510003
      ***************************************************************** 02520003
       1000-INITIALIZE.                                                 02530003
                                                                        02540003
           OPEN INPUT HRD-DCL-INPUT                                     02550003
               OUTPUT DLY-HRDDCL-REPORT.                                02560003
                                                                        02570003
           PERFORM 8000-READ-CRD-INPUT.                                 02580003
                                                                        02590003
           IF PS-END-CRD-OF-FILE                                        02600003
             DISPLAY ' NO THIRD PARTY GIFT CARDS STORE AND FORWARDED  ' 02610003
           END-IF.                                                      02620003
                                                                        02630003
           MOVE FUNCTION CURRENT-DATE (1:8) TO PV-CURR-DATE-CCYYMMDD.   02640003
           ACCEPT PV-CURR-TIME FROM TIME.                               02650003
                                                                        02660003
           MOVE PV-CURR-DATE-MM             TO DP132-RUN-MONTH.         02670003
           MOVE PV-CURR-DATE-DD             TO DP132-RUN-DAY.           02680003
           MOVE PV-CURR-DATE-CCYY           TO DP132-RUN-YEAR.          02690003
                                                                        02700003
           MOVE PV-CURR-TIME-HH             TO DP132-RUN-HOUR.          02710003
           MOVE PV-CURR-TIME-MM             TO DP132-RUN-MINUTE.        02720003
           MOVE PC-PROGRAM-NAME             TO DP132-PROGRAM-NAME.      02730003
           MOVE PC-REPORT-NBR-1             TO DP132-REPORT-NUMBER.     02740003
                                                                        02750003
      ***************************************************************** 02760003
      * 2000-PROCESS-STR-INFO.                                          02770003
      *                                                                 02780003
      * MAINPROCESS FOR STORE RECORDS.                                  02790003
      *                                                                 02800003
      * PARAGRAPH CALLED FROM :- 0000-MAINLINE-PARAGRAPH                02810003
      * PARAGRAPH CALLED TO   :- 2100-PRINT-CRD-HEADINGS                02820003
      *                          8000-READ-CRD-INPUT                    02830003
      ***************************************************************** 02840003
                                                                        02850003
       2000-PROCESS-STR-INFO.                                           02860003
                                                                        02870003
      *    MOVE INPUT FIELDS TO DETAIL RECORD LAYOUT                    02880003
           MOVE PL-CRD-ACCT-NBR             TO DL-CRD-ACCT-NBR.         02890003
           MOVE PL-VND-AUTH-RSPN-CDE        TO DL-VND-AUTH-RSPN-CDE.    02900003
           MOVE PL-CRD-CRE-TRAN-TMST        TO DL-CRD-CRE-TRAN-TMST.    02910003
           MOVE PL-CRD-UPC-NBR              TO DL-CRD-UPC-NBR.          02920003
           MOVE PL-UPC-DESC                 TO DL-UPC-DESC.             02930003
                                                                        02940003
           IF PV-LINE-CNT > PC-LINE-LIMIT                               02950003
               MOVE ZERO                    TO PV-LINE-CNT              02960003
               PERFORM 2100-PRINT-CRD-HEADINGS                          02970003
           END-IF.                                                      02980003
                                                                        02990003
           WRITE DLY-HRDDCL-REPORT-REC      FROM DL-DETAIL-CRD-LINE.    03000003
                                                                        03010003
           ADD +1                           TO PV-LINE-CNT.             03020003
           ADD +1                           TO PC-CRD-OUT-REC-CNT.      03030003
                                                                        03040003
           PERFORM 8000-READ-CRD-INPUT.                                 03050003
                                                                        03060003
      ***************************************************************** 03070003
      * 2100-PRINT-CRD-HEADINGS                                         03080003
      *                                                                 03090003
      * WRITE THE STANDARD KHOLS HEADER AND THE REPORT HEADERS INTO     03100003
      * START OF THE EACH PAGE.                                         03110003
      *                                                                 03120003
      * PARAGRAPH CALLED FROM :- 2000-PROCESS-STR-INFO                  03130003
      * PARAGRAPH CALLED TO   :- NIL                                    03140003
      ***************************************************************** 03150003
       2100-PRINT-CRD-HEADINGS.                                         03160003
                                                                        03170003
           ADD +1                      TO PV-PAGE-CNT.                  03180003
           MOVE PV-PAGE-CNT            TO DP132-PAGE-NUMBER.            03190003
           WRITE DLY-HRDDCL-REPORT-REC FROM                             03200003
                 DP132-STANDRD-HEADER-132-COLS  AFTER ADVANCING PAGE.   03210003
                                                                        03220003
           WRITE DLY-HRDDCL-REPORT-REC FROM HL-HEADING-CRD-LINE-2.      03230003
           WRITE DLY-HRDDCL-REPORT-REC FROM HL-HEADING-CRD-LN-3.        03240003
           WRITE DLY-HRDDCL-REPORT-REC FROM HL-HEADING-CRD-LN-4.        03250003
           WRITE DLY-HRDDCL-REPORT-REC FROM HL-HEADING-CRD-LN-3.        03260003
                                                                        03270003
           MOVE SPACES                 TO DLY-HRDDCL-REPORT-REC.        03280003
           WRITE DLY-HRDDCL-REPORT-REC.                                 03290003
           MOVE +6                     TO PV-LINE-CNT.                  03300003
                                                                        03310003
      ******************************************************************03320003
      *     READ INPUT CARD FILE                                        03330003
      * PARAGRAPH CALLED FROM :- 1000-INITIALIZE                        03340003
      *                       :- 2000-PRCSS-CRD-INFO                    03350003
      * PARAGRAPH CALLED TO   :- NIL                                    03360003
      ******************************************************************03370003
       8000-READ-CRD-INPUT.                                             03380003
                                                                        03390003
           READ HRD-DCL-INPUT               INTO PL-INPUT-CRD-REC       03400003
               AT END                                                   03410003
                  SET PS-END-CRD-OF-FILE      TO TRUE                   03420003
               NOT AT END                                               03430003
                  SET PS-NOT-END-CRD-OF-FILE  TO TRUE                   03440003
                  ADD 1                       TO PC-CRD-INPUT-REC-CNT   03450003
           END-READ.                                                    03460003
                                                                        03470003
      *---------------------------------------------------------------* 03480003
      *            -- END-OF-JOB PROCESSING                           * 03490003
      *---------------------------------------------------------------* 03500003
       9000-END-OF-JOB.                                                 03510003
                                                                        03520003
           MOVE PC-CRD-INPUT-REC-CNT        TO PC-CRD-IN-DIS.           03530003
           MOVE PC-CRD-OUT-REC-CNT          TO PC-CRD-OUT-DIS.          03540003
           DISPLAY '**************************************************' 03550003
           DISPLAY '**                 RUN SUMMARY                  **' 03560003
           DISPLAY '**              PROGRAM = AUKRP056              **' 03570003
           DISPLAY '**************************************************' 03580003
           DISPLAY '                                 '                  03590003
           DISPLAY 'NO. OF INPUT CARD REC PROCESSED  = ' PC-CRD-IN-DIS  03600003
           DISPLAY 'NO. OF OUTPUT CARD REC WRITTEN   = ' PC-CRD-OUT-DIS 03610003
           CLOSE HRD-DCL-INPUT                                          03620003
                 DLY-HRDDCL-REPORT.                                     03630003
                                                                        03640003
