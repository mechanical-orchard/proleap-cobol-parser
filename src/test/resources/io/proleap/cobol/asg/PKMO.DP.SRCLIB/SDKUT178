       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    SDKUT178.                                         00020000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  11/30/2012.                                       00050000
       DATE-COMPILED.                                                   00060000
                                                                        00061000
      ******************************************************************00062000
      * DELTA PROCESSING FOR ACTUAL TRUCK DELIVERIES                    00063002
      *    COMPARES PREVIOUS VS CURRENT EXTRACT FILES AND WRITES        00064002
      *    A CHANGE RECORD FOR ADDS, UPDATES, AND DELETES.              00064102
      ******************************************************************00068000
                                                                        00069000
       ENVIRONMENT DIVISION.                                            00070000
       CONFIGURATION SECTION.                                           00080000
       SOURCE-COMPUTER.    IBM-3090.                                    00090000
       OBJECT-COMPUTER.    IBM-3090.                                    00100000
       INPUT-OUTPUT SECTION.                                            00110000
       FILE-CONTROL.                                                    00120000
                                                                        00130000
           SELECT CURR-EXTRACT-FILE                                     00140000
                  ASSIGN TO INP01.                                      00150000
           SELECT PREV-EXTRACT-FILE                                     00160000
                  ASSIGN TO INP02.                                      00170000
           SELECT DELTA-FILE                                            00180000
                  ASSIGN TO OUT01.                                      00190000
           SELECT DELTA-COUNT-FILE                                      00200000
                  ASSIGN TO OUT02.                                      00210000
                                                                        00220000
       DATA DIVISION.                                                   00230000
       FILE SECTION.                                                    00240000
                                                                        00250000
       FD  CURR-EXTRACT-FILE                                            00260000
           RECORDING MODE IS F                                          00270000
           LABEL RECORDS OMITTED                                        00280000
           BLOCK CONTAINS 0 RECORDS                                     00290000
           DATA RECORD IS CURR-EXTRACT-RECORD.                          00300000
       01  CURR-EXTRACT-RECORD                 PIC X(120).              00310000
                                                                        00320000
       FD  PREV-EXTRACT-FILE                                            00330000
           RECORDING MODE IS F                                          00340000
           LABEL RECORDS OMITTED                                        00350000
           BLOCK CONTAINS 0 RECORDS                                     00360000
           DATA RECORD IS PREV-EXTRACT-RECORD.                          00370000
       01  PREV-EXTRACT-RECORD                 PIC X(120).              00380000
                                                                        00390000
       FD  DELTA-FILE                                                   00400000
           RECORDING MODE IS F                                          00410000
           LABEL RECORDS OMITTED                                        00420000
           BLOCK CONTAINS 0 RECORDS                                     00430000
           DATA RECORD IS DELTA-RECORD.                                 00440000
       01  DELTA-RECORD                        PIC X(104).              00450003
                                                                        00460000
       FD  DELTA-COUNT-FILE                                             00470000
           RECORDING MODE IS F                                          00480000
           LABEL RECORDS OMITTED                                        00490000
           BLOCK CONTAINS 0 RECORDS                                     00500000
           DATA RECORD IS DELTA-COUNT-RECORD.                           00510000
       01  DELTA-COUNT-RECORD                  PIC 9(07).               00511000
                                                                        00512000
       WORKING-STORAGE SECTION.                                         00513000
      ******************************************************************00514000
      **PROGRAM SWITCHES                                                00515000
      ******************************************************************00516000
       01  PS-SWITCH-AREAS.                                             00517000
           05  PS-END-OF-CUR-FILE-SW           PIC X(01)  VALUE 'N'.    00518000
               88  PS-END-OF-CUR-FILE                     VALUE 'Y'.    00519000
               88  PS-NOT-END-OF-CUR-FILE                 VALUE 'N'.    00520000
           05  PS-END-OF-PRV-FILE-SW           PIC X(01)  VALUE 'N'.    00530000
               88  PS-END-OF-PRV-FILE                     VALUE 'Y'.    00540000
               88  PS-NOT-END-OF-PRV-FILE                 VALUE 'N'.    00550000
                                                                        00551002
      ******************************************************************00560000
      **DATE TIME FIELDS                                                00570000
      ******************************************************************00580000
       01 DF-DATE-TIME-FIELDS.                                          00590000
          05 DF-ACCEPT-DATE                    PIC X(06) VALUE SPACES.  00600000
          05 DF-ACCEPT-TIME                    PIC X(08) VALUE SPACES.  00610000
                                                                        00611002
      ******************************************************************00620000
      **PROGRAM ACCUMULATORS (COUNTERS)                                 00630000
      ******************************************************************00640000
       01  PA-PROGRAM-ACCUMULATORS.                                     00650000
           05  PA-CUR-READ                     PIC 9(07) VALUE ZEROS.   00660000
           05  PA-PRV-READ                     PIC 9(07) VALUE ZEROS.   00670000
           05  PA-MATCH-COUNT                  PIC 9(07) VALUE ZEROS.   00680000
           05  PA-DATE-OOB-COUNT               PIC 9(07) VALUE ZEROS.   00690000
           05  PA-WRITE-ADD-COUNT              PIC 9(07) VALUE ZEROS.   00700000
           05  PA-WRITE-UPD-COUNT              PIC 9(07) VALUE ZEROS.   00710000
           05  PA-WRITE-DLT-COUNT              PIC 9(07) VALUE ZEROS.   00720000
           05  PA-TTL-WRITE-COUNT              PIC 9(07) VALUE ZEROS.   00730000
                                                                        00740000
      ******************************************************************00750000
      **PROGRAM VARIABLES                                               00760000
      ******************************************************************00770000
       01  PV-PROGRAM-VARIABLES.                                        00780000
           05  PV-COUNT-RECORD.                                         00781000
               10  PV-RECORDS-WRITTEN-TO-FILE  PIC 9(07) VALUE ZEROES.  00781102
                                                                        00781200
       01  PV-FISCAL-PERIOD-DATES.                                      00781302
           05  PV-CUR-FP-1ST-DTE               PIC X(10) VALUE SPACES.  00781402
           05  PV-CUR-FP-LAST-DTE              PIC X(10) VALUE SPACES.  00781502
           05  PV-PRV-FP-1ST-DTE               PIC X(10) VALUE SPACES.  00781602
           05  PV-PRV-FP-LAST-DTE              PIC X(10) VALUE SPACES.  00781702
           05  PV-NXT-FP-1ST-DTE               PIC X(10) VALUE SPACES.  00781802
           05  PV-NXT-FP-LAST-DTE              PIC X(10) VALUE SPACES.  00781902
                                                                        00782000
      ******************************************************************00782100
      **PRIMARY KEYS USED TO MATCH UP RECORDS                           00782200
      ******************************************************************00782300
       01  PV-PRIMARY-KEYS.                                             00782400
           05  PV-CUR-KEY.                                              00782500
               10  PV-CUR-STR-NBR              PIC 9(04) VALUE ZEROES.  00782602
               10  PV-CUR-TRIP-DATE            PIC X(10) VALUE SPACES.  00782702
               10  PV-CUR-TRAILER-ID           PIC X(12) VALUE SPACES.  00782802
           05  PV-PRV-KEY.                                              00782901
               10  PV-PRV-STR-NBR              PIC 9(04) VALUE ZEROES.  00783002
               10  PV-PRV-TRIP-DATE            PIC X(10) VALUE SPACES.  00783102
               10  PV-PRV-TRAILER-ID           PIC X(12) VALUE SPACES.  00783202
                                                                        00783301
      ******************************************************************00784000
      *               SYSTEM TIME AND DATE VARIABLES                    00785000
      ******************************************************************00786000
       01  SYS-DATE-TIME.                                               00786100
           05  SYS-DATE                        PIC X(08) VALUE SPACES.  00786202
           05  SYS-TIME                        PIC X(08) VALUE SPACES.  00786302
                                                                        00786400
       01  ST-BEG-TIME.                                                 00786500
           05  ST-BEG-HR                       PIC 9(02) VALUE ZEROS.   00786602
           05  FILLER                          PIC X(01) VALUE ':'.     00786702
           05  ST-BEG-MN                       PIC 9(02) VALUE ZEROS.   00786802
                                                                        00786900
       01  ST-END-TIME.                                                 00787000
           05  ST-END-HR                       PIC 9(02) VALUE ZEROS.   00788002
           05  FILLER                          PIC X(01) VALUE ':'.     00789002
           05  ST-END-MN                       PIC 9(02) VALUE ZEROS.   00789102
                                                                        00789200
       01  SD-EDIT-DATE.                                                00789300
           05  SD-MONTH                        PIC 9(02) VALUE ZEROS.   00789402
           05  FILLER                          PIC X(01) VALUE '/'.     00789502
           05  SD-DAY                          PIC 9(02) VALUE ZEROS.   00789602
           05  FILLER                          PIC X(01) VALUE '/'.     00789702
           05  SD-CENT                         PIC 9(02) VALUE ZEROS.   00789802
           05  SD-YEAR                         PIC 9(02) VALUE ZEROS.   00789902
                                                                        00790000
       01  PV-TODAYS-DATE.                                              00790101
           05 PV-YEAR                          PIC 9(04) VALUE ZEROES.  00790202
           05 FILLER                           PIC X(01) VALUE '-'.     00790302
           05 PV-MONTH                         PIC 9(02) VALUE ZEROES.  00790402
           05 FILLER                           PIC X(01) VALUE '-'.     00790502
           05 PV-DAY                           PIC 9(02) VALUE ZEROES.  00790602
                                                                        00791300
      ****************************************************************  00791900
      **ABEND INFORMATION                                               00792000
      ****************************************************************  00792100
       01  AC-ABEND-FIELDS.                                             00792200
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    00792300
                                                           COMP SYNC.   00792400
               88  AC-DATE-ROUTINE-ERROR                 VALUE +4019.   00792500
                                                                        00792600
       01  PV-MISC-ABEND-FIELDS.                                        00793000
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   00794000
           05  PV-OPERATION-MSG                PIC X(40) VALUE SPACE.   00795000
                                                                        00795102
      ******************************************************************00796000
      **COPYBOOKS                                                       00797000
      ******************************************************************00798000
      ****BOTH INPUT FILES USE THE SAME COPYBOOK, THIS MAKES THEM UNIQUE00799000
           COPY SDRD177 REPLACING 'SDRD177-' BY 'SD177-CUR-'.           00800001
           COPY SDRD177 REPLACING 'SDRD177-' BY 'SD177-PRV-'.           00810001
      ****OUTPUT RECORD LAYOUT                                          00820000
           COPY SDRD177 REPLACING 'SDRD177-' BY 'SD177-OUT-'.           00830001
                                                                        00840000
      ******************************************************************00850000
      * PARAMETER LIST FOR THE CALENDAR ROUTINE                         00860000
      ******************************************************************00870000
           COPY DPWS500.                                                00880000
                                                                        00900000
       PROCEDURE DIVISION.                                              00910000
      ******************************************************************00920000
       0000-MAINLINE.                                                   00930000
                                                                        00940000
           PERFORM 1000-INITIALIZATION.                                 00950000
                                                                        00960000
           PERFORM 2000-PROCESS-RECORDS                                 00970000
                   UNTIL PS-END-OF-CUR-FILE                             00980000
                      OR PS-END-OF-PRV-FILE.                            00990000
                                                                        01000000
           PERFORM 2010-PROCESS-EXTRA-RECORDS                           01010004
                   UNTIL PS-END-OF-CUR-FILE                             01020000
                     AND PS-END-OF-PRV-FILE.                            01030000
                                                                        01031000
           PERFORM 9000-WRAP-UP.                                        01032004
           GOBACK.                                                      01033000
                                                                        01034000
      ******************************************************************01035000
      **INITIALIZATION ROUTINE                                          01036000
      ******************************************************************01037000
       1000-INITIALIZATION.                                             01038000
                                                                        01039000
           DISPLAY '*************************************'.             01039102
           DISPLAY '* SDKUT178 - TRUCK ACTUAL DELIVERIES*'.             01039202
           DISPLAY '*            DELTA FILE PROCESSING  *'.             01039302
           DISPLAY '*************************************'.             01039402
           DISPLAY '*'.                                                 01039702
           DISPLAY '* - PROCESSING...'.                                 01039802
                                                                        01039902
           OPEN INPUT  CURR-EXTRACT-FILE                                01040000
                       PREV-EXTRACT-FILE.                               01050000
           OPEN OUTPUT DELTA-FILE.                                      01060000
                                                                        01070000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01090000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01100000
                                                                        01110000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 01120000
                                                                        01130000
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            01140000
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            01150000
                                                                        01160000
           MOVE SYS-DATE (1:2) TO SD-CENT.                              01170000
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              01180000
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             01181000
           MOVE SYS-DATE (7:2) TO SD-DAY.                               01182000
                                                                        01183000
           MOVE SYS-DATE (1:4)         TO PV-YEAR.                      01183101
           MOVE SYS-DATE (5:2)         TO PV-MONTH.                     01183201
           MOVE SYS-DATE (7:2)         TO PV-DAY.                       01183301
           DISPLAY '* TODAYS DATE: ' PV-TODAYS-DATE.                    01183401
                                                                        01183501
           PERFORM 1100-DETERMINE-DATES.                                01184000
                                                                        01185000
           PERFORM 4000-READ-CUR-EXTR-RECORD.                           01186000
           PERFORM 4100-READ-PRV-EXTR-RECORD.                           01187000
                                                                        01188000
      ******************************************************************01189000
      * DETERMINE FISCAL MONTH START / END DATES                        01190000
      ******************************************************************01200000
       1100-DETERMINE-DATES.                                            01210000
                                                                        01220000
           MOVE '1100-DETERMINE-DATES' TO PV-PARAGRAPH-NAME.            01230000
           DISPLAY 'SYSTEM DATE: ' SD-EDIT-DATE.                        01240000
                                                                        01241000
      ***GET START/END DATES OF CURRENT FISCAL MONTH                    01242000
                                                                        01243000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01244000
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       01245000
           SET DPG52-LK-DTE-GREG-CC-FMT  TO TRUE.                       01246000
           MOVE SD-EDIT-DATE             TO DPG52-GREG-CC-DATE-FMT.     01247000
                                                                        01247100
           DISPLAY 'DPG52-GREG-CC-DATE = ' DPG52-GREG-CC-DATE-FMT.      01247200
                                                                        01247300
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                01247400
                                                   DPG52                01247500
                                                   DPG53                01247600
                                                   DPG54                01247700
                                                   DPG55                01247800
                                                   DPG56.               01247900
                                                                        01248000
           EVALUATE TRUE                                                01248100
               WHEN DPG54-NO-ERROR                                      01248200
                   MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO PV-CUR-FP-1ST-DTE 01248300
                   MOVE DPG56-LAST-FP-DB2-ISO-FMT TO PV-CUR-FP-LAST-DTE 01248400
               WHEN OTHER                                               01248500
                   DISPLAY 'ERROR GETTING FISCAL MONTH START DATE'      01248600
                   PERFORM Z5000-ERROR-ROUTINE                          01248700
           END-EVALUATE.                                                01248800
                                                                        01248900
      ***GET START/END DATES OF PREVIOUS FISCAL MONTH                   01249000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01249100
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       01249200
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                       01249300
           MOVE PV-CUR-FP-1ST-DTE        TO DPG52-DB2-ISO-DATE-FMT.     01249400
           SET DPG51-DECREMENT-DATE      TO TRUE.                       01249500
           MOVE 1                        TO DPG51-INCR-DECR-DAYS-9.     01249600
                                                                        01249700
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                01249800
                                                   DPG52                01249900
                                                   DPG53                01250000
                                                   DPG54                01250100
                                                   DPG55                01250200
                                                   DPG56.               01250300
                                                                        01250400
           EVALUATE TRUE                                                01250500
               WHEN DPG54-NO-ERROR                                      01250600
                   MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO PV-PRV-FP-1ST-DTE 01250700
                   MOVE DPG56-LAST-FP-DB2-ISO-FMT  TO PV-PRV-FP-LAST-DTE01250800
               WHEN OTHER                                               01250900
                   DISPLAY 'ERROR GETTING PREV FISCAL MONTH DATES'      01251000
                   PERFORM Z5000-ERROR-ROUTINE                          01251100
           END-EVALUATE.                                                01251200
                                                                        01251300
      ***GET START/END DATES OF NEXT FISCAL MONTH                       01251400
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01251500
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       01251600
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                       01251700
           MOVE PV-CUR-FP-LAST-DTE       TO DPG52-DB2-ISO-DATE-FMT.     01251800
           SET DPG51-INCREMENT-DATE      TO TRUE.                       01251900
           MOVE 1                        TO DPG51-INCR-DECR-DAYS-9.     01252000
                                                                        01252100
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                01252200
                                                   DPG52                01252300
                                                   DPG53                01252400
                                                   DPG54                01252500
                                                   DPG55                01252600
                                                   DPG56.               01252700
                                                                        01252800
           EVALUATE TRUE                                                01252900
               WHEN DPG54-NO-ERROR                                      01253000
                   MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO PV-NXT-FP-1ST-DTE 01253100
                   MOVE DPG56-LAST-FP-DB2-ISO-FMT  TO PV-NXT-FP-LAST-DTE01253200
               WHEN OTHER                                               01253300
                   DISPLAY 'ERROR GETTING NEXT FISCAL MONTH DATES'      01253400
                   PERFORM Z5000-ERROR-ROUTINE                          01253500
           END-EVALUATE.                                                01253600
                                                                        01253700
           DISPLAY 'PREVIOUS FISCAL MONTH START: ' PV-PRV-FP-1ST-DTE.   01253801
           DISPLAY '..UP UNTIL THE CURRENT DATE: ' PV-TODAYS-DATE.      01253901
           DISPLAY '...ONLY COMPARING RECORDS IN THIS RANGE.'.          01254000
           DISPLAY '...'.                                               01255000
                                                                        01255100
      ******************************************************************01255200
      **COMPARES THE INPUT FILES AND IDENTIFIES ADDS, DELETES, AND CHG  01255300
      ******************************************************************01255400
       2000-PROCESS-RECORDS.                                            01255500
                                                                        01255600
           EVALUATE TRUE                                                01255700
      *--> KEY DATA MATCHES, SO CHECK NON-KEY DATA VALUES               01255800
              WHEN PV-CUR-KEY EQUAL PV-PRV-KEY                          01255900
                   IF SD177-CUR-TRUCK-SIZE   NOT EQUAL TO               01256001
                      SD177-PRV-TRUCK-SIZE                              01257001
                   OR SD177-CUR-TRUCK-UNITS  NOT EQUAL TO               01258001
                      SD177-PRV-TRUCK-UNITS                             01259001
                   OR SD177-CUR-HARDLINE-QTY NOT EQUAL TO               01259101
                      SD177-PRV-HARDLINE-QTY                            01259201
                   OR SD177-CUR-SOFTLINE-QTY NOT EQUAL TO               01259301
                      SD177-PRV-SOFTLINE-QTY                            01259401
                   OR SD177-CUR-JEWELRY-QTY  NOT EQUAL TO               01259501
                      SD177-PRV-JEWELRY-QTY                             01259601
                   OR SD177-CUR-HALLMARK-QTY NOT EQUAL TO               01259701
                      SD177-PRV-HALLMARK-QTY                            01259801
                   OR SD177-CUR-COSMETIC-QTY NOT EQUAL TO               01259901
                      SD177-PRV-COSMETIC-QTY                            01260001
      *--> CURRENT FILE MATCHES ON KEY, BUT NON-KEY DOESN'T MATCH       01261000
                     PERFORM 2200-WRITE-CURRENT-UPDATE                  01270000
                   ELSE                                                 01280000
                     ADD +1 TO PA-MATCH-COUNT                           01281000
                   END-IF                                               01282000
                   PERFORM 4000-READ-CUR-EXTR-RECORD                    01283000
                   PERFORM 4100-READ-PRV-EXTR-RECORD                    01284000
      *--> CURRENT FILE HAS AN ADDITIONAL RECORD, SO LIST AS 'A'        01285000
              WHEN PV-CUR-KEY LESS THAN PV-PRV-KEY                      01286000
                   PERFORM 2100-WRITE-CURRENT-ADD                       01287000
                   PERFORM 4000-READ-CUR-EXTR-RECORD                    01288000
      *--> CURRENT FILE IS MISSING A PREV RECORD, SO LIST AS 'D'        01289000
              WHEN PV-CUR-KEY GREATER THAN PV-PRV-KEY                   01290000
                   PERFORM 2300-WRITE-PREVIOUS-DELETE                   01291000
                   PERFORM 4100-READ-PRV-EXTR-RECORD                    01291100
           END-EVALUATE.                                                01291200
                                                                        01291300
      ******************************************************************01291400
      * 2010-PROCESS-EXTRA-RECORDS.                                     01291504
      *     PROCESSES THRU END OF FILE FOR REMAINING RECORDS.           01291600
      ******************************************************************01291700
       2010-PROCESS-EXTRA-RECORDS.                                      01291804
                                                                        01291900
      * WHEN CUR EXTRACT FILE STILL HAS RECORDS... COUNT AS 'A'         01292000
           IF  PS-NOT-END-OF-CUR-FILE                                   01293000
               PERFORM 2100-WRITE-CURRENT-ADD                           01293100
               PERFORM 4000-READ-CUR-EXTR-RECORD                        01293200
           END-IF.                                                      01293300
                                                                        01293400
      * WHEN PRV EXTRACT FILE STILL HAS RECORDS... COUNT AS 'D'         01293500
           IF  PS-NOT-END-OF-PRV-FILE                                   01293600
               PERFORM 2300-WRITE-PREVIOUS-DELETE                       01293700
               PERFORM 4100-READ-PRV-EXTR-RECORD                        01293800
           END-IF.                                                      01293900
                                                                        01294000
      ******************************************************************01295000
      **WRITE CURRENT RECORD - ADD                                      01296000
      ******************************************************************01297000
       2100-WRITE-CURRENT-ADD.                                          01298000
                                                                        01299000
           MOVE SD177-CUR-EXTRACT-RECORD TO SD177-OUT-EXTRACT-RECORD.   01300001
           MOVE 'A' TO SD177-OUT-CHANGE-ACTN-CDE.                       01310001
                                                                        01320000
           IF SD177-OUT-TRIP-DATE >= PV-PRV-FP-1ST-DTE                  01330001
            AND SD177-OUT-TRIP-DATE < PV-TODAYS-DATE                    01340001
              WRITE DELTA-RECORD FROM SD177-OUT-EXTRACT-RECORD          01350001
              ADD +1 TO PA-WRITE-ADD-COUNT                              01360000
              ADD +1 TO PA-TTL-WRITE-COUNT                              01370000
           ELSE                                                         01380000
              ADD +1 TO PA-DATE-OOB-COUNT                               01390000
              DISPLAY 'SKIPPED; DATE OOB: ' SD177-OUT-EXTRACT-RECORD    01400001
           END-IF.                                                      01410000
                                                                        01420000
      ******************************************************************01430000
      **WRITE CURRENT RECORD - UPDATE                                   01440000
      ******************************************************************01450000
       2200-WRITE-CURRENT-UPDATE.                                       01460000
                                                                        01470000
           MOVE SD177-CUR-EXTRACT-RECORD TO SD177-OUT-EXTRACT-RECORD.   01480001
           MOVE 'U' TO SD177-OUT-CHANGE-ACTN-CDE.                       01490001
                                                                        01500000
           IF SD177-OUT-TRIP-DATE >= PV-PRV-FP-1ST-DTE                  01501001
            AND SD177-OUT-TRIP-DATE < PV-TODAYS-DATE                    01502001
              WRITE DELTA-RECORD FROM SD177-OUT-EXTRACT-RECORD          01530001
              ADD +1 TO PA-WRITE-UPD-COUNT                              01540000
              ADD +1 TO PA-TTL-WRITE-COUNT                              01541000
           ELSE                                                         01542000
              ADD +1 TO PA-DATE-OOB-COUNT                               01543000
              DISPLAY 'SKIPPED; DATE OOB: ' SD177-OUT-EXTRACT-RECORD    01544001
           END-IF.                                                      01545000
                                                                        01546000
      ******************************************************************01547000
      **WRITE PREVIOUS RECORD - DELETE                                  01548000
      ******************************************************************01549000
       2300-WRITE-PREVIOUS-DELETE.                                      01549100
                                                                        01549200
           MOVE SD177-PRV-EXTRACT-RECORD TO SD177-OUT-EXTRACT-RECORD.   01549301
           MOVE 'D' TO SD177-OUT-CHANGE-ACTN-CDE.                       01549401
                                                                        01549500
           IF SD177-OUT-TRIP-DATE >= PV-PRV-FP-1ST-DTE                  01549601
            AND SD177-OUT-TRIP-DATE < PV-TODAYS-DATE                    01549701
              WRITE DELTA-RECORD FROM SD177-OUT-EXTRACT-RECORD          01550001
              ADD +1 TO PA-WRITE-DLT-COUNT                              01550100
              ADD +1 TO PA-TTL-WRITE-COUNT                              01550200
           ELSE                                                         01551000
              ADD +1 TO PA-DATE-OOB-COUNT                               01551100
              DISPLAY 'SKIPPED; DATE OOB: ' SD177-OUT-EXTRACT-RECORD    01551201
           END-IF.                                                      01551300
                                                                        01551400
      ******************************************************************01551500
      **READ CURR FILE RECORD                                           01551600
      ******************************************************************01551700
       4000-READ-CUR-EXTR-RECORD.                                       01551800
                                                                        01551900
           READ CURR-EXTRACT-FILE                                       01552000
              INTO SD177-CUR-EXTRACT-RECORD                             01553001
                 AT END                                                 01554000
                 SET PS-END-OF-CUR-FILE TO TRUE                         01555000
           END-READ.                                                    01556000
                                                                        01557000
      * MOVE KEYS                                                       01558000
           IF  PS-NOT-END-OF-CUR-FILE                                   01559000
               ADD +1                       TO PA-CUR-READ              01560000
               MOVE SD177-CUR-STR-NBR       TO PV-CUR-STR-NBR           01570001
               MOVE SD177-CUR-TRIP-DATE     TO PV-CUR-TRIP-DATE         01580001
               MOVE SD177-CUR-TRAILER-ID    TO PV-CUR-TRAILER-ID        01581001
           END-IF.                                                      01590000
                                                                        01600000
      ******************************************************************01610000
      **READ PRICE CHANGE RECORD FROM THE PREVIOUS REPORT FILE          01620000
      ******************************************************************01630000
       4100-READ-PRV-EXTR-RECORD.                                       01640000
                                                                        01650000
           READ PREV-EXTRACT-FILE                                       01660000
              INTO SD177-PRV-EXTRACT-RECORD                             01670001
                 AT END                                                 01680000
                 SET PS-END-OF-PRV-FILE TO TRUE                         01690000
           END-READ.                                                    01700000
                                                                        01710000
      * MOVE KEYS                                                       01720000
           IF  PS-NOT-END-OF-PRV-FILE                                   01730000
               ADD +1                       TO PA-PRV-READ              01740000
               MOVE SD177-PRV-STR-NBR       TO PV-PRV-STR-NBR           01750001
               MOVE SD177-PRV-TRIP-DATE     TO PV-PRV-TRIP-DATE         01760001
               MOVE SD177-PRV-TRAILER-ID    TO PV-PRV-TRAILER-ID        01761001
           END-IF.                                                      01770000
                                                                        01780000
      ******************************************************************01781000
      * DATE-ERROR-ROUTINE.                                             01782000
      ******************************************************************01783000
       Z5000-ERROR-ROUTINE.                                             01784000
                                                                        01785000
           PERFORM 9999-DISPLAY-STATISTICS.                             01786000
                                                                        01786100
           DISPLAY 'PARAGRAPH: ' PV-PARAGRAPH-NAME.                     01786200
           DISPLAY 'ERROR: ' PV-OPERATION-MSG.                          01786300
                                                                        01786400
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         01786500
                                                                        01786600
      ******************************************************************01786700
      **CLOSE FILES                                                     01786800
      ******************************************************************01786900
       9000-WRAP-UP.                                                    01787004
                                                                        01788000
      *    UPDATE THE DELTA COUNT FILE WITH NUMBER OF RECORDS WRITTEN   01789000
           OPEN OUTPUT DELTA-COUNT-FILE.                                01790000
           WRITE DELTA-COUNT-RECORD FROM PA-TTL-WRITE-COUNT.            01791000
           CLOSE DELTA-COUNT-FILE.                                      01792000
                                                                        01793000
           CLOSE CURR-EXTRACT-FILE                                      01794000
                 PREV-EXTRACT-FILE                                      01795000
                 DELTA-FILE.                                            01796000
                                                                        01797000
           PERFORM 9999-DISPLAY-STATISTICS.                             01798000
                                                                        01799000
      ******************************************************************01800000
      **DISPLAY PROGRAM STATS                                           01810000
      ******************************************************************01820000
       9999-DISPLAY-STATISTICS.                                         01830000
                                                                        01840000
           DISPLAY '*************************************************'. 01850000
           DISPLAY '* PROGRAM...........: SDKUT178'.                    01860000
           DISPLAY '* DATE..............: ' DF-ACCEPT-DATE.             01870000
           DISPLAY '* TIME..............: ' DF-ACCEPT-TIME.             01880000
           DISPLAY '*************************************************'. 01890000
           DISPLAY '* CUR INP RECORDS READ: ' PA-CUR-READ.              01900000
           DISPLAY '* PRV INP RECORDS READ: ' PA-PRV-READ.              01910000
           DISPLAY '*************************************************'. 01920000
           DISPLAY '* DELTA PROCESSING...'.                             01930000
           DISPLAY '* - ADD RECORDS COUNT: ' PA-WRITE-ADD-COUNT.        01940000
           DISPLAY '* - UPD RECORDS COUNT: ' PA-WRITE-UPD-COUNT.        01950000
           DISPLAY '* - DLT RECORDS COUNT: ' PA-WRITE-DLT-COUNT.        01960000
           DISPLAY '*************************************************'. 01970000
           DISPLAY '* - TOTAL WRITE COUNT: ' PA-TTL-WRITE-COUNT.        01980000
           DISPLAY '*************************************************'. 01990000
           DISPLAY '* MATCHED RECORDS, NOT NEEDED: ' PA-MATCH-COUNT.    02000000
           DISPLAY '* SKIPPED DUE TO DATE OOB....: ' PA-DATE-OOB-COUNT. 02010000
           DISPLAY '*************************************************'. 02020000
                                                                        02030000
      ******************************************************************02040000
