      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.             TFKRP472.                                00050000
       AUTHOR.                 JACK KRAMER.                             00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            FEBRUARY 1997.                           00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ******************************************************************00110000
      *    THIS PROGRAM PRODUCES A REPORT OF RECEIVED TRANSFERS FOR     00120037
      *  A SPECIFIED PERIOD OF TIME. TRANSFERS ARE PRINTED BY TRANSFER  00130037
      *  STATUS WITHIN STORE NUMBER. STATUS IS EITHER VOIDED, ASSUMPTIVE00131037
      *  OR POSITIVELY RECEIVED, AND POSITIVELY RECEIVED IS             00132037
      *  FURTHER SUBDIVIDED BY DETAIL CHECKED OR NOT DETAIL CHECKED.    00133037
      *****************************************************************         
100404** 10/04/2004 CHANGED BY: RAY GRAF                                        
100404**     EXPAND STORE NUMBER. MAKE DOMAIN COMPLIANT.                        
      *****************************************************************         
                                                                        00150000
      ******************************************************************00160000
       ENVIRONMENT DIVISION.                                            00170000
      ******************************************************************00180000
                                                                        00190000
       CONFIGURATION SECTION.                                           00200000
                                                                        00210000
       SOURCE-COMPUTER.        IBM-3090.                                00220000
       OBJECT-COMPUTER.        IBM-3090.                                00230000
                                                                        00240000
       INPUT-OUTPUT SECTION.                                            00250000
                                                                        00260000
       FILE-CONTROL.                                                    00270000
                                                                                
           SELECT PROCESS-DATES-FILE                                            
               ASSIGN TO INP01.                                                 
                                                                        00300137
           SELECT RECEIVED-XFER-FILE                                    00300237
               ASSIGN TO INP02                                          00300337
               FILE STATUS  IS PV-FILE-STATUS.                                  
                                                                        00280000
           SELECT TRANSFER-REPORT-FILE                                  00290000
               ASSIGN TO RPT01.                                         00300000
      *                                                                 00310000
      ******************************************************************00320000
       DATA DIVISION.                                                   00330000
      ******************************************************************00340000
                                                                        00350000
       FILE SECTION.                                                    00360000
                                                                                
       FD  PROCESS-DATES-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  PROCESS-DATES-RECORD                PIC  X(16).                      
                                                                        00450137
       FD  RECEIVED-XFER-FILE                                           00450237
           RECORDING MODE IS F                                          00450337
           LABEL RECORDS ARE STANDARD                                   00450437
           BLOCK CONTAINS 0 RECORDS.                                    00450537
                                                                        00450637
       01  RECEIVED-XFER-RECORD                PIC  X(80).              00451037
                                                                        00370000
       FD  TRANSFER-REPORT-FILE                                         00380000
           LABEL RECORDS ARE STANDARD                                   00390000
           RECORDING MODE IS F                                          00400000
           BLOCK CONTAINS 0 RECORDS                                     00410000
           RECORD CONTAINS 132 CHARACTERS                               00420000
           DATA RECORD IS TRANSFER-REPORT-RECORD.                       00430000
                                                                        00440000
       01  TRANSFER-REPORT-RECORD PIC X(132).                           00450000
      *                                                                 00460000
       WORKING-STORAGE SECTION.                                         00470000
                                                                        00480000
      *                                                                 00490000
      *  STANDARD REPORT HEADER FOR A 132 COLUMN REPORT                 00500000
           COPY DPWS132O.                                               00510000
      *                                                                 00520000
      *----------------------------------------------------------------*00530000
      *  DATE VALIDATION CALLING PARAMETER LIST                        *00540000
      *----------------------------------------------------------------*00550000
           COPY DPWS500.                                                00560000
                                                                                
      *----------------------------------------------------------------*00550000
      *  RECORD LAYOUT FOR THE PROCESS DATES FILE                               
      *----------------------------------------------------------------*00550000
           COPY TFRD029.                                                        
      *                                                                 00510000
      *----------------------------------------------------------------*00550000
      *  RECORD LAYOUT FOR THE RECEIVED TRANSFERS FILE                  00520000
      *----------------------------------------------------------------*00550000
           COPY TFRD034.                                                00540000
           EJECT                                                        00570000
      *                                                                 00580000
       01  ABEND-CODE                  PIC S9(4)    VALUE ZERO          00590000
                                       COMP SYNC.                       00600000
                                                                        00610000
       01  PS-PROGRAM-SWITCHES.                                         00620000
           05  PS-ITEMS-READ-SW        PIC X(01)    VALUE 'N'.          00630000
               88  PS-ITEMS-READ                    VALUE 'Y'.          00640000
               88  PS-NO-ITEMS-READ                 VALUE 'N'.          00650000
           05  PS-FIRST-STORE-SW       PIC X(01)    VALUE 'Y'.          00630000
               88  PS-FIRST-STORE                   VALUE 'Y'.          00640000
               88  PS-NOT-FIRST-STORE               VALUE 'N'.          00650000
           05  PS-FIRST-TIME-SW        PIC X(01)    VALUE 'Y'.          00660000
               88  PS-FIRST-TIME                    VALUE 'Y'.          00670000
               88  PS-NOT-FIRST-TIME                VALUE 'N'.          00680000
           05  PS-END-OF-RECEIVE-SW    PIC  X(01)   VALUE 'N'.          00720035
               88  PS-END-OF-RECEIVE-FILE           VALUE 'Y'.          00730035
               88  PS-NOT-END-OF-RECEIVE-FILE       VALUE 'N'.          00740035
                                                                        00780000
       01  PC-PROGRAM-CONSTANTS.                                        00790000
           05  PC-PROGRAM-NAME           PIC  X(08)  VALUE  'TFKRP472'. 00800001
           05  PC-CALENDAR-PGM           PIC  X(08)  VALUE  'DPKUT500'. 00810000
           05  PC-TOTAL-LITERAL          PIC  X(06)  VALUE  'TOTALS'.   00820000
           05  PC-GRAND-TOTAL            PIC  X(11)  VALUE              00820000
                                         'GRAND TOTAL'.                 00820000
           05  PC-STORE-LIT              PIC  X(06)  VALUE  'STORE '.   00820000
           05  PC-TRANSFERS              PIC  X(12)  VALUE              00830000
                                         '   TRANSFERS'.                        
           05  PC-VOIDED                 PIC  X(06)  VALUE 'VOIDED'.    00840000
           05  PC-ASSUMPTIVE             PIC  X(10)  VALUE              00840000
                                         'ASSUMPTIVE'.                          
           05  PC-BYPASS-DETAIL-CHK      PIC  X(19)  VALUE              00840000
                                         'BYPASS DETAIL CHECK'.                 
           05  PC-DETAIL-CHECK           PIC  X(14)  VALUE              00840000
                                         'DETAIL CHECKED'.                      
           05  PC-STORE-TOTAL            PIC  X(11)  VALUE              00840000
                                         'STORE TOTAL'.                         
           05  PC-PAGE-MAX               PIC  S9(03) VALUE  +60.        00860000
           05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3      00880000
                                                           COMP-3.      00890000
                                                                        00900000
       01  PV-PROGRAM-VARIABLES.                                        00910000
           05  PV-RETRY-COUNT          PIC S9(4)    VALUE ZERO          00920000
                                       COMP SYNC.                       00930000
           05  PV-PAGE-COUNT           PIC 9(5)    VALUE ZERO.          00940000
           05  PV-TYPE-COUNT           PIC 9(2)    VALUE ZERO.          00940000
           05  PV-ITEMS-READ           PIC 9(8)    VALUE ZERO.          00950000
           05  PV-DATE.                                                         
               10  PV-MONTH            PIC 9(02).                               
               10  FILLER              PIC X(01)   VALUE '/'.                   
               10  PV-DAY              PIC 9(02).                               
               10  FILLER              PIC X(01)   VALUE '/'.                   
               10  PV-CENTURY          PIC 9(02).                               
               10  PV-YEAR             PIC 9(02).                               
           05  PV-FILE-STATUS          PIC  X(02)   VALUE SPACES.               
               88  PV-FILE-SUCCESSFUL               VALUE '00'.                 
               88  PV-FILE-NOT-FOUND                VALUE '23'.                 
           05  PV-FISCAL-START         PIC X(08)   VALUE SPACE.         00960001
           05  PV-FISCAL-END           PIC X(08)   VALUE SPACE.         00970001
           05  PV-FISCAL-MONTH         PIC S9(04)   COMP VALUE ZERO.    00980001
           05  PV-FISCAL-YEAR          PIC S9(04)   COMP VALUE ZERO.    00990001
           05  PV-LINE-COUNT           PIC S9(4)   VALUE ZERO.          01030000
           05  PV-PARAGRAPH-NAME               PIC X(30)   VALUE SPACE. 01040000
           05  PV-DB2-OPERATION                PIC X(30)   VALUE SPACE. 01050000
           05  PV-SUM-QTY              PIC S9(08)    VALUE ZERO COMP-3. 01100000
           05  PV-SUM-PRICE            PIC S9(09)V99 VALUE ZERO COMP-3. 01110000
           05  PV-SUBTOTAL-QTY         PIC S9(08)    VALUE ZERO COMP-3. 01120000
           05  PV-SUBTOTAL-RETAIL      PIC S9(09)V99 VALUE ZERO COMP-3. 01130000
           05  PV-DOC-CMP-QTY          PIC S9(08)    VALUE ZERO COMP-3. 01140001
           05  PV-DOC-CMP-PRICE        PIC S9(09)V99 VALUE ZERO COMP-3. 01150001
           05  PV-DOC-ADJ-QTY          PIC S9(08)    VALUE ZERO COMP-3. 01160001
           05  PV-DOC-ADJ-PRICE        PIC S9(09)V99 VALUE ZERO COMP-3. 01170001
           05  PV-STR-DOCS             PIC S9(08)    VALUE ZERO COMP-3. 01180000
           05  PV-CMP-ASM-QTY          PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-CMP-ASM-RETAIL       PIC S9(09)V99 VALUE ZERO COMP-3. 01200001
           05  PV-CMP-ASM-COUNT        PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-CMP-BYP-QTY          PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-CMP-BYP-RETAIL       PIC S9(09)V99 VALUE ZERO COMP-3. 01200001
           05  PV-CMP-BYP-COUNT        PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-CMP-DTL-QTY          PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-CMP-DTL-RETAIL       PIC S9(09)V99 VALUE ZERO COMP-3. 01200001
           05  PV-CMP-DTL-COUNT        PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-CMP-VD-QTY           PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-CMP-VD-RETAIL        PIC S9(09)V99 VALUE ZERO COMP-3. 01200001
           05  PV-CMP-VD-COUNT         PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-CMP-QTY              PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-CMP-RETAIL           PIC S9(09)V99 VALUE ZERO COMP-3. 01200001
           05  PV-CMP-COUNT            PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-STR-ASM-QTY          PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-STR-ASM-RETAIL       PIC S9(09)V99 VALUE ZERO COMP-3. 01200001
           05  PV-STR-ASM-COUNT        PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-STR-BYP-QTY          PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-STR-BYP-RETAIL       PIC S9(09)V99 VALUE ZERO COMP-3. 01200001
           05  PV-STR-BYP-COUNT        PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-STR-DTL-QTY          PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-STR-DTL-RETAIL       PIC S9(09)V99 VALUE ZERO COMP-3. 01200001
           05  PV-STR-DTL-COUNT        PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-STR-VD-QTY           PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-STR-VD-RETAIL        PIC S9(09)V99 VALUE ZERO COMP-3. 01200001
           05  PV-STR-VD-COUNT         PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-STR-QTY              PIC S9(08)    VALUE ZERO COMP-3. 01190001
           05  PV-STR-RETAIL           PIC S9(09)V99 VALUE ZERO COMP-3. 01200001
           05  PV-STR-COUNT            PIC S9(08)    VALUE ZERO COMP-3. 01190001
       01  HV-HOLD-VARIABLES.                                           01410001
           05  HV-STORE-NBR            PIC 9(04)    VALUE ZERO.         01420001
           05  HV-XFER-STATUS          PIC X(02)    VALUE SPACE.        01440000
                                                                        01540000
       01  BLANK-LINE                  PIC X(132)   VALUE SPACES.       01660000
                                                                        01670000
       01  END-OF-REPORT-LINE.                                          01680000
           05  FILLER                  PIC X(52)    VALUE SPACES.       01690000
           05  FILLER                  PIC X(26)    VALUE               01700000
               '   *** END OF REPORT ***'.                              01710000
           05  FILLER                  PIC X(54)    VALUE SPACES.       01720000
                                                                        01730000
       01  NO-DATA-LINE.                                                01740000
           05  FILLER                  PIC X(35)    VALUE SPACES.       01750000
           05  FILLER                  PIC X(45)    VALUE               01760000
               '****  NO DATA TO PROCESS FOR THIS REPORT  ***'.         01770000
                                                                        01780000
       01  CD-CURRENT-DATE.                                             01790000
           05  CD-CURRENT-YEAR         PIC 9(2)     VALUE ZERO.         01800000
           05  CD-CURRENT-MONTH        PIC 9(2)     VALUE ZERO.         01810000
           05  CD-CURRENT-DAY          PIC 9(2)     VALUE ZERO.         01820000
                                                                        01830000
       01  CT-CURRENT-TIME.                                             01840000
           05  CT-CURRENT-HOUR         PIC 9(2)     VALUE ZERO.         01850000
           05  CT-CURRENT-MINUTE       PIC 9(2)     VALUE ZERO.         01860000
           05  FILLER                  PIC 9(4)     VALUE ZERO.         01870000
                                                                        01880000
       01  HL2-HEADING-LINE-2.                                          01890000
           05  FILLER                  PIC X(55)    VALUE SPACES.       01900001
           05  FILLER                  PIC X(23)    VALUE               01930001
               'RECEIVE TRANSFER REPORT'.                               01940001
           05  FILLER                  PIC X(52)    VALUE SPACES.       01950000
                                                                        01960000
       01  HL3-HEADING-LINE-3.                                          01970001
           05  FILLER                  PIC X(49)  VALUE SPACE.          01980001
           05  FILLER                  PIC X(11)    VALUE               01990001
               'FOR PERIOD'.                                            02000001
           05  HL3-FROM-DATE           PIC X(10)  VALUE SPACES.         02010001
           05  FILLER                  PIC X(04)  VALUE ' TO '.         02020001
           05  HL3-TO-DATE             PIC X(10)  VALUE SPACES.         02030001
                                                                        02040000
       01  HL4-HEADING-LINE-4.                                          02050001
           05  FILLER                  PIC X(07)  VALUE ' STORE '.      02060001
           05  HL4-STORE-NBR           PIC 9999.                        02070001
           05  FILLER                  PIC X(01)  VALUE SPACE.          02080001
           05  HL4-STORE-NAME          PIC X(25)  VALUE SPACE.          02090001
                                                                        02100000
       01  HL5-HEADING-LINE-5.                                          02110001
           05  FILLER                  PIC X(03)  VALUE SPACE.          02120001
           05  FILLER                  PIC X(39)  VALUE 'TYPE OF'.      02120001
           05  FILLER                  PIC X(15)  VALUE 'TOTAL'.        02130001
           05  FILLER                  PIC X(11)  VALUE 'TOTAL'.        02140001
           05  HL5-DATE                PIC X(08)  VALUE 'DATE'.         02150001
                                                                        02180001
       01  HL6-HEADING-LINE-6.                                          02190001
           05  FILLER                  PIC X(03)  VALUE SPACE.          02120001
           05  FILLER                  PIC X(21)  VALUE 'RECEIPT'.      02120001
           05  HL6-TRANSFERS           PIC X(18)  VALUE 'TRANSFER NBR'. 02210001
           05  FILLER                  PIC X(14)  VALUE 'UNITS'.        02220001
           05  FILLER                  PIC X(11)  VALUE 'RETAIL'.       02230001
           05  HL6-RECEIVED            PIC X(12)  VALUE 'RECEIVED'.     02240001
           05  HL6-RECEIVED-BY         PIC X(12)  VALUE 'RECEIVED BY'.  02250001
                                                                        02040000
       01  THL4-TOTAL-HEADING-LINE-4.                                   02050001
           05  FILLER                  PIC X(25)  VALUE                 02060001
                                       ' COMPANY GRAND TOTALS'.         02070001
                                                                        02420000
       01  DL1-DETAIL-LINE-1.                                           02430000
           05  FILLER                  PIC X(03)  VALUE SPACE.          02440001
           05  DL1-STATUS              PIC X(19).                       02450000
100404*    05  FILLER                  PIC X(03)  VALUE SPACE.          02440001
100404     05  FILLER                  PIC X(02)  VALUE SPACE.          02440001
           05  DL1-TRANSFER-NBR.                                        02450000
100404*        10  DL1-FR-STR-NBR      PIC 9(03).                       02450000
100404         10  DL1-FR-STR-NBR      PIC 9(04).                       02450000
               10  DL1-DASH            PIC X(01)  VALUE SPACES.         02460000
               10  DL1-XFER-SEQ-NBR    PIC 9(06).                       02470000
           05  DL1-TRANSFERS REDEFINES DL1-TRANSFER-NBR                 02470000
                                       PIC Z(10).                       02470000
           05  FILLER                  PIC X(01)  VALUE SPACES.         02480001
           05  DL1-QTY                 PIC ZZZ,ZZZ,ZZZ.                 02490001
           05  FILLER                  PIC X(01)  VALUE SPACE.          02500001
           05  DL1-RETAIL              PIC ZZZ,ZZZ,ZZZ.99               02510001
                                           BLANK WHEN ZERO.             02510001
           05  FILLER                  PIC X(04)  VALUE SPACE.          02520001
           05  DL1-DATE-RECEIVED.                                       02530001
               10  DL1-DATE-MM         PIC X(02).                       02530001
               10  DL1-SLASH1          PIC X(01).                       02530001
               10  DL1-DATE-DD         PIC X(02).                       02530001
               10  DL1-SLASH2          PIC X(01).                       02530001
               10  DL1-DATE-CC         PIC X(02).                       02530001
               10  DL1-DATE-YY         PIC X(02).                       02530001
           05  FILLER                  PIC X(03)  VALUE SPACE.          02520001
           05  DL1-RECV-BY-1ST-INIT    PIC X(01).                       02530001
           05  FILLER                  PIC X(01)  VALUE SPACE.          02520001
           05  DL1-RECV-BY-LAST-NAME   PIC X(20).                       02530001
      *---------------------------------------------------------------*         
100404*    DB2 AREA FOR XST_LOC TABLE                                 *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
100404          INCLUDE XST00001                                                
           END-EXEC.                                                            
                                                                        02820000
           EXEC SQL                                                     02830000
               INCLUDE SQLCA                                            02840000
           END-EXEC.                                                    02850000
      *                                                                 02860001
      *  EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING           02870001
      *  SQL CODES FOR DISPLAY                                          02880001
      *                                                                 02890001
           COPY SQLCA2.                                                 02900001
                                                                        03610001
      ******************************************************************03620000
       PROCEDURE DIVISION.                                              03630000
      ******************************************************************03640000
       0000-MAINLINE.                                                   03650000
           PERFORM 1000-INITIALIZE.                                     03660000
           PERFORM 2000-PROCESS-RECEIVE-FILE.                           03680000
                                                                        03790000
           IF PS-NO-ITEMS-READ                                          03800035
               PERFORM 8000-PRINT-HEADINGS                              03810035
               WRITE TRANSFER-REPORT-RECORD                             03820035
                   FROM NO-DATA-LINE                                    03830035
                   AFTER ADVANCING 2                                    03840035
           ELSE                                                         03850035
               PERFORM 8100-PRINT-TOTALS-HEADINGS                       03810035
               PERFORM 8200-GRAND-TOTALS                                03810035
           END-IF.                                                      03850035
                                                                        03860035
           WRITE TRANSFER-REPORT-RECORD                                 03870035
               FROM END-OF-REPORT-LINE                                  03880035
               AFTER ADVANCING 2.                                       03890035
           CLOSE TRANSFER-REPORT-FILE                                   03900000
                 RECEIVED-XFER-FILE                                     04000001
                 PROCESS-DATES-FILE.                                    04000001
           STOP RUN.                                                    03910000
                                                                        03920000
      ******************************************************************03930000
      *  SET UP FOR PROGRAM PROCESSING BY OPENING ALL FILES,            03940000
      *  FILLING THE FIELDS OF THE STANDARD REPORT HEADER,              03950000
      *  READING FIRST RECORD, PRINT HEADINGS.                          03960000
      ******************************************************************03970000
                                                                        03980000
       1000-INITIALIZE.                                                 03990000
           OPEN OUTPUT TRANSFER-REPORT-FILE                             04000001
                INPUT  RECEIVED-XFER-FILE                               04000001
                       PROCESS-DATES-FILE.                              04000001
           ACCEPT CD-CURRENT-DATE FROM DATE.                            04010000
           MOVE CD-CURRENT-YEAR TO DP132O-RUN-YEAR.                     04020000
           MOVE CD-CURRENT-MONTH TO DP132O-RUN-MONTH.                   04040000
           MOVE CD-CURRENT-DAY TO DP132O-RUN-DAY.                       04060000
           MOVE 100 TO PV-LINE-COUNT.                                   04080000
                                                                        04090000
           ACCEPT CT-CURRENT-TIME FROM TIME.                            04100000
           MOVE CT-CURRENT-HOUR TO DP132O-RUN-HOUR.                     04110000
           MOVE CT-CURRENT-MINUTE TO DP132O-RUN-MINUTE.                 04120000
           PERFORM 7000-READ-PROCESS-DATES-FILE.                        01880203
           DISPLAY 'TF029-PROCESS-DATES-RECORD = '                      01880303
               TF029-PROCESS-DATES-RECORD.                              01880403
           MOVE TF029-PROCESS-START-MONTH    TO PV-MONTH.                       
           MOVE TF029-PROCESS-START-DAY      TO PV-DAY.                         
           MOVE TF029-PROCESS-START-YEAR     TO PV-YEAR.                        
           MOVE TF029-PROCESS-START-CENTURY  TO PV-CENTURY.                     
           MOVE PV-DATE                      TO HL3-FROM-DATE.                  
           MOVE TF029-PROCESS-END-MONTH      TO PV-MONTH.                       
           MOVE TF029-PROCESS-END-DAY        TO PV-DAY.                         
           MOVE TF029-PROCESS-END-YEAR       TO PV-YEAR.                        
           MOVE TF029-PROCESS-END-CENTURY    TO PV-CENTURY.                     
           MOVE PV-DATE                      TO HL3-TO-DATE.                    
           MOVE PC-PROGRAM-NAME              TO DP132O-PROGRAM-NAME.    04130001
           MOVE 1 TO DP132O-REPORT-NUMBER.                              04140000
                                                                        04710000
      ******************************************************************05220035
      * CONTROL THE PROCESSING OF THE RECEIVED TRANSFERS FILE           05230035
      ******************************************************************05240035
       2000-PROCESS-RECEIVE-FILE.                                       05250035
           PERFORM                                                      05260035
               UNTIL (PS-END-OF-RECEIVE-FILE)                           05270035
                                                                        05280035
               PERFORM 2100-READ-RECEIVE-FILE                           05290035
                                                                        05300035
               IF  PS-NOT-END-OF-RECEIVE-FILE                           05310035
                   ADD +1                    TO PV-ITEMS-READ           05320035
                   PERFORM 3000-PROCESS-DATA                            05330035
               END-IF                                                   05340030
           END-PERFORM.                                                 05350035
           IF PV-ITEMS-READ > 0                                         05360035
               SET PS-ITEMS-READ             TO TRUE                    05360035
               PERFORM 5100-STORE-TOTALS                                05380035
           END-IF.                                                      05400035
       EJECT                                                            05410035
      *----------------------------------------------------------------*03250105
      *  READ THE RECEIVED TRANSFER FILE.                              *03250205
      *----------------------------------------------------------------*03250305
       2100-READ-RECEIVE-FILE.                                          01880203
           READ RECEIVED-XFER-FILE                                      03250505
             INTO TF034-RECEIVED-XFER-RECORD                            03250605
               AT END                                                           
                 SET PS-END-OF-RECEIVE-FILE          TO TRUE                    
           END-READ.                                                    03251005
      ******************************************************************06560035
      * PROCESS THE REPORT DATA                                         06570035
      ******************************************************************06580035
       3000-PROCESS-DATA.                                               06590035
           IF TF034-RECV-STR-NBR       NOT = HV-STORE-NBR               06700035
               PERFORM 5000-NEW-STORE                                   06710035
           ELSE                                                                 
               IF TF034-XFER-STATUS    NOT = HV-XFER-STATUS                     
                   PERFORM 6000-NEW-STATUS                                      
               END-IF                                                           
           END-IF.                                                      06720035
           MOVE SPACES                TO DL1-DETAIL-LINE-1.             07160001
           IF PV-LINE-COUNT > PC-PAGE-MAX                               07280035
               PERFORM 8000-PRINT-HEADINGS                              07290035
           END-IF.                                                      07300035
           IF PS-FIRST-TIME                                             06600035
               EVALUATE TRUE                                                    
                   WHEN TF034-AR                                                
                       MOVE PC-ASSUMPTIVE         TO DL1-STATUS         07350001
                   WHEN TF034-PR-AND-NO-DTL-CHK                                 
                       MOVE PC-BYPASS-DETAIL-CHK  TO DL1-STATUS         07350001
                   WHEN TF034-PR-AND-DTL-CHK                                    
                       MOVE PC-DETAIL-CHECK       TO DL1-STATUS         07350001
                   WHEN TF034-VOIDED                                            
                       MOVE PC-VOIDED             TO DL1-STATUS         07350001
               END-EVALUATE                                                     
               SET PS-NOT-FIRST-TIME  TO TRUE                           06670035
           END-IF.                                                      06680035
           MOVE TF034-FROM-STR-NBR    TO DL1-FR-STR-NBR.                07160001
           MOVE TF034-XFER-SEQ-NBR    TO DL1-XFER-SEQ-NBR.              07160001
           MOVE TF034-XFER-TOT-UNITS  TO DL1-QTY.                       07170001
           MOVE TF034-XFER-TOT-RETAIL TO DL1-RETAIL.                    07180001
           MOVE TF034-XFER-RECV-MM    TO DL1-DATE-MM.                   07180001
           MOVE TF034-XFER-RECV-DD    TO DL1-DATE-DD.                   07180001
           MOVE TF034-XFER-RECV-CC    TO DL1-DATE-CC.                   07180001
           MOVE TF034-XFER-RECV-YY    TO DL1-DATE-YY.                   07180001
           MOVE '/'                   TO DL1-SLASH1                     07180001
                                         DL1-SLASH2.                    07180001
           MOVE '-'                   TO DL1-DASH                       07180001
           IF TF034-PR-AND-NO-DTL-CHK                                           
               MOVE TF034-RECV-BY-1ST-INIT-NAME                         07180001
                                      TO DL1-RECV-BY-1ST-INIT           07180001
               MOVE TF034-RECV-BY-LAST-NAME                             07180001
                                      TO DL1-RECV-BY-LAST-NAME          07180001
           END-IF.                                                              
           WRITE TRANSFER-REPORT-RECORD                                 07310035
               FROM DL1-DETAIL-LINE-1                                   07320035
               AFTER ADVANCING 1.                                       07330035
           ADD 1                      TO PV-LINE-COUNT.                 07340035
           EVALUATE TRUE                                                        
               WHEN TF034-AR                                                    
                   ADD TF034-XFER-TOT-UNITS   TO PV-STR-ASM-QTY         07350001
                   ADD TF034-XFER-TOT-RETAIL  TO PV-STR-ASM-RETAIL      07360001
                   ADD +1                     TO PV-STR-ASM-COUNT       07360001
               WHEN TF034-PR-AND-NO-DTL-CHK                                     
                   ADD TF034-XFER-TOT-UNITS   TO PV-STR-BYP-QTY         07350001
                   ADD TF034-XFER-TOT-RETAIL  TO PV-STR-BYP-RETAIL      07360001
                   ADD +1                     TO PV-STR-BYP-COUNT       07360001
               WHEN TF034-PR-AND-DTL-CHK                                        
                   ADD TF034-XFER-TOT-UNITS   TO PV-STR-DTL-QTY         07350001
                   ADD TF034-XFER-TOT-RETAIL  TO PV-STR-DTL-RETAIL      07360001
                   ADD +1                     TO PV-STR-DTL-COUNT       07360001
               WHEN TF034-VOIDED                                                
                   ADD TF034-XFER-TOT-UNITS   TO PV-STR-VD-QTY          07350001
                   ADD TF034-XFER-TOT-RETAIL  TO PV-STR-VD-RETAIL       07360001
                   ADD +1                     TO PV-STR-VD-COUNT        07360001
           END-EVALUATE.                                                        
           ADD TF034-XFER-TOT-UNITS   TO PV-STR-QTY.                    07350001
           ADD TF034-XFER-TOT-RETAIL  TO PV-STR-RETAIL.                 07360001
           ADD +1                     TO PV-STR-COUNT.                  07360001
      ******************************************************************06840035
      *  RECEIVE STORE NUMBER BREAK.                                    06850035
      ******************************************************************06860035
       5000-NEW-STORE.                                                  06870035
           IF PS-NOT-FIRST-STORE                                                
               PERFORM 5100-STORE-TOTALS                                06890001
           ELSE                                                                 
               SET PS-NOT-FIRST-STORE    TO TRUE                                
           END-IF.                                                              
           MOVE TF034-RECV-STR-NBR       TO HV-STORE-NBR                07080035
                                            HL4-STORE-NBR               07090035
100404                                      XST00001-LOC-NBR.           07090035
           PERFORM 5200-READ-STORE.                                             
100404     MOVE XST00001-LOC-10-ABBR-NM  TO HL4-STORE-NAME.             07100035
           MOVE TF034-XFER-STATUS        TO HV-XFER-STATUS.                     
      ******************************************************************06840035
      *  RECEIVE STORE NUMBER BREAK.                                    06850035
      ******************************************************************06860035
       5100-STORE-TOTALS.                                               06870035
           IF PV-STR-COUNT > 1                                                  
               IF PV-LINE-COUNT > PC-PAGE-MAX - 6                       07280035
                   PERFORM 8000-PRINT-HEADINGS                          07290035
               END-IF                                                   07300035
               MOVE HV-STORE-NBR          TO HL4-STORE-NBR              07080035
               MOVE PC-TOTAL-LITERAL      TO HL4-STORE-NAME             07100035
               WRITE TRANSFER-REPORT-RECORD                             07830035
                   FROM HL4-HEADING-LINE-4                              07840035
                   AFTER ADVANCING 2                                    07850035
               MOVE SPACES                TO DL1-DETAIL-LINE-1          06900001
               MOVE ZEROES                TO PV-TYPE-COUNT              06900001
               IF PV-STR-ASM-COUNT   > ZERO                             07170001
                   MOVE PC-ASSUMPTIVE         TO DL1-STATUS             07100035
                   MOVE PV-STR-ASM-QTY        TO DL1-QTY                07170001
                   MOVE PV-STR-ASM-RETAIL     TO DL1-RETAIL             07180001
                   MOVE PV-STR-ASM-COUNT      TO DL1-TRANSFERS          07180001
                   WRITE TRANSFER-REPORT-RECORD                         06940035
                       FROM DL1-DETAIL-LINE-1                           06950035
                       AFTER ADVANCING 1                                06960035
                   ADD +1                     TO PV-TYPE-COUNT          06900001
               END-IF                                                   06960035
               IF PV-STR-BYP-COUNT   > ZERO                             07170001
                   MOVE PC-BYPASS-DETAIL-CHK  TO DL1-STATUS             07100035
                   MOVE PV-STR-BYP-QTY        TO DL1-QTY                07170001
                   MOVE PV-STR-BYP-RETAIL     TO DL1-RETAIL             07180001
                   MOVE PV-STR-BYP-COUNT      TO DL1-TRANSFERS          07180001
                   WRITE TRANSFER-REPORT-RECORD                         06940035
                       FROM DL1-DETAIL-LINE-1                           06950035
                       AFTER ADVANCING 1                                06960035
                   ADD +1                     TO PV-TYPE-COUNT          06900001
               END-IF                                                   06960035
               IF PV-STR-DTL-COUNT   > ZERO                             07170001
                   MOVE PC-DETAIL-CHECK       TO DL1-STATUS             07100035
                   MOVE PV-STR-DTL-QTY        TO DL1-QTY                07170001
                   MOVE PV-STR-DTL-RETAIL     TO DL1-RETAIL             07180001
                   MOVE PV-STR-DTL-COUNT      TO DL1-TRANSFERS          07180001
                   WRITE TRANSFER-REPORT-RECORD                         06940035
                       FROM DL1-DETAIL-LINE-1                           06950035
                       AFTER ADVANCING 1                                06960035
                   ADD +1                     TO PV-TYPE-COUNT          06900001
               END-IF                                                   06960035
               IF PV-STR-VD-COUNT    > ZERO                             07170001
                   MOVE PC-VOIDED             TO DL1-STATUS             07100035
                   MOVE PV-STR-VD-QTY         TO DL1-QTY                07170001
                   MOVE PV-STR-VD-RETAIL      TO DL1-RETAIL             07180001
                   MOVE PV-STR-VD-COUNT       TO DL1-TRANSFERS          07180001
                   WRITE TRANSFER-REPORT-RECORD                         06940035
                       FROM DL1-DETAIL-LINE-1                           06950035
                       AFTER ADVANCING 1                                06960035
                   ADD +1                     TO PV-TYPE-COUNT          06900001
               END-IF                                                   06960035
               IF PV-TYPE-COUNT > 1                                     06900001
                   MOVE PC-STORE-TOTAL        TO DL1-STATUS             07100035
                   MOVE PV-STR-QTY            TO DL1-QTY                07170001
                   MOVE PV-STR-RETAIL         TO DL1-RETAIL             07180001
                   MOVE PV-STR-COUNT          TO DL1-TRANSFERS          07180001
                   WRITE TRANSFER-REPORT-RECORD                         06940035
                       FROM DL1-DETAIL-LINE-1                           06950035
                       AFTER ADVANCING 2                                06960035
               END-IF                                                   06960035
           END-IF.                                                      06960035
           MOVE +90                   TO PV-LINE-COUNT.                 06970035
           ADD PV-STR-ASM-QTY         TO PV-CMP-ASM-QTY.                06980001
           ADD PV-STR-ASM-RETAIL      TO PV-CMP-ASM-RETAIL.             06990001
           ADD PV-STR-ASM-COUNT       TO PV-CMP-ASM-COUNT.              06990001
           ADD PV-STR-DTL-QTY         TO PV-CMP-DTL-QTY.                06980001
           ADD PV-STR-DTL-RETAIL      TO PV-CMP-DTL-RETAIL.             06990001
           ADD PV-STR-DTL-COUNT       TO PV-CMP-DTL-COUNT.              06990001
           ADD PV-STR-BYP-QTY         TO PV-CMP-BYP-QTY.                06980001
           ADD PV-STR-BYP-RETAIL      TO PV-CMP-BYP-RETAIL.             06990001
           ADD PV-STR-BYP-COUNT       TO PV-CMP-BYP-COUNT.              06990001
           ADD PV-STR-VD-QTY          TO PV-CMP-VD-QTY.                 06980001
           ADD PV-STR-VD-RETAIL       TO PV-CMP-VD-RETAIL.              06990001
           ADD PV-STR-VD-COUNT        TO PV-CMP-VD-COUNT.               06990001
           ADD PV-STR-QTY             TO PV-CMP-QTY.                    06980001
           ADD PV-STR-RETAIL          TO PV-CMP-RETAIL.                 06990001
           ADD PV-STR-COUNT           TO PV-CMP-COUNT.                  06990001
           MOVE ZEROES                TO PV-STR-ASM-QTY                 07030001
                                         PV-STR-ASM-RETAIL              07040001
                                         PV-STR-ASM-COUNT               07060001
                                         PV-STR-DTL-QTY                 07050001
                                         PV-STR-DTL-RETAIL              07060001
                                         PV-STR-DTL-COUNT               07060001
                                         PV-STR-BYP-QTY                 07050001
                                         PV-STR-BYP-RETAIL              07060001
                                         PV-STR-BYP-COUNT               07060001
                                         PV-STR-VD-QTY                  07030001
                                         PV-STR-VD-RETAIL               07040001
                                         PV-STR-VD-COUNT                07060001
                                         PV-STR-QTY                     07050001
                                         PV-STR-RETAIL                  07060001
                                         PV-STR-COUNT.                  07060001
      *----------------------------------------------------------------*        
      *    GET THE STORE NAME.                                                  
      *----------------------------------------------------------------*        
       5200-READ-STORE.                                                         
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                          
                    OR SQLCODE = ZERO                                           
                    OR SQLCODE = +100                                           
               EXEC SQL                                                         
100404             SELECT LOC_10_ABBR_NM                                        
100404                 INTO :XST00001-LOC-10-ABBR-NM                            
100404                 FROM XST_LOC                                             
100404                 WHERE LOC_NBR = :XST00001-LOC-NBR                        
               END-EXEC                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
100404                 MOVE 'INVALID'     TO XST00001-LOC-10-ABBR-NM            
                   WHEN OTHER                                                   
                       MOVE '5200-READ-STORE'     TO PV-PARAGRAPH-NAME          
                       MOVE 'READ THE STORE TABLE' TO PV-DB2-OPERATION          
                       PERFORM 9100-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
               MOVE '5200-READ-STORE'     TO PV-PARAGRAPH-NAME                  
               MOVE 'READ THE STORE TABLE' TO PV-DB2-OPERATION                  
               PERFORM 9100-SQL-ERROR                                           
           END-IF.                                                              
      ******************************************************************07120035
      *  DOCUMENT BREAK.                                                07130035
      ******************************************************************07140035
       6000-NEW-STATUS.                                                 07150035
           WRITE TRANSFER-REPORT-RECORD                                 07310035
               FROM BLANK-LINE                                          07320035
               AFTER ADVANCING 1.                                       07330035
           ADD 1                      TO PV-LINE-COUNT.                 07340035
           SET PS-FIRST-TIME          TO TRUE.                          06670035
           MOVE TF034-XFER-STATUS     TO HV-XFER-STATUS.                        
      *----------------------------------------------------------------*03250105
      *  READ THE PROCESS BEGIN AND END DATES.                         *03250205
      *----------------------------------------------------------------*03250305
       7000-READ-PROCESS-DATES-FILE.                                    01880203
           READ PROCESS-DATES-FILE                                      03250505
               INTO TF029-PROCESS-DATES-RECORD                          03250605
           END-READ.                                                    03251005
      ******************************************************************07660035
      *  PRINT REPORT HEADINGS                                          07670035
      ******************************************************************07680035
                                                                        07690035
       8000-PRINT-HEADINGS.                                             07700035
           ADD 1 TO PV-PAGE-COUNT.                                      07710035
           MOVE 8             TO PV-LINE-COUNT.                         07720035
           MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    07730035
           SET PS-FIRST-TIME          TO TRUE.                          06670035
           WRITE TRANSFER-REPORT-RECORD                                 07740035
               FROM DP132O-STANDRD-HEADER-132-COLS                      07750035
               AFTER ADVANCING PAGE.                                    07760035
           WRITE TRANSFER-REPORT-RECORD                                 07770035
               FROM HL2-HEADING-LINE-2                                  07780035
               AFTER ADVANCING 1.                                       07790035
           WRITE TRANSFER-REPORT-RECORD                                 07800035
               FROM HL3-HEADING-LINE-3                                  07810035
               AFTER ADVANCING 1.                                       07820035
           WRITE TRANSFER-REPORT-RECORD                                 07830035
               FROM HL4-HEADING-LINE-4                                  07840035
               AFTER ADVANCING 1.                                       07850035
           WRITE TRANSFER-REPORT-RECORD                                 07860035
               FROM HL5-HEADING-LINE-5                                  07870035
               AFTER ADVANCING 2.                                       07880035
           WRITE TRANSFER-REPORT-RECORD                                 07890035
               FROM HL6-HEADING-LINE-6                                  07900035
               AFTER ADVANCING 1.                                       07910035
           WRITE TRANSFER-REPORT-RECORD                                 07920035
               FROM BLANK-LINE                                          07930035
               AFTER ADVANCING 1.                                       07940035
      ******************************************************************07660035
      *  PRINT GRAND TOTAL PAGE REPORT HEADINGS                         07670035
      ******************************************************************07680035
                                                                        07690035
       8100-PRINT-TOTALS-HEADINGS.                                      07700035
           ADD 1                     TO PV-PAGE-COUNT.                  07710035
           MOVE 8                    TO PV-LINE-COUNT.                  07720035
           MOVE SPACES               TO HL5-DATE                        07720035
                                        HL6-RECEIVED                    07720035
                                        HL6-RECEIVED-BY.                07720035
           MOVE PV-PAGE-COUNT        TO DP132O-PAGE-NUMBER.             07730035
           MOVE PC-TRANSFERS         TO HL6-TRANSFERS.                  07730035
           WRITE TRANSFER-REPORT-RECORD                                 07740035
               FROM DP132O-STANDRD-HEADER-132-COLS                      07750035
               AFTER ADVANCING PAGE.                                    07760035
           WRITE TRANSFER-REPORT-RECORD                                 07770035
               FROM HL2-HEADING-LINE-2                                  07780035
               AFTER ADVANCING 1.                                       07790035
           WRITE TRANSFER-REPORT-RECORD                                 07800035
               FROM HL3-HEADING-LINE-3                                  07810035
               AFTER ADVANCING 1.                                       07820035
           WRITE TRANSFER-REPORT-RECORD                                 07830035
               FROM THL4-TOTAL-HEADING-LINE-4                           07840035
               AFTER ADVANCING 1.                                       07850035
           WRITE TRANSFER-REPORT-RECORD                                 07860035
               FROM HL5-HEADING-LINE-5                                  07870035
               AFTER ADVANCING 2.                                       07880035
           WRITE TRANSFER-REPORT-RECORD                                 07890035
               FROM HL6-HEADING-LINE-6                                  07900035
               AFTER ADVANCING 1.                                       07910035
      ******************************************************************07460035
      *  GRAND TOTALS                                                   07470035
      ******************************************************************07480035
       8200-GRAND-TOTALS.                                               07490035
           MOVE SPACES                TO DL1-DETAIL-LINE-1              06900001
           MOVE PC-ASSUMPTIVE         TO DL1-STATUS                     07100035
           MOVE PV-CMP-ASM-QTY        TO DL1-QTY                        07170001
           MOVE PV-CMP-ASM-RETAIL     TO DL1-RETAIL                     07180001
           MOVE PV-CMP-ASM-COUNT      TO DL1-TRANSFERS                  07180001
           WRITE TRANSFER-REPORT-RECORD                                 06940035
               FROM DL1-DETAIL-LINE-1                                   06950035
               AFTER ADVANCING 2                                        06960035
           MOVE PC-BYPASS-DETAIL-CHK  TO DL1-STATUS                     07100035
           MOVE PV-CMP-BYP-QTY        TO DL1-QTY                        07170001
           MOVE PV-CMP-BYP-RETAIL     TO DL1-RETAIL                     07180001
           MOVE PV-CMP-BYP-COUNT      TO DL1-TRANSFERS                  07180001
           WRITE TRANSFER-REPORT-RECORD                                 06940035
               FROM DL1-DETAIL-LINE-1                                   06950035
               AFTER ADVANCING 2                                        06960035
           MOVE PC-DETAIL-CHECK       TO DL1-STATUS                     07100035
           MOVE PV-CMP-DTL-QTY        TO DL1-QTY                        07170001
           MOVE PV-CMP-DTL-RETAIL     TO DL1-RETAIL                     07180001
           MOVE PV-CMP-DTL-COUNT      TO DL1-TRANSFERS                  07180001
           WRITE TRANSFER-REPORT-RECORD                                 06940035
               FROM DL1-DETAIL-LINE-1                                   06950035
               AFTER ADVANCING 2                                        06960035
           MOVE PC-VOIDED             TO DL1-STATUS                     07100035
           MOVE PV-CMP-VD-QTY         TO DL1-QTY                        07170001
           MOVE PV-CMP-VD-RETAIL      TO DL1-RETAIL                     07180001
           MOVE PV-CMP-VD-COUNT       TO DL1-TRANSFERS                  07180001
           WRITE TRANSFER-REPORT-RECORD                                 06940035
               FROM DL1-DETAIL-LINE-1                                   06950035
               AFTER ADVANCING 2                                        06960035
           MOVE PC-GRAND-TOTAL        TO DL1-STATUS                     07100035
           MOVE PV-CMP-QTY            TO DL1-QTY                        07170001
           MOVE PV-CMP-RETAIL         TO DL1-RETAIL                     07180001
           MOVE PV-CMP-COUNT          TO DL1-TRANSFERS                  07180001
           WRITE TRANSFER-REPORT-RECORD                                 06940035
           FROM DL1-DETAIL-LINE-1                                       06950035
               AFTER ADVANCING 3.                                       06960035
      ******************************************************************07950000
      *  FORMAT A DB2 ERROR MESSAGE, RELEASE ALL LOCKS, AND ABEND THE  *07960000
      *  PROGRAM.                                                      *07970000
      ******************************************************************07980000
       9100-SQL-ERROR.                                                  07990000
           MOVE SQLCODE TO SQLCODE2.                                    08000000
           MOVE SQLERRD(3) TO SQLERRD3.                                 08010000
           DISPLAY '** ABEND **'.                                       08020000
           DISPLAY 'TFKRP472 - SQL ERROR'.                              08030000
           DISPLAY '       SQLCODE = ' SQLCODE2.                        08040000
           DISPLAY '      SQLWARN0 = ' SQLWARN0.                        08050000
           DISPLAY '      SQLWARN1 = ' SQLWARN1.                        08060000
           DISPLAY '      SQLWARN2 = ' SQLWARN2.                        08070000
           DISPLAY '      SQLWARN3 = ' SQLWARN3.                        08080000
           DISPLAY '      SQLWARN4 = ' SQLWARN4.                        08090000
           DISPLAY '      SQLWARN5 = ' SQLWARN5.                        08100035
           DISPLAY '      SQLWARN6 = ' SQLWARN6.                        08110000
           DISPLAY '      SQLWARN7 = ' SQLWARN7.                        08120000
           DISPLAY 'ROWS PROCESSED = ' SQLERRD3.                        08130000
           DISPLAY                    '** PARAGRAPH ** = '              08140000
                                       PV-PARAGRAPH-NAME.               08150000
           DISPLAY                    '** OPERATION ** = '              08160000
                                       PV-DB2-OPERATION.                08170000
                                                                        08180000
           EXEC SQL                                                     08190000
               COMMIT                                                   08200000
           END-EXEC.                                                    08210000
                                                                        08220000
           MOVE +4013 TO ABEND-CODE.                                    08230000
           CALL 'ILBOABN0' USING ABEND-CODE.                            08240000
                                                                        08250000
