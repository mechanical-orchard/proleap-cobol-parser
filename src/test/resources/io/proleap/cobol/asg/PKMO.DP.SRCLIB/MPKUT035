000100******************************************************************12/11/95
000200 IDENTIFICATION DIVISION.                                         MPKUT035
000300******************************************************************   LV001
000400                                                                  MPKUT035
000500 PROGRAM-ID.             MPKUT035.                                MPKUT035
000600 AUTHOR.                 MARY SARDINA.                            MPKUT035
000700 INSTALLATION.           KOHLS.                                   MPKUT035
000800 DATE-WRITTEN            OCTOBER 1995.                            MPKUT035
000900 DATE-COMPILED.                                                   MPKUT035
001000                                                                  MPKUT035
001100******************************************************************MPKUT035
001200*   THIS PROGRAM CREATES AN EXTRACT FILE THAT CONTAINS UPC'S THAT MPKUT035
001300*   HAVE NOT YET BEEN CERTIFIED OR UPC'S THAT SOME CERTIFICATION  MPKUT035
001400*   HAS BEEN ATTEMPTED.                                           MPKUT035
001500******************************************************************MPKUT035
001600 ENVIRONMENT DIVISION.                                            MPKUT035
001700                                                                  MPKUT035
001800 CONFIGURATION SECTION.                                           MPKUT035
001900                                                                  MPKUT035
002000 SOURCE-COMPUTER.        IBM-3090.                                MPKUT035
002100 OBJECT-COMPUTER.        IBM-3090.                                MPKUT035
002200                                                                  MPKUT035
002300 INPUT-OUTPUT SECTION.                                            MPKUT035
002400                                                                  MPKUT035
002500 FILE-CONTROL.                                                    MPKUT035
002600                                                                  MPKUT035
002700     SELECT CERT-REQ-FILE-OUT                                     MPKUT035
002800         ASSIGN TO OUT01.                                         MPKUT035
002900                                                                  MPKUT035
003000******************************************************************MPKUT035
003100 DATA DIVISION.                                                   MPKUT035
003200******************************************************************MPKUT035
003300                                                                  MPKUT035
003400 FILE SECTION.                                                    MPKUT035
003500                                                                  MPKUT035
003600 FD  CERT-REQ-FILE-OUT                                            MPKUT035
003700     RECORDING MODE IS F                                          MPKUT035
003800     BLOCK CONTAINS 0 RECORDS                                     MPKUT035
003900     LABEL RECORDS ARE STANDARD.                                  MPKUT035
004000                                                                  MPKUT035
004100 01  CERT-REQ-FILE-OUT-REC               PIC X(150).              MPKUT035
004200                                                                  MPKUT035
004300******************************************************************MPKUT035
004400 WORKING-STORAGE SECTION.                                         MPKUT035
004500******************************************************************MPKUT035
004600                                                                  MPKUT035
004700 01  ABEND-CODE                          PIC S9(04)  VALUE +0     MPKUT035
004800                                                     COMP SYNC.   MPKUT035
004900 01  WS-OUTPUT-CERT-RECORD.                                       MPKUT035
005000     05  WS-OPER-UNT-ID          PIC  9(04)    VALUE ZEROES.      MPKUT035
005100     05  WS-MDSE-PRC-AREA-CDE    PIC  X(04)    VALUE SPACES.      MPKUT035
005200     05  WS-MDSE-LNE-ITM-NBR     PIC  9(15)    VALUE ZERO.        MPKUT035
005300                                                                  MPKUT035
005400     05  WS-ACT-CDE              PIC  X(02)    VALUE SPACES.      MPKUT035
005500     05  WS-PO-NBR               PIC  9(09)    VALUE ZERO.        MPKUT035
005600     05  WS-PO-LNE-NBR           PIC  9(09)    VALUE ZERO.        MPKUT035
005700                                                                  MPKUT035
005800     05  WS-REC-SEQ-NBR          PIC  9(09)    VALUE ZERO.        MPKUT035
005900                                                                  MPKUT035
006000     05  WS-ENT-NAME-DESC        PIC  X(30)    VALUE SPACES.      MPKUT035
006100     05  WS-UPC-NBR              PIC  9(15)    VALUE ZERO.        MPKUT035
006200                                                                  MPKUT035
006300*----------------------------------------------------------------*        
006400* RENAMED THE VARIBLE WS-SEQ-NMBR TO WS-SKU-NMBR AND INCREASED   *        
006500* THE LENGTH FROM 3 TO 8. REDUCED THE FILLER SIZE FROM X(34) TO  *        
006600* X(29).                                            WR # 230ZZ   *        
006700*----------------------------------------------------------------*        
006900     05  WS-MERCH-DEPT-NMBR      PIC  X(03)    VALUE SPACES.              
007000     05  WS-MERCH-CLASS-NMBR     PIC  X(02)    VALUE SPACES.              
007100     05  FILLER                  PIC  X(03)    VALUE SPACES.              
007200     05  WS-CERT-IND             PIC  X(01)    VALUE SPACES.      MPKUT035
007300     05  WS-REQ-FOR-CERT-CDE     PIC  X(01)    VALUE SPACES.      MPKUT035
007400     05  WS-PACK-BY-ST-IND       PIC  X(01)    VALUE SPACES.      MPKUT035
007500     05  WS-REC-TMST.                                             MPKUT035
007600         10  WS-TIMESTAMP-CENT   PIC  9(02)    VALUE ZEROES.      MPKUT035
007700         10  WS-TIMESTAMP-YEAR   PIC  9(02)    VALUE ZEROES.      MPKUT035
007800         10  WS-TIMESTAMP-MONTH  PIC  9(02)    VALUE ZEROES.      MPKUT035
007900         10  WS-TIMESTAMP-DAY    PIC  9(02)    VALUE ZEROES.      MPKUT035
007910     05  WS-SKU-NMBR             PIC  X(08)    VALUE SPACES.      MPKUT035
008000     05  FILLER                  PIC  X(21)    VALUE SPACES.      MPKUT035
008100*----------------------------------------------------------------*        
008200* CHANGES MADE FOR WR # 230ZZ ENDS HERE.                         *        
008300*----------------------------------------------------------------*        
008400*                                                                 MPKUT035
008500 01  PC-PROGRAM-CONSTANTS.                                        MPKUT035
008600     05  PC-MAX-RETRIES                  PIC S9(04)  VALUE +3     MPKUT035
008700                                                     COMP SYNC.   MPKUT035
008800     05  PC-CURRENT-PROGRAM-NAME         PIC  X(08)  VALUE        MPKUT035
008900                                                     'MPKUT035'.  MPKUT035
009000     05  PC-NO-VNDR-NAME-FOUND           PIC  X(15)  VALUE        MPKUT035
009100         'NO VENDOR FOUND'.                                       MPKUT035
009200*                                                                 MPKUT035
009300 01  PV-PROGRAM-VARIABLES.                                        MPKUT035
009400     05  PV-OPEN-COUNTER                 PIC S9(04)  VALUE ZERO   MPKUT035
009500                                                     COMP SYNC.   MPKUT035
009600     05  PV-CLOSE-COUNTER                PIC S9(04)  VALUE ZERO   MPKUT035
009700                                                     COMP SYNC.   MPKUT035
009800     05  PV-RETRY-COUNTER                PIC S9(04)  VALUE ZERO   MPKUT035
009900                                                     COMP SYNC.   MPKUT035
010000     05  PV-SELECT-COUNTER               PIC S9(04)  VALUE ZERO   MPKUT035
010100                                                     COMP SYNC.   MPKUT035
010200     05  PV-UNIT-CNT                     PIC S9(09)  VALUE ZERO   MPKUT035
010300                                                     COMP SYNC.   MPKUT035
010400     05  PV-TRECDIS-ITM-CNT              PIC S9(09)  VALUE ZERO   MPKUT035
010500                                                     COMP SYNC.   MPKUT035
010600     05  PV-CERT-REQ-WRITTEN             PIC  9(07)  VALUE ZERO   MPKUT035
010700                                                     COMP-3.      MPKUT035
010800     05  PV-PARAGRAPH-NAME               PIC  X(30)  VALUE SPACES.MPKUT035
010900     05  PV-DB2-OPERATION-MESSAGE-1      PIC  X(79)  VALUE SPACES.MPKUT035
011000     05  PV-DB2-OPERATION-MESSAGE-2      PIC  X(79)  VALUE SPACES.MPKUT035
011100     05  PV-DB2-OPERATION-MESSAGE-3      PIC  X(79)  VALUE SPACES.MPKUT035
011200     05  PV-DB2-OPERATION-MESSAGE-4      PIC  X(79)  VALUE SPACES.MPKUT035
011300     05  PV-DB2-TABLE-NAME-1             PIC  X(18)  VALUE SPACES.MPKUT035
011400     05  PV-DB2-TABLE-NAME-2             PIC  X(18)  VALUE SPACES.MPKUT035
011500     05  PV-DB2-TABLE-NAME-3             PIC  X(18)  VALUE SPACES.MPKUT035
011600     05  PV-ERROR-MESSAGE-1              PIC  X(60)  VALUE SPACES.MPKUT035
011700     05  PV-ERROR-MESSAGE-2              PIC  X(60)  VALUE SPACES.MPKUT035
011800     05  PV-ERROR-MESSAGE-3              PIC  X(60)  VALUE SPACES.MPKUT035
011900                                                                  MPKUT035
012000     05  PV-TIMESTAMP                    PIC  X(26)  VALUE SPACES.MPKUT035
012100     05  PV-TIMESTAMP-R                  REDEFINES                MPKUT035
012200         PV-TIMESTAMP.                                            MPKUT035
012300         10  PV-TIMESTAMP-CENTURY        PIC  9(02).              MPKUT035
012400         10  PV-TIMESTAMP-YEAR           PIC  9(02).              MPKUT035
012500         10  FILLER                      PIC  X(01).              MPKUT035
012600         10  PV-TIMESTAMP-MONTH          PIC  9(02).              MPKUT035
012700         10  FILLER                      PIC  X(01).              MPKUT035
012800         10  PV-TIMESTAMP-DAY            PIC  9(02).              MPKUT035
012900         10  FILLER                      PIC  X(01).              MPKUT035
013000         10  PV-TIMESTAMP-HOUR           PIC  9(02).              MPKUT035
013100         10  FILLER                      PIC  X(01).              MPKUT035
013200         10  PV-TIMESTAMP-MINUTE         PIC  9(02).              MPKUT035
013300         10  FILLER                      PIC  X(01).              MPKUT035
013400         10  PV-TIMESTAMP-SECONDS        PIC  9(02).              MPKUT035
013500         10  FILLER                      PIC  X(01).              MPKUT035
013600         10  PV-TIMESTAMP-MILLISECONDS   PIC  9(06).              MPKUT035
013700                                                                  MPKUT035
013800     05  PV-DATE-ISO.                                             MPKUT035
013900         10  PV-DATE-ISO-CENTURY         PIC  9(02).              MPKUT035
014000         10  PV-DATE-ISO-YEAR            PIC  9(02).              MPKUT035
014100         10  PV-DATE-ISO-MONTH           PIC  9(02).              MPKUT035
014200         10  PV-DATE-ISO-DAY             PIC  9(02).              MPKUT035
014300                                                                  MPKUT035
014400     05  PV-DATE-OUT.                                             MPKUT035
014500         10  PV-DATE-OUT-YEAR            PIC  9(02).              MPKUT035
014600         10  PV-DATE-OUT-MONTH           PIC  9(02).              MPKUT035
014700         10  PV-DATE-OUT-DAY             PIC  9(02).              MPKUT035
014800                                                                  MPKUT035
014900 01  PS-PROGRAM-SWITCHES.                                         MPKUT035
015000     05  PS-END-OF-RECCER-CSR-SW         PIC X(01)   VALUE 'N'.   MPKUT035
015100         88  PS-END-OF-RECCER                        VALUE 'Y'.   MPKUT035
015200         88  PS-NOT-END-OF-RECCER                    VALUE 'N'.   MPKUT035
015300     05  PS-END-OF-TRECITM-CSR-SW        PIC X(01)   VALUE 'N'.   MPKUT035
015400         88  PS-END-OF-TRECITM-CSR                   VALUE 'Y'.   MPKUT035
015500         88  PS-NOT-END-OF-TRECITM-CSR               VALUE 'N'.   MPKUT035
015600     05  PS-VNDR-NAME-SW                 PIC X(01)   VALUE 'N'.   MPKUT035
015700         88  PS-VNDR-NAME-FOUND                      VALUE 'Y'.   MPKUT035
015800         88  PS-NO-VNDR-NAME-FOUND                   VALUE 'N'.   MPKUT035
015900*                                                                 MPKUT035
016000*                                                                 MPKUT035
016100******************************************************************MPKUT035
016200*  DB2 FORMATTED MESSAGE AREA                                     MPKUT035
016300******************************************************************MPKUT035
016400     COPY DPWS004.                                                MPKUT035
016500*                                                                 MPKUT035
016600******************************************************************MPKUT035
016700***  WORK FIELDS FOR THE CERTIFICATION EXTRACT FILE.              MPKUT035
016800******************************************************************MPKUT035
016900     COPY MPRD051.                                                MPKUT035
017000*                                                                 MPKUT035
040057******************************************************************        
040058*  COPYBOOK FOR LOGISTICS DOMAIN ITEM ACCESS MODULE  WR # 230ZZ           
040059******************************************************************        
040060     COPY XLWS001.                                                        
040070*                                                                 MPKUT035
040080******************************************************************MPKUT035
040090*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE UPC CERTIFICATION  MPKUT035
040100*  TABLE.                                                         MPKUT035
040200******************************************************************MPKUT035
040300     EXEC SQL                                                     MPKUT035
040400          INCLUDE TUPCCER                                         MPKUT035
040500     END-EXEC.                                                    MPKUT035
040600*                                                                 MPKUT035
040700*                                                                 MPKUT035
040800******************************************************************MPKUT035
040900*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE RECEIVER TABLE     MPKUT035
041000******************************************************************MPKUT035
041100     EXEC SQL                                                     MPKUT035
041200          INCLUDE TRECEIV                                         MPKUT035
041300     END-EXEC.                                                    MPKUT035
041400*                                                                 MPKUT035
041500*                                                                 MPKUT035
041600*                                                                 MPKUT035
041700******************************************************************MPKUT035
041800*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE PURCHASE ORDER     MPKUT035
041900*  HEADER TABLE                                                   MPKUT035
042000******************************************************************MPKUT035
042100     EXEC SQL                                                     MPKUT035
042200          INCLUDE TPURCHS                                         MPKUT035
042300     END-EXEC.                                                    MPKUT035
042400*                                                                 MPKUT035
042500******************************************************************MPKUT035
042600*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE ENTITY TABLE       MPKUT035
042700******************************************************************MPKUT035
042800     EXEC SQL                                                     MPKUT035
042900          INCLUDE TENTNME                                         MPKUT035
043000     END-EXEC.                                                    MPKUT035
043100*                                                                 MPKUT035
043200*                                                                 MPKUT035
043300******************************************************************MPKUT035
043400*  COMMUNICATIONS AREA FOR DB2                                    MPKUT035
043500******************************************************************MPKUT035
043600     EXEC SQL                                                     MPKUT035
043700          INCLUDE SQLCA                                           MPKUT035
043800     END-EXEC.                                                    MPKUT035
043900*                                                                 MPKUT035
044000******************************************************************MPKUT035
044100*  CURSOR TO SELECT INFORMATION FROM THE RECEIVER TABLE AND       MPKUT035
044200*  THE UPC CERTIFICATION TABLE                                    MPKUT035
044300******************************************************************MPKUT035
044400*                                                                 MPKUT035
044500     EXEC SQL                                                     MPKUT035
044600          DECLARE RECCER_CSR CURSOR FOR                           MPKUT035
044700           SELECT A.PACK_BY_ST_IND                                MPKUT035
044800                 ,A.REC_TMST                                      MPKUT035
044900                 ,A.OPER_UNT_ID                                   MPKUT035
045000                 ,B.MDSE_LNE_ITM_NBR                              MPKUT035
045100                 ,B.ACT_CDE                                       MPKUT035
045200                 ,B.OPER_UNT_ID                                   MPKUT035
045300                 ,B.PO_NBR                                        MPKUT035
045400                 ,B.REC_SEQ_NBR                                   MPKUT035
045500                 ,B.PO_LNE_NBR                                    MPKUT035
045600                 ,B.UPC_NBR                                       MPKUT035
045700                 ,B.MDSE_PRC_AREA_CDE                             MPKUT035
045800                 ,B.REQ_FOR_CERT_CDE                              MPKUT035
045900           FROM   TRECEIV A                                       MPKUT035
046000                 ,TUPCCER B                                       MPKUT035
046100          WHERE  B.ACT_CDE         IN ('10','11','20','30','60')  MPKUT035
046200            AND  B.PO_NBR = A.ORD_NBR                             MPKUT035
046300            AND  A.REC_SEQ_NBR = B.REC_SEQ_NBR                    MPKUT035
046400            AND  A.STATUS_CDE         = 'VE'                      MPKUT035
046500     END-EXEC.                                                    MPKUT035
046600*                                                                 MPKUT035
046700 PROCEDURE DIVISION.                                              MPKUT035
046800                                                                  MPKUT035
046900******************************************************************MPKUT035
047000*  0000-INITIAL-ROUTINE  -  MAIN LINE OF THE PROGRAM PERFORMS THE MPKUT035
047100*  INITIALIZATION ROUTINE AND CONTROLS THE PROCESSING OF THE PGM. MPKUT035
047200******************************************************************MPKUT035
047300                                                                  MPKUT035
047400 0000-MAINLINE.                                                   MPKUT035
047500                                                                  MPKUT035
047600     PERFORM 0100-HOUSEKEEPING.                                   MPKUT035
047700                                                                  MPKUT035
047800     PERFORM 1000-OPEN-RECCER-CSR.                                MPKUT035
047900                                                                  MPKUT035
048000     PERFORM 1200-FETCH-RECCER-CSR.                               MPKUT035
048100                                                                  MPKUT035
048200     PERFORM 1100-PROCESS-RECCER-RECS                             MPKUT035
048300         UNTIL PS-END-OF-RECCER.                                  MPKUT035
048400                                                                  MPKUT035
048500     PERFORM 1300-CLOSE-RECCER-CSR.                               MPKUT035
048600                                                                  MPKUT035
048700     CLOSE CERT-REQ-FILE-OUT.                                     MPKUT035
048800                                                                  MPKUT035
048900     DISPLAY '** CERTIFICATION REQUEST RECORDS WRITTEN: '         MPKUT035
049000      PV-CERT-REQ-WRITTEN.                                        MPKUT035
049100                                                                  MPKUT035
049200     STOP RUN.                                                    MPKUT035
049300                                                                  MPKUT035
049400 0100-HOUSEKEEPING.                                               MPKUT035
049500                                                                  MPKUT035
049600     OPEN OUTPUT CERT-REQ-FILE-OUT.                               MPKUT035
049700                                                                  MPKUT035
049800 1000-OPEN-RECCER-CSR.                                            MPKUT035
049900******************************************************************MPKUT035
050000*  OPEN THE RECCER-CSR CURSOR.                                    MPKUT035
050100******************************************************************MPKUT035
050200     PERFORM WITH TEST AFTER                                      MPKUT035
050300       VARYING PV-OPEN-COUNTER FROM 1 BY 1                        MPKUT035
050400         UNTIL (PV-OPEN-COUNTER > PC-MAX-RETRIES                  MPKUT035
050500                OR                                                MPKUT035
050600                SQLCODE = ZERO)                                   MPKUT035
050700                                                                  MPKUT035
050800         EXEC SQL                                                 MPKUT035
050900              OPEN RECCER_CSR                                     MPKUT035
051000         END-EXEC                                                 MPKUT035
051100                                                                  MPKUT035
051200         EVALUATE TRUE                                            MPKUT035
051300             WHEN SQLCODE = -904                                  MPKUT035
051400             WHEN SQLCODE = -911                                  MPKUT035
051500                 CONTINUE                                         MPKUT035
051600             WHEN SQLWARN0 NOT = SPACES                           MPKUT035
051700             WHEN SQLCODE < ZERO                                  MPKUT035
051800             WHEN SQLCODE > ZERO                                  MPKUT035
051900                 MOVE '1000-OPEN-RECCER-CSR    '                  MPKUT035
052000                                 TO PV-PARAGRAPH-NAME             MPKUT035
052100                 MOVE 'ATTEMPTING TO OPEN THE RECCER CSR       '  MPKUT035
052200                                 TO PV-DB2-OPERATION-MESSAGE-1    MPKUT035
052300                 MOVE 'TRECEIV'  TO PV-DB2-TABLE-NAME-1           MPKUT035
052400                 MOVE 'TUPCCER'  TO PV-DB2-TABLE-NAME-2           MPKUT035
052500                 PERFORM 9100-DB2-ABEND                           MPKUT035
052600             WHEN SQLCODE = ZERO                                  MPKUT035
052700                 CONTINUE                                         MPKUT035
052800         END-EVALUATE                                             MPKUT035
052900     END-PERFORM.                                                 MPKUT035
053000                                                                  MPKUT035
053100     IF PV-OPEN-COUNTER > PC-MAX-RETRIES                          MPKUT035
053200         MOVE '1000-OPEN-RECCER-CSR'                              MPKUT035
053300                                 TO PV-PARAGRAPH-NAME             MPKUT035
053400         MOVE 'MAX RETRIES EXCEEDED OPENING RECCER CSR            MPKUT035
053500-             'CURSOR FOR THE RECEIV AND UPCCER TABLE'            MPKUT035
053600                                 TO PV-DB2-OPERATION-MESSAGE-1    MPKUT035
053700         MOVE 'TRECEIV'          TO PV-DB2-TABLE-NAME-1           MPKUT035
053800         MOVE 'TUPCCER'          TO PV-DB2-TABLE-NAME-2           MPKUT035
053900         PERFORM 9100-DB2-ABEND                                   MPKUT035
054000     END-IF.                                                      MPKUT035
054100                                                                  MPKUT035
054200 1100-PROCESS-RECCER-RECS.                                        MPKUT035
054300******************************************************************MPKUT035
054400*  PROCESS THE RECEIVERS UNTIL END OF FILE IS REACHED.            MPKUT035
054500******************************************************************MPKUT035
054600     MOVE RECEIV-REC-TMST            TO PV-TIMESTAMP.             MPKUT035
054700     MOVE PV-TIMESTAMP-CENTURY       TO PV-DATE-ISO-CENTURY       MPKUT035
054800                                        WS-TIMESTAMP-CENT.        MPKUT035
054900     MOVE PV-TIMESTAMP-YEAR          TO PV-DATE-ISO-YEAR          MPKUT035
055000                                        WS-TIMESTAMP-YEAR.        MPKUT035
055100     MOVE PV-TIMESTAMP-MONTH         TO PV-DATE-ISO-MONTH         MPKUT035
055200                                        WS-TIMESTAMP-MONTH.       MPKUT035
055300     MOVE PV-TIMESTAMP-DAY           TO PV-DATE-ISO-DAY           MPKUT035
055400                                        WS-TIMESTAMP-DAY.         MPKUT035
055500*                                                                 MPKUT035
055600     MOVE RECEIV-OPER-UNT-ID         TO WS-OPER-UNT-ID.           MPKUT035
055700     MOVE RECEIV-PACK-BY-ST-IND      TO WS-PACK-BY-ST-IND.        MPKUT035
055800     MOVE UPCCER-MDSE-LNE-ITM-NBR    TO WS-MDSE-LNE-ITM-NBR.      MPKUT035
055900     MOVE UPCCER-ACT-CDE             TO WS-ACT-CDE.               MPKUT035
056000     MOVE UPCCER-PO-NBR              TO WS-PO-NBR.                MPKUT035
056100     MOVE UPCCER-REC-SEQ-NBR         TO WS-REC-SEQ-NBR.           MPKUT035
056200     MOVE UPCCER-PO-LNE-NBR          TO WS-PO-LNE-NBR.            MPKUT035
056300     MOVE UPCCER-UPC-NBR             TO WS-UPC-NBR.               MPKUT035
056400     MOVE UPCCER-MDSE-PRC-AREA-CDE   TO WS-MDSE-PRC-AREA-CDE.     MPKUT035
056500     MOVE UPCCER-REQ-FOR-CERT-CDE    TO WS-REQ-FOR-CERT-CDE.      MPKUT035
056600*                                                                 MPKUT035
056700     PERFORM 1400-CALL-XLKUT-DOMAIN-PGM                           MPKUT035
056800*                                                                 MPKUT035
056900*                                                                 MPKUT035
057000     PERFORM 1600-GET-IN-HSE-VNDR-NAME.                           MPKUT035
057100*                                                                 MPKUT035
057200     PERFORM 8000-WRITE-EXTRACT-FILE.                             MPKUT035
057300*                                                                 MPKUT035
057400     PERFORM 1200-FETCH-RECCER-CSR.                               MPKUT035
057500*                                                                 MPKUT035
057600 1200-FETCH-RECCER-CSR.                                           MPKUT035
057700******************************************************************MPKUT035
057800*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.        *MPKUT035
057900******************************************************************MPKUT035
058000     INITIALIZE DCLTRECEIV.                                       MPKUT035
058100     INITIALIZE DCLTUPCCER.                                       MPKUT035
058200                                                                  MPKUT035
058300     EXEC SQL                                                     MPKUT035
058400          FETCH RECCER_CSR                                        MPKUT035
058500           INTO :RECEIV-PACK-BY-ST-IND                            MPKUT035
058600              , :RECEIV-REC-TMST                                  MPKUT035
058700              , :RECEIV-OPER-UNT-ID                               MPKUT035
058800              , :UPCCER-MDSE-LNE-ITM-NBR                          MPKUT035
058900              , :UPCCER-ACT-CDE                                   MPKUT035
059000              , :UPCCER-OPER-UNT-ID                               MPKUT035
059100              , :UPCCER-PO-NBR                                    MPKUT035
059200              , :UPCCER-REC-SEQ-NBR                               MPKUT035
059300              , :UPCCER-PO-LNE-NBR                                MPKUT035
059400              , :UPCCER-UPC-NBR                                   MPKUT035
059500              , :UPCCER-MDSE-PRC-AREA-CDE                         MPKUT035
059600              , :UPCCER-REQ-FOR-CERT-CDE                          MPKUT035
059700     END-EXEC.                                                    MPKUT035
059800                                                                  MPKUT035
059900     EVALUATE TRUE                                                MPKUT035
060000         WHEN SQLCODE = ZERO                                      MPKUT035
060100             CONTINUE                                             MPKUT035
060200         WHEN SQLCODE = +100                                      MPKUT035
060300             SET PS-END-OF-RECCER TO TRUE                         MPKUT035
060400         WHEN SQLWARN0 NOT = SPACES                               MPKUT035
060500         WHEN SQLCODE < ZERO                                      MPKUT035
060600         WHEN SQLCODE > ZERO                                      MPKUT035
060700             MOVE '1200-FETCH-RECCER-CSR'                         MPKUT035
060800                                 TO PV-PARAGRAPH-NAME             MPKUT035
060900             MOVE 'ATTEMPTING TO FETCH A ROW FROM THE             MPKUT035
061000-                 'RECCER CSR'                                    MPKUT035
061100                                 TO PV-DB2-OPERATION-MESSAGE-1    MPKUT035
061200             MOVE 'UPCCER'      TO PV-DB2-TABLE-NAME-1            MPKUT035
061300             MOVE 'RECEIV'      TO PV-DB2-TABLE-NAME-2            MPKUT035
061400             PERFORM 9100-DB2-ABEND                               MPKUT035
061500     END-EVALUATE.                                                MPKUT035
061600                                                                  MPKUT035
061700 1300-CLOSE-RECCER-CSR.                                           MPKUT035
061800******************************************************************MPKUT035
061900*  CLOSE THE RECCER-CSR CURSOR.                                   MPKUT035
062000******************************************************************MPKUT035
062100     PERFORM WITH TEST AFTER                                      MPKUT035
062200       VARYING PV-CLOSE-COUNTER FROM 1 BY 1                       MPKUT035
062300         UNTIL (PV-CLOSE-COUNTER > PC-MAX-RETRIES                 MPKUT035
062400                OR                                                MPKUT035
062500                SQLCODE = ZERO)                                   MPKUT035
062600                                                                  MPKUT035
062700         EXEC SQL                                                 MPKUT035
062800              CLOSE RECCER_CSR                                    MPKUT035
062900         END-EXEC                                                 MPKUT035
063000                                                                  MPKUT035
063100         EVALUATE TRUE                                            MPKUT035
063200             WHEN SQLCODE = -904                                  MPKUT035
063300             WHEN SQLCODE = -911                                  MPKUT035
063400                 CONTINUE                                         MPKUT035
063500             WHEN SQLWARN0 NOT = SPACES                           MPKUT035
063600             WHEN SQLCODE < ZERO                                  MPKUT035
063700             WHEN SQLCODE > ZERO                                  MPKUT035
063800                 MOVE '1300-CLOSE-RECCER-CSR    '                 MPKUT035
063900                                 TO PV-PARAGRAPH-NAME             MPKUT035
064000                 MOVE 'ATTEMPTING TO CLOSE THE RECCER CSR'        MPKUT035
064100                                 TO PV-DB2-OPERATION-MESSAGE-1    MPKUT035
064200                 MOVE 'TRECEIV'  TO PV-DB2-TABLE-NAME-1           MPKUT035
064300                 MOVE 'TUPCCER'  TO PV-DB2-TABLE-NAME-2           MPKUT035
064400                 PERFORM 9100-DB2-ABEND                           MPKUT035
064500             WHEN SQLCODE = ZERO                                  MPKUT035
064600                 CONTINUE                                         MPKUT035
064700         END-EVALUATE                                             MPKUT035
064800     END-PERFORM.                                                 MPKUT035
064900                                                                  MPKUT035
065000     IF PV-CLOSE-COUNTER > PC-MAX-RETRIES                         MPKUT035
065100         MOVE '1300-CLOSE-RECCER-CSR'                             MPKUT035
065200                                 TO PV-PARAGRAPH-NAME             MPKUT035
065300         MOVE 'MAX RETRIES EXCEEDED CLOSING THE RECCER            MPKUT035
065400-             'CURSOR FOR THE RECEIVE UPCCER TABLE'               MPKUT035
065500                                 TO PV-DB2-OPERATION-MESSAGE-1    MPKUT035
065600         MOVE 'TRECEIV'          TO PV-DB2-TABLE-NAME-1           MPKUT035
065700         MOVE 'TUPCCER'          TO PV-DB2-TABLE-NAME-2           MPKUT035
065800         PERFORM 9100-DB2-ABEND                                   MPKUT035
065900     END-IF.                                                      MPKUT035
065901                                                                  MPKUT035
065902*----------------------------------------------------------------*        
065903*ADDED CODE TO CALL LOGISTICS DOMAIN ITEM ACCESS MODULE WR # 230ZZ        
065904*----------------------------------------------------------------*        
065905 1400-CALL-XLKUT-DOMAIN-PGM.                                      MPKUT035
065906                                                                  MPKUT035
224700            INITIALIZE XL001-ITEM-COMMAREA                                
224701            MOVE UPCCER-MDSE-LNE-ITM-NBR                                  
224702                                  TO XL001-KEY-ITM-NBR                    
224703                                                                          
224704            SET  XL001-BY-ITEM                                            
224705                 XL001-SELECT-SKU                                         
224706                 XL001-SELECT-VND-STYL                                    
224707                 XL001-SELECT-UPC TO TRUE                                 
224708                                                                          
224709            CALL XL001-VSM-PGM USING  XL001-ITEM-COMMAREA                 
224710                                                                          
224711            EVALUATE TRUE                                                 
224712                WHEN XL001-SKU-SQLCODE = +0                               
376308                WHEN XL001-SKU-SQLCODE = +100                             
376309                WHEN XL001-SKU-SQLCODE = -305                             
376310                      MOVE XL001-SKU-NBR                                  
376311                                       TO  WS-SKU-NMBR                    
376312                      PERFORM 1405-SELECT-VND-STYL                        
376313                WHEN XL001-SKU-CONTENTION                                 
376314                      MOVE '7020-SELECT-SKU-BY-ITM'                       
376315                                       TO PV-PARAGRAPH-NAME               
376316                      MOVE 'SKU CONTENTION'                               
376317                                     TO PV-DB2-OPERATION-MESSAGE-1        
376318                      MOVE 'XLV_SKU '  TO PV-DB2-TABLE-NAME-1             
376319                      PERFORM 9100-DB2-ABEND                              
376320                WHEN OTHER                                                
376321                      MOVE '7020-SELECT-SKU-BY-ITM'                       
376322                                       TO PV-PARAGRAPH-NAME               
376323                      MOVE 'INVALID RETURN'                               
376324                                     TO PV-DB2-OPERATION-MESSAGE-1        
376325                      MOVE 'XLV_SKU '  TO PV-DB2-TABLE-NAME-1             
376326                      PERFORM 9100-DB2-ABEND                              
376327            END-EVALUATE.                                                 
376328  1405-SELECT-VND-STYL.                                                   
376329            EVALUATE TRUE                                                 
376330                WHEN XL001-VND-STYL-SQLCODE = +0                          
376331                WHEN XL001-VND-STYL-SQLCODE = +100                        
376332                WHEN XL001-VND-STYL-SQLCODE = -305                        
376333                      MOVE XL001-DEPT-NBR-3CH                             
376334                                       TO  WS-MERCH-DEPT-NMBR             
376335                      MOVE XL001-SUB-CL-NBR-2CH                           
376336                                       TO  WS-MERCH-CLASS-NMBR            
376337                      PERFORM 1410-SELECT-UPC                             
376338                WHEN XL001-VND-STYL-CONTENTION                            
376339                      MOVE '7100-SELECT-VND-STYL'                         
376340                                       TO PV-PARAGRAPH-NAME               
376341                      MOVE 'VND-STYL-CONTENTION'                          
376342                                     TO PV-DB2-OPERATION-MESSAGE-1        
376343                      MOVE 'XLV_VND_STYL'   TO PV-DB2-TABLE-NAME-1        
376344                      PERFORM 9100-DB2-ABEND                              
376345                WHEN OTHER                                                
376346                      MOVE '7100-SELECT-VND-STYL'                         
376347                                       TO PV-PARAGRAPH-NAME               
376348                      MOVE 'INVALID RETURN'                               
376349                                     TO PV-DB2-OPERATION-MESSAGE-1        
376350                      MOVE 'XLV_VND_STYLE'  TO PV-DB2-TABLE-NAME-1        
376351                      PERFORM 9100-DB2-ABEND                              
376352            END-EVALUATE.                                                 
376353  1410-SELECT-UPC.                                                        
376354            EVALUATE TRUE                                                 
376355                WHEN XL001-UPC-SQLCODE = +0                               
376356                WHEN XL001-UPC-SQLCODE = +100                             
376357                WHEN XL001-UPC-SQLCODE = -305                             
376358                      MOVE XL001-CERTD-IND                                
376359                                       TO  WS-CERT-IND                    
376360                WHEN XL001-VND-STYL-CONTENTION                            
376361                      MOVE '7300-SELECT-UPC'                              
376362                                       TO PV-PARAGRAPH-NAME               
376363                      MOVE 'UPC-CONTENTION'                               
376364                                     TO PV-DB2-OPERATION-MESSAGE-1        
376365                      MOVE 'XLV_UPC'        TO PV-DB2-TABLE-NAME-1        
376366                      PERFORM 9100-DB2-ABEND                              
376367                WHEN OTHER                                                
376368                      MOVE '7300-SELECT-UPC'                              
376369                                       TO PV-PARAGRAPH-NAME               
376370                      MOVE 'INVALID RETURN'                               
376380                                     TO PV-DB2-OPERATION-MESSAGE-1        
376381                      MOVE 'XLV_UPC'        TO PV-DB2-TABLE-NAME-1        
376382                      PERFORM 9100-DB2-ABEND                              
376383            END-EVALUATE.                                                 
376384*----------------------------------------------------------------*        
376385* CHANGES MADE FOR WR # 230ZZ ENDS HERE.                         *        
376386*----------------------------------------------------------------*        
376387*                                                                 MPKUT035
376388*1500-EXIT.                                                       MPKUT035
376389                                                                  MPKUT035
376390 1600-GET-IN-HSE-VNDR-NAME.                                       MPKUT035
376400******************************************************************MPKUT035
376500*  GET THE IN HOUSE VENDOR NAME FROM THE VENDOR DATABASE.         MPKUT035
376600******************************************************************MPKUT035
376700                                                                  MPKUT035
376800     EXEC SQL                                                     MPKUT035
376900         SELECT B.ENT_NAME_DESC                                   MPKUT035
377000             INTO :ENTNME-ENT-NAME-DESC                           MPKUT035
377100         FROM TPURCHS A,                                          MPKUT035
377200              TENTNME B                                           MPKUT035
377300         WHERE A.VER_STATUS_CDE = 'A'                             MPKUT035
377400           AND A.PO_NBR  =  :UPCCER-PO-NBR                        MPKUT035
377500           AND A.ENT_ID  =  B.ENT_ID                              MPKUT035
377600           AND B.TYPE_CDE = '01'                                  MPKUT035
377700     END-EXEC.                                                    MPKUT035
377800     EVALUATE TRUE                                                MPKUT035
377900         WHEN SQLCODE = ZERO                                      MPKUT035
378000             MOVE ENTNME-ENT-NAME-DESC TO                         MPKUT035
378100                                   WS-ENT-NAME-DESC               MPKUT035
378200         WHEN SQLCODE = +100                                      MPKUT035
378300              MOVE PC-NO-VNDR-NAME-FOUND                          MPKUT035
378400                                 TO WS-ENT-NAME-DESC              MPKUT035
378500         WHEN SQLWARN0 NOT = SPACES                               MPKUT035
378600         WHEN SQLCODE < ZERO                                      MPKUT035
378700         WHEN SQLCODE > ZERO                                      MPKUT035
378800             MOVE '2030-GET-INHOUSE VENDOR'                       MPKUT035
378900                                 TO PV-PARAGRAPH-NAME             MPKUT035
379000             MOVE 'ATTEMPTING TO FETCH OPER UNT AND FCLTY ID      MPKUT035
379100-                 'FROM TPURCHS AND TENTNME'                      MPKUT035
379200                                 TO PV-DB2-OPERATION-MESSAGE-1    MPKUT035
379300             PERFORM 9100-DB2-ABEND                               MPKUT035
379400     END-EVALUATE.                                                MPKUT035
379500*1600-EXIT.                                                       MPKUT035
379600                                                                  MPKUT035
379700 8000-WRITE-EXTRACT-FILE.                                         MPKUT035
379800     WRITE CERT-REQ-FILE-OUT-REC                                  MPKUT035
379900         FROM WS-OUTPUT-CERT-RECORD.                              MPKUT035
380000     ADD 1 TO PV-CERT-REQ-WRITTEN.                                MPKUT035
380100                                                                  MPKUT035
380200                                                                  MPKUT035
380300 9100-DB2-ABEND.                                                  MPKUT035
380400******************************************************************MPKUT035
380500*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL    MPKUT035
380600*  ERROR OR WARNING CODE OCCURS.                                  MPKUT035
380700******************************************************************MPKUT035
380800     DISPLAY '9100-DB2-ABEND'.                                    MPKUT035
380900     DISPLAY '** ABEND           **'.                             MPKUT035
381000     DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  MPKUT035
381100     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        MPKUT035
381200     DISPLAY '** TABLES AFFECTED ** = ' PV-DB2-TABLE-NAME-1.      MPKUT035
381300     DISPLAY '**                 ** = ' PV-DB2-TABLE-NAME-2.      MPKUT035
381400     DISPLAY '**                 ** = ' PV-DB2-TABLE-NAME-3.      MPKUT035
381500     DISPLAY '** OPERATION       ** = ' PV-DB2-OPERATION-MESSAGE-1MPKUT035
381600     DISPLAY '**                 ** = ' PV-DB2-OPERATION-MESSAGE-2MPKUT035
381700     DISPLAY '**                 ** = ' PV-DB2-OPERATION-MESSAGE-3MPKUT035
381800     DISPLAY '**                 ** = ' PV-DB2-OPERATION-MESSAGE-4MPKUT035
381900     DISPLAY '**                 ** = '                           MPKUT035
382000                                                                  MPKUT035
382100******************************************************************MPKUT035
382200* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    MPKUT035
382300* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         MPKUT035
382400* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     MPKUT035
382500* FORMAT.                                                         MPKUT035
382600******************************************************************MPKUT035
382700     COPY DPPD004.                                                MPKUT035
382800                                                                  MPKUT035
382900     MOVE +4013                  TO ABEND-CODE.                   MPKUT035
383000     CALL 'ILBOABN0' USING ABEND-CODE.                            MPKUT035
383100*                                                                 MPKUT035
383200*                                                                 MPKUT035
