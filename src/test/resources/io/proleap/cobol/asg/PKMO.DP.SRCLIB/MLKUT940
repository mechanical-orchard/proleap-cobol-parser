000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    MLKUT940.                                                 
000600 AUTHOR.        MARK FRAMNESS.                                            
000700 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000800 DATE-WRITTEN.  04/11/11.                                                 
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*    ML WEEKLY PRICE CHANGE EXTRACT                                       
001300******************************************************************        
001400*    THIS PROGRAM WILL CREATE ML PRICE CHANGE EXTRACT RECORDS             
001500*    BY READING THE EDITED PRICE CHANGE DATA AND CALCULATING THE          
001600*    APPROPRIATE MARKDOWN/MARKUP DOLLARS.                                 
001700******************************************************************        
001800*CHANGE HISTORY:                                                          
001900* WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
002000* ------- ---------- ----------- --------------------------------         
002100*    CHANGED: 02/15/99       BY: PATI GREEN                               
002200*    IF A SKU WAS NOT FOUND ON THE CALL TO THE SKU TABLE                  
002300*    CHANGED LOGIC TO GENERATE AN INTERNAL UPC FOR DEPT-CL-000 IF         
002400*    RECORD USING THE INTERNAL UPC INSTEAD OF BYPASSING THE SKU           
002500*    NOT FOUND CONDITION.                                                 
002600******************************************************************        
002700*    CHANGED: 02/23/99       BY: PATI GREEN                               
002800*    CHANGED TO DETERMINE SUNDAY DATE BY CALLING THE CALENDAR             
002900*    ROUTINE USING THE DATE ON THE INPUT RECORD INSTEAD OF READING        
003000*    A DATE CONTROL CARD (SO NON-CUTOFF/CUTOFF WEEK RUNS GET THE          
003100*    APPROPRIATE SUNDAY DATE ON THE OUTPUT FILES).                        
003200*    ALSO CHANGED 099-10 LOGIC TO CHECK CATEGORY BEFORE COMPUTING         
003300*    THE DOLLARS TO ACCUMULATE FOR DISPLAY.                               
003400******************************************************************        
003500*    CHANGED: 03/16/99       BY: PATI GREEN                               
003600*    ADDED CHECK TO SEE IF THE INPUT RECORD HAS REASON CODE = 746         
003700*    AND CATEGORY = 2; IF IT DOES, SET THE CATEGORY CODE TO 1.            
003800******************************************************************        
003900*    CHANGED: 07/13/99       BY: PATI GREEN                               
004000*    CHANGED PARA 2000 TO BYPASS ALL DEPT 099 (NOT JUST 099-10).          
004100******************************************************************        
004200*    CHANGED: 05/08/00       BY: DAN FRANKLIN                             
004300*    ADDED REASON CODES 024 AND 792                                       
004400******************************************************************        
004500*    CHANGED: 08/07/00       BY: DAN FRANKLIN                             
004600*    ADDED REASON CODE 052                                                
004700******************************************************************        
004800*    CHANGED: 02/16/01       BY: PATI GREEN                               
004900*    ADDED REASON CODE 112                                                
005000******************************************************************        
005100*    CHANGED: 06/25/01       BY: MARIBETH ZAMORA                          
005200*    REMOVED REASON CODES 110/160 FROM THE LIST OF VALID RSN CDES         
005300******************************************************************        
005400*    CHANGED: 02/28/02       BY: PATI GREEN                               
005500*    ADDED REASON CODE 060 (DENIM ADJUSTMENT)                             
005600******************************************************************        
005700*    CHANGED: 09/26/02       BY: BETTY DORST                              
005800*    REPLACED CALLS TO SKU TABLE WITH CALLS TO TSKXREF                    
005900*    REPLACED CALLS TO DPKUT040 WITH CALLS TO UPC CHECK DIGIT             
006000******************************************************************        
006100*    CHANGED: 12/31/02       BY: PATI GREEN     WR: 21661                 
006200*    CHANGED TO PULL DEPT & CLASS FROM MLRD023 FIELDS INSTEAD OF          
006300*    INFERRING FROM NEW SKU NUMBER. ADD SKU_STAT_CDE <> '00' TO           
006400*    TSKXREF CALL IN 7000. (PART OF NIS PROJECT)                          
006500*****************************************************************         
006600*    CHANGED: 09/29/03       BY: CARL RICHTER   WR: 25618                 
006700*    ADDED REASON CODE '775' FOR E-BOGO.                                  
006800******************************************************************        
006900*    CHANGED: 01/16/04       BY: MARIBETH ZAMORA WR: 25608                
007000*    ADDED REASON CODE '777' FOR E-BOGO  II.                              
007100******************************************************************        
007200*    CHANGED: 05/14/04       BY: THOMAS DUFFY   WR: 25831                 
007300*    INCREASED WS PV-STORE-NBR-CHAR TO PIC X(05), AND                     
007400*    PV-STORE-NBR-NUM TO PIC 9(05).                                       
007500******************************************************************        
007600*    CHANGED: 06/03/04       BY: STACY BIESZK   WR: 26310                 
007700*    REMOVED EDIT WHICH CONVERTED PC-CATEGORY = 2 (REGISTER) TO           
007800*    PC-CATEGORY = 1(PROMO) FOR MKDN-MKUP-RSN-CDE = 746 (PROMO)           
007900******************************************************************        
008000******************************************************************        
008100*    CHANGED: 10/12/05       BY: BETTY DORST    WR: 28003                 
008200*    REPLACED MLRD023 WITH MLRD033 TO OBTAIN NEW FIELDS FOR               
008300*    MARKDOWN OPTIMZATION PROJECT - MKDN-CDE & ORIG-CLR-DTE               
008400*    ALSO ADDED NEW REASON CODE 025, 026, 189                             
008500******************************************************************        
008600*    CHANGED: 01/25/06       BY: THOMAS DUFFY   WR: 29980                 
008700*    UPDATE WORKING STORAGE PV-VALID_RSN_CDE TO INCLUDE NEW               
008800*    VALID REASON CODE OF "790".                                          
008900*                                                                         
009000* XXXXXX  10-14-2008 ROD JANSSEN MWK178/MWK198 CHANGE TO PASS THE         
009100*                                REASON CODE, GOOD OR BAD, TO THE         
009200*                                MW MART. MWK198 WILL BE ADJUSTED         
009300*                                TO VALIDATE THE CODES ACCORDINGLY        
009400*                                                                         
009500*    CHANGED: 04/11/11       BY: MARK FRAMNESS  WR: 3033D                 
009600*                                CLONE FROM MWKUT940 AND CLONE            
009700*                                COPYBOOK MWRD024 TO MLRD940              
      ******************************************************************        
MAF212*    CHANGED: 02/29/2012 MARK A FRAMNESS                                  
MAF212*             CHANGE TO PASS THE PC-FIN-LIAB-DTE FIELD TO THE             
MAF212*             TERADATA EDW.                                               
MAF212*             ALSO, OUTPUT DEPT 99 RECORDS & RETAIN ONLY THE              
MAF212*             CATEGORY 3, 4, AND 5 COMPUTATION                            
      ******************************************************************        
PM3403*    CHANGED: 04/20/2012 SCOTT JACKSON                                    
PM3403*             LARGER RETAIL EXPANSION PROJECT. RECOMPILE ONLY FOR         
PM3403*             COPYBOOK CHANGES FOR MLRD033                                
      ******************************************************************        
205464*    CHANGED: 08/16/18       BY: JIM ARENDT    CHG0205464                 
205464*    UPDATE WORKING STORAGE PV-VALID_RSN_CDE TO INCLUDE NEW               
205464*    VALID REASON CODE OF "710".                                          
205464******************************************************************        
205464*    CHANGED: 02/14/23       BY: JEAN KURK     CHG0281369                 
205464*    CHANGE CALL OF UPC CHECK DIGIT ROUTINE TO BE DYNAMIC                 
205464******************************************************************        
008900*                                                                         
010300                                                                          
010400******************************************************************        
010500 ENVIRONMENT DIVISION.                                                    
010600******************************************************************        
010700                                                                          
010800 INPUT-OUTPUT SECTION.                                                    
010900                                                                          
011000 FILE-CONTROL.                                                            
011100                                                                          
011200     SELECT MW-EDIT-PRCHG-FILE                                            
011300         ASSIGN TO INP01.                                                 
011400                                                                          
011500     SELECT MW-PRCHG-EXTRACT-FILE                                         
011600         ASSIGN TO OUT01.                                                 
011700                                                                          
011800******************************************************************        
011900 DATA DIVISION.                                                           
012000******************************************************************        
012100                                                                          
012200 FILE SECTION.                                                            
012300                                                                          
012400 FD  MW-EDIT-PRCHG-FILE                                                   
012500     RECORDING MODE IS F                                                  
012600     LABEL RECORDS ARE STANDARD                                           
012700     BLOCK CONTAINS 0 RECORDS                                             
012800     RECORD CONTAINS 150 CHARACTERS                                       
012900     DATA RECORD IS MW-EDIT-PRCHG-RECORD.                                 
013000                                                                          
013100 01  MW-EDIT-PRCHG-FILE-RECORD   PIC X(150).                              
013200                                                                          
013300 FD  MW-PRCHG-EXTRACT-FILE                                                
013400     RECORDING MODE IS F                                                  
013500     LABEL RECORDS ARE STANDARD                                           
013600     BLOCK CONTAINS 0 RECORDS                                             
013700     RECORD CONTAINS 88 CHARACTERS                                        
013800     DATA RECORD IS MW-PRCHG-EXTRACT-RECORD.                              
013900                                                                          
014000 01  MW-PRCHG-EXTRACT-RECORD     PIC X(88).                               
014100                                                                          
014200                                                                          
014300******************************************************************        
014400 WORKING-STORAGE SECTION.                                                 
014500******************************************************************        
014600                                                                          
014700 01  ABEND-CODE                       PIC S9(04) COMP SYNC                
014800                                                 VALUE +0.                
014900     88  ABEND-4001                              VALUE +4001.             
015000     88  ABEND-4002                              VALUE +4002.             
015100     88  ABEND-4003                              VALUE +4003.             
015200     88  ABEND-4004                              VALUE +4004.             
015300     88  ABEND-4005                              VALUE +4005.             
015400     88  ABEND-4006                              VALUE +4006.             
015500     88  ABEND-4007                              VALUE +4007.             
015600     88  ABEND-4008                              VALUE +4008.             
015700     88  ABEND-4009                              VALUE +4009.             
015800     88  ABEND-4012                              VALUE +4012.             
015900     88  ABEND-4013                              VALUE +4013.             
016000                                                                          
016100 01  PC-PROGRAM-CONSTANTS.                                                
016200     05  PC-PROGRAM-NAME              PIC X(8)   VALUE 'MLKUT940'.        
016300     05  PC-MAX-RETRIES               PIC S9(04) COMP SYNC                
016400                                                 VALUE +3.                
016500     05  PC-MAX-BEFORE-COMMIT         PIC S9(04) COMP SYNC                
016600                                                 VALUE +500.              
016700     05  PC-STATUS-98                 PIC  X(02) VALUE '98'.              
016800     05  PC-SEQ-000                   PIC  X(03) VALUE '000'.             
016900     05  PC-SEQ-999                   PIC  X(03) VALUE '999'.             
017000     05  PC-DA-STORE-400              PIC  X(05) VALUE '00400'.           
017100                                                                          
017200 01  PS-PROGRAM-SWITCHES.                                                 
017300     05  PS-END-OF-CONTROL-CARD-SW    PIC X      VALUE 'N'.               
017400         88  PS-END-OF-CONTROL-CARD              VALUE 'Y'.               
017500         88  PS-NOT-END-OF-CONTROL-CARD          VALUE 'N'.               
017600     05  PS-END-OF-INPUT-FILE-SW      PIC X      VALUE 'N'.               
017700         88  PS-END-OF-INPUT-FILE                VALUE 'Y'.               
017800         88  PS-NOT-END-OF-INPUT-FILE            VALUE 'N'.               
017900     05  PS-END-OF-SKU-STORE-SW       PIC X      VALUE 'N'.               
018000         88  PS-END-OF-SKU-STORE                 VALUE 'Y'.               
018100         88  PS-NOT-END-OF-SKU-STORE             VALUE 'N'.               
018200     05  PS-SKU-FOUND-SW              PIC X      VALUE 'Y'.               
018300         88  PS-SKU-FOUND                        VALUE 'Y'.               
018400         88  PS-SKU-NOT-FOUND                    VALUE 'N'.               
018500                                                                          
018600 01  PROGRAM-VARIABLES.                                                   
018700                                                                          
018800     05  PV-SAVE-SKU             PIC X(08)   VALUE SPACES.                
018900                                                                          
019000     05  PV-SAVE-PRD-DIM-KY-ID.                                           
019100         10  PV-SAVE-UPC-NBR     PIC 9(15)   VALUE ZEROES.                
019200         10  PV-SAVE-ITEM-NBR    PIC 9(15)   VALUE ZEROES.                
019300                                                                          
019400     05  PV-STORE-NBR-CHAR       PIC X(05)   VALUE SPACES.                
019500     05  PV-STORE-NBR-NUM REDEFINES PV-STORE-NBR-CHAR                     
019600                                 PIC 9(05).                               
019700                                                                          
019800     05  PV-RSN-CDE              PIC X(03).                               
019900         88  PV-VALID-RSN-CDE    VALUES                                   
020000                                        '021' '022' '023' '024'           
020100                                        '025' '026'                       
020200                                        '030' '040' '050' '051'           
020300                                        '052' '060' '112'                 
020400                                        '115' '165' '187' '189'           
020500                                        '175' '180' '185' '195'           
020600                                        '270' '295' '315' '395'           
020700                                        '495' '501' '502'                 
020800                                        '701' '702' '703' '704'           
020900                                        '705' '706' '707' '708'           
205464                                        '709' '710'                       
021000                                        '728' '733' '746'                 
021100                                        '753' '771' '775' '777'           
021200                                        '780' '790' '792' '796'           
021300                                        '797' '798' '997' '998'           
021400                                        '999'.                            
021500     05  PV-PRCHG-AMT            PIC S9(09)V99 COMP-3                     
021600                                             VALUE ZEROES.                
021700                                                                          
021800     05  PV-09910-TOTAL-AMT      PIC S9(09)V99                            
021900                                             VALUE ZEROES.                
022000     05  PV-09910-TOTAL-QTY      PIC S9(09)                               
022100                                             VALUE ZEROES.                
022200                                                                          
022300     05  PV-09910-DISPLAY-AMT    PIC +9(09).99                            
022400                                             VALUE ZEROES.                
022500     05  PV-09910-DISPLAY-QTY    PIC +9(09)                               
022600                                             VALUE ZEROES.                
022700                                                                          
022800     05  PV-UPC-NBR              PIC S9(15)  COMP-3                       
022900                                             VALUE ZEROES.                
023000                                                                          
023100     05  PV-QTY-MARKED           PIC S9(9)   VALUE ZEROES.                
023200     05  PV-SUB                  PIC 9(02)   VALUE 0.                     
023300                                                                          
023400     05  PV-IN-COUNT             PIC 9(9)    VALUE 0.                     
023500     05  PV-SEL-COUNT            PIC 9(9)    VALUE 0.                     
023600     05  PV-OUT-COUNT            PIC 9(9)    VALUE 0.                     
023700     05  PV-09910-COUNT          PIC 9(9)    VALUE 0.                     
023800     05  PV-COMMIT-COUNT         PIC S9(04)  COMP SYNC                    
023900                                             VALUE +0.                    
024000                                                                          
024100     05  PV-DB2-OPER-ATTEMPTED   PIC X(30)   VALUE SPACES.                
024200     05  PV-PARAGRAPH-NAME       PIC X(30)   VALUE SPACES.                
024300     05  PV-SQL-SUB              PIC 9(03)   VALUE ZEROES.                
024400                                                                          
024500     05  PV-HOLD-PREV-DATE       PIC X(10)   VALUE SPACES.                
024600     05  PV-HOLD-SUNDAY-DATE     PIC X(10)   VALUE SPACES.                
024700                                                                          
024800******************************************************************        
024900* INPUT FILE LAYOUT OF THE ML EDITED PRICE CHANGE FILE                    
025000******************************************************************        
025100     COPY MLRD033.                                                        
025200                                                                          
025300******************************************************************        
025400*    OUTPUT FILE LAYOUT OF THE ML PRICE CHANGE EXTRACT FILE               
025500******************************************************************        
025600********* MLRD940.                                                        
025610     COPY MLRD940.                                                        
025700                                                                          
025800*****************************************************************         
025900* UPC PARAMETER LIST FOR THE UPC CHECKDIGIT ROUTINE (DPKUT040)            
026000*****************************************************************         
026100                                                                          
026200     COPY DPWS040I.                                                       
026300                                                                          
026400******************************************************************        
026500*    CALENDAR ROTUINE COPYBOOK                                            
026600******************************************************************        
026700                                                                          
026800     COPY DPWS500.                                                        
026900                                                                          
027000*****************************************************************         
027100* DB2 FORMATTED ERROR MESSAGE                                             
027200*****************************************************************         
027300                                                                          
027400     COPY DPWS004.                                                        
027500                                                                          
027600*****************************************************************         
027700* DCLGEN FOR THE TSKXREF TABLE                                            
027800*****************************************************************         
027900                                                                          
028000     EXEC SQL                                                             
028100         INCLUDE TSKXREF                                                  
028200     END-EXEC.                                                            
028300                                                                          
028400******************************************************************        
028500* SQL COMMUNICATIONS WORK AREA                                            
028600******************************************************************        
028700                                                                          
028800     EXEC SQL                                                             
028900         INCLUDE SQLCA                                                    
029000     END-EXEC.                                                            
029100                                                                          
029200******************************************************************        
029300 PROCEDURE DIVISION.                                                      
029400******************************************************************        
029500 0000-MAINLINE.                                                           
029600                                                                          
029700     PERFORM 1000-INITIALIZATION.                                         
029800                                                                          
029900     PERFORM 2000-PROCESS-FILE                                            
030000         UNTIL PS-END-OF-INPUT-FILE.                                      
030100                                                                          
030200     PERFORM 9000-END-OF-JOB.                                             
030300                                                                          
030400     GOBACK.                                                              
030500                                                                          
030600******************************************************************        
030700*  OPEN ALL FILES.                                                        
030800*  READ THE DATE CONTROL RECORD. AND READING THE FIRST RECORD             
030900*  READ THE FIRST RECORD FROM THE INPUT FILE.                             
031000******************************************************************        
031100 1000-INITIALIZATION.                                                     
031200                                                                          
031300     OPEN INPUT  MW-EDIT-PRCHG-FILE                                       
031400          OUTPUT MW-PRCHG-EXTRACT-FILE.                                   
031500                                                                          
031600     DISPLAY '   '.                                                       
031700     DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
031800                                                                          
031900     PERFORM 8100-READ-MW-EDIT-PRCHG-FILE.                                
032000                                                                          
032100******************************************************************        
032200*   PROCESS THE INPUT FILE RECORDS.                                       
032300*                                                                         
032400*   IF THE DEPT = '099', ANY CLASS, ACCUMMULATE THE QTY AND               
032500*   AND AMTS, THEN BYPASS THE RECORD.                                     
032600*   OTHERWISE,                                                            
032700*     IF SKU-SEQ-NBR = '000' OR '999'                                     
032800*        CALL UPC CHECK TO GENERATE THE INTERNAL UPC FOR THE              
032900*           DEPT-CL-000.                                                  
033000*       ('999' AND '000' SEQ#'S ARE PROCESSED AS DEPT-CL-000.)            
033100*     ELSE                                                                
033200*        GET THE ITEM NBR FOR THE SKU FROM TSKXREF.                       
033300*                                                                         
033400*   FORMAT THE OUTPUT RECORD.                                             
033500*   READ THE NEXT INPUT RECORD.                                           
033600******************************************************************        
033700 2000-PROCESS-FILE.                                                       
033800                                                                          
033900     IF ML033-PC-DEPT-NBR = '099'                                         
034000         ADD 1 TO PV-09910-COUNT                                          
034100         ADD ML033-PC-QTY TO PV-09910-TOTAL-QTY                           
MAF212*THE BELOW COMPUTATION ASSUMES PC CATEGORY 3, 4, AND 5 ONLY.              
MAF212*PC CATEGORY 1 & 2 RECORDS ARE NOT EXPECTED HERE AS THEY ARE              
MAF212*FILTERED OUT IN MLK2561 S040. IF 1 & 2 COMPUTATION NEEDED SEE            
MAF212*MWKUT940                                                                 
034700         COMPUTE PV-09910-TOTAL-AMT = PV-09910-TOTAL-AMT +                
034800                 (ML033-PC-QTY *                                          
034900                (ML033-PC-OLD-PR-AMT - ML033-PC-NEW-PR-AMT))              
035000     END-IF.                                                              
035100*ALL RECORDS EVEN DEPT 99 ARE NOW OUTPUT                                  
035200     IF    ML033-PC-SKU = PV-SAVE-SKU                                     
035300         CONTINUE                                                         
035400     ELSE                                                                 
035500         MOVE ML033-PC-SKU TO PV-SAVE-SKU                                 
035600         IF ML033-PC-SKU(6:3) NOT EQUAL PC-SEQ-000                        
035700          AND ML033-PC-SKU(6:3) NOT EQUAL PC-SEQ-999                      
035800             PERFORM 2100-GET-ITEM-NBR-FOR-SKU                            
035900         ELSE                                                             
036000             MOVE ZEROES TO PV-SAVE-ITEM-NBR                              
036100             MOVE ZEROES TO PV-SAVE-UPC-NBR                               
036200             PERFORM 2200-GET-UPC-NBR-FOR-SEQ-000                         
036300         END-IF                                                           
036400     END-IF                                                               
036500     PERFORM 2300-FORMAT-OUTPUT-RECORD                                    
036700                                                                          
036800     PERFORM 8100-READ-MW-EDIT-PRCHG-FILE.                                
036900                                                                          
037000                                                                          
037100******************************************************************        
037200*   RETRIEVE THE SKU ITEM NUMBER FROM TSKXREF.                            
037300*                                                                         
037400*   IF THE SKU FOUND IS NOT A RENUMBERED SKU (STAT 98), MOVE ITEM         
037500*      NBR TO THE SAVE-ITEM-NBR, MOVE 0 TO THE SAVE-UPC-NBR.              
037600*   OTHERWISE, CALL TSKXREF TABLE TO RETREIVE THE NEW SKU'S ITEM          
037700*      NUMBER BY JOINING ON THE INTR-UPC-ID OF THE OLD SKU WHERE          
037800*      TSKXREF.SKU_STAT_CDE <> '00' (DELETED) OR '98' (RENUMBERED)        
037900*      MOVE THE ITEM NBR TO THE SAVE-ITEM-NBR AND MOVE 0 TO THE           
038000*      SAVE-UPC-NBR.                                                      
038100*   IF THE SKU WAS NOT FOUND, MOVE 0 TO THE SAVE-ITEM-NBR AND             
038200*      SAVE-UPC-NBR; THEN GENERATE AN INTERNAL UPC FOR DEPT-CL-000        
038300******************************************************************        
038400 2100-GET-ITEM-NBR-FOR-SKU.                                               
038500                                                                          
038600     SET PS-SKU-FOUND TO TRUE.                                            
038700     MOVE PV-SAVE-SKU       TO SKXREF-SKU.                                
038800     PERFORM 7000-SELECT-SKU-ITEM-NBR.                                    
038900                                                                          
039000     IF PS-SKU-FOUND                                                      
039100         IF SKXREF-SKU-STAT-CDE NOT EQUAL PC-STATUS-98                    
039200             MOVE SKXREF-ITM-NBR TO PV-SAVE-ITEM-NBR                      
039300             MOVE ZEROES         TO PV-SAVE-UPC-NBR                       
039400         ELSE                                                             
039500             PERFORM 7100-SELECT-RENUM-SKU-ITEM-NBR                       
039600             IF SQLCODE = +0                                              
039700               MOVE SKXREF-ITM-NBR TO PV-SAVE-ITEM-NBR                    
039800               MOVE ZEROES         TO PV-SAVE-UPC-NBR                     
039900               DISPLAY '** RENUMBERED OLD SKU: '                          
040000                                       PV-SAVE-SKU                        
040100               DISPLAY '**              ITEM#: '                          
040200                                       PV-SAVE-ITEM-NBR                   
040300             ELSE                                                         
040400               DISPLAY '** ABEND **'                                      
040500               DISPLAY '** PROGRAM = ' PC-PROGRAM-NAME ' **'              
040600               DISPLAY                                                    
040700                   '** PARAGRAPH = 2100-GET-ITEM-NBR-FOR-SKU'             
040800               DISPLAY '** ERROR ACCESSING TSKXREF FOR SKU: '             
040900                                             PV-SAVE-SKU                  
041000               SET ABEND-4004 TO TRUE                                     
041100               CALL 'ILBOABN0' USING ABEND-CODE                           
041200             END-IF                                                       
041300         END-IF                                                           
041400     ELSE                                                                 
041500         DISPLAY '*** SKU NOT FOUND ON TSKXREF: '                         
041600                      PV-SAVE-SKU ' **'                                   
041700         MOVE ZEROES TO PV-SAVE-ITEM-NBR                                  
041800         MOVE ZEROES TO PV-SAVE-UPC-NBR                                   
041900         PERFORM 2200-GET-UPC-NBR-FOR-SEQ-000                             
042000     END-IF.                                                              
042100                                                                          
042200     IF PV-COMMIT-COUNT = PC-MAX-BEFORE-COMMIT                            
042300         PERFORM 7600-COMMIT                                              
042400     END-IF.                                                              
042500                                                                          
042600******************************************************************        
042700*   GENERATE THE INTERNAL UPC NBR FOR DEPT-CL-000 SKU'S.                  
042800*   MOVE THE GENERATED INTERNAL UPC TO THE SAVE-UPC-NBR.                  
042900*   ('999' AND '000' SEQ#'S ARE PROCESSED AS DEPT-CL-000.)                
043000******************************************************************        
043100 2200-GET-UPC-NBR-FOR-SEQ-000.                                            
043200                                                                          
043300     MOVE ML033-PC-DEPT-NBR  TO DP040I-DEPARTMENT-NUMBER.                 
043400     MOVE ML033-PC-CLASS-NBR TO DP040I-CLASS-NUMBER.                      
043500     MOVE PC-SEQ-000         TO DP040I-SEQUENCE-NUMBER.                   
043600*  GENERATE INTERNAL UPC CODE FROM SKU                                    
043700     SET DP040I-GEN-INTERNAL-UPC-OPTION TO TRUE.                          
043800     CALL DP040I-UPC-CHECK-DIGIT-WO-IUPC                                  
043800                     USING DP040I-UPC-CHECK-DIGIT-PARMS.                  
043900     IF DP040I-NO-ERROR-DETECTED                                          
044000         MOVE DP040I-UPC-CODE TO PV-SAVE-UPC-NBR                          
044100     ELSE                                                                 
044200         DISPLAY '** ABEND **'                                            
044300         DISPLAY '** PROGRAM = ' PC-PROGRAM-NAME ' **'                    
044400         DISPLAY '** PARAGRAPH = 2200-GET-UPC-NBR-FOR-SEQ-000'            
044500         DISPLAY '** ERROR ACCESSING UPC CHECK FOR SKU: '                 
044600                                               PV-SAVE-SKU                
044700         SET ABEND-4004 TO TRUE                                           
044800         CALL 'ILBOABN0' USING ABEND-CODE                                 
044900     END-IF.                                                              
045000                                                                          
045100******************************************************************        
045200* FORMAT A PRICE CHANGE EXTRACT RECORD AND WRITE IT TO THE                
045300*   OUTPUT FILE.                                                          
045400* IF THE CATEGORY IS 1 (PLU/PROMO) OR 2 (REGISTER), THE AMOUNTS           
045500*   ARE ALREADY EXTENDED, SO COMPUTE THE DIFFERENCE WITHOUT REGARD        
045600*   TO QTY.  ALL OTHER CATERGORIES NEED TO MULTIPLY TO QTY BY THE         
045700*   DIFFERENCE IN ORDER TO EXTEND THE AMOUNT.                             
045800* WRITE THE OUTPUT RECORD.                                                
045900* ADD 1 TO THE COUNTER.                                                   
046000******************************************************************        
046100 2300-FORMAT-OUTPUT-RECORD.                                               
046200                                                                          
046300     INITIALIZE ML940-PRCHG-EXTRACT-RECORD.                               
046400                                                                          
046500     MOVE PV-SAVE-PRD-DIM-KY-ID TO ML940-PRCHG-PRD-DIM-KY-ID.             
046600                                                                          
046700*    IF ML033-PC-EDIT-DTE = PV-HOLD-PREV-DATE                             
046800*        MOVE PV-HOLD-SUNDAY-DATE TO ML940-PRCHG-TM-DIM-KY-DTE            
046900*    ELSE                                                                 
047000*        PERFORM 2310-GET-SUNDAY-DATE                                     
047100*        MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO                                
047200*                                   PV-HOLD-SUNDAY-DATE                   
047300*                                   ML940-PRCHG-TM-DIM-KY-DTE             
047400*        MOVE ML033-PC-EDIT-DTE TO PV-HOLD-PREV-DATE                      
047500*    END-IF.                                                              
047600*FOR DAILY FILE TAKE THE DATE AS IT COMES                                 
047700     MOVE ML033-PC-EDIT-DTE     TO ML940-PRCHG-TM-DIM-KY-DTE.             
047800     MOVE ML033-PC-STORE-NBR    TO PV-STORE-NBR-CHAR.                     
047900     MOVE PV-STORE-NBR-NUM      TO ML940-PRCHG-PHY-SITE-ID.               
048000                                                                          
048100     MOVE ML033-PC-TYP-CDE      TO ML940-PRCHG-TYP-CDE.                   
048200                                                                          
048300     IF ML033-PC-STORE-NBR = PC-DA-STORE-400                              
048400         MOVE '4'   TO ML033-PC-CATEGORY                                  
048500         MOVE '999' TO ML033-PC-MKDN-MKUP-RSN-CDE                         
048600     END-IF.                                                              
048700                                                                          
048800*    IF ML033-PC-MKDN-MKUP-RSN-CDE = '746'                                
048900*     AND ML033-PC-CATEGORY  = '2'                                        
049000*        MOVE '1' TO ML033-PC-CATEGORY                                    
049100*    END-IF.                                                              
049200*                                                                         
049300     MOVE ML033-PC-CATEGORY  TO ML940-PRCHG-CTG-CDE.                      
049400                                                                          
049500**** MOVE ML033-PC-MKDN-MKUP-RSN-CDE TO PV-RSN-CDE.                       
049600**** IF PV-VALID-RSN-CDE                                                  
049700         MOVE ML033-PC-MKDN-MKUP-RSN-CDE TO ML940-PRCHG-RSN-CDE           
049800**** ELSE                                                                 
049900****     MOVE SPACES                     TO ML940-PRCHG-RSN-CDE           
050000**** END-IF.                                                              
050100****                                                                      
050200     MOVE ML033-PC-QTY          TO ML940-PRCHG-UNT-QTY.                   
050300                                                                          
050400     IF ML033-PC-CATEGORY = '1'                                           
050500       OR ML033-PC-CATEGORY = '2'                                         
050600         COMPUTE PV-PRCHG-AMT =                                           
050700                 ML033-PC-OLD-PR-AMT - ML033-PC-NEW-PR-AMT                
050800     ELSE                                                                 
050900         COMPUTE PV-PRCHG-AMT = ML033-PC-QTY *                            
051000                (ML033-PC-OLD-PR-AMT - ML033-PC-NEW-PR-AMT)               
051100     END-IF.                                                              
051200     MOVE PV-PRCHG-AMT TO ML940-PRCHG-AMT.                                
051300                                                                          
051400     MOVE ML033-PC-NEW-PR-AMT TO ML940-PRCHG-NEW-UNT-RTL-AMT.             
051500     MOVE ML033-PC-ORIG-RTL-NONGRP TO                                     
051600                                 ML940-PRCHG-OLD-UNT-RTL-AMT.             
051700     IF ML033-PC-ORIG-CLR-DTE = SPACES                                    
051800       MOVE '9999-09-09' TO ML940-PRCHG-ORIG-CLR-DTE                      
051900     ELSE                                                                 
052000       MOVE ML033-PC-ORIG-CLR-DTE TO ML940-PRCHG-ORIG-CLR-DTE             
052100     END-IF.                                                              
052200     MOVE ML033-PC-MKDN-CDE     TO ML940-PRCHG-MKDN-CDE.                  
052300                                                                          
MAF212     IF ML033-PC-FIN-LIAB-DTE = SPACES                                    
MAF212       MOVE '9999-09-09' TO ML940-PRCHG-FIN-LIAB-DTE                      
MAF212     ELSE                                                                 
MAF212       MOVE ML033-PC-FIN-LIAB-DTE  TO ML940-PRCHG-FIN-LIAB-DTE            
MAF212     END-IF.                                                              
                                                                                
053000     WRITE MW-PRCHG-EXTRACT-RECORD                                        
053100           FROM ML940-PRCHG-EXTRACT-RECORD.                               
053200                                                                          
053300     ADD 1 TO PV-OUT-COUNT.                                               
053400                                                                          
053500                                                                          
053600******************************************************************        
053700*  CALL THE CALENDAR ROUTINE TO DETERMINE THE FISCAL SUNDAY DATE          
053800*  FOR THE INPUT RECORD EDIT DATE.                                        
053900******************************************************************        
054000 2310-GET-SUNDAY-DATE.                                                    
054100                                                                          
054200     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
054300                                                                          
054400     ACCEPT DPG51-SYSTEM-DATE      FROM DATE.                             
054500                                                                          
054600     SET DPG51-FISCAL-454-CALENDAR                                        
054700         DPG51-DO-NOT-INCR-DECR-DATE                                      
054800         DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
054900                                                                          
055000     MOVE ML033-PC-EDIT-DTE        TO DPG52-LK-DATE-INPUT.                
055100                                                                          
055200     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52                  
055300                                             DPG53 DPG54                  
055400                                             DPG55 DPG56.                 
055500                                                                          
055600     EVALUATE TRUE                                                        
055700         WHEN DPG54-NO-ERROR                                              
055800             CONTINUE                                                     
055900         WHEN OTHER                                                       
056000             DISPLAY '** ABEND **'                                        
056100             DISPLAY '** PROGRAM = ' PC-PROGRAM-NAME ' **'                
056200             DISPLAY '** PARAGRAPH = 2310-GET-SUNDAY-DATE'                
056300             DISPLAY '** BAD CALL TO CALENDAR ROUTINE USING '             
056400                                           ML033-PC-EDIT-DTE              
056500             DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE            
056600             DISPLAY '** DPG51 = ' DPG51                                  
056700             DISPLAY '** DPG52 = ' DPG52                                  
056800             DISPLAY '** DPG53 = ' DPG53                                  
056900             DISPLAY '** DPG54 = ' DPG54                                  
057000             SET ABEND-4003 TO TRUE                                       
057100             CALL 'ILBOABN0' USING ABEND-CODE                             
057200     END-EVALUATE.                                                        
057300                                                                          
057400******************************************************************        
057500* SELECT ITEM NUMBER FROM TSKXREF TABLE FOR THE CURRENT SKU.              
057600******************************************************************        
057700 7000-SELECT-SKU-ITEM-NBR.                                                
057800                                                                          
057900     PERFORM WITH TEST AFTER                                              
058000         VARYING PV-SQL-SUB FROM 1 BY 1                                   
058100         UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
058200            OR   SQLCODE = 0                                              
058300            OR   SQLCODE = +100)                                          
058400                                                                          
058500         EXEC SQL                                                         
058600               SELECT ITM_NBR                                             
058700                    , SKU_STAT_CDE                                        
058800                 INTO :SKXREF-ITM-NBR                                     
058900                    , :SKXREF-SKU-STAT-CDE                                
059000                 FROM TSKXREF                                             
059100                WHERE SKU =  :SKXREF-SKU                                  
059200                  AND SKU_STAT_CDE <> '00'                                
059300         END-EXEC                                                         
059400                                                                          
059500         EVALUATE TRUE                                                    
059600             WHEN SQLCODE = 0                                             
059700                  ADD 1 TO PV-SEL-COUNT                                   
059800                           PV-COMMIT-COUNT                                
059900             WHEN SQLCODE = -904                                          
060000                 CONTINUE                                                 
060100             WHEN SQLCODE = +100                                          
060200                 SET PS-SKU-NOT-FOUND TO TRUE                             
060300             WHEN SQLWARN NOT = SPACES                                    
060400             WHEN SQLCODE NOT = ZERO                                      
060500                 DISPLAY '** ERROR SELECTING TO TSKXREF FOR SKU:'         
060600                                               PV-SAVE-SKU                
060700                 MOVE '7000-SELECT-SKU-ITEM-NBR'                          
060800                     TO PV-PARAGRAPH-NAME                                 
060900                 MOVE 'SELECT' TO PV-DB2-OPER-ATTEMPTED                   
061000                 PERFORM 9999-SQL-ABEND                                   
061100         END-EVALUATE                                                     
061200     END-PERFORM.                                                         
061300                                                                          
061400     IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
061500         MOVE '7000-SELECT-SKU-ITEM-NBR' TO PV-PARAGRAPH-NAME             
061600         MOVE 'MAX RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED             
061700         PERFORM 9999-SQL-ABEND                                           
061800     END-IF.                                                              
061900                                                                          
062000                                                                          
062100****************************************************************          
062200*  SELECT ITEM NUMBER FROM TSKXREF TABLE FOR THE NEW SKU.                 
062300*   (RENUMBERED "TO" SKU)                                                 
062400****************************************************************          
062500 7100-SELECT-RENUM-SKU-ITEM-NBR.                                          
062600                                                                          
062700     PERFORM WITH TEST AFTER                                              
062800         VARYING PV-SQL-SUB FROM 1 BY 1                                   
062900         UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
063000            OR   SQLCODE = 0                                              
063100            OR   SQLCODE = +100)                                          
063200                                                                          
063300         EXEC SQL                                                         
063400             SELECT B.ITM_NBR                                             
063500             INTO :SKXREF-ITM-NBR                                         
063600             FROM TSKXREF A                                               
063700                 ,TSKXREF B                                               
063800             WHERE A.SKU       = :SKXREF-SKU                              
063900               AND A.INTR_UPC_ID = B.INTR_UPC_ID                          
064000               AND B.SKU_STAT_CDE NOT IN ('00', '98')                     
064100         END-EXEC                                                         
064200                                                                          
064300         EVALUATE TRUE                                                    
064400             WHEN SQLCODE = 0                                             
064500             WHEN SQLCODE = -904                                          
064600                 CONTINUE                                                 
064700             WHEN SQLCODE = +100                                          
064800            DISPLAY '*** ITEM NOT FOUND FOR OLD RENUMBERD SKU: '          
064900                         PV-SAVE-SKU ' **'                                
065000                 MOVE '7100-SELECT-RENUM-SKU-ITEM-NBR'                    
065100                     TO PV-PARAGRAPH-NAME                                 
065200                 MOVE 'SELECT' TO PV-DB2-OPER-ATTEMPTED                   
065300                 PERFORM 9999-SQL-ABEND                                   
065400             WHEN SQLWARN NOT = SPACES                                    
065500             WHEN SQLCODE NOT = ZERO                                      
065600                 MOVE '7100-SELECT-RENUM-SKU-ITEM-NBR'                    
065700                     TO PV-PARAGRAPH-NAME                                 
065800                 MOVE 'SELECT' TO PV-DB2-OPER-ATTEMPTED                   
065900                 PERFORM 9999-SQL-ABEND                                   
066000         END-EVALUATE                                                     
066100     END-PERFORM.                                                         
066200                                                                          
066300     IF PV-SQL-SUB > PC-MAX-RETRIES                                       
066400         MOVE '7100-SELECT-RENUM-SKU-ITEM-NBR'                            
066500                                     TO PV-PARAGRAPH-NAME                 
066600         MOVE 'MAX RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED             
066700         PERFORM 9999-SQL-ABEND                                           
066800     END-IF.                                                              
066900                                                                          
067000******************************************************************        
067100*    COMMIT, TO FREE UP LOCKS.                                            
067200******************************************************************        
067300 7600-COMMIT.                                                             
067400                                                                          
067500     EXEC SQL                                                             
067600        COMMIT                                                            
067700     END-EXEC.                                                            
067800                                                                          
067900     IF SQLCODE NOT = +000                                                
068000         MOVE '7600-COMMIT' TO PV-PARAGRAPH-NAME                          
068100         MOVE 'COMMIT'      TO PV-DB2-OPER-ATTEMPTED                      
068200         PERFORM 9999-SQL-ABEND                                           
068300     END-IF.                                                              
068400                                                                          
068500*    DISPLAY 'PERFORMED 7600-COMMIT'.                                     
068600                                                                          
068700     MOVE 0 TO PV-COMMIT-COUNT.                                           
068800                                                                          
068900************************************************************              
069000*  READ THE MP DAILY DETAILED RECEIPTS REPORT FILE                        
069100************************************************************              
069200 8100-READ-MW-EDIT-PRCHG-FILE.                                            
069300                                                                          
069400     READ MW-EDIT-PRCHG-FILE INTO ML033-DW-PC-RECORD                      
069500         AT END                                                           
069600             SET PS-END-OF-INPUT-FILE TO TRUE                             
069700         NOT AT END                                                       
069800             ADD 1          TO   PV-IN-COUNT.                             
069900                                                                          
070000************************************************************              
070100*  DISPLAY RECORD COUNTS AT END OF JOB                                    
070200*  CLOSE THE FILES.                                                       
070300************************************************************              
070400 9000-END-OF-JOB.                                                         
070500                                                                          
070600     MOVE PV-09910-TOTAL-QTY TO PV-09910-DISPLAY-QTY.                     
070700     MOVE PV-09910-TOTAL-AMT TO PV-09910-DISPLAY-AMT.                     
070800                                                                          
070900     DISPLAY '***** CONTROL REPORT / RECORD COUNTS ****'.                 
071000                                                                          
071010*FOR TERADATA MDSE EDW WE ALLOW DEPT 099 RECORDS TO FLOW THROUGH          
071020*REPORT ON NUMBER *_DETECTED_* INSTEAD OF NUMBER BYPASSED                 
MAF212     DISPLAY 'TOTAL DEPT 099 RECORDS DETECTED : ' PV-09910-COUNT.         
071200                                                                          
071300     DISPLAY 'TOTAL DEPT 099 RECORDS QUANTITY : '                         
071400                                            PV-09910-DISPLAY-QTY.         
071500                                                                          
071600     DISPLAY 'TOTAL DEPT 099 RECORDS AMOUNT   : '                         
071700                                            PV-09910-DISPLAY-AMT.         
071800                                                                          
071900     DISPLAY 'TOTAL SELECTS TO TSKXREF: '  PV-SEL-COUNT.                  
072000                                                                          
072100     DISPLAY 'TOTAL INP01     RECORDS : ' PV-IN-COUNT.                    
072200                                                                          
072300     DISPLAY 'TOTAL OUT01     RECORDS : ' PV-OUT-COUNT.                   
072400                                                                          
072500     CLOSE MW-EDIT-PRCHG-FILE                                             
072600           MW-PRCHG-EXTRACT-FILE.                                         
072700                                                                          
072800                                                                          
072900******************************************************************        
073000* ABEND THE PROGRAM IN THE EVENT THAT AN SQL ERROR OR WARNING             
073100* CODE OCCURS.                                                            
073200******************************************************************        
073300 9999-SQL-ABEND.                                                          
073400                                                                          
073500     DISPLAY '** ABEND     **'.                                           
073600     DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
073700     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
073800     DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
073900                                                                          
074000*-----------------------------------------------------------------        
074100* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
074200* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
074300* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
074400* FORMAT.                                                                 
074500*-----------------------------------------------------------------        
074600                                                                          
074700     COPY DPPD004.                                                        
074800                                                                          
074900     SET ABEND-4013 TO TRUE.                                              
075000     CALL 'ILBOABN0' USING ABEND-CODE.                                    
