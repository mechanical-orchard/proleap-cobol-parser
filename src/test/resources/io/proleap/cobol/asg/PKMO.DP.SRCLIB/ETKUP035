00001  IDENTIFICATION DIVISION.                                         05/31/95
00002                                                                   ETKUP035
00003                                                                      LV007
00004  PROGRAM-ID.    ETKUP035.                                         ETKUP035
00005  AUTHOR.        STEVE FLUNKER.                                    ETKUP035
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          ETKUP035
00007  DATE-WRITTEN.  05-15-95.                                         ETKUP035
00008  DATE-COMPILED.                                                   ETKUP035
00009                                                                   ETKUP035
00010 ******************************************************************ETKUP035
00011 *      ETKUP035 - EDI/PO RECYCLE (UPDATE/DELETE)                     CL**3
00012 *                                                                 ETKUP035
00013 * THIS PROGRAM RUNS UNDER THE DB2 CHECKPOINT RESTART SYSTEM.      ETKUP035
00014 * IT READS THE PREPARED EDI PO'S TO BE UPDATE/DELETED FILE FROM      CL**3
00015 * ETKUT035. FOR EACH RECORD READ, IT UPDATES OR DELETES ROWS ON      CL**3
00016 * THE APPROPRIATE TABLE.  THIS IS THE REVALIDATION PORTION OF THE    CL**3
00017 * RECYCLE.                                                        ETKUP035
00018 ******************************************************************ETKUP035
260107*  DATE  08/19/21                                                         
260107*  CHANGE MADE:                                                           
260107*   ADD COPYBOOK DPWS991 TO MAKE CALL TO DPKUT901 DYNAMIC                 
260107*  MADE BY: JIM HUNTER              WR# : CHG0260107                      
260107******************************************************************   CL**7
00019                                                                   ETKUP035
00020                                                                   ETKUP035
00021                                                                   ETKUP035
00022  ENVIRONMENT DIVISION.                                            ETKUP035
00023                                                                   ETKUP035
00024                                                                   ETKUP035
00025  CONFIGURATION SECTION.                                           ETKUP035
00026                                                                   ETKUP035
00027  SOURCE-COMPUTER.    IBM-3090.                                    ETKUP035
00028  OBJECT-COMPUTER.    IBM-3090.                                    ETKUP035
00029                                                                   ETKUP035
00030                                                                   ETKUP035
00031  INPUT-OUTPUT SECTION.                                            ETKUP035
00032                                                                   ETKUP035
00033  FILE-CONTROL.                                                    ETKUP035
00034                                                                   ETKUP035
00035  DATA DIVISION.                                                   ETKUP035
00036                                                                   ETKUP035
00037                                                                   ETKUP035
00038  WORKING-STORAGE SECTION.                                         ETKUP035
00039                                                                   ETKUP035
00040  01  ABEND-CODE                       PIC S9(04)    COMP SYNC     ETKUP035
00041                                                     VALUE +0.     ETKUP035
00042  01  PO-UPD-DEL-RECORD.                                              CL**3
00043      05 UD-TABLE-NAME             PIC X(07).                         CL**3
00044      05 UD-TP-DKEY                PIC S9(9) COMP.                    CL**3
00045      05 UD-TP-DKEY-TMST           PIC X(26) VALUE SPACES.            CL**3
00046      05 UD-DATA-AREA              PIC X(354).                        CL**3
00047      05 UD-TFGTRHL-AREA REDEFINES UD-DATA-AREA.                      CL**3
00048         10  UD-TRN-SET-CDE        PIC X(3).                          CL**3
00049         10  UD-TRN-DTL-TXT        PIC 9(9).                          CL**3
00050         10  UD-RLSE-IND           PIC X(01).                         CL**3
00051         10  UD-RLSE-DTE           PIC X(10).                         CL**3
00052         10  UD-USER-ID            PIC X(08).                         CL**3
00053         10  FILLER                PIC X(323).                     ETKUP035
00054      05 UD-TTRPOHL-AREA REDEFINES UD-DATA-AREA.                      CL**3
00055         10  UD-PO-NBR             PIC 9(9).                          CL**3
00056         10  UD-DEPT-NBR           PIC X(03).                         CL**3
00057         10  UD-PO-RLSE-IND        PIC X(01).                         CL**3
00058         10  FILLER                PIC X(341).                     ETKUP035
00059      05 UD-TTRTRDT-AREA REDEFINES UD-DATA-AREA.                      CL**3
00060         10  UD-TRN-SET-SEQ-NBR    PIC S9(9) COMP.                    CL**3
00061         10  UD-TRN-SET-DATA       PIC X(350).                        CL**3
00062      05 UD-UPD-DEL-FLAG           PIC X(01) VALUE SPACES.            CL**3
00063         88  UD-UPDATE-DB                    VALUE 'U'.               CL**3
00064         88  UD-DELETE-DB                    VALUE 'D'.               CL**3
00065         88  UD-NO-DB-ACTION                 VALUE ' '.               CL**3
00066                                                                   ETKUP035
00067  01  WS-SAVE-DATE                     PIC X(10) VALUE SPACES.        CL**3
00068  01  WS-PO-NBR-CONVERT.                                           ETKUP035
00069      05  WS-PO-NBR                    PIC X(09) VALUE SPACES.     ETKUP035
00070      05  FILLER                       PIC X(21) VALUE SPACES.     ETKUP035
00071  01  WS-PO-NBR-TRPOHL REDEFINES WS-PO-NBR-CONVERT.                ETKUP035
00072      05  WS-PO-NBR-22                 PIC X(22).                  ETKUP035
00073      05  FILLER                       PIC X(08).                  ETKUP035
00074                                                                   ETKUP035
00075  01  PC-PROGRAM-CONSTANTS.                                        ETKUP035
00076      05  PC-MAX-RETRIES               PIC S9(04)    COMP SYNC     ETKUP035
00077                                                     VALUE +3.     ETKUP035
00078      05  PC-MAX-DEADLOCKS             PIC S9(04)    COMP SYNC     ETKUP035
00079                                                     VALUE +3.     ETKUP035
00080      05  PC-CKPT-JOB-NAME             PIC  X(08)    VALUE         ETKUP035
00081                                                          'ETK162'.ETKUP035
00082      05  PC-CKPT-PLAN-NAME            PIC  X(08)    VALUE         ETKUP035
00083                                                        'ETKUP035'.ETKUP035
00084      05  PC-TRAN-PRES-FUNC-CODES.                                 ETKUP035
00085          10  PC-TRAN-PRES-FUNC-OPEN   PIC  X(05)    VALUE 'OPEN'. ETKUP035
00086          10  PC-TRAN-PRES-FUNC-TRANS  PIC  X(05)    VALUE 'TRANS'.ETKUP035
00087          10  PC-TRAN-PRES-FUNC-RSTRT  PIC  X(05)    VALUE 'RSTRT'.ETKUP035
00088          10  PC-TRAN-PRES-FUNC-CLOSE  PIC  X(05)    VALUE 'CLOSE'.ETKUP035
00089                                                                   ETKUP035
00090  01  PS-PROGRAM-SWITCHES.                                         ETKUP035
00091      05  PS-RESTART-REQUIRED-SW       PIC X         VALUE 'N'.    ETKUP035
00092          88  PS-RESTART-REQUIRED                    VALUE 'Y'.    ETKUP035
00093          88  PS-RESTART-NOT-REQUIRED                VALUE 'N'.    ETKUP035
00094                                                                   ETKUP035
00095  01  PV-PROGRAM-VARIABLES.                                        ETKUP035
00096      05  PV-SQL-SUB                   PIC 9(03)     VALUE ZERO.   ETKUP035
00097      05  PV-DEADLOCK-COUNTER          PIC 9(03)     VALUE ZERO.   ETKUP035
00098      05  PV-PARAGRAPH-NAME            PIC X(30)     VALUE SPACE.  ETKUP035
00099      05  PV-DB2-OPER-ATTEMPTED        PIC X(30)     VALUE SPACE.  ETKUP035
00100                                                                   ETKUP035
00101 ***************************************************************** ETKUP035
00102 *  DCLGEN FOR THE FUNCTIONAL GROUP HELD TABLE.                    ETKUP035
00103 ***************************************************************** ETKUP035
00104                                                                   ETKUP035
00105      EXEC SQL                                                     ETKUP035
00106          INCLUDE TFGTRHL                                          ETKUP035
00107      END-EXEC.                                                    ETKUP035
00108                                                                   ETKUP035
00109 ***************************************************************** ETKUP035
00110 *  DCLGEN FOR THE PO HELD TABLE.                                  ETKUP035
00111 ***************************************************************** ETKUP035
00112                                                                   ETKUP035
00113      EXEC SQL                                                     ETKUP035
00114          INCLUDE TTRPOHL                                          ETKUP035
00115      END-EXEC.                                                    ETKUP035
00116                                                                   ETKUP035
00117 ***************************************************************** ETKUP035
00118 *  DCLGEN FOR THE FUNCTIONAL GROUP HELD DATA TABLE.               ETKUP035
00119 ***************************************************************** ETKUP035
00120                                                                   ETKUP035
00121      EXEC SQL                                                     ETKUP035
00122          INCLUDE TFGTRDT                                          ETKUP035
00123      END-EXEC.                                                    ETKUP035
00124                                                                   ETKUP035
00125 ***************************************************************** ETKUP035
00126 *  COMMUNICATIONS AREA FOR DB2                                    ETKUP035
00127 ***************************************************************** ETKUP035
00128                                                                   ETKUP035
00129      COPY SQLCA2.                                                 ETKUP035
00130                                                                   ETKUP035
00131                                                                   ETKUP035
00132 ***************************************************************** ETKUP035
00133 *  DB2 FORMATTED ERROR MESSAGE                                    ETKUP035
00134 ***************************************************************** ETKUP035
00135                                                                   ETKUP035
00136      COPY DPWS004.                                                ETKUP035
00137                                                                   ETKUP035
00138                                                                   ETKUP035
00139 ***************************************************************** ETKUP035
00140 *  TRANSACTION PRESENTATION MODULE COMMUNICATIONS AREA            ETKUP035
00141 ***************************************************************** ETKUP035
00142                                                                   ETKUP035
260107     COPY DPWS991.                                                ETKUP035
00142                                                                   ETKUP035
00143      COPY DPLS001.                                                ETKUP035
00144      05  WORKING-STORAGE-PASSED.                                  ETKUP035
00145          10  FILLER                   PIC X.                      ETKUP035
00146                                                                   ETKUP035
00147  01  FILLER                           PIC  X(4096)  VALUE SPACE.  ETKUP035
00148                                                                   ETKUP035
00149 ******************************************************************ETKUP035
00150 *  COMMUNICATIONS AREA FOR DB2                                   *ETKUP035
00151 ******************************************************************ETKUP035
00152                                                                   ETKUP035
00153      EXEC SQL                                                     ETKUP035
00154          INCLUDE SQLCA                                            ETKUP035
00155      END-EXEC.                                                    ETKUP035
00156  EJECT                                                            ETKUP035
00157  PROCEDURE DIVISION.                                              ETKUP035
00158                                                                   ETKUP035
00159                                                                   ETKUP035
00160  A100-PROGRAM-MAINLINE.                                           ETKUP035
00161                                                                   ETKUP035
00162                                                                   ETKUP035
00163      MOVE PC-TRAN-PRES-FUNC-OPEN TO DP001-FUNC-CODE.              ETKUP035
00164      PERFORM Z100-BUILD-COMM-AREA-TRAN-PRES.                      ETKUP035
00165                                                                   ETKUP035
00166      PERFORM B200-PROCESS-PO-RECYCLE-RECS                         ETKUP035
00167          UNTIL DP001-PREP-TRANS-EOF.                              ETKUP035
00168                                                                   ETKUP035
00169      MOVE PC-TRAN-PRES-FUNC-CLOSE TO DP001-FUNC-CODE.             ETKUP035
00170      PERFORM Z100-BUILD-COMM-AREA-TRAN-PRES.                      ETKUP035
00171                                                                   ETKUP035
00172                                                                   ETKUP035
00173      STOP RUN.                                                    ETKUP035
00174                                                                   ETKUP035
00175                                                                   ETKUP035
00176 ******************************************************************ETKUP035
00177 * PROCESS THE RECORDS ON THE PO RECLYCLE FILE.  FOR EACH          ETKUP035
00178 * RECORD ON THE FILE, DELETE OR UPDATE A ROW IN THE APPROPRIATE      CL**4
00179 * TABLE.                                                          ETKUP035
00180 ******************************************************************ETKUP035
00181  B200-PROCESS-PO-RECYCLE-RECS.                                    ETKUP035
00182                                                                   ETKUP035
00183      SET PS-RESTART-NOT-REQUIRED TO TRUE.                         ETKUP035
00184                                                                   ETKUP035
00185      PERFORM C100-BUILD-UPD-DEL-ROW.                                 CL**3
00186                                                                   ETKUP035
00187 *    *------------------------------------------------------------ETKUP035
00188 *    * IF A RESTART IS REQUIRED, ISSUE A RESTART CALL RATHER THAN ETKUP035
00189 *    * A TRANS CALL. THIS WILL ALLOW PROCESSING TO RESUME WITH    ETKUP035
00190 *    * THE FIRST TRANSACTION AFTER THE LAST COMMIT.               ETKUP035
00191 *    *------------------------------------------------------------ETKUP035
00192      IF PS-RESTART-REQUIRED                                       ETKUP035
00193          MOVE PC-TRAN-PRES-FUNC-RSTRT TO DP001-FUNC-CODE          ETKUP035
00194      ELSE                                                         ETKUP035
00195          MOVE PC-TRAN-PRES-FUNC-TRANS TO DP001-FUNC-CODE          ETKUP035
00196      END-IF.                                                      ETKUP035
00197                                                                   ETKUP035
00198      PERFORM Z100-BUILD-COMM-AREA-TRAN-PRES.                      ETKUP035
00199 *                                                                 ETKUP035
00200 ******************************************************************ETKUP035
00201 * DETERMINE WHICH TABLE THE ROW SHOULD BE UPDATED INTO AND           CL**5
00202 * PERFORM THE APPROPRIATE UPDATE.                                    CL**5
00203 ******************************************************************ETKUP035
00204  C100-BUILD-UPD-DEL-ROW.                                             CL**3
00205                                                                   ETKUP035
00206      IF UD-TABLE-NAME = 'TFGTRHL'                                    CL**3
00207         AND UD-UPDATE-DB                                             CL**4
00208                                                                      CL**4
00209         MOVE UD-TRN-DTL-TXT TO WS-PO-NBR                             CL**3
00210                                                                   ETKUP035
00211         EXEC SQL                                                  ETKUP035
00212            SET :WS-SAVE-DATE = CURRENT DATE                          CL**3
00213         END-EXEC                                                  ETKUP035
00214                                                                   ETKUP035
00215         EVALUATE TRUE                                             ETKUP035
00216            WHEN SQLCODE = 0                                       ETKUP035
00217               CONTINUE                                            ETKUP035
00218            WHEN OTHER                                             ETKUP035
00219               MOVE 'C100-BUILD-UPD-DEL-ROW' TO                       CL**3
00220                    PV-PARAGRAPH-NAME                              ETKUP035
00221               MOVE 'SELECT CURRENT TIMESTAMP' TO                  ETKUP035
00222                    PV-DB2-OPER-ATTEMPTED                          ETKUP035
00223               PERFORM Z900-SQL-ABEND                              ETKUP035
00224         END-EVALUATE                                              ETKUP035
00225                                                                   ETKUP035
00226         PERFORM Z200-UPDATE-TFGTRHL-ROW                              CL**4
00227      ELSE                                                            CL**5
00228         IF UD-TABLE-NAME = 'TTRPOHL'                                 CL**5
00229            AND UD-UPDATE-DB                                          CL**5
00230            MOVE UD-PO-NBR TO WS-PO-NBR                               CL**5
00231            PERFORM Z300-UPDATE-TTRPOHL-ROW                           CL**5
00232         ELSE                                                         CL**5
00233            IF UD-TABLE-NAME = 'TTRPOHL'                              CL**5
00234               AND UD-DELETE-DB                                       CL**5
00235               PERFORM Z350-DELETE-TTRPOHL-ROW                        CL**5
00236            ELSE                                                      CL**5
00237               IF UD-TABLE-NAME = 'TFGTRDT'                           CL**5
00238                  AND UD-UPDATE-DB                                    CL**5
00239                  PERFORM Z400-UPDATE-TFGTRDT-ROW                     CL**5
00240               ELSE                                                   CL**5
00241                  IF UD-TABLE-NAME = 'TFGTRDT'                        CL**5
00242                     AND UD-DELETE-DB                                 CL**5
00243                     PERFORM Z450-DELETE-TFGTRDT-ROW                  CL**5
00244                  END-IF                                              CL**5
00245               END-IF                                                 CL**5
00246            END-IF                                                    CL**5
00247         END-IF                                                       CL**5
00248      END-IF.                                                      ETKUP035
00249 *                                                                 ETKUP035
00250 ******************************************************************ETKUP035
00251 * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION      ETKUP035
00252 * PRESENTATION MODULE.                                            ETKUP035
00253 ******************************************************************ETKUP035
00254                                                                   ETKUP035
00255  Z100-BUILD-COMM-AREA-TRAN-PRES.                                  ETKUP035
00256                                                                   ETKUP035
00257      MOVE LENGTH OF WORKING-STORAGE-PASSED                        ETKUP035
00258                              TO DP001-WORK-STRG-LENGTH.           ETKUP035
00259      MOVE PC-CKPT-JOB-NAME   TO DP001-JOB-NAME.                   ETKUP035
00260      MOVE PC-CKPT-PLAN-NAME  TO DP001-PLAN-NAME.                  ETKUP035
00261      PERFORM Z900-CALL-TRANS-PRES-MODULE.                         ETKUP035
00262      MOVE DP001-TRANS-RECORD TO PO-UPD-DEL-RECORD.                   CL**3
00263      IF DP001-CKPT-TAKEN                                          ETKUP035
00264          INITIALIZE PV-DEADLOCK-COUNTER                           ETKUP035
00265      END-IF.                                                      ETKUP035
00266                                                                   ETKUP035
00267 ******************************************************************ETKUP035
00268 *  UPDATE ROW IN THE TFGTRHL TABLE                                   CL**5
00269 ******************************************************************ETKUP035
00270  Z200-UPDATE-TFGTRHL-ROW.                                            CL**4
00271 *                                                                 ETKUP035
00272      PERFORM                                                      ETKUP035
00273          WITH TEST AFTER                                          ETKUP035
00274          VARYING PV-SQL-SUB                                       ETKUP035
00275          FROM 1 BY 1                                              ETKUP035
00276          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       ETKUP035
00277                    OR SQLCODE = -911                              ETKUP035
00278                    OR SQLCODE = 0)                                ETKUP035
00279 *                                                                 ETKUP035
00280          EXEC SQL                                                 ETKUP035
00281              UPDATE TFGTRHL                                          CL**4
00282              SET RLSE_DTE = :WS-SAVE-DATE,                           CL**4
00283                  RLSE_IND = 'Y',                                     CL**4
00284                  USER_ID  = :PC-CKPT-JOB-NAME                        CL**6
00285              WHERE TP_DKEY      = :UD-TP-DKEY AND                    CL**4
00286                    TP_DKEY_TMST = :UD-TP-DKEY-TMST                   CL**4
00287          END-EXEC                                                 ETKUP035
00288 *                                                                 ETKUP035
00289          EVALUATE TRUE                                            ETKUP035
00290              WHEN SQLCODE = -904                                  ETKUP035
00291              WHEN SQLCODE = 0                                     ETKUP035
00292                  CONTINUE                                         ETKUP035
00293              WHEN SQLCODE = -911                                  ETKUP035
00294                  ADD +1 TO PV-DEADLOCK-COUNTER                    ETKUP035
00295                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS        ETKUP035
00296                      MOVE 'Z200-UPDATE-TFGTRHL-ROW'                  CL**4
00297                                      TO PV-PARAGRAPH-NAME         ETKUP035
00298                      MOVE 'MAX DEADLOCKS EXCEEDED'                ETKUP035
00299                                      TO PV-DB2-OPER-ATTEMPTED     ETKUP035
00300                      PERFORM Z900-SQL-ABEND                       ETKUP035
00301                  ELSE                                             ETKUP035
00302                      SET PS-RESTART-REQUIRED TO TRUE              ETKUP035
00303                  END-IF                                           ETKUP035
00304              WHEN SQLWARN NOT = SPACES                            ETKUP035
00305              WHEN SQLCODE NOT = ZERO                              ETKUP035
00306                  MOVE 'Z200-UPDATE-TFGTRHL-ROW'                      CL**4
00307                                TO  PV-PARAGRAPH-NAME              ETKUP035
00308                  MOVE 'UPDATE ROW INTO TFGTRHL'                      CL**5
00309                                TO  PV-DB2-OPER-ATTEMPTED          ETKUP035
00310                  PERFORM Z900-SQL-ABEND                           ETKUP035
00311          END-EVALUATE                                             ETKUP035
00312      END-PERFORM.                                                 ETKUP035
00313 *                                                                 ETKUP035
00314      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        ETKUP035
00315          MOVE 'Z200-UPDATE-TFGTRHL-ROW'                              CL**4
00316                                TO  PV-PARAGRAPH-NAME              ETKUP035
00317          MOVE 'VERIFY RETRIES EXCEEDED'                           ETKUP035
00318                                TO  PV-DB2-OPER-ATTEMPTED          ETKUP035
00319          PERFORM Z900-SQL-ABEND                                   ETKUP035
00320      END-IF.                                                      ETKUP035
00321 *                                                                 ETKUP035
00322 ******************************************************************ETKUP035
00323 *  UPDATE ROW IN THE TTRPOHL TABLE                                   CL**5
00324 ******************************************************************ETKUP035
00325  Z300-UPDATE-TTRPOHL-ROW.                                            CL**5
00326 *                                                                 ETKUP035
00327      PERFORM                                                      ETKUP035
00328          WITH TEST AFTER                                          ETKUP035
00329          VARYING PV-SQL-SUB                                       ETKUP035
00330          FROM 1 BY 1                                              ETKUP035
00331          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       ETKUP035
00332                    OR SQLCODE = -911                              ETKUP035
00333                    OR SQLCODE = 0)                                ETKUP035
00334 *                                                                 ETKUP035
00335          EXEC SQL                                                 ETKUP035
00336              UPDATE TTRPOHL                                          CL**5
00337              SET DEPT_NBR  = :UD-DEPT-NBR                            CL**5
00338              WHERE TP_DKEY      = :UD-TP-DKEY AND                    CL**5
00339                    TP_DKEY_TMST = :UD-TP-DKEY-TMST                   CL**5
00340          END-EXEC                                                 ETKUP035
00341 *                                                                 ETKUP035
00342          EVALUATE TRUE                                            ETKUP035
00343              WHEN SQLCODE = -904                                  ETKUP035
00344              WHEN SQLCODE = 0                                     ETKUP035
00345                  CONTINUE                                         ETKUP035
00346              WHEN SQLCODE = -911                                  ETKUP035
00347                  ADD +1 TO PV-DEADLOCK-COUNTER                    ETKUP035
00348                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS        ETKUP035
00349                      MOVE 'Z300-UPDATE-TTRPOHL-ROW'                  CL**5
00350                                      TO PV-PARAGRAPH-NAME         ETKUP035
00351                      MOVE 'MAX DEADLOCKS EXCEEDED'                ETKUP035
00352                                      TO PV-DB2-OPER-ATTEMPTED     ETKUP035
00353                      PERFORM Z900-SQL-ABEND                       ETKUP035
00354                  ELSE                                             ETKUP035
00355                      SET PS-RESTART-REQUIRED TO TRUE              ETKUP035
00356                  END-IF                                           ETKUP035
00357              WHEN SQLWARN NOT = SPACES                            ETKUP035
00358              WHEN SQLCODE NOT = ZERO                              ETKUP035
00359                  MOVE 'Z300-UPDATE-TTRPOHL-ROW'                      CL**5
00360                                TO  PV-PARAGRAPH-NAME              ETKUP035
00361                  MOVE 'UPDATE ROW INTO TTRPOHL'                      CL**5
00362                                TO  PV-DB2-OPER-ATTEMPTED          ETKUP035
00363                  PERFORM Z900-SQL-ABEND                           ETKUP035
00364          END-EVALUATE                                             ETKUP035
00365      END-PERFORM.                                                 ETKUP035
00366 *                                                                 ETKUP035
00367      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        ETKUP035
00368          MOVE 'Z300-UPDATE-TTRPOHL-ROW'                              CL**5
00369                                TO  PV-PARAGRAPH-NAME              ETKUP035
00370          MOVE 'VERIFY RETRIES EXCEEDED'                           ETKUP035
00371                                TO  PV-DB2-OPER-ATTEMPTED          ETKUP035
00372          PERFORM Z900-SQL-ABEND                                   ETKUP035
00373      END-IF.                                                      ETKUP035
00374 *                                                                 ETKUP035
00375 ******************************************************************   CL**5
00376 *  DELETE ROW IN THE TTRPOHL TABLE                                   CL**5
00377 ******************************************************************   CL**5
00378  Z350-DELETE-TTRPOHL-ROW.                                            CL**5
00379 *                                                                    CL**5
00380      PERFORM                                                         CL**5
00381          WITH TEST AFTER                                             CL**5
00382          VARYING PV-SQL-SUB                                          CL**5
00383          FROM 1 BY 1                                                 CL**5
00384          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**5
00385                    OR SQLCODE = -911                                 CL**5
00386                    OR SQLCODE = 0)                                   CL**5
00387 *                                                                    CL**5
00388          EXEC SQL                                                    CL**5
00389              DELETE FROM TTRPOHL                                     CL**5
00390              WHERE TP_DKEY      = :UD-TP-DKEY AND                    CL**5
00391                    TP_DKEY_TMST = :UD-TP-DKEY-TMST                   CL**5
00392          END-EXEC                                                    CL**5
00393 *                                                                    CL**5
00394          EVALUATE TRUE                                               CL**5
00395              WHEN SQLCODE = -904                                     CL**5
00396              WHEN SQLCODE = 0                                        CL**5
00397                  CONTINUE                                            CL**5
00398              WHEN SQLCODE = -911                                     CL**5
00399                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL**5
00400                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL**5
00401                      MOVE 'Z350-DELETE-TTRPOHL-ROW'                  CL**5
00402                                      TO PV-PARAGRAPH-NAME            CL**5
00403                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL**5
00404                                      TO PV-DB2-OPER-ATTEMPTED        CL**5
00405                      PERFORM Z900-SQL-ABEND                          CL**5
00406                  ELSE                                                CL**5
00407                      SET PS-RESTART-REQUIRED TO TRUE                 CL**5
00408                  END-IF                                              CL**5
00409              WHEN SQLWARN NOT = SPACES                               CL**5
00410              WHEN SQLCODE NOT = ZERO                                 CL**5
00411                  MOVE 'Z350-DELETE-TTRPOHL-ROW'                      CL**5
00412                                TO  PV-PARAGRAPH-NAME                 CL**5
00413                  MOVE 'DELETE ROW FROM TTRPOHL'                      CL**5
00414                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00415                  PERFORM Z900-SQL-ABEND                              CL**5
00416          END-EVALUATE                                                CL**5
00417      END-PERFORM.                                                    CL**5
00418 *                                                                    CL**5
00419      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**5
00420          MOVE 'Z350-DELETE-TTRPOHL-ROW'                              CL**5
00421                                TO  PV-PARAGRAPH-NAME                 CL**5
00422          MOVE 'VERIFY RETRIES EXCEEDED'                              CL**5
00423                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00424          PERFORM Z900-SQL-ABEND                                      CL**5
00425      END-IF.                                                         CL**5
00426 *                                                                    CL**5
00427 ******************************************************************ETKUP035
00428 *  UPDATE ROW IN THE TFGTRDT TABLE                                   CL**5
00429 ******************************************************************ETKUP035
00430  Z400-UPDATE-TFGTRDT-ROW.                                            CL**5
00431 *                                                                 ETKUP035
00432      PERFORM                                                      ETKUP035
00433          WITH TEST AFTER                                          ETKUP035
00434          VARYING PV-SQL-SUB                                       ETKUP035
00435          FROM 1 BY 1                                              ETKUP035
00436          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       ETKUP035
00437                    OR SQLCODE = -911                              ETKUP035
00438                    OR SQLCODE = 0)                                ETKUP035
00439 *                                                                 ETKUP035
00440          EXEC SQL                                                 ETKUP035
00441              UPDATE TFGTRDT                                          CL**5
00442              SET TRN_SET_DATA = :UD-TRN-SET-DATA                     CL**5
00443              WHERE TP_DKEY         = :UD-TP-DKEY AND                 CL**5
00444                    TP_DKEY_TMST    = :UD-TP-DKEY-TMST AND            CL**5
00445                    TRN_SET_SEQ_NBR = :UD-TRN-SET-SEQ-NBR             CL**5
00446          END-EXEC                                                 ETKUP035
00447 *                                                                 ETKUP035
00448          EVALUATE TRUE                                            ETKUP035
00449              WHEN SQLCODE = -904                                  ETKUP035
00450              WHEN SQLCODE = 0                                     ETKUP035
00451                  CONTINUE                                         ETKUP035
00452              WHEN SQLCODE = -911                                  ETKUP035
00453                  ADD +1 TO PV-DEADLOCK-COUNTER                    ETKUP035
00454                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS        ETKUP035
00455                      MOVE 'Z400-UPDATE-TFGTRDT-ROW'                  CL**5
00456                                      TO PV-PARAGRAPH-NAME         ETKUP035
00457                      MOVE 'MAX DEADLOCKS EXCEEDED'                ETKUP035
00458                                      TO PV-DB2-OPER-ATTEMPTED     ETKUP035
00459                      PERFORM Z900-SQL-ABEND                       ETKUP035
00460                  ELSE                                             ETKUP035
00461                      SET PS-RESTART-REQUIRED TO TRUE              ETKUP035
00462                  END-IF                                           ETKUP035
00463              WHEN SQLWARN NOT = SPACES                            ETKUP035
00464              WHEN SQLCODE NOT = ZERO                              ETKUP035
00465                  MOVE 'Z400-UPDATE-TFGTRDT-ROW'                      CL**5
00466                                TO  PV-PARAGRAPH-NAME              ETKUP035
00467                  MOVE 'UPDATE ROW INTO TFGTRDT'                      CL**5
00468                                TO  PV-DB2-OPER-ATTEMPTED          ETKUP035
00469                  PERFORM Z900-SQL-ABEND                           ETKUP035
00470          END-EVALUATE                                             ETKUP035
00471      END-PERFORM.                                                 ETKUP035
00472 *                                                                 ETKUP035
00473      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        ETKUP035
00474          MOVE 'Z400-UPDATE-TFGTRDT-ROW'                              CL**5
00475                                TO  PV-PARAGRAPH-NAME              ETKUP035
00476          MOVE 'VERIFY RETRIES EXCEEDED'                           ETKUP035
00477                                TO  PV-DB2-OPER-ATTEMPTED          ETKUP035
00478          PERFORM Z900-SQL-ABEND                                   ETKUP035
00479      END-IF.                                                      ETKUP035
00480 *                                                                 ETKUP035
00481 ******************************************************************   CL**5
00482 *  DELETE ROW FROM THE TFGTRDT TABLE                                 CL**5
00483 ******************************************************************   CL**5
00484  Z450-DELETE-TFGTRDT-ROW.                                            CL**5
00485 *                                                                    CL**5
00486      PERFORM                                                         CL**5
00487          WITH TEST AFTER                                             CL**5
00488          VARYING PV-SQL-SUB                                          CL**5
00489          FROM 1 BY 1                                                 CL**5
00490          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**5
00491                    OR SQLCODE = -911                                 CL**5
00492                    OR SQLCODE = 0)                                   CL**5
00493 *                                                                    CL**5
00494          EXEC SQL                                                    CL**5
00495              DELETE FROM TFGTRDT                                     CL**5
00496              WHERE TP_DKEY         = :UD-TP-DKEY AND                 CL**5
00497                    TP_DKEY_TMST    = :UD-TP-DKEY-TMST AND            CL**5
00498                    TRN_SET_SEQ_NBR = :UD-TRN-SET-SEQ-NBR             CL**5
00499          END-EXEC                                                    CL**5
00500 *                                                                    CL**5
00501          EVALUATE TRUE                                               CL**5
00502              WHEN SQLCODE = -904                                     CL**5
00503              WHEN SQLCODE = 0                                        CL**5
00504                  CONTINUE                                            CL**5
00505              WHEN SQLCODE = -911                                     CL**5
00506                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL**5
00507                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL**5
00508                      MOVE 'Z450-DELETE-TFGTRDT-ROW'                  CL**5
00509                                      TO PV-PARAGRAPH-NAME            CL**5
00510                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL**5
00511                                      TO PV-DB2-OPER-ATTEMPTED        CL**5
00512                      PERFORM Z900-SQL-ABEND                          CL**5
00513                  ELSE                                                CL**5
00514                      SET PS-RESTART-REQUIRED TO TRUE                 CL**5
00515                  END-IF                                              CL**5
00516              WHEN SQLWARN NOT = SPACES                               CL**5
00517              WHEN SQLCODE NOT = ZERO                                 CL**5
00518                  MOVE 'Z450-DELETE-TFGTRDT-ROW'                      CL**5
00519                                TO  PV-PARAGRAPH-NAME                 CL**5
00520                  MOVE 'DELETE ROW FROM TFGTRDT'                      CL**5
00521                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00522                  PERFORM Z900-SQL-ABEND                              CL**5
00523          END-EVALUATE                                                CL**5
00524      END-PERFORM.                                                    CL**5
00525 *                                                                    CL**5
00526      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**5
00527          MOVE 'Z450-DELETE-TFGTRDT-ROW'                              CL**5
00528                                TO  PV-PARAGRAPH-NAME                 CL**5
00529          MOVE 'VERIFY RETRIES EXCEEDED'                              CL**5
00530                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00531          PERFORM Z900-SQL-ABEND                                      CL**5
00532      END-IF.                                                         CL**5
00533 *                                                                    CL**5
00534 ******************************************************************ETKUP035
00535 * ABEND THE PROGRAM IN THE EVENT THAT AN SQL ERROR OR WARNING     ETKUP035
00536 * CODE OCCURS.                                                    ETKUP035
00537 ******************************************************************ETKUP035
00538  Z900-SQL-ABEND.                                                  ETKUP035
00539                                                                   ETKUP035
00540      DISPLAY '** ABEND     **'.                                   ETKUP035
00541      DISPLAY '** PROGRAM   ** = ETKUP035'.                        ETKUP035
00542      DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              ETKUP035
00543      DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.          ETKUP035
00544                                                                   ETKUP035
00545                                                                   ETKUP035
00546 ******************************************************************ETKUP035
00547 * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    ETKUP035
00548 * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         ETKUP035
00549 * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     ETKUP035
00550 * FORMAT.                                                         ETKUP035
00551 ******************************************************************ETKUP035
00552      COPY DPPD004.                                                ETKUP035
00553                                                                   ETKUP035
00554      MOVE +4013 TO ABEND-CODE.                                    ETKUP035
00555      CALL 'ILBOABN0' USING ABEND-CODE.                            ETKUP035
00556                                                                   ETKUP035
00557 ******************************************************************ETKUP035
00558 * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    ETKUP035
00559 * MENTS THAT ARE REQUIRED TO INVOKE THE TRANSACTION PRESENTATION  ETKUP035
00560 * MODULE (DPKUT901) OF THE DB2 CHECKPOINT RESTART SYSTEM.         ETKUP035
00561 ******************************************************************ETKUP035
00562      COPY DPPD001.                                                ETKUP035
