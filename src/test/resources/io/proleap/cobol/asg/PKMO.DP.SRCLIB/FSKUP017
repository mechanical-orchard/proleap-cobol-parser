000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.    FSKUP017.                                                 
000400 AUTHOR.        CHUCK ALBERTY -- EXACTA.                                  
000500 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000600 DATE-WRITTEN.  09/24/91.                                                 
000700 DATE-COMPILED.                                                           
000800                                                                          
000900******************************************************************        
001000*        WEEKLY SKU FS REPORT INDICATOR UPDATE PROGRAM                    
001100*                                                                         
001200*  THE PURPOSE OF THIS PROGRAM IS TO UPDATE THE FS_RPT_IND COLUMN         
001300*  FOR SELECTED ROWS IN THE SKU TABLE (TSK).  SELECTED ROWS ARE           
001400*  THOSE ROWS THAT MEET THE FOLLOWING CRITERIA:                           
001500*          STATUS CODE:  10      (NEW)                                    
001600*                        20      (ACTIVE)                                 
001700*                        25      (MIXED)                                  
001800*                        30      (CLEARANCE)                              
001900*                        40      (PROMOTIONAL)                            
002000*                        90                                               
002100*           FS RPT IND:  NOT 'Y'                                          
002200*      FIRST RCVD DATE:  0       (NO RECEIPTS FOR THE ITEM)               
002300*                                                                         
002400*  WHEN A SKU PASSES THE ABOVE EDITS, IT IS THEN CHECKED AGAINST          
002500*  THE DB2 PO DATABASE SO THE UPDATE WILL ONLY TAKE PLACE WHEN            
002600*  THE CURRENT DATE IS WITHIN 70 DAYS (OR ANY NUMBER OF DAYS              
002700*  SPECIFIED IN THE CONTROL CARD) OF THE FIRST PURCHASE ORDER             
002800*  SHIP DATE.                                                             
002900*                                                                         
003000*  THIS PROGRAM WILL BE RUN ON A WEEKLY BASIS AND WILL UPDATE             
003100*  THE FS_RPT_IND ONLY FOR SKUS THAT ARE WITHIN THE SPECIFIED             
003200*  TIME FRAME.  THE CUTOFF DATE IS DETERMINED BY ADDING THE               
003300*  NUMBER OF DAYS SPECIFIED IN THE CONTROL CARD TO THE CURRENT            
003400*  DATE.                                                                  
003500*                                                                         
003600*  CHANGED 02/95 BY M. DEERING - THE CALL TO THE OLD DATE ROUTINE         
003700*                                (DPKUT105) WAS REPLACED WITH A           
003800*                                CALL TO THE NEW DATE ROUTINE             
003900*                                (DPKUT500).                              
003910* -------------------------------------------------------------- *      11
004000*  CHANGED 06/96    THIS PROGRAM WAS PULLED  OUT OF PRODUCTION            
004100*  BY ANN RAISLER   SEVERAL MONTHS PRIOR DUE TO A DATE ROUTINE            
004200*                   PROBLEM. WITH THE AUTOMATION OF REPORT IND            
004300*                   THIS PROGRAM WAS RENEWED. THE FOLLOWING               
004400*                   CHANGES WERE MADE AT THAT TIME:                       
004500*                   - REMOVED CONTROL CARD                                
004600*                   - REMOVED DATE ROUTINE...USED DB2 CALC                
004700*                   - ADD RPT IND TURNOFF FUNCTION                        
004800*                   - ADD READ TO TMDSEDP FOR DATE TIMEFRAME              
004900*                   - ADD NEW STATUS CODES OF 30 AND 90                   
005000*                   - CHANGE OUTPUT TO FLAT FILE INSTEAD OF               
005100*                     SYSTEM LOG.                                         
005110* -------------------------------------------------------------- *      11
005200*  CHANGED 08/96    I COMMENTTED OUT CODE THAT COULD POSSIBLELY           
005300*  BY CINDY ROMNSKO CHANGE AN ACTIVE SKUS RPT INDICATOR TO "N".           
005400*                   ONCE THE INDICATOR IS SET TO "Y THE ONLY TIME         
005500*                   IT SHOULD BE CHANGED TO A "N" IS WHEN IS GOES         
005600*                   CLEARANCE.                                            
005700* -------------------------------------------------------------- *      11
005800*  CHANGED 03/97    I CHANGED THE CODE FOR THE TSK LOG RECORD FOR         
005900*  BY DAN GRUBER    THE FS-RPT-IND FROM A '01' TO A '23' TO MAKE          
006000*                   IT CONSISTANT WITH PROGRAM FSKCS018.                  
006100* -------------------------------------------------------------- *      11
006200*  CHANGED 03/19/98 Y2K LOGIC CHANGES TO USE 80 AS THE WINDOWING          
006300*  BY PAUL KEBER    DATE YEAR.  ALSO REMOVED HARDCODED CUTOFF DATE        
006400*                   FROM CHECK FOR STATUS 90 RECORDS.                     
006500* -------------------------------------------------------------- *      11
006600*  CHANGED 11/16/99 REGIONAL CLEARANCE PRICING PROJECT                    
006700*  BY CRAIG FAHEY   CHANGED THE CODE TO TREAT STATUS CODES OF 25          
006800*                   SIMILAR TO STATUS CODES OF 20.  WR#10760              
006900* -------------------------------------------------------------- *      11
007000*  CHANGED 05/21/01 ADDED CODE TO ONLY PERFORM PARAG. 3110- IF            
007100*  BY JOHN PRICE    TSK STATUS-CODE = 10, 20, 25, 40 AND THE              
007200*                   MDSEDP DAYS-BEF-SHIP-NBR NOT EQUAL TO "999"           
007300*                   AND THE TSK FS-RPT-IND IS "N"                         
007400* -------------------------------------------------------------- *      11
007500*  CHANGED 04/26/02 CHANGED THE DEPT/CLASS TABLE FROM 7500 ENTRIES        
007600*  BY MARY COLEMAN  TO 12000 ENTRIES IN RESPONSE TO ML OVERFLOWING        
007700*                   THEIR TABLE FOR DEPT/CLASS.                           
007800* -------------------------------------------------------------- *      11
007830*  04/04/03 - DONALD TOMLINSON                                          11
007840*             (WR23946)   NI-SKU CONVERSION.  REPLACE TSK DEPT,         11
007841*    CLASS, SEQ FIELDS WITH TSKXREF DEPT, SUBCLASS, SKU. CHANGES        11
007842*    TO 2100-, 2910-, 3010-, 3110-, 3120-, 3121-, 3900-, 3910-          11
007843*    PARAGRAPHS. COPY BOOK FSRD500O CHANGED REPLACING SEQ WITH          11
007844*    SKU.                                                               11
007850* -------------------------------------------------------------- *      11
007830*  05/06/04 - MARIA KLAMIK                                              11
007840*             (WR26355)   STORE NUMBER EXPANSION PROJECT.               11
007840*    RECOMPILE IN PROD FOR FSRD500O COPYBOOK CHANGES.                   11
007800* -------------------------------------------------------------- *      11
KP0206*  02/03/06 - KRISTIN PETERSON                                          11
KP0206*          (PM00846598)   FIXED ISSUE REPORTED BY MIO - SOME            11
KP0206*    STYLES WERE NOT INCLUDED IN THE STYLE SUMMARY BECAUSE              11
KP0206*    FS-RPT-IND WAS NOT BEING SET CORRECTLY.  INCREASED MAX NBR         11
KP0206*    OF TIMES PARAGRAPH 3000-MAIN-PROCESS MAY BE PERFORMED FROM         11
KP0206*    7500 TO 12000 TO COINCIDE WITH THE MAX NBR OF OCCURRENCES          11
KP0206*    FOR TABLE DEPT-CLASS-OCC.                                          11
KP0206* -------------------------------------------------------------- *      11
KP0207*  02/23/07 - KRISTIN PETERSON                                          11
KP0207*          (WR32104)  CHANGE REQUIRED DUE TO MARKDOWN OPTIMIZATION      11
KP0207*    CHANGES MAKING ALL STORES SAS STORES.  NEED TO NOW RETRIEVE        11
KP0207*    THE ORIGINAL CLEARANCE DATE FROM THE TUPCPLS STORE ZERO ROW        11
KP0207*    INSTEAD OF USING THE STATUS CHANGED DATE FROM TSK.                 11
KP0207* -------------------------------------------------------------- *      11
004968*  06/23/08 - TAISA KONTAROVICH                                         11
004968*          (INC000000382903) INCREASE RETRY TIMES TO 5 AND FIX          11
004968*          ABENDING LOGIC FOR SELECT STATEMENT.                         11
004968* -------------------------------------------------------------- *      11
CR0471*  02/08/13 - COGNIZANT                        CRQ000000040471.         11
CR0471*    THE INTERNAL TABLE SPACE IS INCREASED FROM 12000 TO 16000.         11
CR0471*    HENCE THE "PERFORM UNTIL" LIMIT FOR PARA 3000- AND 2100-           11
CR0471*    FOR TABLE DEPT-CLASS-OCC.                                          11
CR0471* -------------------------------------------------------------- *      11
007900******************************************************************        
008000                                                                          
008100                                                                          
008200 ENVIRONMENT DIVISION.                                                    
008300                                                                          
008400 CONFIGURATION SECTION.                                                   
008500                                                                          
008600 INPUT-OUTPUT SECTION.                                                    
008700                                                                          
008800 FILE-CONTROL.                                                            
008900                                                                          
009000     SELECT MAINT-LOG-FILE-OUT                                            
009100         ASSIGN TO OUT01.                                                 
009200                                                                          
009300                                                                          
009400 DATA DIVISION.                                                           
009500                                                                          
009600 FILE SECTION.                                                            
009700                                                                          
009800 FD  MAINT-LOG-FILE-OUT                                                   
009900     LABEL RECORDS ARE STANDARD                                           
010000     RECORDING MODE IS F                                                  
010100     BLOCK CONTAINS 0 RECORDS                                             
010200     RECORD CONTAINS 160 CHARACTERS                                       
010300     DATA RECORD IS MAINT-LOG-RECORD-OUT.                                 
010400                                                                          
010500 01  MAINT-LOG-RECORD-OUT            PIC  X(160).                         
010600                                                                          
010700                                                                          
010800 WORKING-STORAGE SECTION.                                                 
010900                                                                          
011000 01  WS-SWITCHES.                                                         
011100     05  WS-UPDATES-DONE-SW          PIC  X(01)    VALUE SPACE.           
011200         88  WS-UPDATES-DONE                       VALUE 'Y'.             
011300*                                                                         
011400     05  WS-SKU-ROW-FOUND            PIC  X(01)    VALUE SPACE.           
011500         88  SKU-ROW-FOUND                         VALUE 'Y'.             
011600         88  SKU-ROW-NOT-FOUND                     VALUE 'N'.             
011700*                                                                         
011800 01  WS-LITERALS.                                                         
011900     05  WS-LIT-BATCHPGM             PIC  X(08)    VALUE                  
012000         'BATCHPGM'.                                                      
012100     05  WS-LIT-PROGRAM-NAME         PIC  X(08)    VALUE                  
012200         'FSKUP017'.                                                      
012300     05  WS-LIT-DPT                  PIC  X(03)    VALUE 'DPT'.           
012400*                                                                         
012500*                                                                         
012600 01  WS-VARIABLES.                                                        
012700     05  WS-DEPTNO                   PIC  X(03)    VALUE SPACES.          
012800*                                                                         
012900*                                                                         
KP0207 01  PV-PROGRAM-VARIABLES.                                                
KP0207     05  PV-RETRY-CNT                PIC S9(04)    VALUE ZERO             
KP0207                                                   COMP SYNC.             
KP0207                                                                          
KP0207 01  PC-PROGRAM-CONSTANTS.                                                
004968     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +5               
KP0207                                                   COMP SYNC.             
KP0207                                                                          
013000 01  WS-MISC-FIELDS.                                                      
013100     05  WS-ABEND-CODE               PIC S9(04)    VALUE ZERO             
013200                                                   COMP SYNC.             
013300     05  WS-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.          
013400     05  WS-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
013500                                                                          
013600                                                                          
013700     05  WS-STAT-DATE                PIC 9(8).                            
013800                                                                          
013900     05  WS-STAT-CHGD-DATE REDEFINES WS-STAT-DATE.                        
014000         10  WS-STAT-CC              PIC 99.                              
014100         10  WS-STAT-YYMMDD          PIC 9(6).                            
014200         10  FILLER REDEFINES WS-STAT-YYMMDD.                             
014300             15  WS-STAT-YY          PIC 99.                              
014400             15  WS-STAT-MM          PIC 99.                              
014500             15  WS-STAT-DD          PIC 99.                              
014600                                                                          
014700     05  WS-SHIP-DATE-X              PIC X(10).                           
014800                                                                          
014900     05  WS-SHIP-DATE REDEFINES WS-SHIP-DATE-X.                           
015000         10  WS-SHIP-DATE-CCYY       PIC  9(04).                          
015100         10  FILLER                  PIC  X.                              
015200         10  WS-SHIP-DATE-MM         PIC  9(02).                          
015300         10  FILLER                  PIC  X.                              
015400         10  WS-SHIP-DATE-DD         PIC  9(02).                          
015500                                                                          
015600     05  WS-SHIP-CUTOFF-DATE.                                             
015700         10  WS-SHIP-CUTOFF-CCYY     PIC  9(04).                          
015800         10  WS-SHIP-CUTOFF-MM       PIC  9(02).                          
015900         10  WS-SHIP-CUTOFF-DD       PIC  9(02).                          
016000                                                                          
016100     05  WS-PLNSHP-DATE-X            PIC X(10).                           
016200                                                                          
016300     05  WS-PLNSHP-DATE REDEFINES WS-PLNSHP-DATE-X.                       
016400         10  WS-PLNSHP-DATE-CCYY     PIC  9(04).                          
016500         10  FILLER                  PIC  X.                              
016600         10  WS-PLNSHP-DATE-MM       PIC  9(02).                          
016700         10  FILLER                  PIC  X.                              
016800         10  WS-PLNSHP-DATE-DD       PIC  9(02).                          
016900                                                                          
KP0207     05  WS-ORIG-CLR-DATE-X          PIC X(10).                           
KP0207                                                                          
KP0207     05  WS-ORIG-CLR-DATE REDEFINES WS-ORIG-CLR-DATE-X.                   
KP0207         10  WS-ORIG-CLR-DATE-CC     PIC  9(02).                          
KP0207         10  WS-ORIG-CLR-DATE-YY     PIC  9(02).                          
KP0207         10  FILLER                  PIC  X.                              
KP0207         10  WS-ORIG-CLR-DATE-MM     PIC  9(02).                          
KP0207         10  FILLER                  PIC  X.                              
KP0207         10  WS-ORIG-CLR-DATE-DD     PIC  9(02).                          
KP0207                                                                          
017000     05  WS-PLNSHP.                                                       
017100         10  WS-PLNSHP-CCYY          PIC  9(04).                          
017200         10  WS-PLNSHP-MM            PIC  9(02).                          
017300         10  WS-PLNSHP-DD            PIC  9(02).                          
017400                                                                          
017500     05  WS-CLR-DATE-X               PIC X(10).                           
017600                                                                          
017700     05  WS-CLR-DATE REDEFINES WS-CLR-DATE-X.                             
017800         10  WS-CLR-DATE-CCYY        PIC  9(04).                          
017900         10  FILLER                  PIC  X.                              
018000         10  WS-CLR-DATE-MM          PIC  9(02).                          
018100         10  FILLER                  PIC  X.                              
018200         10  WS-CLR-DATE-DD          PIC  9(02).                          
018300                                                                          
018400     05  WS-CLR-CUTOFF-DATE.                                              
018500         10  WS-CLR-CUTOFF-CCYY      PIC  9(04).                          
018600         10  WS-CLR-CUTOFF-MM        PIC  9(02).                          
018700         10  WS-CLR-CUTOFF-DD        PIC  9(02).                          
018800                                                                          
018900     05  WS-TODAYS-DATE.                                                  
019000         10  WS-TODAYS-DATE-YY       PIC  9(02)    VALUE ZERO.            
019100         10  WS-TODAYS-DATE-MM       PIC  9(02)    VALUE ZERO.            
019200         10  WS-TODAYS-DATE-DD       PIC  9(02)    VALUE ZERO.            
019300                                                                          
019400     05  WS-TODAYS-TIME.                                                  
019500         10  WS-CURRENT-TIME.                                             
019600             15  WS-TODAYS-TIME-HH   PIC  9(02)    VALUE ZERO.            
019700             15  WS-TODAYS-TIME-MM   PIC  9(02)    VALUE ZERO.            
019800             15  WS-TODAYS-TIME-SS   PIC  9(02)    VALUE ZERO.            
019900         10  FILLER                  PIC  9(02)    VALUE ZERO.            
020000*MD 2/95                                                                  
020100     05  WS-DATE2.                                                        
020200         10  WS-DATE2-MM             PIC 9(2)      VALUE ZERO.            
020300         10  WS-DATE2-DD             PIC 9(2)      VALUE ZERO.            
020400         10  WS-DATE2-YY             PIC 9(2)      VALUE ZERO.            
020500*MD 2/95                                                                  
020600*                                                                         
020700     05  WS-ROWS-UPDATED             PIC 9(08)     VALUE ZERO.            
020800     05  WS-ROWS-UPDATED-Z           PIC ZZ,ZZZ,ZZZ.                      
KP0207                                                                          
KP0207     05  WS-TUPCPLS-SEL-CNT          PIC 9(08)     VALUE ZERO.            
KP0207     05  WS-TUPCPLS-SEL-CNT-Z        PIC ZZ,ZZZ,ZZZ.                      
020900*                                                                         
021000 01  CM-CHANGE-MESSAGE-LOG-AREA.                                          
021100     05  CM-CHANGE-TSK-MSG-23.                                            
021200         10  FILLER                  PIC  X(02)    VALUE '23'.            
021300         10  FILLER                  PIC  X(18)    VALUE                  
021400             'FS RPT IND:  OLD=('.                                        
021500         10  CM-FS-RPT-IND-OLD       PIC  X(01).                          
021600         10  FILLER                  PIC  X(09)    VALUE                  
021700             ')   NEW=('.                                                 
021800         10  CM-FS-RPT-IND-NEW       PIC  X(01).                          
021900         10  FILLER                  PIC  X(01)    VALUE                  
022000             ')'.                                                         
022100*                                                                         
022200*                                                                         
022300 01  WS-PROGRAM-ERROR-MESSAGES.                                           
022400     05  PE-MSG-1                    PIC  X(30)    VALUE                  
022500         'OPEN DPT_CLS_CSR CURSOR       '.                                
022600     05  PE-MSG-2                    PIC  X(30)    VALUE                  
022700         'FETCH OF DPT_CLS_CSR          '.                                
022800     05  PE-MSG-3                    PIC  X(30)    VALUE                  
022900         'CLOSE DPT_CLS_CSR CURSOR      '.                                
023000     05  PE-MSG-4                    PIC  X(30)    VALUE                  
023100         'OPEN SKU_CSR CURSOR           '.                                
023200     05  PE-MSG-5                    PIC  X(30)    VALUE                  
023300         'FETCH OF SKU_CSR              '.                                
023400     05  PE-MSG-6                    PIC  X(30)    VALUE                  
023500         'CLOSE SKU_CSR CURSOR          '.                                
023600     05  PE-MSG-7                    PIC  X(30)    VALUE                  
023700         'UPDATE TSK TABLE              '.                                
023800     05  PE-MSG-8                    PIC  X(30)    VALUE                  
023900         'CONTROL CARD DAYS NOT NUMERIC '.                                
024000     05  PE-MSG-9                    PIC  X(30)    VALUE                  
024100         'CALC SHIP CUTOFF DATE-TMDSEDP'.                                 
024200     05  PE-MSG-10                   PIC  X(30)    VALUE                  
024300         'CALC CLEAR CUTOFF DATE-TMDSEDP'.                                
024400     05  PE-MSG-12                   PIC  X(30)    VALUE                  
024500         'DEPT CLASS TABLE OVERFLOWED   '.                                
024600     05  PE-MSG-13                   PIC  X(33)    VALUE                  
024700         'BAD RETURN CODE FROM DPKUT105 -- '.                             
024800     05  PE-MSG-14                   PIC  X(33)    VALUE                  
024900         'SELECT OF SHIP DATE (TPLNSHP) -- '.                             
025000     05  PE-MSG-15                   PIC  X(33)    VALUE                  
025100         'BAD COMMIT ON THE TSK TABLE   -- '.                             
025200*                                                                         
025300*                                                                         
025400*                                                                         
025500******************************************************************        
025600**  DEPT/CLASS TABLE (ALL VALID DEPT/CLASS COMBINATIONS)                  
025700******************************************************************        
CR0471 01  DEPT-CLASS-TABLE                PIC X(80000)   VALUE SPACES.         
025900 01  DEPT-CLASS-TBL REDEFINES DEPT-CLASS-TABLE.                           
CR0471     05  DEPT-CLASS-OCC OCCURS 16000 TIMES                                
026100                        INDEXED BY DC-INDX.                               
026200         10  DC-DEPT                 PIC X(03).                           
026300         10  DC-CLASS                PIC X(02).                           
026400*                                                                         
026500*                                                                         
026600******************************************************************        
026700***  NI SKU CONVERSION WORK FIELDS                                        
026800******************************************************************        
026900     COPY ISWS006.                                                        
027000*                                                                         
027010*                                                                         
027020******************************************************************        
027030***  RECORD LAYOUT FOR THE MERCHANDISING SYSTEM MAINTENANCE LOG           
027040******************************************************************        
027050     COPY FSRD500O.                                                       
027060*                                                                         
027100*                                                                         
027200******************************************************************        
027300***  SQL RELATED INCLUDE STATEMENTS AND DCLGENS                           
027400******************************************************************        
027500     COPY DPWS004.                                                        
027600                                                                          
027700     EXEC SQL                                                             
027800          INCLUDE SQLCA                                                   
027900     END-EXEC.                                                            
028000                                                                          
028100     EXEC SQL                                                             
028200          INCLUDE TSK                                                     
028300     END-EXEC.                                                            
028400                                                                          
028410     EXEC SQL                                                             
028420          INCLUDE TSKXREF                                                 
028430     END-EXEC.                                                            
028440                                                                          
028500     EXEC SQL                                                             
028600          INCLUDE TMDSEAR                                                 
028700     END-EXEC.                                                            
028800                                                                          
028900     EXEC SQL                                                             
029000          INCLUDE TPURITM                                                 
029100     END-EXEC.                                                            
029200                                                                          
029300     EXEC SQL                                                             
029400          INCLUDE TPLNSHP                                                 
029500     END-EXEC.                                                            
029600                                                                          
029700     EXEC SQL                                                             
029800          INCLUDE TMDSEDP                                                 
029900     END-EXEC.                                                            
030000                                                                          
KP0207     EXEC SQL                                                             
KP0207          INCLUDE TUPCPLS                                                 
KP0207     END-EXEC.                                                            
KP0207                                                                          
030100                                                                          
030200******************************************************************        
030300**  CURSOR USED TO RETRIEVE DISTINCT DEPT/CLASS COMBINATIONS              
030400******************************************************************        
030500     EXEC SQL                                                             
030600          DECLARE DPT_CLS_CSR CURSOR                                      
030700           WITH HOLD FOR                                                  
030800           SELECT DISTINCT DEPT,                                          
031000                  SUBCLASS                                                
031100             FROM TSKXREF                                                 
031200            ORDER BY DEPT,                                                
031300                     SUBCLASS                                             
031400     END-EXEC.                                                            
031500*                                                                         
031600******************************************************************        
031700**  CURSOR USED TO RETRIEVE SKU AND SKU COLUMNS                           
031800******************************************************************        
031900     EXEC SQL                                                             
032000          DECLARE SKU_CSR CURSOR                                          
032100           WITH HOLD FOR                                                  
032200           SELECT DEPT,                                                   
032300                  SUBCLASS,                                               
032400                  SKU,                                                    
032600                  STATUS_CODE,                                            
032700                  ITEM_NMBR,                                              
032800                  FS_RPT_IND                                              
032900             FROM TSK                                                     
032910                 ,TSKXREF                                                 
033010            WHERE DEPT          = :SKXREF-DEPT                            
033100              AND SUBCLASS      = :SKXREF-SUBCLASS                        
033110              AND ITEM_NMBR     = ITM_NBR                                 
033200              AND STATUS_CODE        IN (10, 20, 25, 30, 40, 90)          
033300         ORDER BY DEPT,                                                   
033400                  SUBCLASS,                                               
033500                  SKU                                                     
033600     END-EXEC.                                                            
033700*                                                                         
033800 LINKAGE SECTION.                                                         
033900*                                                                         
034000*                                                                         
034100 PROCEDURE DIVISION.                                                      
034200                                                                          
034300 0000-PROGRAM-MAINLINE.                                                   
034400******************************************************************        
034500*  CONTROLS THE MAIN PROCESSING OF THE PROGRAM.                           
034600******************************************************************        
034700*                                                                         
034800     PERFORM 1000-INITIAL-PROCESS.                                        
034900*                                                                         
035000     PERFORM 2000-GET-DEPTS-AND-CLASSES.                                  
035100*                                                                         
035200     PERFORM 3000-MAIN-PROCESS                                            
035300       VARYING DC-INDX FROM 1 BY 1                                        
CR0471         UNTIL DC-INDX > 16000                                            
035500            OR WS-UPDATES-DONE.                                           
035600*                                                                         
035700     PERFORM 8000-END-PROCESS.                                            
035800*                                                                         
035900*                                                                         
KP0207     DISPLAY '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'.                    
KP0207     MOVE WS-TUPCPLS-SEL-CNT TO WS-TUPCPLS-SEL-CNT-Z.                     
KP0207     DISPLAY 'WS-TUPCPLS-SEL-CNT  = ' WS-TUPCPLS-SEL-CNT-Z.               
036000     DISPLAY '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'.                    
036100     MOVE WS-ROWS-UPDATED  TO  WS-ROWS-UPDATED-Z.                         
036200     DISPLAY 'WS-ROWS-UPDATED CNT = ' WS-ROWS-UPDATED-Z.                  
036300     DISPLAY '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'.                    
036400*                                                                         
036500*                                                                         
036600     STOP RUN.                                                            
036700*                                                                         
036800*                                                                         
036900 1000-INITIAL-PROCESS.                                                    
037000******************************************************************        
037100*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
037200*    -- VALIDATE CONTROL CARD                                             
037300*    -- DETERMINE CUTOFF DATE (ACCEPT DATE)                               
037400*    -- OPEN MAINTENANCE LOG FILE                                         
037500*                                                                         
037600*  THIS PROGRAM REQUIRES A CONTROL CARD WHICH CONTAINS THE NUMBER         
037700*  OF DAYS BEFORE THE FIRST SHIP DATE FOR THE SKU THAT THE SKU            
037800*  WILL HAVE THE FS REPORT INDICATOR CHANGED TO 'Y'.                      
037900******************************************************************        
038000     ACCEPT WS-TODAYS-DATE       FROM DATE.                               
038100*                                                                         
038200     ACCEPT WS-TODAYS-TIME       FROM TIME.                               
038300*                                                                         
038400     INITIALIZE DCLTSK                                                    
038500                FS500O-MAINTENANCE-LOG-RECORD.                            
038600     OPEN OUTPUT MAINT-LOG-FILE-OUT.                                      
038700                                                                          
038800*                                                                         
038900 2000-GET-DEPTS-AND-CLASSES.                                              
039000******************************************************************        
039100*  THIS IS THE DRIVER PARAGRAPH FOR LOADING THE DEPT/CLASS TABLE          
039200*    -- OPEN THE DPT_CLS_CSR                                              
039300*    -- DO THE PRIMING FETCH                                              
039400*    -- LOOP THROUGH THE RESULTS TABLE UNTIL ALL DEPT/CLASS               
039500*       COMBINATIONS ARE LOADED                                           
039600*    -- CLOSE THE DPT_CLS_CSR                                             
039700******************************************************************        
039800     PERFORM 2900-OPEN-DPT-CLS-CSR.                                       
039900     PERFORM 2910-FETCH-DPT-CLS-CSR.                                      
040000*                                                                         
040100     PERFORM 2100-LOAD-DEPT-CLASS-TABLE                                   
040200       VARYING DC-INDX FROM 1 BY 1                                        
CR0471         UNTIL DC-INDX > 16000                                            
040400            OR SQLCODE = +100.                                            
040500*                                                                         
040600     IF  SQLCODE = +100                                                   
040700         PERFORM 2920-CLOSE-DPT-CLS-CSR                                   
040800     ELSE                                                                 
040900         DISPLAY '** ABEND **'                                            
041000         DISPLAY PE-MSG-12                                                
041100         MOVE +4004              TO WS-ABEND-CODE                         
041200         CALL 'ILBOABN0' USING WS-ABEND-CODE.                             
041300*                                                                         
041400*                                                                         
041500 2100-LOAD-DEPT-CLASS-TABLE.                                              
041600******************************************************************        
041700*  THIS PARAGRAPH LOOPS AND DOES THE FOLLOWING:                           
041800*    -- LOAD ONE DEPT/CLASS COMBINATION INTO THE NEXT OCCURRENCE          
041900*       IN THE TABLE                                                      
042000*    -- FETCH THE NEXT DEPT/CLASS COMBINATION                             
042100******************************************************************        
042200     MOVE SKXREF-DEPT            TO NI-DEPT-9-3.                          
042210     MOVE NI-DEPT-X-3            TO DC-DEPT  (DC-INDX).                   
042300     MOVE SKXREF-SUBCLASS        TO NI-SUB-CLASS-9-2.                     
042310     MOVE NI-SUB-CLASS-X-2       TO DC-CLASS (DC-INDX).                   
042400*                                                                         
042500     PERFORM 2910-FETCH-DPT-CLS-CSR.                                      
042600*                                                                         
042700*                                                                         
042800 2900-OPEN-DPT-CLS-CSR.                                                   
042900******************************************************************        
043000*  OPEN THE DPT_CLS_CSR                                                   
043100******************************************************************        
043200     EXEC SQL                                                             
043300          OPEN DPT_CLS_CSR                                                
043400     END-EXEC.                                                            
043500*                                                                         
043600     EVALUATE TRUE                                                        
043700         WHEN SQLCODE = ZERO                                              
043800              CONTINUE                                                    
043900         WHEN OTHER                                                       
044000              MOVE '2900-OPEN-DPT-CLS-CSR'                                
044100                                 TO WS-ABEND-PARAGRAPH                    
044200              MOVE PE-MSG-1      TO WS-DB2-OPERATION-ATTEMPTED            
044300              PERFORM 9200-SQL-ABEND                                      
044400     END-EVALUATE.                                                        
044500*                                                                         
044600*                                                                         
044700 2910-FETCH-DPT-CLS-CSR.                                                  
044800******************************************************************        
044900*  FETCH THE NEXT DEPT/CLASS COMBINATION USING THE DPT-CLS-CSR            
045000******************************************************************        
045100     EXEC SQL                                                             
045200          FETCH DPT_CLS_CSR                                               
045300           INTO :SKXREF-DEPT,                                             
045400                :SKXREF-SUBCLASS                                          
045500     END-EXEC.                                                            
045600*                                                                         
045700     IF  SQLCODE = ZERO                                                   
045800      OR SQLCODE = +100                                                   
045900         CONTINUE                                                         
046000     ELSE                                                                 
046100         MOVE '2910-FETCH-DPT-CLS-CSR'                                    
046200                                 TO WS-ABEND-PARAGRAPH                    
046300         MOVE PE-MSG-2           TO WS-DB2-OPERATION-ATTEMPTED            
046400         PERFORM 9200-SQL-ABEND                                           
046500     END-IF.                                                              
046600*                                                                         
046700*                                                                         
046800 2920-CLOSE-DPT-CLS-CSR.                                                  
046900******************************************************************        
047000*  CLOSE THE DPT_CLS_CSR                                                  
047100******************************************************************        
047200     EXEC SQL                                                             
047300          CLOSE DPT_CLS_CSR                                               
047400     END-EXEC.                                                            
047500*                                                                         
047600     IF  SQLCODE = ZERO                                                   
047700         CONTINUE                                                         
047800     ELSE                                                                 
047900         MOVE '2920-CLOSE-DPT-CLS-CSR'                                    
048000                                 TO WS-ABEND-PARAGRAPH                    
048100         MOVE PE-MSG-3           TO WS-DB2-OPERATION-ATTEMPTED            
048200         PERFORM 9200-SQL-ABEND                                           
048300     END-IF.                                                              
048400*                                                                         
048500*                                                                         
048600 3000-MAIN-PROCESS.                                                       
048700******************************************************************        
048800*  DO THE MAIN PROCESSING FOR THE PROGRAM.  THIS PARAGRAPH IS A           
048900*  LOOP AND IS PERFORMED FOR EACH DEPT/CLASS COMBINATION.                 
049000******************************************************************        
049100*                                                                         
049200     SET SKU-ROW-FOUND TO TRUE.                                           
049300*                                                                         
049400     IF  DC-DEPT  (DC-INDX) = SPACES                                      
049500         SET WS-UPDATES-DONE     TO TRUE                                  
049600     ELSE                                                                 
049700         IF WS-DEPTNO NOT = DC-DEPT (DC-INDX)                             
049800             MOVE DC-DEPT (DC-INDX) TO WS-DEPTNO                          
049900             PERFORM 3010-GET-TMDSEDP-DAYS                                
050000         END-IF                                                           
050100         PERFORM 3900-OPEN-SKU-CSR                                        
050200         PERFORM 3100-PROCESS-LOOP                                        
050300           UNTIL SKU-ROW-NOT-FOUND                                        
050400         PERFORM 3920-CLOSE-SKU-CSR                                       
050500         PERFORM 3999-COMMIT                                              
050600     END-IF.                                                              
050700*                                                                         
050800*                                                                         
050900 3010-GET-TMDSEDP-DAYS.                                                   
051000     DISPLAY 'DEPT NUMBER = ' WS-DEPTNO.                                  
051100                                                                          
051200****************************************************************          
051300*  CALCULATE THE CUTOFF DATE FOR SHIP DATE BASED ON THE        *          
051400*  CURRENT DATE INCREMENTED BY THE WINDOW FOR THIS DEPARTMENT  *          
051500****************************************************************          
051600     MOVE SPACES TO WS-SHIP-DATE-X.                                       
051700     EXEC SQL                                                             
051800          SELECT                                                          
051900             D.DAYS_BEF_SHIP_NBR,                                         
052000             DATE(CURRENT DATE + D.DAYS_BEF_SHIP_NBR DAYS)                
052100             INTO :MDSEDP-DAYS-BEF-SHIP-NBR,                              
052200                  :WS-SHIP-DATE-X                                         
052300          FROM                                                            
052400                 TMDSEAR A,                                               
052500                 TMDSEDP D                                                
052600          WHERE                                                           
052700                 A.MDSELV_CDE   = :WS-LIT-DPT                             
052800            AND  A.DEPT_NBR     = :WS-DEPTNO                              
052900            AND  A.MDSEAR_DKEY  = D.MDSEAR_DKEY                           
053000            AND  CURRENT DATE BETWEEN A.STRT_DTE AND A.END_DTE            
053100     END-EXEC.                                                            
053200                                                                          
053300      EVALUATE TRUE                                                       
053400          WHEN SQLCODE = +0                                               
053500              MOVE WS-SHIP-DATE-CCYY TO WS-SHIP-CUTOFF-CCYY               
053600              MOVE WS-SHIP-DATE-MM   TO WS-SHIP-CUTOFF-MM                 
053700              MOVE WS-SHIP-DATE-DD   TO WS-SHIP-CUTOFF-DD                 
053800              DISPLAY 'WS-SHIP-DATE = ' WS-SHIP-DATE-X                    
053900          WHEN SQLWARN0 NOT = SPACES                                      
054000          WHEN SQLCODE < +0                                               
054100          WHEN SQLCODE > +0                                               
054200               MOVE '3010-GET-TMDSEDP-DAYS-SHIP'                          
054300                               TO WS-ABEND-PARAGRAPH                      
054400               MOVE PE-MSG-9      TO WS-DB2-OPERATION-ATTEMPTED           
054500               MOVE +4013         TO WS-ABEND-CODE                        
054600               PERFORM 9200-SQL-ABEND                                     
054700      END-EVALUATE.                                                       
054800                                                                          
054900****************************************************************          
055000*  CALCULATE THE CUTOFF DATE FOR CLEARANCE DATE BASED ON THE   *          
055100*  CURRENT DATE DECREMENTED BY THE WINDOW FOR THIS DEPARTMENT  *          
055200****************************************************************          
055300     MOVE SPACES TO WS-CLR-DATE-X.                                        
055400     EXEC SQL                                                             
055500          SELECT                                                          
055600             D.DAYS_AFT_CLR_NBR,                                          
055700             DATE(CURRENT DATE - D.DAYS_AFT_CLR_NBR DAYS)                 
055800             INTO :MDSEDP-DAYS-AFT-CLR-NBR,                               
055900                  :WS-CLR-DATE-X                                          
056000          FROM                                                            
056100                 TMDSEAR A,                                               
056200                 TMDSEDP D                                                
056300          WHERE                                                           
056400                 A.MDSELV_CDE   = :WS-LIT-DPT                             
056500            AND  A.DEPT_NBR     = :WS-DEPTNO                              
056600            AND  A.MDSEAR_DKEY  = D.MDSEAR_DKEY                           
056700            AND  CURRENT DATE BETWEEN A.STRT_DTE AND A.END_DTE            
056800     END-EXEC.                                                            
056900                                                                          
057000      EVALUATE TRUE                                                       
057100          WHEN SQLCODE = +0                                               
057200              MOVE WS-CLR-DATE-CCYY TO WS-CLR-CUTOFF-CCYY                 
057300              MOVE WS-CLR-DATE-MM   TO WS-CLR-CUTOFF-MM                   
057400              MOVE WS-CLR-DATE-DD   TO WS-CLR-CUTOFF-DD                   
057500              DISPLAY 'WS-CLR-DATE = ' WS-CLR-DATE-X                      
057600              DISPLAY ' '                                                 
057700          WHEN SQLWARN0 NOT = SPACES                                      
057800          WHEN SQLCODE < +0                                               
057900          WHEN SQLCODE > +0                                               
058000               MOVE '3010-GET-TMDSEDP-DAYS-CLEAR'                         
058100                               TO WS-ABEND-PARAGRAPH                      
058200               MOVE PE-MSG-10     TO WS-DB2-OPERATION-ATTEMPTED           
058300               MOVE +4013         TO WS-ABEND-CODE                        
058400               PERFORM 9200-SQL-ABEND                                     
058500      END-EVALUATE.                                                       
058600                                                                          
058700 3100-PROCESS-LOOP.                                                       
058800******************************************************************        
058900*  THIS PARAGRAPH LOOPS THROUGH THE ROWS RETRIEVED BY THE SKU             
059000*  CURSOR AND UPDATES THE FS REPORT INDICATOR FOR THOSE SKUS              
059100*  THAT MEET THE CUTOFF DATE CRITERIA.                                    
059200******************************************************************        
059300*                                                                         
059400*                                                                         
059500     PERFORM 3910-FETCH-SKU-CSR.                                          
059600     EVALUATE TRUE                                                        
059700       WHEN SQLCODE = +0                                                  
059800           SET SKU-ROW-FOUND TO TRUE                                      
059900*****IF NOT CLEARANCE: SET TO 'Y' IF DAYS BEFORE SHIP IS 999 OR           
060000*****IF THE CURRENT DATE + THE NUMBER OF DAYS BEFORE SHIP IS              
060100*****GREATER THAN THE "DO NOT SHIP BEFORE" DATE ON THE PO DB              
060200           EVALUATE TRUE                                                  
060300              WHEN TSK-STATUS-CODE = 10                                   
060400              WHEN TSK-STATUS-CODE = 20                                   
060500              WHEN TSK-STATUS-CODE = 25                                   
060600              WHEN TSK-STATUS-CODE = 40                                   
060700                 IF MDSEDP-DAYS-BEF-SHIP-NBR = 999                        
060800                   IF TSK-FS-RPT-IND NOT = 'Y'                            
060900                       MOVE TSK-FS-RPT-IND TO CM-FS-RPT-IND-OLD           
061000                       MOVE 'Y' TO TSK-FS-RPT-IND                         
061100                                   CM-FS-RPT-IND-NEW                      
061200                       PERFORM 3120-UPDATE-FS-RPT-IND                     
061300                   END-IF                                                 
061400                 ELSE                                                     
061500*NOT = '999' ? ...CHECK CUTOFF DATE THEN                                  
061600                   IF TSK-FS-RPT-IND NOT = 'Y'                            
061700                      PERFORM 3110-GET-SHIP-DATE                          
061800                      IF WS-SHIP-CUTOFF-DATE > WS-PLNSHP                  
061900                      OR WS-SHIP-CUTOFF-DATE = WS-PLNSHP                  
062000                         MOVE TSK-FS-RPT-IND TO CM-FS-RPT-IND-OLD         
062100                         MOVE 'Y' TO TSK-FS-RPT-IND                       
062200                                     CM-FS-RPT-IND-NEW                    
062300                         PERFORM 3120-UPDATE-FS-RPT-IND                   
062400                      END-IF                                              
062500                   END-IF                                                 
062600                 END-IF                                                   
062700           WHEN TSK-STATUS-CODE =  30                                     
KP0207               PERFORM 3130-SELECT-ORIG-CLR-DTE                           
KP0207               MOVE WS-ORIG-CLR-DATE-CC TO WS-STAT-CC                     
KP0207               MOVE WS-ORIG-CLR-DATE-YY TO WS-STAT-YY                     
KP0207               MOVE WS-ORIG-CLR-DATE-MM TO WS-STAT-MM                     
KP0207               MOVE WS-ORIG-CLR-DATE-DD TO WS-STAT-DD                     
063800               IF WS-CLR-CUTOFF-DATE < WS-STAT-DATE                       
063900               OR WS-CLR-CUTOFF-DATE = WS-STAT-DATE                       
064000                  IF TSK-FS-RPT-IND NOT = 'Y'                             
064100                    MOVE TSK-FS-RPT-IND TO CM-FS-RPT-IND-OLD              
064200                    MOVE 'Y' TO TSK-FS-RPT-IND                            
064300                                CM-FS-RPT-IND-NEW                         
064400                    PERFORM 3120-UPDATE-FS-RPT-IND                        
064500                  END-IF                                                  
064600               ELSE                                                       
064700                  IF TSK-FS-RPT-IND NOT = 'N'                             
064800                    MOVE TSK-FS-RPT-IND TO CM-FS-RPT-IND-OLD              
064900                    MOVE 'N' TO TSK-FS-RPT-IND                            
065000                                 CM-FS-RPT-IND-NEW                        
065100                    PERFORM 3120-UPDATE-FS-RPT-IND                        
065200                  END-IF                                                  
065300               END-IF                                                     
065400*                                                                         
065500           WHEN TSK-STATUS-CODE =  90                                     
065600               IF TSK-FS-RPT-IND NOT = 'Y'                                
065700                   MOVE TSK-FS-RPT-IND TO CM-FS-RPT-IND-OLD               
065800                   MOVE 'Y' TO TSK-FS-RPT-IND                             
065900                               CM-FS-RPT-IND-NEW                          
066000                   PERFORM 3120-UPDATE-FS-RPT-IND                         
066100               END-IF                                                     
066200          END-EVALUATE                                                    
066300*                                                                         
066400         WHEN SQLCODE = +100                                              
066500              SET SKU-ROW-NOT-FOUND TO TRUE                               
066600*                                                                         
066700         WHEN OTHER                                                       
066800              MOVE '3100-PROCESS-LOOP'                                    
066900                                 TO WS-ABEND-PARAGRAPH                    
067000              MOVE PE-MSG-5      TO WS-DB2-OPERATION-ATTEMPTED            
067100              PERFORM 9200-SQL-ABEND                                      
067200*                                                                         
067300     END-EVALUATE.                                                        
067400*                                                                         
067500*                                                                         
067600 3110-GET-SHIP-DATE.                                                      
067700******************************************************************        
067800*  GET THE SHIP DATE OF THE FIRST DATABASE RECORD FOR THE SKU             
067900******************************************************************        
068000*                                                                         
068100*                                                                         
068200      EXEC SQL                                                            
068300           SELECT  MIN(DONT_SHIP_BEF_DTE)                                 
068400             INTO  :PLNSHP-DONT-SHIP-BEF-DTE                              
068500             FROM  TPURITM, TPLNSHP                                       
068800               WHERE TPURITM.ITM_NBR     = :TSK-ITEM-NMBR                 
068900                 AND TPURITM.VER_STATUS_CDE  =  'A'                       
069000                 AND TPLNSHP.VER_STATUS_CDE  =  'A'                       
069100                 AND TPURITM.PO_NBR = TPLNSHP.PO_NBR                      
069200      END-EXEC.                                                           
069300                                                                          
069400      EVALUATE TRUE                                                       
069500          WHEN SQLCODE = +0                                               
069600               MOVE PLNSHP-DONT-SHIP-BEF-DTE                              
069700                              TO WS-PLNSHP-DATE-X                         
069800               MOVE WS-PLNSHP-DATE-CCYY                                   
069900                              TO WS-PLNSHP-CCYY                           
070000               MOVE WS-PLNSHP-DATE-MM                                     
070100                              TO WS-PLNSHP-MM                             
070200               MOVE WS-PLNSHP-DATE-DD                                     
070300                              TO WS-PLNSHP-DD                             
070400                                                                          
070500                                                                          
070600                                                                          
070700          WHEN SQLCODE = -305                                             
070800          WHEN SQLCODE = +100                                             
070900              MOVE '9999-99-99' TO  WS-PLNSHP-DATE-X                      
071000               MOVE WS-PLNSHP-DATE-CCYY                                   
071100                              TO WS-PLNSHP-CCYY                           
071200               MOVE WS-PLNSHP-DATE-MM                                     
071300                              TO WS-PLNSHP-MM                             
071400               MOVE WS-PLNSHP-DATE-DD                                     
071500                              TO WS-PLNSHP-DD                             
071600                                                                          
071700          WHEN SQLWARN0 NOT = SPACES                                      
071800          WHEN SQLCODE < +0                                               
071900          WHEN SQLCODE > +0                                               
072000               MOVE '3110-GET-SHIP-DATE'                                  
072100                                  TO WS-ABEND-PARAGRAPH                   
072200               MOVE PE-MSG-14     TO WS-DB2-OPERATION-ATTEMPTED           
072300               MOVE +4013         TO WS-ABEND-CODE                        
072400               PERFORM  9200-SQL-ABEND                                    
072500      END-EVALUATE.                                                       
072600                                                                          
072700*                                                                         
072800*                                                                         
072900 3120-UPDATE-FS-RPT-IND.                                                  
073000******************************************************************        
073100*  PERFORM THE UPDATE FUNCTION TO UPDATE THE FS REPORT INDICATOR          
073200******************************************************************        
073300*                                                                         
073400                                                                          
073500                                                                          
073600     EXEC SQL                                                             
073700          UPDATE TSK                                                      
073800             SET FS_RPT_IND  = :TSK-FS-RPT-IND                            
073900           WHERE ITEM_NMBR        = :TSK-ITEM-NMBR                        
074200     END-EXEC.                                                            
074300                                                                          
074400     IF  SQLCODE = ZERO                                                   
074500         PERFORM 3121-WRITE-MAINT-LOG                                     
074600                                                                          
074700         ADD 1  TO  WS-ROWS-UPDATED                                       
074800                                                                          
074900     ELSE                                                                 
075000         MOVE '3120-UPDATE-FS-RPT-IND'                                    
075100                                 TO WS-ABEND-PARAGRAPH                    
075200         MOVE PE-MSG-7           TO WS-DB2-OPERATION-ATTEMPTED            
075300         PERFORM 9200-SQL-ABEND                                           
075400     END-IF.                                                              
075500                                                                          
075600                                                                          
075700 3121-WRITE-MAINT-LOG.                                                    
075800******************************************************************        
075900*  WRITES MAINTENANCE LOG RECORDS                                         
076000******************************************************************        
076100     SET FS500O-CHANGE-CODE      TO TRUE.                                 
076200     SET FS500O-TSK-TABLE        TO TRUE.                                 
076300     MOVE WS-TODAYS-DATE         TO FS500O-TRANSACTION-DATE.              
076400     MOVE WS-CURRENT-TIME        TO FS500O-TRANSACTION-TIME.              
076500     MOVE WS-LIT-PROGRAM-NAME    TO FS500O-PROGRAM-NAME.                  
076600     MOVE WS-LIT-BATCHPGM        TO FS500O-USER-ID.                       
076700     MOVE SKXREF-DEPT            TO NI-DEPT-9-3.                          
076710     MOVE NI-DEPT-9-3            TO FS500O-TSK-DEPT.                      
076800     MOVE SKXREF-SUBCLASS        TO NI-SUB-CLASS-9-2.                     
076810     MOVE NI-SUB-CLASS-9-2       TO FS500O-TSK-CLASS.                     
076900     MOVE SKXREF-SKU             TO NI-SKU-9.                             
076910     MOVE NI-SKU-9               TO FS500O-TSK-SKU.                       
077000     MOVE CM-CHANGE-TSK-MSG-23   TO FS500O-OUTPUT-MESSAGE-AREA.           
077100*                                                                         
077210     WRITE MAINT-LOG-RECORD-OUT                                           
077300         FROM FS500O-MAINTENANCE-LOG-RECORD.                              
077400*                                                                         
077500*                                                                         
KP0207******************************************************************        
KP0207*   GET THE ORIG CLR DATE FROM THE TUPCPLS STORE ZERO ROW.                
KP0207******************************************************************        
KP0207 3130-SELECT-ORIG-CLR-DTE.                                                
KP0207                                                                          
KP0207     PERFORM WITH TEST AFTER                                              
KP0207     VARYING PV-RETRY-CNT FROM 1 BY 1                                     
KP0207       UNTIL PV-RETRY-CNT > PC-MAX-RETRIES                                
KP0207          OR SQLCODE = +0                                                 
KP0207          OR SQLCODE = +100                                               
KP0207                                                                          
KP0207        EXEC  SQL                                                         
KP0207              SELECT   ORIG_CLR_DTE                                       
KP0207                INTO   :UPCPLS-ORIG-CLR-DTE                               
KP0207                FROM   TUPCPLS                                            
KP0207               WHERE   ITM_NBR       = :TSK-ITEM-NMBR                     
KP0207                 AND   STR_NBR       =  0                                 
KP0207                 AND   UPC_STAT_CDE  = '30'                               
KP0207                 AND ((EFF_BEG_TMST <= CURRENT TIMESTAMP)                 
KP0207                 AND ((EFF_END_TMST >= CURRENT TIMESTAMP)                 
KP0207                  OR  (EFF_END_TMST IS NULL)))                            
KP0207        END-EXEC                                                          
KP0207       EVALUATE TRUE                                                      
KP0207           WHEN SQLCODE = ZERO                                            
KP0207              MOVE UPCPLS-ORIG-CLR-DTE TO WS-ORIG-CLR-DATE-X              
KP0207              ADD  +1    TO WS-TUPCPLS-SEL-CNT                            
KP0207           WHEN SQLCODE = +100                                            
KP0207              ADD  +1    TO WS-TUPCPLS-SEL-CNT                            
KP0207           WHEN SQLWARN0 NOT = SPACES                                     
KP0207           WHEN SQLCODE = -911                                            
KP0207           WHEN SQLCODE = -904                                            
KP0207               CONTINUE                                                   
KP0207           WHEN OTHER                                                     
KP0207             MOVE '3130-SELECT-ORIG-CLR-DTE'                              
KP0207               TO  WS-ABEND-PARAGRAPH                                     
KP0207             MOVE 'SELECT ORIG CLR DATE'                                  
KP0207               TO  WS-DB2-OPERATION-ATTEMPTED                             
KP0207             DISPLAY '*******************************'                    
KP0207             DISPLAY 'ITEM NUMBER = ' TSK-ITEM-NMBR                       
KP0207             DISPLAY 'STORE NUMBER = 0'                                   
KP0207             DISPLAY '*******************************'                    
KP0207             PERFORM 9200-SQL-ABEND                                       
KP0207       END-EVALUATE                                                       
KP0207     END-PERFORM.                                                         
KP0207*                                                                         
KP0207     IF (SQLCODE = 0                                                      
004968       OR SQLCODE = +100)                                                 
KP0207          CONTINUE                                                        
KP0207     ELSE                                                                 
KP0207          MOVE '3130-SELECT-ORIG-CLR-DTE'                                 
KP0207            TO  WS-ABEND-PARAGRAPH                                        
KP0207          MOVE 'SELECT ORIG CLR DATE'                                     
KP0207            TO  WS-DB2-OPERATION-ATTEMPTED                                
KP0207          DISPLAY '*******************************'                       
KP0207          DISPLAY 'ITEM NUMBER = ' TSK-ITEM-NMBR                          
KP0207          DISPLAY 'STORE NUMBER = 0'                                      
KP0207          DISPLAY '*******************************'                       
KP0207          PERFORM 9200-SQL-ABEND                                          
KP0207     END-IF.                                                              
KP0207                                                                          
KP0207                                                                          
077600 3900-OPEN-SKU-CSR.                                                       
077700******************************************************************        
077800*  OPEN THE SKU_CSR                                                       
077900******************************************************************        
078000     MOVE DC-DEPT  (DC-INDX)     TO NI-DEPT-X-3.                          
078010     MOVE NI-DEPT-9-3            TO SKXREF-DEPT.                          
078100     MOVE DC-CLASS (DC-INDX)     TO NI-SUB-CLASS-X-2.                     
078110     MOVE NI-SUB-CLASS-9-2       TO SKXREF-SUBCLASS.                      
078200*                                                                         
078300     EXEC SQL                                                             
078400          OPEN SKU_CSR                                                    
078500     END-EXEC.                                                            
078600*                                                                         
078700     EVALUATE TRUE                                                        
078800         WHEN SQLCODE = ZERO                                              
078900              CONTINUE                                                    
079000         WHEN OTHER                                                       
079100              MOVE '3900-OPEN-SKU-CSR'                                    
079200                                 TO WS-ABEND-PARAGRAPH                    
079300              MOVE PE-MSG-4      TO WS-DB2-OPERATION-ATTEMPTED            
079400              PERFORM 9200-SQL-ABEND                                      
079500     END-EVALUATE.                                                        
079600*                                                                         
079700*                                                                         
079800 3910-FETCH-SKU-CSR.                                                      
079900******************************************************************        
080000*  FETCH THE NEXT SKU IN THE DEPT/CLASS USING THE SKU_CSR                 
080100******************************************************************        
080200     EXEC SQL                                                             
080300          FETCH SKU_CSR                                                   
080400           INTO :SKXREF-DEPT,                                             
080500                :SKXREF-SUBCLASS,                                         
080600                :SKXREF-SKU,                                              
080800                :TSK-STATUS-CODE,                                         
080900                :TSK-ITEM-NMBR,                                           
081000                :TSK-FS-RPT-IND                                           
081100     END-EXEC.                                                            
081200*                                                                         
081300*                                                                         
081400 3920-CLOSE-SKU-CSR.                                                      
081500******************************************************************        
081600*  CLOSE THE SKU_CSR                                                      
081700******************************************************************        
081800     EXEC SQL                                                             
081900          CLOSE SKU_CSR                                                   
082000     END-EXEC.                                                            
082100*                                                                         
082200     IF  SQLCODE = ZERO                                                   
082300         CONTINUE                                                         
082400     ELSE                                                                 
082500         MOVE '3920-CLOSE-SKU-CSR'                                        
082600                                 TO WS-ABEND-PARAGRAPH                    
082700         MOVE PE-MSG-6           TO WS-DB2-OPERATION-ATTEMPTED            
082800         PERFORM 9200-SQL-ABEND                                           
082900     END-IF.                                                              
083000*                                                                         
083100*                                                                         
083200 3999-COMMIT.                                                             
083300******************************************************************        
083400*  COMMIT THE CHANGES MADE FOR THE DEPT/CLASS                             
083500******************************************************************        
083600     EXEC SQL                                                             
083700          COMMIT                                                          
083800     END-EXEC.                                                            
083900                                                                          
084000     EVALUATE TRUE                                                        
084100         WHEN SQLWARN0 NOT = SPACES                                       
084200         WHEN SQLCODE < 0                                                 
084300         WHEN SQLCODE > 0                                                 
084400         MOVE '3999-COMMIT'                                               
084500                                 TO WS-ABEND-PARAGRAPH                    
084600         MOVE PE-MSG-15          TO WS-DB2-OPERATION-ATTEMPTED            
084700         PERFORM 9200-SQL-ABEND                                           
084800                                                                          
084900         WHEN SQLCODE = 0                                                 
085000             CONTINUE                                                     
085100                                                                          
085200     END-EVALUATE.                                                        
085300*                                                                         
085400*                                                                         
085500 8000-END-PROCESS.                                                        
085600******************************************************************        
085700*  CLOSE MAINT LOG FILE                                                   
085800******************************************************************        
085900     CLOSE MAINT-LOG-FILE-OUT.                                            
086000*                                                                         
086100*                                                                         
086200 9200-SQL-ABEND.                                                          
086300******************************************************************        
086400*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
086500*  ERROR OR WARNING CODE OCCURS.                                          
086600******************************************************************        
086700     DISPLAY '***************************'.                               
086800     DISPLAY '**       ABEND           **'.                               
086900     DISPLAY '**  IN PROGRAM FSKUP017  **'.                               
087000     DISPLAY '***************************'.                               
087100     DISPLAY '** PARAGRAPH ** = ' WS-ABEND-PARAGRAPH.                     
087200     DISPLAY '** OPERATION ** = ' WS-DB2-OPERATION-ATTEMPTED.             
087300*                                                                         
087400******************************************************************        
087500*  THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-           
087600*  MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                
087700*  'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE            
087800*  FORMAT.                                                                
087900******************************************************************        
088000     COPY DPPD004.                                                        
088100*                                                                         
088200     EXEC SQL                                                             
088300          COMMIT                                                          
088400     END-EXEC.                                                            
088500*                                                                         
088600     MOVE +4013                  TO WS-ABEND-CODE.                        
088700     CALL 'ILBOABN0' USING WS-ABEND-CODE.                                 
088800*                                                                         
