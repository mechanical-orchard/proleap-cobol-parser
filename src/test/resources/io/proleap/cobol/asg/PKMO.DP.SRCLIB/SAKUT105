       IDENTIFICATION DIVISION.                                         00010008
       PROGRAM-ID.      SAKUT105.                                       00020008
       AUTHOR.          THOMAS HANSON.                                  00030008
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040008
       DATE-WRITTEN.    APRIL, 2009.                                    00050008
                                                                        00060008
      ******************************************************************00061008
      *     CREATE WEEKLY DMM / GMM SUMMARY SALES FILE FOR FINANCE      00061108
      ******************************************************************00061208
      *                                                                 00061308
      *  THIS PROGRAM SUMS UP ALL REGULAR & PROMOTIONAL SALES VERSUS    00061408
      *  CLEARANCE SALES BY DEPARTMENT.  SINCE EACH DEPARTMENT IS       00061508
      *  ASSIGNED TO A DMM & A GMM, WE CAN ACCUMULATE THEIR TOTALS      00061608
      *  AS EACH DEPARTMENT IS PROCESSED.                               00061708
      *                                                                 00061808
      *  THIS WAS REQUESTED BY FINANCE TO DETERMINE HOW WELL EACH       00061908
      *  DMM & GMM ARE PERFORMING IN COMPARISON TO EACH OTHER.          00062008
      *                                                                 00063008
      *  THIS WILL RUN FOR THE PREVIOUS WEEK'S DATES SUNDAY THROUGH     00064008
      *  SATURDAY.  AT THIS TIME THE PROGRAM WILL RUN MONDAY MORNING.   00065008
      *  EACH SALES DATE PROCESSED IS SUMMED UP SEPARATELY.             00066008
      *                                                                 00067008
      ******************************************************************00068008
      *  HISTORY LOG:                                                   00069008
      ******************************************************************00070008
      *  DATE: 04/06/09                                                 00080008
      *  WR/IR#: WR35893                                                00090008
      *  PROGRAMMER:  THOMAS HANSON                                     00100008
      *  DESCRIPTION: INITIAL IMPLEMENTATION.                           00110008
      *               FOR THE PAST SEVERAL MONTHS THIS WAS RUN MANUALLY.00120008
      ******************************************************************00154127
      *  DATE: 07/19/12                                                 00154227
      *  WR/IR#: CRQ000000029300                                        00154327
      *  PROGRAMMER: COGNIZANT                                          00154427
      *  DESCRIPTION: CHANGED THE FTP PATH NAME IN PARA 1700- AS PER    00154527
      *               THE RECOMMENDATIONS                               00154627
      ******************************************************************00154727
p8612 ******************************************************************00154827
p8612 *  DATE: 09/01/2014                                               00154927
p8612 *  WR/IR#: CHG0092405                                             00155027
p8612 *  PROGRAMMER: COGNIZANT                                          00155127
p8612 *  DESCRIPTION: EXCLUDE NEW MARKET PLACE DEPARTMENT 070.          00155227
p8612 ******************************************************************00155327
      ******************************************************************00155431
      *  DATE: 08/27/15                                                 00155534
      *  WR/IR#: CHG0120825                                             00155633
      *  PROGRAMMER: COGNIZANT                                          00155731
      *  DESCRIPTION: CHANGED THE FTP TO MFT CONTROL CARD               00155831
      *                                                                 00155931
      ******************************************************************00156037
MKTPLC******************************************************************00156137
MKTPLC*  DATE: 07/29/2020                                               00156238
MKTPLC*  WR/IR#: CHG0246098                                             00156338
MKTPLC*  PROGRAMMER: Barb Schultz                                       00156438
MKTPLC*  DESCRIPTION: EXCLUDE NEW MARKET PLACE DEPARTMENT 985 AND DELETE00156537
MKTPLC*               WAYFAIR DEPT 070.                                 00156637
SEPHGC******************************************************************00156739
SEPHGC*  DATE: 04/01/2022                                               00156839
SEPHGC*  WR/IR#: CHG0268311                                             00156940
SEPHGC*  PROGRAMMER: Jim Hunter                                         00157039
SEPHGC*  DESCRIPTION: EXCLUDE NEW SEPHORA GIFT CARD DEPARTMENT 712      00157139
      ******************************************************************00157339
      ******************************************************************00157439
                                                                        00157539
      ******************************************************************00157639
       ENVIRONMENT DIVISION.                                            00157739
      ******************************************************************00158008
       CONFIGURATION SECTION.                                           00159008
       SOURCE-COMPUTER. IBM-3090.                                       00160008
       OBJECT-COMPUTER. IBM-3090.                                       00170008
                                                                        00180008
       INPUT-OUTPUT SECTION.                                            00190008
       FILE-CONTROL.                                                    00200008
                                                                        00210008
           SELECT OVERRIDE-DATE-PERIOD-FILE                             00220008
               ASSIGN                         TO INP01.                 00230008
           SELECT SCHEDULE-LOAD-DATE-FILE                               00240008
               ASSIGN                         TO INP02.                 00241008
           SELECT DMM-GMM-SUM-FILE                                      00242008
               ASSIGN                         TO OUT01.                 00243008
MFTCHG     SELECT MFT-COMMANDS-FILE                                     00244029
               ASSIGN                         TO OUT02.                 00245008
                                                                        00246008
       DATA DIVISION.                                                   00247008
       FILE SECTION.                                                    00248008
                                                                        00249008
       FD  OVERRIDE-DATE-PERIOD-FILE                                    00250008
           RECORDING MODE F                                             00260008
           BLOCK CONTAINS 0 RECORDS                                     00270008
           LABEL RECORDS ARE OMITTED                                    00280008
           DATA RECORD IS OVERRIDE-DATE-PERIOD-RECORD.                  00290008
       01  OVERRIDE-DATE-PERIOD-RECORD        PIC X(80).                00300008
                                                                        00310008
       FD  SCHEDULE-LOAD-DATE-FILE                                      00311008
           RECORDING MODE F                                             00312008
           BLOCK CONTAINS 0 RECORDS                                     00313008
           LABEL RECORDS ARE OMITTED                                    00314008
           DATA RECORD IS SCHEDULE-LOAD-DATE-RECORD.                    00315008
       01  SCHEDULE-LOAD-DATE-RECORD          PIC X(80).                00316008
                                                                        00317008
       FD  DMM-GMM-SUM-FILE                                             00318008
           RECORDING MODE F                                             00319008
           BLOCK CONTAINS 0 RECORDS                                     00320008
           LABEL RECORDS ARE OMITTED                                    00330008
           DATA RECORD IS DMM-GMM-SUM-FILE-RECORD.                      00340008
       01  DMM-GMM-SUM-FILE-RECORD            PIC X(175).               00350008
                                                                        00360008
MFTCHG FD  MFT-COMMANDS-FILE                                            00361029
           RECORDING MODE F                                             00362008
           BLOCK CONTAINS 0 RECORDS                                     00363008
           LABEL RECORDS ARE OMITTED                                    00364008
MFTCHG     DATA RECORD IS MFT-COMMANDS-RECORD.                          00364130
MFTCHG 01  MFT-COMMANDS-RECORD                PIC X(80).                00364229
                                                                        00364308
       WORKING-STORAGE SECTION.                                         00364408
      ******************************************************************00364508
      *  PS- PROGRAM SWITCHES                                           00364608
      ******************************************************************00364708
       01  PS-PROGRAM-SWITCHES.                                         00364808
           05  PS-DMM-GMM-END-OF-CSR-SW       PIC X(01)  VALUE 'N'.     00364908
               88  PS-DMM-GMM-END-OF-CSR                 VALUE 'Y'.     00365008
               88  PS-DMM-GMM-NOT-END-OF-CSR             VALUE 'N'.     00366008
           05  PS-REG-SALES-CSR-SW            PIC X(01)  VALUE 'N'.     00367008
               88  PS-REG-SALES-CSR-END                  VALUE 'Y'.     00368008
               88  PS-REG-SALES-CSR-NOT-END              VALUE 'N'.     00369008
                                                                        00370008
      ******************************************************************00380008
      *  PC- PROGRAM CONSTANTS                                          00381008
      ******************************************************************00382008
       01  PC-PROGRAM-CONSTANTS.                                        00383008
           05  PC-CURRENT-PROGRAM-NAME        PIC X(08)  VALUE          00384008
               'SAKUT105'.                                              00385008
           05  PC-OUTPUT-DMM-GMM-HDR-LNE-1    PIC X(80)  VALUE          00386008
               'REGULAR AND CLEARANCE SALES / UNITS'.                   00387008
           05  PC-OUTPUT-DMM-GMM-HDR-LNE-2    PIC X(80)  VALUE          00388008
               'BY SALES DATE BY DMM & GMM        '.                    00389008
           05  PC-2-DAYS                      PIC 9(01)  VALUE 2.       00390008
           05  PC-8-DAYS                      PIC 9(01)  VALUE 8.       00390108
           05  PC-MAX-TEMP-TABLE-LIMIT        PIC 9(04)  VALUE 1000.    00390208
                                                                        00390308
      ******************************************************************00390408
      *  AB- ABEND ERROR HANDLING                                       00390508
      ******************************************************************00390608
       01  ABEND-CODE                         PIC S9(4)  COMP VALUE +0. 00390708
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.   00390808
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.   00390908
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.   00391008
                                                                        00392008
       01  PV-MISC-ABEND-FIELDS.                                        00393008
           05  AB-PARAGRAPH-NAME              PIC X(30)  VALUE SPACES.  00394008
           05  AB-OPERATION-MSG-1             PIC X(40)  VALUE SPACES.  00395008
           05  AB-OPERATION-MSG-2             PIC X(40)  VALUE SPACES.  00396008
                                                                        00397008
      ******************************************************************00398008
      *  PV- PROGRAM WORKING STORAGE VARIABLES                          00399008
      ******************************************************************00400008
       01  PV-PROGRAM-VARIABLES.                                        00410008
           05  PV-SALE-TYPE                   PIC X(01)  VALUE SPACES.  00420008
           05  PV-ANNUAL-COMP-STORES-COUNT    PIC S9(5)  COMP.          00430008
                                                                        00440008
           05  PV-CSR-DEPT-NBR                PIC X(03)  VALUE SPACES.  00450008
                                                                        00460008
           05  PV-CSR-ITEM-COUNT              PIC S9(9)  COMP.          00470008
           05  PV-CSR-SALES                   PIC S9(11)V9(2) COMP-3    00480008
                                                         VALUE ZEROES.  00490008
           05  PV-CLR-ITEM-COUNT              PIC S9(9)  COMP.          00500008
           05  PV-CLR-SALES                   PIC S9(11)V9(2) COMP-3    00510008
                                                         VALUE ZEROES.  00520008
           05  PV-CSR-SALES-DATE              PIC X(10)  VALUE SPACES.  00530008
           05  PV-CURRENT-DATE                PIC X(10)  VALUE SPACES.  00540008
           05  PV-PREVIOUS-DATE               PIC X(10)  VALUE SPACES.  00550008
                                                                        00560008
           05  PV-INDEX1                      PIC S9(4)  VALUE +0       00570008
                                                         COMP SYNC.     00580008
           05  PV-GMM-DMM-IDX-TEMP            PIC S9(4)  VALUE +0       00590008
                                                         COMP SYNC.     00600008
           05  PV-NUM-DMM-IDX                 PIC S9(4)  VALUE +0       00610008
                                                         COMP SYNC.     00620008
           05  PV-NUM-GMM-IDX                 PIC S9(4)  VALUE +0       00630008
                                                         COMP SYNC.     00640008
                                                                        00650008
         05  PV-DATE-AND-TIME-VARIABLES.                                00660008
             10  PV-CUR-SYS-DATE              PIC X(10)  VALUE SPACE.   00670008
             10  PV-ANNUAL-COMP-DATE.                                   00680008
                 15  PV-DTE-COMP-CCYY         PIC X(04)  VALUE SPACES.  00690008
                 15  FILLER                   PIC X(01)  VALUE '-'.     00700008
                 15  PV-DTE-COMP-MM           PIC X(02)  VALUE '12'.    00710008
                 15  FILLER                   PIC X(01)  VALUE '-'.     00720008
                 15  PV-DTE-COMP-DD           PIC X(02)  VALUE '31'.    00730008
             10  PV-DTE-COMP-TEMP-CCYY        PIC 9(04)  VALUE ZEROES.  00740008
             10  PV-DB2-FY-COMP-DTE           PIC X(10)  VALUE SPACES.  00750008
                                                                        00760008
       01  SQL-DATE-EXECUTION.                                          00770008
           05  FILLER                         PIC X(15)  VALUE SPACES.  00780008
           05  CC-PERIOD-START-DATE           PIC X(10)  VALUE SPACES.  00790008
           05  FILLER                         PIC X(01)  VALUE SPACE.   00800008
           05  CC-PERIOD-END-DATE             PIC X(10)  VALUE SPACES.  00810008
                                                                        00820008
       01  SA-REPORT-DETAIL-CURRENT.                                    00830008
           05  SA-DEPT-NBR-CUR                PIC S9(4)V COMP-3.        00840008
           05  SA-DMM-NBR-CUR                 PIC S9(4)V COMP-3.        00850008
           05  SA-GMM-NBR-CUR                 PIC S9(4)V COMP-3.        00860008
                                                                        00870008
       01  PV-OUTPUT-DMM-GMM-HDR-LNE-3.                                 00880008
           05  FILLER                         PIC X(08)  VALUE          00890008
               'FOR THE '.                                              00900008
           05  PV-DISPLAY-STORES-COUNT        PIC Z(05)  VALUE SPACES.  00910008
           05  FILLER                         PIC X(18)  VALUE          00920008
               ' STORES OPENED BY '.                                    00930008
           05  PV-DISPLAY-COMP-DATE           PIC X(10)  VALUE SPACES.  00940008
                                                                        00950008
       01  PV-OUTPUT-DMM-GMM-HDR-LNE-4.                                 00960008
           05  FILLER                         PIC X(12)  VALUE          00961008
               'SALES DATES '.                                          00962008
           05  PV-DISPLAY-START-DATE          PIC X(10)  VALUE SPACES.  00963008
           05  FILLER                         PIC X(09)  VALUE          00964008
               ' THROUGH '.                                             00965008
           05  PV-DISPLAY-END-DATE            PIC X(10)  VALUE SPACES.  00966008
                                                                        00967008
       01  PV-OUTPUT-DMM-GMM-HDR-LNE-6.                                 00968008
           05  FILLER                         PIC X(50)  VALUE          00969008
               'SALES DATE,DMM/GMM,#,REGULAR & PROMO SALES AMOUNT,'.    00970008
           05  FILLER                         PIC X(50)  VALUE          00971008
               'REGULAR & PROMO UNIT COUNT,CLEARANCE SALES AMOUNT,'.    00972008
           05  FILLER                         PIC X(20)  VALUE          00973008
               'CLEARANCE UNIT COUNT'.                                  00974008
                                                                        00975008
MFTCHG 01  PV-MFT-REC.                                                  00976925
MFTCHG        10 PV-MFT-FILLER        PIC X(13).                        00977026
MFTCHG        10 PV-MFT-LF            PIC X(57) VALUE                   00977126
MFTCHG      '   SET %DIR1 = "\\FINFILE\data01\FINANCE\FINPRIV\FP&A\DMM'.00977225
MFTCHG        10 PV-MFT-LF1           PIC X(10) VALUE                   00977326
MFTCHG      ' -'.                                                       00977425
MFTCHG 01  PV-MFT-REC1.                                                 00977525
MFTCHG        10 PV-MFT-RF            PIC X(40) VALUE                   00977626
MFTCHG      'sales by department\"'.                                    00977725
MFTCHG        10 PV-MFT-FILLER        PIC X(40).                        00977826
MFTCHG 01  PV-MFT-REC2.                                                 00977925
MFTCHG        10 PV-MFT-CSV-RF2      PIC X(37) VALUE                    00978026
MFTCHG      'SET %DIR1 = %DIR1 + DMM.GMM.SUM.FILE.'.                    00978125
MFTCHG     10  PV-MFT-START-DATE              PIC X(10)  VALUE SPACES.  00978225
MFTCHG     10  FILLER                         PIC X(01)  VALUE '.'.     00978325
MFTCHG     10  PV-MFT-END-DATE                PIC X(10)  VALUE SPACES.  00978425
MFTCHG        10 PV-MFT-FILLER        PIC X(13).                        00978626
MFTCHG 01  PV-MFT-REC4.                                                 00978725
MFTCHG        10 PV-MFT-CSV           PIC X(40) VALUE                   00978826
MFTCHG      ' SET %DIR2 = ".CSV"'.                                      00978925
MFTCHG        10 PV-MFT-FILLER        PIC X(40).                        00979026
MFTCHG 01  PV-MFT-REC5.                                                 00979125
MFTCHG        10 PV-MFT-DIR           PIC X(40) VALUE                   00979226
MFTCHG      ' SET %RF = %DIR1 + %DIR2'.                                 00979325
MFTCHG        10 PV-MFT-FILLER        PIC X(40).                        00979426
MFTCHG 01  PV-MFT-REC6.                                                 00979525
MFTCHG        10 PV-MFT-CSV1          PIC X(40) VALUE                   00979626
MFTCHG      ' SET %NODE = "PCIFSSA"'.                                   00979725
MFTCHG        10 PV-MFT-FILLER        PIC X(40).                        00979826
MFTCHG 01  PV-MFT-REC7.                                                 00979925
MFTCHG        10 PV-MFT-CSV           PIC X(40) VALUE                   00980026
MFTCHG      ' QUOTE OFF'.                                               00980125
MFTCHG        10 PV-MFT-FILLER        PIC X(40).                        00980226
MFTCHG 01  PV-MFT-REC8.                                                 00980325
MFTCHG        10 PV-MFT-CSV3          PIC X(40) VALUE                   00980426
MFTCHG      '  CALL SENDTEXT'.                                          00980525
MFTCHG        10 PV-MFT-FILLER        PIC X(40).                        00980626
MFTCHG 01  PV-MFT-REC9.                                                 00980725
MFTCHG        10 PV-MFT-CSV4          PIC X(40) VALUE                   00980826
MFTCHG      ':DONE'.                                                    00980925
MFTCHG        10 PV-MFT-FILLER        PIC X(40).                        00981026
MFTCHG 01  PV-MFT-REC10.                                                00981125
MFTCHG        10 PV-MFT-CSV5          PIC X(40) VALUE                   00981226
MFTCHG      '  EXIT'.                                                   00981325
MFTCHG        10 PV-MFT-FILLER        PIC X(40).                        00981426
MFTCHG 01  PV-MFT-REC11.                                                00981525
MFTCHG        10 PV-MFT-CSV6          PIC X(60) VALUE                   00981626
MFTCHG      ':SENDTEXT     * PROCESS SEND FILE REQUEST'.                00981725
MFTCHG        10 PV-MFT-FILLER        PIC X(20).                        00981826
MFTCHG 01  PV-MFT-REC12.                                                00981925
MFTCHG        10 PV-MFT-CSV5          PIC X(60) VALUE                   00982026
MFTCHG      '   PROCESS,SAK105C,TRANSFER,SEND'.                         00982125
MFTCHG        10 PV-MFT-FILLER        PIC X(20).                        00982226
MFTCHG 01  PV-MFT-REC13.                                                00982325
MFTCHG        10 PV-MFT-CSV5          PIC X(60) VALUE                   00982426
MFTCHG      '        DSN=PKMO.SA.DMM.GMM.SUM.FILE'.                     00982525
MFTCHG        10 PV-MFT-FILLER        PIC X(20).                        00982626
                                                                        00982708
       01  PV-DMM-GMM-RECORD-LAYOUT.                                    00982808
           05  SA-OUTPUT-DATE                 PIC X(10)  VALUE SPACES.  00982908
           05  FILLER                         PIC X(01)  VALUE ','.     00983008
           05  SA-OUTPUT-DMM-GMM-TYPE         PIC X(03)  VALUE SPACES.  00984008
           05  FILLER                         PIC X(01)  VALUE ','.     00990008
           05  SA-OUTPUT-DMM-GMM-NBR          PIC X(04)  VALUE SPACES.  01000008
           05  FILLER                         PIC X(01)  VALUE ','.     01010008
           05  SA-OUTPUT-REG-AMT              PIC Z(11).Z(2).           01020008
           05  FILLER                         PIC X(01)  VALUE ','.     01030008
           05  SA-OUTPUT-REG-CNT              PIC 9(9).                 01040008
           05  FILLER                         PIC X(01)  VALUE ','.     01050008
           05  SA-OUTPUT-CLR-AMT              PIC Z(11).Z(2).           01060008
           05  FILLER                         PIC X(01)  VALUE ','.     01070008
           05  SA-OUTPUT-CLR-CNT              PIC 9(9).                 01080008
                                                                        01090008
       01  PV-GMM-DMM-TABLE-INDEX-BY-DEPT.                              01100008
           05  PV-GMM-DMM-TBL OCCURS 1000 TIMES                         01110008
                              INDEXED BY PV-GMM-DMM-IDX.                01120008
               10  SA-DEPT-NBR                PIC X(04)  VALUE SPACES.  01130008
               10  SA-DMM-NBR                 PIC X(04)  VALUE SPACES.  01140008
               10  SA-GMM-NBR                 PIC X(04)  VALUE SPACES.  01150008
                                                                        01160008
       01  PV-DMM-TMP-TABLE-INDEX-BY-DMM.                               01170008
           05  PV-DMM-TMP-TBL OCCURS 1000 TIMES                         01180008
                              INDEXED BY PV-DMM-TMP-IDX.                01190008
               10  SA-DMM-NBR-TMP             PIC X(04)  VALUE SPACES.  01200008
               10  SA-DMM-REG-AMOUNT-TMP      PIC S9(11)V9(2) COMP-3    01210008
                                                         VALUE ZEROES.  01220008
               10  SA-DMM-REG-COUNT-TMP       PIC S9(9)  COMP.          01230008
               10  SA-DMM-CLR-AMOUNT-TMP      PIC S9(11)V9(2) COMP-3    01240008
                                                         VALUE ZEROES.  01250008
               10  SA-DMM-CLR-COUNT-TMP       PIC S9(9)  COMP.          01260008
                                                                        01270008
       01  PV-GMM-TMP-TABLE-INDEX-BY-GMM.                               01280008
           05  PV-GMM-TMP-TBL OCCURS 1000 TIMES                         01290008
                              INDEXED BY PV-GMM-TMP-IDX.                01300008
               10  SA-GMM-NBR-TMP             PIC X(04)  VALUE SPACES.  01310008
               10  SA-GMM-REG-AMOUNT-TMP      PIC S9(11)V9(2) COMP-3    01320008
                                                         VALUE ZEROES.  01330008
               10  SA-GMM-REG-COUNT-TMP       PIC S9(9)  COMP.          01340008
               10  SA-GMM-CLR-AMOUNT-TMP      PIC S9(11)V9(2) COMP-3    01350008
                                                         VALUE ZEROES.  01360008
               10  SA-GMM-CLR-COUNT-TMP       PIC S9(9)  COMP.          01370008
                                                                        01380008
      ******************************************************************01390008
      *  KOHLS DATE ROUTINE CALLING PARAMETERS                          01400008
      ******************************************************************01410008
           COPY DPWS500.                                                01420008
                                                                        01430008
      ******************************************************************01440008
      *  DB2 MESSAGE AREA                                               01450008
      ******************************************************************01460008
           COPY SQLCA2.                                                 01470008
                                                                        01480008
      ******************************************************************01490008
      *    DB2 COMMUNICATIONS AREA                                      01500008
      ******************************************************************01510008
           EXEC SQL                                                     01520008
               INCLUDE SQLCA                                            01530008
           END-EXEC.                                                    01540008
                                                                        01550008
      ******************************************************************01560008
      *  DB2 TABLE TEPPART - PARTITION SALES DATE TABLE                 01570008
      ******************************************************************01580008
           EXEC SQL                                                     01590008
               INCLUDE TEPPART                                          01600008
           END-EXEC.                                                    01610008
                                                                        01620008
      ******************************************************************01630008
      *  DB2 TABLE TEPTRN - SALES TRANSACTIONS TABLE                    01640008
      ******************************************************************01650008
           EXEC SQL                                                     01660008
               INCLUDE TEPTRN                                           01670008
           END-EXEC.                                                    01671008
                                                                        01672008
      ******************************************************************01673008
      *  DB2 TABLE TEPITM - LINE ITEM MERCHANDISE TABLE                 01674008
      ******************************************************************01675008
           EXEC SQL                                                     01676008
               INCLUDE TEPITM                                           01677008
           END-EXEC.                                                    01678008
                                                                        01679008
      ******************************************************************01680008
      *  DB2 TABLE XST00001(XST_LOC) - STORE LOCATION TABLE             01690008
      ******************************************************************01700008
           EXEC SQL                                                     01710008
               INCLUDE XST00001                                         01720008
           END-EXEC.                                                    01730008
                                                                        01740008
      ******************************************************************01750008
      *  DB2 TABLE XST00015(XST_DEPT) - DEPARTMENT INFORMATION TABLE    01760008
      ******************************************************************01770008
           EXEC SQL                                                     01780008
               INCLUDE XST00015                                         01790008
           END-EXEC.                                                    01800008
                                                                        01810008
      ******************************************************************01820008
      *  DB2 TABLE XST00067(XST_DMA) - DMM / GMM ASSIGNMENT TABLE       01830008
      ******************************************************************01840008
           EXEC SQL                                                     01850008
               INCLUDE XST00067                                         01860008
           END-EXEC.                                                    01870008
                                                                        01880008
      ******************************************************************01890008
      *  DEPT-DMM-GMM-CSR CURSOR DECLARATION                            01900008
      *  USED TO SET-UP THE DEPARTMENT TO DMM/GMM RELATIONSHIPS TABLE   01910008
      ******************************************************************01920008
           EXEC SQL                                                     01930008
             DECLARE DEPT-DMM-GMM-CSR CURSOR FOR                        01940008
                  SELECT  DEPT.DEPT_NBR                                 01950008
                         ,DMMGMM.DMA_NBR                                01960008
                         ,DMMGMM.GMA_NBR                                01970008
                    FROM  XST_DEPT DEPT                                 01980008
                         ,XST_DMA  DMMGMM                               01990008
                   WHERE  DEPT.DMA_NBR = DMMGMM.DMA_NBR                 02000008
                  WITH UR                                               02010008
                  FOR FETCH ONLY                                        02020008
           END-EXEC.                                                    02030008
                                                                        02040008
      ******************************************************************02050008
      *  SALES-SUM-CSR CURSOR DECLARATION                               02060008
      *  UNION CURSOR: TYPE 'A' = REGULAR & PROMOTIONAL SALES & COUNTS  02070008
      *                TYPE 'C' = CLEARANCE SALES & COUNTS              02080008
      ******************************************************************02090008
           EXEC SQL                                                     02100008
             DECLARE SALES-SUM-CSR CURSOR FOR                           02110008
                  SELECT  SUM(ITM.SLD_QTY)   AS COUNT                   02120008
                         ,SUM(NET_CHRGD_AMT) AS AMOUNT                  02130008
                         ,PRT.SLS_DTE        AS DATE                    02140008
                         ,ITM.DEPT_NBR                                  02150008
                         ,'A'                                           02160008
                    FROM  TEPPART PRT                                   02170008
                         ,TEPTRN  TRN                                   02180008
                         ,TEPITM  ITM                                   02190008
                         ,XST_LOC LOC                                   02200008
                   WHERE  PRT.SLS_DTE BETWEEN :CC-PERIOD-START-DATE     02210008
                                          AND :CC-PERIOD-END-DATE       02220008
                     AND  PRT.DB_STRUC_CDE       = 'O'                  02230008
                     AND  PRT.STR_GP_CDE         = 'A'                  02240008
                     AND  TRN.PARTN_NBR          = PRT.PARTN_NBR        02250008
                     AND  TRN.VOID_ABRT_CDE      = 'N'                  02260008
                     AND  TRN.TRN_STAT_CDE IN ('V', 'Z', 'L')           02270008
                     AND  TRN.TRN_TYP_CDE  IN ('01','02','11','12')     02280008
                                                                        02290008
                     AND  ITM.PARTN_NBR          = TRN.PARTN_NBR        02300008
                     AND  ITM.STR_NBR            = TRN.STR_NBR          02310008
                     AND  ITM.RGST_ID            = TRN.RGST_ID          02320008
                     AND  ITM.TRN_NBR            = TRN.TRN_NBR          02330008
                     AND  ITM.STRT_TM            = TRN.STRT_TM          02340008
                     AND  ITM.SLS_CORR_SEQ_NBR   = TRN.SLS_CORR_SEQ_NBR 02350008
                                                                        02360008
                     AND  ITM.LIV_RSLU_CDE       = '+'                  02370008
                     AND  ITM.SAL_TYP_CDE  IN ('R', 'S', 'P', 'N')      02380008
                     AND  ITM.DEPT_NBR NOT IN ('098', '099', '800',     02390008
                                               '818', '833', '834',     02400008
                                               '817', '981', '983',     02410028
SEPHGC                                         '985', '712', '000')     02412039
                     AND  ITM.STR_NBR            = LOC.LOC_NBR          02420008
                                                                        02430008
                     AND  LOC.OPN_DTE           <= :PV-DB2-FY-COMP-DTE  02440008
                                                                        02450008
                  GROUP BY PRT.SLS_DTE, ITM.DEPT_NBR                    02460008
                                                                        02470008
                UNION                                                   02480008
                                                                        02490008
                  SELECT  SUM(ITM.SLD_QTY)   AS COUNT                   02500008
                         ,SUM(NET_CHRGD_AMT) AS AMOUNT                  02510008
                         ,PRT.SLS_DTE        AS DATE                    02520008
                         ,ITM.DEPT_NBR                                  02530008
                         ,'C'                                           02540008
                    FROM  TEPPART PRT                                   02550008
                         ,TEPTRN  TRN                                   02560008
                         ,TEPITM  ITM                                   02570008
                         ,XST_LOC LOC                                   02580008
                   WHERE  PRT.SLS_DTE BETWEEN :CC-PERIOD-START-DATE     02590008
                                          AND :CC-PERIOD-END-DATE       02600008
                     AND  PRT.DB_STRUC_CDE       = 'O'                  02610008
                     AND  PRT.STR_GP_CDE         = 'A'                  02620008
                     AND  TRN.PARTN_NBR          = PRT.PARTN_NBR        02630008
                     AND  TRN.VOID_ABRT_CDE      = 'N'                  02640008
                     AND  TRN.TRN_STAT_CDE IN ('V', 'Z', 'L')           02650008
                     AND  TRN.TRN_TYP_CDE  IN ('01','02','11','12')     02660008
                                                                        02670008
                     AND  ITM.PARTN_NBR          = TRN.PARTN_NBR        02680008
                     AND  ITM.STR_NBR            = TRN.STR_NBR          02690008
                     AND  ITM.RGST_ID            = TRN.RGST_ID          02700008
                     AND  ITM.TRN_NBR            = TRN.TRN_NBR          02710008
                     AND  ITM.STRT_TM            = TRN.STRT_TM          02720008
                     AND  ITM.SLS_CORR_SEQ_NBR   = TRN.SLS_CORR_SEQ_NBR 02730008
                                                                        02740008
                     AND  ITM.LIV_RSLU_CDE       = '+'                  02750008
                     AND  ITM.SAL_TYP_CDE IN ('G', 'C')                 02760008
                     AND  ITM.DEPT_NBR NOT IN ('098', '099', '800',     02770008
                                               '818', '833', '834',     02780008
                                               '817', '981', '983',     02790028
SEPHGC                                         '985', '712', '000')     02792039
                     AND  ITM.STR_NBR = LOC.LOC_NBR                     02800008
                                                                        02810008
                     AND  LOC.OPN_DTE           <= :PV-DB2-FY-COMP-DTE  02820008
                                                                        02830008
                  GROUP BY PRT.SLS_DTE, ITM.DEPT_NBR                    02840008
                                                                        02850008
                  ORDER BY 3, 4                                         02860008
                WITH UR                                                 02870008
                FOR FETCH ONLY                                          02880008
           END-EXEC.                                                    02890008
                                                                        02900008
      ******************************************************************02910008
                                                                        02920008
      ***************************************************************** 02930008
       PROCEDURE DIVISION.                                              02940008
      ***************************************************************** 02950008
       0000-MAINLINE.                                                   02960008
                                                                        02970008
           PERFORM 1000-INITIALIZATION.                                 02980008
                                                                        02990010
      *    PRE-LOAD A TEMP TABLE WITH DEPT TO DMM/GMM RELATIONSHIPS     03000008
           PERFORM 2000-OPEN-DEPT-DMM-GMM-CSR.                          03010008
           PERFORM 2100-FETCH-DEPT-DMM-GMM-CSR.                         03020008
           PERFORM 2200-PROCESS-DEPT-DMM-GMM-CSR                        03030008
                UNTIL PS-DMM-GMM-END-OF-CSR.                            03040008
           PERFORM 2300-CLOSE-DEPT-DMM-GMM-CSR.                         03050008
                                                                        03060010
      *    PROCESS EACH SALES DATE, ADDING TO DMM/GMM TEMP TABLES       03070008
      *    PRINT OUT THE PREVIOUS SALES DATE TTLS WHEN THE DATE CHANGES 03080008
           PERFORM 3000-OPEN-SALES-SUM-CSR.                             03090008
           PERFORM 3100-FETCH-REG-SALES-CSR.                            03100008
           PERFORM 3200-PROCESS-REG-SALES-CSR                           03110008
                UNTIL PS-REG-SALES-CSR-END.                             03120008
           PERFORM 3300-CLOSE-SALES-SUM-CSR.                            03130008
                                                                        03140010
           PERFORM 9999-WRAP-UP.                                        03150008
                                                                        03160008
           STOP RUN.                                                    03170008
                                                                        03180008
                                                                        03190008
      ******************************************************************03200008
      *  1000-INITIALIZATION.                                           03210008
      *  GET THE CURRENT SYSTEM DATE FROM DB2.                          03220008
      *  CHECK TO SEE IF ANY OVERRIDE START/END DATES ENTERED IN CC INP.03230008
      *  GET THE ANNUAL COMP DATE BY CHECKING FISCAL YEAR DATE VALUES.  03240008
MFTCHG*  WRITE OUT THE FIRST FEW HEADER LINES OF THE GMM/DMM & MFT FILE.03250030
      *  FINALLY, CLEAR OUT THE TEMP TABLES IN PREP FOR USE.            03260008
      ******************************************************************03270008
       1000-INITIALIZATION.                                             03280008
                                                                        03290008
           DISPLAY '*********************************'.                 03300008
           DISPLAY '* SAKUT105 SYSOUT BEGINS HERE ! *'.                 03310008
           DISPLAY '*********************************'.                 03320008
           DISPLAY ' '.                                                 03330008
                                                                        03340008
      *    OVERRIDE CC INPUT WILL BE EMPTY ON NORMAL PROD RUNS          03350008
           PERFORM 1100-READ-OVERRIDE-CC-INPUT.                         03360008
                                                                        03370008
      *    IF OVERRIDE INPUT IS EMPTY, FIGURE OUT THE START / END DATES 03380008
           IF CC-PERIOD-START-DATE EQUAL SPACES                         03390008
      *       ESP SCHEDULE LOAD DATE WILL BE OUR SQL PERIOD END DATE    03400008
              PERFORM 1200-READ-SCHED-LOAD-CC-INPUT                     03410008
              PERFORM 1300-GET-SQL-PERIOD-START-DATE                    03420008
           END-IF.                                                      03430008
                                                                        03440008
      *    FIGURE OUT COMP DATE TO CHECK FOR OPEN STORES BEFORE X DATE  03450008
           PERFORM 1400-GET-ANNUAL-COMP-DATE.                           03460008
           PERFORM 1500-GET-ANNUAL-COMP-STORES.                         03470008
                                                                        03480008
           OPEN OUTPUT DMM-GMM-SUM-FILE                                 03490008
MFTCHG                 MFT-COMMANDS-FILE.                               03500030
           INITIALIZE  DMM-GMM-SUM-FILE-RECORD                          03510008
                       MFT-COMMANDS-RECORD.                             03520020
           PERFORM 1600-WRITE-DMM-GMM-HDR-LINES.                        03530008
MFTCHG     PERFORM 1700-WRITE-MFT-COMMAND-LINES.                        03540030
                                                                        03550008
           PERFORM 5000-CLEAR-OUT-TEMP-TABLES.                          03560008
                                                                        03570008
      ******************************************************************03580008
      *  1100-READ-OVERRIDE-CC-INPUT.                                   03590008
      *  RETRIEVE DATES FROM CONTROL CARD INPUT IF ENTERED.             03600008
      *  NORMALLY IN PRODUCTION THE DATES IN THE CC WILL BE EMPTY,      03610008
      *  HOWEVER THIS WILL ALLOW SUPPORT TO RUN THIS JOB ON A DAY       03620008
      *  OTHER THAN MONDAY IF REQUIRED DUE TO SYSTEM ISSUES.            03630008
      ******************************************************************03640008
       1100-READ-OVERRIDE-CC-INPUT.                                     03650008
                                                                        03660008
           INITIALIZE SQL-DATE-EXECUTION.                               03670008
           OPEN INPUT OVERRIDE-DATE-PERIOD-FILE.                        03680008
                                                                        03690008
           READ OVERRIDE-DATE-PERIOD-FILE INTO SQL-DATE-EXECUTION.      03700008
                                                                        03710008
           IF CC-PERIOD-START-DATE EQUAL SPACES                         03720008
              DISPLAY 'OVERRIDE START/END DATES NOT ENTERED...'         03730008
              DISPLAY 'SELECTING PREVIOUS WEEK SUNDAY THROUGH SATURDAY.'03740008
           ELSE                                                         03750008
              DISPLAY 'OVERRIDE START/END DATES FOUND IN INP01 CC...'   03760008
              DISPLAY '         START DATE: ' CC-PERIOD-START-DATE      03770008
              DISPLAY '           END DATE: ' CC-PERIOD-END-DATE        03780008
      *       REQUIRED FOR FISCAL YEAR DETERMINATION LATER ON....       03790008
              MOVE CC-PERIOD-END-DATE TO PV-CUR-SYS-DATE                03800008
           END-IF.                                                      03810008
                                                                        03820008
           CLOSE OVERRIDE-DATE-PERIOD-FILE.                             03830008
                                                                        03840008
      ******************************************************************03850008
      *  1200-READ-SCHED-LOAD-CC-INPUT.                                 03860008
      *  RETRIEVE THE ESP SCHEDULE LOAD DATE. THIS SHOULD BE A SATURDAY 03870008
      *  DATE, WHICH IS OUR END DATE. ALL WE SHOULD HAVE TO DO NOW IS   03880008
      *  TO CALL THE DATE ROUTINE LATER ON TO PULL THE SUN START DATE.  03890008
      ******************************************************************03900008
       1200-READ-SCHED-LOAD-CC-INPUT.                                   03910008
                                                                        03920008
           INITIALIZE PV-CUR-SYS-DATE.                                  03930008
           OPEN INPUT SCHEDULE-LOAD-DATE-FILE.                          03940008
                                                                        03950008
           READ SCHEDULE-LOAD-DATE-FILE INTO PV-CUR-SYS-DATE.           03960008
           IF PV-CUR-SYS-DATE EQUAL SPACES                              03970008
             MOVE '1200-READ-SCHED-LOAD-CC-INPUT'  TO AB-PARAGRAPH-NAME 03980008
             MOVE 'ESP SCHD LOAD DATE CC IS EMPTY' TO AB-OPERATION-MSG-103990008
             MOVE 'PLEASE UPDATE THE CC AND RERUN' TO AB-OPERATION-MSG-204000008
             SET ABEND-4003-CTRL-CARD-ERROR        TO TRUE              04010008
             PERFORM 9000-NON-SQL-ABEND                                 04020008
           ELSE                                                         04030008
              DISPLAY 'ESP SCHEDULE LOAD DATE CC: ' PV-CUR-SYS-DATE     04040008
              MOVE PV-CUR-SYS-DATE                 TO CC-PERIOD-END-DATE04050008
           END-IF.                                                      04060008
                                                                        04070008
           CLOSE SCHEDULE-LOAD-DATE-FILE.                               04080008
                                                                        04090008
      ******************************************************************04100008
      *  1300-GET-SQL-PERIOD-START-DATE.                                04110008
      *  RETRIEVE THE SUNDAY START DATE, ASSUMES THIS JOB LOADED INTO   04120008
      *  THE ESP SCHEDULE ON SATURDAY AND RUNS EARLY SUNDAY MORNING.    04130008
      ******************************************************************04140008
       1300-GET-SQL-PERIOD-START-DATE.                                  04150008
                                                                        04160008
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              04170008
           SET DPG51-FISCAL-454-CALENDAR   TO TRUE.                     04180008
           SET DPG52-LK-DTE-ISO-FMT        TO TRUE.                     04190008
           SET DPG51-DO-NOT-INCR-DECR-DATE TO TRUE.                     04200008
           MOVE PV-CUR-SYS-DATE            TO DPG52-DB2-ISO-DATE-FMT.   04210008
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    04220008
                                                   DPG54 DPG55 DPG56.   04230008
                                                                        04240008
           IF DPG54-SEVERE-ERROR OR DPG54-DATE-INVALID                  04250008
             MOVE '1300-GET-SQL-PERIOD-START-DATE' TO AB-PARAGRAPH-NAME 04260008
             MOVE 'DATE ROUTINE - CALC START DATE' TO AB-OPERATION-MSG-104270008
             MOVE DPG54-ERROR-MESSAGE              TO AB-OPERATION-MSG-204280008
             SET ABEND-4019-DATE-ROUTINE-ERROR     TO TRUE              04290008
             PERFORM 9000-NON-SQL-ABEND                                 04300008
           END-IF.                                                      04310008
                                                                        04320008
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT       TO CC-PERIOD-START-DATE.04330008
           DISPLAY 'PREV WEEK SUNDAY START DATE: ' CC-PERIOD-START-DATE.04340008
                                                                        04350008
      ******************************************************************04360008
      *  1400-GET-ANNUAL-COMP-DATE.                                     04370008
      *  FEED THE DATE ROUTINE THE CURRENT SYSTEM DATE AS INPUT TO GET  04380008
      *  THE FIRST DAY OF THE FISCAL YEAR FOR THAT DATE. SUBTRACT 2 YRS 04390008
      *  TO HELP DETERMINE THE ANNUAL COMP DATE THAT WILL BE USED IN THE04400008
      *  SQL. IE. CURRENT SYS DATE = 2/15/09, AND THE DATE ON THE FY IS 04410008
      *  STILL 2009, SO WE WANT THE SQL TO PULL ALL STORES OPEN AS OF   04420008
      *  12/31/2007... OR OPENED AS OF 2008 FOR THE ANNUAL COMP CALC.   04420108
      ******************************************************************04420208
       1400-GET-ANNUAL-COMP-DATE.                                       04420308
                                                                        04420408
           INITIALIZE DPG51 DPG52 DPG53                                 04420508
                      DPG54 DPG55 DPG56.                                04420608
                                                                        04420708
           SET DPG51-FISCAL-454-CALENDAR   TO TRUE.                     04420808
           SET DPG52-LK-DTE-ISO-FMT        TO TRUE.                     04420908
           MOVE PV-CUR-SYS-DATE            TO DPG52-LK-DATE-INPUT.      04421008
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    04421108
                                                   DPG54 DPG55 DPG56.   04421208
                                                                        04421308
           IF DPG54-SEVERE-ERROR OR DPG54-DATE-INVALID                  04421408
             MOVE '1400-GET-ANNUAL-COMP-DATE   ' TO AB-PARAGRAPH-NAME   04421508
             MOVE 'DT ROUTINE - GET ANNUAL COMP' TO AB-OPERATION-MSG-1  04421608
             MOVE DPG54-ERROR-MESSAGE            TO AB-OPERATION-MSG-2  04421708
             SET ABEND-4019-DATE-ROUTINE-ERROR   TO TRUE                04421808
             PERFORM 9000-NON-SQL-ABEND                                 04421908
           END-IF.                                                      04422008
                                                                        04422108
           COMPUTE PV-DTE-COMP-TEMP-CCYY =                              04422208
                   ( DPG56-FIRST-FD-DB2-ISO-FMT-YR - 2 ) .              04422308
           MOVE PV-DTE-COMP-TEMP-CCYY TO PV-DTE-COMP-CCYY.              04422408
           MOVE PV-ANNUAL-COMP-DATE   TO PV-DB2-FY-COMP-DTE.            04422508
           DISPLAY 'DB2 FY COMP DATE: '  PV-DB2-FY-COMP-DTE.            04422608
                                                                        04422708
      ******************************************************************04422808
      *  1500-GET-ANNUAL-COMP-STORES.                                   04422908
      *  BASED ON THE ANNUAL FY COMP DATE RETRIEVED, FIGURE OUT HOW MANY04423008
      *  STORES ARE BEING INCLUDED IN THE SQL QUERIES FOR THIS SUMMARY. 04423108
      ******************************************************************04423208
       1500-GET-ANNUAL-COMP-STORES.                                     04423308
                                                                        04423408
           EXEC SQL                                                     04423508
              SELECT  COUNT(LOC_NBR)                                    04423608
                INTO  :PV-ANNUAL-COMP-STORES-COUNT                      04423708
                FROM  XST_LOC                                           04423808
               WHERE  LOC_TYP_CDE  = 'DS'                               04423908
                 AND  OPN_DTE     <= :PV-DB2-FY-COMP-DTE                04424008
                 AND  CLO_DTE     >= :PV-DB2-FY-COMP-DTE                04424108
              WITH UR                                                   04424208
           END-EXEC                                                     04424308
                                                                        04424408
           EVALUATE TRUE                                                04424508
             WHEN SQLCODE = +0                                          04424608
             WHEN SQLCODE = +100                                        04424708
               DISPLAY 'ANNUAL COMP STORES:' PV-ANNUAL-COMP-STORES-COUNT04424808
                                                                        04424908
             WHEN OTHER                                                 04425008
               MOVE '1500-GET-ANNUAL-COMP-STORES' TO AB-PARAGRAPH-NAME  04425108
               MOVE 'SELECT COUNT OF STORES     ' TO AB-OPERATION-MSG-1 04425208
               PERFORM 9000-SQL-ABEND                                   04425308
           END-EVALUATE.                                                04425408
                                                                        04425508
      ******************************************************************04425608
      *  1600-WRITE-DMM-GMM-HDR-LINES.                                  04425708
      *  SET UP AND WRITE OUT THE FIRST FEW LINES IN THE DMM / GMM FILE 04425808
      *  INCLUDING LINES THAT DISPLAY THE DATES BEING EXTRACTED AND THE 04425908
      *  NUMBER OF STORES INCLUDED IN THE DETAILS.                      04426008
      ******************************************************************04426108
       1600-WRITE-DMM-GMM-HDR-LINES.                                    04426208
                                                                        04426308
           WRITE DMM-GMM-SUM-FILE-RECORD                                04426408
                 FROM PC-OUTPUT-DMM-GMM-HDR-LNE-1.                      04426508
           WRITE DMM-GMM-SUM-FILE-RECORD                                04426608
                 FROM PC-OUTPUT-DMM-GMM-HDR-LNE-2.                      04426708
           MOVE  PV-ANNUAL-COMP-STORES-COUNT TO PV-DISPLAY-STORES-COUNT.04426808
           MOVE  PV-DB2-FY-COMP-DTE TO PV-DISPLAY-COMP-DATE.            04426908
           WRITE DMM-GMM-SUM-FILE-RECORD                                04427008
                 FROM PV-OUTPUT-DMM-GMM-HDR-LNE-3.                      04427108
                                                                        04427208
           MOVE CC-PERIOD-START-DATE TO PV-DISPLAY-START-DATE.          04427308
           MOVE CC-PERIOD-END-DATE TO PV-DISPLAY-END-DATE.              04427408
           WRITE DMM-GMM-SUM-FILE-RECORD                                04427508
                 FROM PV-OUTPUT-DMM-GMM-HDR-LNE-4.                      04427608
                                                                        04427708
           MOVE SPACES TO DMM-GMM-SUM-FILE-RECORD.                      04427808
           WRITE DMM-GMM-SUM-FILE-RECORD.                               04427908
                                                                        04428008
           MOVE PV-OUTPUT-DMM-GMM-HDR-LNE-6 TO DMM-GMM-SUM-FILE-RECORD. 04429008
           WRITE DMM-GMM-SUM-FILE-RECORD.                               04429108
                                                                        04429208
MFTCHG******************************************************************04429329
MFTCHG*  1700-WRITE-MFT-COMMAND-LINES.                                  04429429
MFTCHG*  SET UP AND WRITE OUT THE MFT COMMAND LINES, INCLUDING          04429529
MFTCHG*  WRITING OUT A UNIQUE FILE NAME USING START / END DATES.        04429629
MFTCHG******************************************************************04429729
MFTCHG 1700-WRITE-MFT-COMMAND-LINES.                                    04429829
MFTCHG                                                                  04429929
MFTCHG     MOVE PV-MFT-REC TO MFT-COMMANDS-RECORD.                      04430025
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04430125
MFTCHG     MOVE PV-MFT-REC1  TO MFT-COMMANDS-RECORD.                    04430225
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04430325
MFTCHG     MOVE CC-PERIOD-END-DATE   TO PV-MFT-END-DATE.                04430425
MFTCHG     MOVE CC-PERIOD-START-DATE TO PV-MFT-START-DATE.              04430525
MFTCHG     MOVE PV-MFT-REC2 TO MFT-COMMANDS-RECORD.                     04430625
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04430725
MFTCHG     MOVE PV-MFT-REC4 TO MFT-COMMANDS-RECORD.                     04430825
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04430925
MFTCHG     MOVE PV-MFT-REC5 TO MFT-COMMANDS-RECORD.                     04431025
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04431125
MFTCHG     MOVE PV-MFT-REC6 TO MFT-COMMANDS-RECORD.                     04431225
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04431325
MFTCHG     MOVE PV-MFT-REC7 TO MFT-COMMANDS-RECORD.                     04431425
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04431525
MFTCHG     MOVE PV-MFT-REC8 TO MFT-COMMANDS-RECORD.                     04431625
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04431725
MFTCHG     MOVE PV-MFT-REC9 TO MFT-COMMANDS-RECORD.                     04431825
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04431925
MFTCHG     MOVE PV-MFT-REC10 TO MFT-COMMANDS-RECORD.                    04432025
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04432125
MFTCHG     MOVE PV-MFT-REC11 TO MFT-COMMANDS-RECORD.                    04432225
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04432325
MFTCHG     MOVE PV-MFT-REC12 TO MFT-COMMANDS-RECORD.                    04432425
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04432525
MFTCHG     MOVE PV-MFT-REC13 TO MFT-COMMANDS-RECORD.                    04432625
MFTCHG     WRITE MFT-COMMANDS-RECORD.                                   04432725
                                                                        04434708
      ******************************************************************04434808
      *  2000-OPEN-DEPT-DMM-GMM-CSR.                                    04434908
      *  OPEN THE DEPT DMM/GMM CROSS REFERENCE CURSOR.                  04435008
      ******************************************************************04435108
       2000-OPEN-DEPT-DMM-GMM-CSR.                                      04435208
                                                                        04435308
           EXEC SQL                                                     04435408
               OPEN DEPT-DMM-GMM-CSR                                    04435508
           END-EXEC.                                                    04435608
                                                                        04435708
           EVALUATE TRUE                                                04435808
               WHEN SQLCODE = +0                                        04435908
                   DISPLAY 'SUCCESSFUL OPEN DEPT-DMM-GMM-CSR'           04436008
                   CONTINUE                                             04436108
               WHEN OTHER                                               04436208
                   MOVE '2000-OPEN-DEPT-DMM-GMM-CSR       '             04436308
                                     TO AB-PARAGRAPH-NAME               04436408
                   MOVE 'OPEN ERROR ON DEPT-DMM-GMM-CSR '               04437008
                                     TO AB-OPERATION-MSG-1              04438008
                   PERFORM 9000-SQL-ABEND                               04439008
           END-EVALUATE.                                                04440008
                                                                        04450008
      ******************************************************************04460008
      *  2100-FETCH-DEPT-DMM-GMM-CSR.                                   04470008
      *  FETCH THE DATA FROM THE DEPT DMM/GMM CROSS REFERENCE CURSOR.   04480008
      ******************************************************************04490008
       2100-FETCH-DEPT-DMM-GMM-CSR.                                     04500008
                                                                        04510008
           EXEC SQL                                                     04520008
               FETCH  DEPT-DMM-GMM-CSR                                  04530008
                INTO  :SA-DEPT-NBR-CUR                                  04540008
                    , :SA-DMM-NBR-CUR                                   04550008
                    , :SA-GMM-NBR-CUR                                   04560008
           END-EXEC.                                                    04570008
                                                                        04580008
           EVALUATE TRUE                                                04590008
               WHEN SQLCODE = +0                                        04600008
                 CONTINUE                                               04610008
                                                                        04620008
               WHEN SQLCODE = +100                                      04630008
                   DISPLAY 'END OF DEPT-DMM-GMM-CSR'                    04640008
                   SET PS-DMM-GMM-END-OF-CSR TO TRUE                    04650008
                                                                        04660008
               WHEN OTHER                                               04670008
                   MOVE '2100-FETCH-DEPT-DMM-GMM-CSR      '             04680008
                                     TO AB-PARAGRAPH-NAME               04690008
                   MOVE 'FETCH ERROR ON DEPT-DMM-GMM-CSR'               04700008
                                     TO AB-OPERATION-MSG-1              04710008
                   PERFORM 9000-SQL-ABEND                               04720008
           END-EVALUATE.                                                04730008
                                                                        04740008
      ******************************************************************04750008
      *  2200-PROCESS-DEPT-DMM-GMM-CSR.                                 04760008
      *  PROCESS THE DATA RETRIEVED FROM THE FETCH.                     04770008
      ******************************************************************04780008
       2200-PROCESS-DEPT-DMM-GMM-CSR.                                   04790008
                                                                        04800008
      *    USE DEPT NBR AS AN INDEX TO THE DMM/GMM DEPT XREF TABLE      04810008
           MOVE SA-DEPT-NBR-CUR   TO PV-GMM-DMM-IDX-TEMP                04820008
           SET PV-GMM-DMM-IDX     TO PV-GMM-DMM-IDX-TEMP                04830008
                                                                        04840008
           MOVE SA-DEPT-NBR-CUR   TO SA-DEPT-NBR(PV-GMM-DMM-IDX)        04850008
           MOVE SA-DMM-NBR-CUR    TO SA-DMM-NBR(PV-GMM-DMM-IDX)         04860008
           MOVE SA-GMM-NBR-CUR    TO SA-GMM-NBR(PV-GMM-DMM-IDX)         04870008
                                                                        04880008
           PERFORM 2100-FETCH-DEPT-DMM-GMM-CSR.                         04890008
                                                                        04900008
      ***************************************************************** 04910008
      *  2300-CLOSE-DEPT-DMM-GMM-CSR.                                   04920008
      *  CLOSE THE DEPT DMM/GMM CROSS REFERENCE CURSOR.                 04930008
      ***************************************************************** 04940008
       2300-CLOSE-DEPT-DMM-GMM-CSR.                                     04950008
                                                                        04960008
           EXEC SQL                                                     04970008
               CLOSE DEPT-DMM-GMM-CSR                                   04980008
           END-EXEC.                                                    04990008
                                                                        05000008
           EVALUATE TRUE                                                05010008
               WHEN SQLCODE = +0                                        05020008
                   CONTINUE                                             05030008
                                                                        05040008
               WHEN OTHER                                               05050008
                   MOVE '2300-CLOSE-DEPT-DMM-GMM-CSR   '                05060008
                                     TO AB-PARAGRAPH-NAME               05070008
                   MOVE 'CLOSE ERROR ON DEPT-DMM-GMM-CSR'               05080008
                                     TO AB-OPERATION-MSG-1              05090008
                   PERFORM 9000-SQL-ABEND                               05100008
           END-EVALUATE.                                                05110008
                                                                        05120008
      ******************************************************************05130008
      *  3000-OPEN-SALES-SUM-CSR.                                       05140008
      *  OPENS THE SALES SUMMARY BY DATE AND DEPT CURSOR.               05150008
      ******************************************************************05160008
       3000-OPEN-SALES-SUM-CSR.                                         05170008
                                                                        05180008
           EXEC SQL                                                     05190008
               OPEN SALES-SUM-CSR                                       05200008
           END-EXEC.                                                    05210008
                                                                        05220008
           EVALUATE TRUE                                                05230008
               WHEN SQLCODE = +0                                        05240008
                   CONTINUE                                             05250008
                                                                        05260008
               WHEN OTHER                                               05270008
                   MOVE '3000-OPEN-SALES-CSR             '              05280008
                                     TO AB-PARAGRAPH-NAME               05290008
                   MOVE 'OPEN ERROR ON SALES-CSR '                      05300008
                                     TO AB-OPERATION-MSG-1              05310008
                   DISPLAY 'START DATE:|' CC-PERIOD-START-DATE          05320008
                   DISPLAY '  END DATE:|' CC-PERIOD-END-DATE            05330008
                   DISPLAY ' COMP DATE:|' PV-DB2-FY-COMP-DTE            05340008
                   PERFORM 9000-SQL-ABEND                               05350008
           END-EVALUATE.                                                05360008
                                                                        05370008
      ******************************************************************05380008
      *  3100-FETCH-REG-SALES-CSR.                                      05390008
      *  FETCH THE DATA FROM THE SALES SUMMARY BY DATE AND DEPT CURSOR. 05400008
      ******************************************************************05410008
       3100-FETCH-REG-SALES-CSR.                                        05420008
                                                                        05430008
           EXEC SQL                                                     05440008
               FETCH  SALES-SUM-CSR                                     05450008
                INTO  :PV-CSR-ITEM-COUNT                                05460008
                    , :PV-CSR-SALES                                     05470008
                    , :PV-CSR-SALES-DATE                                05480008
                    , :PV-CSR-DEPT-NBR                                  05490008
                    , :PV-SALE-TYPE                                     05500008
           END-EXEC.                                                    05510008
                                                                        05520008
           EVALUATE TRUE                                                05530008
               WHEN SQLCODE = +0                                        05540008
                   CONTINUE                                             05550008
                                                                        05560008
               WHEN SQLCODE = +100                                      05570008
                   DISPLAY 'END OF SALES-CSR'                           05580008
                   SET PS-REG-SALES-CSR-END TO TRUE                     05590008
                                                                        05600008
               WHEN OTHER                                               05610008
                   MOVE '2100-FETCH-SALES-CSR      '                    05620008
                                     TO AB-PARAGRAPH-NAME               05630008
                   MOVE 'FETCH ERROR ON SALES-CSR'                      05640008
                                     TO AB-OPERATION-MSG-1              05650008
                   PERFORM 9000-SQL-ABEND                               05660008
           END-EVALUATE.                                                05670008
                                                                        05680008
      ******************************************************************05690008
      *  3200-PROCESS-REG-SALES-CSR.                                    05700008
      *  PROCESS THE DATA RETRIEVED FROM THE FETCH.                     05710008
      ******************************************************************05720008
       3200-PROCESS-REG-SALES-CSR.                                      05730008
                                                                        05740008
           MOVE PV-CSR-SALES-DATE TO PV-CURRENT-DATE                    05750008
           IF PV-CURRENT-DATE NOT EQUAL PV-PREVIOUS-DATE                05760008
              PERFORM 4000-WRITE-SLS-RPT-DETAIL-LINE                    05770008
              PERFORM 5000-CLEAR-OUT-TEMP-TABLES                        05780008
           END-IF                                                       05790008
                                                                        05800008
           MOVE PV-CSR-DEPT-NBR TO PV-GMM-DMM-IDX-TEMP                  05810008
           SET  PV-GMM-DMM-IDX  TO PV-GMM-DMM-IDX-TEMP                  05820008
                                                                        05830008
           IF SA-DEPT-NBR(PV-GMM-DMM-IDX) EQUAL SPACES                  05840008
              DISPLAY 'DEPT ' PV-CSR-DEPT-NBR ' '                       05850008
                      'INVALID, NO GMM/DMM ASSOC EXISTS'                05860008
           ELSE                                                         05870008
              MOVE SA-DMM-NBR(PV-GMM-DMM-IDX) TO PV-NUM-DMM-IDX         05880008
              SET PV-DMM-TMP-IDX TO PV-NUM-DMM-IDX                      05890008
                                                                        05900008
              MOVE SA-DMM-NBR(PV-GMM-DMM-IDX)                           05910008
                           TO SA-DMM-NBR-TMP(PV-DMM-TMP-IDX)            05920008
                                                                        05930008
              IF PV-SALE-TYPE EQUAL 'A'                                 05940008
                 ADD PV-CSR-SALES                                       05950008
                     TO SA-DMM-REG-AMOUNT-TMP(PV-DMM-TMP-IDX)           05960008
                 ADD PV-CSR-ITEM-COUNT                                  05970008
                     TO SA-DMM-REG-COUNT-TMP(PV-DMM-TMP-IDX)            05980008
              END-IF                                                    05990008
                                                                        06000008
              IF PV-SALE-TYPE EQUAL 'C'                                 06010008
                 ADD PV-CSR-SALES                                       06020008
                     TO SA-DMM-CLR-AMOUNT-TMP(PV-DMM-TMP-IDX)           06030008
                 ADD PV-CSR-ITEM-COUNT                                  06040008
                     TO SA-DMM-CLR-COUNT-TMP(PV-DMM-TMP-IDX)            06050008
              END-IF                                                    06060008
                                                                        06070008
              MOVE SA-GMM-NBR(PV-GMM-DMM-IDX) TO PV-NUM-GMM-IDX         06080008
              SET PV-GMM-TMP-IDX TO PV-NUM-GMM-IDX                      06090008
                                                                        06100008
              MOVE SA-GMM-NBR(PV-GMM-DMM-IDX)                           06110008
                TO SA-GMM-NBR-TMP(PV-GMM-TMP-IDX)                       06120008
                                                                        06130008
              IF PV-SALE-TYPE EQUAL 'A'                                 06140008
                 ADD PV-CSR-SALES                                       06150008
                     TO SA-GMM-REG-AMOUNT-TMP(PV-GMM-TMP-IDX)           06160008
                 ADD PV-CSR-ITEM-COUNT                                  06170008
                     TO SA-GMM-REG-COUNT-TMP(PV-GMM-TMP-IDX)            06180008
              END-IF                                                    06190008
                                                                        06200008
              IF PV-SALE-TYPE EQUAL 'C'                                 06210008
                 ADD PV-CSR-SALES                                       06220008
                     TO SA-GMM-CLR-AMOUNT-TMP(PV-GMM-TMP-IDX)           06230008
                 ADD PV-CSR-ITEM-COUNT                                  06240008
                     TO SA-GMM-CLR-COUNT-TMP(PV-GMM-TMP-IDX)            06250008
              END-IF                                                    06260008
           END-IF                                                       06270008
                                                                        06280008
           MOVE PV-CURRENT-DATE TO PV-PREVIOUS-DATE                     06290008
           PERFORM 3100-FETCH-REG-SALES-CSR.                            06300008
                                                                        06310008
      ***************************************************************** 06320008
      *  3300-CLOSE-SALES-SUM-CSR.                                      06330008
      *  CLOSE SALES SUMMARY BY DATE AND DEPT CURSOR.                   06340008
      ***************************************************************** 06350008
       3300-CLOSE-SALES-SUM-CSR.                                        06360008
                                                                        06370008
           EXEC SQL                                                     06380008
               CLOSE SALES-SUM-CSR                                      06390008
           END-EXEC.                                                    06400008
                                                                        06410008
           EVALUATE TRUE                                                06420008
               WHEN SQLCODE = +0                                        06430008
                   CONTINUE                                             06440008
                                                                        06450008
               WHEN OTHER                                               06460008
                   MOVE '3300-CLOSE-SALES-CSR   '                       06470008
                                     TO AB-PARAGRAPH-NAME               06480008
                   MOVE 'CLOSE ERROR ON SALES-CSR'                      06490008
                                     TO AB-OPERATION-MSG-1              06500008
                   PERFORM 9000-SQL-ABEND                               06510008
           END-EVALUATE.                                                06520008
                                                                        06530008
      ******************************************************************06540008
      *  4000-WRITE-SLS-RPT-DETAIL-LINE.                                06550008
      *  WRITE OUT A SUM LINE FROM EACH OF THE TEMP TABLES (DMM/GMM).   06560008
      ******************************************************************06570008
       4000-WRITE-SLS-RPT-DETAIL-LINE.                                  06580008
                                                                        06590008
           MOVE 1 TO PV-INDEX1                                          06600008
           PERFORM VARYING PV-INDEX1 FROM 1 BY 1                        06610008
              UNTIL PV-INDEX1 = PC-MAX-TEMP-TABLE-LIMIT                 06620008
              IF SA-DMM-NBR-TMP(PV-INDEX1) > 0                          06630008
                                                                        06640008
                  MOVE PV-PREVIOUS-DATE TO SA-OUTPUT-DATE               06650008
                  MOVE 'DMM' TO SA-OUTPUT-DMM-GMM-TYPE                  06660008
                  MOVE SA-DMM-NBR-TMP(PV-INDEX1)                        06670008
                       TO SA-OUTPUT-DMM-GMM-NBR                         06680008
                  MOVE SA-DMM-REG-AMOUNT-TMP(PV-INDEX1)                 06690008
                       TO SA-OUTPUT-REG-AMT                             06700008
                  MOVE SA-DMM-REG-COUNT-TMP(PV-INDEX1)                  06710008
                       TO SA-OUTPUT-REG-CNT                             06720008
                  MOVE SA-DMM-CLR-AMOUNT-TMP(PV-INDEX1)                 06730008
                       TO SA-OUTPUT-CLR-AMT                             06740008
                  MOVE SA-DMM-CLR-COUNT-TMP(PV-INDEX1)                  06750008
                       TO SA-OUTPUT-CLR-CNT                             06760008
                                                                        06770008
                  WRITE DMM-GMM-SUM-FILE-RECORD                         06780008
                        FROM PV-DMM-GMM-RECORD-LAYOUT                   06790008
                  INITIALIZE PV-DMM-GMM-RECORD-LAYOUT                   06800008
                                                                        06810008
              END-IF                                                    06820008
           END-PERFORM.                                                 06830008
                                                                        06840008
           MOVE 1 TO PV-INDEX1                                          06850008
           PERFORM VARYING PV-INDEX1 FROM 1 BY 1                        06860008
              UNTIL PV-INDEX1 = PC-MAX-TEMP-TABLE-LIMIT                 06870008
              IF SA-GMM-NBR-TMP(PV-INDEX1) > 0                          06880008
                                                                        06890008
                  MOVE PV-PREVIOUS-DATE TO SA-OUTPUT-DATE               06900008
                  MOVE 'GMM' TO SA-OUTPUT-DMM-GMM-TYPE                  06910008
                  MOVE SA-GMM-NBR-TMP(PV-INDEX1)                        06920008
                       TO SA-OUTPUT-DMM-GMM-NBR                         06930008
                  MOVE SA-GMM-REG-AMOUNT-TMP(PV-INDEX1)                 06940008
                       TO SA-OUTPUT-REG-AMT                             06950008
                  MOVE SA-GMM-REG-COUNT-TMP(PV-INDEX1)                  06960008
                       TO SA-OUTPUT-REG-CNT                             06970008
                  MOVE SA-GMM-CLR-AMOUNT-TMP(PV-INDEX1)                 06980008
                       TO SA-OUTPUT-CLR-AMT                             06990008
                  MOVE SA-GMM-CLR-COUNT-TMP(PV-INDEX1)                  07000008
                       TO SA-OUTPUT-CLR-CNT                             07010008
                                                                        07020008
                  WRITE DMM-GMM-SUM-FILE-RECORD                         07030008
                        FROM PV-DMM-GMM-RECORD-LAYOUT                   07040008
                  INITIALIZE PV-DMM-GMM-RECORD-LAYOUT                   07050008
                                                                        07060008
              END-IF                                                    07070008
           END-PERFORM.                                                 07080008
                                                                        07090008
      ******************************************************************07100008
      *  5000-CLEAR-OUT-TEMP-TABLES.                                    07110008
      *  CLEAR OUT TEMPORARY TABLES                                   * 07120008
      ******************************************************************07130008
       5000-CLEAR-OUT-TEMP-TABLES.                                      07140008
                                                                        07150008
           MOVE 1 TO PV-INDEX1                                          07160008
           PERFORM VARYING PV-INDEX1 FROM 1 BY 1                        07170008
              UNTIL PV-INDEX1 = PC-MAX-TEMP-TABLE-LIMIT                 07180008
              MOVE ZEROES TO   SA-DMM-REG-AMOUNT-TMP(PV-INDEX1)         07190008
                               SA-DMM-REG-COUNT-TMP(PV-INDEX1)          07200008
                               SA-DMM-CLR-AMOUNT-TMP(PV-INDEX1)         07210008
                               SA-DMM-CLR-COUNT-TMP(PV-INDEX1)          07220008
              MOVE SPACES TO   SA-DMM-NBR-TMP(PV-INDEX1)                07230008
           END-PERFORM.                                                 07240008
                                                                        07250008
           MOVE 1 TO PV-INDEX1                                          07260008
           PERFORM VARYING PV-INDEX1 FROM 1 BY 1                        07270008
              UNTIL PV-INDEX1 = PC-MAX-TEMP-TABLE-LIMIT                 07280008
              MOVE ZEROES TO   SA-GMM-REG-AMOUNT-TMP(PV-INDEX1)         07290008
                               SA-GMM-REG-COUNT-TMP(PV-INDEX1)          07300008
                               SA-GMM-CLR-AMOUNT-TMP(PV-INDEX1)         07310008
                               SA-GMM-CLR-COUNT-TMP(PV-INDEX1)          07320008
              MOVE SPACES TO   SA-GMM-NBR-TMP(PV-INDEX1)                07330008
           END-PERFORM.                                                 07340008
                                                                        07350008
      ******************************************************************07360008
      *  9000-SQL-ABEND.                                                07370008
      *  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL    07380008
      *  BASED ERROR HAPPENS DURING THE COURSE OF PROGRAM EXECUTION.    07390008
      ******************************************************************07400008
       9000-SQL-ABEND.                                                  07410008
                                                                        07420008
           PERFORM 9999-WRAP-UP.                                        07430008
                                                                        07440008
           MOVE SQLCODE TO SQLCODE2.                                    07450008
           MOVE SQLERRD(3) TO SQLERRD3.                                 07460008
           DISPLAY '** ABEND           ** = SQLABEND'.                  07470008
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  07480008
           DISPLAY '** PARAGRAPH       ** = ' AB-PARAGRAPH-NAME.        07490008
           DISPLAY '** OPERATION       ** = ' AB-OPERATION-MSG-1.       07500008
           DISPLAY '** SQLCODE         ** = ' SQLCODE2.                 07510008
           DISPLAY '** ROWS PROCESSED  ** = ' SQLERRD3.                 07520008
           DISPLAY '** SQLERRM         ** = ' SQLERRM.                  07530008
           DISPLAY '** SQLERRMC        ** = ' SQLERRMC.                 07540008
           SET ABEND-4013-DB2-ERROR    TO TRUE.                         07550008
                                                                        07560008
           CALL 'ILBOABN0' USING ABEND-CODE.                            07570008
                                                                        07580008
      ******************************************************************07590008
      *  9000-NON-SQL-ABEND                                             07600008
      *  PERFORMS AN ABEND WHEN A NON-SQL BASED ERROR TAKES PLACE DURING07610008
      *  PROGRAM EXECUTION. LIKELY A CONTROL CARD OR DATE ROUTINE ISSUE.07620008
      ******************************************************************07630008
       9000-NON-SQL-ABEND.                                              07640008
                                                                        07650008
           PERFORM 9999-WRAP-UP.                                        07660008
                                                                        07670008
           DISPLAY '** ABEND           **'.                             07680008
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  07690008
           DISPLAY '** PARAGRAPH       ** = ' AB-PARAGRAPH-NAME.        07700008
           DISPLAY '** OPERATION       ** = ' AB-OPERATION-MSG-1.       07710008
           DISPLAY '**                 ** = ' AB-OPERATION-MSG-2.       07720008
                                                                        07730008
           CALL 'ILBOABN0' USING ABEND-CODE.                            07740008
                                                                        07750008
      ******************************************************************07760008
      *  9999-WRAP-UP.                                                  07770008
      *  PERFORM A FINAL WRITE TO ENSURE THE LAST RECORD IS WRITTEN TO  07780008
      *  THE DMM/GMM OUTPUT FILE. CLOSE OPEN FILES.                     07790008
      ******************************************************************07800008
       9999-WRAP-UP.                                                    07810008
                                                                        07820008
      *    //SINCE WE ARE WRITING PREVIOUS DATA ON BREAKS, WE NEED      07830008
      *    //TO PERFORM ONE LAST DETAIL LINE WRITE TO GET IT IN THE FILE07840008
           PERFORM 4000-WRITE-SLS-RPT-DETAIL-LINE.                      07850008
                                                                        07860008
           CLOSE DMM-GMM-SUM-FILE                                       07870008
MFTCHG           MFT-COMMANDS-FILE.                                     07880029
                                                                        07890008
      ******************************************************************07900008
