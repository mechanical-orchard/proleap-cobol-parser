       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         ETKUT117.                                            
       DATE-WRITTEN.       03-20-2000.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:                                                                 
      *    THIS PROGRAM WILL CREATE A STATUS EXTRACT FILE THAT WILL BE          
      * USED TO UPDATE THE TABLE TTNSTLC. INFORMATION FOR THE EXTRACT           
      * FILE COMES FROM THE PO ACKS RECYCLE DATASET. THE MODULE ONLY            
      * USES THE PO ACKS, 855S.                                                 
      *              855 - PURCHASE ORDER ACKNOWLEDGEMENTS.                     
      ******************************************************************        
0305JF* 03-05-2001 JON FIEDLER 0305JF                                           
0305JF* ADDED THE TRN-TXT-DTL AND THE INCHG-CNTL-NBR TO THE SYSOUT              
0305JF* ERROR MESSAGE.                                                          
0305JF******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO IN01.                                                  
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS V                                                  
           LABEL RECORDS ARE OMITTED                                            
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
           COPY DPRD010O.                                                       
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS ET024-EXTRACT-RECORD.                                 
                                                                                
           COPY ETRD024.                                                        
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  INPUT-RECORD.                                                        
           05 I-TABLE-NAME              PIC X(07).                              
           05 I-TP-DKEY                 PIC S9(9) COMP.                         
           05 I-DATA-AREA               PIC X(354).                             
           05 I-TFGTRHL-AREA  REDEFINES I-DATA-AREA.                            
              10  I-TRN-SET-CDE         PIC X(3).                               
              10  I-TRN-DTL-TXT         PIC 9(9).                               
              10  I-RLSE-IND            PIC X(01).                              
              10  I-RLSE-DTE            PIC X(10).                              
              10  I-USER-ID             PIC X(08).                              
              10  FILLER                PIC X(323).                             
           05 I-TTRPOHL-AREA  REDEFINES I-DATA-AREA.                            
              10  I-PO-NBR              PIC 9(9).                               
              10  I-DEPT-NBR            PIC X(03).                              
              10  I-PO-RLSE-IND         PIC X(01).                              
              10  FILLER                PIC X(341).                             
           05 I-TTRTRDT-AREA  REDEFINES I-DATA-AREA.                            
              10  I-TRN-SET-SEQ-NBR     PIC S9(9) COMP.                         
              10  I-TRN-SET-DATA        PIC X(350).                             
           05 I-REST-OF-RECORD          PIC X(1626).                            
                                                                                
           COPY PORD002.                                                        
                                                                                
       01  WS-TALLY                      PIC S9(09) COMP VALUE ZEROS.           
       01  WS-DB2-TIMESTAMP              PIC X(26) VALUE SPACES.                
                                                                                
       01  WS-VEND-INCHG-ID-DESC         PIC X(02) VALUE SPACES.                
       01  WS-VEND-INCHG-ID              PIC X(15) VALUE SPACES.                
                                                                                
       01  WS-PO-NUMBER                  PIC 9(09) VALUE ZEROS.                 
       01  WS-PO-NUMBER-REFORMAT REDEFINES                                      
           WS-PO-NUMBER                  PIC X(09).                             
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  WS-END-OF-FILE-SW         PIC X(1)  VALUE 'N'.                   
               88  END-OF-FILE                     VALUE 'Y'.                   
           05  WS-ERRORS-FOUND-SW        PIC X(1)  VALUE 'N'.                   
               88  ERRORS-FOUND                    VALUE 'Y'.                   
           05  END-OF-TP-DKEY-CSR-SW     PIC X(1)  VALUE 'N'.                   
               88  NOT-END-OF-TP-DKEY-CSR          VALUE 'N'.                   
               88  END-OF-TP-DKEY-CSR              VALUE 'Y'.                   
                                                                                
       01  PV-PARAGRAPH-NAME             PIC X(30).                             
       01  PV-DB2-OPER-ATTEMPTED         PIC X(30).                             
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
           88 ABEND-4095                         VALUE +4095.                   
                                                                                
      ******************************************************************        
      * COMMUNICATIONS AREA2 FOR DB2                                            
      ******************************************************************        
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
      * SQLCA MESSAGE MODULE DSNTIAR ARGUMENTS......                            
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * DB2 ERROR HANDLING ARGUMENTS....                                        
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TRADING PARTNER TABLE ARGUMENTS.....                                    
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TABLE CURSOR FOR THE TRADING PARTNER FUNCTIONAL CONTROL GROUP.          
      ******************************************************************        
           EXEC SQL                                                             
             DECLARE TP_DKEY_CSR CURSOR FOR                                     
               SELECT TP_DKEY                                                   
               FROM TTRDPRT                                                     
               WHERE VEND_INCHG_ID_DESC = :WS-VEND-INCHG-ID-DESC                
               AND VEND_INCHG_ID = :WS-VEND-INCHG-ID                            
DUPREC         AND INACTV_DTE = '9999-09-09'                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *FLOW:    THE MODULE SEQUENTIALLY READS THE PO ACKS RECYCLE               
      *      DATASET LOOKING FOR ALL TFGTRDT TABLE RECORDS. (THE RECYCLE        
      *      DATASET CONTAINS INFORMATION FOR TABLES TFGTRHL, TFGTRDT,          
      *      AND TTRPOHL) LOOK FOR PO ACKS HEADER RECORDS. IF FOUND,            
      *      MOVE THE RECYCLE DATASET RECORD TO THE PO ACKS RECORD              
      *      LAYOUT.                                                            
      *                                                                         
      *         CREATE A STATUS FILE EXTRACT RECORD USING THE FIELD             
      *      REQUIREMENTS DEFINED IN THE COPYBOOK ETRD024. THE STATUS           
      *      INDICATOR WOULD BE A '4 - ON HOLD'.                                
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE-DRIVER-SECTION.                                            
                                                                                
           PERFORM 0100-INITIALIZATION                                          
              THRU 0100-INITIALIZATION-EXIT.                                    
                                                                                
           PERFORM 1000-PROCESS-INPUT-FILE                                      
              THRU 1000-PROCESS-INPUT-FILE-EXIT                                 
                 UNTIL END-OF-FILE.                                             
                                                                                
           CLOSE INPUT-FILE                                                     
                 OUTPUT-FILE.                                                   
                                                                                
           IF ERRORS-FOUND AND                                                  
              ABEND-CODE IS NOT EQUAL TO +4013                                  
               SET ABEND-4095 TO TRUE.                                          
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 0100-INITIALIZATION.....                                                
      ******************************************************************        
       0100-INITIALIZATION.                                                     
                                                                                
           OPEN  INPUT INPUT-FILE                                               
                OUTPUT OUTPUT-FILE.                                             
                                                                                
           EXEC SQL                                                             
               SET :WS-DB2-TIMESTAMP = CURRENT TIMESTAMP                        
           END-EXEC.                                                            
                                                                                
           MOVE 'N'  TO WS-END-OF-FILE-SW.                                      
           MOVE 'N'  TO WS-ERRORS-FOUND-SW.                                     
                                                                                
           INITIALIZE INPUT-RECORD.                                             
           INITIALIZE PO002-EDI855-RECORD.                                      
           INITIALIZE ET024-EXTRACT-RECORD.                                     
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       0100-INITIALIZATION-EXIT.                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 1000-PROCESS THE INPUT FILE..                                           
      *     SEQUENTIALLY READ THE PO ACKS RECYCLE DATASET LOOKING FOR           
      *     ALL TFGTRDT TABLE RECORDS. IF FOUND, MOVE THE RECYCLE               
      *     DATASET TO THE PO ACKS RECORD LAYOUT FOR STATUS EXTRACT             
      *     FILE RECORD GENERATION.                                             
      ******************************************************************        
       1000-PROCESS-INPUT-FILE.                                                 
                                                                                
           MOVE DP010O-TRANS-REC    TO INPUT-RECORD.                            
                                                                                
           IF I-TABLE-NAME IS EQUAL TO 'TFGTRDT'                                
               MOVE I-TRN-SET-DATA  TO PO002-EDI855-RECORD                      
               IF PO002-HEADER-RECORD                                           
                   PERFORM 2000-GEN-EXTRACT-REC                                 
                      THRU 2000-GEN-EXTRACT-REC-EXIT                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-INPUT-FILE-EXIT.                                            
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 2000-GENERATE THE EXTRACT RECORD...                                     
      *    - USE TP-DKEY FROM INPUT RECORD TO GET TRADING PARTNER ID            
      *    - USE TRADING PARTNER ID TO GET TP-DKEY FOR ALL VENDORS              
      *      USING THE SAME TRADING PARTNER ID                                  
      ******************************************************************        
       2000-GEN-EXTRACT-REC.                                                    
                                                                                
           PERFORM 3000-GET-TP-ID                                               
              THRU 3000-GET-TP-ID-EXIT.                                         
                                                                                
           PERFORM 4000-OPEN-TP-DKEY-CSR                                        
              THRU 4000-OPEN-TP-DKEY-CSR-EXIT.                                  
                                                                                
           PERFORM 4100-FETCH-TP-DKEY                                           
              THRU 4100-FETCH-TP-DKEY-EXIT.                                     
                                                                                
           PERFORM 5000-CREATE-EXTRACT-REC                                      
              THRU 5000-CREATE-EXTRACT-REC-EXIT                                 
                UNTIL END-OF-TP-DKEY-CSR.                                       
                                                                                
           PERFORM 4200-CLOSE-TP-DKEY-CSR                                       
              THRU 4200-CLOSE-TP-DKEY-CSR-EXIT.                                 
                                                                                
       2000-GEN-EXTRACT-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3000-GET-TP-ID                                                          
      ******************************************************************        
       3000-GET-TP-ID.                                                          
                                                                                
           EXEC SQL                                                             
             SELECT VEND_INCHG_ID_DESC                                          
                   ,VEND_INCHG_ID                                               
               INTO :TRDPRT-VEND-INCHG-ID-DESC                                  
                   ,:TRDPRT-VEND-INCHG-ID                                       
               FROM TTRDPRT                                                     
              WHERE TP_DKEY = :I-TP-DKEY                                        
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '3000-GET-TP-ID' TO PV-PARAGRAPH-NAME                        
              MOVE 'GET TP ID' TO PV-DB2-OPER-ATTEMPTED                         
              PERFORM 9900-SQL-ERROR.                                           
                                                                                
       3000-GET-TP-ID-EXIT.                                                     
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 4000-OPEN-TP-DKEY-CSR                                                   
      ******************************************************************        
       4000-OPEN-TP-DKEY-CSR.                                                   
                                                                                
           SET NOT-END-OF-TP-DKEY-CSR TO TRUE.                                  
                                                                                
           MOVE TRDPRT-VEND-INCHG-ID-DESC TO WS-VEND-INCHG-ID-DESC.             
           MOVE TRDPRT-VEND-INCHG-ID      TO WS-VEND-INCHG-ID.                  
                                                                                
           EXEC SQL                                                             
              OPEN TP_DKEY_CSR                                                  
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '4000-OPEN-TP-DKEY-CSR' TO PV-PARAGRAPH-NAME                 
              MOVE 'OPEN CURSOR' TO PV-DB2-OPER-ATTEMPTED                       
              PERFORM 9900-SQL-ERROR.                                           
                                                                                
       4000-OPEN-TP-DKEY-CSR-EXIT.                                              
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 4100-FETCH-TP-DKEY                                                      
      *   READ THE TRADING PARTNER TABLE TO GET THE TP-DKEY. LOAD THE           
      * EXTRACT DATASET WITH HEADER INFORMATION SPECIFICALLY NEEDED FOR         
      * DOCUMENT TRACKING.                                                      
      ******************************************************************        
       4100-FETCH-TP-DKEY.                                                      
                                                                                
           EXEC SQL                                                             
               FETCH TP_DKEY_CSR                                                
               INTO :TRDPRT-TP-DKEY                                             
           END-EXEC.                                                            
                                                                                
           IF SQLCODE IS EQUAL TO ZEROS                                         
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY SPACES                                                    
              DISPLAY 'UNABLE TO LOCATE TP-DKEY TABLE ENTRY'                    
              DISPLAY 'VEND_INCHG_ID_DESC = ' WS-VEND-INCHG-ID-DESC             
              DISPLAY 'VEND_INCHG_ID = ' WS-VEND-INCHG-ID                       
              DISPLAY 'FUNC-CDE-CNTL-NBR = ' PO002-H-GS-CNTL-NBR                
0305JF        DISPLAY 'TRN-DTL-TXT = ' PO002-H-PO-NBR                           
              DISPLAY 'TRN-SET-CNTL-NBR = '  PO002-H-ST-CNTL-NBR                
0305JF        DISPLAY 'INCHG-CNTL-NBR = ' PO002-H-ISA-CNTL-NBR                  
              DISPLAY 'TRN-SET-CDE = 855'                                       
              DISPLAY 'SQLCODE = ' SQLCODE                                      
              SET END-OF-TP-DKEY-CSR TO TRUE                                    
              SET ERRORS-FOUND TO TRUE.                                         
                                                                                
       4100-FETCH-TP-DKEY-EXIT.                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 4200-CLOSE-TP-DKEY-CSR                                                  
      ******************************************************************        
       4200-CLOSE-TP-DKEY-CSR.                                                  
                                                                                
           EXEC SQL                                                             
              CLOSE TP_DKEY_CSR                                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '4200-CLOSE-TP-DKEY-CSR' TO PV-PARAGRAPH-NAME                
              MOVE 'CLOSE CURSOR' TO PV-DB2-OPER-ATTEMPTED                      
              PERFORM 9900-SQL-ERROR.                                           
                                                                                
       4200-CLOSE-TP-DKEY-CSR-EXIT.                                             
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 5000-CREATE THE EXTRACT RECORD                                          
      ******************************************************************        
       5000-CREATE-EXTRACT-REC.                                                 
                                                                                
           INITIALIZE ET024-EXTRACT-RECORD.                                     
                                                                                
           MOVE TRDPRT-TP-DKEY        TO ET024-TP-DKEY.                         
           MOVE PO002-H-TIMESTAMP     TO ET024-TP-DKEY-TMST.                    
           MOVE PO002-H-GS-CNTL-NBR   TO ET024-FUNC-CDE-CNTL-NBR.               
           MOVE PO002-H-ST-CNTL-NBR   TO ET024-TRN-SET-CNTL-NBR.                
                                                                                
           MOVE ZEROS                 TO WS-TALLY.                              
           MOVE PO002-H-PO-NBR        TO WS-PO-NUMBER.                          
           INSPECT WS-PO-NUMBER-REFORMAT                                        
               TALLYING WS-TALLY FOR LEADING ZEROS.                             
           MOVE WS-PO-NUMBER-REFORMAT                                           
               (1 + WS-TALLY:9 - WS-TALLY) TO ET024-TRN-DTL-TXT.                
                                                                                
           MOVE WS-DB2-TIMESTAMP      TO ET024-AUD-TMST.                        
           MOVE '4'                   TO ET024-STATUS-CDE.                      
           MOVE '855'                 TO ET024-TRN-SET-CDE.                     
                                                                                
           WRITE ET024-EXTRACT-RECORD.                                          
                                                                                
           PERFORM 4100-FETCH-TP-DKEY                                           
              THRU 4100-FETCH-TP-DKEY-EXIT.                                     
                                                                                
       5000-CREATE-EXTRACT-REC-EXIT.                                            
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 8000-READ THE INPUT FILE...                                             
      ******************************************************************        
       8000-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE NEXT                                                 
               AT END  MOVE 'Y' TO WS-END-OF-FILE-SW.                           
                                                                                
       8000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 9900-SQL ERROR HANDLING ROUTINE.                                        
      ******************************************************************        
       9900-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
