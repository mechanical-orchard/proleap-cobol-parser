000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.      XSKUP069.                                               
000300 AUTHOR.          PAUL OMAN.                                              
000400 DATE-WRITTEN.    APRIL 2003.                                             
000500                                                                          
001000*--------------------------------------------------------------*          
002300*                                                              *          
002400* DESCRIPTION:                                                 *          
      *      PURGES UPCS FROM THE XST_UPC TABLE A CERTAIN NUMBER OF  *          
      *      DAYS AFTER THE UPC HAS BEEN MARKED FOR DELETE WITH A 'D'*          
      *      IN THE LAST_ACTN_CDE FIELD.                             *          
001200*                                                              *          
001300*  INPUTS:                                                     *          
001400*      XSCC065   CONTAINS TODAYS DATE (THE DATE THE JOB WAS    *          
001500*     (XSWS065)  SCHEDULED.                                    *          
001600*                                                              *          
001700*      XSCC069   HOLDS THE NUMBER OF DAYS TO WAIT BEFORE       *          
001800*     (XSWS069)  PHYSICALLY DELETING A LOGICAL (MARKED) DELETE.*          
001900*                                                              *          
002000* OUTPUTS:                                                     *          
002100*                                                              *          
002200*      NONE                                                    *          
002600*                                                              *          
003600*--------------------------------------------------------------*          
003700 ENVIRONMENT DIVISION.                                                    
003800*--------------------------------------------------------------*          
003900 CONFIGURATION SECTION.                                                   
004000 SOURCE-COMPUTER.        IBM-3090.                                        
004100 OBJECT-COMPUTER.        IBM-3090.                                        
004200                                                                          
004300 INPUT-OUTPUT SECTION.                                                    
004400                                                                          
004500 FILE-CONTROL.                                                            
004600                                                                          
004700     SELECT CONTROL-CARD-TODAYS-DATE                                      
004800            ASSIGN TO INP01.                                              
004900                                                                          
005000     SELECT CONTROL-CARD-NUMBER-OF-DAYS                                   
005100            ASSIGN TO INP02.                                              
005200                                                                          
005300 DATA DIVISION.                                                           
005400 FILE SECTION.                                                            
005500                                                                          
005700 FD  CONTROL-CARD-TODAYS-DATE                                             
005800     RECORDING MODE F                                                     
005900     BLOCK CONTAINS 0 RECORDS                                             
006000     LABEL RECORDS ARE OMITTED                                            
006100     DATA RECORD IS CONTROL-TODAYS-DATE-RECORD.                           
006200 01  CONTROL-TODAYS-DATE-RECORD   PIC  X(080).                            
006300                                                                          
006400 FD  CONTROL-CARD-NUMBER-OF-DAYS                                          
006500     RECORDING MODE F                                                     
006600     BLOCK CONTAINS 0 RECORDS                                             
006700     LABEL RECORDS ARE OMITTED                                            
006800     DATA RECORD IS CONTROL-CARD-DAYS-RECORD.                             
006900 01  CONTROL-CARD-DAYS-RECORD     PIC  X(080).                            
007000                                                                          
007100 WORKING-STORAGE SECTION.                                                 
007200*--------------------------------------------------------------*          
007300* PS - PROGRAM SWITCHES                                                   
007400*--------------------------------------------------------------*          
007500 01  PS-PROGRAM-SWITCHES.                                                 
007900     05  PS-END-OF-CNTL-CRD-SW        PIC  X(001).                        
008100         88  MORE-CNTL-CRDS                       VALUE 'Y'.              
008000         88  END-OF-CNTL-CRDS                     VALUE 'N'.              
008200                                                                          
008510     05  PS-MORE-DELETE-UPC-SW        PIC  X(001).                        
008520         88  MORE-DELETE-UPC                      VALUE 'Y'.              
008530         88  END-OF-DELETE-UPC                    VALUE 'N'.              
009200                                                                          
009300*--------------------------------------------------------------*          
009400* PROGRAM CONSTANTS                                                       
009500*--------------------------------------------------------------*          
009600 01  PC-PROGRAM-CONSTANTS.                                                
009700     05  PC-PROGRAM-NAME          PIC X(08) VALUE 'XSKUP069'.             
009800     05  PC-MAX-RETRIES           PIC 9(9) VALUE 5.                       
010100     05  PC-COMMIT-MAX            PIC 9(9) VALUE 1000.                    
010200                                                                          
010300*--------------------------------------------------------------*          
010400* PV- PROGRAM VARIABLES                                                   
010500*--------------------------------------------------------------*          
010600 01  PV-PROGRAM-VARAIBLES.                                                
010700     05  PV-PARAGRAPH-NAME        PIC X(40)   VALUE SPACES.               
010900     05  PD-DB2-OPERATION         PIC X(15)   VALUE SPACES.               
011110                                                                          
011200     05  PV-SQL-SUB               PIC S9(04)  VALUE 0  COMP.              
011300     05  PV-ABEND-CODE            PIC 9999    VALUE 0  COMP.              
011300     05  PV-CALC-TIMESTAMP        PIC X(26)   VALUE SPACES.               
012200                                                                          
012300*--------------------------------------------------------------*          
012400* XSWS065 CONTROL CARD - TODAYS DATE                                      
012500*--------------------------------------------------------------*          
012600     COPY XSWS065.                                                        
012700                                                                          
012800*--------------------------------------------------------------*          
012900* XSWS069 CONTROL CARD - NUMBER OF DAYS                                   
013000*--------------------------------------------------------------*          
013100     COPY XSWS069.                                                        
013200                                                                          
013300*--------------------------------------------------------------*          
013400* DATE ROUTINE COPY MEMBERS                                               
013500*--------------------------------------------------------------*          
013600     COPY DPWS500.                                                        
013700                                                                          
013800*--------------------------------------------------------------*          
013900* VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                 
014000*--------------------------------------------------------------*          
014100     COPY DPWS004.                                                        
014200                                                                          
014300*--------------------------------------------------------------*          
014400* DB2 ABEND COPYBOOK                                                      
014500*--------------------------------------------------------------*          
014600     COPY DPWS900.                                                        
014700                                                                          
014800*--------------------------------------------------------------*          
014900* DB2 MESSAGE AREA                                                        
015000*--------------------------------------------------------------*          
015100     COPY SQLCA2.                                                         
015200                                                                          
015300*--------------------------------------------------------------*          
015400* PROGRAM COUNTERS                                                        
015500*--------------------------------------------------------------*          
015600 01  PV-PROGRAM-COUNTERS         COMP-3.                                  
015710     05  PV-NUMBER-OF-RETRIES     PIC 9(9) VALUE 0.                       
015710     05  PV-FETCH-CNT             PIC 9(9) VALUE 0.                       
015710     05  PV-DELETE-CNT            PIC 9(9) VALUE 0.                       
015710     05  PV-CNT-SINCE-COMMIT      PIC 9(9) VALUE 0.                       
016100                                                                          
016200*--------------------------------------------------------------*          
016300* ERROR HANDLING                                                          
016400*--------------------------------------------------------------*          
016500 01  PV-MISC-ABEND-FIELDS.                                                
016600     05  PV-OPERATION-MSG-1      PIC X(40) VALUE SPACES.                  
016700     05  PV-OPERATION-MSG-2      PIC X(40) VALUE SPACES.                  
016800     05  PV-DB2-OPERATION        PIC X(30) VALUE SPACES.                  
016900                                                                          
017000 01  ABEND-CODE                  PIC S9(4) VALUE 0  COMP SYNC.            
017100     88  ABEND-4003-CTRL-CARD-ERROR        VALUE +4003.                   
017200     88  ABEND-4013-DB2-ERROR              VALUE +4013.                   
017800                                                                          
017900*--------------------------------------------------------------*          
018000* DB2 COMMUNICATIONS AREA                                                 
018100*--------------------------------------------------------------*          
018200     EXEC SQL                                                             
018300       INCLUDE SQLCA                                                      
018400     END-EXEC.                                                            
018500                                                                          
018600*--------------------------------------------------------------*          
018700* DB2 TABLE XST00012 (XST_UPC) UPC TABLE                                  
018800*--------------------------------------------------------------*          
018900     EXEC SQL                                                             
019000       INCLUDE XST00012                                                   
019100     END-EXEC.                                                            
021776                                                                          
021777*-----------------------------------------------------------              
021778* DELETE-UPC-CSR FINDS ALL ROWS IN THE XST_UPC TABLE THAT ARE             
021779* MARKED FOR DELETE, AND HAVE NOT BEEN UPDATED FOR THE NUMBER             
021779* OF DAYS SPECIFIED IN THE CONTROL CARD.                                  
021780*-----------------------------------------------------------              
021781     EXEC SQL                                                             
021782       DECLARE DELETE-UPC-CSR CURSOR FOR                                  
021783            SELECT  UPC_ID                                                
021790              FROM  XST_UPC                                               
021792             WHERE  LAST_UPD_TMST < :XST00012-LAST-UPD-TMST               
                     AND  LAST_ACTN_CDE = 'D'                                   
021794           WITH UR                                                        
021795     END-EXEC.                                                            
019200                                                                          
022700                                                                          
022800 LINKAGE SECTION.                                                         
022900                                                                          
023000*--------------------------------------------------------------*          
023100 PROCEDURE DIVISION.                                                      
023200*--------------------------------------------------------------*          
023300 0000-MAINLINE.                                                           
023400                                                                          
023500     PERFORM 1000-INITIALIZATION.                                         
023600                                                                          
023700     PERFORM 2000-PROCESS-UPCS-FOR-DELETE.                                
023800                                                                          
023900     PERFORM 9999-WRAPUP.                                                 
024000                                                                          
024100     GOBACK.                                                              
024400                                                                          
024500                                                                          
024600*--------------------------------------------------------------*          
024700* 1000-INITIALIZATION.                                                    
024800*     INITIALIZATION AREA - INITIALIZES ALL VARIABLES, OPENS              
024900*     FILES, READS INPUT FILES, AND PROCESSES ANY INPUT FILE              
025000*     PARAMETERS IT MAY NEED.                                             
025100*--------------------------------------------------------------*          
025200 1000-INITIALIZATION.                                                     
025300                                                                          
025400     OPEN INPUT  CONTROL-CARD-TODAYS-DATE                                 
025500                 CONTROL-CARD-NUMBER-OF-DAYS.                             
                                                                                
025600     DISPLAY ' '.                                                         
025700     DISPLAY '*********************************************'.             
025800     DISPLAY ' START OF PROGRAM NAME: ' PC-PROGRAM-NAME.                  
025900     DISPLAY '*********************************************'.             
026000     DISPLAY ' '.                                                         
026100                                                                          
026200     PERFORM 1030-READ-TODAYS-DATE.                                       
026300     PERFORM 1040-READ-NUMBER-OF-DAYS.                                    
026400     PERFORM 9100-DATE-ROUTINE.                                           
026700                                                                          
026800                                                                          
026900*--------------------------------------------------------------*          
027000* 1030-READ-TODAYS-DATE.                                                  
027100*     READ CONTROL CARD XSCC065. THIS CONTROL CARD CONTAINS               
027200*     THE TODAYS DATE.                                                    
027300*--------------------------------------------------------------*          
027400 1030-READ-TODAYS-DATE.                                                   
027500                                                                          
027600     READ CONTROL-CARD-TODAYS-DATE                                        
027700                               INTO CC-XSCC065-CONTROL-CARD               
027800       AT END                                                             
027900          SET END-OF-CNTL-CRDS TO TRUE.                                   
028000                                                                          
028100     IF END-OF-CNTL-CRDS                                                  
028200       DISPLAY ' '                                                        
028300       DISPLAY '** ABEND     **'                                          
028400       DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME                      
028500       DISPLAY '** PARAGRAPH **  = 1030-READ-BATCH-DATE-RECORD'           
028600       DISPLAY '** XSCC065 CONTROL FILE IS EMPTY  '                       
028700       DISPLAY ' '                                                        
028800       MOVE +4002 TO ABEND-CODE                                           
028900       CALL 'ILBOABN0' USING ABEND-CODE                                   
029000     ELSE                                                                 
029100       DISPLAY '** TODAYS DATE CONTROL CARD           **'                 
029200       DISPLAY '** XSCC065       **  = '                                  
029300       DISPLAY CC-XSCC065-CONTROL-CARD                                    
029400       DISPLAY ' '                                                        
029700                                                                          
029800     END-IF.                                                              
030100                                                                          
030200                                                                          
030300*--------------------------------------------------------------*          
030400* 1040-READ-NUMBER-OF-DAYS.                                               
030500*     READ CONTROL CARD XSCC069. THIS CONTROL CARD CONTAINS               
030600*     THE NUMBER OF DAYS TO KEEP ROWS ON XST_UPC THAT HAVE BEEN           
      *      MARKED FOR DELETE, BEFORE PHYSICALLY DELETING THEM.                
030700*--------------------------------------------------------------*          
030800 1040-READ-NUMBER-OF-DAYS.                                                
030900                                                                          
031000     READ CONTROL-CARD-NUMBER-OF-DAYS                                     
031100                               INTO CC-XSCC069-CONTROL-CARD               
031200       AT END                                                             
031300          SET END-OF-CNTL-CRDS TO TRUE.                                   
031400                                                                          
031500     IF END-OF-CNTL-CRDS                                                  
031600       DISPLAY ' '                                                        
031700       DISPLAY '** ABEND     **'                                          
031800       DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME                      
031900       DISPLAY '** PARAGRAPH **  = 1040-TIMESTAMP-RECORD'                 
032000       DISPLAY '** XSCC069 CONTROL FILE IS EMPTY  '                       
032100       DISPLAY ' '                                                        
032200       MOVE +4002 TO ABEND-CODE                                           
032300       CALL 'ILBOABN0' USING ABEND-CODE                                   
032400     ELSE                                                                 
032500       DISPLAY '** NUMBER OF DAYS                       **'               
032600       DISPLAY '   XSCC069                                '               
032700       DISPLAY CC-XSCC069-CONTROL-CARD                                    
032800       DISPLAY ' '                                                        
032900       DISPLAY ' '                                                        
033000     END-IF.                                                              
033300                                                                          
033400                                                                          
033500*--------------------------------------------------------------*          
033600* 2000-PROCESS-UPCS-FOR-DELETE                                 *          
033700*                                                              *          
033800*  PROCESSES THROUGH THE DELETE UPC CURSOR, DELETING EVERY ROW *          
033700*  FETCHED.                                                    *          
033700*                                                              *          
033900*--------------------------------------------------------------*          
034000 2000-PROCESS-UPCS-FOR-DELETE.                                            
036503                                                                          
           PERFORM 3000-OPEN-DELETE-UPC-CSR.                                    
034900                                                                          
034901     SET MORE-DELETE-UPC TO TRUE.                                         
           PERFORM 3100-FETCH-DELETE-UPC-CSR.                                   
                                                                                
           PERFORM 2100-PROCESS-FETCHED-UPC                                     
             UNTIL END-OF-DELETE-UPC.                                           
                                                                                
           PERFORM 3200-CLOSE-DELETE-UPC-CSR.                                   
                                                                                
           IF PV-CNT-SINCE-COMMIT > 0                                           
             PERFORM 9000-COMMIT                                                
           END-IF.                                                              
036910                                                                          
033400                                                                          
033500*--------------------------------------------------------------*          
033600* 2100-PROCESS-FETCHED-UPC                                     *          
033700*                                                              *          
033800*  DELETES THE UPC AND FETCHES THE NEXT ROW FROM THE CURSOR.   *          
033700*                                                              *          
033900*--------------------------------------------------------------*          
034000 2100-PROCESS-FETCHED-UPC.                                                
036503                                                                          
           PERFORM 2200-DELETE-UPC.                                             
034900                                                                          
           IF PV-CNT-SINCE-COMMIT > PC-COMMIT-MAX                               
             PERFORM 9000-COMMIT                                                
             DISPLAY '--- INTERIM COMMIT PERFORMED ---'                         
                                                                                
             PERFORM 3000-OPEN-DELETE-UPC-CSR                                   
           END-IF.                                                              
                                                                                
           PERFORM 3100-FETCH-DELETE-UPC-CSR.                                   
036800                                                                          
042810                                                                          
042820*--------------------------------------------------------------*          
042830* 2200-DELETE-UPC.                                             *          
042840*                                                              *          
042840*  DELETES A SPECIFIC UPC ID FROM THE XST_UPC TABLE.           *          
042850*  SPECIFIES THAT THE LAST ACTION CODE MUST BE 'D', JUST IN    *          
042850*  CASE THE UPC ID WOULD SOMEHOW BE REUSED (NOT EXPECTED TO    *          
042850*  HAPPEN).                                                    *          
042850*                                                              *          
042870*--------------------------------------------------------------*          
042880 2200-DELETE-UPC.                                                         
042881                                                                          
042882     MOVE '2200-DELETE-UPC' TO PV-PARAGRAPH-NAME.                         
042890                                                                          
028390     PERFORM                                                              
028391         WITH TEST AFTER                                                  
028392         VARYING PV-SQL-SUB                                               
028393         FROM 1 BY 1                                                      
028394         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
028395                   OR  SQLCODE = 0)                                       
028396                                                                          
042891         EXEC SQL                                                         
042892           DELETE                                                         
042896             FROM  XST_UPC                                                
042897            WHERE  UPC_ID = :XST00012-UPC-ID                              
                    AND  LAST_ACTN_CDE = 'D'                                    
042899                                                                          
042900         END-EXEC                                                         
042901                                                                          
 42902         EVALUATE TRUE                                                    
042903           WHEN SQLCODE = 0                                               
042911             ADD 1 TO PV-DELETE-CNT                                       
042911             ADD 1 TO PV-CNT-SINCE-COMMIT                                 
042913                                                                          
042924           WHEN SQLCODE = -904                                            
042925           WHEN SQLCODE = -911                                            
042926           WHEN SQLCODE = -913                                            
                   CONTINUE                                                     
042932                                                                          
042933           WHEN OTHER                                                     
042934             MOVE 'ERROR ON DELETE OF UPC'  TO PV-DB2-OPERATION           
                   DISPLAY 'UPC ID: ' XST00012-UPC-ID                           
042935             PERFORM 9900-SQL-ABEND-ROUTINE                               
042936                                                                          
042937         END-EVALUATE                                                     
028416     END-PERFORM.                                                         
028417                                                                          
028418     IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
028421        MOVE 'DELETE UPC RETRIES EXCEEDED'                                
028422                              TO  PV-DB2-OPERATION                        
028423        PERFORM 9900-SQL-ABEND-ROUTINE                                    
028424     END-IF.                                                              
042938                                                                          
058500                                                                          
038236*----------------------------------------------------------               
038237* 3000-OPEN-DELETE-UPC-CSR.                                               
038238*     OPEN THE DELETE UPC CURSOR.                                         
038239*----------------------------------------------------------               
038240 3000-OPEN-DELETE-UPC-CSR.                                                
                                                                                
039000     MOVE '3000-OPEN-DELETE-UPC-CSR' TO PV-PARAGRAPH-NAME.                
060400     MOVE PV-CALC-TIMESTAMP          TO XST00012-LAST-UPD-TMST.           
038241                                                                          
038242     PERFORM                                                              
038243         WITH TEST AFTER                                                  
038244         VARYING PV-SQL-SUB                                               
038245         FROM 1 BY 1                                                      
038246         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
038247                   OR  SQLCODE = 0)                                       
038248                                                                          
038249           EXEC SQL                                                       
038250              OPEN DELETE-UPC-CSR                                         
038260           END-EXEC                                                       
038270                                                                          
038280         EVALUATE TRUE                                                    
038290             WHEN SQLCODE = -904                                          
038300             WHEN SQLCODE = -911                                          
038400             WHEN SQLCODE = -913                                          
038500                  CONTINUE                                                
                                                                                
038600             WHEN SQLCODE = 0                                             
038700                  CONTINUE                                                
                                                                                
038800             WHEN OTHER                                                   
039200                  MOVE 'ERROR ON OPEN OF DELETE UPC CSR'                  
039300                                     TO PV-DB2-OPERATION                  
039400                  PERFORM 9900-SQL-ABEND-ROUTINE                          
039500         END-EVALUATE                                                     
039600     END-PERFORM.                                                         
039700                                                                          
039800     IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
040100        MOVE 'OPEN RETRIES EXCEEDED'                                      
040200                              TO  PV-DB2-OPERATION                        
040300        PERFORM 9900-SQL-ABEND-ROUTINE                                    
040400     END-IF.                                                              
040500                                                                          
040800                                                                          
040900*----------------------------------------------------------               
041000* 3100-FETCH-DELETE-UPC-CSR.                                              
041100*     FETCH THE DELETE UPC CURSOR.                                        
041200*----------------------------------------------------------               
041300 3100-FETCH-DELETE-UPC-CSR.                                               
                                                                                
044500     MOVE '3100-FETCH-DELETE-UPC-CSR' TO PV-PARAGRAPH-NAME.               
041400                                                                          
041500     PERFORM                                                              
041600         WITH TEST AFTER                                                  
041700         VARYING PV-SQL-SUB                                               
041800         FROM 1 BY 1                                                      
041900         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
042000                   OR  SQLCODE = 0                                        
042100                   OR  SQLCODE = +100)                                    
042200                                                                          
042300           EXEC SQL                                                       
042400              FETCH DELETE-UPC-CSR                                        
042500              INTO  :XST00012-UPC-ID                                      
043200           END-EXEC                                                       
043300                                                                          
043400          EVALUATE TRUE                                                   
043500             WHEN SQLCODE = -904                                          
043600             WHEN SQLCODE = -911                                          
043700             WHEN SQLCODE = -913                                          
043800                  CONTINUE                                                
                                                                                
043900             WHEN SQLCODE = ZERO                                          
044000                  ADD 1 TO PV-FETCH-CNT                                   
                                                                                
044100             WHEN SQLCODE = +100                                          
044200                  SET END-OF-DELETE-UPC   TO TRUE                         
                                                                                
044400             WHEN OTHER                                                   
044700                  MOVE 'ERROR FETCHING DELETE UPC CSR'                    
044800                                     TO PV-DB2-OPERATION                  
044900                  DISPLAY 'SQLCODE= ' SQLCODE                             
045000                  PERFORM 9900-SQL-ABEND-ROUTINE                          
045100          END-EVALUATE                                                    
045200     END-PERFORM.                                                         
045300                                                                          
045400     IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
045500         MOVE '3100-FETCH-DELETE-UPC-CSR'                                 
045600                               TO  PV-PARAGRAPH-NAME                      
045700         MOVE 'FETCH RETRIES EXCEEDED'                                    
045800                               TO  PV-DB2-OPERATION                       
045900         PERFORM 9900-SQL-ABEND-ROUTINE                                   
046000     END-IF.                                                              
046100                                                                          
046400                                                                          
046500*-----------------------------------------------------------              
046600* 3200-CLOSE-DELETE-UPC-CSR.                                              
046700*     CLOSE THE DELETE UPC CURSOR.                                        
046800*-----------------------------------------------------------              
046900 3200-CLOSE-DELETE-UPC-CSR.                                               
                                                                                
049000     MOVE '3200-CLOSE-DELETE-UPC-CSR' TO PV-PARAGRAPH-NAME.               
047000                                                                          
047100     PERFORM                                                              
047200         WITH TEST AFTER                                                  
047300         VARYING PV-SQL-SUB                                               
047400         FROM 1 BY 1                                                      
047500         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
047600                   OR  SQLCODE = 0)                                       
047700                                                                          
047800           EXEC SQL                                                       
047900              CLOSE DELETE-UPC-CSR                                        
048000           END-EXEC                                                       
048100                                                                          
048200          EVALUATE TRUE                                                   
048300             WHEN SQLCODE = -904                                          
048400             WHEN SQLCODE = -911                                          
048500             WHEN SQLCODE = -913                                          
048700                  CONTINUE                                                
                                                                                
048600             WHEN SQLCODE = 0                                             
048700                  CONTINUE                                                
                                                                                
048900             WHEN OTHER                                                   
049200                  MOVE 'ERROR ON CLOSE DELETE UPC CSR'                    
049300                                      TO PV-DB2-OPERATION                 
049400                  PERFORM 9900-SQL-ABEND-ROUTINE                          
049500          END-EVALUATE                                                    
049600     END-PERFORM.                                                         
049700                                                                          
049800     IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
050100        MOVE 'CLOSE RETRIES EXCEEDED'                                     
050200                               TO  PV-DB2-OPERATION                       
050300        PERFORM 9900-SQL-ABEND-ROUTINE                                    
050310     END-IF.                                                              
021545                                                                          
021546*---------------------------------------------------------                
021547* 9000-COMMIT.                                                            
021548*  THIS ROUTINE EXECUTES THE COMMIT TO DB2 DELETES.                       
021548*  THE COMMIT WILL CLOSE THE CURSOR, SO IF THERE ARE MORE                 
021548*  UPCS THAT NEED TO BE DELETED, THE CURSOR WILL NEED TO BE               
021548*  RE-OPENED.                                                             
021549*---------------------------------------------------------                
021550 9000-COMMIT.                                                             
                                                                                
021579     MOVE '9000-COMMIT' TO PV-PARAGRAPH-NAME.                             
021551                                                                          
021552     PERFORM                                                              
021553         WITH TEST AFTER                                                  
021554         VARYING PV-SQL-SUB                                               
021555         FROM 1 BY 1                                                      
021556         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
021557                   OR  SQLCODE = 0)                                       
021558         EXEC SQL                                                         
021559             COMMIT                                                       
021560         END-EXEC                                                         
021561                                                                          
021562         EVALUATE TRUE                                                    
021563             WHEN SQLCODE = -904                                          
021564             WHEN SQLCODE = -911                                          
021565             WHEN SQLCODE = -913                                          
021567                 CONTINUE                                                 
                                                                                
021566             WHEN SQLCODE = 0                                             
021567                 MOVE ZERO TO PV-CNT-SINCE-COMMIT                         
                                                                                
021569             WHEN OTHER                                                   
021572                 MOVE 'COMMIT XST_UPC DELETES'                            
021573                               TO  PV-DB2-OPERATION                       
021574                 PERFORM 9900-SQL-ABEND-ROUTINE                           
021575         END-EVALUATE                                                     
021576     END-PERFORM.                                                         
021577                                                                          
021578     IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
021581         MOVE 'COMMIT RETRIES EXCEEDED'                                   
021582                               TO  PV-DB2-OPERATION                       
021583         PERFORM 9900-SQL-ABEND-ROUTINE                                   
021584     END-IF.                                                              
050320                                                                          
058600                                                                          
058700*--------------------------------------------------------------*          
058800* DATE ROUTINE TO SET UP THE PROCESS DATE IN                              
058900* CCYYMMDD FORMAT.                                                        
059000*--------------------------------------------------------------*          
059100 9100-DATE-ROUTINE.                                                       
059200                                                                          
059300     INITIALIZE DPG51 DPG52 DPG53                                         
059400                DPG54 DPG55 DPG56.                                        
059500                                                                          
059610     MOVE CC-XSCC069-NUMBER-DAYS  TO DPG51-INCR-DECR-DAYS-9.              
059700     MOVE CC-XSCC065-TODAYS-DATE  TO DPG52-DB2-ISO-DATE-FMT.              
059800                                                                          
059900     PERFORM 9200-CALL-DATE-ROUTINE.                                      
060000                                                                          
060100     STRING DPG55-DB2-ISO-DATE-FMT                                        
060200            '-00.00.00.000000'                                            
060300             DELIMITED BY SIZE                                            
060400             INTO PV-CALC-TIMESTAMP.                                      
060500     DISPLAY 'CALCULATED TIMESTAMP ----> '                                
060600              PV-CALC-TIMESTAMP.                                          
060700                                                                          
061100                                                                          
061200*--------------------------------------------------------------*          
061300* 9200-CALL-DATE-ROUTINE.                                                 
061400*                                                                         
061500*--------------------------------------------------------------*          
061600 9200-CALL-DATE-ROUTINE.                                                  
061700                                                                          
061800     SET DPG51-FISCAL-454-CALENDAR  TO TRUE.                              
061900     SET DPG51-DECREMENT-DATE       TO TRUE.                              
062000     SET DPG52-LK-DTE-ISO-FMT       TO TRUE.                              
062100                                                                          
062200     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
062300                                             DPG54 DPG55 DPG56.           
062400                                                                          
062500     IF DPG54-SEVERE-ERROR OR DPG54-DATE-INVALID                          
062600       DISPLAY '**       ABEND         **'                                
062700       DISPLAY '** PROGRAM = XSKUP069  **'                                
062800       DISPLAY 'DATE = '                                                  
062900                DPG52-DB2-ISO-DATE-FMT                                    
063000       DISPLAY DPG54-ERROR-MESSAGE                                        
063100       MOVE 4019 TO PV-ABEND-CODE                                         
063200       CALL 'ILBOABN0' USING PV-ABEND-CODE                                
063300     END-IF.                                                              
063600                                                                          
063700                                                                          
063800*--------------------------------------------------------------*          
063900* 9900-SQL-ABEND-ROUTINE.                                                 
064000*     SQL ABEND ROUTINE.                                                  
064100*--------------------------------------------------------------*          
064200 9900-SQL-ABEND-ROUTINE.                                                  
064300                                                                          
064400     DISPLAY '** ABEND     **'.                                           
064500     DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
064600     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
064700     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.                       
064800                                                                          
064900     COPY DPPD004.                                                        
065000     SET ABEND-4013-DB2-ERROR TO TRUE.                                    
065100     CALL 'ILBOABN0' USING ABEND-CODE.                                    
065400                                                                          
065500                                                                          
065600*--------------------------------------------------------------*          
065700* 9999-WRAPUP.                                                            
065800*     END OF PROGRAM ROUTINE.  CLOSES ALL FILES AND DISPLAYS              
065900*     ANY DATA FROM THE PROGRAM.                                          
066000*--------------------------------------------------------------*          
066100 9999-WRAPUP.                                                             
066200                                                                          
066300     CLOSE CONTROL-CARD-TODAYS-DATE                                       
066400           CONTROL-CARD-NUMBER-OF-DAYS.                                   
066500                                                                          
066600     DISPLAY ' '.                                                         
066700     DISPLAY '*********************************************'.             
066800     DISPLAY '   END OF PROGRAM NAME: ' PC-PROGRAM-NAME.                  
066900     DISPLAY '*********************************************'.             
067300     DISPLAY ' '.                                                         
           DISPLAY 'NUMBER OF UPCS FETCHED: ' PV-FETCH-CNT.                     
           DISPLAY 'NUMBER OF UPCS DELETED: ' PV-DELETE-CNT.                    
067800                                                                          
067900                                                                          
