       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    SDKUT175.                                         00030000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  10/08/12.                                         00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      * EXTRACT OF TRUCK PROJECTED NON-CANCELLED DELIVERIES FOR EDW     00090112
      ******************************************************************00090700
                                                                        00090800
       ENVIRONMENT DIVISION.                                            00090900
       CONFIGURATION SECTION.                                           00091000
                                                                        00092000
       SOURCE-COMPUTER.  IBM-3090.                                      00093000
       OBJECT-COMPUTER.  IBM-3090.                                      00094000
                                                                        00095000
       INPUT-OUTPUT SECTION.                                            00096000
       FILE-CONTROL.                                                    00097000
                                                                        00098000
           SELECT EXTRACT-FILE ASSIGN TO OUT01.                         00099000
                                                                        00110000
       DATA DIVISION.                                                   00120000
       FILE SECTION.                                                    00130000
                                                                        00140000
       FD  EXTRACT-FILE                                                 00150000
           RECORDING MODE IS F                                          00160000
           LABEL RECORDS ARE STANDARD                                   00170000
           RECORD CONTAINS 80 CHARACTERS                                00180000
           BLOCK CONTAINS 0 RECORDS                                     00190000
           DATA RECORD IS EXTRACT-RECORD.                               00200000
                                                                        00210000
       01  EXTRACT-RECORD                    PIC X(80).                 00220012
                                                                        00320000
      ******************************************************************00330000
      *                     WORKING STORAGE                             00340000
      ******************************************************************00350000
       WORKING-STORAGE SECTION.                                         00360000
                                                                        00370000
      ******************************************************************00380000
      * PS - PROGRAM SWITCHES                                           00390000
      ******************************************************************00400000
       01  PS-PROGRAM-SWITCHES.                                         00410000
           05  PS-END-OF-CSR-SW              PIC X     VALUE SPACE.     00420012
               88  PS-END-OF-CSR                       VALUE 'Y'.       00430012
               88  PS-NOT-END-OF-CSR                   VALUE 'N'.       00440012
                                                                        00450000
      ******************************************************************00460000
      * PC - PROGRAM CONSTANTS                                          00470000
      ******************************************************************00480000
       01  PC-PROGRAM-CONSTANTS.                                        00490000
           05  PC-PROGRAM-NAME               PIC X(08) VALUE 'SDKUT175'.00500012
           05  PC-FIELD-DELIMITER            PIC X(01) VALUE '|'.       00510012
                                                                        00520000
      ******************************************************************00530000
      * PV - PROGRAM VARIABLES                                          00540000
      ******************************************************************00550000
       01  PV-PROGRAM-VARIABLES.                                        00560000
           05  PV-CURRENT-DATE               PIC X(10) VALUE SPACES.    00570012
           05  PV-CUR-FP-LAST-DTE            PIC X(10) VALUE SPACES.    00571012
           05  PV-NXT-FP-LAST-DTE            PIC X(10) VALUE SPACES.    00572012
           05  PV-TRIP-QTY                   PIC X(02) VALUE SPACES.    00573016
                                                                        00580000
         05  PV-ABEND-VARIABLES.                                        00590000
           10  PV-PARAGRAPH-NAME             PIC X(30) VALUE SPACES.    00600012
           10  PV-OPERATION-MSG-1            PIC X(40) VALUE SPACES.    00610012
           10  PV-MSG-TEXT                   PIC X(40) VALUE SPACES.    00620012
           10  PV-DB2-OPERATION              PIC X(30) VALUE SPACES.    00630012
                                                                        00640000
      ******************************************************************00650006
      *               SYSTEM TIME AND DATE VARIABLES                    00660006
      ******************************************************************00670006
       01  SYS-DATE-TIME.                                               00671006
           05  SYS-DATE                      PIC X(08) VALUE SPACES.    00672012
           05  SYS-TIME                      PIC X(08) VALUE SPACES.    00673012
                                                                        00674006
       01  ST-BEG-TIME.                                                 00675006
           05  ST-BEG-HR                     PIC 9(02) VALUE ZEROS.     00676012
           05  FILLER                        PIC X(01) VALUE ':'.       00677012
           05  ST-BEG-MN                     PIC 9(02) VALUE ZEROS.     00678012
                                                                        00679006
       01  ST-END-TIME.                                                 00679106
           05  ST-END-HR                     PIC 9(02) VALUE ZEROS.     00679212
           05  FILLER                        PIC X(01) VALUE ':'.       00679312
           05  ST-END-MN                     PIC 9(02) VALUE ZEROS.     00679412
                                                                        00679506
       01  SD-EDIT-DATE.                                                00679606
           05  SD-MONTH                      PIC 9(02) VALUE ZEROS.     00679712
           05  FILLER                        PIC X(01) VALUE '/'.       00679812
           05  SD-DAY                        PIC 9(02) VALUE ZEROS.     00679912
           05  FILLER                        PIC X(01) VALUE '/'.       00680012
           05  SD-CENT                       PIC 9(02) VALUE ZEROS.     00680112
           05  SD-YEAR                       PIC 9(02) VALUE ZEROS.     00680212
                                                                        00680306
      ******************************************************************00682000
      * PA - PROGRAM ACCUMULATORS (COUNTERS)                            00690000
      ******************************************************************00700000
       01  PA-PROGRAM-ACCUMULATORS.                                     00710000
           05  PA-FETCH-COUNT                PIC 9(07) VALUE ZEROES.    00720012
           05  PA-WRITE-COUNT                PIC 9(07) VALUE ZEROES.    00730012
                                                                        00740000
      ******************************************************************00750000
      * AC - ABEND ERROR CODES                                          00760000
      ******************************************************************00770000
       01  ABEND-CODE                        PIC S9(04) VALUE +0        00780012
                                                       COMP SYNC.       00781012
           88  ABEND-4013-DB2-ERROR                    VALUE +4013.     00790012
                                                                        00800000
      ******************************************************************00810000
      * EXTRACT RECORD LAYOUT                                           00820000
      ******************************************************************00830000
           COPY SDRD175.                                                00840001
                                                                        00850000
      ******************************************************************00851008
      * PARAMETER LIST FOR THE CALENDAR ROUTINE                         00852008
      ******************************************************************00853008
           COPY DPWS500.                                                00854008
                                                                        00855008
      ******************************************************************00860000
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         00870000
      ******************************************************************00880000
           COPY DPWS004.                                                00890000
                                                                        00900000
      ******************************************************************00910000
      * DB2 MESSAGE AREA                                                00920000
      ******************************************************************00930000
           COPY SQLCA2.                                                 00940000
                                                                        00950000
      ******************************************************************00960000
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                 00970000
      ******************************************************************00980000
           EXEC SQL                                                     00990000
               INCLUDE SQLCA                                            01000000
           END-EXEC.                                                    01010000
                                                                        01020000
      ******************************************************************01030000
      * DB2 TABLE SDT_STR_RCVG                                          01040000
      ******************************************************************01050000
           EXEC SQL                                                     01060000
               INCLUDE SDT00032                                         01070000
           END-EXEC.                                                    01080000
                                                                        01090000
      ******************************************************************01100000
      * SDT_STR_RCVG CURSOR - NON-CANCELLED TRUCK PROJECTED DELIVERIES  01110000
      ******************************************************************01120000
           EXEC SQL                                                     01130000
             DECLARE NON-CNCL-TRK-PROJ-CSR CURSOR FOR                   01140000
               SELECT STR_NBR                                           01150000
                    , TRLR_ARRVL_DTE                                    01160000
                    , CURRENT DATE AS CURRENT_DATE                      01200000
                    , SUBSTR(DIGITS(COUNT(*)),9,2)                      01200114
               FROM SDT_STR_RCVG                                        01201000
               WHERE TRLR_ARRVL_DTE >= CURRENT_DATE                     01202005
                 AND TRLR_ARRVL_DTE <= :PV-NXT-FP-LAST-DTE              01203006
                 AND TRLR_VEH_NBR = 'PR'                                01203117
               GROUP BY STR_NBR                                         01203214
                    , TRLR_ARRVL_DTE                                    01203314
                    , CURRENT_DATE                                      01203414
               ORDER BY STR_NBR                                         01204000
                      , TRLR_ARRVL_DTE                                  01205000
               FOR FETCH ONLY                                           01207000
               WITH UR                                                  01208000
           END-EXEC.                                                    01209000
                                                                        01210000
      ******************************************************************01220000
       PROCEDURE DIVISION.                                              01230000
      ******************************************************************01240000
      * MAIN DRIVER                                                     01250000
      ******************************************************************01260000
       0000-MAINLINE.                                                   01270000
                                                                        01280000
           PERFORM 1000-INITIALIZATION.                                 01290000
                                                                        01300000
           PERFORM 2000-EXTRACT-PROCESSING                              01310000
                   UNTIL PS-END-OF-CSR.                                 01320000
                                                                        01330000
           PERFORM 3000-WRAP-UP.                                        01340000
                                                                        01350000
           PERFORM 9999-SUMMARY.                                        01360000
                                                                        01370000
           GOBACK.                                                      01380000
                                                                        01390000
      ******************************************************************01400000
      * 1000-INITIALIZATION                                             01410000
      ******************************************************************01420000
       1000-INITIALIZATION.                                             01430000
                                                                        01440000
           DISPLAY '*************************************'.             01450000
           DISPLAY '* SDKUT175 - SDT_STR_RCVG           *'.             01460000
           DISPLAY '*            DB2 TABLE EXTRACT      *'.             01470000
           DISPLAY '*            TRUCK NON-CANCELLED    *'.             01471012
           DISPLAY '*            PROJECTED DELIVERIES   *'.             01472012
           DISPLAY '*************************************'.             01480000
           DISPLAY '*'.                                                 01490000
           DISPLAY '* - PROCESSING...'.                                 01500000
                                                                        01501006
           PERFORM 1100-GET-NXT-FISC-MONTH-END.                         01502006
                                                                        01510000
           OPEN OUTPUT EXTRACT-FILE.                                    01520000
           PERFORM 2300-OPEN-NON-CNCL-TRK-CSR.                          01530002
           PERFORM 2400-FETCH-NON-CNCL-TRK-CSR.                         01540002
           MOVE SPACE TO SDRD175-EXTRACT-RECORD.                        01541018
                                                                        01550000
      ******************************************************************01551006
      * 1100-GET-NXT-FISC-MONTH-END.                                    01552006
      ******************************************************************01553006
       1100-GET-NXT-FISC-MONTH-END.                                     01554006
                                                                        01555006
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 01555106
                                                                        01555206
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            01555306
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            01555406
           MOVE SYS-DATE (1:2) TO SD-CENT.                              01555606
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              01555706
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             01555806
           MOVE SYS-DATE (7:2) TO SD-DAY.                               01555906
           DISPLAY 'SYSTEM DATE: ' SD-EDIT-DATE.                        01557006
                                                                        01558006
      ***GET END DATE FOR CURRENT FISCAL MONTH                          01559006
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01559206
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       01559306
           SET DPG52-LK-DTE-GREG-CC-FMT  TO TRUE.                       01559406
           MOVE SD-EDIT-DATE             TO DPG52-GREG-CC-DATE-FMT.     01559506
                                                                        01559606
           DISPLAY 'DPG52-GREG-CC-DATE = ' DPG52-GREG-CC-DATE-FMT.      01559706
                                                                        01559806
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                01559906
                                                   DPG52                01560006
                                                   DPG53                01560106
                                                   DPG54                01560206
                                                   DPG55                01560306
                                                   DPG56.               01560406
                                                                        01560506
           EVALUATE TRUE                                                01560606
               WHEN DPG54-NO-ERROR                                      01560706
                   MOVE DPG56-LAST-FP-DB2-ISO-FMT TO PV-CUR-FP-LAST-DTE 01560906
                   DISPLAY '- PV-CUR-FP-LAST-DTE:' PV-CUR-FP-LAST-DTE   01561006
               WHEN OTHER                                               01561106
                   DISPLAY 'ERROR GETTING FISCAL MONTH END DATE'        01561206
                   PERFORM Z5000-ERROR-ROUTINE                          01561306
           END-EVALUATE.                                                01561406
                                                                        01561506
      ***GET END DATE FOR NEXT FISCAL MONTH                             01563906
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01564006
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       01564106
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                       01564206
           MOVE PV-CUR-FP-LAST-DTE       TO DPG52-DB2-ISO-DATE-FMT.     01564306
           SET DPG51-INCREMENT-DATE      TO TRUE.                       01564406
           MOVE 1                        TO DPG51-INCR-DECR-DAYS-9.     01564506
                                                                        01564606
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                01564706
                                                   DPG52                01564806
                                                   DPG53                01564906
                                                   DPG54                01565006
                                                   DPG55                01565106
                                                   DPG56.               01565206
                                                                        01565306
           EVALUATE TRUE                                                01565406
               WHEN DPG54-NO-ERROR                                      01565506
                   MOVE DPG56-LAST-FP-DB2-ISO-FMT  TO PV-NXT-FP-LAST-DTE01565706
                   DISPLAY '- PV-NXT-FP-LAST-DTE:' PV-NXT-FP-LAST-DTE   01565810
               WHEN OTHER                                               01565906
                   DISPLAY 'ERROR GETTING NEXT FISCAL MONTH DATES'      01566006
                   PERFORM Z5000-ERROR-ROUTINE                          01566106
           END-EVALUATE.                                                01566206
                                                                        01566306
      ******************************************************************01568000
      * 2000-EXTRACT-PROCESSING                                         01570000
      ******************************************************************01580000
       2000-EXTRACT-PROCESSING.                                         01590000
                                                                        01600000
           PERFORM 2100-MOVE-TO-LAYOUT.                                 01610000
           PERFORM 2200-WRITE-EXTRACT-RECORD.                           01620000
                                                                        01630000
           PERFORM 2400-FETCH-NON-CNCL-TRK-CSR.                         01640002
                                                                        01650000
      ******************************************************************01660000
      * 2100-MOVE-TO-LAYOUT.                                            01670000
      ******************************************************************01680000
       2100-MOVE-TO-LAYOUT.                                             01690000
                                                                        01700000
           MOVE SDT00032-STR-NBR         TO SDRD175-STR-NBR.            01710011
           MOVE PC-FIELD-DELIMITER       TO SDRD175-DELIMITER-1.        01720011
           MOVE SDT00032-TRLR-ARRVL-DTE  TO SDRD175-TRLR-ARRVL-DTE.     01730011
           MOVE PC-FIELD-DELIMITER       TO SDRD175-DELIMITER-2.        01740011
           MOVE PV-CURRENT-DATE          TO SDRD175-CURRENT-DATE.       01792011
           MOVE PC-FIELD-DELIMITER       TO SDRD175-DELIMITER-3.        01792111
           MOVE PV-TRIP-QTY              TO SDRD175-TRIP-QTY.           01792215
           MOVE PC-FIELD-DELIMITER       TO SDRD175-DELIMITER-4.        01792315
           MOVE SPACES                   TO SDRD175-CHANGE-ACTN-CDE.    01792415
                                                                        01793000
      ******************************************************************01794000
      * 2200-WRITE-EXTRACT-RECORD.                                      01795000
      ******************************************************************01796000
       2200-WRITE-EXTRACT-RECORD.                                       01797000
                                                                        01798000
           WRITE EXTRACT-RECORD                                         01799000
               FROM SDRD175-EXTRACT-RECORD.                             01800011
                                                                        01810000
           ADD +1 TO PA-WRITE-COUNT.                                    01820000
                                                                        01830000
      ******************************************************************01840000
      * 2300-OPEN-NON-CNCL-TRK-CSR.                                     01850002
      ******************************************************************01860000
       2300-OPEN-NON-CNCL-TRK-CSR.                                      01870002
                                                                        01880000
           EXEC SQL                                                     01890000
             OPEN NON-CNCL-TRK-PROJ-CSR                                 01900000
           END-EXEC.                                                    01910000
                                                                        01920000
           EVALUATE TRUE                                                01930000
             WHEN SQLCODE = +0                                          01940000
               CONTINUE                                                 01950000
                                                                        01960000
             WHEN OTHER                                                 01970000
               MOVE 'OPENING SQL CURSOR    ' TO PV-DB2-OPERATION        01980000
               MOVE '2300-OPEN-WKLY-STR-ADJ' TO PV-PARAGRAPH-NAME       01990000
               PERFORM 9020-SQL-ABEND-ROUTINE                           02000000
           END-EVALUATE.                                                02010000
                                                                        02020000
      ******************************************************************02030000
      * 2400-FETCH-NON-CNCL-TRK-CSR                                     02040002
      ******************************************************************02050000
       2400-FETCH-NON-CNCL-TRK-CSR.                                     02060002
                                                                        02070000
           INITIALIZE DCLSDT00032.                                      02080000
                                                                        02090000
           EXEC SQL                                                     02100000
             FETCH NON-CNCL-TRK-PROJ-CSR                                02110000
               INTO :SDT00032-STR-NBR                                   02120000
                  , :SDT00032-TRLR-ARRVL-DTE                            02130000
                  , :PV-CURRENT-DATE                                    02170000
                  , :PV-TRIP-QTY                                        02171015
           END-EXEC                                                     02180000
                                                                        02190000
           EVALUATE TRUE                                                02200000
             WHEN SQLCODE = +0                                          02210000
               ADD +1 TO PA-FETCH-COUNT                                 02220000
               SET PS-NOT-END-OF-CSR TO TRUE                            02230000
                                                                        02240000
             WHEN SQLCODE = +100                                        02250000
               SET PS-END-OF-CSR TO TRUE                                02260000
                                                                        02270000
             WHEN OTHER                                                 02280000
               MOVE 'FETCHING CSR         ' TO PV-DB2-OPERATION         02290000
               MOVE '2400-                ' TO PV-PARAGRAPH-NAME        02300000
               PERFORM 9020-SQL-ABEND-ROUTINE                           02310000
           END-EVALUATE.                                                02320000
                                                                        02330000
      ******************************************************************02340000
      * 3000-WRAP-UP                                                    02350000
      ******************************************************************02360000
       3000-WRAP-UP.                                                    02370000
                                                                        02380000
           CLOSE EXTRACT-FILE.                                          02390000
           PERFORM 3100-CLOSE-NON-CNCL-TRK-CSR.                         02400003
           DISPLAY '* - NON-CNCL PROJ TRK EXTR CMPLTED'.                02460000
                                                                        02480000
      ******************************************************************02490000
      * 3100-CLOSE-NON-CNCL-TRK-CSR.                                    02500003
      ******************************************************************02510000
       3100-CLOSE-NON-CNCL-TRK-CSR.                                     02520003
                                                                        02530000
           EXEC SQL                                                     02540000
             CLOSE NON-CNCL-TRK-PROJ-CSR                                02550000
           END-EXEC.                                                    02560000
                                                                        02570000
           EVALUATE TRUE                                                02580000
             WHEN SQLCODE = +0                                          02590000
               CONTINUE                                                 02600000
                                                                        02610000
             WHEN OTHER                                                 02620000
               MOVE 'CLOSING CSR           '  TO PV-DB2-OPERATION       02630000
               MOVE '3100-CLOSE-CSR         ' TO PV-PARAGRAPH-NAME      02640000
               PERFORM 9020-SQL-ABEND-ROUTINE                           02650000
           END-EVALUATE.                                                02660000
                                                                        02670000
      ******************************************************************02680000
      * 9020-SQL-ABEND-ROUTINE                                          02690000
      ******************************************************************02700000
       9020-SQL-ABEND-ROUTINE.                                          02710000
                                                                        02720000
           DISPLAY '*'.                                                 02730000
           DISPLAY '** ABEND     **'.                                   02740000
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                02750000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              02760000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               02770000
                                                                        02780000
           COPY DPPD004.                                                02790000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            02800000
           CALL 'ILBOABN0' USING ABEND-CODE.                            02810000
                                                                        02810106
      ******************************************************************02811006
      * DATE-ERROR-ROUTINE.                                             02812006
      ******************************************************************02813006
       Z5000-ERROR-ROUTINE.                                             02814006
                                                                        02815006
           PERFORM 9999-SUMMARY.                                        02816006
                                                                        02817006
           DISPLAY 'PARAGRAPH: ' PV-PARAGRAPH-NAME                      02818006
           DISPLAY 'ERROR: ' PV-OPERATION-MSG-1                         02819007
           DISPLAY 'SQLCODE: ' SQLCODE                                  02819106
           DISPLAY '         ' SQLERRMC.                                02819206
                                                                        02819306
           CALL 'ILBOABN0' USING ABEND-CODE.                            02819406
                                                                        02820000
      ******************************************************************02830000
      * 9999-SUMMARY.                                                   02840000
      ******************************************************************02850000
       9999-SUMMARY.                                                    02860000
                                                                        02870000
           DISPLAY '*'.                                                 02880000
           DISPLAY '*************************************'.             02890000
           DISPLAY '* PROGRAM SUMMARY - SDKUT175        *'.             02900012
           DISPLAY '*************************************'.             02910000
           DISPLAY '* - ROWS EXTRACTED      : ' PA-FETCH-COUNT '   *'.  02920000
           DISPLAY '* - ROWS WRITTEN TO FILE: ' PA-WRITE-COUNT '   *'.  02930000
           DISPLAY '*************************************'.             02940000
                                                                        02950000
      ******************************************************************02960000
