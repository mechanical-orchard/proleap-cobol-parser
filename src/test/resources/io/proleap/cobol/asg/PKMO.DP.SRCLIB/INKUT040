       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT040.                                       
       AUTHOR.                  JIM ARENDT.                                     
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            09/02/2013.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  INVENTORY TODAY STORE LIST                                    *        
      *                                                                *        
      *  THIS PROGRAM WILL READ A CONTROL CARD CONTAINING THE CURRENT  *        
      *  DATE AND RETRIEVE ALL STORES TAKING INVENTORY AS OF THAT DATE.*        
      *  IT WILL THEN RETRIEVE THE PLANNED INVENTORY DATE FOR THE STORE*        
      *  AND WRITE THE DATA TO AN OUTPUT FILE.                         *        
      *                                                                *        
      *  THE INPUT TO THIS PROGRAM WILL BE THE INVENTORY DATE          *        
      *  IN THE FORMAT OF CCYY-MM-DD.  FOR EXAMPLE: '2013-06-20'       *        
      *                                                                *        
      *CHANGE HISTORY:                                                 *        
      * WR/PROJ   DATE      PGMR        DESCRIPTION OF CHANGES         *        
      * -------   --------- ----------- --------------------------------        
      * CHG63198  09/2013   J ARENDT    INITIAL IMPLEMENTATION                  
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-CONTROL-CARD                                            
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUTPUT-STORE-FILE                                             
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-CONTROL-CARD                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INPUT-CONTROL-RECORD           PIC  X(80).                           
                                                                                
       FD  OUTPUT-STORE-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  OUTPUT-STORE-RECORD            PIC  X(04).                           
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                     PIC S9(04)    VALUE ZERO              
                                                        COMP SYNC.              
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME          PIC  X(30)    VALUE SPACES.           
           05  PV-DB2-OPER-ATTEMPTED      PIC  X(30)    VALUE SPACES.           
           05  PC-PROGRAM-NAME            PIC  X(08)    VALUE                   
                                                        'INKUT040'.             
                                                                                
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-STORES-READ-CNT         PIC  9(06)    VALUE ZEROES.           
           05  PA-OUT-STORE-CNT           PIC  9(06)    VALUE ZEROES.           
           05  PA-EDIT                    PIC  ZZZ,ZZ9.                         
                                                                                
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-CONTROL-SW       PIC  X(01)    VALUE 'N'.              
               88  PS-END-OF-CONTROL                    VALUE 'Y'.              
               88  PS-NOT-END-OF-CONTROL                VALUE 'N'.              
           05  PS-END-OF-CSR-SW           PIC  X(01)    VALUE 'N'.              
               88  PS-END-OF-CSR                        VALUE 'Y'.              
               88  PS-NOT-END-OF-CSR                    VALUE 'N'.              
                                                                                
      *****************************************************************         
       01  WS-INPUT-FILE.                                                       
           05  WIF-RUN-DATE               PIC  X(10)    VALUE SPACES.           
           05  FILLER                     PIC  X(70)    VALUE SPACES.           
                                                                                
      *****************************************************************         
       01  WS-OUTPUT-RECORD.                                                    
           05  WOF-OUT-LOC                PIC  9(04)    VALUE ZEROES.           
                                                                                
      *****  SQL ABEND ROUTINE WORKING STORAGE                                  
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * DB2 TABLE TINCNTL                                                       
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 TABLE TINVPAR                                                       
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE TINVPAR_CSR CURSOR FOR                                  
                 SELECT DISTINCT A.LOC_NBR                                      
                   FROM TINVPAR A                                               
                       ,TINCNTL B                                               
                 WHERE A.ACTL_FIN_BK_DTE  = '9999-09-09'                        
                   AND A.ACTL_INV_DTE     = :WIF-RUN-DATE                       
                   AND A.LOC_INV_STAT_CDE = 'IN'                                
                   AND A.INV_ID           = B.INV_ID                            
                   AND B.ACTV_IND         = 'Y'                                 
                 ORDER BY LOC_NBR                                               
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       1000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1100-INITIALIZE-RTN.                                         
                                                                                
           PERFORM 2000-PROCESS-CSR                                             
               UNTIL PS-END-OF-CSR.                                             
                                                                                
           PERFORM 1900-END-RTN.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * OPEN INPUT/OUTPUT FILES AND READ THE FIRST CURSOR RECORD                
      ******************************************************************        
       1100-INITIALIZE-RTN.                                                     
                                                                                
           OPEN INPUT  INPUT-CONTROL-CARD                                       
                OUTPUT OUTPUT-STORE-FILE.                                       
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '**************************************'.                    
           DISPLAY '*** PROGRAM NAME: '      PC-PROGRAM-NAME.                   
           DISPLAY ' '.                                                         
                                                                                
           PERFORM 4000-READ-CONTROL-CARD.                                      
                                                                                
           PERFORM 6000-OPEN-TINVPAR-CSR.                                       
                                                                                
      ******************************************************************        
      * CLOSE FILES AND DISPLAY COUNTS                                          
      ******************************************************************        
       1900-END-RTN.                                                            
                                                                                
           CLOSE INPUT-CONTROL-CARD                                             
                 OUTPUT-STORE-FILE.                                             
                                                                                
           PERFORM 6200-CLOSE-TINVPAR-CSR.                                      
                                                                                
           DISPLAY '** PROGRAM COUNTS **'.                                      
           MOVE PA-STORES-READ-CNT        TO PA-EDIT.                           
           DISPLAY '  STORES READ:    '      PA-EDIT.                           
           MOVE PA-OUT-STORE-CNT          TO PA-EDIT.                           
           DISPLAY '  STORES WRITTEN: '      PA-EDIT.                           
           DISPLAY ' '.                                                         
                                                                                
      ******************************************************************        
      * READ ALL RECORDS FROM THE CURSOR AND WRITE THE DATA TO OUTPUT           
      ******************************************************************        
       2000-PROCESS-CSR.                                                        
                                                                                
           PERFORM 6100-FETCH-TINVPAR-CSR.                                      
           IF PS-END-OF-CSR                                                     
              CONTINUE                                                          
           ELSE                                                                 
              MOVE INVPAR-LOC-NBR        TO WOF-OUT-LOC                         
              PERFORM 5000-WRITE-OUTPUT-RECORD                                  
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 4000-READ-CONTROL-CARD.                                                 
      ******************************************************************        
       4000-READ-CONTROL-CARD.                                                  
                                                                                
           READ INPUT-CONTROL-CARD                                              
               INTO WS-INPUT-FILE                                               
                   AT END                                                       
                       MOVE 'Y' TO PS-END-OF-CONTROL-SW.                        
                                                                                
           IF PS-END-OF-CONTROL                                                 
               DISPLAY ' '                                                      
               DISPLAY ' *** NO CONTROL CARD FOUND TO PROCESS ***'              
               DISPLAY ' '                                                      
           ELSE                                                                 
               DISPLAY ' RUN DATE = ' WIF-RUN-DATE                              
               DISPLAY ' '                                                      
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 5000-WRITE-OUTPUT-RECORD                                                
      ******************************************************************        
       5000-WRITE-OUTPUT-RECORD.                                                
                                                                                
           WRITE OUTPUT-STORE-RECORD FROM                                       
                 WS-OUTPUT-RECORD.                                              
                                                                                
           ADD +1               TO PA-OUT-STORE-CNT.                            
                                                                                
      ******************************************************************        
      *  OPEN THE TINVPAR_CSR CURSOR                                            
      ******************************************************************        
       6000-OPEN-TINVPAR-CSR.                                                   
                                                                                
           EXEC SQL                                                             
               OPEN TINVPAR_CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                CONTINUE                                                        
                                                                                
              WHEN OTHER                                                        
                DISPLAY ' ERROR RUN DATE = '    WIF-RUN-DATE                    
                MOVE '6000-OPEN-TINVPAR-CSR' TO PV-PARAGRAPH-NAME               
                MOVE 'OPEN TINVPAR CURSOR'   TO PV-DB2-OPER-ATTEMPTED           
                PERFORM 9900-SQL-ABEND                                          
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  READ A RECORD FROM THE TINVPAR_CSR CURSOR                              
      ******************************************************************        
       6100-FETCH-TINVPAR-CSR.                                                  
                                                                                
           EXEC SQL                                                             
               FETCH TINVPAR_CSR                                                
               INTO :INVPAR-LOC-NBR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                  ADD 1  TO PA-STORES-READ-CNT                                  
                                                                                
              WHEN +100                                                         
                  SET PS-END-OF-CSR TO TRUE                                     
                                                                                
              WHEN OTHER                                                        
                  DISPLAY ' ERROR RUN DATE = '     WIF-RUN-DATE                 
                  MOVE '6100-FETCH-TINVPAR-CSR' TO PV-PARAGRAPH-NAME            
                  MOVE 'FETCH TINVPAR CURSOR'   TO PV-DB2-OPER-ATTEMPTED        
                  PERFORM 9900-SQL-ABEND                                        
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  CLOSE THE TINVPAR_CSR CURSOR                                           
      ******************************************************************        
       6200-CLOSE-TINVPAR-CSR.                                                  
                                                                                
           EXEC SQL                                                             
               CLOSE TINVPAR_CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                  CONTINUE                                                      
                                                                                
              WHEN OTHER                                                        
                  MOVE '6200-CLOSE-TINVPAR-CSR' TO PV-PARAGRAPH-NAME            
                  MOVE 'CLOSE TINVPAR CURSOR'   TO PV-DB2-OPER-ATTEMPTED        
                  PERFORM 9900-SQL-ABEND                                        
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
