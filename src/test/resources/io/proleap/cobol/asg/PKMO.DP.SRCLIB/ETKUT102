       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT102.                                                 
       AUTHOR.        TIA JOHNSTON.                                             
       DATE-WRITTEN.  09/27/99.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      ** THIS PROGRAM PRINTS A REPORT SHOWING EDI PRODUCTION ASN VENDORS        
      ** AND THEIR ACTIVATION DATE AS WELL AS THEIR SCAN CAPABILITY.            
      ******************************************************************        
JF0503******************************************************************        
JF0503* 05/14/2003 JON FIEDLER                    WR#: 24663                    
JF0503* CHANGE MADE: CHANGED LOGIC TO USE THE EDI_PREPK_CDE FROM THE            
JF0503* TEXTENT TABLE. THIS FIELD WAS ENHANCED TO IDENTIFY COMPOUND             
JF0503* PACK VENDORS.                                                           
JF0503******************************************************************        
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT REPORT-FILE                                                   
               ASSIGN TO RPT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  REPORT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-RECORD.                                        
                                                                                
       01  REPORT-RECORD                PIC X(132).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-DATES-AND-TIME.                                                   
           05  WS-CURRENT-DATE.                                                 
               10  WS-CURRENT-YY        PIC 9(2)  VALUE ZERO.                   
               10  WS-CURRENT-MM        PIC 9(2)  VALUE ZERO.                   
               10  WS-CURRENT-DD        PIC 9(2)  VALUE ZERO.                   
           05  WS-CURRENT-TIME.                                                 
               10  WS-CURRENT-HR        PIC 9(2)  VALUE ZERO.                   
               10  WS-CURRENT-MIN       PIC 9(2)  VALUE ZERO.                   
               10  FILLER               PIC 9(4)  VALUE ZERO.                   
                                                                                
       01  WS-MISC.                                                             
           05  WS-LINE-CNT    COMP-3    PIC S9(5) VALUE +57.                    
           05  WS-PAGE-CNT    COMP-3    PIC S9(5) VALUE ZERO.                   
           05  WS-LINE-MAX              PIC S9(5) VALUE +55.                    
           05  ABEND-CODE     COMP SYNC PIC S9(4) VALUE +0.                     
           05  WS-ASN-CNT     COMP-3    PIC S9(5) VALUE +0.                     
           05  WS-UCC-CNT     COMP-3    PIC S9(5) VALUE +0.                     
           05  WS-PREPACK-CNT COMP-3    PIC S9(5) VALUE +0.                     
                                                                                
       01  WS-REPORT-DATE.                                                      
           05  WS-REPORT-MM             PIC 9(2).                               
           05  FILLER                   PIC X(1)  VALUE '/'.                    
           05  WS-REPORT-DD             PIC 9(2).                               
           05  FILLER                   PIC X(1)  VALUE '/'.                    
           05  WS-REPORT-YY             PIC 9(2).                               
                                                                                
       01  WS-SQL-DATE.                                                         
           05  WS-SQL-CC                PIC 9(2).                               
           05  WS-SQL-YY                PIC 9(2).                               
           05  FILLER                   PIC X(1).                               
           05  WS-SQL-MM                PIC 9(2).                               
           05  FILLER                   PIC X(1).                               
           05  WS-SQL-DD                PIC 9(2).                               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARA                  PIC X(30).                              
           05  PV-DB2-OPER-ATTEMPTED    PIC X(30).                              
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-VEND-CSR-SW       PIC X     VALUE 'N'.                    
               88  END-VEND-CSR                   VALUE 'Y'.                    
                                                                                
           COPY DPWS004.                                                        
                                                                                
                                                                                
       01  REPORT-HEADING1.                                                     
           05  FILLER                   PIC X(40) VALUE SPACES.                 
           05  FILLER                   PIC X(42) VALUE                         
               'EDI PRODUCTION ASN VENDORS AND THEIR SCAN '.                    
           05  FILLER                   PIC X(10) VALUE                         
               'CAPABILITY'.                                                    
           05  FILLER                   PIC X(40) VALUE SPACES.                 
                                                                                
       01  REPORT-HEADING2.                                                     
           05  FILLER                   PIC X(06) VALUE 'VENDOR'.               
           05  FILLER                   PIC X(29) VALUE SPACES.                 
           05  FILLER                   PIC X(10) VALUE 'SETUP DATE'.           
           05  FILLER                   PIC X(05) VALUE SPACES.                 
           05  FILLER                   PIC X(06) VALUE 'UCC128'.               
           05  FILLER                   PIC X(05) VALUE SPACES.                 
           05  FILLER                   PIC X(08) VALUE 'PRE-PACK'.             
           05  FILLER                   PIC X(63) VALUE SPACES.                 
                                                                                
       01  REPORT-HEADING3.                                                     
           05  FILLER                   PIC X(06) VALUE ALL '-'.                
           05  FILLER                   PIC X(29) VALUE SPACES.                 
           05  FILLER                   PIC X(10) VALUE ALL '-'.                
           05  FILLER                   PIC X(05) VALUE SPACES.                 
           05  FILLER                   PIC X(06) VALUE ALL '-'.                
           05  FILLER                   PIC X(05) VALUE SPACES.                 
           05  FILLER                   PIC X(08) VALUE ALL '-'.                
           05  FILLER                   PIC X(63) VALUE SPACES.                 
                                                                                
       01  REPORT-DETAIL-LINE.                                                  
           05  RPT-VENDOR-NAME          PIC X(30).                              
           05  FILLER                   PIC X(06).                              
           05  RPT-SETUP-DATE           PIC X(08).                              
           05  FILLER                   PIC X(09).                              
           05  RPT-UCC128               PIC X(01).                              
           05  FILLER                   PIC X(11).                              
           05  RPT-PREPACK              PIC X(01).                              
           05  FILLER                   PIC X(66).                              
                                                                                
       01  REPORT-TOTAL-LINE1.                                                  
           05  FILLER                   PIC X(30) VALUE ALL '-'.                
           05  FILLER                   PIC X(20) VALUE SPACES.                 
           05  FILLER                   PIC X(06) VALUE ALL '-'.                
           05  FILLER                   PIC X(05) VALUE SPACES.                 
           05  FILLER                   PIC X(08) VALUE ALL '-'.                
           05  FILLER                   PIC X(63) VALUE SPACES.                 
                                                                                
       01  REPORT-TOTAL-LINE2.                                                  
           05  FILLER                   PIC X(24) VALUE SPACES.                 
           05  RPT-TOT-ASN              PIC ZZ,ZZ9.                             
           05  FILLER                   PIC X(20) VALUE SPACES.                 
           05  RPT-TOT-UCC              PIC ZZ,ZZ9.                             
           05  FILLER                   PIC X(07) VALUE SPACES.                 
           05  RPT-TOT-PREPACK          PIC ZZ,ZZ9.                             
           05  FILLER                   PIC X(63) VALUE SPACES.                 
                                                                                
           COPY DPWS132O.                                                       
                                                                                
           COPY SQLCA2.                                                         
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TENTNME                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDTRN                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TEXTENT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    VENDOR CURSOR                                                        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             DECLARE VENDOR_CSR CURSOR FOR                                      
                SELECT C.ENT_NAME_DESC,                                         
                       B.ACTV_DTE,                                              
                       D.VLD_CART_LBL_IND,                                      
JF0503                 D.EDI_PO_PREPK_CDE                                       
                FROM TTRDPRT A,                                                 
                     TTRDTRN B,                                                 
                     TENTNME C,                                                 
                     TEXTENT D                                                  
                WHERE A.INACTV_DTE = '9999-09-09'                               
                  AND A.TP_DKEY = B.TP_DKEY                                     
                  AND A.ENT_ID = C.ENT_ID                                       
                  AND A.ENT_ID = D.ENT_ID                                       
                  AND C.TYPE_CDE = '01'                                         
                  AND B.INACTV_DTE = '9999-09-09'                               
                  AND B.TRAN_SET_CDE = '856'                                    
                  AND B.PROD_TEST_IND = 'P'                                     
                ORDER BY C.ENT_NAME_DESC                                        
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
                                                                                
           PERFORM 8000-OPEN-VEND-CURSOR THRU 8000-EXIT.                        
                                                                                
           PERFORM 2000-PROCESS-VEND THRU 2000-EXIT                             
              UNTIL END-VEND-CSR.                                               
                                                                                
           PERFORM 3500-PRINT-TOTAL-LINE THRU 3500-EXIT.                        
                                                                                
           PERFORM 8010-CLOSE-VEND-CURSOR THRU 8010-EXIT.                       
                                                                                
           CLOSE REPORT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN OUTPUT REPORT-FILE.                                             
           MOVE SPACES TO REPORT-DETAIL-LINE.                                   
                                                                                
           ACCEPT WS-CURRENT-DATE FROM DATE.                                    
           MOVE WS-CURRENT-YY     TO DP132O-RUN-YEAR.                           
           MOVE WS-CURRENT-MM     TO DP132O-RUN-MONTH.                          
           MOVE WS-CURRENT-DD     TO DP132O-RUN-DAY.                            
           ACCEPT WS-CURRENT-TIME FROM TIME.                                    
           MOVE WS-CURRENT-HR     TO DP132O-RUN-HOUR.                           
           MOVE WS-CURRENT-MIN    TO DP132O-RUN-MINUTE.                         
           MOVE 'ETKUT102'        TO DP132O-PROGRAM-NAME.                       
           MOVE 1                 TO DP132O-REPORT-NUMBER.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-PROCESS-VEND.                                                       
                                                                                
           PERFORM 8005-FETCH-VEND THRU 8005-EXIT.                              
                                                                                
           IF END-VEND-CSR                                                      
              GO TO 2000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE ENTNME-ENT-NAME-DESC TO RPT-VENDOR-NAME.                        
           ADD +1 TO WS-ASN-CNT.                                                
                                                                                
           MOVE TRDTRN-ACTV-DTE TO WS-SQL-DATE.                                 
           MOVE WS-SQL-MM TO WS-REPORT-MM.                                      
           MOVE WS-SQL-DD TO WS-REPORT-DD.                                      
           MOVE WS-SQL-YY TO WS-REPORT-YY.                                      
           MOVE WS-REPORT-DATE TO RPT-SETUP-DATE.                               
                                                                                
           IF EXTENT-VLD-CART-LBL-IND = 'Y'                                     
              MOVE 'Y' TO RPT-UCC128                                            
              ADD +1 TO WS-UCC-CNT                                              
           ELSE                                                                 
              MOVE 'N' TO RPT-UCC128                                            
           END-IF.                                                              
                                                                                
JF0503     IF EXTENT-EDI-PO-PREPK-CDE = 'Y' OR 'C'                              
                MOVE 'Y' TO RPT-PREPACK                                         
                ADD +1 TO WS-PREPACK-CNT                                        
           ELSE                                                                 
                MOVE 'N' TO RPT-PREPACK                                         
           END-IF.                                                              
                                                                                
           PERFORM 3000-PRINT-VENDOR THRU 3000-EXIT.                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PRINT-VENDOR.                                                       
                                                                                
           IF WS-LINE-CNT > WS-LINE-MAX                                         
              PERFORM 3010-PRINT-HEADINGS THRU 3010-EXIT.                       
                                                                                
           WRITE REPORT-RECORD FROM REPORT-DETAIL-LINE                          
              AFTER ADVANCING 1 LINE.                                           
           ADD +1 TO WS-LINE-CNT.                                               
           MOVE SPACES TO REPORT-DETAIL-LINE.                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3010-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1 TO WS-PAGE-CNT.                                                
           MOVE WS-PAGE-CNT TO DP132O-PAGE-NUMBER.                              
                                                                                
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS              
              AFTER ADVANCING PAGE.                                             
                                                                                
           WRITE REPORT-RECORD FROM REPORT-HEADING1                             
              AFTER ADVANCING 1 LINE.                                           
                                                                                
           WRITE REPORT-RECORD FROM REPORT-HEADING2                             
              AFTER ADVANCING 3 LINES.                                          
                                                                                
           WRITE REPORT-RECORD FROM REPORT-HEADING3                             
              AFTER ADVANCING 1 LINE.                                           
                                                                                
           MOVE +7 TO WS-LINE-CNT.                                              
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3500-PRINT-TOTAL-LINE.                                                   
                                                                                
           MOVE WS-ASN-CNT     TO RPT-TOT-ASN.                                  
           MOVE WS-UCC-CNT     TO RPT-TOT-UCC.                                  
           MOVE WS-PREPACK-CNT TO RPT-TOT-PREPACK.                              
                                                                                
           WRITE REPORT-RECORD FROM REPORT-TOTAL-LINE1                          
              AFTER ADVANCING 2 LINES.                                          
           WRITE REPORT-RECORD FROM REPORT-TOTAL-LINE2                          
              AFTER ADVANCING 1 LINE.                                           
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       8000-OPEN-VEND-CURSOR.                                                   
                                                                                
           MOVE '8000-'              TO PV-PARA                                 
           MOVE 'OPEN VENDOR CURSOR' TO PV-DB2-OPER-ATTEMPTED.                  
                                                                                
           EXEC SQL                                                             
              OPEN VENDOR_CSR                                                   
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       8005-FETCH-VEND.                                                         
                                                                                
           MOVE '8005-'            TO PV-PARA                                   
           MOVE 'FETCH VENDOR CSR' TO PV-DB2-OPER-ATTEMPTED.                    
                                                                                
           EXEC SQL                                                             
             FETCH VENDOR_CSR                                                   
             INTO :ENTNME-ENT-NAME-DESC,                                        
                  :TRDTRN-ACTV-DTE,                                             
                  :EXTENT-VLD-CART-LBL-IND,                                     
JF0503            :EXTENT-EDI-PO-PREPK-CDE                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET END-VEND-CSR TO TRUE                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       8005-EXIT.                                                               
           EXIT.                                                                
                                                                                
       8010-CLOSE-VEND-CURSOR.                                                  
                                                                                
           MOVE '8010-'            TO PV-PARA                                   
           MOVE 'CLOSE VENDOR CSR' TO PV-DB2-OPER-ATTEMPTED.                    
                                                                                
           EXEC SQL                                                             
              CLOSE VENDOR_CSR                                                  
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       8010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    SQL ABEND ROUTINE                                                    
      *****************************************************************         
       9000-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ETKUT102'.                              
           DISPLAY '**  PARAGRAPH **  = ' PV-PARA.                              
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ABEND THRU 9999-EXIT.                                   
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'             USING ABEND-CODE.                        
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
