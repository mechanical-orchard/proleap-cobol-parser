       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKRP056.                                                 
       AUTHOR.        JIMMY/INFOSYS.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  10/07/08.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM WAS COPIED FROM MLKRP054 AND MODIFIED TO CREATE            
      * THE EDIT SPLIT TOTALS FOR ALL STORES REPORT ONLY AT CYCLE LEVEL.        
      *                                                                         
      * THIS PROGRAM READS THE EDIT SPLIT REPORT DATA FILE AND                  
      * REPORTS MERCHANDISE LEDGER AND INVENTORY LEDGER COST AND RETAIL         
      * AMOUNTS.                                                                
      *                                                                         
      * THIS REPORT IS THE EDIT SPLIT CORPORATE REPORT. IT REPORTS              
      * TOTALS BY FINANCIAL LIABILITY DATE WITHIN TRANSACTION CODE.             
      * IT BREAKS AND TOTALS BY CYCLE.                                          
      *                                                                         
      * WR/PROJ DATE       PROGAMMER    DESCRIPTION OF CHANGES                  
      * ------- ---------- ------------ --------------------------------        
      * W33304  10-07-2008 JIMMY/        NEW PROGRAM CREATED AS PART            
      *                    INFOSYS       OF HOLDING INVENTORY LEDGER            
      *                                  OPEN - HILO                            
      * F33304  09-10-2009 JILL LIN      FIX PRODUCTION ISSUE FOR               
      *                                  INC000000947354                        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT EDIT-SPLIT-FILE                                               
               ASSIGN TO INP01.                                                 
           SELECT CYCLE-SPLIT-REPORT                                            
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  EDIT-SPLIT-FILE                                                      
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F                                                 
           DATA RECORD IS EDIT-SPLIT-RECORD.                                    
       01  EDIT-SPLIT-RECORD           PIC X(110).                              
                                                                                
       FD  CYCLE-SPLIT-REPORT                                                   
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 133 CHARACTERS                                       
           RECORDING  MODE IS F                                                 
           DATA RECORD IS CYCLE-SPLIT-REPORT-LINE.                              
       01  CYCLE-SPLIT-REPORT-LINE      PIC X(133).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES               PIC S9(04)    VALUE +3              
                                                              COMP SYNC.        
           05  PC-LINE-LIMIT-FOR-DTL        PIC S9(04)    VALUE +57             
                                                              COMP SYNC.        
           05  PC-LINE-LIMIT-FOR-HD-TC      PIC S9(04)    VALUE +54             
                                                              COMP SYNC.        
           05  PC-EFF-DATE-HD-LIT           PIC  X(08)    VALUE                 
                                                             'EFF DATE'.        
           05  PC-INVENTORY-ONLY-MODE       PIC  X(20)  VALUE                   
                                                     '(INVENTORY ONLY)'.        
           05  PC-NON-CYCLE-NAME            PIC  X(20)  VALUE                   
                                                 'NON-INVENTORY STORES'.        
           05  PC-FNL-CYCLE-NAME            PIC  X(20)  VALUE                   
                                                         'FINAL TOTALS'.        
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACE.          
           05  PV-ADVANCE                   PIC S9(03)    VALUE ZERO            
                                                          COMP-3.               
           05  PV-PAGE-COUNT                PIC S9(05)    VALUE ZERO.           
           05  PV-LINE-COUNT                PIC S9(04)    VALUE ZERO            
                                                          COMP SYNC.            
           05  PV-ABEND-MESSAGE             PIC  X(75)    VALUE SPACE.          
           05  PV-SQL-SUB                   PIC  9(03)    VALUE ZERO.           
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30)    VALUE SPACE.          
           05  PV-YYMMDD-DATE.                                                  
               10  PV-YYMMDD-YY             PIC  X(02)    VALUE SPACE.          
               10  PV-YYMMDD-MM             PIC  X(02)    VALUE SPACE.          
               10  PV-YYMMDD-DD             PIC  X(02)    VALUE SPACE.          
           05  PV-ISO-FMT-DATE.                                                 
               10  PV-ISO-FMT-CC            PIC  X(02).                         
               10  PV-ISO-FMT-YY            PIC  X(02).                         
               10  FILLER                   PIC  X.                             
               10  PV-ISO-FMT-MM            PIC  X(02).                         
               10  FILLER                   PIC  X.                             
               10  PV-ISO-FMT-DD            PIC  X(02).                         
           05  PV-TIME-HHMMSS.                                                  
               10  PV-TIME-HHMMSS-HH        PIC  X(02)    VALUE SPACES.         
               10  PV-TIME-HHMMSS-MM        PIC  X(02)    VALUE SPACES.         
               10  PV-TIME-HHMMSS-SS        PIC  X(02)    VALUE SPACES.         
           05  PV-LAST-CNTL-CARD-READ       PIC  X(80).                         
       01  TOT-TOTAL-FIELDS.                                                    
           05  TOT-FLB-FIN-LIAB-DTE-TOTALS.                                     
               10  TOT-FLB-CURR-ML-COST      PIC  S9(13)V99 COMP-3.             
               10  TOT-FLB-CURR-ML-RETAIL    PIC  S9(13)V99 COMP-3.             
               10  TOT-FLB-CURR-INV-COST     PIC  S9(13)V99 COMP-3.             
               10  TOT-FLB-CURR-INV-RETAIL   PIC  S9(13)V99 COMP-3.             
           05  TOT-TCD-TRANS-CDE-TOTALS.                                        
               10  TOT-TCD-CURR-ML-COST      PIC  S9(13)V99 COMP-3.             
               10  TOT-TCD-CURR-ML-RETAIL    PIC  S9(13)V99 COMP-3.             
               10  TOT-TCD-CURR-INV-COST     PIC  S9(13)V99 COMP-3.             
               10  TOT-TCD-CURR-INV-RETAIL   PIC  S9(13)V99 COMP-3.             
           05  TOT-CYC-CYCLE-TOTALS.                                            
               10  TOT-CYC-CURR-ML-COST      PIC  S9(13)V99 COMP-3.             
               10  TOT-CYC-CURR-ML-RETAIL    PIC  S9(13)V99 COMP-3.             
               10  TOT-CYC-CURR-INV-COST     PIC  S9(13)V99 COMP-3.             
               10  TOT-CYC-CURR-INV-RETAIL   PIC  S9(13)V99 COMP-3.             
           05  TOT-FNL-FINAL-TOTALS.                                            
               10  TOT-FNL-CURR-ML-COST      PIC  S9(13)V99 COMP-3.             
               10  TOT-FNL-CURR-ML-RETAIL    PIC  S9(13)V99 COMP-3.             
               10  TOT-FNL-CURR-INV-COST     PIC  S9(13)V99 COMP-3.             
               10  TOT-FNL-CURR-INV-RETAIL   PIC  S9(13)V99 COMP-3.             
           05  TOT-FNL-FINAL-TOTALS1.                                           
               10  TOT-FNL-CURR-ML-COST1     PIC  S9(13)V99 COMP-3.             
               10  TOT-FNL-CURR-ML-RETAIL1   PIC  S9(13)V99 COMP-3.             
               10  TOT-FNL-CURR-INV-COST1    PIC  S9(13)V99 COMP-3.             
               10  TOT-FNL-CURR-INV-RETAIL1  PIC  S9(13)V99 COMP-3.             
           05  TOT-FNL-FINAL-TOTALS-TP      OCCURS   999                        
                                            INDEXED BY FNL-TP-IDX.              
               10  TOT-FIN-LIAB-DTE-TP       PIC  X(10) VALUE SPACES.           
               10  TOT-FNL-CURR-ML-COST-TP   PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-ML-RETAIL-TP PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-COST-TP  PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-RETAIL-TP                                   
                                             PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
           05  TOT-FNL-FINAL-TOTALS-TS      OCCURS   999                        
                                            INDEXED BY FNL-TS-IDX.              
               10  TOT-FIN-LIAB-DTE-TS       PIC  X(10) VALUE SPACES.           
               10  TOT-FNL-CURR-ML-COST-TS   PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-ML-RETAIL-TS PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-COST-TS  PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-RETAIL-TS                                   
                                             PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
           05  TOT-FNL-FINAL-TOTALS-TF      OCCURS   999                        
                                            INDEXED BY FNL-TF-IDX.              
               10  TOT-FIN-LIAB-DTE-TF       PIC  X(10) VALUE SPACES.           
               10  TOT-FNL-CURR-ML-COST-TF   PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-ML-RETAIL-TF PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-COST-TF  PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-RETAIL-TF                                   
                                             PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
           05  TOT-FNL-FINAL-TOTALS-TT      OCCURS   999                        
                                            INDEXED BY FNL-TT-IDX.              
               10  TOT-FIN-LIAB-DTE-TT       PIC  X(10) VALUE SPACES.           
               10  TOT-FNL-CURR-ML-COST-TT   PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-ML-RETAIL-TT PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-COST-TT  PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-RETAIL-TT                                   
                                             PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
           05  TOT-FNL-FINAL-TOTALS-TM      OCCURS   999                        
                                            INDEXED BY FNL-TM-IDX.              
               10  TOT-FIN-LIAB-DTE-TM       PIC  X(10) VALUE SPACES.           
               10  TOT-FNL-CURR-ML-COST-TM   PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-ML-RETAIL-TM PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-COST-TM  PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-RETAIL-TM                                   
                                             PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
           05  TOT-FNL-FINAL-TOTALS-TU      OCCURS   999                        
                                            INDEXED BY FNL-TU-IDX.              
               10  TOT-FIN-LIAB-DTE-TU       PIC  X(10) VALUE SPACES.           
               10  TOT-FNL-CURR-ML-COST-TU   PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-ML-RETAIL-TU PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-COST-TU  PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-RETAIL-TU                                   
                                             PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
           05  TOT-FNL-FINAL-TOTALS-TA      OCCURS   999                        
                                            INDEXED BY FNL-TA-IDX.              
               10  TOT-FIN-LIAB-DTE-TA       PIC  X(10) VALUE SPACES.           
               10  TOT-FNL-CURR-ML-COST-TA   PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-ML-RETAIL-TA PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-COST-TA  PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-RETAIL-TA                                   
                                             PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
           05  TOT-FNL-FINAL-TOTALS-PR      OCCURS   999                        
                                            INDEXED BY FNL-PR-IDX.              
               10  TOT-FIN-LIAB-DTE-PR       PIC  X(10) VALUE SPACES.           
               10  TOT-FNL-CURR-ML-COST-PR   PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-ML-RETAIL-PR PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-COST-PR  PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
               10  TOT-FNL-CURR-INV-RETAIL-PR                                   
                                             PIC  S9(13)V99 COMP-3              
                                                      VALUE ZEROES.             
                                                                                
       01  SA-SAVE-AREAS.                                                       
           05  SA-PREV-CYCLE-NBR       PIC S9(09) COMP VALUE ZERO.              
           05  SA-PREV-TRANS-CDE       PIC X(02)   VALUE SPACE.                 
               88  SA-PREV-TRANS-PURCHASE          VALUE '10'.                  
               88  SA-PREV-TRANS-SALE              VALUE '20'.                  
               88  SA-PREV-TRANS-TRANSFER-FROM     VALUE '30'.                  
               88  SA-PREV-TRANS-TRANSFER-TO       VALUE '31'.                  
               88  SA-PREV-TRANS-MARKDOWN          VALUE '40'.                  
               88  SA-PREV-TRANS-MARKUP            VALUE '50'.                  
               88  SA-PREV-TRANS-ADJUSTMENT        VALUE '80'.                  
           05  SA-PREV-SYST-ORIG-CDE   PIC X(02)   VALUE SPACES.                
               88  SA-PREV-PURCH-RECEIPT           VALUE '08'.                  
           05  SA-PREV-FIN-LIAB-DTE    PIC X(10)   VALUE SPACE.                 
           05  SA-CURR-FSCL-YR-PER     PIC X(06)   VALUE SPACE.                 
                                                                                
       01  SW-SWITCHES.                                                         
           05  SW-END-OF-EDIT-SPLIT-FILE-SW PIC X         VALUE 'N'.            
               88  SW-END-OF-EDIT-SPLIT-FILE              VALUE 'Y'.            
               88  SW-NOT-END-OF-EDIT-SPLIT-FILE          VALUE 'N'.            
           05  SW-EMPTY-EDIT-SPLIT-FILE-SW  PIC X         VALUE 'N'.            
               88  SW-EMPTY-EDIT-SPLIT-FILE               VALUE 'Y'.            
               88  SW-NOT-EMPTY-EDIT-SPLIT-FILE           VALUE 'N'.            
           05  SW-CYCLE-BREAK-SW            PIC X         VALUE 'N'.            
               88  SW-CYCLE-BREAK                         VALUE 'Y'.            
               88  SW-NOT-CYCLE-BREAK                     VALUE 'N'.            
           05  SW-TRANS-CDE-BREAK-SW        PIC X         VALUE 'N'.            
               88  SW-TRANS-CDE-BREAK                     VALUE 'Y'.            
               88  SW-NOT-TRANS-CDE-BREAK                 VALUE 'N'.            
           05  SW-TRANS-CDE-DISP-SW         PIC X         VALUE 'N'.            
               88  SW-TRANS-CDE-DISP                      VALUE 'Y'.            
               88  SW-NOT-TRANS-CDE-DISP                  VALUE 'N'.            
                                                                                
       01  FPN-FSCL-PER-NAME-TABLE-DATA.                                        
               15  FPN-ENTRY01-DATA         PIC X(09) VALUE 'FEBRUARY '.        
               15  FPN-ENTRY02-DATA         PIC X(09) VALUE 'MARCH    '.        
               15  FPN-ENTRY03-DATA         PIC X(09) VALUE 'APRIL    '.        
               15  FPN-ENTRY04-DATA         PIC X(09) VALUE 'MAY      '.        
               15  FPN-ENTRY05-DATA         PIC X(09) VALUE 'JUNE     '.        
               15  FPN-ENTRY06-DATA         PIC X(09) VALUE 'JULY     '.        
               15  FPN-ENTRY07-DATA         PIC X(09) VALUE 'AUGUST   '.        
               15  FPN-ENTRY08-DATA         PIC X(09) VALUE 'SEPTEMBER'.        
               15  FPN-ENTRY09-DATA         PIC X(09) VALUE 'OCTOBER  '.        
               15  FPN-ENTRY10-DATA         PIC X(09) VALUE 'NOVEMBER '.        
               15  FPN-ENTRY11-DATA         PIC X(09) VALUE 'DECEMBER '.        
               15  FPN-ENTRY12-DATA         PIC X(09) VALUE 'JANUARY  '.        
                                                                                
       01  FPN-FSCL-PER-NAME-TABLE          REDEFINES                           
           FPN-FSCL-PER-NAME-TABLE-DATA.                                        
           05  FPN-ENTRY                    OCCURS 12.                          
               10  FPN-FSCL-PER-NAME        PIC X(09).                          
                                                                                
       01  FPN-SUB-X.                                                           
           05  FPN-SUB                      PIC 9(02).                          
                                                                                
                                                                                
       01  TCT-TRANS-CDE-TABLE-DATA.                                            
      *    *------------------------------------------------------------        
      *    *                    !!! NOTE !!!                                    
      *    *------------------------------------------------------------        
      *    * IF ANY CHANGES ARE MADE TO THE FORMAT OR SIZE OF THIS              
      *    * HARD-CODED TABLE DATA, TCT-TRANS-CDE-TABLE MUST BE CHANGED         
      *    * ACCORDINGLY.                                                       
      *    *------------------------------------------------------------        
           10  TCT-ENTRY1-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '10  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'PURCH ADJST'.          
                                                                                
           10  TCT-ENTRY8-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '1008'.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'RECEIPTS   '.          
                                                                                
           10  TCT-ENTRY2-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '20  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'SALES'.                
           10  TCT-ENTRY3-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '30  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'TRANSFERS OUT'.        
           10  TCT-ENTRY4-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '31  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'TRANSFERS IN'.         
           10  TCT-ENTRY5-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '40  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'MARKDOWNS'.            
           10  TCT-ENTRY6-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '50  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'MARKUPS'.              
           10  TCT-ENTRY7-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '80  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'ADJUSTMENTS'.          
       01  TCT-TRANS-CDE-TABLE              REDEFINES                           
           TCT-TRANS-CDE-TABLE-DATA.                                            
      *    *------------------------------------------------------------        
      *    *                !!! NOTE !!!                                        
      *    *------------------------------------------------------------        
      *    *                                                                    
      *    * IF ANY CHANGES ARE MADE TO THIS TABLE, THE HARD-CODED              
      *    * TABLE DATA (TCT-TRANS-CDE-TABLE-DATA) MUST BE CHANGED              
      *    * ACCORDINGLY.                                                       
      *    *                                                                    
      *    * IF THE  NUMBER OF ENTRIES IS CHANGED, TCT-MAX-ENTRIES              
      *    * MUST BE CHANGED TO REFLECT THE CORRECT NUMBER OF ENTRIES.          
      *    *------------------------------------------------------------        
           05  TCT-ENTRY                    OCCURS   8                          
                                            INDEXED BY TCT-IDX.                 
               10  TCT-TRANS-CDE            PIC  X(02).                         
               10  TCT-SYS-ORIG-CDE         PIC  X(02).                         
               10  TCT-TRANS-CDE-DESC       PIC  X(15).                         
                                                                                
      **1  TCT-MAX-ENTRIES                  PIC S9(04)    VALUE +7              
       01  TCT-MAX-ENTRIES                  PIC S9(04)    VALUE +8              
                                                          COMP SYNC.            
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
      *****************************************************************         
      * EDIT-SPLIT-RECORD LAYOUT.                                               
      *****************************************************************         
                                                                                
           COPY INRD035.                                                        
                                                                                
      ******************************************************************        
      * STANDARD REPORT HEADING                                                 
      ******************************************************************        
                                                                                
           COPY DPWS133O.                                                       
                                                                                
       01  HD-CP-CORP-HEADING.                                                  
           05  FILLER                       PIC  X(44)    VALUE SPACE.          
           05  FILLER                       PIC  X(11)    VALUE                 
                                                          'EDIT SPLIT '.        
           05  FILLER                       PIC  X(11)    VALUE                 
                                                          'TOTALS FOR '.        
           05  FILLER                       PIC  X(10)    VALUE                 
                                                           'ALL STORES'.        
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  HD-CP-PROCESSING-MODE        PIC  X(20)    VALUE SPACE.          
                                                                                
       01  HD-FT-FINAL-TOTAL-HEADING.                                           
           05  FILLER                       PIC  X(52)    VALUE SPACE.          
           05  FILLER                       PIC  X(11)    VALUE                 
                                                          'EDIT SPLIT '.        
           05  FILLER                       PIC  X(12)    VALUE                 
                                                         'FINAL TOTALS'.        
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  HD-FT-PROCESSING-MODE        PIC  X(20)    VALUE SPACE.          
                                                                                
       01  HD-ED-EDIT-DATE-HEADING.                                             
           05  FILLER                       PIC  X(5)     VALUE 'CYCLE'.        
           05  FILLER                       PIC  X        VALUE SPACES.         
           05  HD-CYCLE-NO                  PIC  9(05)    VALUE ZEROES.         
           05  FILLER                       PIC  X        VALUE SPACES.         
           05  FILLER                       PIC  X        VALUE '-'.            
           05  FILLER                       PIC  X        VALUE SPACES.         
           05  HD-CYCLE-NAME                PIC  X(20)    VALUE SPACES.         
           05  FILLER                       PIC  X(24)    VALUE SPACES.         
           05  FILLER                       PIC  X(03)    VALUE 'FOR'.          
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  HD-ED-DATE.                                                      
               15  HD-ED-DATE-MM            PIC  X(02).                         
               15  FILLER                   PIC  X        VALUE '/'.            
               15  HD-ED-DATE-DD            PIC  X(02).                         
               15  FILLER                   PIC  X        VALUE '/'.            
               15  HD-ED-DATE-YY            PIC  X(02).                         
                                                                                
       01  HD-FP-FISCAL-PER-HEADING.                                            
           05  FILLER                       PIC  X(11)    VALUE SPACE.          
           05  FILLER                       PIC  X(07)    VALUE ALL '-'.        
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(04)    VALUE 'ML: '.         
           05  HD-FP-CURR-FISC-PERIOD-NAME  PIC  X(09).                         
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(17)    VALUE ALL '-'.        
           05  FILLER                       PIC  X(04)    VALUE SPACES.         
           05  FILLER                       PIC  X(09)    VALUE ALL '-'.        
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(04)    VALUE 'IN: '.         
           05  HD-FP-INV-FISC-PERIOD-NAME   PIC  X(09).                         
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(21)    VALUE ALL '-'.        
           05  FILLER                       PIC  X(02)    VALUE SPACE.          
                                                                                
       01  HD-MI-ML-INV-HEADING.                                                
           05  FILLER                       PIC  X(20)    VALUE SPACE.          
           05  FILLER                       PIC  X(18)    VALUE                 
                                                   'MERCHANDISE LEDGER'.        
           05  FILLER                       PIC  X(23)    VALUE SPACE.          
           05  FILLER                       PIC  X(16)    VALUE                 
                                                     'INVENTORY LEDGER'.        
           05  FILLER                       PIC  X(63)    VALUE SPACE.          
       01  HD-CL-COLUMN-HEADING.                                                
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  HD-CL-EFF-DATE-HD-LIT        PIC  X(08)    VALUE SPACE.          
           05  FILLER                       PIC  X(08)    VALUE SPACE.          
           05  FILLER                       PIC  X(04)    VALUE 'COST'.         
           05  FILLER                       PIC  X(15)    VALUE SPACE.          
           05  FILLER                       PIC  X(06)    VALUE                 
                                                               'RETAIL'.        
           05  FILLER                       PIC  X(15)    VALUE SPACE.          
           05  FILLER                       PIC  X(04)    VALUE 'COST'.         
           05  FILLER                       PIC  X(16)    VALUE SPACE.          
           05  FILLER                       PIC  X(06)    VALUE                 
                                                               'RETAIL'.        
           05  FILLER                       PIC  X(51)    VALUE SPACE.          
                                                                                
       01  HD-TC-TRANS-CDE-HEADING.                                             
           05  HD-TC-TRANS-CDE-DESC         PIC  X(15).                         
                                                                                
       01  DL-DETAIL-LINE.                                                      
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  DL-EFF-DATE.                                                     
               10  DL-EFF-DATE-MM           PIC  X(02).                         
               10  FILLER                   PIC  X        VALUE '/'.            
               10  DL-EFF-DATE-DD           PIC  X(02).                         
               10  FILLER                   PIC  X        VALUE '/'.            
               10  DL-EFF-DATE-YY           PIC  X(02).                         
           05  FILLER                       PIC  X(07)    VALUE SPACE.          
           05  DL-CURR-ML-COST              PIC  ZZZ,ZZZ,ZZZ.99-.               
           05  FILLER                       PIC  X(05)    VALUE SPACE.          
           05  DL-CURR-ML-RETAIL            PIC  ZZZ,ZZZ,ZZZ.99-.               
           05  FILLER                       PIC  X(06)    VALUE SPACE.          
           05  DL-CURR-INV-COST             PIC ZZZ,ZZZ,ZZZ.99-.                
           05  FILLER                       PIC  X(06)    VALUE SPACE.          
           05  DL-CURR-INV-RETAIL           PIC ZZZ,ZZZ,ZZZ.99-.                
           05  FILLER                       PIC  X(06)    VALUE SPACE.          
                                                                                
       01  TL-TC1-TRANS-CDE-TOTAL-LINE-1.                                       
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-TC1-TRANS-CDE-DESC        PIC  X(15).                         
                                                                                
       01  TL-TC2-TRANS-CDE-TOTAL-LINE-2.                                       
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(11)    VALUE 'TOTAL'.        
           05  TL-TC2-CURR-ML-COST          PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-TC2-CURR-ML-RETAIL        PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X(02)    VALUE SPACE.          
           05  TL-TC2-CURR-INV-COST         PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X(02)    VALUE SPACE.          
           05  TL-TC2-CURR-INV-RETAIL       PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X(02)    VALUE SPACE.          
                                                                                
                                                                                
       01  TL-FN1-FINAL-TOTAL-LINE-1.                                           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(05)    VALUE 'FINAL'.        
                                                                                
       01  TL-FN2-FINAL-TOTAL-LINE-2.                                           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(11)    VALUE 'TOTAL'.        
           05  TL-FN2-CURR-ML-COST          PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-FN2-CURR-ML-RETAIL        PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X(02)    VALUE SPACE.          
           05  TL-FN2-CURR-INV-COST         PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X(02)    VALUE SPACE.          
           05  TL-FN2-CURR-INV-RETAIL       PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X(02)    VALUE SPACE.          
                                                                                
       01  ER-END-OF-REPORT-LINE.                                               
           05  FILLER                       PIC  X(21)    VALUE                 
                                                '*** END OF REPORT ***'.        
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * DB2 STORE TABLE DCLGEN.                                                 
      *****************************************************************         
                                                                                
                                                                                
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
           OPEN INPUT  EDIT-SPLIT-FILE                                          
                OUTPUT CYCLE-SPLIT-REPORT.                                      
                                                                                
           PERFORM B100-PROCESS-CONTROL-CARD.                                   
                                                                                
           PERFORM B200-C-CREATE-REPORT.                                        
                                                                                
           CLOSE EDIT-SPLIT-FILE                                                
                 CYCLE-SPLIT-REPORT.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * MAKE SURE THAT THE EDIT-SPLIT FILE CONTAINS A CONTROL CARD AS           
      * THE FIRST RECORD. IF THE CONTROL CARD IS PRESENT, USE ITS               
      * FIELDS TO BUILD SOME OF THE HEADING FIELDS.                             
      ******************************************************************        
       B100-PROCESS-CONTROL-CARD.                                               
                                                                                
           MOVE 'B100-PROCESS-CONTROL-CARD' TO PV-PARAGRAPH-NAME.               
                                                                                
           PERFORM Z100-READ-EDIT-SPLIT-FILE.                                   
                                                                                
           IF SW-END-OF-EDIT-SPLIT-FILE                                         
               MOVE 'NO CONTROL CARD ON EDIT-SPLIT-FILE'                        
                          TO PV-ABEND-MESSAGE                                   
               MOVE SPACE TO EDIT-SPLIT-RECORD                                  
               MOVE +4003 TO ABEND-CODE                                         
               PERFORM Z999-ABEND                                               
           ELSE                                                                 
               IF NOT IN035-RPT1-CNTL-CARD-HDR-REC                              
                   MOVE '1ST RECORD ON EDIT-SPLIT-FILE NOT CONTROL CARD'        
                              TO PV-ABEND-MESSAGE                               
                   MOVE +4003 TO ABEND-CODE                                     
                   PERFORM Z999-ABEND                                           
               ELSE                                                             
                   PERFORM UNTIL IN035-RPT1-DETAIL-REC                          
                              OR SW-END-OF-EDIT-SPLIT-FILE                      
                       MOVE IN035-RPT1-CNTL-CARD-RECORD                         
                                            TO PV-LAST-CNTL-CARD-READ           
                       MOVE IN035-RPT1-EDIT-DTE                                 
                                            TO PV-ISO-FMT-DATE                  
                       MOVE PV-ISO-FMT-MM   TO HD-ED-DATE-MM                    
                       MOVE PV-ISO-FMT-DD   TO HD-ED-DATE-DD                    
                       MOVE PV-ISO-FMT-YY   TO HD-ED-DATE-YY                    
                       MOVE IN035-RPT1-CURR-FSCL-YR-PER                         
                                            TO SA-CURR-FSCL-YR-PER              
                       MOVE IN035-RPT1-CURR-FSCL-PER-CDE                        
                                            TO FPN-SUB-X                        
                       MOVE FPN-FSCL-PER-NAME (FPN-SUB)                         
                                         TO HD-FP-CURR-FISC-PERIOD-NAME         
                       IF IN035-RPT1-MODE-INV-ONLY                              
                           MOVE PC-INVENTORY-ONLY-MODE                          
                                         TO HD-CP-PROCESSING-MODE               
                                            HD-FT-PROCESSING-MODE               
                       END-IF                                                   
                       PERFORM Z100-READ-EDIT-SPLIT-FILE                        
                   END-PERFORM                                                  
                   DISPLAY '** INKRP056 INPUT CONTROL CARD ='                   
                            PV-LAST-CNTL-CARD-READ                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * CREATE THE CYCLE EDIT SPLIT REPORT BY FINANCIAL LIABILITY DATE          
      * WITHIN TRANSACTION CODE. THIS REPORT IS CREATED                         
      * FROM THE INPUT FILE.                                                    
      ******************************************************************        
       B200-C-CREATE-REPORT.                                                    
                                                                                
           PERFORM C200-C-INITIALIZATION.                                       
           PERFORM Z300-C-PRINT-STD-RPT-HEADING.                                
                                                                                
           IF SW-NOT-END-OF-EDIT-SPLIT-FILE                                     
               MOVE IN035-RPT1-FIN-LIAB-DTE  TO SA-PREV-FIN-LIAB-DTE            
               MOVE IN035-RPT1-TRANS-CDE     TO SA-PREV-TRANS-CDE               
               MOVE IN035-RPT1-SYST-ORIG-CDE TO SA-PREV-SYST-ORIG-CDE           
               MOVE IN035-INV-ID             TO SA-PREV-CYCLE-NBR               
                                                HD-CYCLE-NO                     
               IF  IN035-INV-ID = ZEROES                                        
                   MOVE PC-NON-CYCLE-NAME    TO HD-CYCLE-NAME                   
               ELSE                                                             
                   PERFORM Z600-READ-TINCNTL                                    
                   MOVE INCNTL-INV-NM-TEXT (1:INCNTL-INV-NM-LEN)                
                                             TO HD-CYCLE-NAME                   
               END-IF                                                           
                                                                                
               PERFORM Z360-C-PRINT-CORP-HEADING                                
               PERFORM Z400-C-PRINT-CONSTANT-HEADINGS                           
               PERFORM Z500-PRINT-TRANS-CDE-HEADING                             
                                                                                
               PERFORM C300-C-PROCESS-EDIT-SPLIT-FILE                           
                   UNTIL SW-END-OF-EDIT-SPLIT-FILE                              
                                                                                
               PERFORM D200-C-FIN-LIAB-BREAK                                    
               PERFORM D300-C-TRANS-CDE-BREAK                                   
               PERFORM D400-C-CYCLE-NBR-BREAK                                   
               MOVE '99999'              TO HD-CYCLE-NO                         
               MOVE PC-FNL-CYCLE-NAME    TO HD-CYCLE-NAME                       
               PERFORM C400-C-PRINT-FINAL-TOTALS                                
           ELSE                                                                 
               SET SW-EMPTY-EDIT-SPLIT-FILE TO TRUE                             
           END-IF.                                                              
                                                                                
           MOVE ER-END-OF-REPORT-LINE TO CYCLE-SPLIT-REPORT-LINE.               
           MOVE 3 TO PV-ADVANCE.                                                
           PERFORM Z200-C-WRITE-REPORT-LINE.                                    
                                                                                
                                                                                
      ******************************************************************        
      * INITIALIZE FIELDS AS NECESSARY FOR THE CYCLE SPLIT REPORT.     *        
      ******************************************************************        
       C200-C-INITIALIZATION.                                                   
                                                                                
           INITIALIZE PV-PAGE-COUNT                                             
                      TOT-TOTAL-FIELDS                                          
                      PV-PROGRAM-VARIABLES                                      
                      DCLTINCNTL.                                               
                                                                                
           SET SW-NOT-EMPTY-EDIT-SPLIT-FILE                                     
               SW-NOT-CYCLE-BREAK                                               
               SW-NOT-TRANS-CDE-BREAK TO TRUE.                                  
                                                                                
           ACCEPT PV-YYMMDD-DATE FROM DATE.                                     
           MOVE   PV-YYMMDD-YY TO DP133O-RUN-YEAR.                              
           MOVE   PV-YYMMDD-MM TO DP133O-RUN-MONTH.                             
           MOVE   PV-YYMMDD-DD TO DP133O-RUN-DAY.                               
           ACCEPT PV-TIME-HHMMSS FROM TIME.                                     
           MOVE PV-TIME-HHMMSS-HH   TO DP133O-RUN-HOUR.                         
           MOVE PV-TIME-HHMMSS-MM   TO DP133O-RUN-MINUTE.                       
           MOVE 'INKRP056'          TO DP133O-PROGRAM-NAME.                     
           MOVE 1 TO DP133O-REPORT-NUMBER.                                      
                                                                                
           MOVE PC-EFF-DATE-HD-LIT   TO HD-CL-EFF-DATE-HD-LIT.                  
                                                                                
                                                                                
      ******************************************************************        
      * READ AND PROCESS THE INPUT FILE. BEFORE EACH RECORD IS         *        
      * PROCESSED, CHECK FOR CONTROL BREAKS, AND DO ANY NECESSARY      *        
      * CONTROL BREAK PROCESSING.                                      *        
      ******************************************************************        
       C300-C-PROCESS-EDIT-SPLIT-FILE.                                          
                                                                                
                                                                                
           IF IN035-INV-ID NOT = SA-PREV-CYCLE-NBR                              
               PERFORM D200-C-FIN-LIAB-BREAK                                    
               PERFORM D300-C-TRANS-CDE-BREAK                                   
               PERFORM D400-C-CYCLE-NBR-BREAK                                   
               SET SW-CYCLE-BREAK TO TRUE                                       
           ELSE                                                                 
               IF IN035-RPT1-TRANS-CDE NOT = SA-PREV-TRANS-CDE                  
               OR IN035-RPT1-SYST-ORIG-CDE NOT =                                
                                            SA-PREV-SYST-ORIG-CDE               
                   PERFORM D200-C-FIN-LIAB-BREAK                                
                   PERFORM D300-C-TRANS-CDE-BREAK                               
                   SET SW-TRANS-CDE-BREAK TO TRUE                               
               ELSE                                                             
                   IF IN035-RPT1-FIN-LIAB-DTE NOT =                             
                                             SA-PREV-FIN-LIAB-DTE               
                       PERFORM D200-C-FIN-LIAB-BREAK                            
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM D500-C-PROCESS-EDIT-SPLIT-REC.                               
           PERFORM Z100-READ-EDIT-SPLIT-FILE.                                   
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL PAGE FOR THE CYCLE SPLIT REPORT.                  
      ******************************************************************        
       C400-C-PRINT-FINAL-TOTALS.                                               
                                                                                
           PERFORM Z300-C-PRINT-STD-RPT-HEADING.                                
           PERFORM Z375-C-PRINT-FINAL-TOT-HEADING.                              
           PERFORM Z400-C-PRINT-CONSTANT-HEADINGS.                              
                                                                                
           SET TCT-IDX TO 1.                                                    
           SET SW-NOT-TRANS-CDE-DISP    TO TRUE.                                
           INITIALIZE TOT-FNL-FINAL-TOTALS1.                                    
           PERFORM C405-C-PROCESS-FNL-TRANS-CDES                                
               VARYING FNL-TP-IDX FROM 1 BY 1                                   
               UNTIL TOT-FIN-LIAB-DTE-TP (FNL-TP-IDX) = SPACES.                 
           PERFORM C445-C-PROCESS-FNL-TRANS-TOTAL.                              
                                                                                
           SET TCT-IDX TO 2.                                                    
           SET SW-NOT-TRANS-CDE-DISP    TO TRUE.                                
           INITIALIZE TOT-FNL-FINAL-TOTALS1.                                    
           PERFORM C440-C-PROCESS-FNL-TRANS-CDES                                
               VARYING FNL-PR-IDX FROM 1 BY 1                                   
               UNTIL TOT-FIN-LIAB-DTE-PR (FNL-PR-IDX) = SPACES.                 
           PERFORM C445-C-PROCESS-FNL-TRANS-TOTAL.                              
                                                                                
           SET TCT-IDX TO 3.                                                    
           SET SW-NOT-TRANS-CDE-DISP    TO TRUE.                                
           INITIALIZE TOT-FNL-FINAL-TOTALS1.                                    
           PERFORM C410-C-PROCESS-FNL-TRANS-CDES                                
               VARYING FNL-TS-IDX FROM 1 BY 1                                   
               UNTIL TOT-FIN-LIAB-DTE-TS (FNL-TS-IDX) = SPACES.                 
           PERFORM C445-C-PROCESS-FNL-TRANS-TOTAL.                              
                                                                                
           SET TCT-IDX TO 4.                                                    
           SET SW-NOT-TRANS-CDE-DISP    TO TRUE.                                
           INITIALIZE TOT-FNL-FINAL-TOTALS1.                                    
           PERFORM C415-C-PROCESS-FNL-TRANS-CDES                                
               VARYING FNL-TF-IDX FROM 1 BY 1                                   
               UNTIL TOT-FIN-LIAB-DTE-TF (FNL-TF-IDX) = SPACES.                 
           PERFORM C445-C-PROCESS-FNL-TRANS-TOTAL.                              
                                                                                
           SET TCT-IDX TO 5.                                                    
           SET SW-NOT-TRANS-CDE-DISP    TO TRUE.                                
           INITIALIZE TOT-FNL-FINAL-TOTALS1.                                    
           PERFORM C420-C-PROCESS-FNL-TRANS-CDES                                
               VARYING FNL-TT-IDX FROM 1 BY 1                                   
               UNTIL TOT-FIN-LIAB-DTE-TT (FNL-TT-IDX) = SPACES.                 
           PERFORM C445-C-PROCESS-FNL-TRANS-TOTAL.                              
                                                                                
           SET TCT-IDX TO 6.                                                    
           SET SW-NOT-TRANS-CDE-DISP    TO TRUE.                                
           INITIALIZE TOT-FNL-FINAL-TOTALS1.                                    
           PERFORM C425-C-PROCESS-FNL-TRANS-CDES                                
               VARYING FNL-TM-IDX FROM 1 BY 1                                   
               UNTIL TOT-FIN-LIAB-DTE-TM (FNL-TM-IDX) = SPACES.                 
           PERFORM C445-C-PROCESS-FNL-TRANS-TOTAL.                              
                                                                                
           SET TCT-IDX TO 7.                                                    
           SET SW-NOT-TRANS-CDE-DISP    TO TRUE.                                
           INITIALIZE TOT-FNL-FINAL-TOTALS1.                                    
           PERFORM C430-C-PROCESS-FNL-TRANS-CDES                                
               VARYING FNL-TU-IDX FROM 1 BY 1                                   
               UNTIL TOT-FIN-LIAB-DTE-TU (FNL-TU-IDX) = SPACES.                 
           PERFORM C445-C-PROCESS-FNL-TRANS-TOTAL.                              
                                                                                
           SET TCT-IDX TO 8.                                                    
           SET SW-NOT-TRANS-CDE-DISP    TO TRUE.                                
           INITIALIZE TOT-FNL-FINAL-TOTALS1.                                    
           PERFORM C435-C-PROCESS-FNL-TRANS-CDES                                
               VARYING FNL-TA-IDX FROM 1 BY 1                                   
               UNTIL TOT-FIN-LIAB-DTE-TA (FNL-TA-IDX) = SPACES.                 
           PERFORM C445-C-PROCESS-FNL-TRANS-TOTAL.                              
                                                                                
           MOVE TL-FN1-FINAL-TOTAL-LINE-1 TO CYCLE-SPLIT-REPORT-LINE.           
           MOVE 2 TO PV-ADVANCE.                                                
           PERFORM Z200-C-WRITE-REPORT-LINE.                                    
           MOVE TOT-FNL-CURR-ML-COST    TO TL-FN2-CURR-ML-COST.                 
           MOVE TOT-FNL-CURR-ML-RETAIL  TO TL-FN2-CURR-ML-RETAIL.               
           MOVE TOT-FNL-CURR-INV-COST   TO TL-FN2-CURR-INV-COST.                
           MOVE TOT-FNL-CURR-INV-RETAIL TO TL-FN2-CURR-INV-RETAIL.              
                                                                                
           MOVE TL-FN2-FINAL-TOTAL-LINE-2 TO CYCLE-SPLIT-REPORT-LINE.           
           MOVE 1 TO PV-ADVANCE.                                                
           PERFORM Z200-C-WRITE-REPORT-LINE.                                    
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL PAGE FOR THE CORPORATE REPORT.                    
      ******************************************************************        
       C405-C-PROCESS-FNL-TRANS-CDES.                                           
           IF  TOT-FIN-LIAB-DTE-TP (FNL-TP-IDX) NOT = SPACES                    
               PERFORM D600-C-PROCESS-TANS-CDE-DESC                             
               MOVE TOT-FIN-LIAB-DTE-TP (FNL-TP-IDX)                            
                                                  TO PV-ISO-FMT-DATE            
               MOVE PV-ISO-FMT-MM                 TO DL-EFF-DATE-MM             
               MOVE PV-ISO-FMT-DD                 TO DL-EFF-DATE-DD             
               MOVE PV-ISO-FMT-YY                 TO DL-EFF-DATE-YY             
               MOVE TOT-FNL-CURR-ML-COST-TP (FNL-TP-IDX)                        
                 TO  DL-CURR-ML-COST                                            
               MOVE TOT-FNL-CURR-ML-RETAIL-TP (FNL-TP-IDX)                      
                 TO  DL-CURR-ML-RETAIL                                          
               MOVE TOT-FNL-CURR-INV-COST-TP (FNL-TP-IDX)                       
                 TO  DL-CURR-INV-COST                                           
               MOVE TOT-FNL-CURR-INV-RETAIL-TP (FNL-TP-IDX)                     
                 TO  DL-CURR-INV-RETAIL                                         
               ADD TOT-FNL-CURR-ML-COST-TP (FNL-TP-IDX)                         
                 TO TOT-FNL-CURR-ML-COST1                                       
               ADD TOT-FNL-CURR-ML-RETAIL-TP (FNL-TP-IDX)                       
                 TO TOT-FNL-CURR-ML-RETAIL1                                     
               ADD TOT-FNL-CURR-INV-COST-TP (FNL-TP-IDX)                        
                 TO TOT-FNL-CURR-INV-COST1                                      
               ADD TOT-FNL-CURR-INV-RETAIL-TP (FNL-TP-IDX)                      
                 TO TOT-FNL-CURR-INV-RETAIL1                                    
               PERFORM D700-C-PROCESS-DETAIL-LINE                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL PAGE FOR THE CORPORATE REPORT.                    
      ******************************************************************        
       C410-C-PROCESS-FNL-TRANS-CDES.                                           
           IF TOT-FIN-LIAB-DTE-TS (FNL-TS-IDX) NOT = SPACES                     
               PERFORM D600-C-PROCESS-TANS-CDE-DESC                             
               MOVE TOT-FIN-LIAB-DTE-TS (FNL-TS-IDX)                            
                                                  TO PV-ISO-FMT-DATE            
               MOVE PV-ISO-FMT-MM                 TO DL-EFF-DATE-MM             
               MOVE PV-ISO-FMT-DD                 TO DL-EFF-DATE-DD             
               MOVE PV-ISO-FMT-YY                 TO DL-EFF-DATE-YY             
               MOVE TOT-FNL-CURR-ML-COST-TS (FNL-TS-IDX)                        
                 TO  DL-CURR-ML-COST                                            
               MOVE TOT-FNL-CURR-ML-RETAIL-TS (FNL-TS-IDX)                      
                 TO  DL-CURR-ML-RETAIL                                          
               MOVE TOT-FNL-CURR-INV-COST-TS (FNL-TS-IDX)                       
                 TO  DL-CURR-INV-COST                                           
               MOVE TOT-FNL-CURR-INV-RETAIL-TS (FNL-TS-IDX)                     
                 TO  DL-CURR-INV-RETAIL                                         
               ADD TOT-FNL-CURR-ML-COST-TS (FNL-TS-IDX)                         
                 TO TOT-FNL-CURR-ML-COST1                                       
               ADD TOT-FNL-CURR-ML-RETAIL-TS (FNL-TS-IDX)                       
                 TO TOT-FNL-CURR-ML-RETAIL1                                     
               ADD TOT-FNL-CURR-INV-COST-TS (FNL-TS-IDX)                        
                 TO TOT-FNL-CURR-INV-COST1                                      
               ADD TOT-FNL-CURR-INV-RETAIL-TS (FNL-TS-IDX)                      
                 TO TOT-FNL-CURR-INV-RETAIL1                                    
               PERFORM D700-C-PROCESS-DETAIL-LINE                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL PAGE FOR THE CORPORATE REPORT.                    
      ******************************************************************        
       C415-C-PROCESS-FNL-TRANS-CDES.                                           
           IF  TOT-FIN-LIAB-DTE-TF (FNL-TF-IDX) NOT = SPACES                    
               PERFORM D600-C-PROCESS-TANS-CDE-DESC                             
               MOVE TOT-FIN-LIAB-DTE-TF (FNL-TF-IDX)                            
                                                  TO PV-ISO-FMT-DATE            
               MOVE PV-ISO-FMT-MM                 TO DL-EFF-DATE-MM             
               MOVE PV-ISO-FMT-DD                 TO DL-EFF-DATE-DD             
               MOVE PV-ISO-FMT-YY                 TO DL-EFF-DATE-YY             
               MOVE TOT-FNL-CURR-ML-COST-TF (FNL-TF-IDX)                        
                 TO  DL-CURR-ML-COST                                            
               MOVE TOT-FNL-CURR-ML-RETAIL-TF (FNL-TF-IDX)                      
                 TO  DL-CURR-ML-RETAIL                                          
               MOVE TOT-FNL-CURR-INV-COST-TF (FNL-TF-IDX)                       
                 TO  DL-CURR-INV-COST                                           
               MOVE TOT-FNL-CURR-INV-RETAIL-TF (FNL-TF-IDX)                     
                 TO  DL-CURR-INV-RETAIL                                         
               ADD TOT-FNL-CURR-ML-COST-TF (FNL-TF-IDX)                         
                 TO TOT-FNL-CURR-ML-COST1                                       
               ADD TOT-FNL-CURR-ML-RETAIL-TF (FNL-TF-IDX)                       
                 TO TOT-FNL-CURR-ML-RETAIL1                                     
               ADD TOT-FNL-CURR-INV-COST-TF (FNL-TF-IDX)                        
                 TO TOT-FNL-CURR-INV-COST1                                      
               ADD TOT-FNL-CURR-INV-RETAIL-TF (FNL-TF-IDX)                      
                 TO TOT-FNL-CURR-INV-RETAIL1                                    
               PERFORM D700-C-PROCESS-DETAIL-LINE                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL PAGE FOR THE CORPORATE REPORT.                    
      ******************************************************************        
       C420-C-PROCESS-FNL-TRANS-CDES.                                           
           IF  TOT-FIN-LIAB-DTE-TT (FNL-TT-IDX) NOT = SPACES                    
               PERFORM D600-C-PROCESS-TANS-CDE-DESC                             
               MOVE TOT-FIN-LIAB-DTE-TT (FNL-TT-IDX)                            
                                                  TO PV-ISO-FMT-DATE            
               MOVE PV-ISO-FMT-MM                 TO DL-EFF-DATE-MM             
               MOVE PV-ISO-FMT-DD                 TO DL-EFF-DATE-DD             
               MOVE PV-ISO-FMT-YY                 TO DL-EFF-DATE-YY             
               MOVE TOT-FNL-CURR-ML-COST-TT (FNL-TT-IDX)                        
                 TO  DL-CURR-ML-COST                                            
               MOVE TOT-FNL-CURR-ML-RETAIL-TT (FNL-TT-IDX)                      
                 TO  DL-CURR-ML-RETAIL                                          
               MOVE TOT-FNL-CURR-INV-COST-TT (FNL-TT-IDX)                       
                 TO  DL-CURR-INV-COST                                           
               MOVE TOT-FNL-CURR-INV-RETAIL-TT (FNL-TT-IDX)                     
                 TO  DL-CURR-INV-RETAIL                                         
               ADD TOT-FNL-CURR-ML-COST-TT (FNL-TT-IDX)                         
                 TO TOT-FNL-CURR-ML-COST1                                       
               ADD TOT-FNL-CURR-ML-RETAIL-TT (FNL-TT-IDX)                       
                 TO TOT-FNL-CURR-ML-RETAIL1                                     
               ADD TOT-FNL-CURR-INV-COST-TT (FNL-TT-IDX)                        
                 TO TOT-FNL-CURR-INV-COST1                                      
               ADD TOT-FNL-CURR-INV-RETAIL-TT (FNL-TT-IDX)                      
                 TO TOT-FNL-CURR-INV-RETAIL1                                    
               PERFORM D700-C-PROCESS-DETAIL-LINE                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL PAGE FOR THE CORPORATE REPORT.                    
      ******************************************************************        
       C425-C-PROCESS-FNL-TRANS-CDES.                                           
           IF  TOT-FIN-LIAB-DTE-TM (FNL-TM-IDX) NOT = SPACES                    
               PERFORM D600-C-PROCESS-TANS-CDE-DESC                             
               MOVE TOT-FIN-LIAB-DTE-TM (FNL-TM-IDX)                            
                                                  TO PV-ISO-FMT-DATE            
               MOVE PV-ISO-FMT-MM                 TO DL-EFF-DATE-MM             
               MOVE PV-ISO-FMT-DD                 TO DL-EFF-DATE-DD             
               MOVE PV-ISO-FMT-YY                 TO DL-EFF-DATE-YY             
               MOVE TOT-FNL-CURR-ML-COST-TM (FNL-TM-IDX)                        
                 TO  DL-CURR-ML-COST                                            
               MOVE TOT-FNL-CURR-ML-RETAIL-TM (FNL-TM-IDX)                      
                 TO  DL-CURR-ML-RETAIL                                          
               MOVE TOT-FNL-CURR-INV-COST-TM (FNL-TM-IDX)                       
                 TO  DL-CURR-INV-COST                                           
               MOVE TOT-FNL-CURR-INV-RETAIL-TM (FNL-TM-IDX)                     
                 TO  DL-CURR-INV-RETAIL                                         
               ADD TOT-FNL-CURR-ML-COST-TM (FNL-TM-IDX)                         
                 TO TOT-FNL-CURR-ML-COST1                                       
               ADD TOT-FNL-CURR-ML-RETAIL-TM (FNL-TM-IDX)                       
                 TO TOT-FNL-CURR-ML-RETAIL1                                     
               ADD TOT-FNL-CURR-INV-COST-TM (FNL-TM-IDX)                        
                 TO TOT-FNL-CURR-INV-COST1                                      
               ADD TOT-FNL-CURR-INV-RETAIL-TM (FNL-TM-IDX)                      
                 TO TOT-FNL-CURR-INV-RETAIL1                                    
               PERFORM D700-C-PROCESS-DETAIL-LINE                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL PAGE FOR THE CORPORATE REPORT.                    
      ******************************************************************        
       C430-C-PROCESS-FNL-TRANS-CDES.                                           
           IF  TOT-FIN-LIAB-DTE-TU (FNL-TU-IDX) NOT = SPACES                    
               PERFORM D600-C-PROCESS-TANS-CDE-DESC                             
               MOVE TOT-FIN-LIAB-DTE-TU (FNL-TU-IDX)                            
                                                  TO PV-ISO-FMT-DATE            
               MOVE PV-ISO-FMT-MM                 TO DL-EFF-DATE-MM             
               MOVE PV-ISO-FMT-DD                 TO DL-EFF-DATE-DD             
               MOVE PV-ISO-FMT-YY                 TO DL-EFF-DATE-YY             
               MOVE TOT-FNL-CURR-ML-COST-TU (FNL-TU-IDX)                        
                 TO  DL-CURR-ML-COST                                            
               MOVE TOT-FNL-CURR-ML-RETAIL-TU (FNL-TU-IDX)                      
                 TO  DL-CURR-ML-RETAIL                                          
               MOVE TOT-FNL-CURR-INV-COST-TU (FNL-TU-IDX)                       
                 TO  DL-CURR-INV-COST                                           
               MOVE TOT-FNL-CURR-INV-RETAIL-TU (FNL-TU-IDX)                     
                 TO  DL-CURR-INV-RETAIL                                         
               ADD TOT-FNL-CURR-ML-COST-TU (FNL-TU-IDX)                         
                 TO TOT-FNL-CURR-ML-COST1                                       
               ADD TOT-FNL-CURR-ML-RETAIL-TU (FNL-TU-IDX)                       
                 TO TOT-FNL-CURR-ML-RETAIL1                                     
               ADD TOT-FNL-CURR-INV-COST-TU (FNL-TU-IDX)                        
                 TO TOT-FNL-CURR-INV-COST1                                      
               ADD TOT-FNL-CURR-INV-RETAIL-TU (FNL-TU-IDX)                      
                 TO TOT-FNL-CURR-INV-RETAIL1                                    
               PERFORM D700-C-PROCESS-DETAIL-LINE                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL PAGE FOR THE CORPORATE REPORT.                    
      ******************************************************************        
       C435-C-PROCESS-FNL-TRANS-CDES.                                           
           IF  TOT-FIN-LIAB-DTE-TA (FNL-TA-IDX) NOT = SPACES                    
               PERFORM D600-C-PROCESS-TANS-CDE-DESC                             
               MOVE TOT-FIN-LIAB-DTE-TA (FNL-TA-IDX)                            
                                                  TO PV-ISO-FMT-DATE            
               MOVE PV-ISO-FMT-MM                 TO DL-EFF-DATE-MM             
               MOVE PV-ISO-FMT-DD                 TO DL-EFF-DATE-DD             
               MOVE PV-ISO-FMT-YY                 TO DL-EFF-DATE-YY             
               MOVE TOT-FNL-CURR-ML-COST-TA (FNL-TA-IDX)                        
                 TO  DL-CURR-ML-COST                                            
               MOVE TOT-FNL-CURR-ML-RETAIL-TA (FNL-TA-IDX)                      
                 TO  DL-CURR-ML-RETAIL                                          
               MOVE TOT-FNL-CURR-INV-COST-TA (FNL-TA-IDX)                       
                 TO  DL-CURR-INV-COST                                           
               MOVE TOT-FNL-CURR-INV-RETAIL-TA (FNL-TA-IDX)                     
                 TO  DL-CURR-INV-RETAIL                                         
               ADD TOT-FNL-CURR-ML-COST-TA (FNL-TA-IDX)                         
                 TO TOT-FNL-CURR-ML-COST1                                       
               ADD TOT-FNL-CURR-ML-RETAIL-TA (FNL-TA-IDX)                       
                 TO TOT-FNL-CURR-ML-RETAIL1                                     
               ADD TOT-FNL-CURR-INV-COST-TA (FNL-TA-IDX)                        
                 TO TOT-FNL-CURR-INV-COST1                                      
               ADD TOT-FNL-CURR-INV-RETAIL-TA (FNL-TA-IDX)                      
                 TO TOT-FNL-CURR-INV-RETAIL1                                    
               PERFORM D700-C-PROCESS-DETAIL-LINE                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL PAGE FOR THE CORPORATE REPORT.                    
      ******************************************************************        
       C440-C-PROCESS-FNL-TRANS-CDES.                                           
           IF  TOT-FIN-LIAB-DTE-PR (FNL-PR-IDX) NOT = SPACES                    
               PERFORM D600-C-PROCESS-TANS-CDE-DESC                             
               MOVE TOT-FIN-LIAB-DTE-PR (FNL-PR-IDX)                            
                                                  TO PV-ISO-FMT-DATE            
               MOVE PV-ISO-FMT-MM                 TO DL-EFF-DATE-MM             
               MOVE PV-ISO-FMT-DD                 TO DL-EFF-DATE-DD             
               MOVE PV-ISO-FMT-YY                 TO DL-EFF-DATE-YY             
               MOVE TOT-FNL-CURR-ML-COST-PR (FNL-PR-IDX)                        
                 TO  DL-CURR-ML-COST                                            
               MOVE TOT-FNL-CURR-ML-RETAIL-PR (FNL-PR-IDX)                      
                 TO  DL-CURR-ML-RETAIL                                          
               MOVE TOT-FNL-CURR-INV-COST-PR (FNL-PR-IDX)                       
                 TO  DL-CURR-INV-COST                                           
               MOVE TOT-FNL-CURR-INV-RETAIL-PR (FNL-PR-IDX)                     
                 TO  DL-CURR-INV-RETAIL                                         
               ADD TOT-FNL-CURR-ML-COST-PR (FNL-PR-IDX)                         
                 TO TOT-FNL-CURR-ML-COST1                                       
               ADD TOT-FNL-CURR-ML-RETAIL-PR (FNL-PR-IDX)                       
                 TO TOT-FNL-CURR-ML-RETAIL1                                     
               ADD TOT-FNL-CURR-INV-COST-PR (FNL-PR-IDX)                        
                 TO TOT-FNL-CURR-INV-COST1                                      
               ADD TOT-FNL-CURR-INV-RETAIL-PR (FNL-PR-IDX)                      
                 TO TOT-FNL-CURR-INV-RETAIL1                                    
               PERFORM D700-C-PROCESS-DETAIL-LINE                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL PAGE FOR THE CORPORATE REPORT.                    
      ******************************************************************        
       C445-C-PROCESS-FNL-TRANS-TOTAL.                                          
           IF  SW-TRANS-CDE-DISP                                                
               MOVE TL-TC1-TRANS-CDE-TOTAL-LINE-1                               
                                            TO CYCLE-SPLIT-REPORT-LINE          
               MOVE 2 TO PV-ADVANCE                                             
               PERFORM Z200-C-WRITE-REPORT-LINE                                 
               MOVE TOT-FNL-CURR-ML-COST1    TO TL-TC2-CURR-ML-COST             
               MOVE TOT-FNL-CURR-ML-RETAIL1  TO TL-TC2-CURR-ML-RETAIL           
               MOVE TOT-FNL-CURR-INV-COST1   TO TL-TC2-CURR-INV-COST            
               MOVE TOT-FNL-CURR-INV-RETAIL1 TO TL-TC2-CURR-INV-RETAIL          
                                                                                
               MOVE TL-TC2-TRANS-CDE-TOTAL-LINE-2                               
                                            TO CYCLE-SPLIT-REPORT-LINE          
               MOVE 1 TO PV-ADVANCE                                             
               PERFORM Z200-C-WRITE-REPORT-LINE                                 
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * CHECK THE TOTALS FOR THE FINANCIAL LIABILITY DATE. IF THEY ARE          
      * ALL ZERO, SKIP THE BREAK PROCESSING SO THAT THE DATE WILL NOT           
      * BE REPORTED. OTHERWISE, CONTINUE THE BREAK PROCESSING.                  
      ******************************************************************        
       D200-C-FIN-LIAB-BREAK.                                                   
                                                                                
           IF  TOT-FLB-CURR-ML-COST     = ZERO                                  
           AND TOT-FLB-CURR-ML-RETAIL   = ZERO                                  
           AND TOT-FLB-CURR-INV-COST    = ZERO                                  
           AND TOT-FLB-CURR-INV-RETAIL  = ZERO                                  
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM E100-C-CONTINUE-FIN-LIAB-BREAK                           
           END-IF.                                                              
                                                                                
           MOVE IN035-RPT1-FIN-LIAB-DTE TO SA-PREV-FIN-LIAB-DTE.                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS A BREAK ON TRANS-CDE: FORMAT AND PRINT THE TOTAL                
      * LINE, ADD THE TRANS-CDE TOTALS TO THE NEXT HIGHER LEVEL TOTALS,         
      * AND INITIALIZE THE TRANS-CDE TOTALS.                                    
      ******************************************************************        
       D300-C-TRANS-CDE-BREAK.                                                  
                                                                                
           MOVE TL-TC1-TRANS-CDE-TOTAL-LINE-1                                   
                                        TO CYCLE-SPLIT-REPORT-LINE.             
           MOVE 2 TO PV-ADVANCE.                                                
           PERFORM Z200-C-WRITE-REPORT-LINE.                                    
                                                                                
           MOVE TOT-TCD-CURR-ML-COST    TO TL-TC2-CURR-ML-COST.                 
           MOVE TOT-TCD-CURR-ML-RETAIL  TO TL-TC2-CURR-ML-RETAIL.               
           MOVE TOT-TCD-CURR-INV-COST   TO TL-TC2-CURR-INV-COST.                
           MOVE TOT-TCD-CURR-INV-RETAIL TO TL-TC2-CURR-INV-RETAIL.              
                                                                                
           MOVE TL-TC2-TRANS-CDE-TOTAL-LINE-2                                   
                                        TO CYCLE-SPLIT-REPORT-LINE.             
           MOVE 1 TO PV-ADVANCE.                                                
           PERFORM Z200-C-WRITE-REPORT-LINE.                                    
                                                                                
           IF SA-PREV-TRANS-PURCHASE                                            
           OR SA-PREV-TRANS-TRANSFER-TO                                         
           OR SA-PREV-TRANS-MARKUP                                              
           OR SA-PREV-TRANS-ADJUSTMENT                                          
               ADD TOT-TCD-CURR-ML-COST    TO TOT-CYC-CURR-ML-COST              
               ADD TOT-TCD-CURR-ML-RETAIL  TO TOT-CYC-CURR-ML-RETAIL            
               ADD TOT-TCD-CURR-INV-COST   TO TOT-CYC-CURR-INV-COST             
               ADD TOT-TCD-CURR-INV-RETAIL TO TOT-CYC-CURR-INV-RETAIL           
           ELSE                                                                 
               SUBTRACT TOT-TCD-CURR-ML-COST                                    
                                            FROM TOT-CYC-CURR-ML-COST           
               SUBTRACT TOT-TCD-CURR-ML-RETAIL                                  
                                            FROM TOT-CYC-CURR-ML-RETAIL         
               SUBTRACT TOT-TCD-CURR-INV-COST                                   
                                            FROM TOT-CYC-CURR-INV-COST          
               SUBTRACT TOT-TCD-CURR-INV-RETAIL                                 
                                            FROM TOT-CYC-CURR-INV-RETAIL        
           END-IF.                                                              
                                                                                
                                                                                
           INITIALIZE TOT-TCD-TRANS-CDE-TOTALS.                                 
           MOVE IN035-RPT1-TRANS-CDE    TO SA-PREV-TRANS-CDE.                   
           MOVE IN035-RPT1-SYST-ORIG-CDE TO SA-PREV-SYST-ORIG-CDE.              
                                                                                
      ******************************************************************        
      * PROCESS A BREAK ON CYCLE-NBR: FORMAT AND PRINT THE TOTAL                
      * LINE, ADD THE CYCLE TOTALS TO THE NEXT HIGHER LEVEL TOTALS,             
      * AND INITIALIZE THE CYCLE TOTALS.                                        
      ******************************************************************        
       D400-C-CYCLE-NBR-BREAK.                                                  
                                                                                
           PERFORM Z600-READ-TINCNTL                                            
           MOVE INCNTL-INV-NM-TEXT (1:INCNTL-INV-NM-LEN)                        
                                       TO HD-CYCLE-NAME                         
           ADD TOT-CYC-CURR-ML-COST    TO TOT-FNL-CURR-ML-COST.                 
           ADD TOT-CYC-CURR-ML-RETAIL  TO TOT-FNL-CURR-ML-RETAIL.               
           ADD TOT-CYC-CURR-INV-COST   TO TOT-FNL-CURR-INV-COST.                
           ADD TOT-CYC-CURR-INV-RETAIL TO TOT-FNL-CURR-INV-RETAIL.              
                                                                                
           INITIALIZE TOT-CYC-CYCLE-TOTALS.                                     
           MOVE IN035-INV-ID           TO SA-PREV-CYCLE-NBR                     
                                          HD-CYCLE-NO.                          
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS EACH INPUT RECORD: PRINT HEADINGS AS NECESSARY DUE TO           
      * CONTROL BREAKS; IF THE FINANCIAL LIABILITY DATE HAS CHANGED,            
      *  ADD THE COST AND RETAIL AMOUNTS TO THE LOWEST LEVEL TOTALS.            
      ******************************************************************        
       D500-C-PROCESS-EDIT-SPLIT-REC.                                           
                                                                                
           IF SW-CYCLE-BREAK                                                    
               PERFORM Z300-C-PRINT-STD-RPT-HEADING                             
               PERFORM Z360-C-PRINT-CORP-HEADING                                
               PERFORM Z400-C-PRINT-CONSTANT-HEADINGS                           
               PERFORM Z500-PRINT-TRANS-CDE-HEADING                             
               SET SW-NOT-CYCLE-BREAK TO TRUE                                   
           ELSE                                                                 
               IF SW-TRANS-CDE-BREAK                                            
                   PERFORM Z500-PRINT-TRANS-CDE-HEADING                         
                   SET SW-NOT-TRANS-CDE-BREAK TO TRUE                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF IN035-RPT1-INCLUDE-IN-ML                                          
               ADD IN035-RPT1-EXTD-COST-AMT                                     
                                          TO TOT-FLB-CURR-ML-COST               
               ADD IN035-RPT1-EXTD-RTL-AMT                                      
                                          TO TOT-FLB-CURR-ML-RETAIL             
           END-IF.                                                              
                                                                                
           IF IN035-RPT1-INCLUDE-IN-INV                                         
              AND IN035-INV-ID NOT = ZEROES                                     
               ADD IN035-RPT1-EXTD-COST-AMT                                     
                                          TO TOT-FLB-CURR-INV-COST              
               ADD IN035-RPT1-EXTD-RTL-AMT                                      
                                          TO TOT-FLB-CURR-INV-RETAIL            
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS TO OBTAIN TRANSACTION CODE DESCRIPTION.                         
      ******************************************************************        
       D600-C-PROCESS-TANS-CDE-DESC.                                            
           IF  SW-NOT-TRANS-CDE-DISP                                            
               MOVE TCT-TRANS-CDE-DESC (TCT-IDX)                                
                                        TO HD-TC-TRANS-CDE-DESC                 
                                           TL-TC1-TRANS-CDE-DESC                
               WRITE CYCLE-SPLIT-REPORT-LINE                                    
                   FROM HD-TC-TRANS-CDE-HEADING                                 
                   AFTER ADVANCING 2 LINES                                      
               SET SW-TRANS-CDE-DISP    TO TRUE                                 
               ADD +2                   TO PV-LINE-COUNT                        
               MOVE 2                   TO PV-ADVANCE                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS TO WRITE DETAIL LINE TO THE REPORT.                             
      ******************************************************************        
       D700-C-PROCESS-DETAIL-LINE.                                              
           IF PV-LINE-COUNT > PC-LINE-LIMIT-FOR-DTL                             
               PERFORM Z300-C-PRINT-STD-RPT-HEADING                             
               PERFORM Z360-C-PRINT-CORP-HEADING                                
               PERFORM Z400-C-PRINT-CONSTANT-HEADINGS                           
           END-IF.                                                              
                                                                                
           MOVE DL-DETAIL-LINE TO CYCLE-SPLIT-REPORT-LINE.                      
           PERFORM Z200-C-WRITE-REPORT-LINE.                                    
           MOVE 1 TO PV-ADVANCE.                                                
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS A BREAK ON FIN-LIAB-DTE: FORMAT AND PRINT A DETAIL LINE,        
      * ADD THE FIN-LIAB TOTALS TO THE NEXT HIGHER                              
      * LEVEL TOTALS, AND INITIALIZE THE FIN-LIAB TOTALS.                       
      ******************************************************************        
       E100-C-CONTINUE-FIN-LIAB-BREAK.                                          
                                                                                
           MOVE 'E100-C-CONTINUE-FIN-LIAB-BREAK' TO PV-PARAGRAPH-NAME.          
                                                                                
           MOVE SA-PREV-FIN-LIAB-DTE    TO PV-ISO-FMT-DATE.                     
           MOVE PV-ISO-FMT-MM           TO DL-EFF-DATE-MM.                      
           MOVE PV-ISO-FMT-DD           TO DL-EFF-DATE-DD.                      
           MOVE PV-ISO-FMT-YY           TO DL-EFF-DATE-YY.                      
                                                                                
           MOVE TOT-FLB-CURR-ML-COST    TO DL-CURR-ML-COST.                     
           MOVE TOT-FLB-CURR-ML-RETAIL  TO DL-CURR-ML-RETAIL.                   
           MOVE TOT-FLB-CURR-INV-COST   TO DL-CURR-INV-COST.                    
           MOVE TOT-FLB-CURR-INV-RETAIL TO DL-CURR-INV-RETAIL.                  
                                                                                
           IF PV-LINE-COUNT > PC-LINE-LIMIT-FOR-DTL                             
               PERFORM Z300-C-PRINT-STD-RPT-HEADING                             
               PERFORM Z360-C-PRINT-CORP-HEADING                                
               PERFORM Z400-C-PRINT-CONSTANT-HEADINGS                           
           END-IF.                                                              
                                                                                
           MOVE DL-DETAIL-LINE TO CYCLE-SPLIT-REPORT-LINE.                      
           PERFORM Z200-C-WRITE-REPORT-LINE.                                    
           MOVE 1 TO PV-ADVANCE.                                                
                                                                                
                                                                                
           ADD TOT-FLB-CURR-ML-COST    TO TOT-TCD-CURR-ML-COST.                 
           ADD TOT-FLB-CURR-ML-RETAIL  TO TOT-TCD-CURR-ML-RETAIL.               
           ADD TOT-FLB-CURR-INV-COST   TO TOT-TCD-CURR-INV-COST.                
           ADD TOT-FLB-CURR-INV-RETAIL TO TOT-TCD-CURR-INV-RETAIL.              
                                                                                
                                                                                
           EVALUATE TRUE                                                        
               WHEN SA-PREV-PURCH-RECEIPT                                       
                   SET FNL-PR-IDX TO 1                                          
                   SEARCH TOT-FNL-FINAL-TOTALS-PR                               
                       WHEN (TOT-FIN-LIAB-DTE-PR (FNL-PR-IDX) = SPACES          
                            OR SA-PREV-FIN-LIAB-DTE =                           
                            TOT-FIN-LIAB-DTE-PR (FNL-PR-IDX))                   
                            CONTINUE                                            
                   END-SEARCH                                                   
                   MOVE SA-PREV-FIN-LIAB-DTE                                    
                        TO TOT-FIN-LIAB-DTE-PR (FNL-PR-IDX)                     
                   ADD TOT-FLB-CURR-ML-COST                                     
                       TO TOT-FNL-CURR-ML-COST-PR (FNL-PR-IDX)                  
                   ADD TOT-FLB-CURR-ML-RETAIL                                   
                       TO TOT-FNL-CURR-ML-RETAIL-PR (FNL-PR-IDX)                
                   ADD TOT-FLB-CURR-INV-COST                                    
                       TO TOT-FNL-CURR-INV-COST-PR (FNL-PR-IDX)                 
                   ADD TOT-FLB-CURR-INV-RETAIL                                  
                       TO TOT-FNL-CURR-INV-RETAIL-PR (FNL-PR-IDX)               
               WHEN SA-PREV-TRANS-PURCHASE                                      
                   SET FNL-TP-IDX TO 1                                          
                   SEARCH TOT-FNL-FINAL-TOTALS-TP                               
                       WHEN (TOT-FIN-LIAB-DTE-TP (FNL-TP-IDX) = SPACES          
                            OR SA-PREV-FIN-LIAB-DTE =                           
                            TOT-FIN-LIAB-DTE-TP (FNL-TP-IDX))                   
                            CONTINUE                                            
                   END-SEARCH                                                   
                   MOVE SA-PREV-FIN-LIAB-DTE                                    
                        TO TOT-FIN-LIAB-DTE-TP (FNL-TP-IDX)                     
                   ADD TOT-FLB-CURR-ML-COST                                     
                       TO TOT-FNL-CURR-ML-COST-TP (FNL-TP-IDX)                  
                   ADD TOT-FLB-CURR-ML-RETAIL                                   
                       TO TOT-FNL-CURR-ML-RETAIL-TP (FNL-TP-IDX)                
                   ADD TOT-FLB-CURR-INV-COST                                    
                       TO TOT-FNL-CURR-INV-COST-TP (FNL-TP-IDX)                 
                   ADD TOT-FLB-CURR-INV-RETAIL                                  
                       TO TOT-FNL-CURR-INV-RETAIL-TP (FNL-TP-IDX)               
               WHEN SA-PREV-TRANS-SALE                                          
                   SET FNL-TS-IDX TO 1                                          
                   SEARCH TOT-FNL-FINAL-TOTALS-TS                               
                       WHEN (TOT-FIN-LIAB-DTE-TS (FNL-TS-IDX) = SPACES          
                            OR SA-PREV-FIN-LIAB-DTE =                           
                            TOT-FIN-LIAB-DTE-TS (FNL-TS-IDX))                   
                            CONTINUE                                            
                   END-SEARCH                                                   
                   MOVE SA-PREV-FIN-LIAB-DTE                                    
                        TO TOT-FIN-LIAB-DTE-TS (FNL-TS-IDX)                     
                   ADD TOT-FLB-CURR-ML-COST                                     
                       TO TOT-FNL-CURR-ML-COST-TS (FNL-TS-IDX)                  
                   ADD TOT-FLB-CURR-ML-RETAIL                                   
                       TO TOT-FNL-CURR-ML-RETAIL-TS (FNL-TS-IDX)                
                   ADD TOT-FLB-CURR-INV-COST                                    
                       TO TOT-FNL-CURR-INV-COST-TS (FNL-TS-IDX)                 
                   ADD TOT-FLB-CURR-INV-RETAIL                                  
                       TO TOT-FNL-CURR-INV-RETAIL-TS (FNL-TS-IDX)               
               WHEN SA-PREV-TRANS-TRANSFER-FROM                                 
                   SET FNL-TF-IDX TO 1                                          
                   SEARCH TOT-FNL-FINAL-TOTALS-TF                               
                       WHEN (TOT-FIN-LIAB-DTE-TF (FNL-TF-IDX) = SPACES          
                            OR SA-PREV-FIN-LIAB-DTE =                           
                            TOT-FIN-LIAB-DTE-TF (FNL-TF-IDX))                   
                            CONTINUE                                            
                   END-SEARCH                                                   
                   MOVE SA-PREV-FIN-LIAB-DTE                                    
                        TO TOT-FIN-LIAB-DTE-TF (FNL-TF-IDX)                     
                   ADD TOT-FLB-CURR-ML-COST                                     
                       TO TOT-FNL-CURR-ML-COST-TF (FNL-TF-IDX)                  
                   ADD TOT-FLB-CURR-ML-RETAIL                                   
                       TO TOT-FNL-CURR-ML-RETAIL-TF (FNL-TF-IDX)                
                   ADD TOT-FLB-CURR-INV-COST                                    
                       TO TOT-FNL-CURR-INV-COST-TF (FNL-TF-IDX)                 
                   ADD TOT-FLB-CURR-INV-RETAIL                                  
                       TO TOT-FNL-CURR-INV-RETAIL-TF (FNL-TF-IDX)               
               WHEN SA-PREV-TRANS-TRANSFER-TO                                   
                   SET FNL-TT-IDX TO 1                                          
                   SEARCH TOT-FNL-FINAL-TOTALS-TT                               
                       WHEN (TOT-FIN-LIAB-DTE-TT (FNL-TT-IDX) = SPACES          
                            OR SA-PREV-FIN-LIAB-DTE =                           
                            TOT-FIN-LIAB-DTE-TT (FNL-TT-IDX))                   
                            CONTINUE                                            
                   END-SEARCH                                                   
                   MOVE SA-PREV-FIN-LIAB-DTE                                    
                        TO TOT-FIN-LIAB-DTE-TT (FNL-TT-IDX)                     
                   ADD TOT-FLB-CURR-ML-COST                                     
                       TO TOT-FNL-CURR-ML-COST-TT (FNL-TT-IDX)                  
                   ADD TOT-FLB-CURR-ML-RETAIL                                   
                       TO TOT-FNL-CURR-ML-RETAIL-TT (FNL-TT-IDX)                
                   ADD TOT-FLB-CURR-INV-COST                                    
                       TO TOT-FNL-CURR-INV-COST-TT (FNL-TT-IDX)                 
                   ADD TOT-FLB-CURR-INV-RETAIL                                  
                       TO TOT-FNL-CURR-INV-RETAIL-TT (FNL-TT-IDX)               
               WHEN SA-PREV-TRANS-MARKDOWN                                      
                   SET FNL-TM-IDX TO 1                                          
                   SEARCH TOT-FNL-FINAL-TOTALS-TM                               
                       WHEN (TOT-FIN-LIAB-DTE-TM (FNL-TM-IDX) = SPACES          
                            OR SA-PREV-FIN-LIAB-DTE =                           
                            TOT-FIN-LIAB-DTE-TM (FNL-TM-IDX))                   
                            CONTINUE                                            
                   END-SEARCH                                                   
                   MOVE SA-PREV-FIN-LIAB-DTE                                    
                        TO TOT-FIN-LIAB-DTE-TM (FNL-TM-IDX)                     
                   ADD TOT-FLB-CURR-ML-COST                                     
                       TO TOT-FNL-CURR-ML-COST-TM (FNL-TM-IDX)                  
                   ADD TOT-FLB-CURR-ML-RETAIL                                   
                       TO TOT-FNL-CURR-ML-RETAIL-TM (FNL-TM-IDX)                
                   ADD TOT-FLB-CURR-INV-COST                                    
                       TO TOT-FNL-CURR-INV-COST-TM (FNL-TM-IDX)                 
                   ADD TOT-FLB-CURR-INV-RETAIL                                  
                       TO TOT-FNL-CURR-INV-RETAIL-TM (FNL-TM-IDX)               
               WHEN SA-PREV-TRANS-MARKUP                                        
                   SET FNL-TU-IDX TO 1                                          
                   SEARCH TOT-FNL-FINAL-TOTALS-TU                               
                       WHEN (TOT-FIN-LIAB-DTE-TU (FNL-TU-IDX) = SPACES          
                            OR SA-PREV-FIN-LIAB-DTE =                           
                            TOT-FIN-LIAB-DTE-TU (FNL-TU-IDX))                   
                            CONTINUE                                            
                   END-SEARCH                                                   
                   MOVE SA-PREV-FIN-LIAB-DTE                                    
                        TO TOT-FIN-LIAB-DTE-TU (FNL-TU-IDX)                     
                   ADD TOT-FLB-CURR-ML-COST                                     
                       TO TOT-FNL-CURR-ML-COST-TU (FNL-TU-IDX)                  
                   ADD TOT-FLB-CURR-ML-RETAIL                                   
                       TO TOT-FNL-CURR-ML-RETAIL-TU (FNL-TU-IDX)                
                   ADD TOT-FLB-CURR-INV-COST                                    
                       TO TOT-FNL-CURR-INV-COST-TU (FNL-TU-IDX)                 
                   ADD TOT-FLB-CURR-INV-RETAIL                                  
                       TO TOT-FNL-CURR-INV-RETAIL-TU (FNL-TU-IDX)               
               WHEN SA-PREV-TRANS-ADJUSTMENT                                    
                   SET FNL-TA-IDX TO 1                                          
                   SEARCH TOT-FNL-FINAL-TOTALS-TA                               
                       WHEN (TOT-FIN-LIAB-DTE-TA (FNL-TA-IDX) = SPACES          
                            OR SA-PREV-FIN-LIAB-DTE =                           
                            TOT-FIN-LIAB-DTE-TA (FNL-TA-IDX))                   
                            CONTINUE                                            
                   END-SEARCH                                                   
                   MOVE SA-PREV-FIN-LIAB-DTE                                    
                        TO TOT-FIN-LIAB-DTE-TA (FNL-TA-IDX)                     
                   ADD TOT-FLB-CURR-ML-COST                                     
                       TO TOT-FNL-CURR-ML-COST-TA (FNL-TA-IDX)                  
                   ADD TOT-FLB-CURR-ML-RETAIL                                   
                       TO TOT-FNL-CURR-ML-RETAIL-TA (FNL-TA-IDX)                
                   ADD TOT-FLB-CURR-INV-COST                                    
                       TO TOT-FNL-CURR-INV-COST-TA (FNL-TA-IDX)                 
                   ADD TOT-FLB-CURR-INV-RETAIL                                  
                       TO TOT-FNL-CURR-INV-RETAIL-TA (FNL-TA-IDX)               
           END-EVALUATE.                                                        
                                                                                
                                                                                
           INITIALIZE TOT-FLB-FIN-LIAB-DTE-TOTALS.                              
                                                                                
                                                                                
      ******************************************************************        
      * READ THE INPUT FILE.                                                    
      ******************************************************************        
       Z100-READ-EDIT-SPLIT-FILE.                                               
                                                                                
           READ EDIT-SPLIT-FILE                                                 
               INTO IN035-RPT1-RECORD                                           
                   AT END SET SW-END-OF-EDIT-SPLIT-FILE TO TRUE.                
                                                                                
                                                                                
      ******************************************************************        
      * WRITE THE OUTPUT FILE.                                                  
      ******************************************************************        
       Z200-C-WRITE-REPORT-LINE.                                                
                                                                                
           WRITE CYCLE-SPLIT-REPORT-LINE                                        
               AFTER ADVANCING PV-ADVANCE.                                      
           ADD PV-ADVANCE TO PV-LINE-COUNT.                                     
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE STANDARD REPORT HEADING LINE FOR THE CYCLE REPORT.            
      ******************************************************************        
       Z300-C-PRINT-STD-RPT-HEADING.                                            
                                                                                
           ADD 1 TO PV-PAGE-COUNT.                                              
           MOVE PV-PAGE-COUNT TO DP133O-PAGE-NUMBER.                            
           MOVE DP133O-STANDRD-HEADER-133-COLS                                  
               TO CYCLE-SPLIT-REPORT-LINE                                       
           MOVE SPACES TO CYCLE-SPLIT-REPORT-LINE(133:1)                        
           WRITE CYCLE-SPLIT-REPORT-LINE                                        
               AFTER ADVANCING PAGE.                                            
           MOVE +1 TO PV-LINE-COUNT.                                            
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE CORP HEADING LINE.                                            
      ******************************************************************        
       Z360-C-PRINT-CORP-HEADING.                                               
                                                                                
           WRITE CYCLE-SPLIT-REPORT-LINE FROM HD-CP-CORP-HEADING                
               AFTER ADVANCING 2 LINES.                                         
           ADD +2 TO PV-LINE-COUNT.                                             
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL HEADING LINE FOR THE CYCLE REPORT.                
      ******************************************************************        
       Z375-C-PRINT-FINAL-TOT-HEADING.                                          
                                                                                
           WRITE CYCLE-SPLIT-REPORT-LINE FROM HD-FT-FINAL-TOTAL-HEADING         
                   AFTER ADVANCING 2 LINES.                                     
           ADD +2 TO PV-LINE-COUNT.                                             
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE CYCLE REPORT HEADINGS THAT ARE THE SAME FOR BOTH THE          
      * DETAIL PAGES AND THE FINAL TOTAL PAGE. THE TRANS-CDE HEADING IS         
      * NOT PRINTED HERE. IT IS PRINTED SPEARATELY BECAUSE UNLIKE THE           
      * HEADINGS PRINTED HERE, IT IS NOT REPRINTED UPON A PAGE BREAK.           
      ******************************************************************        
       Z400-C-PRINT-CONSTANT-HEADINGS.                                          
                                                                                
           IF  IN035-RPT1-INV-FSCL-PER-CDE NOT = SPACES                         
               MOVE IN035-RPT1-INV-FSCL-PER-CDE                                 
                                         TO FPN-SUB-X                           
               MOVE FPN-FSCL-PER-NAME (FPN-SUB)                                 
                                         TO                                     
                                     HD-FP-INV-FISC-PERIOD-NAME                 
           ELSE                                                                 
               MOVE HD-FP-CURR-FISC-PERIOD-NAME                                 
                                         TO                                     
                                     HD-FP-INV-FISC-PERIOD-NAME                 
           END-IF.                                                              
                                                                                
           WRITE CYCLE-SPLIT-REPORT-LINE FROM HD-ED-EDIT-DATE-HEADING           
               AFTER ADVANCING 1 LINES.                                         
           WRITE CYCLE-SPLIT-REPORT-LINE FROM HD-FP-FISCAL-PER-HEADING          
               AFTER ADVANCING 2 LINES.                                         
           WRITE CYCLE-SPLIT-REPORT-LINE FROM HD-MI-ML-INV-HEADING              
               AFTER ADVANCING 2 LINES.                                         
           WRITE CYCLE-SPLIT-REPORT-LINE FROM HD-CL-COLUMN-HEADING              
               AFTER ADVANCING 1 LINE.                                          
           ADD +6 TO PV-LINE-COUNT.                                             
           MOVE 2  TO PV-ADVANCE.                                               
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE TRANS-CDE HEADING. FOR THE CYCLE REPORT,                      
      * TRANS-CDE TABLE IS SEARCHED TO FIND THE DESCRIPTION THAT WILL           
      * BE USED IN THE HEADING.                                                 
      ******************************************************************        
       Z500-PRINT-TRANS-CDE-HEADING.                                            
                                                                                
           MOVE 'Z500-PRINT-TRANS-CDE-HEADING' TO PV-PARAGRAPH-NAME.            
                                                                                
           SET TCT-IDX TO 1                                                     
                                                                                
           SEARCH TCT-ENTRY                                                     
               AT END                                                           
                   MOVE 'TRANS-CDE ON INPUT RECORD NOT FOUND IN TCT-            
      -                    'TRANS-CDE-TABLE.' TO PV-ABEND-MESSAGE               
                   MOVE +4008 TO ABEND-CODE                                     
                   PERFORM Z999-ABEND                                           
               WHEN TCT-TRANS-CDE (TCT-IDX) = IN035-RPT1-TRANS-CDE              
                AND TCT-SYS-ORIG-CDE (TCT-IDX)                                  
                                = IN035-RPT1-SYST-ORIG-CDE                      
                   MOVE TCT-TRANS-CDE-DESC (TCT-IDX)                            
                                            TO HD-TC-TRANS-CDE-DESC             
                                               TL-TC1-TRANS-CDE-DESC            
           END-SEARCH                                                           
                                                                                
           IF PV-LINE-COUNT > PC-LINE-LIMIT-FOR-HD-TC                           
               PERFORM Z300-C-PRINT-STD-RPT-HEADING                             
               PERFORM Z360-C-PRINT-CORP-HEADING                                
               PERFORM Z400-C-PRINT-CONSTANT-HEADINGS                           
           END-IF.                                                              
                                                                                
           WRITE CYCLE-SPLIT-REPORT-LINE                                        
               FROM HD-TC-TRANS-CDE-HEADING                                     
               AFTER ADVANCING 2 LINES                                          
                                                                                
           ADD +2 TO PV-LINE-COUNT.                                             
           MOVE 2 TO PV-ADVANCE.                                                
                                                                                
                                                                                
      *****************************************************************         
      * THIS PARAGRAPH RETRIEVES THE CYCLE NAME.                                
      *****************************************************************         
       Z600-READ-TINCNTL.                                                       
                                                                                
           MOVE 'Z600-READ-TINCNTL' TO PV-PARAGRAPH-NAME.                       
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                
F33304            OR SQLCODE = 0 OR SQLCODE = +100                              
                                                                                
               MOVE IN035-INV-ID         TO INCNTL-INV-ID                       
                                                                                
               EXEC SQL                                                         
                    SELECT INV_NM                                               
                    INTO :INCNTL-INV-NM                                         
                    FROM TINCNTL                                                
                    WHERE INV_ID = :INCNTL-INV-ID                               
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
F33304             WHEN SQLCODE = +100                                          
F33304                 MOVE 'NON-INVENTORY STORES' TO INCNTL-INV-NM             
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z600-READ-TINCNTL'                                 
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'READING CYCLE TABLE'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE 'READ RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKRP056'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
       Z999-ABEND.                                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKRP056'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  ' PV-ABEND-MESSAGE.                                     
           DISPLAY '**  INPUT RECORD: ' EDIT-SPLIT-RECORD.                      
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
