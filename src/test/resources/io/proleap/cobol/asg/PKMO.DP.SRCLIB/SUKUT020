000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUT020.                                                 
000600 AUTHOR.        STEPHANIE HERON.                                          
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   DECEMBER, 1997.                                           
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM CREATES AN EXTRACT FILE FROM TSUAUPC OF AUDITS THAT       
001300*  WERE SELECTED TO BE DELETED.                                           
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
003420     SELECT SUATRN-AUDIT-FILE                                             
003430         ASSIGN TO IN01.                                                  
003431                                                                          
003440     SELECT SUAUPC-AUDIT-FILE                                             
003450         ASSIGN TO OUT01.                                                 
003460                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
005410 FD  SUATRN-AUDIT-FILE                                                    
005420     RECORDING MODE IS F                                                  
005430     LABEL RECORDS ARE STANDARD                                           
005440     BLOCK CONTAINS 0 RECORDS.                                            
005450                                                                          
005460 01  SUATRN-AUDIT-RECORD        PIC  X(103).                              
005470*                                                                         
005480 FD  SUAUPC-AUDIT-FILE                                                    
005490     RECORDING MODE IS F                                                  
005491     LABEL RECORDS ARE STANDARD                                           
005492     BLOCK CONTAINS 0 RECORDS.                                            
005493                                                                          
005494 01  SUAUPC-AUDIT-RECORD        PIC  X(140).                              
005495*                                                                         
005500*                                                                         
005600******************************************************************        
005700 WORKING-STORAGE SECTION.                                                 
007400******************************************************************        
007500*  DB2 FORMATTED MESSAGE AREA                                             
007600******************************************************************        
007700     COPY DPWS004.                                                        
007800*                                                                         
008100******************************************************************        
008200*  COMMUNICATIONS AREA FOR DB2                                            
008300******************************************************************        
008400     EXEC SQL                                                             
008500          INCLUDE SQLCA                                                   
008600     END-EXEC.                                                            
008800*                                                                         
011180******************************************************************        
011190*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
011191*  CARTON AUDIT TABLE                                                     
011192******************************************************************        
011193     EXEC SQL                                                             
011194          INCLUDE TSUATRN                                                 
011195     END-EXEC.                                                            
011196                                                                          
011197******************************************************************        
011198*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
011199*  CARTON UPC TABLE                                                       
011200******************************************************************        
011201     EXEC SQL                                                             
011202          INCLUDE TSUAUPC                                                 
011203     END-EXEC.                                                            
011204                                                                          
011210 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
011300                                                   COMP SYNC.             
011400                                                                          
011500 01  PS-PROGRAM-SWITCHES.                                                 
011600     05  PS-EOF-AUDIT-FILE-SW        PIC  X(01)    VALUE 'N'.             
011700         88  PS-EOF-AUDIT-FILE                     VALUE 'Y'.             
011800         88  PS-MORE-AUDIT-FILE                    VALUE 'N'.             
011804     05  PS-EOF-UPC-CSR-SW           PIC  X(01)    VALUE 'N'.             
011805         88  PS-EOF-UPC-CSR                        VALUE 'Y'.             
011806         88  PS-MORE-UPC-CSR                       VALUE 'N'.             
012000                                                                          
012100 01  PC-PROGRAM-CONSTANTS.                                                
012200     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
012300                                                   COMP SYNC.             
012400     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
012500         'SUKUT020'.                                                      
012600     05  PC-CURRENT-OPERATING-COMPANY                                     
012700                                     PIC  X(02)    VALUE '03'.            
012800     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3               
012900                                                   COMP SYNC.             
013000     05  PC-CURRENT-TIMESTAMP        PIC  X(26)    VALUE SPACES.          
013010     05  PC-COMPLETE-TIMESTAMP       PIC  X(26)    VALUE SPACES.          
013020     05  PC-INPROCESS-TIMESTAMP      PIC  X(26)    VALUE SPACES.          
013100                                                                          
013200 01  PV-PROGRAM-VARIABLES.                                                
013300     05  PV-ITEM-NBR                 PIC  9(15)    VALUE ZERO.            
013400     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
013500     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
013600     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO             
013700                                                   COMP SYNC.             
014200     05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO             
014300                                                   COMP SYNC.             
014310     05  PV-READ-COUNTER             PIC S9(09)    VALUE ZERO             
014320                                                   COMP SYNC.             
014400     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
014500                                                                          
014600     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO             
014700                                                   COMP-3.                
014800                                                                          
014900                                                                          
015000 01  SD-SYSTEM-DATE.                                                      
015100     05  SD-SYSTEM-DATE-YY           PIC  9(02)      VALUE ZERO.          
015200     05  SD-SYSTEM-DATE-MM           PIC  9(02)      VALUE ZERO.          
015300     05  SD-SYSTEM-DATE-DD           PIC  9(02)      VALUE ZERO.          
015400                                                                          
015500*                                                                         
015600*  CURSOR DECLARATION FOR ALL THE AUDITS THAT HAVE BEEN COMPLETED         
015700*  FOR THREE WEEKS OR MORE.                                               
015800*                                                                         
015900                                                                          
016000******************************************************************        
016100*  MODIFIED CURSOR UPC_CURSOR TO REMOVE SKU_DEPT_NBR,  WR# 21361 *        
016200*  SKU_CL_NBR AND SKU_SEQ_NBR.                                   *        
016300******************************************************************        
018399                                                                          
018400     EXEC SQL                                                             
018401          DECLARE UPC_CURSOR CURSOR FOR                                   
018402           SELECT  AUD_CNTL_NBR                                           
018404                  ,SHIPT_UNT_ID                                           
018405                  ,UPC_NBR                                                
018406                  ,SCN_TMST                                               
018407                  ,AUD_USR_ID                                             
018411                  ,ITM_DESC                                               
018412                  ,VND_STYL_DESC                                          
018413                  ,UPC_CLOR_DESC                                          
018414                  ,UPC_SIZE_DESC                                          
018415                  ,UPC_RTL_AMT                                            
018416                  ,UPC_CERTD_IND                                          
                        ,SCND_UNT_QTY                                           
                        ,SCN_TYP_CDE                                            
                        ,SKU_NBR                                                
                        ,DEPT_NBR                                               
                        ,MAJ_CL_NBR                                             
                        ,SUB_CL_NBR                                             
018417             FROM TSUAUPC                                                 
018418            WHERE AUD_CNTL_NBR  = :SUATRN-AUD-CNTL-NBR                    
018419              AND SHIPT_UNT_ID  = :SUATRN-SHIPT-UNT-ID                    
018420     END-EXEC.                                                            
018421                                                                          
018430******************************************************************        
018500*  CHANGES MADE FOR WR# 21361 ENDS HERE.                         *        
018510******************************************************************        
018520                                                                          
018530******************************************************************        
018600 PROCEDURE DIVISION.                                                      
018700******************************************************************        
018800                                                                          
018900 0000-PROGRAM-MAINLINE.                                                   
019000******************************************************************        
019100* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
019200*      PROGRAM.                                                           
019300******************************************************************        
019400                                                                          
019500     PERFORM 1000-INITIALIZE.                                             
019510                                                                          
019520     IF PS-MORE-AUDIT-FILE                                                
019700         PERFORM 2000-PROCESS-AUDITS                                      
019800            UNTIL PS-EOF-AUDIT-FILE                                       
019900     END-IF.                                                              
019901                                                                          
020000     PERFORM 9000-EOJ-ROUTINE.                                            
020100                                                                          
020200     STOP RUN.                                                            
020300                                                                          
020400 1000-INITIALIZE.                                                         
020500******************************************************************        
020600*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
020700*    -- OPEN OUTPUT FILE                                                  
020800*    -- OPEN CURSOR                                                       
020900*    -- FETCH CURSOR                                                      
021000******************************************************************        
021100                                                                          
021200     ACCEPT SD-SYSTEM-DATE FROM DATE.                                     
021300                                                                          
021400     OPEN OUTPUT SUAUPC-AUDIT-FILE.                                       
021410     OPEN INPUT  SUATRN-AUDIT-FILE.                                       
021600                                                                          
021700     PERFORM 7000-READ-AUDIT-FILE.                                        
021800                                                                          
021801     IF PS-EOF-AUDIT-FILE                                                 
021802        CONTINUE                                                          
021803     ELSE                                                                 
021810        PERFORM 7100-OPEN-UPC-CURSOR                                      
021900        PERFORM 7200-FETCH-UPC-CURSOR                                     
021910     END-IF.                                                              
022000                                                                          
025410 2000-PROCESS-AUDITS.                                                     
025500******************************************************************        
025600*  PROCESS THE AUDITS THAT FALL INTO THE REQUIRED TIME FRAME TO           
025700*  BE DELETED.  A CURSOR WILL BE OPENED FOR EACH AUDIT TO BRING           
025800*  BACK ALL THE CARTONS THAT ARE ASSOCIATED TO IT, AND A FILE             
025810*  CONTAINING THESE CARTONS WILL BE WRITTEN.                              
025900******************************************************************        
026000                                                                          
026102                                                                          
026103     PERFORM UNTIL PS-EOF-UPC-CSR                                         
026104        PERFORM 8000-WRITE-AUDIT-FILE                                     
026120        PERFORM 7200-FETCH-UPC-CURSOR                                     
026130     END-PERFORM.                                                         
026140                                                                          
026141     SET PS-MORE-UPC-CSR TO TRUE.                                         
026150     PERFORM 7300-CLOSE-UPC-CURSOR.                                       
026160                                                                          
026170     PERFORM 7000-READ-AUDIT-FILE.                                        
026300                                                                          
026310     IF PS-EOF-AUDIT-FILE                                                 
026320        CONTINUE                                                          
026330     ELSE                                                                 
026400        PERFORM 7100-OPEN-UPC-CURSOR                                      
026401     END-IF.                                                              
026403                                                                          
026410 7000-READ-AUDIT-FILE.                                                    
026500******************************************************************        
026600*  READ THE INPUT FILE                                                    
026700******************************************************************        
026710                                                                          
026720      READ SUATRN-AUDIT-FILE INTO                                         
026730           DCLTSUATRN AT END SET                                          
026740           PS-EOF-AUDIT-FILE TO TRUE.                                     
026750                                                                          
026760      IF PS-EOF-AUDIT-FILE                                                
026770         CONTINUE                                                         
026780      ELSE                                                                
026790         ADD +1 TO PV-READ-COUNTER                                        
026791      END-IF.                                                             
026792                                                                          
026800 7100-OPEN-UPC-CURSOR.                                                    
026900******************************************************************        
027000*  OPEN THE CURSOR.                                                       
027100******************************************************************        
027400     EXEC SQL                                                             
027500          OPEN UPC_CURSOR                                                 
027600     END-EXEC.                                                            
027700                                                                          
027800     EVALUATE TRUE                                                        
027810         WHEN SQLCODE = ZERO                                              
027820             ADD +1 TO PV-OPEN-COUNTER                                    
028200         WHEN SQLWARN0 NOT = SPACES                                       
028300         WHEN SQLCODE < ZERO                                              
028400         WHEN SQLCODE > ZERO                                              
028500             MOVE '7100-OPEN-UPC-CURSOR       '                           
028600                             TO PV-PARAGRAPH-NAME                         
028700             MOVE 'OPEN THE UPC_CURSOR      '                             
028800                             TO PV-DB2-OPERATION-ATTEMPTED                
028810             DISPLAY 'AUDIT CONTROL NUMBER IS '                           
028820                     SUATRN-AUD-CNTL-NBR                                  
028830             DISPLAY 'LAST CART ID PROCESSED WAS '                        
028840                     SUATRN-SHIPT-UNT-ID                                  
028900             PERFORM 9100-SQL-ABEND                                       
029200     END-EVALUATE.                                                        
030200                                                                          
030300 7200-FETCH-UPC-CURSOR.                                                   
030400******************************************************************        
030500*  FETCH THE RESULTS FROM THE COMPLETE CURSOR.                            
030600******************************************************************        
030610******************************************************************        
030620*  MODIFIED FETCH STATEMENT, REMOVED                   WR# 21361 *        
030630*  :SUAUPC-SKU-DEPT-NBR, :SUAUPC-SKU-CL-NBR AND                  *        
030640*  :SUAUPC-SKU-SEQ-NBR.                                          *        
030650******************************************************************        
030700     EXEC SQL                                                             
030800          FETCH UPC_CURSOR                                                
030801               INTO :SUAUPC-AUD-CNTL-NBR                                  
030802                   ,:SUAUPC-SHIPT-UNT-ID                                  
030803                   ,:SUAUPC-UPC-NBR                                       
030804                   ,:SUAUPC-SCN-TMST                                      
030805                   ,:SUAUPC-AUD-USR-ID                                    
030809                   ,:SUAUPC-ITM-DESC                                      
030810                   ,:SUAUPC-VND-STYL-DESC                                 
030811                   ,:SUAUPC-UPC-CLOR-DESC                                 
030812                   ,:SUAUPC-UPC-SIZE-DESC                                 
030820                   ,:SUAUPC-UPC-RTL-AMT                                   
030900                   ,:SUAUPC-UPC-CERTD-IND                                 
                         ,:SUAUPC-SCND-UNT-QTY                                  
                         ,:SUAUPC-SCN-TYP-CDE                                   
                         ,:SUAUPC-SKU-NBR                                       
                         ,:SUAUPC-DEPT-NBR                                      
                         ,:SUAUPC-MAJ-CL-NBR                                    
                         ,:SUAUPC-SUB-CL-NBR                                    
032300     END-EXEC.                                                            
032310******************************************************************        
032320*  CHANGES MADE FOR WR# 21361 ENDS HERE.                         *        
032330******************************************************************        
032400                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN SQLCODE = ZERO                                              
032700             CONTINUE                                                     
032800         WHEN SQLCODE = +100                                              
032900             SET PS-EOF-UPC-CSR                                           
033000                                 TO TRUE                                  
033100         WHEN SQLWARN0 NOT = SPACES                                       
033200         WHEN SQLCODE < ZERO                                              
033300         WHEN SQLCODE > ZERO                                              
033400             MOVE '7200-FETCH-UPC-CURSOR  '                               
033500                                 TO PV-PARAGRAPH-NAME                     
033600             MOVE 'FETCH THE COMPLETE-CURSOR   '                          
033700                                 TO PV-DB2-OPERATION-ATTEMPTED            
033710             DISPLAY 'AUDIT CONTROL NUMBER IS '                           
033720                     SUATRN-AUD-CNTL-NBR                                  
033730             DISPLAY 'LAST CART ID PROCESSED WAS '                        
033740                     SUATRN-SHIPT-UNT-ID                                  
033800             PERFORM 9100-SQL-ABEND                                       
033900     END-EVALUATE.                                                        
034000                                                                          
034204 7300-CLOSE-UPC-CURSOR.                                                   
034205******************************************************************        
034206*  CLOSE THE UPC CURSOR.                                                  
034207******************************************************************        
034208     EXEC SQL                                                             
034209          CLOSE UPC_CURSOR                                                
034211     END-EXEC.                                                            
034212                                                                          
034213     EVALUATE TRUE                                                        
034214         WHEN SQLCODE = ZERO                                              
034215             CONTINUE                                                     
034219         WHEN SQLWARN0 NOT = SPACES                                       
034220         WHEN SQLCODE < ZERO                                              
034221         WHEN SQLCODE > ZERO                                              
034222             MOVE '7300-CLOSE-UPC-CURSOR '                                
034223                                 TO PV-PARAGRAPH-NAME                     
034224             MOVE 'CLOSE THE UPC CURSOR        '                          
034225                                 TO PV-DB2-OPERATION-ATTEMPTED            
034226             DISPLAY 'AUDIT CONTROL NUMBER IS '                           
034227                     SUATRN-AUD-CNTL-NBR                                  
034228             DISPLAY 'LAST CART ID PROCESSED WAS '                        
034229                     SUATRN-SHIPT-UNT-ID                                  
034230             PERFORM 9100-SQL-ABEND                                       
034231     END-EVALUATE.                                                        
034232                                                                          
034233 8000-WRITE-AUDIT-FILE.                                                   
034240******************************************************************        
034300*  WRITE THE SUAUPC-AUDIT-RECORD                                          
034400******************************************************************        
034500     INITIALIZE SUAUPC-AUDIT-RECORD.                                      
034600     WRITE SUAUPC-AUDIT-RECORD                                            
034700         FROM DCLTSUAUPC.                                                 
034800     ADD +1 TO PV-WRITE-COUNTER.                                          
034900                                                                          
035800 9000-EOJ-ROUTINE.                                                        
035900******************************************************************        
036000*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
036100******************************************************************        
036200     CLOSE SUAUPC-AUDIT-FILE                                              
036300           SUATRN-AUDIT-FILE.                                             
036400                                                                          
036500     MOVE PV-READ-COUNTER TO PV-DISPLAY-COUNT.                            
036600     DISPLAY 'TOTAL INPUT RECORDS READ '                                  
036601     PV-DISPLAY-COUNT.                                                    
036610     MOVE PV-OPEN-COUNTER TO PV-DISPLAY-COUNT.                            
036620     DISPLAY 'TOTAL OPENS ON THE UPC CUSROR (THIS SHOULD MATCH '.         
036621     DISPLAY 'THE READ COUNT) '               PV-DISPLAY-COUNT.           
036622     MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                           
036623     DISPLAY 'TOTAL SUAUPC RECORDS WRITTEN '  PV-DISPLAY-COUNT.           
036700                                                                          
040292                                                                          
040300 9100-SQL-ABEND.                                                          
040400******************************************************************        
040500*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
040600*  ERROR OR WARNING CODE OCCURS.                                          
040700******************************************************************        
040800     DISPLAY '9100-SQL-ABEND'.                                            
040900     DISPLAY '** ABEND     **'.                                           
041000     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
041100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
041200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
041300                                                                          
041400******************************************************************        
041500* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
041600* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
041700* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
041800* FORMAT.                                                                 
041900******************************************************************        
042000     COPY DPPD004.                                                        
042100                                                                          
042200     MOVE +4013                  TO ABEND-CODE.                           
042300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
