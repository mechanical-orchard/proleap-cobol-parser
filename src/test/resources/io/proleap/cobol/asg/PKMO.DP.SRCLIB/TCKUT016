      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
                                                                        00040000
       PROGRAM-ID.             TCKUT016.                                00050000
       AUTHOR.                 BARB SCHULTZ.                            00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            NOVEMBER 2010.                           00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ***************************************************************** 00110000
      *  THIS PROGRAM READS THE SORTED BANK SETTLEMENT FILE AND       * 00120000
      *  SUMMARIZES THE ECOMMERCE TRANSACTIONS INTO ONE.              * 00130000
      ***************************************************************** 00140000
      ******************************************************************00150000
      * HISTORY LOG:                                                    00160000
      ******************************************************************00170000
      * DATE:        2013-06-28                                         00180000
      * WR/IR#:      CHG53632                                           00190000
      * PROGRAMMER:  COGNIZANT                                          00200000
      * DESCRIPTION: INCLUDED A NEW FIELD "OMNI-CHNL-FFLMT-TYP-CDE" AS  00210000
      *              PART OF OCF COMPANION CHANGE.                      00220000
      ******************************************************************00230000
P9696 ******************************************************************00240000
P9696 * DATE:        2014-01-16                                         00250000
P9696 * WR/IR#:      P9696 - AJB SETTLEMENT                             00260000
P9696 * PROGRAMMER:  COGNIZANT                                          00270000
P9696 * DESCRIPTION: STORE '873' ROLLUP LOGIC IS ALREADY COVERED IN     00280000
P9696 *              TCKUT007 PROGRAM.                                  00290000
P9696 *              SO REMOVED LOGIC OF MOVING 873 STORE NUMBER TO     00300000
P9696 *              PV-SAVE-STR-NBR WHEN TC005-ECOMM-ORD-NBR IS NOT    00310000
P9696 *              EQUAL TO SPACES IN 2500-MOVE-TC005-TO-SAVE-FIELDS  00320000
P9696 *              PARA.                                              00330000
P9696 *              ADDED TC005-AUTH-REF-NBR TO TRANSACTION ROLLUP     00340000
P9696 *              LOGIC.                                             00350000
P9696 ******************************************************************00360000
SHIP  * DATE:        2019-03-20                                         00370000
SHIP  * WR/IR#:      CHG0220426                                         00380000
SHIP  * PROGRAMMER:  BARB SCHULTZ                                       00390000
SHIP  * DESCRIPTION: ADDED LOGIC TO THE ROLLUP TO NOT ROLLUP ANY OMNI   00400000
SHIP  *              TYPE SHIPMENTS.  THIS IS DUE TO THE SETTLE BY      00410000
SHIP  *              SHIP PROJECT.                                      00420000
      ******************************************************************00430000
SHIP2 * DATE:        01/21/2020                                        *00440000
SHIP2 * WR/IR#:      CHG0233046                                        *00450000
SHIP2 * PROGRAMMER:  BARB SCHULTZ                                      *00460000
SHIP2 * DESCRIPTION: CHANGED THE MC FIELDS TO USE THE VISA FIELDS.     *00470000
SHIP2 *              BOTH VISA AND MC ARE GOING TO BE USING THE SAME   *00480000
SHIP2 *              MULT CLEAR FIELDS FROM THE DATABASE. MOVED        *00490000
SHIP2 *              PARTIAL SHIPMENT INDICATOR TO TCRD005. THIS IS    *00500000
SHIP2 *              EITHER A 'Y' FOR PARTIAL SHIPMENT, 'N' MEANING    *00510000
SHIP2 *              THIS IS NOT A PARTIAL SHIPMENT.  IF IT IS NOT AN  *00520000
SHIP2 *              ECOMM TRANSACTION OR THERE IS NO ORDER NBR, WE    *00530000
SHIP2 *              MOVE ' ' AS THIS IS THE DEFAULT.                  *00540000
SHIP2 *              ALSO IF 1 OF 2 AND 2 OF 2 BOTH S2H COME THRU WE   *00550000
SHIP2 *              ROLL THESE TOGETHER AND MOVE 'Y' TO THE           *00560000
SHIP2 *              PAR-SHIP-IND, IF WE ARE MISSING 1 OF 2, WE STILL  *00570000
SHIP2 *              MOVE 'Y'.  IF WE HAVE 1 OR 1, THEN WE MOVE 'N'.   *00580000
SHIP2 *              IF IT IS AMEX OR DISCOVER, PART-SHP-IND = ' '.    *00590000
SHIP2 *              IF VISA-MULT-CLR-SEQ-NBR = 0, PART-SHP-IND = ' '. *00600000
      ******************************************************************00610000
112321* DATE:        11/22/2021                                        *00620000
112321* WR/IR#:      CHG0264322                                        *00630000
112321* PROGRAMMER:  DAN PAYNTER                                       *00640000
112321* DESCRIPTION: ALLOW DISCOVER SETTLE BY SHIPMENT                 *00650000
      ******************************************************************00660000
062823* DATE:        06/28/2023                                        *00661000
062823* WR/IR#:      CHG0290239                                        *00662000
062823* PROGRAMMER:  DAN PAYNTER                                       *00663000
062823* DESCRIPTION: CHANGES TO ENABLE TRAILING TRANSACTIONS           *00664000
062823******************************************************************00665000
       ENVIRONMENT DIVISION.                                            00670000
      ***************************************************************** 00680000
                                                                        00690000
       CONFIGURATION SECTION.                                           00700000
                                                                        00710000
       SOURCE-COMPUTER.        IBM-3090.                                00720000
                                                                        00730000
       OBJECT-COMPUTER.        IBM-3090.                                00740000
                                                                        00750000
       INPUT-OUTPUT SECTION.                                            00760000
                                                                        00770000
       FILE-CONTROL.                                                    00780000
                                                                        00790000
           SELECT BANK-IN-FILE                                          00800000
               ASSIGN TO INP01.                                         00810000
                                                                        00820000
           SELECT BANK-OUT-FILE                                         00830000
               ASSIGN TO OUT01.                                         00840000
                                                                        00850000
                                                                        00860000
      ******************************************************************00870000
       DATA DIVISION.                                                   00880000
      ******************************************************************00890000
                                                                        00900000
       FILE SECTION.                                                    00910000
                                                                        00920000
       FD  BANK-IN-FILE                                                 00930000
           RECORDING MODE IS F                                          00940000
           BLOCK CONTAINS 0 RECORDS                                     00950000
           LABEL RECORDS ARE STANDARD.                                  00960000
                                                                        00970000
       01  BANK-IN-RECORD  PIC X(298).                                  00980000
                                                                        00990000
       FD  BANK-OUT-FILE                                                01000000
           RECORDING MODE IS F                                          01010000
           BLOCK CONTAINS 0 RECORDS                                     01020000
           LABEL RECORDS ARE STANDARD.                                  01030000
                                                                        01040000
       01  BANK-OUT-RECORD PIC X(298).                                  01050000
                                                                        01060000
      ******************************************************************01070000
       WORKING-STORAGE SECTION.                                         01080000
      ******************************************************************01090000
       01  FILLER                            PIC X(50)  VALUE           01100000
           ' *** WORKING STORAGE STARTS HERE FOR TCKUT016 *** '.        01110000
                                                                        01120000
      ******************************************************************01130000
      *   PROGRAM SWITCHES                                             *01140000
      ******************************************************************01150000
       01  PS-PROGRAM-SWITCHES.                                         01160000
           05  PS-END-OF-BANK-FILE-SW        PIC X(01)  VALUE 'N'.      01170000
               88  PS-END-OF-BANK-FILE                  VALUE 'Y'.      01180000
           05  PS-FIRST-TIME-SW              PIC X(01)  VALUE 'N'.      01190000
               88  PS-FIRST-TIME                        VALUE 'Y'.      01200000
SHIP2      05  PS-ROLLUP-SW                PIC  X(01)    VALUE 'N'.     01210000
SHIP2          88  PS-ROLLUP                             VALUE 'Y'.     01220000
SHIP2          88  PS-NOT-ROLLUP                         VALUE 'N'.     01230000
SHIP2      05  PS-OMNI-ORDER-SW            PIC  X(01)    VALUE 'N'.     01240000
SHIP2          88  PS-OMNI-ORDER                         VALUE 'Y'.     01250000
SHIP2          88  PS-NOT-OMNI-ORDER                     VALUE 'N'.     01260000
SHIP2      05  PS-FIRST-RECORD-SW          PIC  X(01)    VALUE 'N'.     01270000
SHIP2          88  PS-FIRST-RECORD                       VALUE 'Y'.     01280000
SHIP2          88  PS-NOT-FIRST-RECORD                   VALUE 'N'.     01290000
                                                                        01300000
      ******************************************************************01310000
      *   PROGRAM CONSTANTS                                            *01320000
      ******************************************************************01330000
       01  PC-CONSTANTS.                                                01340000
           05  PC-PROGRAM-NAME             PIC X(08)     VALUE          01350000
                                                         'TCKUT016'.    01360000
                                                                        01370000
      ******************************************************************01380000
      *   PROGRAM ACCUMULATERS.                                        *01390000
      ******************************************************************01400000
       01  PA-ACCUMULATORS.                                             01410000
           05  PA-BANK-REC-CHG             PIC  9(07)    VALUE ZEROES.  01420000
           05  PA-BANK-FILE-RECS-READ      PIC  9(07)    VALUE ZEROES.  01430000
           05  PA-BANK-FILE-RECS-WRITTEN   PIC  9(07)    VALUE ZEROES.  01440000
                                                                        01450000
      ******************************************************************01460000
      *   PROGRAM VARIABLES.                                           *01470000
      ******************************************************************01480000
       01  PV-PROGRAM-VARIABLES.                                        01490000
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   01500000
           05  PV-SLS-DTE-CHG-COUNT        PIC  S9(10)   VALUE ZEROES.  01510000
           05  PV-TOT-TNDR-AMT             PIC S9(9)V99  VALUE ZEROES.  01520000
           05  PV-TOT-SLS-TX-AMT           PIC S9(7)V99  VALUE ZEROES.  01530000
           05  PV-SAVE-TC005.                                           01540000
               10  PV-SAVE-PARTN-NBR       PIC S9(04)    COMP.          01550000
               10  PV-SAVE-STR-NBR         PIC S9(04)    COMP.          01560000
               10  PV-SAVE-RGST-ID         PIC S9(04)    COMP.          01570000
               10  PV-SAVE-TRN-NBR         PIC S9(04)    COMP.          01580000
               10  PV-SAVE-STRT-TM         PIC  X(08).                  01590000
               10  PV-SAVE-SLS-CORR-SEQ-NBR PIC S9(04)   COMP.          01600000
               10  PV-SAVE-TRN-TYP-CDE     PIC  X(02).                  01610000
               10  PV-SAVE-SLS-DTE         PIC  X(10).                  01620000
               10  PV-SAVE-TRN-DTE         PIC  X(10).                  01630000
               10  PV-SAVE-TNDR-SEQ-NBR    PIC S9(04)    COMP.          01640000
               10  PV-SAVE-TNDR-TYP-CDE    PIC  X(02).                  01650000
               10  PV-SAVE-TNDR-ID         PIC  X(33).                  01660000
               10  PV-SAVE-TNDR-AMT        PIC S9(07)V99 COMP-3.        01670000
               10  PV-SAVE-TNDR-ID-SRC-CDE PIC  X(02).                  01680000
               10  PV-SAVE-AUTH-NBR        PIC  X(06).                  01690000
               10  PV-SAVE-AUTH-SRC-CDE    PIC  X(02).                  01700000
               10  PV-SAVE-CPS-TRN-ID      PIC S9(15)    COMP-3.        01710000
               10  PV-SAVE-MC-CMPLIN-CDE   PIC  X(04).                  01720000
               10  PV-SAVE-VIP-SRC-CDE     PIC  X(01).                  01730000
               10  PV-SAVE-VIP-RSPN-CDE    PIC  X(02).                  01740000
               10  PV-SAVE-BNKNET-NBR      PIC  X(15).                  01750000
               10  PV-SAVE-SETTLE-STORE-NBR PIC  9(04).                 01760000
               10  PV-SAVE-ECOMM-REAUTH-IND PIC  X(01).                 01770000
               10  PV-SAVE-ECOMM-ORD-NBR   PIC  X(25).                  01780000
               10  PV-SAVE-AUTH-REF-NBR    PIC  X(45).                  01790000
               10  PV-SAVE-TRAN-CDE        PIC  X(01).                  01800000
               10  PV-SAVE-MERCHANT-NBR    PIC  X(15).                  01810000
               10  PV-SAVE-AVS-RSPN-CDE    PIC  X(01).                  01820000
               10  PV-SAVE-VOID-ABRT-CDE   PIC  X(01).                  01830000
               10  PV-SAVE-SLS-TX-AMT      PIC S9(07)V99 COMP-3.        01840000
               10  PV-SAVE-CRD-LVL-RSLT-CDE PIC  X(02).                 01850000
               10  PV-SAVE-CVV-RSPN-CDE    PIC  X(01).                  01860000
               10  PV-SAVE-E-BUS-IND       PIC  X(01).                  01870000
               10  PV-SAVE-NETWK-REF-ID    PIC  X(15).                  01880000
               10  PV-SAVE-TRSM-TMST       PIC  X(26).                  01890000
               10  PV-SAVE-SYS-TRC-AUD-NBR PIC  9(06).                  01900000
               10  PV-SAVE-TRK-DAT-CND-CDE PIC  X(02).                  01910000
C53632         10  PV-SAVE-OMNI-CHNL-FLMT-CDE                           01920000
C53632                                     PIC X(03).                   01930000
SHIP           10  PV-SAVE-VISA-MULT-SEQ-NBR PIC 9(04).                 01940000
SHIP           10  PV-SAVE-VISA-TOT-SHP-NBR  PIC 9(04).                 01950000
SHIP           10  PV-SAVE-FULFILLMENT-TYPE  PIC X(01).                 01960000
SHIP2          10  PV-SAVE-PAR-SHIP-IND      PIC X(01).                 01970000
062823         10  PV-SAVE-TRLING-TRN-IND    PIC X(01).                 01980000
062823         10  PV-SAVE-COBRAND-PYMT-IND  PIC X(01).                 01990000
                                                                        02000000
           05  PV-HOLD-TC005.                                           02010000
               10  PV-HOLD-PARTN-NBR        PIC S9(04)    COMP.         02020000
               10  PV-HOLD-STR-NBR          PIC S9(04)    COMP.         02030000
               10  PV-HOLD-RGST-ID          PIC S9(04)    COMP.         02040000
               10  PV-HOLD-TRN-NBR          PIC S9(04)    COMP.         02050000
               10  PV-HOLD-STRT-TM          PIC  X(08).                 02060000
               10  PV-HOLD-SLS-CORR-SEQ-NBR PIC S9(04)   COMP.          02070000
               10  PV-HOLD-TRN-TYP-CDE      PIC  X(02).                 02080000
               10  PV-HOLD-SLS-DTE          PIC  X(10).                 02090000
               10  PV-HOLD-TRN-DTE          PIC  X(10).                 02100000
               10  PV-HOLD-TNDR-SEQ-NBR     PIC S9(04)    COMP.         02110000
               10  PV-HOLD-TNDR-TYP-CDE     PIC  X(02).                 02120000
               10  PV-HOLD-TNDR-ID          PIC  X(33).                 02130000
               10  PV-HOLD-TNDR-AMT         PIC S9(07)V99 COMP-3.       02140000
               10  PV-HOLD-TNDR-ID-SRC-CDE  PIC  X(02).                 02150000
               10  PV-HOLD-AUTH-NBR         PIC  X(06).                 02160000
               10  PV-HOLD-AUTH-SRC-CDE     PIC  X(02).                 02170000
               10  PV-HOLD-CPS-TRN-ID       PIC S9(15)    COMP-3.       02180000
               10  PV-HOLD-MC-CMPLIN-CDE    PIC  X(04).                 02190000
               10  PV-HOLD-VIP-SRC-CDE      PIC  X(01).                 02200000
               10  PV-HOLD-VIP-RSPN-CDE     PIC  X(02).                 02210000
               10  PV-HOLD-BNKNET-NBR       PIC  X(15).                 02220000
               10  PV-HOLD-SETTLE-STORE-NBR PIC  9(04).                 02230000
               10  PV-HOLD-ECOMM-REAUTH-IND PIC  X(01).                 02240000
               10  PV-HOLD-ECOMM-ORD-NBR    PIC  X(25).                 02250000
               10  PV-HOLD-AUTH-REF-NBR     PIC  X(45).                 02260000
               10  PV-HOLD-TRAN-CDE         PIC  X(01).                 02270000
               10  PV-HOLD-MERCHANT-NBR     PIC  X(15).                 02280000
               10  PV-HOLD-AVS-RSPN-CDE     PIC  X(01).                 02290000
               10  PV-HOLD-VOID-ABRT-CDE    PIC  X(01).                 02300000
               10  PV-HOLD-SLS-TX-AMT       PIC S9(07)V99 COMP-3.       02310000
               10  PV-HOLD-CRD-LVL-RSLT-CDE PIC  X(02).                 02320000
               10  PV-HOLD-CVV-RSPN-CDE     PIC  X(01).                 02330000
               10  PV-HOLD-E-BUS-IND        PIC  X(01).                 02340000
               10  PV-HOLD-NETWK-REF-ID     PIC  X(15).                 02350000
               10  PV-HOLD-TRSM-TMST        PIC  X(26).                 02360000
               10  PV-HOLD-SYS-TRC-AUD-NBR  PIC  9(06).                 02370000
               10  PV-HOLD-TRK-DAT-CND-CDE  PIC  X(02).                 02380000
C53632         10  PV-HOLD-OMNI-CHNL-FLMT-CDE                           02390000
C53632                                      PIC X(03).                  02400000
SHIP           10  PV-HOLD-VISA-MULT-SEQ-NBR PIC 9(04).                 02410000
SHIP           10  PV-HOLD-VISA-TOT-SHP-NBR  PIC 9(04).                 02420000
SHIP           10  PV-HOLD-FULFILLMENT-TYPE  PIC X(01).                 02430000
SHIP2          10  PV-HOLD-PAR-SHIP-IND      PIC X(01).                 02440000
062823         10  PV-HOLD-TRLING-TRN-IND    PIC X(01).                 02441000
062823         10  PV-HOLD-COBRAND-PYMT-IND  PIC X(01).                 02442000
                                                                        02450000
      ******************************************************************02460000
      *  BANK FILE LAYOUT                                              *02470000
      ******************************************************************02480000
           COPY TCRD005.                                                02490000
                                                                        02500000
      ***************************************************************** 02510000
      * PROCEDURE DIVISION.                                           * 02520000
      ***************************************************************** 02530000
       PROCEDURE DIVISION.                                              02540000
                                                                        02550000
      ******************************************************************02560000
      * 0000-MAINLINE-PARAGRAPH                                        *02570000
      *      PRODUCE DAILY DEBIT EXCEPTION REPORT                      *02580000
      ******************************************************************02590000
       0000-MAINLINE-PARAGRAPH.                                         02600000
                                                                        02610000
           PERFORM 1000-INITIALIZE-PROGRAM.                             02620000
                                                                        02630000
           PERFORM 2000-PROCESS-BANK-DATA                               02640000
             UNTIL PS-END-OF-BANK-FILE.                                 02650000
                                                                        02660000
           PERFORM 8000-END-PROGRAM.                                    02670000
                                                                        02680000
           GOBACK.                                                      02690000
                                                                        02700000
      ******************************************************************02710000
      * 1000-INITIALIZE-PROGRAM                                         02720000
      *  -  OPEN FILES                                                  02730000
      *  -  READ 1ST RECORD ON BANK FILE                                02740000
      ******************************************************************02750000
       1000-INITIALIZE-PROGRAM.                                         02760000
                                                                        02770000
           OPEN INPUT  BANK-IN-FILE                                     02780000
                OUTPUT BANK-OUT-FILE.                                   02790000
                                                                        02800000
           INITIALIZE PV-SAVE-TC005                                     02810000
                      PV-HOLD-TC005.                                    02820000
           PERFORM 4000-READ-BANK-IN-FILE.                              02830000
                                                                        02840000
           MOVE ZEROES              TO PV-TOT-TNDR-AMT                  02850000
                                       PV-TOT-SLS-TX-AMT.               02860000
SHIP2      MOVE 'N'                 TO PS-OMNI-ORDER-SW.                02870000
           PERFORM 2500-MOVE-TC005-TO-SAVE-FIELDS.                      02880000
           SET PS-FIRST-TIME TO TRUE.                                   02890000
                                                                        02900000
      ******************************************************************02910000
      * FOR ECOMMERCE TRANSACTIONS -                                    02920000
      *   ADD THE AMOUNTS OF THE RECORDS TOGETHER IF THE ORDER NUMBER   02930000
      *       AND AUTH NUMBER ARE THE SAME                              02940000
      ******************************************************************02950000
       2000-PROCESS-BANK-DATA.                                          02960000
                                                                        02970000
      * MAKE SURE THE RECORD IS AN ECOMM RECORD BEFORE ADDING AMOUNTS   02980000
      *    TOGETHER                                                     02990000
           IF PV-SAVE-AUTH-NBR       =  TC005-AUTH-NBR      AND         03000000
              PV-SAVE-ECOMM-ORD-NBR  =  TC005-ECOMM-ORD-NBR AND         03010000
P9696         PV-SAVE-AUTH-REF-NBR   =  TC005-AUTH-REF-NBR  AND         03020000
SHIP2         PV-SAVE-FULFILLMENT-TYPE = TC005-FULFILLMENT-TYPE AND     03030000
              TC005-ECOMM-ORD-NBR NOT EQUAL SPACES AND                  03040000
              TC005-TRN-TYP-CDE = '01' AND                              03050000
SHIP          TC005-FULFILLMENT-TYPE =  'S'                             03060000
                 IF PS-FIRST-TIME                                       03070000
                    MOVE 'N' TO PS-FIRST-TIME-SW                        03080000
                 ELSE                                                   03090000
                    ADD PV-SAVE-TNDR-AMT     TO PV-TOT-TNDR-AMT         03100000
                    ADD PV-SAVE-SLS-TX-AMT   TO PV-TOT-SLS-TX-AMT       03110000
                    ADD +1                   TO PA-BANK-REC-CHG         03120000
SHIP2               MOVE 'Y'                 TO PS-ROLLUP-SW            03130000
                    PERFORM 2500-MOVE-TC005-TO-SAVE-FIELDS              03140000
                 END-IF                                                 03150000
           ELSE                                                         03160000
              IF PS-FIRST-TIME                                          03170000
                 MOVE 'N' TO PS-FIRST-TIME-SW                           03180000
              ELSE                                                      03190000
                 IF PV-TOT-TNDR-AMT NOT EQUAL ZERO AND                  03200000
SHIP2               PV-SAVE-FULFILLMENT-TYPE = 'S'                      03210000
                      ADD PV-SAVE-TNDR-AMT     TO PV-TOT-TNDR-AMT       03220000
                      ADD PV-SAVE-SLS-TX-AMT   TO PV-TOT-SLS-TX-AMT     03230000
                      MOVE PV-TOT-TNDR-AMT     TO PV-HOLD-TNDR-AMT      03240000
                      MOVE PV-TOT-SLS-TX-AMT   TO PV-HOLD-SLS-TX-AMT    03250000
                      MOVE ZEROES              TO PV-TOT-TNDR-AMT       03260000
                                                  PV-TOT-SLS-TX-AMT     03270000
                 ELSE                                                   03280000
SHIP2               IF PV-SAVE-FULFILLMENT-TYPE = 'S'                   03290000
SHIP2                  ADD PV-SAVE-TNDR-AMT    TO PV-HOLD-TNDR-AMT      03300000
SHIP2                  ADD PV-SAVE-SLS-TX-AMT  TO PV-HOLD-SLS-TX-AMT    03310000
SHIP2               ELSE                                                03320000
                       MOVE PV-SAVE-TNDR-AMT    TO PV-HOLD-TNDR-AMT     03330000
                       MOVE PV-SAVE-SLS-TX-AMT  TO PV-HOLD-SLS-TX-AMT   03340000
SHIP2               END-IF                                              03350000
                 END-IF                                                 03360000
                 PERFORM 2600-MOVE-SAVE-FIELDS-TO-HOLD                  03370000
                 WRITE BANK-OUT-RECORD FROM PV-HOLD-TC005               03380000
                 MOVE ZEROES TO PV-HOLD-TNDR-AMT                        03390000
                                PV-HOLD-SLS-TX-AMT                      03400000
                 ADD +1 TO PA-BANK-FILE-RECS-WRITTEN                    03410000
SHIP2            IF TC005-ECOMM-ORD-NBR NOT EQUAL PV-SAVE-ECOMM-ORD-NBR 03420000
SHIP2               MOVE 'N' TO PS-OMNI-ORDER-SW                        03430000
SHIP2               MOVE 'N' TO PS-FIRST-RECORD-SW                      03440000
SHIP2               MOVE 'N' TO PS-ROLLUP-SW                            03450000
SHIP2            END-IF                                                 03460000
                 PERFORM 2500-MOVE-TC005-TO-SAVE-FIELDS                 03470000
              END-IF                                                    03480000
           END-IF.                                                      03490000
                                                                        03500000
           PERFORM 4000-READ-BANK-IN-FILE.                              03510000
                                                                        03520000
                                                                        03530000
      ******************************************************************03540000
      *  MOVE THE FIELDS FROM THE FILE TO SAVE FIELDS                  *03550000
      ******************************************************************03560000
       2500-MOVE-TC005-TO-SAVE-FIELDS.                                  03570000
           MOVE TC005-PARTN-NBR     TO PV-SAVE-PARTN-NBR.               03580000
           MOVE TC005-STR-NBR       TO PV-SAVE-STR-NBR.                 03590000
           MOVE TC005-RGST-ID       TO PV-SAVE-RGST-ID.                 03600000
           MOVE TC005-TRN-NBR       TO PV-SAVE-TRN-NBR.                 03610000
           MOVE TC005-STRT-TM       TO PV-SAVE-STRT-TM.                 03620000
           MOVE TC005-SLS-CORR-SEQ-NBR TO PV-SAVE-SLS-CORR-SEQ-NBR.     03630000
           MOVE TC005-TRN-TYP-CDE   TO PV-SAVE-TRN-TYP-CDE.             03640000
           MOVE TC005-SLS-DTE       TO PV-SAVE-SLS-DTE.                 03650000
           MOVE TC005-TRN-DTE       TO PV-SAVE-TRN-DTE.                 03660000
           MOVE TC005-TNDR-SEQ-NBR  TO PV-SAVE-TNDR-SEQ-NBR.            03670000
           MOVE TC005-TNDR-TYP-CDE  TO PV-SAVE-TNDR-TYP-CDE.            03680000
           MOVE TC005-TNDR-ID       TO PV-SAVE-TNDR-ID.                 03690000
           MOVE TC005-TNDR-AMT      TO PV-SAVE-TNDR-AMT.                03700000
           MOVE TC005-TNDR-ID-SRC-CDE TO PV-SAVE-TNDR-ID-SRC-CDE.       03710000
           MOVE TC005-AUTH-NBR      TO PV-SAVE-AUTH-NBR.                03720000
           MOVE TC005-AUTH-SRC-CDE  TO PV-SAVE-AUTH-SRC-CDE.            03730000
           MOVE TC005-CPS-TRN-ID    TO PV-SAVE-CPS-TRN-ID.              03740000
           MOVE TC005-MC-CMPLIN-CDE TO PV-SAVE-MC-CMPLIN-CDE.           03750000
           MOVE TC005-VIP-SRC-CDE   TO PV-SAVE-VIP-SRC-CDE.             03760000
           MOVE TC005-VIP-RSPN-CDE  TO PV-SAVE-VIP-RSPN-CDE.            03770000
           MOVE TC005-BNKNET-NBR    TO PV-SAVE-BNKNET-NBR.              03780000
           MOVE TC005-SETTLE-STORE-NBR TO PV-SAVE-SETTLE-STORE-NBR.     03790000
           MOVE TC005-ECOMM-REAUTH-IND TO PV-SAVE-ECOMM-REAUTH-IND.     03800000
           MOVE TC005-ECOMM-ORD-NBR TO PV-SAVE-ECOMM-ORD-NBR.           03810000
           MOVE TC005-AUTH-REF-NBR  TO PV-SAVE-AUTH-REF-NBR.            03820000
           MOVE TC005-TRAN-CDE      TO PV-SAVE-TRAN-CDE.                03830000
           MOVE TC005-MERCHANT-NBR  TO PV-SAVE-MERCHANT-NBR.            03840000
           MOVE TC005-AVS-RSPN-CDE  TO PV-SAVE-AVS-RSPN-CDE.            03850000
           MOVE TC005-VOID-ABRT-CDE TO PV-SAVE-VOID-ABRT-CDE.           03860000
           MOVE TC005-SLS-TX-AMT    TO PV-SAVE-SLS-TX-AMT.              03870000
           MOVE TC005-CRD-LVL-RSLT-CDE TO PV-SAVE-CRD-LVL-RSLT-CDE.     03880000
           MOVE TC005-CVV-RSPN-CDE  TO PV-SAVE-CVV-RSPN-CDE.            03890000
           MOVE TC005-E-BUS-IND     TO PV-SAVE-E-BUS-IND.               03900000
           MOVE TC005-NETWK-REF-ID  TO PV-SAVE-NETWK-REF-ID.            03910000
           MOVE TC005-TRSM-TMST     TO PV-SAVE-TRSM-TMST.               03920000
           MOVE TC005-SYS-TRC-AUD-NBR TO PV-SAVE-SYS-TRC-AUD-NBR.       03930000
           MOVE TC005-TRK-DAT-CND-CDE TO PV-SAVE-TRK-DAT-CND-CDE.       03940000
C53632     MOVE TC005-OMNI-CHNL-FFLMT-TYP-CDE TO                        03950000
C53632                               PV-SAVE-OMNI-CHNL-FLMT-CDE.        03960000
SHIP       MOVE TC005-VISA-MULT-CLR-SEQ-NBR                             03970000
SHIP                                    TO PV-SAVE-VISA-MULT-SEQ-NBR.   03980000
SHIP2      IF TC005-VISA-MULT-CLR-SEQ-NBR = 1                           03990000
SHIP2         MOVE 'Y' TO PS-FIRST-RECORD-SW                            04000000
SHIP2      END-IF.                                                      04010000
SHIP       MOVE TC005-VISA-TOT-SHP-NBR  TO PV-SAVE-VISA-TOT-SHP-NBR.    04020000
SHIP       MOVE TC005-FULFILLMENT-TYPE  TO PV-SAVE-FULFILLMENT-TYPE.    04030000
SHIP2      IF TC005-FULFILLMENT-TYPE = 'O'                              04040000
SHIP2         MOVE 'Y' TO PS-OMNI-ORDER-SW                              04050000
SHIP2      END-IF.                                                      04060000
SHIP2      MOVE TC005-PAR-SHIP-IND      TO PV-SAVE-PAR-SHIP-IND.        04070000
062823     MOVE TC005-TRLING-TRN-IND    TO PV-SAVE-TRLING-TRN-IND.      04080000
062823     MOVE TC005-COBRAND-PYMT-IND  TO PV-SAVE-COBRAND-PYMT-IND.    04090000
                                                                        04100000
      ***************************************************************** 04110000
      * MOVE THE SAVE FIELDS TO THE HOLD FIELDS                       * 04120000
      ***************************************************************** 04130000
       2600-MOVE-SAVE-FIELDS-TO-HOLD.                                   04140000
           MOVE PV-SAVE-PARTN-NBR      TO PV-HOLD-PARTN-NBR.            04150000
           MOVE PV-SAVE-STR-NBR        TO PV-HOLD-STR-NBR.              04160000
           MOVE PV-SAVE-RGST-ID        TO PV-HOLD-RGST-ID.              04170000
           MOVE PV-SAVE-TRN-NBR        TO PV-HOLD-TRN-NBR.              04180000
           MOVE PV-SAVE-STRT-TM        TO PV-HOLD-STRT-TM.              04190000
           MOVE PV-SAVE-SLS-CORR-SEQ-NBR TO PV-HOLD-SLS-CORR-SEQ-NBR.   04200000
           MOVE PV-SAVE-TRN-TYP-CDE    TO PV-HOLD-TRN-TYP-CDE.          04210000
           MOVE PV-SAVE-SLS-DTE        TO PV-HOLD-SLS-DTE.              04220000
           MOVE PV-SAVE-TRN-DTE        TO PV-HOLD-TRN-DTE.              04230000
           MOVE PV-SAVE-TNDR-SEQ-NBR   TO PV-HOLD-TNDR-SEQ-NBR.         04240000
           MOVE PV-SAVE-TNDR-TYP-CDE   TO PV-HOLD-TNDR-TYP-CDE.         04250000
           MOVE PV-SAVE-TNDR-ID        TO PV-HOLD-TNDR-ID.              04260000
C53632     MOVE PV-SAVE-OMNI-CHNL-FLMT-CDE TO                           04270000
C53632                                PV-HOLD-OMNI-CHNL-FLMT-CDE.       04280000
           IF PV-HOLD-TNDR-AMT > ZERO                                   04290000
               CONTINUE                                                 04300000
           ELSE                                                         04310000
               MOVE PV-SAVE-TNDR-AMT   TO PV-HOLD-TNDR-AMT              04320000
               MOVE PV-SAVE-SLS-TX-AMT TO PV-HOLD-SLS-TX-AMT            04330000
           END-IF.                                                      04340000
           MOVE PV-SAVE-TNDR-ID-SRC-CDE TO PV-HOLD-TNDR-ID-SRC-CDE.     04350000
           MOVE PV-SAVE-AUTH-NBR       TO PV-HOLD-AUTH-NBR.             04360000
           MOVE PV-SAVE-AUTH-SRC-CDE   TO PV-HOLD-AUTH-SRC-CDE.         04370000
           MOVE PV-SAVE-CPS-TRN-ID     TO PV-HOLD-CPS-TRN-ID.           04380000
           MOVE PV-SAVE-MC-CMPLIN-CDE  TO PV-HOLD-MC-CMPLIN-CDE.        04390000
           MOVE PV-SAVE-VIP-SRC-CDE    TO PV-HOLD-VIP-SRC-CDE.          04400000
           MOVE PV-SAVE-VIP-RSPN-CDE   TO PV-HOLD-VIP-RSPN-CDE.         04410000
           MOVE PV-SAVE-BNKNET-NBR     TO PV-HOLD-BNKNET-NBR.           04420000
           MOVE PV-SAVE-SETTLE-STORE-NBR TO PV-HOLD-SETTLE-STORE-NBR.   04430000
           MOVE PV-SAVE-ECOMM-REAUTH-IND TO PV-HOLD-ECOMM-REAUTH-IND.   04440000
           MOVE PV-SAVE-ECOMM-ORD-NBR  TO PV-HOLD-ECOMM-ORD-NBR.        04450000
           MOVE PV-SAVE-AUTH-REF-NBR   TO PV-HOLD-AUTH-REF-NBR.         04460000
           MOVE PV-SAVE-TRAN-CDE       TO PV-HOLD-TRAN-CDE.             04470000
           MOVE PV-SAVE-MERCHANT-NBR   TO PV-HOLD-MERCHANT-NBR.         04480000
           MOVE PV-SAVE-AVS-RSPN-CDE   TO PV-HOLD-AVS-RSPN-CDE.         04490000
           MOVE PV-SAVE-VOID-ABRT-CDE  TO PV-HOLD-VOID-ABRT-CDE.        04500000
           MOVE PV-SAVE-CRD-LVL-RSLT-CDE TO PV-HOLD-CRD-LVL-RSLT-CDE.   04510000
           MOVE PV-SAVE-CVV-RSPN-CDE   TO PV-HOLD-CVV-RSPN-CDE.         04520000
           MOVE PV-SAVE-E-BUS-IND      TO PV-HOLD-E-BUS-IND.            04530000
           MOVE PV-SAVE-NETWK-REF-ID   TO PV-HOLD-NETWK-REF-ID.         04540000
           MOVE PV-SAVE-TRSM-TMST      TO PV-HOLD-TRSM-TMST.            04550000
           MOVE PV-SAVE-SYS-TRC-AUD-NBR TO PV-HOLD-SYS-TRC-AUD-NBR.     04560000
           MOVE PV-SAVE-TRK-DAT-CND-CDE TO PV-HOLD-TRK-DAT-CND-CDE.     04570000
SHIP       MOVE PV-SAVE-VISA-MULT-SEQ-NBR TO PV-HOLD-VISA-MULT-SEQ-NBR. 04580000
SHIP       MOVE PV-SAVE-VISA-TOT-SHP-NBR  TO PV-HOLD-VISA-TOT-SHP-NBR.  04590000
SHIP       MOVE PV-SAVE-FULFILLMENT-TYPE  TO PV-HOLD-FULFILLMENT-TYPE.  04600000
                                                                        04610000
SHIP2      IF PS-FIRST-RECORD-SW = 'Y' AND                              04620000
SHIP2         PV-SAVE-VISA-MULT-SEQ-NBR = PV-SAVE-VISA-TOT-SHP-NBR AND  04630000
SHIP2         PV-SAVE-VISA-TOT-SHP-NBR  = 1                             04640000
SHIP2            MOVE 'N' TO PV-SAVE-PAR-SHIP-IND                       04650000
SHIP2      ELSE                                                         04660000
SHIP2         IF PS-FIRST-RECORD-SW = 'Y' AND                           04670000
SHIP2           PV-SAVE-VISA-MULT-SEQ-NBR = PV-SAVE-VISA-TOT-SHP-NBR AND04680000
SHIP2           PS-NOT-OMNI-ORDER AND                                   04690000
SHIP2           PS-ROLLUP                                               04700000
SHIP2              MOVE 'N' TO PV-SAVE-PAR-SHIP-IND                     04710000
SHIP2         ELSE                                                      04720000
SHIP2            MOVE 'Y' TO PV-SAVE-PAR-SHIP-IND                       04730000
SHIP2         END-IF                                                    04740000
SHIP2      END-IF.                                                      04750000
SHIP2                                                                   04760000
112321     IF PV-HOLD-TNDR-TYP-CDE = '08'                               04770000
SHIP2          MOVE ' ' TO PV-SAVE-PAR-SHIP-IND                         04780000
SHIP2      END-IF.                                                      04790000
SHIP2                                                                   04800000
SHIP2      IF PV-HOLD-VISA-TOT-SHP-NBR = 1                              04810000
SHIP2          MOVE 'N' TO PV-SAVE-PAR-SHIP-IND                         04820000
SHIP2      END-IF.                                                      04830000
SHIP2                                                                   04840000
SHIP       IF PV-HOLD-VISA-MULT-SEQ-NBR = 0                             04850000
SHIP2          MOVE ' ' TO PV-SAVE-PAR-SHIP-IND                         04860000
SHIP2      END-IF.                                                      04870000
SHIP2                                                                   04880000
SHIP2      MOVE PV-SAVE-PAR-SHIP-IND      TO PV-HOLD-PAR-SHIP-IND.      04890000
062823     MOVE PV-SAVE-TRLING-TRN-IND    TO PV-HOLD-TRLING-TRN-IND.    04891000
062823     MOVE PV-SAVE-COBRAND-PYMT-IND  TO PV-HOLD-COBRAND-PYMT-IND.  04892000
                                                                        04900000
      ******************************************************************04910000
      *    READ MAF RECORD                                             *04920000
      ******************************************************************04930000
       4000-READ-BANK-IN-FILE.                                          04940000
                                                                        04950000
           INITIALIZE TC005-EXTRACT-RECORD.                             04960000
                                                                        04970000
           READ BANK-IN-FILE INTO TC005-EXTRACT-RECORD                  04980000
              AT END                                                    04990000
                 SET PS-END-OF-BANK-FILE TO TRUE                        05000000
           END-READ.                                                    05010000
                                                                        05020000
           IF PS-END-OF-BANK-FILE                                       05030000
              IF PV-SAVE-ECOMM-ORD-NBR NOT EQUAL SPACES                 05040000
                 ADD PV-SAVE-TNDR-AMT     TO PV-TOT-TNDR-AMT            05050000
                 ADD PV-SAVE-SLS-TX-AMT   TO PV-TOT-SLS-TX-AMT          05060000
              END-IF                                                    05070000
              IF PV-TOT-TNDR-AMT NOT EQUAL ZERO                         05080000
                 MOVE PV-TOT-TNDR-AMT     TO PV-HOLD-TNDR-AMT           05090000
                 MOVE PV-TOT-SLS-TX-AMT   TO PV-HOLD-SLS-TX-AMT         05100000
              ELSE                                                      05110000
                 MOVE PV-SAVE-TNDR-AMT    TO PV-HOLD-TNDR-AMT           05120000
                 MOVE PV-SAVE-SLS-TX-AMT  TO PV-HOLD-SLS-TX-AMT         05130000
              END-IF                                                    05140000
              IF PV-HOLD-TNDR-AMT NOT EQUAL ZERO                        05150000
                 PERFORM 2600-MOVE-SAVE-FIELDS-TO-HOLD                  05160000
                 WRITE BANK-OUT-RECORD FROM PV-HOLD-TC005               05170000
                 ADD +1 TO PA-BANK-FILE-RECS-WRITTEN                    05180000
              END-IF                                                    05190000
           ELSE                                                         05200000
              ADD +1 TO PA-BANK-FILE-RECS-READ                          05210000
           END-IF.                                                      05220000
                                                                        05230000
      ******************************************************************05240000
      * 8000-END-PROGRAM                                               *05250000
      *     CLOSE FILES                                                *05260000
      ******************************************************************05270000
       8000-END-PROGRAM.                                                05280000
                                                                        05290000
           DISPLAY 'TOTAL RECORDS READ '                                05300000
                        PA-BANK-FILE-RECS-READ.                         05310000
                                                                        05320000
           DISPLAY 'TOTAL RECORDS WRITTEN '                             05330000
                        PA-BANK-FILE-RECS-WRITTEN.                      05340000
                                                                        05350000
           DISPLAY 'TOTAL TRANSACTIONS SUMMARIZED '                     05360000
                        PA-BANK-REC-CHG.                                05370000
                                                                        05380000
           CLOSE BANK-IN-FILE                                           05390000
                 BANK-OUT-FILE.                                         05400000
                                                                        05410000
