       IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.              INKUT202.                                       
000400 AUTHOR.                  JIM SHIFF.                                      
000500 INSTALLATION.            KOHLS DEPARTMENT STORES.                        
000600 DATE-WRITTEN.            04/14/00.                                       
000700 DATE-COMPILED.                                                           
000800                                                                          
000900******************************************************************        
001000*              TOTAL PIECE COUNT BY AREA FOR A SPECIFIC STORE.            
001100*                                                                         
001200* THIS PROGRAM WILL EXTRACT FROM  TSUMINV AND TINVDTL TABLES.             
001300* THE INPUT FILE WILL SUPPLY THE STORES TO USE AND THE OUTPUT FILE        
001400* WILL BE THE DATA FROM THE TABLES.                                       
001500*                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
W21732* W21732   11-01-2002  NON-INTELLIGENT SKU. - IGSI                        
25248 * 25248    10-06-2003  UPC CAPABILITIES -ROD JANSSEN                      
W26600* W26600   06-  -2004  PHYSICAL INVENTORY STORE EXPANSION                 
W26600*                      M JOHNSON                                          
003400******************************************************************        
003500                                                                          
003800 ENVIRONMENT DIVISION.                                                    
004100 CONFIGURATION SECTION.                                                   
004300 SOURCE-COMPUTER. IBM-3090.                                               
004400 OBJECT-COMPUTER. IBM-3090.                                               
004700 INPUT-OUTPUT SECTION.                                                    
004900 FILE-CONTROL.                                                            
005000                                                                          
005100     SELECT INV-COUNT-FILE                                                
005200         ASSIGN TO INP01.                                                 
005300                                                                          
005400     SELECT INVENTORY-EXTRACT-FILE                                        
005500         ASSIGN TO OUT01.                                                 
005600                                                                          
006200 DATA DIVISION.                                                           
006500 FILE SECTION.                                                            
006600                                                                          
006700 FD  INV-COUNT-FILE                                                       
006800     RECORDING MODE IS F                                                  
006900     LABEL RECORDS ARE STANDARD                                           
007000     BLOCK CONTAINS 0 RECORDS.                                            
007200 01  INV-COUNT-RECORD                 PIC X(80).                          
007300                                                                          
007400 FD  INVENTORY-EXTRACT-FILE                                               
007500     RECORDING MODE IS F                                                  
007600     LABEL RECORDS ARE STANDARD                                           
007700     BLOCK CONTAINS 0 RECORDS.                                            
007900 01  INVENTORY-EXTRACT-RECORD         PIC X(30).                          
008000                                                                          
008900 WORKING-STORAGE SECTION.                                                 
010300****************************************************************          
010400*  ERROR MESSAGE AREA FOR DB2                                             
010500****************************************************************          
010600                                                                          
010700     COPY DPWS004.                                                        
010800                                                                          
012100 01  PS-PROGRAM-SWITCHES.                                                 
012200     05  PS-END-OF-INV-COUNT-SW       PIC X      VALUE 'N'.               
012300         88  PS-END-OF-INV-COUNT                 VALUE 'Y'.               
012300         88  PS-MORE-INV-COUNT                   VALUE 'N'.               
012700     05  PS-END-OF-STORE-CUR-SW       PIC X      VALUE 'N'.               
012800         88  PS-END-OF-STORE-CUR                 VALUE 'Y'.               
012800         88  PS-MORE-STORE-CUR                   VALUE 'N'.               
013500                                                                          
014600 01  ABEND-CODE                       PIC S9(04) COMP                     
014700                                                 VALUE ZERO.              
013600 01  PV-PROGRAM-VARIABLES.                                                
014100     05  PV-DB2-OPER-ATTEMPTED        PIC X(30).                          
014200     05  PV-PARAGRAPH-NAME            PIC X(30).                          
                                                                                
      *****************************************************************         
      * RECORD LAYOUT OF OUTPUT FILE                                            
      *****************************************************************         
           COPY INWS004.                                                        
      *****************************************************************         
      * RECORD LAYOUT OF OUTPUT FILE                                            
      *****************************************************************         
25248      COPY INRD135.                                                        
                                                                                
014800                                                                          
014900****************************************************************          
015000* INVENTORY SUM TABLE                                                     
015100****************************************************************          
015200                                                                          
015300     EXEC SQL                                                             
015400         INCLUDE TSUMINV                                                  
015500     END-EXEC.                                                            
015600                                                                          
015700****************************************************************          
015800* INVENTORY DETAIL TABLE                                                  
015900****************************************************************          
016000                                                                          
016100     EXEC SQL                                                             
016200         INCLUDE TINVDTL                                                  
016300     END-EXEC.                                                            
016400                                                                          
      ******************************************************************        
      *  COMMUNICATION AREA FOR DB2                                             
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
                                                                                
019700****************************************************************          
019800*  INVENTORY CUROSR                                                       
019900****************************************************************          
020000                                                                          
020100     EXEC SQL                                                             
020200        DECLARE INV-CSR CURSOR FOR                                        
020300            SELECT                                                        
W26600                LOC_NBR,                                                  
                      AREA_NBR,                                                 
W21732                REC_SKU_NBR,                                              
25248                 REC_QTY,                                                  
25248                 REC_UPC_NBR                                               
020500            FROM                                                          
020600                TINVDTL A                                                 
W26600            WHERE A.LOC_NBR     = :INVDTL-LOC-NBR                         
022000                                                                          
022100        UNION ALL                                                         
022200                                                                          
022300            SELECT                                                        
W26600                LOC_NBR,                                                  
                      AREA_NBR,                                                 
W26600                SKU_NBR AS REC_SKU_NBR,                                   
W26600                INV_QTY AS REC_QTY,                                       
W26600                UPC_NBR AS REC_UPC_NBR                                    
022500            FROM                                                          
022600                TSUMINV B                                                 
W21732            WHERE B.LOC_NBR     = :SUMINV-LOC-NBR                         
W26600        ORDER BY LOC_NBR, AREA_NBR, REC_SKU_NBR                           
023000        FOR FETCH ONLY                                                    
W26600        WITH UR                                                           
023100     END-EXEC.                                                            
023200                                                                          
027100 PROCEDURE DIVISION.                                                      
027200                                                                          
027300                                                                          
027400 0000-INKUT202.                                                           
027500                                                                          
           PERFORM 1200-INITIALIZATION.                                         
027900                                                                          
           PERFORM 2000-MAINLINE                                                
029000         UNTIL PS-END-OF-INV-COUNT.                                       
                                                                                
029100     CLOSE INV-COUNT-FILE                                                 
029200           INVENTORY-EXTRACT-FILE.                                        
029400                                                                          
W26600     GOBACK.                                                              
029600                                                                          
       1200-INITIALIZATION.                                                     
027600     OPEN INPUT  INV-COUNT-FILE                                           
027700          OUTPUT INVENTORY-EXTRACT-FILE.                                  
029700                                                                          
030000     PERFORM 4000-READ-INV-COUNT-FILE.                                    
030200     IF PS-MORE-INV-COUNT                                                 
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY '*****************'                                      
               DISPLAY ' EMPTY INPUT FILE'                                      
               DISPLAY '*****************'                                      
           END-IF.                                                              
                                                                                
029800 2000-MAINLINE.                                                           
030100                                                                          
030200     IF PS-MORE-INV-COUNT                                                 
030700         PERFORM 2100-TABLE-CURSOR                                        
030800     END-IF.                                                              
030900                                                                          
030000     PERFORM 4000-READ-INV-COUNT-FILE.                                    
                                                                                
033500 2100-TABLE-CURSOR.                                                       
033600                                                                          
           MOVE  'N' TO  PS-END-OF-STORE-CUR-SW.                                
           PERFORM 8000-OPEN-STORE-CURSOR.                                      
033600                                                                          
           PERFORM 3000-EXTRACT-STORE-FOR-TOTALS                                
              UNTIL PS-END-OF-STORE-CUR.                                        
033600                                                                          
           PERFORM 8300-CLOSE-STORE-CURSOR.                                     
033600                                                                          
033600                                                                          
045200 3000-EXTRACT-STORE-FOR-TOTALS.                                           
045300                                                                          
049400     PERFORM 8600-FETCH-INVENTORY-CUR.                                    
045300                                                                          
045300     IF PS-MORE-STORE-CUR                                                 
              PERFORM 7100-WRITE-STORE-FILE                                     
049300     END-IF.                                                              
049500                                                                          
049600                                                                          
049700 4000-READ-INV-COUNT-FILE.                                                
049800                                                                          
049900     READ INV-COUNT-FILE INTO IN004-INVENTORY-COUNT-RECORD                
050000         AT END                                                           
050000            MOVE 'Y' TO PS-END-OF-INV-COUNT-SW.                           
050100                                                                          
050200                                                                          
050300 7100-WRITE-STORE-FILE.                                                   
050400                                                                          
050500     WRITE INVENTORY-EXTRACT-RECORD                                       
050600         FROM INRD135-PIECE-EXTR-REC.                                     
050700                                                                          
065600 8000-OPEN-STORE-CURSOR.                                                  
                                                                                
W26600     MOVE IN004-STR-NBR  TO  INVDTL-LOC-NBR                               
W26600                             SUMINV-LOC-NBR.                              
065700                                                                          
065800     EXEC SQL                                                             
065900          OPEN INV-CSR                                                    
066000     END-EXEC.                                                            
066100                                                                          
066200     EVALUATE TRUE                                                        
066300         WHEN SQLCODE = ZERO                                              
066400             CONTINUE                                                     
066500         WHEN OTHER                                                       
066600             MOVE '8000-OPEN-STORE-CURSOR '                               
066700                                          TO PV-PARAGRAPH-NAME            
066800             MOVE 'OPEN STORE-CURSOR'     TO PV-DB2-OPER-ATTEMPTED        
066900             PERFORM 9998-SQL-ABEND                                       
067000     END-EVALUATE.                                                        
067100                                                                          
067100                                                                          
067200 8300-CLOSE-STORE-CURSOR.                                                 
067100                                                                          
065800     EXEC SQL                                                             
065900         CLOSE INV-CSR                                                    
066000     END-EXEC.                                                            
066100                                                                          
066200     EVALUATE TRUE                                                        
066300         WHEN SQLCODE = ZERO                                              
066400             CONTINUE                                                     
066500         WHEN OTHER                                                       
066600             MOVE '8300-CLOSE STORE '                                     
066700                                          TO PV-PARAGRAPH-NAME            
066800             MOVE 'CLOSE STORE CUR  '     TO PV-DB2-OPER-ATTEMPTED        
066900             PERFORM 9998-SQL-ABEND                                       
067000     END-EVALUATE.                                                        
067100                                                                          
072900 8600-FETCH-INVENTORY-CUR.                                                
073000                                                                          
073100     EXEC SQL                                                             
073200         FETCH                                                            
073300             INV-CSR                                                      
073400         INTO                                                             
W26600            :INVDTL-LOC-NBR                                               
W21732           ,:INVDTL-AREA-NBR                                              
W21732           ,:INVDTL-REC-SKU-NBR                                           
W21732           ,:INVDTL-REC-QTY                                               
25248            ,:INVDTL-REC-UPC-NBR                                           
075200     END-EXEC.                                                            
075300                                                                          
075400     EVALUATE TRUE                                                        
075500         WHEN SQLCODE = ZERO                                              
W26600             MOVE IN004-STR-NBR       TO IN135-STORE                      
W21732             MOVE INVDTL-AREA-NBR     TO IN135-AREA                       
W21732             MOVE INVDTL-REC-SKU-NBR  TO IN135-SKU                        
W21732             MOVE INVDTL-REC-QTY      TO IN135-QTY                        
25248              MOVE INVDTL-REC-UPC-NBR  TO IN135-UPC-CODE                   
075700         WHEN SQLCODE = +100                                              
075800             MOVE 'Y' TO PS-END-OF-STORE-CUR-SW                           
075900         WHEN OTHER                                                       
076000             MOVE '8600-FETCH-STORE-CSR' TO PV-PARAGRAPH-NAME             
076100             MOVE 'FETCH STORE-CSR'      TO PV-DB2-OPER-ATTEMPTED         
076200             PERFORM 9998-SQL-ABEND                                       
076300     END-EVALUATE.                                                        
076500                                                                          
078100                                                                          
078200 9998-SQL-ABEND.                                                          
078300                                                                          
078400     MOVE +4013 TO ABEND-CODE.                                            
078500     DISPLAY ' '.                                                         
078600     DISPLAY '** ABEND **         ** = SQLERROR'.                         
078700     DISPLAY '** PROGRAM          ** = INKUT202'.                         
078800     DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
078900     DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
079000                                                                          
079100*    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
079200*    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
079300     COPY DPPD004.                                                        
079400                                                                          
079500     PERFORM 9999-ABEND.                                                  
079600                                                                          
079700                                                                          
079800 9999-ABEND.                                                              
079900                                                                          
080000     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
