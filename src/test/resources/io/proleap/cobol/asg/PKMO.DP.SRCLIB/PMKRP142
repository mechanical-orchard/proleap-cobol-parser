       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         PMKRP142.                                            
       AUTHOR.             TODD C. MICHALEK.                                    
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       OCTOBER 2005.                                        
       DATE-COMPILED.                                                           
      ******************************************************************        
      * MERCHANT VENDOR AD SPACE ASSIGNMENT REPORTING                  *        
      ******************************************************************        
      * 10/06/2005 - TODD C. MICHALEK - WR29320                        *        
      * - NEW PROGRAM                                                  *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT CONTROL-FILE         ASSIGN TO INP01.                         
           SELECT VENDOR-AD-SPACE-FILE ASSIGN TO INP02.                         
           SELECT REPORT-FILE          ASSIGN TO RPT01.                         
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-FILE                                                         
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-RECORD                   PIC  X(80).                         
                                                                                
       FD  VENDOR-AD-SPACE-FILE                                                 
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  VENDOR-AD-SPACE-REC              PIC  X(300).                        
                                                                                
       FD  REPORT-FILE                                                          
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  REPORT-RECORD                    PIC  X(200).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)    VALUE +0 COMP.        
                                                                                
      ******************************************************************        
      *    CR-CONTROL RECORD                                           *        
      ******************************************************************        
       01  CR-CONTROL-RECORD.                                                   
           05  CR-PERIOD-END-DATE.                                              
               10  CR-PERIOD-END-DTE-MM     PIC  X(02)   VALUE SPACES.          
               10  FILLER                   PIC  X(01)   VALUE SPACES.          
               10  CR-PERIOD-END-DTE-DD     PIC  X(02)   VALUE SPACES.          
               10  FILLER                   PIC  X(01)   VALUE SPACES.          
               10  CR-PERIOD-END-DTE-CC     PIC  X(02)   VALUE SPACES.          
               10  CR-PERIOD-END-DTE-YY     PIC  X(02)   VALUE SPACES.          
           05  FILLER                       PIC  X(01)   VALUE SPACES.          
           05  CR-TYPE-OF-BATCH-RUN         PIC  X(01)   VALUE SPACES.          
               88  CR-END-OF-WEEK                        VALUE 'W'.             
               88  CR-END-OF-MONTH                       VALUE 'M'.             
               88  CR-END-OF-QUARTER                     VALUE 'Q'.             
           05  FILLER                       PIC  X(68)   VALUE SPACES.          
      ******************************************************************        
      *    PC-PROGRAM CONSTANTS                                        *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME             PIC  X(08) VALUE 'PMKRP142'.         
           05  PC-ONE                      PIC  9(01) VALUE 1.                  
           05  PC-TWO                      PIC  9(01) VALUE 2.                  
           05  PC-DASH                     PIC  X(03) VALUE ' - '.              
           05  PC-HEADER-LINE-COUNT        PIC S9(03) VALUE +11                 
                                                      COMP.                     
           05  PC-MAX-LINES-PER-PAGE       PIC S9(03) VALUE +64                 
                                                      COMP.                     
           05  PC-GMA-HEADING              PIC  X(08) VALUE                     
               'GMM:    '.                                                      
           05  PC-DMA-HEADING              PIC  X(08) VALUE                     
               'DMM:    '.                                                      
           05  PC-BYR-HEADING              PIC  X(08) VALUE                     
               'BUYER:  '.                                                      
           05  PC-PROMOTION-TOTAL          PIC  X(16) VALUE                     
               'PROMOTION TOTAL '.                                              
           05  PC-STD-TOTAL                PIC  X(16) VALUE                     
               '      STD TOTAL '.                                              
           05  PC-TAB-TOTAL                PIC  X(16) VALUE                     
               '      TAB TOTAL '.                                              
           05  PC-FLEXI-TOTAL              PIC  X(16) VALUE                     
               '    FLEXI TOTAL '.                                              
           05  PC-SUNDAY-TOTAL             PIC  X(16) VALUE                     
               '   SUNDAY TOTAL '.                                              
           05  PC-WEEKEND-TOTAL            PIC  X(16) VALUE                     
               '  WEEKEND TOTAL '.                                              
           05  PC-MID-WEEK-TOTAL           PIC  X(16) VALUE                     
               ' MID-WEEK TOTAL '.                                              
           05  PC-VENDOR-TOTAL             PIC  X(16) VALUE                     
               '   VENDOR TOTAL '.                                              
      ******************************************************************        
      *    RESPROGRAM VARIABLES                                        *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC  X(30)     VALUE SPACES.        
           05  PV-HOLD-PROMO-END-TMST       PIC  X(16)     VALUE SPACES.        
           05  PV-HOLD-MDA-SIZE             PIC  X(07)     VALUE SPACES.        
           05  PV-HOLD-SECTION              PIC  X(08)     VALUE SPACES.        
           05  PV-HOLD-VND-NM               PIC  X(30)     VALUE SPACES.        
           05  PV-HOLD-BYR-NBR              PIC  X(04)     VALUE SPACES.        
           05  PV-HOLD-DMA-NBR              PIC  X(04)     VALUE SPACES.        
           05  PV-HOLD-GMA-NBR              PIC  X(04)     VALUE SPACES.        
           05  PV-PROMO-TOTAL-AD-SP-SIZE    PIC  9V9(03)   VALUE ZEROS          
                                                           COMP-3.              
           05  PV-PROMO-TOTAL-COST          PIC  9(08)V9(02)                    
                                                           VALUE ZEROS          
                                                           COMP-3.              
           05  PV-MDA-SIZE-TOTAL-AD-SP-SIZE PIC  9V9(03)   VALUE ZEROS          
                                                           COMP-3.              
           05  PV-MDA-SIZE-TOTAL-COST       PIC  9(08)V9(02)                    
                                                           VALUE ZEROS          
                                                           COMP-3.              
           05  PV-SECTION-TOTAL-AD-SP-SIZE  PIC  9V9(03)   VALUE ZEROS          
                                                           COMP-3.              
           05  PV-SECTION-TOTAL-COST        PIC  9(08)V9(02)                    
                                                           VALUE ZEROS          
                                                           COMP-3.              
           05  PV-VENDOR-TOTAL-AD-SP-SIZE   PIC  9V9(03)   VALUE ZEROS          
                                                           COMP-3.              
           05  PV-VENDOR-TOTAL-COST         PIC  9(08)V9(02)                    
                                                           VALUE ZEROS          
                                                           COMP-3.              
           05  PV-CURRENT-DATE.                                                 
               10  PV-CURRENT-YEAR          PIC  9(02)     VALUE ZERO.          
               10  PV-CURRENT-MONTH         PIC  9(02)     VALUE ZERO.          
               10  PV-CURRENT-DAY           PIC  9(02)     VALUE ZERO.          
           05  PV-CURRENT-TIME.                                                 
               10  PV-CURRENT-HOUR          PIC  9(02)     VALUE ZERO.          
               10  PV-CURRENT-MINUTE        PIC  9(02)     VALUE ZERO.          
               10  FILLER                   PIC  9(04)     VALUE ZERO.          
           05  PV-VENDOR-ADSP-TOT-VALUES.                                       
               10  PV-VENDOR-ADSP-TOT-WEEKLY.                                   
                   15  FILLER               PIC  X(12)     VALUE                
                       'WEEK ENDING '.                                          
                   15  PV-VENDOR-ADSP-TOT-WEEK-DATE                             
                                            PIC  X(10)     VALUE SPACES.        
                   15  FILLER               PIC  X(03)     VALUE SPACES.        
               10  PV-VENDOR-ADSP-TOT-MONTHLY.                                  
                   15  FILLER               PIC  X(13)     VALUE                
                       'MONTH ENDING '.                                         
                   15  PV-VENDOR-ADSP-TOT-MONTH-DATE                            
                                            PIC  X(10)     VALUE SPACES.        
                   15  FILLER               PIC  X(02)     VALUE SPACES.        
               10  PV-VENDOR-ADSP-TOT-QUARTERLY.                                
                   15  FILLER               PIC  X(15)     VALUE                
                       'QUARTER ENDING '.                                       
                   15  PV-VENDOR-ADSP-TOT-QUARTER-DT                            
                                            PIC  X(10)     VALUE SPACES.        
      ******************************************************************        
      *    PS-PROGRAM SWITCHES                                         *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-CONTROL-FILE-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-CONTROL-FILE                  VALUE 'Y'.           
           05  PS-END-OF-VENDOR-AD-FILE-SW    PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-VENDOR-AD-FILE                VALUE 'Y'.           
      ******************************************************************        
      *    PA-PROGRAM ACCUMULATORS                                     *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-REPORT-RECORDS-WRITTEN      PIC S9(11)   COMP-3               
                                                           VALUE ZEROES.        
           05  PA-LINE-COUNTER                PIC S9(11)   COMP-3               
                                                           VALUE ZEROES.        
           05  PA-PAGE-COUNTER                PIC S9(11)   COMP-3               
                                                           VALUE ZEROES.        
      ******************************************************************        
      * COPYBOOK FOR THE VENDOR AD SPACE ASSIGNMENT EXTRACT FILE       *        
      ******************************************************************        
           COPY PMRD138.                                                        
      ******************************************************************        
      *    STANDARD HEADER FOR 200 COLUMN REPORT                       *        
      ******************************************************************        
           COPY DPWS200O.                                                       
                                                                                
       01  REPORT-TITLE-LINE.                                                   
           05  FILLER                         PIC  X(87)   VALUE SPACES.        
           05  FILLER                         PIC  X(26)   VALUE                
               'VENDOR AD SPACE ASSIGNMENT'.                                    
           05  FILLER                         PIC  X(87)   VALUE SPACES.        
                                                                                
       01  REPORT-TITLE-LINE2.                                                  
           05  FILLER                         PIC  X(80)   VALUE SPACES.        
           05  RPT-TITLE-LINE2-VALUE          PIC  X(40)   VALUE SPACES.        
           05  FILLER                         PIC  X(80)   VALUE SPACES.        
                                                                                
       01  REPORT-TITLE-LINE2-VALUES.                                           
           05  RPT-TL2-WEEKLY.                                                  
               10  FILLER                     PIC  X(28)   VALUE                
                   ' FOR THE FISCAL WEEK ENDING '.                              
               10  RPT-TL2-WEEKLY-DATE        PIC  X(10)   VALUE SPACES.        
               10  FILLER                     PIC  X(02)   VALUE SPACES.        
           05  RPT-TL2-MONTHLY.                                                 
               10  FILLER                     PIC  X(29)   VALUE                
                   ' FOR THE FISCAL MONTH ENDING '.                             
               10  RPT-TL2-MONTHLY-DATE       PIC  X(10)   VALUE SPACES.        
               10  FILLER                     PIC  X(01)   VALUE SPACES.        
           05  RPT-TL2-QUARTERLY.                                               
               10  FILLER                     PIC  X(30)   VALUE                
                   'FOR THE FISCAL QUARTER ENDING '.                            
               10  RPT-TL2-QUARTERLY-DATE     PIC  X(10)   VALUE SPACES.        
                                                                                
       01  REPORT-HEADING-LINE.                                                 
           05  RPT-HL-HEADING                 PIC  X(08)   VALUE SPACES.        
           05  RPT-HL-GMA-DMA-BYR-NBR         PIC  9(04)   VALUE ZEROS.         
           05  FILLER                         PIC  X(01)   VALUE SPACES.        
           05  RPT-HL-GMA-DMA-BYR-NAME        PIC  X(30)   VALUE SPACES.        
           05  FILLER                         PIC  X(157)  VALUE SPACES.        
                                                                                
       01  REPORT-HEADING-LINE1.                                                
           05  FILLER                         PIC  X(08)   VALUE                
               'VENDOR: '.                                                      
           05  RPT-HL1-VND-NBR                PIC  X(10)   VALUE SPACES.        
           05  FILLER                         PIC  X(03)   VALUE ' - '.         
           05  RPT-HL1-VND-NM                 PIC  X(30)   VALUE SPACES.        
           05  FILLER                         PIC  X(149)  VALUE SPACES.        
                                                                                
       01  REPORT-HEADING-LINE2.                                                
           05  FILLER                         PIC  X(120)  VALUE SPACES.        
           05  FILLER                         PIC  X(40)   VALUE                
               '                             SPACE SPACE'.                      
           05  FILLER                         PIC  X(40)   VALUE SPACES.        
                                                                                
       01  REPORT-HEADING-LINE3.                                                
           05  FILLER                         PIC  X(40)   VALUE                
               '         PAGE COST  AD EVENT            '.                      
           05  FILLER                         PIC  X(40)   VALUE                
               '            PROMOTION                   '.                      
           05  FILLER                         PIC  X(40)   VALUE                
               '    PROMOTION DATES                     '.                      
           05  FILLER                         PIC  X(40)   VALUE                
               '# OF PAGES PAGE # SEQUENCE # CODE  SIZE '.                      
           05  FILLER                         PIC  X(40)   VALUE                
               '     TOTAL COST                         '.                      
                                                                                
       01  REPORT-DETAIL-LINE1.                                                 
           05  RPT-DL1-SECTION                PIC  X(08).                       
           05  FILLER                         PIC  X(192)  VALUE SPACES.        
                                                                                
       01  REPORT-DETAIL-LINE2.                                                 
           05  RPT-DL2-MDA-SIZE               PIC  X(07).                       
           05  FILLER                         PIC  X(02)   VALUE SPACES.        
           05  RPT-DL2-VNDADSP-COST-SPACES    PIC  X(10).                       
           05  RPT-DL2-VNDADSP-COST REDEFINES                                   
               RPT-DL2-VNDADSP-COST-SPACES    PIC  $$,$$$,$$9.                  
           05  FILLER                         PIC  X(01)   VALUE SPACES.        
           05  RPT-DL2-AD-EVNT-NBR-TXT        PIC  X(10)   VALUE SPACES.        
           05  FILLER                         PIC  X(01)   VALUE SPACES.        
           05  RPT-DL2-AD-EVNT-NM             PIC  X(20)   VALUE SPACES.        
           05  FILLER                         PIC  X(01)   VALUE SPACES.        
           05  RPT-DL2-PROMO-NBR-TXT          PIC  X(10)   VALUE SPACES.        
           05  FILLER                         PIC  X(01)   VALUE SPACES.        
           05  RPT-DL2-PROMO-NM               PIC  X(20)   VALUE SPACES.        
           05  FILLER                         PIC  X(01)   VALUE SPACES.        
           05  RPT-DL2-PROMO-STRT-TMST        PIC  X(16)   VALUE SPACES.        
           05  RPT-DL2-DASH                   PIC  X(03)   VALUE SPACES.        
           05  RPT-DL2-PROMO-END-TMST         PIC  X(16)   VALUE SPACES.        
           05  FILLER                         PIC  X(06)   VALUE SPACES.        
           05  RPT-DL2-MDA-PG-QTY             PIC  Z(05)   VALUE SPACES.        
           05  FILLER                         PIC  X(04)   VALUE SPACES.        
           05  RPT-DL2-AD-PG-NBR              PIC  Z(03)   VALUE SPACES.        
           05  FILLER                         PIC  X(08)   VALUE SPACES.        
           05  RPT-DL2-AD-PG-SEQ-NBR          PIC  Z(03)   VALUE SPACES.        
           05  FILLER                         PIC  X(01)   VALUE SPACES.        
           05  RPT-DL2-AD-SP-CDE              PIC  X(04)   VALUE SPACES.        
           05  FILLER                         PIC  X(02)   VALUE SPACES.        
           05  RPT-DL2-AD-SP-SIZE             PIC  Z.999   VALUE SPACES.        
           05  FILLER                         PIC  X(01)   VALUE SPACES.        
           05  RPT-DL2-TOTAL-COST             PIC  $$$,$$$,$$$.99               
                                                           VALUE ZEROS.         
           05  FILLER                         PIC  X(25)   VALUE SPACES.        
                                                                                
       01  REPORT-TOTAL-LINE.                                                   
           05  FILLER                         PIC  X(138)  VALUE SPACES.        
           05  RPT-TL-TYPE-TOTAL              PIC  X(16)   VALUE SPACES.        
           05  FILLER                         PIC  X(01)   VALUE SPACES.        
           05  RPT-TL-AD-SP-SIZE              PIC  Z.999   VALUE ZEROS.         
           05  FILLER                         PIC  X(01)   VALUE SPACES.        
           05  RPT-TL-TOTAL-COST              PIC  $$$,$$$,$$$.99               
                                                           VALUE ZEROS.         
           05  FILLER                         PIC  X(25)   VALUE SPACES.        
                                                                                
       01  REPORT-BLANK-LINE                  PIC  X(200)  VALUE SPACES.        
                                                                                
       01  REPORT-ENDING-LINE.                                                  
           05  FILLER                         PIC  X(90)   VALUE SPACES.        
           05  FILLER                         PIC  X(19)   VALUE                
               '***END OF REPORT***'.                                           
           05  FILLER                         PIC  X(91)   VALUE SPACES.        
      ******************************************************************        
      *    DATE ROUTINE COPY MEMBERS                                   *        
      ******************************************************************        
           COPY DPWS500.                                                        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAINLINE.                                                           
           PERFORM B100-INITIALIZE.                                             
                                                                                
           IF PS-END-OF-VENDOR-AD-FILE                                          
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM B200-PROCESS-REPORT                                      
                   UNTIL PS-END-OF-VENDOR-AD-FILE                               
               PERFORM D200-PRINT-PROMO-TOTAL                                   
               PERFORM D300-PRINT-MDA-SIZE-TOTAL                                
               PERFORM D400-PRINT-SECTION-TOTAL                                 
               PERFORM D500-PRINT-VENDOR-TOTAL                                  
               PERFORM D800-PROCESS-PAGE-FOOTER                                 
               PERFORM D100-PROCESS-PAGE-BREAK                                  
           END-IF.                                                              
                                                                                
           PERFORM B900-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
      ******************************************************************        
      *    B100-INITIALIZE                                             *        
      ******************************************************************        
       B100-INITIALIZE.                                                         
           OPEN INPUT CONTROL-FILE                                              
                      VENDOR-AD-SPACE-FILE                                      
           OPEN OUTPUT REPORT-FILE.                                             
                                                                                
           READ CONTROL-FILE INTO CR-CONTROL-RECORD                             
               AT END SET PS-END-OF-CONTROL-FILE TO TRUE.                       
                                                                                
           IF PS-END-OF-CONTROL-FILE                                            
               DISPLAY ' '                                                      
               DISPLAY '** ABEND     **'                                        
               DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME                    
               DISPLAY '** PARAGRAPH **  = B100-INITIALIZE      '               
               DISPLAY '** CONTROL FILE IS EMPTY  '                             
               DISPLAY ' '                                                      
               MOVE +4002 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           ELSE                                                                 
               PERFORM  Z815-DISPLAY-CONTROLS                                   
           END-IF.                                                              
                                                                                
           ACCEPT PV-CURRENT-DATE      FROM DATE.                               
           MOVE PV-CURRENT-YEAR          TO DP200O-RUN-YEAR.                    
           MOVE PV-CURRENT-MONTH         TO DP200O-RUN-MONTH.                   
           MOVE PV-CURRENT-DAY           TO DP200O-RUN-DAY.                     
                                                                                
           ACCEPT PV-CURRENT-TIME      FROM TIME.                               
           MOVE PV-CURRENT-HOUR          TO DP200O-RUN-HOUR.                    
           MOVE PV-CURRENT-MINUTE        TO DP200O-RUN-MINUTE.                  
                                                                                
           MOVE PC-PROGRAM-NAME          TO DP200O-PROGRAM-NAME.                
           MOVE PC-ONE                   TO DP200O-REPORT-NUMBER.               
                                                                                
           PERFORM C100-READ-VENDOR-AD-SPACE-FILE.                              
                                                                                
           PERFORM C200-INIT-REPORT-TITLES-OUTPUT.                              
                                                                                
           IF PS-END-OF-VENDOR-AD-FILE                                          
               ADD PC-ONE                TO PA-PAGE-COUNTER                     
               MOVE PA-PAGE-COUNTER      TO DP200O-PAGE-NUMBER                  
                                                                                
               WRITE REPORT-RECORD                                              
                   FROM DP200O-STANDRD-HEADER-200-COLS                          
                   AFTER ADVANCING PAGE                                         
                                                                                
               INITIALIZE DP200O-LASER-NEW-PAGE-INDICATR                        
                                                                                
               WRITE REPORT-RECORD                                              
                   FROM REPORT-TITLE-LINE                                       
                   AFTER ADVANCING 1 LINE                                       
                                                                                
               WRITE REPORT-RECORD                                              
                   FROM REPORT-TITLE-LINE2                                      
                   AFTER ADVANCING 1 LINE                                       
                                                                                
           ELSE                                                                 
               MOVE PM138-GMA-NBR      TO PV-HOLD-GMA-NBR                       
               MOVE PM138-DMA-NBR      TO PV-HOLD-DMA-NBR                       
               MOVE PM138-BYR-NBR      TO PV-HOLD-BYR-NBR                       
               MOVE PM138-VND-NBR      TO RPT-HL1-VND-NBR                       
               MOVE PM138-VND-NM       TO RPT-HL1-VND-NM                        
                                          PV-HOLD-VND-NM                        
               MOVE PM138-MDA-SIZE     TO PV-HOLD-MDA-SIZE                      
               MOVE PM138-SECTION      TO PV-HOLD-SECTION                       
               MOVE PM138-PROMO-END-TMST                                        
                                       TO PV-HOLD-PROMO-END-TMST                
               PERFORM D100-PROCESS-PAGE-BREAK                                  
               PERFORM C300-PRINT-DETAIL-LINE1                                  
               PERFORM C400-INIT-DETAIL-LINE2                                   
               PERFORM C500-PRINT-DETAIL-LINE2                                  
           END-IF.                                                              
      ******************************************************************        
      *    B200-PROCESS-REPORT                                         *        
      ******************************************************************        
       B200-PROCESS-REPORT.                                                     
           IF PM138-GMA-NBR = PV-HOLD-GMA-NBR AND                               
              PM138-DMA-NBR = PV-HOLD-DMA-NBR AND                               
              PM138-BYR-NBR = PV-HOLD-BYR-NBR AND                               
              PM138-VND-NM = PV-HOLD-VND-NM                                     
              IF PM138-SECTION = PV-HOLD-SECTION                                
                 IF PM138-MDA-SIZE = PV-HOLD-MDA-SIZE                           
                     IF PM138-PROMO-END-TMST = PV-HOLD-PROMO-END-TMST           
                         PERFORM C500-PRINT-DETAIL-LINE2                        
                     ELSE                                                       
                         PERFORM D200-PRINT-PROMO-TOTAL                         
                         PERFORM C400-INIT-DETAIL-LINE2                         
                         PERFORM C500-PRINT-DETAIL-LINE2                        
                     END-IF                                                     
                 ELSE                                                           
                     PERFORM D200-PRINT-PROMO-TOTAL                             
                     PERFORM D300-PRINT-MDA-SIZE-TOTAL                          
                     PERFORM C400-INIT-DETAIL-LINE2                             
                     PERFORM C500-PRINT-DETAIL-LINE2                            
                 END-IF                                                         
              ELSE                                                              
                  PERFORM D200-PRINT-PROMO-TOTAL                                
                  PERFORM D300-PRINT-MDA-SIZE-TOTAL                             
                  PERFORM D400-PRINT-SECTION-TOTAL                              
                  PERFORM C300-PRINT-DETAIL-LINE1                               
                  PERFORM C400-INIT-DETAIL-LINE2                                
                  PERFORM C500-PRINT-DETAIL-LINE2                               
              END-IF                                                            
           ELSE                                                                 
               PERFORM D200-PRINT-PROMO-TOTAL                                   
               PERFORM D300-PRINT-MDA-SIZE-TOTAL                                
               PERFORM D400-PRINT-SECTION-TOTAL                                 
               PERFORM D500-PRINT-VENDOR-TOTAL                                  
               PERFORM D800-PROCESS-PAGE-FOOTER                                 
               PERFORM D100-PROCESS-PAGE-BREAK                                  
               PERFORM C300-PRINT-DETAIL-LINE1                                  
               PERFORM C400-INIT-DETAIL-LINE2                                   
               PERFORM C500-PRINT-DETAIL-LINE2                                  
           END-IF.                                                              
      ******************************************************************        
      *    B900-EOJ-ROUTINE                                            *        
      ******************************************************************        
       B900-EOJ-ROUTINE.                                                        
           ADD PC-TWO  TO PA-LINE-COUNTER.                                      
                                                                                
           IF PA-LINE-COUNTER >= PC-MAX-LINES-PER-PAGE                          
               PERFORM D100-PROCESS-PAGE-BREAK                                  
           END-IF.                                                              
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM REPORT-ENDING-LINE                                          
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           PERFORM D800-PROCESS-PAGE-FOOTER.                                    
                                                                                
           CLOSE CONTROL-FILE                                                   
                 VENDOR-AD-SPACE-FILE                                           
                 REPORT-FILE.                                                   
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'MESSAGES FOR PROGRAM ' PC-PROGRAM-NAME.                     
           DISPLAY 'NUMBER OF REPORT RECORDS WRITTEN:  '                        
               PA-REPORT-RECORDS-WRITTEN.                                       
           DISPLAY ' '.                                                         
      ******************************************************************        
      *    C100-READ-VENDOR-AD-SPACE-FILE                              *        
      ******************************************************************        
       C100-READ-VENDOR-AD-SPACE-FILE.                                          
           READ VENDOR-AD-SPACE-FILE INTO PM138-VENDOR-ADSP-REC                 
               AT END SET PS-END-OF-VENDOR-AD-FILE TO TRUE.                     
      ******************************************************************        
      *    C200-INIT-REPORT-TITLES-OUTPUT                              *        
      ******************************************************************        
       C200-INIT-REPORT-TITLES-OUTPUT.                                          
           EVALUATE TRUE                                                        
               WHEN CR-END-OF-WEEK                                              
                   MOVE CR-PERIOD-END-DATE                                      
                                       TO RPT-TL2-WEEKLY-DATE                   
                                          PV-VENDOR-ADSP-TOT-WEEK-DATE          
                   MOVE RPT-TL2-WEEKLY TO RPT-TITLE-LINE2-VALUE                 
               WHEN CR-END-OF-MONTH                                             
                   MOVE CR-PERIOD-END-DATE                                      
                                       TO RPT-TL2-MONTHLY-DATE                  
                                          PV-VENDOR-ADSP-TOT-MONTH-DATE         
                   MOVE RPT-TL2-MONTHLY                                         
                                       TO RPT-TITLE-LINE2-VALUE                 
               WHEN CR-END-OF-QUARTER                                           
                   MOVE CR-PERIOD-END-DATE                                      
                                       TO RPT-TL2-QUARTERLY-DATE                
                                          PV-VENDOR-ADSP-TOT-QUARTER-DT         
                   MOVE RPT-TL2-QUARTERLY                                       
                                       TO RPT-TITLE-LINE2-VALUE                 
           END-EVALUATE.                                                        
      ******************************************************************        
      *    C300-PRINT-DETAIL-LINE1                                     *        
      ******************************************************************        
       C300-PRINT-DETAIL-LINE1.                                                 
           ADD PC-ONE                    TO PA-LINE-COUNTER.                    
           IF PA-LINE-COUNTER >= PC-MAX-LINES-PER-PAGE                          
               PERFORM D100-PROCESS-PAGE-BREAK                                  
               ADD PC-ONE                TO PA-LINE-COUNTER                     
           END-IF.                                                              
                                                                                
           MOVE PM138-SECTION          TO RPT-DL1-SECTION.                      
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM REPORT-DETAIL-LINE1                                         
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           INITIALIZE REPORT-DETAIL-LINE1.                                      
      ******************************************************************        
      *     C400-INIT-DETAIL-LINE2                                     *        
      ******************************************************************        
       C400-INIT-DETAIL-LINE2.                                                  
           MOVE PM138-MDA-SIZE         TO RPT-DL2-MDA-SIZE.                     
           MOVE PM138-COST-PER-PAGE    TO RPT-DL2-VNDADSP-COST.                 
           MOVE PM138-AD-EVNT-NBR-TXT  TO RPT-DL2-AD-EVNT-NBR-TXT.              
           MOVE PM138-AD-EVNT-NM       TO RPT-DL2-AD-EVNT-NM.                   
           MOVE PM138-PROMO-NBR-TXT    TO RPT-DL2-PROMO-NBR-TXT.                
           MOVE PM138-PROMO-NM         TO RPT-DL2-PROMO-NM.                     
           MOVE PM138-PROMO-STRT-TMST  TO RPT-DL2-PROMO-STRT-TMST.              
           MOVE PC-DASH                TO RPT-DL2-DASH.                         
           MOVE PM138-PROMO-END-TMST   TO RPT-DL2-PROMO-END-TMST.               
           MOVE PM138-MDA-PG-QTY       TO RPT-DL2-MDA-PG-QTY.                   
      ******************************************************************        
      *     C500-PRINT-DETAIL-LINE2                                    *        
      ******************************************************************        
       C500-PRINT-DETAIL-LINE2.                                                 
           ADD PC-ONE                    TO PA-LINE-COUNTER.                    
           IF PA-LINE-COUNTER >= PC-MAX-LINES-PER-PAGE                          
               PERFORM D100-PROCESS-PAGE-BREAK                                  
               ADD PC-ONE                TO PA-LINE-COUNTER                     
           END-IF.                                                              
                                                                                
           MOVE PM138-AD-PG-NBR        TO RPT-DL2-AD-PG-NBR.                    
           MOVE PM138-AD-PG-SEQ-NBR    TO RPT-DL2-AD-PG-SEQ-NBR.                
           MOVE PM138-AD-SP-CDE        TO RPT-DL2-AD-SP-CDE.                    
           MOVE PM138-AD-SP-SIZE       TO RPT-DL2-AD-SP-SIZE.                   
           MOVE PM138-EST-COST         TO RPT-DL2-TOTAL-COST.                   
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM REPORT-DETAIL-LINE2                                         
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           ADD PC-ONE  TO PA-REPORT-RECORDS-WRITTEN.                            
           ADD PM138-AD-SP-SIZE        TO PV-PROMO-TOTAL-AD-SP-SIZE             
                                          PV-MDA-SIZE-TOTAL-AD-SP-SIZE          
                                          PV-SECTION-TOTAL-AD-SP-SIZE           
                                          PV-VENDOR-TOTAL-AD-SP-SIZE.           
           ADD PM138-EST-COST          TO PV-PROMO-TOTAL-COST                   
                                          PV-MDA-SIZE-TOTAL-COST                
                                          PV-SECTION-TOTAL-COST                 
                                          PV-VENDOR-TOTAL-COST.                 
           INITIALIZE REPORT-DETAIL-LINE2.                                      
                                                                                
           PERFORM C100-READ-VENDOR-AD-SPACE-FILE.                              
      ******************************************************************        
      *     D100-PROCESS-PAGE-BREAK                                    *        
      ******************************************************************        
       D100-PROCESS-PAGE-BREAK.                                                 
           ADD PC-ONE                TO PA-PAGE-COUNTER.                        
           MOVE PA-PAGE-COUNTER      TO DP200O-PAGE-NUMBER.                     
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM DP200O-STANDRD-HEADER-200-COLS                              
               AFTER ADVANCING PAGE.                                            
           INITIALIZE DP200O-LASER-NEW-PAGE-INDICATR.                           
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM REPORT-TITLE-LINE                                           
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM REPORT-TITLE-LINE2                                          
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE PC-GMA-HEADING         TO RPT-HL-HEADING.                       
           MOVE PM138-GMA-NBR          TO RPT-HL-GMA-DMA-BYR-NBR.               
           STRING PM138-GMM-FIRST-NM                                            
               DELIMITED BY SPACE                                               
               ' '                                                              
               PM138-GMM-LAST-NM                                                
               DELIMITED BY SIZE INTO RPT-HL-GMA-DMA-BYR-NAME.                  
           WRITE REPORT-RECORD                                                  
               FROM REPORT-HEADING-LINE                                         
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           MOVE PC-DMA-HEADING         TO RPT-HL-HEADING.                       
           MOVE PM138-DMA-NBR          TO RPT-HL-GMA-DMA-BYR-NBR.               
           STRING PM138-DMM-FIRST-NM                                            
               DELIMITED BY SPACE                                               
               ' '                                                              
               PM138-DMM-LAST-NM                                                
               DELIMITED BY SIZE INTO RPT-HL-GMA-DMA-BYR-NAME.                  
           WRITE REPORT-RECORD                                                  
               FROM REPORT-HEADING-LINE                                         
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           MOVE PC-BYR-HEADING         TO RPT-HL-HEADING.                       
           MOVE PM138-BYR-NBR          TO RPT-HL-GMA-DMA-BYR-NBR.               
           STRING PM138-BYR-FIRST-NM                                            
               DELIMITED BY SPACE                                               
               ' '                                                              
               PM138-BYR-LAST-NM                                                
               DELIMITED BY SIZE INTO RPT-HL-GMA-DMA-BYR-NAME.                  
           WRITE REPORT-RECORD                                                  
               FROM REPORT-HEADING-LINE                                         
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM REPORT-HEADING-LINE1                                        
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM REPORT-HEADING-LINE2                                        
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM REPORT-HEADING-LINE3                                        
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM REPORT-BLANK-LINE                                           
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE PC-HEADER-LINE-COUNT TO PA-LINE-COUNTER.                        
      ******************************************************************        
      *     D200-PRINT-PROMO-TOTAL.                                    *        
      ******************************************************************        
       D200-PRINT-PROMO-TOTAL.                                                  
           MOVE PC-PROMOTION-TOTAL     TO RPT-TL-TYPE-TOTAL.                    
           MOVE PV-PROMO-TOTAL-AD-SP-SIZE                                       
                                       TO RPT-TL-AD-SP-SIZE.                    
           MOVE PV-PROMO-TOTAL-COST    TO RPT-TL-TOTAL-COST.                    
           PERFORM D650-PRINT-TOTAL-LINE-DOUBLE.                                
           PERFORM D700-PRINT-BLANK-LINE.                                       
           MOVE ZEROS                  TO PV-PROMO-TOTAL-AD-SP-SIZE             
                                          PV-PROMO-TOTAL-COST.                  
           MOVE PM138-PROMO-END-TMST   TO PV-HOLD-PROMO-END-TMST.               
      ******************************************************************        
      *    D300-PRINT-MDA-SIZE-TOTAL.                                  *        
      *    NOTE: BECAUSE THE PROMOTION TOTAL LINE CAN BE FOLLOWED BY A *        
      *          DOUBLE SPACED MDA SIZE TOTAL LINE OR A SINGLE SPACED  *        
      *          DETAIL LINE, AND A SPACE WAS NEEDED BETWEEN A         *        
      *          PROMOTION TOTAL LINE AND A DETAIL LINE COMBINATION,   *        
      *          A BLANK LINE IS ALWAYS PRINTED AFTER A PROMOTION      *        
      *          TOTAL LINE, RESULTING IN THE NEED TO HAVE CHANGED THE *        
      *          MDA SIZE TOTAL LINE TO PRINT AFTER ADVANCING 1 LINE   *        
      ******************************************************************        
       D300-PRINT-MDA-SIZE-TOTAL.                                               
           EVALUATE PV-HOLD-MDA-SIZE                                            
               WHEN 'STD    '                                                   
                   MOVE PC-STD-TOTAL   TO RPT-TL-TYPE-TOTAL                     
               WHEN 'TAB    '                                                   
                   MOVE PC-TAB-TOTAL   TO RPT-TL-TYPE-TOTAL                     
               WHEN 'FLEXI  '                                                   
                   MOVE PC-FLEXI-TOTAL TO RPT-TL-TYPE-TOTAL                     
           END-EVALUATE.                                                        
           MOVE PV-MDA-SIZE-TOTAL-AD-SP-SIZE                                    
                                       TO RPT-TL-AD-SP-SIZE.                    
           MOVE PV-MDA-SIZE-TOTAL-COST TO RPT-TL-TOTAL-COST.                    
           PERFORM D600-PRINT-TOTAL-LINE-SINGLE.                                
           PERFORM D700-PRINT-BLANK-LINE.                                       
           MOVE ZEROS                  TO PV-MDA-SIZE-TOTAL-AD-SP-SIZE          
                                          PV-MDA-SIZE-TOTAL-COST.               
                                                                                
           MOVE PM138-MDA-SIZE         TO PV-HOLD-MDA-SIZE.                     
      ******************************************************************        
      *    D400-PRINT-SECTION-TOTAL                                    *        
      *    NOTE: BECAUSE THE MDA SIZE TOTAL LINE CAN BE FOLLOWED BY A  *        
      *          DOUBLE SPACED SECTION TOTAL LINE OR A SINGLE SPACED   *        
      *          DETAIL LINE, AND A SPACE WAS NEEDED BETWEEN A         *        
      *          MDA SIZE TOTAL LINE AND A DETAIL LINE COMBINATION,    *        
      *          A BLANK LINE IS ALWAYS PRINTED AFTER A MDA SIZE       *        
      *          TOTAL LINE, RESULTING IN THE NEED TO HAVE CHANGED THE *        
      *          SECTION TOTAL LINE TO PRINT AFTER ADVANCING 1 LINE    *        
      ******************************************************************        
       D400-PRINT-SECTION-TOTAL.                                                
           EVALUATE PV-HOLD-SECTION                                             
               WHEN 'SUNDAY  '                                                  
                   MOVE PC-SUNDAY-TOTAL                                         
                                       TO RPT-TL-TYPE-TOTAL                     
               WHEN 'WEEKEND '                                                  
                   MOVE PC-WEEKEND-TOTAL                                        
                                       TO RPT-TL-TYPE-TOTAL                     
               WHEN 'MID-WEEK'                                                  
                   MOVE PC-MID-WEEK-TOTAL                                       
                                       TO RPT-TL-TYPE-TOTAL                     
           END-EVALUATE.                                                        
           MOVE PV-SECTION-TOTAL-AD-SP-SIZE                                     
                                       TO RPT-TL-AD-SP-SIZE.                    
           MOVE PV-SECTION-TOTAL-COST  TO RPT-TL-TOTAL-COST.                    
           PERFORM D600-PRINT-TOTAL-LINE-SINGLE.                                
           MOVE ZEROS                  TO PV-SECTION-TOTAL-AD-SP-SIZE           
                                          PV-SECTION-TOTAL-COST.                
           MOVE PM138-SECTION          TO PV-HOLD-SECTION.                      
      ******************************************************************        
      *    D500-PRINT-VENDOR-TOTAL                                     *        
      ******************************************************************        
       D500-PRINT-VENDOR-TOTAL.                                                 
           MOVE PC-VENDOR-TOTAL        TO RPT-TL-TYPE-TOTAL.                    
           MOVE PV-VENDOR-TOTAL-AD-SP-SIZE                                      
                                       TO RPT-TL-AD-SP-SIZE.                    
           MOVE PV-VENDOR-TOTAL-COST   TO RPT-TL-TOTAL-COST.                    
           PERFORM D650-PRINT-TOTAL-LINE-DOUBLE.                                
           MOVE ZEROS                  TO PV-VENDOR-TOTAL-AD-SP-SIZE            
                                          PV-VENDOR-TOTAL-COST.                 
           MOVE PM138-GMA-NBR          TO PV-HOLD-GMA-NBR.                      
           MOVE PM138-DMA-NBR          TO PV-HOLD-DMA-NBR.                      
           MOVE PM138-BYR-NBR          TO PV-HOLD-BYR-NBR.                      
           MOVE PM138-VND-NBR          TO RPT-HL1-VND-NBR.                      
           MOVE PM138-VND-NM           TO RPT-HL1-VND-NM                        
                                          PV-HOLD-VND-NM.                       
      ******************************************************************        
      *    D600-PRINT-TOTAL-LINE-SINGLE                                *        
      ******************************************************************        
       D600-PRINT-TOTAL-LINE-SINGLE.                                            
           ADD PC-ONE                TO PA-LINE-COUNTER.                        
           IF PA-LINE-COUNTER >= PC-MAX-LINES-PER-PAGE                          
               PERFORM D100-PROCESS-PAGE-BREAK                                  
               ADD PC-ONE            TO PA-LINE-COUNTER                         
           END-IF.                                                              
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM REPORT-TOTAL-LINE                                           
               AFTER ADVANCING 1 LINES.                                         
           INITIALIZE REPORT-TOTAL-LINE.                                        
      ******************************************************************        
      *    D650-PRINT-TOTAL-LINE-DOUBLE                                *        
      ******************************************************************        
       D650-PRINT-TOTAL-LINE-DOUBLE.                                            
           ADD PC-TWO                TO PA-LINE-COUNTER.                        
           IF PA-LINE-COUNTER >= PC-MAX-LINES-PER-PAGE                          
               PERFORM D100-PROCESS-PAGE-BREAK                                  
               ADD PC-TWO            TO PA-LINE-COUNTER                         
           END-IF.                                                              
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM REPORT-TOTAL-LINE                                           
               AFTER ADVANCING 2 LINES.                                         
           INITIALIZE REPORT-TOTAL-LINE.                                        
      ******************************************************************        
      *    D700-PRINT-BLANK-LINE                                       *        
      ******************************************************************        
       D700-PRINT-BLANK-LINE.                                                   
           ADD PC-ONE                TO PA-LINE-COUNTER.                        
           IF PA-LINE-COUNTER >= PC-MAX-LINES-PER-PAGE                          
               PERFORM D100-PROCESS-PAGE-BREAK                                  
               ADD PC-ONE            TO PA-LINE-COUNTER                         
           END-IF.                                                              
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM REPORT-BLANK-LINE                                           
               AFTER ADVANCING 1 LINES.                                         
           INITIALIZE REPORT-TOTAL-LINE.                                        
      ******************************************************************        
      *    D800-PROCESS-PAGE-FOOTER                                    *        
      ******************************************************************        
       D800-PROCESS-PAGE-FOOTER.                                                
           WRITE REPORT-RECORD                                                  
               FROM DP200O-ATRNY-CLNT-HDR-200-COLS                              
               AFTER ADVANCING 2 LINES.                                         
      ******************************************************************        
      *    Z815-DISPLAY-CONTROLS                                       *        
      ******************************************************************        
       Z815-DISPLAY-CONTROLS.                                                   
           DISPLAY ' '.                                                         
           DISPLAY 'CONTROLS FOR PROGRAM ' PC-PROGRAM-NAME ': '.                
           DISPLAY ' '.                                                         
           DISPLAY 'PERIOD END DTE:  ' CR-PERIOD-END-DATE.                      
           DISPLAY '   TYPE OF RUN:  ' CR-TYPE-OF-BATCH-RUN.                    
           DISPLAY ' '.                                                         
