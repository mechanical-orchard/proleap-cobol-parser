       TITLE 'PURGE CARTON DIRECTIVES'.                                         
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    SUKUT146.                                                 
       AUTHOR.        SUDHAN VEERASINGH.                                        
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN   MARCH 2010.                                               
       DATE-COMPILED.                                                           
      ******************************************************************        
      *  SUT_CART_DIRV_TRKG IS POPULATED BY THE CARTON AUDIT           *        
      *  APPLICATION. CARTON DIRECTIVES ARE CURRENTLY ONLY BEING USED  *        
      *  AT OTTAWA DC(DC890). WE CURRENTLY PURGE DIRECIVES THAT ARE    *        
      *  GREATER THAN X DAYS OLD(TKRPRPM - RELPRG). THIS WILL BE AN    *        
      *  ADDITIONAL PURGE TO ALSO PURGE RELEASED DIRECTIVES AFTER X    *        
      *  DAYS OLD(TKRPRPM - RELPRG).                                   *        
      *                                                                *        
      *  THIS PROGRAM WILL USE A CURSOR TO DETERMINE THE CARTONS ON THE*        
      *  DIRECTIVE TABLE THAT CAN BE DELETED.                          *        
      *                                                                *        
      *  FOR EACH ROW READ FROM THE CURSOR, A SEPARATE CURSOR WILL BE  *        
      *  USED TO GET ALL OF THE ROWS FOR THAT CARTON AND WRITE THEM TO *        
      *  THE EXTRACT FILE IN SURD095 LAYOUT.                           *        
      *                                                                *        
      *  THE OUTPUT FILE WILL BE USED BY PROGRAM SUKUP026 TO PURGE THE *        
      *  ROWS.                                                         *        
      *                                                                *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT UNLOAD-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  UNLOAD-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
      *    FROM SUT00102                                                        
       01  UNLOAD-REC                       PIC X(168).                         
      *                                                                         
      /                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
           COPY SURD095.                                                        
                                                                                
           COPY DPWS004.                                                        
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SUT00102                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TCOCNTL                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TKRPRPM                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TSUCRDV                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
       01  ABEND-CODE                      PIC S9(04)    VALUE +0               
                                                         COMP SYNC.             
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-PURGE-CSR-SW       PIC  X(01) VALUE 'N'.               
               88  PS-END-PURGE-CSR-YES                VALUE 'Y'.               
               88  PS-END-PURGE-CSR-NO                 VALUE 'N'.               
                                                                                
           05  PS-END-OF-DRIVER-CSR-SW      PIC  X(01) VALUE 'N'.               
               88  PS-END-DRIVER-CSR-YES               VALUE 'Y'.               
               88  PS-END-DRIVER-CSR-NO                VALUE 'N'.               
                                                                                
           05  PS-DIRECTIVE-RSN-CODE              PIC X(03).                    
               88  PS-AUDIT-NO-LONG-REQD          VALUE 'AUN'.                  
               88  PS-AUDIT-PASSED                VALUE 'AUP'.                  
               88  PS-CARTON-RECLAIMED            VALUE 'RCL'.                  
               88  PS-CARTON-RLSD-VIA-CRTN-AUD    VALUE 'RLC'.                  
               88  PS-MANUALLY-RLSD               VALUE 'RLF'.                  
               88  PS-CARTON-RLSD-VIA-ONLINE      VALUE 'RLO'.                  
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08) VALUE 'SUKUT146'.         
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
           05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
           05  PV-WRITE-DRIVE-COUNTER      PIC  S9(9) COMP.                     
           05  PV-FETCH-DRIVE-COUNTER      PIC  S9(9) COMP.                     
           05  PV-FETCH-PURGE              PIC  S9(9) COMP.                     
           05  PV-DIVERT-COUNT             PIC  S9(9) COMP.                     
           05  PV-RLSE-COUNT               PIC  S9(9) COMP.                     
           05  PV-SHIP-UNIT-ZERO-CNT       PIC  S9(9) COMP.                     
           05  PV-TSUCRDV-NOT-FOUND        PIC  S9(9) COMP.                     
           05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
           05  PV-SHIPT-UNT-ID             PIC S9(18)V COMP-3                   
                                                     VALUE ZEROES.              
           05  PV-FUNC-QTY                 PIC 9(9)  VALUE ZEROES.              
           05  PV-DIVERT-RSN-CDE           PIC X(03) VALUE SPACES.              
           05  PV-OPER-UNT-ID              PIC  S9(4) COMP                      
                                                     VALUE ZEROES.              
           05  PV-BRCDE-NBR                PIC X(22) VALUE SPACES.              
           05  PV-TIME-STAMP               PIC X(26) VALUE SPACES.              
           05  PV-CRE-TMST                 PIC X(26) VALUE SPACES.              
           05  PV-CURRENT-TIME-STAMP       PIC X(26) VALUE SPACES.              
           05  PV-PURGE-B4-DATE            PIC X(26) VALUE SPACES.              
      /                                                                         
           EXEC SQL                                                             
               DECLARE DRIVER-CSR CURSOR FOR                                    
               SELECT                                                           
                   A.BRCDE_NBR                                                  
                  ,A.CRE_TMST                                                   
                  ,A.OPER_UNT_ID                                                
                  ,A.DIRV_RSN_CDE                                               
                  ,A.SHIPT_UNT_ID                                               
                FROM  SUT_CART_DIRV_TRKG A                                      
               WHERE  A.CRE_TMST <= :PV-PURGE-B4-DATE                           
                 AND  A.CRE_TMST IN                                             
                 (SELECT MAX(B.CRE_TMST)                                        
                      FROM SUT_CART_DIRV_TRKG B                                 
                      WHERE A.BRCDE_NBR = B.BRCDE_NBR)                          
           END-EXEC.                                                            
      /                                                                         
           EXEC SQL                                                             
               DECLARE PURGE-CSR CURSOR FOR                                     
               SELECT                                                           
                   BRCDE_NBR                                                    
                  ,CRE_TMST                                                     
                  ,OPER_UNT_ID                                                  
                  ,DIRV_RSN_CDE                                                 
                  ,LGCL_LOC_NM                                                  
                  ,AUD_CNTL_NBR                                                 
                  ,SHIPT_UNT_ID                                                 
                  ,AUD_STAT_CDE                                                 
                  ,PO_NBR                                                       
                  ,RCVR_SEQ_NBR                                                 
                  ,PO_LNE_NBR                                                   
                  ,PREPK_ID                                                     
                  ,STR_NBR                                                      
                  ,CRE_PGM_NM                                                   
                  ,ACKNL_SEQ_NBR                                                
                  ,CASI_CDE                                                     
                  ,CART_STAT_CDE                                                
                  ,AUD_FALR_RSN_NM                                              
                  ,STR_ZN_ID                                                    
                  ,SEASET_YR                                                    
                  ,SEASET_SHRT_NM                                               
                  ,CNVYR_PRC_ACTN_NM                                            
                  ,CRE_BY_USR_ID                                                
                FROM  SUT_CART_DIRV_TRKG                                        
               WHERE  BRCDE_NBR = :PV-BRCDE-NBR                                 
           END-EXEC.                                                            
      /                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * THIS MAINLINE CONTROLS THE ENTIRE PROGRAM FLOW.                *        
      ******************************************************************        
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-DRIVER-CSR                                      
                   UNTIL PS-END-DRIVER-CSR-YES                                  
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
      /                                                                         
      ******************************************************************        
      * THIS PARAGRAPH DOES THE FOLLOWING FUNCTIONALITIES.             *        
      * 01. OPEN THE OUTPUT FILE.                                      *        
      * 02. GET THE DATE BEFORE FOR WHICH DATA NEEDS TO BE PURGED.     *        
      * 03. OPEN THE DRIVER CURSOR.                                    *        
      * 04. FETCH THE FIRST RECORD FROM THE DRIVER CURSOR.             *        
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN OUTPUT UNLOAD-FILE                                              
                                                                                
           DISPLAY '**************************************************'.        
           DISPLAY '                                        '.                  
           DISPLAY '** PROGRAM SUKUT146 EXECUTION STARTED **'.                  
           DISPLAY '                                        '.                  
                                                                                
           PERFORM 1100-GET-PURGE-B4-DATE.                                      
           DISPLAY '** SUKUT146 - NUMBER OF DAYS TO PURGE : '                   
           PV-FUNC-QTY                                                          
           DISPLAY '** SUKUT146 - CURRENT TIMESTAMP       : '                   
           PV-CURRENT-TIME-STAMP                                                
           DISPLAY '** SUKUT146 - PURGE BEFORE DATE       : '                   
           PV-PURGE-B4-DATE                                                     
           DISPLAY '                                        '.                  
           DISPLAY '**************************************************'.        
                                                                                
           PERFORM 8100-OPEN-DRIVER-CSR.                                        
           PERFORM 8200-FETCH-DRIVER-CSR.                                       
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH SELECTS THE PURGE BEFORE DATE FROM THE  TKRPRPM *        
      * TABLE USING THE PARM 'RELPRG'  AND THE FUNC_QTY VALUE FOR      *        
      * 'RELPRG' PARAMETER.                                            *        
      ******************************************************************        
                                                                                
       1100-GET-PURGE-B4-DATE.                                                  
                                                                                
           MOVE '1100-GET-PURGE-B4-DATE'    TO PV-PARAGRAPH-NAME.               
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                   FUNC_QTY AS DAYS_TO_PURGE                                    
                 , CURRENT TIMESTAMP                                            
                 , CURRENT TIMESTAMP - (FUNC_QTY DAYS) AS                       
                           PURGE_BEFORE_DATE                                    
               INTO                                                             
                   :KRPRPM-FUNC-QTY                                             
                 , :PV-CURRENT-TIME-STAMP                                       
                 , :PV-PURGE-B4-DATE                                            
               FROM                                                             
                   TKRPRPM                                                      
               WHERE                                                            
                   SYS_PRC_FUNC_CDE = 'RELPRG'                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE KRPRPM-FUNC-QTY TO PV-FUNC-QTY                          
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '1100-GET-PURGE-B4-DATE' TO PV-PARAGRAPH-NAME           
                   MOVE 'SELECT'        TO PV-DB2-OPERATION-ATTEMPTED           
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH CONTROLS THE ENTRIE FUNCTIONALITY  OF THIS      *        
      * PROGRAM.                                                       *        
      * 01 FOR EACH RECORD FETCHED FROM THE DRIVER CURSOR IT           *        
      * CHECKES DIRETIVE REASON CODE. IF ANY OF THE DEFINED REASON CODE*        
      * THEN IT WILL TAKE THE BAR CODE OF THAT RECORD AND SELECTS THE  *        
      * RECORDS FROM SUT_CART_DIRV_TRKG TABLE WHICH ARE ELIGIBLE FOR   *        
      * PURGE FOR THAT BAR CODE.                                       *        
      *                                                                *        
      * 02 IF THE RECORD FETCHED FROM THE DRIVER CURSOR IS NOT THE     *        
      * DEFINED DIRETIVE REASON CODE, THEN IT CHECKS FOR THE SHIPPING  *        
      * UNIT ID. IF THE SHIPPING UNIT ID IS LESS THAN OR EQUAL TO ZEROS*        
      * THEN THAT RECORD WILL BE BYE-PASSED AND THE NEXT RECORD FROM   *        
      * THE DRIVER CURSOR WILL BE FETCHED.                             *        
      *                                                                *        
      * 03 IF THE RECORD FETCHED FROM THE DRIVER CURSOR IS NOT THE     *        
      * DEFINED DIRETIVE REASON CODE, THEN IT CHECKS FOR THE SHIPPING  *        
      * UNIT ID. IF THE SHIPPING UNIT ID IS GREATER THAN ZEROES        *        
      * THEN THAT RECORD WILL BE CHECKED AGAINST TSUCRDV TABLE.        *        
      * IF THE DIVERT_TMST IS GREATHER THAN THE CRE_TMST FROM THE INPUT*        
      * THEN IT WILL TAKE THE BAR CODE OF THAT RECORD AND SELECTS THE  *        
      * RECORDS FROM SUT_CART_DIRV_TRKG TABLE WHICH ARE ELIGIBLE FOR   *        
      * PURGE FOR THAT BAR CODE.                                       *        
      *                                                                *        
      * IF THE RECORD IS NOT FOUND IN THE TSUCRDV TABLE, THEN THAT     *        
      * RECORD WILL BE BYE-PASSED.                                     *        
      ******************************************************************        
                                                                                
       2000-PROCESS-DRIVER-CSR.                                                 
                                                                                
             IF PS-AUDIT-NO-LONG-REQD        OR                                 
                PS-AUDIT-PASSED              OR                                 
                PS-CARTON-RECLAIMED          OR                                 
                PS-CARTON-RLSD-VIA-CRTN-AUD  OR                                 
                PS-MANUALLY-RLSD             OR                                 
                PS-CARTON-RLSD-VIA-ONLINE                                       
                ADD +1 TO PV-RLSE-COUNT                                         
                SET PS-END-PURGE-CSR-NO TO TRUE                                 
                PERFORM 8600-OPEN-PURGE-CSR                                     
                PERFORM 8700-FETCH-PURGE-CSR UNTIL                              
                PS-END-PURGE-CSR-YES                                            
                PERFORM 8800-CLOSE-PURGE-CSR                                    
             ELSE                                                               
              IF PV-SHIPT-UNT-ID <= 0                                           
                ADD +1 TO PV-SHIP-UNIT-ZERO-CNT                                 
              ELSE                                                              
                 PERFORM 2100-CHECK-DIRECETIVE-TIME                             
              END-IF                                                            
             END-IF                                                             
                                                                                
             PERFORM 8200-FETCH-DRIVER-CSR.                                     
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH CHECKS THAT THE DIVERT TIME STAMP IS GREATER    *        
      * THAN THE CREATE TIME STAMP IN THE INPUT RECORD WHICH WAS       *        
      * FETCHED FROM THE DRIVER CURSOR.                                *        
      ******************************************************************        
                                                                                
       2100-CHECK-DIRECETIVE-TIME.                                              
                                                                                
           MOVE '2100-CHECK-DIRECETIVE-TIME' TO PV-PARAGRAPH-NAME.              
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                   OPER_UNT_ID                                                  
                 , DIVERT_TMST                                                  
                 , SHIPT_UNT_ID                                                 
               INTO                                                             
                   :SUCRDV-OPER-UNT-ID                                          
                 , :SUCRDV-DIVERT-TMST                                          
                 , :SUCRDV-SHIPT-UNT-ID                                         
               FROM                                                             
                   TSUCRDV                                                      
               WHERE SHIPT_UNT_ID = :PV-SHIPT-UNT-ID                            
                 AND DIVERT_TMST  > :PV-CRE-TMST                                
               ORDER BY DIVERT_TMST DESC                                        
               FETCH FIRST ROW ONLY                                             
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-END-PURGE-CSR-NO TO TRUE                              
                   PERFORM 8600-OPEN-PURGE-CSR                                  
                   PERFORM 8700-FETCH-PURGE-CSR UNTIL                           
                   PS-END-PURGE-CSR-YES                                         
                   PERFORM 8800-CLOSE-PURGE-CSR                                 
                   ADD +1 TO PV-DIVERT-COUNT                                    
               WHEN SQLCODE = 100                                               
                   ADD +1 TO PV-TSUCRDV-NOT-FOUND                               
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '2100-CHECK-DIRECETIVE-TIME'                            
                                        TO PV-PARAGRAPH-NAME                    
                   MOVE 'SELECT'        TO PV-DB2-OPERATION-ATTEMPTED           
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH BUILDS THE OUTPUT RECORD IN SURD095 LAYOUT.     *        
      ******************************************************************        
                                                                                
       3000-BUILD-SU095-REC.                                                    
                                                                                
           MOVE SUT00102-BRCDE-NBR          TO SU095-BRCDE-NBR                  
           MOVE SUT00102-CRE-TMST           TO SU095-CRE-TMST                   
           MOVE SUT00102-OPER-UNT-ID        TO SU095-OPER-UNT-ID                
           MOVE SUT00102-DIRV-RSN-CDE       TO SU095-DIRV-RSN-CDE               
           MOVE SUT00102-LGCL-LOC-NM        TO SU095-LGCL-LOC-NM                
           MOVE SUT00102-AUD-CNTL-NBR       TO SU095-AUD-CNTL-NBR               
           MOVE SUT00102-SHIPT-UNT-ID       TO SU095-SHIPT-UNT-ID               
           MOVE SUT00102-AUD-STAT-CDE       TO SU095-AUD-STAT-CDE               
           MOVE SUT00102-PO-NBR             TO SU095-PO-NBR                     
           MOVE SUT00102-RCVR-SEQ-NBR       TO SU095-RCVR-SEQ-NBR               
           MOVE SUT00102-PO-LNE-NBR         TO SU095-PO-LNE-NBR                 
           MOVE SUT00102-PREPK-ID           TO SU095-PREPK-ID                   
           MOVE SUT00102-STR-NBR            TO SU095-STR-NBR                    
           MOVE SUT00102-CRE-PGM-NM         TO SU095-CRE-PGM-NM                 
           MOVE SUT00102-ACKNL-SEQ-NBR      TO SU095-ACKNL-SEQ-NBR              
           MOVE SUT00102-CASI-CDE           TO SU095-CASI-CDE                   
           MOVE SUT00102-CART-STAT-CDE      TO SU095-CART-STAT-CDE              
           MOVE SUT00102-AUD-FALR-RSN-NM    TO SU095-AUD-FALR-RSN-NM            
           MOVE SUT00102-STR-ZN-ID          TO SU095-STR-ZN-ID                  
           MOVE SUT00102-SEASET-YR          TO SU095-SEASET-YR                  
           MOVE SUT00102-SEASET-SHRT-NM     TO SU095-SEASET-SHRT-NM             
           MOVE SUT00102-CNVYR-PRC-ACTN-NM  TO SU095-CNVYR-PRC-ACTN-NM          
           MOVE SUT00102-CRE-BY-USR-ID      TO SU095-CRE-BY-USR-ID.             
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH WRITES THE OUTPUT RECORD IN SURD095 LAYOUT.     *        
      ******************************************************************        
                                                                                
       8000-WRITE-SU095-REC.                                                    
           WRITE UNLOAD-REC FROM                                                
               SU095-RECORD.                                                    
           ADD +1                           TO PV-WRITE-DRIVE-COUNTER.          
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH OPENS THE DRIVER CURSOR.                        *        
      ******************************************************************        
                                                                                
       8100-OPEN-DRIVER-CSR.                                                    
                                                                                
           MOVE '8100-OPEN-DRIVER-CSR'      TO PV-PARAGRAPH-NAME.               
                                                                                
           EXEC SQL                                                             
               OPEN DRIVER-CSR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-END-PURGE-CSR-NO  TO TRUE                             
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE 'OPEN DRIVER CURSOR ' TO                                
                         PV-DB2-OPERATION-ATTEMPTED                             
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH FETCHES THE RECORD FROM THE DRIVER CURSOR.      *        
      ******************************************************************        
                                                                                
       8200-FETCH-DRIVER-CSR.                                                   
                                                                                
           MOVE '8200-FETCH-DRIVER-CSR'     TO PV-PARAGRAPH-NAME.               
           INITIALIZE DCLSUT00102                                               
                      PS-DIRECTIVE-RSN-CODE                                     
                      PV-BRCDE-NBR                                              
                      PV-CRE-TMST                                               
                      PV-OPER-UNT-ID                                            
                      PV-SHIPT-UNT-ID.                                          
                                                                                
           EXEC SQL                                                             
               FETCH DRIVER-CSR                                                 
               INTO                                                             
                    :PV-BRCDE-NBR                                               
                 ,  :PV-CRE-TMST                                                
                 ,  :PV-OPER-UNT-ID                                             
                 ,  :PS-DIRECTIVE-RSN-CODE                                      
                 ,  :PV-SHIPT-UNT-ID                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD +1 TO PV-FETCH-DRIVE-COUNTER                             
               WHEN SQLCODE = +100                                              
                   SET PS-END-DRIVER-CSR-YES TO TRUE                            
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE 'FETCH DRIVER CURSOR ' TO                               
                   PV-DB2-OPERATION-ATTEMPTED                                   
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH CLOSES THE DRIVER CURSOR.                       *        
      ******************************************************************        
                                                                                
       8300-CLOSE-DRIVER-CSR.                                                   
                                                                                
           MOVE '8300-CLOSE-DRIVER-CSR' TO PV-PARAGRAPH-NAME                    
           EXEC SQL                                                             
               CLOSE DRIVER-CSR                                                 
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE 'CLOSE DRIVER CURSOR ' TO                               
                   PV-DB2-OPERATION-ATTEMPTED                                   
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
      /                                                                         
      ******************************************************************        
      * THIS PARAGRAPH OPENS THE PURGE CURSOR.                         *        
      ******************************************************************        
                                                                                
       8600-OPEN-PURGE-CSR.                                                     
                                                                                
           MOVE '8600-OPEN-PURGE-CSR'       TO PV-PARAGRAPH-NAME.               
                                                                                
           EXEC SQL                                                             
               OPEN PURGE-CSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-END-PURGE-CSR-NO  TO TRUE                             
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE 'OPEN PURGE CURSOR ' TO                                 
                   PV-DB2-OPERATION-ATTEMPTED                                   
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH FETCHES THE RECORD FROM THE PURGE CURSOR.       *        
      ******************************************************************        
                                                                                
       8700-FETCH-PURGE-CSR.                                                    
                                                                                
           MOVE '8700-FETCH-PURGE-CSR'      TO PV-PARAGRAPH-NAME.               
           INITIALIZE DCLSUT00102.                                              
                                                                                
           EXEC SQL                                                             
               FETCH PURGE-CSR                                                  
               INTO                                                             
                    :SUT00102-BRCDE-NBR                                         
                 ,  :SUT00102-CRE-TMST                                          
                 ,  :SUT00102-OPER-UNT-ID                                       
                 ,  :SUT00102-DIRV-RSN-CDE                                      
                 ,  :SUT00102-LGCL-LOC-NM                                       
                 ,  :SUT00102-AUD-CNTL-NBR                                      
                 ,  :SUT00102-SHIPT-UNT-ID                                      
                 ,  :SUT00102-AUD-STAT-CDE                                      
                 ,  :SUT00102-PO-NBR                                            
                 ,  :SUT00102-RCVR-SEQ-NBR                                      
                 ,  :SUT00102-PO-LNE-NBR                                        
                 ,  :SUT00102-PREPK-ID                                          
                 ,  :SUT00102-STR-NBR                                           
                 ,  :SUT00102-CRE-PGM-NM                                        
                 ,  :SUT00102-ACKNL-SEQ-NBR                                     
                 ,  :SUT00102-CASI-CDE                                          
                 ,  :SUT00102-CART-STAT-CDE                                     
                 ,  :SUT00102-AUD-FALR-RSN-NM                                   
                 ,  :SUT00102-STR-ZN-ID                                         
                 ,  :SUT00102-SEASET-YR                                         
                 ,  :SUT00102-SEASET-SHRT-NM                                    
                 ,  :SUT00102-CNVYR-PRC-ACTN-NM                                 
                 ,  :SUT00102-CRE-BY-USR-ID                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    ADD +1 TO PV-FETCH-PURGE                                    
                    PERFORM 3000-BUILD-SU095-REC                                
                    PERFORM 8000-WRITE-SU095-REC                                
               WHEN SQLCODE = +100                                              
                   SET PS-END-PURGE-CSR-YES TO TRUE                             
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE 'FETCH PURGE CURSOR ' TO                                
                   PV-DB2-OPERATION-ATTEMPTED                                   
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH CLOSES THE PURGE CURSOR.                        *        
      ******************************************************************        
                                                                                
       8800-CLOSE-PURGE-CSR.                                                    
                                                                                
           MOVE '8800-CLOSE-PURGE-CSR' TO PV-PARAGRAPH-NAME                     
           EXEC SQL                                                             
               CLOSE PURGE-CSR                                                  
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE 'CLOSE PURGE CURSOR ' TO                                
                   PV-DB2-OPERATION-ATTEMPTED                                   
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH DOES THE FOLLOWING FUNCTIONALITIES              *        
      * 01. CLOSES THE DRIVER CURSOR.                                  *        
      * 02. CLOSES THE OUTPUT FILE.                                    *        
      * 03. DISPLAYS THE RECORD READ,FETCHED, WRITTEN INFORMATION      *        
      ******************************************************************        
                                                                                
       9000-EOJ-ROUTINE.                                                        
                                                                                
           PERFORM 8300-CLOSE-DRIVER-CSR.                                       
                                                                                
           CLOSE UNLOAD-FILE.                                                   
                                                                                
           MOVE PV-FETCH-DRIVE-COUNTER       TO PV-DISPLAY-COUNT.               
           DISPLAY '** SUKUT146 - TOTAL NO OF DRIVER RECORDS FETCHED ='         
                   PV-DISPLAY-COUNT.                                            
                                                                                
           MOVE PV-DIVERT-COUNT                TO PV-DISPLAY-COUNT.             
           DISPLAY '** SUKUT146 - TOTAL NUMBER OF DIVERT RECORDS     ='         
                   PV-DISPLAY-COUNT.                                            
                                                                                
           MOVE PV-RLSE-COUNT                  TO PV-DISPLAY-COUNT.             
           DISPLAY '** SUKUT146 - TOTAL NUMBER OF RELEASED RECORDS   ='         
                   PV-DISPLAY-COUNT.                                            
                                                                                
           MOVE PV-SHIP-UNIT-ZERO-CNT          TO PV-DISPLAY-COUNT.             
           DISPLAY '** SUKUT146 - NO. OF LESS THAN ZERO SU RECORDS   ='         
                   PV-DISPLAY-COUNT.                                            
                                                                                
           MOVE PV-TSUCRDV-NOT-FOUND           TO PV-DISPLAY-COUNT.             
           DISPLAY '** SUKUT146 - NO. OF TSUCRDV NOT FOUND RECORDS   ='         
                   PV-DISPLAY-COUNT.                                            
                                                                                
           MOVE PV-FETCH-PURGE               TO PV-DISPLAY-COUNT.               
           DISPLAY '** SUKUT146 - TOTAL NO OF PURGE  RECORDS FETCHED ='         
                   PV-DISPLAY-COUNT.                                            
                                                                                
           MOVE PV-WRITE-DRIVE-COUNTER       TO PV-DISPLAY-COUNT.               
           DISPLAY '** SUKUT146 - DIRECTIVES PURGE RECORDS WRITTEN   ='         
                   PV-DISPLAY-COUNT.                                            
                                                                                
           DISPLAY '                                                  '.        
           DISPLAY '** PROGRAM SUKUT146 EXECUTION COMPLETED           '.        
           DISPLAY '                                                  '.        
           DISPLAY '**************************************************'.        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH ABENDS WHEN EVER THERE IS ABNORMAL SQL ERROR    *        
      * CODE WRITTEN FROM THE SQL PROCESSES.                           *        
      ******************************************************************        
                                                                                
       9100-SQL-ABEND.                                                          
           DISPLAY '9100-SQL-ABEND'.                                            
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
