000100******************************************************************        
000200******************************************************************        
000300 IDENTIFICATION DIVISION.                                                 
000400******************************************************************        
000500******************************************************************        
000600                                                                          
000700 PROGRAM-ID.     BPKUT494.                                                
000800 AUTHOR.         COGNIZANT.                                               
000900 INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
001000 DATE-WRITTEN.   05/03/2010.                                              
001100 DATE-COMPILED.                                                           
001200                                                                          
001300******************************************************************        
001400*                                                                         
001500*                  EP N_SLS / N_EOP BREAKDOWN                             
001600*                                                                         
001700******************************************************************        
001800                                                                          
001900******************************************************************        
002000*  CHANGES:                                                               
002100*                                                                         
002200*  05/03/2010     COGNIZANT    WR# 2344                                   
002300*     -  CHANGED THE PROGRAM FOR EP UPGRADE PROCESS.                      
002400*        PICKUP THE SALES RETAIL AMOUNTS FOR NEW STORES BY MONTH L        
002500*        WISE FOR ACTUAL SALES STAGING TABLE.                             
002600*                                                                         
002700*  12/14/2011     COGNIZANT    CRQ# 16360                                 
002800*     -  MODIFY EXTRACT DATE LOGIC TO HAVE THE MONTH BEGIN DATE           
002900*        AS START DATE AND LAST WEEKEND DATE AS END DATE.                 
11752A*                                                                         
11752A*  06/09/2017     J SELWITSCHKA                                           
11752A*     -  CHANGES FOR EP UPGRADE.                                          
11752A*        MODIFY EXTRACT DATE LOGIC TO HAVE THE WEEK BEGIN DATE            
11752A*        AS START DATE INSTEAD OF THE MONTH BEGIN DATE.                   
11752A*                                                                         
11752A*  06/22/2017     J SELWITSCHKA                                           
11752A*     - NEW VERSION BPKUT494 IS A COPY OF BPKUT493 WITH EP                
11752A*       UPGRADE CHANGES. CREATED BECAUSE EP UPGRADE VERSION               
11752A*       WILL BE RUNNING IN PARALLEL WITH OLD VERSION.                     
11752A*                                                                         
003000******************************************************************        
003100                                                                          
003200******************************************************************        
003300******************************************************************        
003400 ENVIRONMENT DIVISION.                                                    
003500******************************************************************        
003600******************************************************************        
003700                                                                          
003800******************************************************************        
003900 CONFIGURATION SECTION.                                                   
004000******************************************************************        
004100                                                                          
004200 SOURCE-COMPUTER.        IBM-3090.                                        
004300 OBJECT-COMPUTER.        IBM-3090.                                        
004400                                                                          
004500******************************************************************        
004600 INPUT-OUTPUT SECTION.                                                    
004700******************************************************************        
004800                                                                          
004900 FILE-CONTROL.                                                            
005000                                                                          
005100     SELECT CONTROL-CARD-FILE                                             
005200         ASSIGN TO INP01.                                                 
005300                                                                          
005400     SELECT INV-FILE                                                      
005500         ASSIGN TO OUT01.                                                 
005600                                                                          
005700     SELECT RERUN-FILE                                                    
005800         ASSIGN TO OUT02.                                                 
005900                                                                          
006000******************************************************************        
006100******************************************************************        
006200 DATA DIVISION.                                                           
006300******************************************************************        
006400******************************************************************        
006500                                                                          
006600******************************************************************        
006700 FILE SECTION.                                                            
006800******************************************************************        
006900                                                                          
007000 FD  CONTROL-CARD-FILE                                                    
007100     RECORDING MODE IS F                                                  
007200     LABEL RECORDS ARE STANDARD                                           
007300     BLOCK CONTAINS 0 RECORDS.                                            
007400                                                                          
007500 01  CONTROL-CARD-RECORD              PIC X(80).                          
007600                                                                          
007700 FD  INV-FILE                                                             
007800     RECORDING MODE IS F                                                  
007900     LABEL RECORDS ARE STANDARD                                           
008000     BLOCK CONTAINS 0 RECORDS.                                            
008100                                                                          
008200 01  INV-RECORD                       PIC X(700).                         
008300                                                                          
008400 FD  RERUN-FILE                                                           
008500     RECORDING MODE IS F                                                  
008600     LABEL RECORDS ARE STANDARD                                           
008700     BLOCK CONTAINS 0 RECORDS.                                            
008800                                                                          
008900 01  RERUN-RECORD                     PIC X(80).                          
009000                                                                          
009100******************************************************************        
009200 WORKING-STORAGE SECTION.                                                 
009300******************************************************************        
009400                                                                          
009500 01  PS-PROGRAM-SWITCHES.                                                 
009600     05  PS-END-OF-CONTROL-CARDS-SW   PIC X      VALUE 'N'.               
009700         88  PS-END-OF-CONTROL-CARDS             VALUE 'Y'.               
009800     05  PS-END-OF-CURSOR-SW          PIC X      VALUE 'N'.               
009900         88  PS-END-OF-CSR                       VALUE 'Y'.               
010000         88  PS-RESET-CSR-SWITCH                 VALUE 'N'.               
010100     05  PS-MULTIPLE-MONTH-SW         PIC X      VALUE 'N'.               
010200         88  PS-MULTIPLE-MONTHS                  VALUE 'Y'.               
010300                                                                          
010400 01  PC-PROGRAM-CONSTANTS.                                                
010500     05  PC-PHYSSTR                   PIC X(07)  VALUE 'PHYSSTR'.         
010600     05  PC-TOT1                      PIC X(04)  VALUE 'TOT1'.            
010700     05  PC-MTD                       PIC X(03)  VALUE 'MTD'.             
010800                                                                          
010900 01  PV-PROGRAM-VARIABLES.                                                
011000     05  PV-START-DATE                PIC X(10).                          
011100     05  PV-END-DATE                  PIC X(10).                          
011200     05  PV-BOP-START-DATE            PIC X(10).                          
011300     05  PV-BOP-END-DATE              PIC X(10).                          
011400     05  PV-CC-START-DATE             PIC X(10).                          
011500     05  PV-CC-END-DATE               PIC X(10).                          
011600     05  PV-SEASON-BEGIN-DATE         PIC X(10).                          
011700     05  PV-NEXT-SEASON-END-DATE      PIC X(10).                          
011800     05  PV-SEASON-ONE                PIC X(10).                          
011900     05  PV-SEASON-TWO                PIC X(10).                          
012000     05  PV-SEASON-ONE-END-DTE        PIC X(10).                          
012100     05  PV-FSCL-WK-END-DTE           PIC X(10).                          
012200     05  PV-SEASON                    PIC X(01).                          
012300     05  PV-YEAR                      PIC X(04).                          
012400     05  PV-WEEK-NBR                  PIC X(02).                          
012500     05  PV-FSCL-SEASON               PIC X(01).                          
012600     05  PV-FSCL-YEAR                 PIC X(04).                          
012700     05  PV-FSCL-WK-NBR               PIC X(02).                          
012800     05  PV-SEAS-YR                   PIC X(04).                          
012900     05  PV-SEAS-MN                   PIC X(02).                          
013000     05  PV-NEW-MONTH                 PIC X(02).                          
013100     05  PV-MN-NBR                    PIC X(02).                          
013200     05  PV-HOLD-OPENED-MN            PIC S9(2)V COMP-3.                  
013300     05  PV-HOLD-FSCL-WK-DATE         PIC X(10).                          
013400     05  PV-PARAGRAPH-NAME            PIC X(30).                          
013500     05  PV-DB2-OPER-ATTEMPTED        PIC X(30).                          
013600     05  PV-ABEND-CODE                PIC S9(04) COMP VALUE ZERO.         
013700     05  PV-RECORD-COUNT              PIC 9(04) VALUE ZERO.               
013800                                                                          
013900****************************************************************          
014000* RECORD LAYOUT FOR CONTROL CARD FILE.                                    
014100****************************************************************          
014200                                                                          
014300 01  CC-CONTROL-CARD.                                                     
014400     05  CC-EXTRACT-START-DATE        PIC X(10).                          
014500     05  FILLER                       PIC X(01).                          
014600     05  CC-EXTRACT-END-DATE          PIC X(10).                          
014700     05  FILLER                       PIC X(59).                          
014800                                                                          
014900******************************************************************        
015000*  COPYBOOK FOR THE OUTPUT LAYOUT                                         
015100******************************************************************        
11752A*    COPY BPRD150.                                                        
11752A     COPY BPRD151.                                                        
015300 EJECT                                                                    
015400******************************************************************        
015500*  COPYBOOK FOR THE CALENDAR ROUTINE                                      
015600******************************************************************        
015700                                                                          
015800     COPY DPWS500.                                                        
015900                                                                          
016000****************************************************************          
016100* STORE TABLE - NEW XMT_LOC                                               
016200****************************************************************          
016300                                                                          
016400     EXEC SQL                                                             
016500         INCLUDE XMT00001                                                 
016600     END-EXEC.                                                            
016700                                                                          
016800****************************************************************          
016900* MERCHANDISE LEDGER CONTROL TABLE                                        
017000****************************************************************          
017100                                                                          
017200     EXEC SQL                                                             
017300         INCLUDE TMLCNTL                                                  
017400     END-EXEC.                                                            
017500                                                                          
017600****************************************************************          
017700* DATE ATTRIBUTE TABLE                                                    
017800****************************************************************          
017900                                                                          
018000     EXEC SQL                                                             
018100         INCLUDE TDTATTR                                                  
018200     END-EXEC.                                                            
018300                                                                          
018400****************************************************************          
018500* MERCHANDISE LEDGER WEEKLY MAJOR CLASS BY STORE TABLE                    
018600****************************************************************          
018700                                                                          
018800     EXEC SQL                                                             
018900         INCLUDE TWKMCLS                                                  
019000     END-EXEC.                                                            
019100                                                                          
019200****************************************************************          
019300*  COMMUNICATION AREAS FOR DB2                                            
019400****************************************************************          
019500                                                                          
019600     EXEC SQL                                                             
019700         INCLUDE SQLCA                                                    
019800     END-EXEC.                                                            
019900                                                                          
020000******************************************************************        
020100* DB2 COMMUNICATION AREA 2                                                
020200******************************************************************        
020300                                                                          
020400     COPY SQLCA2.                                                         
020500                                                                          
020600****************************************************************          
020700*  WEEKLY MAJOR CLASS N_SLS / N_EOP BREAKDOWN CURSOR                      
020800****************************************************************          
020900                                                                          
021000     EXEC SQL                                                             
021100        DECLARE NEW-CLS-CSR CURSOR FOR                                    
021200          SELECT   B.DEPT_NBR                                             
021300                  ,B.MAJ_CL_NBR                                           
021400                  ,B.FSCL_WK_END_DTE                                      
021500                  ,A.NW_STR_OPNG_GP_MN                                    
021600                  ,SUM(B.SLS_RTL_AMT)                                     
021700            FROM  XMT_LOC A                                               
021800                 ,TWKMCLS B                                               
021900                 ,TDTATTR C                                               
022000           WHERE  B.FSCL_WK_NBR = C.FSCL_WK_NBR                           
022100             AND  B.FSCL_WK_END_DTE BETWEEN :PV-START-DATE                
022200                                        AND :PV-END-DATE                  
022300             AND  C.KY_DTE  = B.FSCL_WK_END_DTE                           
022400             AND  A.LOC_NBR = B.STR_NBR                                   
022500             AND  A.LOC_NBR IN                                            
022600             (SELECT B.LOC_NBR                                            
022700                FROM XMT_LOC B                                            
022800               WHERE B.OPN_DTE  BETWEEN :PV-SEASON-BEGIN-DATE             
022900                                    AND :PV-NEXT-SEASON-END-DATE          
023000                 AND B.E_BUS_IND       =  'N'                             
023100                 AND B.LOC_TYP_CDE     =  'DS'                            
023200                 AND B.LOC_NBR NOT IN                                     
023300                    (SELECT A.REPLT_STR_NBR                               
023400                       FROM XMT_LOC A                                     
023500                      WHERE A.REPLT_STR_NBR   > 0                         
023600                        AND A.LOC_TYP_CDE     =  'DS'))                   
023700        GROUP BY  DEPT_NBR, MAJ_CL_NBR,                                   
023800                  B.FSCL_WK_END_DTE, NW_STR_OPNG_GP_MN                    
023900        ORDER BY  NW_STR_OPNG_GP_MN, FSCL_WK_END_DTE,                     
024000                  DEPT_NBR, MAJ_CL_NBR                                    
024100        WITH UR                                                           
024200     END-EXEC.                                                            
024300                                                                          
024400                                                                          
024500******************************************************************        
024600******************************************************************        
024700 PROCEDURE DIVISION.                                                      
024800******************************************************************        
024900******************************************************************        
025000                                                                          
025100 0000-MAINLINE.                                                           
025200                                                                          
025300     PERFORM 1000-INITIALIZE.                                             
025400                                                                          
025500     PERFORM 2000-EOP-CLASS UNTIL PS-END-OF-CSR.                          
025600                                                                          
025700     CLOSE CONTROL-CARD-FILE                                              
025800           INV-FILE                                                       
025900           RERUN-FILE.                                                    
026000                                                                          
026100     IF PS-MULTIPLE-MONTHS                                                
026200        MOVE 1111 TO RETURN-CODE                                          
026300     END-IF.                                                              
026400                                                                          
026500     STOP RUN.                                                            
026600                                                                          
026700                                                                          
026800******************************************************************        
026900*                                                                         
027000*  OPEN INPUT AND OUTPUT FILES AND DETERMINE THE PROCESSING DATES         
027100*                                                                         
027200******************************************************************        
027300 1000-INITIALIZE.                                                         
027400     OPEN  INPUT CONTROL-CARD-FILE                                        
027500          OUTPUT INV-FILE                                                 
027600                 RERUN-FILE.                                              
027700                                                                          
027800     PERFORM 1100-SET-EXTRACT-DATES.                                      
027900                                                                          
028000     PERFORM 1200-VALIDATE-DATE-RANGE.                                    
028100                                                                          
028200     PERFORM 1400-OPEN-CURSOR.                                            
028300                                                                          
028400                                                                          
028500******************************************************************        
028600*                                                                         
028700* IF THERE ARE DATES ON THE CONTROL CARD, WE WILL USE THEM IN OUR         
028800* PROCESSING (OVERRIDE).  IF NOT, WE WILL EXTRACT THE PROCESSING          
028900* DATES FROM THE MERCHANDISE LEDGER CONTROL TABLE                         
029000*                                                                         
029100******************************************************************        
029200 1100-SET-EXTRACT-DATES.                                                  
029300     PERFORM 7000-READ-CONTROL-CARD-FILE.                                 
029400                                                                          
029500     IF CC-EXTRACT-START-DATE = SPACE                                     
029600         PERFORM 1110-USE-ML-CNTL-DATES                                   
029700     ELSE                                                                 
029800         DISPLAY '***********************************************'        
029900         DISPLAY '** OVERRIDE DATE USED: ' CC-CONTROL-CARD '**'           
030000         DISPLAY '***********************************************'        
030100         PERFORM 1120-USE-OVERRIDE-DATE                                   
030200     END-IF.                                                              
030300                                                                          
030400                                                                          
030500******************************************************************        
030600*                                                                         
030700* USE THE LEDGER CONTROL TABLE FOR THE PROCESSING DATE RANGE              
030800*                                                                         
030900******************************************************************        
031000 1110-USE-ML-CNTL-DATES.                                                  
031100     EXEC SQL                                                             
031200          SELECT                                                          
11752A*                FSCL_MN_BEG_DTE                                          
11752A                 FSCL_WK_BEG_DTE                                          
031400                ,MXPSTG_FSCL_WK_DTE                                       
031500            INTO                                                          
11752A*                :DTATTR-FSCL-MN-BEG-DTE                                  
11752A                 :DTATTR-FSCL-WK-BEG-DTE                                  
031700                ,:MLCNTL-MXPSTG-FSCL-WK-DTE                               
031800            FROM                                                          
031900                 TMLCNTL                                                  
032000                ,TDTATTR                                                  
032100            WHERE                                                         
032200                 KY_DTE = OPN_FSCL_WK_DTE                                 
032300     END-EXEC.                                                            
032400                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN SQLCODE = ZERO                                              
11752A*            MOVE DTATTR-FSCL-MN-BEG-DTE   TO                             
11752A             MOVE DTATTR-FSCL-WK-BEG-DTE   TO                             
032800                  PV-START-DATE                                           
032900             MOVE MLCNTL-MXPSTG-FSCL-WK-DTE TO                            
033000                  PV-END-DATE                                             
033100         WHEN OTHER                                                       
033200             MOVE '1110-USE-ML-CNTL-DATES' TO PV-PARAGRAPH-NAME           
033300             MOVE 'SELECT ROW FROM TMLCNTL'                               
033400                                          TO PV-DB2-OPER-ATTEMPTED        
033500             PERFORM 9999-SQL-ABEND                                       
033600     END-EVALUATE.                                                        
033700                                                                          
033800                                                                          
033900******************************************************************        
034000*                                                                         
034100* USE AN OVERRIDE CONTROL CARD FOR THE PROCESSING DATE RANGE              
034200*                                                                         
034300******************************************************************        
034400 1120-USE-OVERRIDE-DATE.                                                  
034500     MOVE CC-EXTRACT-START-DATE TO PV-START-DATE.                         
034600     MOVE CC-EXTRACT-END-DATE   TO PV-END-DATE.                           
034700                                                                          
034800                                                                          
034900******************************************************************        
035000*                                                                         
035100* SINCE A STORE IS ONLY CONSIDERED NEW FOR THE SEASON IT OPENS IN,        
035200* IF A DATE RANGE CROSSES SEASONS, WE MUST USE DIFFERNT SEASON            
035300* DATES IN THE SELECT CURSOR.                                             
035400*                                                                         
035500* - IF WE DETERMINE THAT A DATE RANGE CROSSES SEASONS, WE WILL            
035600*   PROCESS THE FIRST SEASON IN ONE RUN FOLLOWED BY THE SECOND            
035700*   SEASON IN A SUBSEQUENT RUN. THIS IS SET UP IN THE JOB TO              
035800*   RECOGNIZE A SPECIFIC RETURN CODE (1111) WHICH TELLS IT THAT           
035900*   WE ARE CROSSING SEASONS AND MUST RUN A SECOND TIME.                   
036000*                                                                         
036100* - FOR THE SECOND RUN, WE CREATE A NEW CONTROL CARD.  WE BREAK           
036200*   THE DATE RANGE AS FOLLOWS:                                            
036300*       - RUN 1: START-DATE TO SEASON-END-DATE                            
036400*       - RUN 2: NEXT-SEASON-BEGIN-DATE TO END-DATE                       
036500*  *** NOTE THAT A NORMAL RUN WOULD BE START-DATE TO END-DATE ***         
036600*                                                                         
036700******************************************************************        
036800 1200-VALIDATE-DATE-RANGE.                                                
036900     EXEC SQL                                                             
037000          SELECT  FSCL_YR_NBR                                             
037100                 ,FSCL_MN_NBR                                             
037200                 ,SEA_BEG_DTE                                             
037300                 ,SEA_END_DTE                                             
037400            INTO  :DTATTR-FSCL-YR-NBR                                     
037500                 ,:DTATTR-FSCL-MN-NBR                                     
037600                 ,:DTATTR-SEA-BEG-DTE                                     
037700                 ,:DTATTR-SEA-END-DTE                                     
037800            FROM  TDTATTR                                                 
037900           WHERE  KY_DTE = :PV-START-DATE                                 
038000     END-EXEC.                                                            
038100                                                                          
038200     EVALUATE TRUE                                                        
038300         WHEN SQLCODE = ZERO                                              
038400             MOVE DTATTR-FSCL-YR-NBR TO PV-SEAS-YR                        
038500             MOVE DTATTR-FSCL-MN-NBR TO PV-SEAS-MN                        
038600             MOVE DTATTR-SEA-BEG-DTE TO PV-SEASON-ONE                     
038700             MOVE DTATTR-SEA-END-DTE TO PV-SEASON-ONE-END-DTE             
038800         WHEN OTHER                                                       
038900             MOVE '1200-VALIDATE-DATE-RANGE' TO PV-PARAGRAPH-NAME         
039000             MOVE 'SELECT ROW FROM TDTATTR'                               
039100                                          TO PV-DB2-OPER-ATTEMPTED        
039200             PERFORM 9999-SQL-ABEND                                       
039300     END-EVALUATE.                                                        
039400                                                                          
039500     EXEC SQL                                                             
039600          SELECT  SEA_BEG_DTE                                             
039700            INTO  :DTATTR-SEA-BEG-DTE                                     
039800            FROM  TDTATTR                                                 
039900           WHERE  KY_DTE = :PV-END-DATE                                   
040000     END-EXEC.                                                            
040100                                                                          
040200     EVALUATE TRUE                                                        
040300         WHEN SQLCODE = ZERO                                              
040400             MOVE DTATTR-SEA-BEG-DTE TO PV-SEASON-TWO                     
040500         WHEN OTHER                                                       
040600             MOVE '1200-VALIDATE-DATE-RANGE' TO PV-PARAGRAPH-NAME         
040700             MOVE 'SELECT ROW FROM TDTATTR'                               
040800                                          TO PV-DB2-OPER-ATTEMPTED        
040900             PERFORM 9999-SQL-ABEND                                       
041000     END-EVALUATE.                                                        
041100                                                                          
041200     MOVE PV-SEASON-ONE TO PV-SEASON-BEGIN-DATE.                          
041300                                                                          
041400     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
041500     MOVE PV-SEASON-BEGIN-DATE     TO DPG52-LK-DATE-INPUT.                
041600     SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
041700     SET DPG51-INCREMENT-DATE      TO TRUE.                               
041800     SET DPG52-LK-DTE-FISC-PRD-CC  TO TRUE.                               
041900     MOVE PV-SEAS-YR               TO DPG52-FISCAL-CC-PRD-YR-X.           
042000     MOVE PV-SEAS-MN               TO DPG52-FISCAL-CC-PRD-PP-X.           
042100     MOVE 6                        TO                                     
042200                                   DPG51-INCR-DECR-FISCAL-PRD-9.          
042300     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                        
042400                                             DPG52                        
042500                                             DPG53                        
042600                                             DPG54                        
042700                                             DPG55                        
042800                                             DPG56.                       
042900     EVALUATE TRUE                                                        
043000         WHEN DPG54-NO-ERROR                                              
043100            MOVE DPG56-LAST-FS-DB2-ISO-FMT TO                             
043200                  PV-NEXT-SEASON-END-DATE                                 
043300         WHEN (DPG54-SEVERE-ERROR OR DPG54-DATE-INVALID)                  
043400             DISPLAY '** ABEND ** IN BPKUT494'                            
043500             DISPLAY 'ERROR CALLING THE CALENDAR ROUTINE'                 
043600             DISPLAY 'DATE TODAY = ' DPG52-DB2-ISO-DATE                   
043700             MOVE +4003 TO PV-ABEND-CODE                                  
043800             CALL 'ILBOABN0' USING PV-ABEND-CODE                          
043900     END-EVALUATE.                                                        
044000                                                                          
044100     IF PV-SEASON-ONE NOT EQUAL PV-SEASON-TWO                             
044200        SET PS-MULTIPLE-MONTHS TO TRUE                                    
044300        MOVE PV-END-DATE TO PV-CC-END-DATE                                
044400        MOVE PV-SEASON-ONE-END-DTE TO PV-END-DATE                         
044500                                                                          
044600        EXEC SQL                                                          
044700             SELECT  SEA_BEG_DTE                                          
044800               INTO  :DTATTR-SEA-BEG-DTE                                  
044900               FROM  TDTATTR                                              
045000              WHERE  KY_DTE = :PV-CC-END-DATE                             
045100        END-EXEC                                                          
045200                                                                          
045300        EVALUATE TRUE                                                     
045400            WHEN SQLCODE = ZERO                                           
045500                MOVE DTATTR-SEA-BEG-DTE TO PV-CC-START-DATE               
045600            WHEN OTHER                                                    
045700                MOVE '1200-VALIDATE-DATE-RANGE'                           
045800                                      TO PV-PARAGRAPH-NAME                
045900                MOVE 'SELECT ROW FROM TDTATTR'                            
046000                                      TO PV-DB2-OPER-ATTEMPTED            
046100                PERFORM 9999-SQL-ABEND                                    
046200        END-EVALUATE                                                      
046300                                                                          
046400        MOVE PV-CC-START-DATE TO CC-EXTRACT-START-DATE                    
046500        MOVE PV-CC-END-DATE   TO CC-EXTRACT-END-DATE                      
046600                                                                          
046700        WRITE RERUN-RECORD FROM CC-CONTROL-CARD                           
046800                                                                          
046900     END-IF.                                                              
047000******************************************************************        
047100*                                                                         
047200* OPEN THE N_SLS / N_EOP BREAKDOWN CURSOR                                 
047300*                                                                         
047400******************************************************************        
047500 1400-OPEN-CURSOR.                                                        
047600     EXEC SQL                                                             
047700         OPEN NEW-CLS-CSR                                                 
047800     END-EXEC.                                                            
047900                                                                          
048000     EXEC SQL                                                             
048100         FETCH NEW-CLS-CSR                                                
048200         INTO  :WKMCLS-DEPT-NBR                                           
048300              ,:WKMCLS-MAJ-CL-NBR                                         
048400              ,:WKMCLS-FSCL-WK-END-DTE                                    
048500              ,:XMT00001-NW-STR-OPNG-GP-MN                                
048600              ,:WKMCLS-SLS-RTL-AMT                                        
048700     END-EXEC.                                                            
048800                                                                          
048900     EVALUATE SQLCODE                                                     
049000         WHEN ZERO                                                        
049100             PERFORM 8100-SET-BREAK-POINTS                                
049200             ADD 1 TO PV-RECORD-COUNT                                     
053800         WHEN +100                                                        
053900             SET PS-END-OF-CSR TO TRUE                                    
                   DISPLAY 'NO NEW STORE TO PROCESS'                            
049300         WHEN OTHER                                                       
049400             MOVE '8000-GET-NEXT-RECORD' TO                               
049500                  PV-PARAGRAPH-NAME                                       
049600             PERFORM 9999-SQL-ABEND                                       
049700     END-EVALUATE.                                                        
049800                                                                          
049900                                                                          
050000******************************************************************        
050100*                                                                         
050200* PROCESS THE CURSOR ONE RECORD AT A TIME WRITING THE FORMATTED           
050300* OUTPUT RECORD TO AN OUTPUT FILE.                                        
050400*                                                                         
050500******************************************************************        
050600 2000-EOP-CLASS.                                                          
050700                                                                          
050800     PERFORM 8100-SET-BREAK-POINTS                                        
050900                                                                          
051000     PERFORM 8300-DATA-MOVES                                              
051100     PERFORM 8000-GET-NEXT-RECORD.                                        
051200                                                                          
051300                                                                          
051400******************************************************************        
051500*   READ CONTROL CARD FILE                                                
051600******************************************************************        
051700 7000-READ-CONTROL-CARD-FILE.                                             
051800     READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
051900         AT END SET PS-END-OF-CONTROL-CARDS TO TRUE.                      
052000                                                                          
052100                                                                          
052200******************************************************************        
052300*    FETCH NEXT RECORD FOR PROCESSING                                     
052400******************************************************************        
052500 8000-GET-NEXT-RECORD.                                                    
052600     EXEC SQL                                                             
052700         FETCH NEW-CLS-CSR                                                
052800         INTO  :WKMCLS-DEPT-NBR                                           
052900              ,:WKMCLS-MAJ-CL-NBR                                         
053000              ,:WKMCLS-FSCL-WK-END-DTE                                    
053100              ,:XMT00001-NW-STR-OPNG-GP-MN                                
053200              ,:WKMCLS-SLS-RTL-AMT                                        
053300     END-EXEC.                                                            
053400                                                                          
053500     EVALUATE SQLCODE                                                     
053600         WHEN ZERO                                                        
053700             ADD 1 TO PV-RECORD-COUNT                                     
053800         WHEN +100                                                        
053900             SET PS-END-OF-CSR TO TRUE                                    
054000         WHEN OTHER                                                       
054100             MOVE '8000-GET-NEXT-RECORD' TO                               
054200                  PV-PARAGRAPH-NAME                                       
054300             PERFORM 9999-SQL-ABEND                                       
054400     END-EVALUATE.                                                        
054500                                                                          
054600                                                                          
054700******************************************************************        
054800*   SET BREAK POINTS AND MOVE THE DLR/CST AMOUNT                          
054900******************************************************************        
055000 8100-SET-BREAK-POINTS.                                                   
055100                                                                          
055200     MOVE WKMCLS-FSCL-WK-END-DTE TO PV-HOLD-FSCL-WK-DATE.                 
055300     MOVE XMT00001-NW-STR-OPNG-GP-MN  TO PV-HOLD-OPENED-MN.               
055400                                                                          
055500     EXEC SQL                                                             
055600           SELECT                                                         
055700                  FSCL_YR_NBR                                             
055800                 ,FSCL_WK_NBR                                             
055900                 ,SEA_DESC                                                
056000            INTO                                                          
056100                  :DTATTR-FSCL-YR-NBR                                     
056200                 ,:DTATTR-FSCL-WK-NBR                                     
056300                 ,:DTATTR-SEA-DESC                                        
056400            FROM                                                          
056500                  TDTATTR                                                 
056600            WHERE                                                         
056700                  KY_DTE = :WKMCLS-FSCL-WK-END-DTE                        
056800     END-EXEC.                                                            
056900                                                                          
057000     EVALUATE TRUE                                                        
057100            WHEN SQLCODE = ZERO                                           
057200                MOVE DTATTR-FSCL-YR-NBR TO PV-FSCL-YEAR                   
057300                MOVE DTATTR-FSCL-WK-NBR TO PV-FSCL-WK-NBR                 
057400                MOVE DTATTR-SEA-DESC    TO PV-FSCL-SEASON                 
057500            WHEN OTHER                                                    
057600                MOVE '8100-SET-BREAK-POINTS' TO PV-PARAGRAPH-NAME         
057700                MOVE 'SELECT ROW FROM TDTATTR'                            
057800                                          TO PV-DB2-OPER-ATTEMPTED        
057900                PERFORM 9999-SQL-ABEND                                    
058000     END-EVALUATE.                                                        
058100     MOVE XMT00001-NW-STR-OPNG-GP-MN TO PV-MN-NBR.                        
058200     EXEC SQL                                                             
058300          SELECT                                                          
058400                 DISTINCT(FSCL_MN_NBR)                                    
058500            INTO                                                          
058600                 :DTATTR-FSCL-MN-NBR                                      
058700            FROM                                                          
058800                 TDTATTR                                                  
058900           WHERE                                                          
059000                 CAL_MN_NBR = :PV-MN-NBR                                  
059100     END-EXEC.                                                            
059200                                                                          
059300     EVALUATE TRUE                                                        
059400         WHEN SQLCODE = ZERO                                              
059500             MOVE DTATTR-FSCL-MN-NBR TO PV-NEW-MONTH                      
059600         WHEN OTHER                                                       
059700             MOVE '8100-SET-BREAK-POINTS' TO PV-PARAGRAPH-NAME            
059800             MOVE 'SELECT ROW FROM TDTATTR'                               
059900                                       TO PV-DB2-OPER-ATTEMPTED           
060000             PERFORM 9999-SQL-ABEND                                       
060100     END-EVALUATE.                                                        
060200     INITIALIZE BP150-RECORD.                                             
060300     INITIALIZE BP150-ACTUAL-SALES                                        
060400     INITIALIZE BP150-ACTUAL-RECEIPTS                                     
060500     MOVE ZEROES TO BP150-C-SLS-DLR                                       
060600     MOVE ZEROES TO BP150-N-SLS-DLR                                       
060700     EVALUATE PV-NEW-MONTH                                                
060800        WHEN '01'                                                         
060900            DIVIDE WKMCLS-SLS-RTL-AMT  BY 1000                            
061000                                       GIVING BP150-N-SLS1-DLR            
061100        WHEN '02'                                                         
061200            DIVIDE WKMCLS-SLS-RTL-AMT  BY 1000                            
061300                                       GIVING BP150-N-SLS2-DLR            
061400        WHEN '03'                                                         
061500            DIVIDE WKMCLS-SLS-RTL-AMT  BY 1000                            
061600                                       GIVING BP150-N-SLS3-DLR            
061700        WHEN '04'                                                         
061800            DIVIDE WKMCLS-SLS-RTL-AMT  BY 1000                            
061900                                      GIVING BP150-N-SLS4-DLR             
062000        WHEN '05'                                                         
062100            DIVIDE WKMCLS-SLS-RTL-AMT  BY 1000                            
062200                                       GIVING BP150-N-SLS5-DLR            
062300        WHEN '06'                                                         
062400            DIVIDE WKMCLS-SLS-RTL-AMT  BY 1000                            
062500                                       GIVING BP150-N-SLS6-DLR            
062600        WHEN '07'                                                         
062700            DIVIDE WKMCLS-SLS-RTL-AMT  BY 1000                            
062800                                       GIVING BP150-N-SLS7-DLR            
062900        WHEN '08'                                                         
063000            DIVIDE WKMCLS-SLS-RTL-AMT  BY 1000                            
063100                                       GIVING BP150-N-SLS8-DLR            
063200        WHEN '09'                                                         
063300            DIVIDE WKMCLS-SLS-RTL-AMT  BY 1000                            
063400                                       GIVING BP150-N-SLS9-DLR            
063500        WHEN '10'                                                         
063600            DIVIDE WKMCLS-SLS-RTL-AMT  BY 1000                            
063700                                       GIVING BP150-N-SLS10-DLR           
063800        WHEN '11'                                                         
063900            DIVIDE WKMCLS-SLS-RTL-AMT  BY 1000                            
064000                                       GIVING BP150-N-SLS11-DLR           
064100        WHEN '12'                                                         
064200            DIVIDE WKMCLS-SLS-RTL-AMT  BY 1000                            
064300                                       GIVING BP150-N-SLS12-DLR           
064400     END-EVALUATE.                                                        
064500                                                                          
064600                                                                          
064700******************************************************************        
064800*   DATA MOVE AND WRITE THE RECORD                                        
064900******************************************************************        
065000 8300-DATA-MOVES.                                                         
065100     MOVE WKMCLS-DEPT-NBR           TO BP150-DEPARTMENT-NBR1              
065200     MOVE WKMCLS-MAJ-CL-NBR         TO BP150-CLASS-NBR1                   
065300     MOVE PC-PHYSSTR                TO BP150-ORGANIZATION-NAME1           
065400     MOVE PV-FSCL-SEASON            TO BP150-SEASON1                      
065500     MOVE PV-FSCL-YEAR              TO BP150-YEAR1                        
065600     MOVE PV-FSCL-WK-NBR            TO BP150-WEEK-NBR1                    
065700     INITIALIZE INV-RECORD.                                               
065800     WRITE INV-RECORD FROM BP150-ACTUAL-SALES.                            
065900                                                                          
066000                                                                          
066100******************************************************************        
066200*                                                                *        
066300*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL   *        
066400*                  ERROR OR WARNING CODE OCCURS                  *        
066500*                                                                *        
066600******************************************************************        
066700 9999-SQL-ABEND.                                                          
066800                                                                          
066900     MOVE SQLCODE        TO  SQLCODE2.                                    
067000     MOVE SQLERRD(3)     TO  SQLERRD3.                                    
067100                                                                          
067200     DISPLAY '***************************'                                
067300     DISPLAY '**       ABEND           **'                                
067400     DISPLAY '**  IN PROGRAM BPKUT494  **'                                
067500     DISPLAY '***************************'                                
067600     DISPLAY '** PARAGRAPH **      = ' PV-PARAGRAPH-NAME                  
067700     DISPLAY '** OPERATION **      = ' PV-DB2-OPER-ATTEMPTED              
067800     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
067900     DISPLAY '** SQLCODE **        = ' SQLCODE2.                          
068000                                                                          
068100     MOVE +4013         TO PV-ABEND-CODE.                                 
068200     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
