       IDENTIFICATION DIVISION.                                         00010000
      ******************************************************************00011099
       PROGRAM-ID.    PXKUT026.                                         00012099
       AUTHOR.        JOYCE KNOKE.                                      00013099
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00014099
       DATE-WRITTEN.  NOVEMBER 2006.                                    00015099
       DATE-COMPILED.                                                   00016099
      ***************************************************************** 00017099
      * THIS PROGRAM READS THE FILE CREATED IN PXKUT025. THIS FILE IS   00018099
      * COMPRIZED OF ALL THE SKUS INCLUDED IN THE MK1 FOR EACH EVENT    00019099
      ***************************************************************** 00020099
      * 09/13/2007 - DAVE WELLER                                                
      *     CHANGED INPUT RECORD LENGTH TO 24                                   
      ***************************************************************** 00020099
       ENVIRONMENT DIVISION.                                            00030099
      ***************************************************************** 00040099
       CONFIGURATION SECTION.                                           00050099
                                                                        00060099
       INPUT-OUTPUT SECTION.                                            00070099
                                                                        00080099
       FILE-CONTROL.                                                    00090099
                                                                        00100099
           SELECT IN-SKU-FILE                                           00110099
               ASSIGN TO INP01.                                         00120099
                                                                        00130099
           SELECT OUT-SKU-FILE                                          00140099
               ASSIGN TO OUT01.                                         00150099
                                                                        00160099
      ******************************************************************00170099
       DATA DIVISION.                                                   00180099
      ******************************************************************00190099
       FILE SECTION.                                                    00200099
                                                                        00210099
       FD  IN-SKU-FILE                                                  00220099
           RECORDING MODE F                                             00230099
           LABEL RECORDS ARE STANDARD                                   00240099
           BLOCK CONTAINS 0 RECORDS.                                    00250099
       01  IN-SKU-RECORD                       PIC  X(24).              00260099
                                                                        00270099
       FD  OUT-SKU-FILE                                                 00280099
           RECORDING MODE F                                             00290099
           LABEL RECORDS ARE STANDARD                                   00300099
           BLOCK CONTAINS 0 RECORDS.                                    00310099
       01  OUT-SKU-RECORD                      PIC  X(27).              00320099
                                                                        00330099
      ****************************************************************  00340099
       WORKING-STORAGE SECTION.                                         00350099
      ****************************************************************  00360099
      **PROGRAM ACCUMULATORS                                            00370099
      ****************************************************************  00380099
       01  PA-PROGRAM-ACCUMULATORS.                                     00390099
           05  PA-TOT-SKU-RECS-READ         PIC 9(13) VALUE ZEROS.      00400099
           05  PA-TOT-SKU-BYPASSED          PIC 9(13) VALUE ZEROS.      00410099
           05  PA-TOT-SKU-WRITTEN           PIC 9(13) VALUE ZEROS.      00420099
                                                                        00430099
      ****************************************************************  00440099
      **PROGRAM VARIABLES.                                              00450099
      ****************************************************************  00460099
       01  PV-PROGRAM-VARIABLES.                                        00470099
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.     00480099
           05  PV-OPERATION-MSG-1           PIC  X(40) VALUE SPACE.     00490099
           05  PV-OPERATION-MSG-2           PIC  X(40) VALUE SPACE.     00500099
           05  PV-OPERATION-MSG             PIC  X(30) VALUE SPACE.     00510099
           05  PV-DB2-OPERATION             PIC  X(30) VALUE SPACE.     00520099
           05  PV-EVENT-DATE                PIC  X(10) VALUE SPACE.     00530099
           05  PV-SQL-SUB                   PIC S9(04) COMP             00540099
                                                       VALUE +0.        00550099
           05  PV-SAVE-SKU-NBR              PIC S9(08) COMP-3           00560099
                                                       VALUE +0.        00570099
                                                                        00580099
      ****************************************************************  00590099
      **PROGRAM SWITCHES.                                               00600099
      *****-**********************************************************  00600199
       01  PS-PROGRAM-SWITCHES.                                         00600200
           05  PS-END-OF-FILE-SW            PIC  X(01) VALUE 'N'.       00600300
               88 PS-END-OF-FILE-N                     VALUE 'N'.       00600400
               88 PS-END-OF-FILE-Y                     VALUE 'Y'.       00600500
           05  PS-END-OF-CSR-SW             PIC  X(01) VALUE 'N'.       00600600
               88 PS-END-OF-CSR-N                      VALUE 'N'.       00600700
               88 PS-END-OF-CSR-Y                      VALUE 'Y'.       00600800
           05  PS-SKU-FOUND-SW              PIC  X(01) VALUE ' '.       00600999
               88 PS-SKU-NOT-FOUND-N                   VALUE 'N'.       00601099
               88 PS-SKU-FOUND-Y                       VALUE 'Y'.       00602099
           05  PS-SKU-STORE-FOUND-SW        PIC  X(01) VALUE ' '.       00603099
               88 PS-SKU-STORE-FOUND-N                 VALUE 'N'.       00604099
               88 PS-SKU-STORE-FOUND-Y                 VALUE 'Y'.       00605099
           05  PS-SKUS-UNMATCHED-SW         PIC  X(01) VALUE 'N'.       00606099
               88 PS-SKUS-UNMATCHED-N                  VALUE 'N'.       00607099
               88 PS-SKUS-UNMATCHED-Y                  VALUE 'Y'.       00608099
                                                                        00609099
      ****************************************************************  00610099
      **PROGRAM CONSTANTS.                                              00620099
      ****************************************************************  00630099
       01  PC-PROGRAM-CONSTANTS.                                        00640099
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08) VALUE 'PXKUT026'.00650099
           05  PC-MAX-RETRIES               PIC S9(004) COMP            00660099
                                                       VALUE +5.        00670099
                                                                        00680099
      ****************************************************************  00690099
      **ABEND INFORMATION                                               00700099
      ****************************************************************  00710099
       01  AC-ABEND-CODE                    PIC S9(04) VALUE ZERO       00720099
                                                       COMP SYNC.       00730099
           88  AC-DB2-ERROR                            VALUE +4013.     00740099
           88  AC-CONTROL-CARD-ERROR                   VALUE +4003.     00750099
                                                                        00760099
      ************************************************************      00770099
      * MOS SKU EXTRACT                                                 00780099
      ************************************************************      00790099
           COPY PXRD031.                                                00800099
                                                                        00810099
      ************************************************************      00820099
      * VALID MOS ITEMS                                                 00830099
      ************************************************************      00840099
           COPY PXRD032.                                                00850099
                                                                        00860099
      ***************************************************************** 01060000
      *  ABEND ROUTINE INFORMATION                                      01090000
      ***************************************************************** 01091099
           COPY DPWS900.                                                01100000
                                                                        01100199
      ***************************************************************** 01100200
      *  ABEND ROUTINE INFORMATION                                      01110000
      ***************************************************************** 01111099
           COPY DPWS004.                                                01120000
                                                                        01130099
      ************************************************************      01140099
      * DB2 MESSAGE AREA                                                01150099
      ************************************************************      01160099
           COPY SQLCA2.                                                 01170099
                                                                        01180099
      ************************************************************      01190099
      * DB2 COMMUNICATIONS AREA                                         01200099
      ************************************************************      01210099
           EXEC SQL                                                     01220099
             INCLUDE SQLCA                                              01230099
           END-EXEC.                                                    01240099
                                                                        01250099
      ************************************************************      01260099
      * DB2 TABLE XST_SKU (XST00010)                                    01270099
      ************************************************************      01280099
           EXEC SQL                                                     01290099
             INCLUDE XST00010                                           01300099
           END-EXEC.                                                    01310099
                                                                        01320099
      ************************************************************      01330099
      * DB2 TABLE XST_SKU_LOC (XST00008)                                01340099
      ************************************************************      01350099
           EXEC SQL                                                     01360099
             INCLUDE XST00008                                           01370099
           END-EXEC.                                                    01380099
                                                                        01380199
      ****************************************************************  01380200
       PROCEDURE DIVISION.                                              01480000
      ******************************************************************01490000
       0000-MAINLINE.                                                   01500000
           PERFORM 1000-HOUSEKEEPING.                                   01520000
                                                                        01530099
           PERFORM 2000-PROCESS-INPUT-FILE                              01540099
               UNTIL PS-END-OF-FILE-Y.                                  26140000
                                                                        26140199
           PERFORM 9000-END-OF-JOB.                                     26140200
                                                                        26140300
           STOP RUN.                                                    26140400
                                                                        26140500
      ****************************************************************  26140600
      *     OPEN FILES, READ IN AND VALIDATE CONTROL DATE.              26140700
      ****************************************************************  26140800
       1000-HOUSEKEEPING.                                               26140900
           OPEN INPUT  IN-SKU-FILE                                      26141000
                OUTPUT OUT-SKU-FILE.                                    26141100
                                                                        26141200
           PERFORM 5000-READ-INPUT-FILE.                                26141399
                                                                        26141400
           IF PS-END-OF-FILE-Y                                          26170000
               DISPLAY SPACES                                           26180099
               DISPLAY ALL '*'                                          26190099
               DISPLAY '  INPUT FILE IS EMPTY '                         26200099
               DISPLAY ALL '*'                                          26210099
               DISPLAY SPACES                                           26220099
           END-IF.                                                      26230099
                                                                        26230199
      ****************************************************************  26230200
      *     PROCESS INPUT FILE                                          26230300
      ****************************************************************  26230400
       2000-PROCESS-INPUT-FILE.                                         26230500
           SET PS-SKUS-UNMATCHED-N TO TRUE.                             26230699
           MOVE SPACES TO PS-SKU-FOUND-SW.                              26230799
           MOVE PX031-SKU-NBR TO PV-SAVE-SKU-NBR.                       26230899
           PERFORM 2200-VERIFY-SKU-NBR.                                 26230900
                                                                        26231099
           IF PS-SKU-FOUND-Y                                            26232099
             AND XST00008-LOC-SKU-STAT-CDE = '30'                       26233099
               PERFORM 3000-PROCESS-SKUS                                26234099
               UNTIL PS-SKUS-UNMATCHED-Y                                26235099
           END-IF.                                                      26236099
                                                                        26236199
           IF PS-SKU-FOUND-Y                                            26236200
             AND XST00008-LOC-SKU-STAT-CDE = '25'                       26236399
             AND XST00010-REGN-PRIC-IND = 'Y'                           26236499
               PERFORM 3200-PROCESS-SKU-BY-STORE                        26236599
               UNTIL PS-SKUS-UNMATCHED-Y                                26236699
           END-IF.                                                      26236799
                                                                        26236899
           IF PS-SKU-NOT-FOUND-N                                        26236999
               PERFORM 3400-BYPASS-SKU                                  26237099
               UNTIL PS-SKUS-UNMATCHED-Y                                26238099
           END-IF.                                                      26239099
                                                                        26240099
      ******************************************************************26250099
      * VERIFY SKU NUMBER                                               26260099
      ******************************************************************26270099
       2200-VERIFY-SKU-NBR.                                             26280099
           INITIALIZE DCLXST00008                                       26290099
                      DCLXST00010.                                      26300099
                                                                        26310099
           PERFORM                                                      26320099
               WITH TEST AFTER                                          26330099
               VARYING PV-SQL-SUB                                       26340099
               FROM 1 BY 1                                              26350099
               UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                 26360099
                         OR  SQLCODE = 0                                26370099
                         OR  SQLCODE = +100)                            26380099
                                                                        26390099
               EXEC SQL                                                 26400099
                 SELECT LOC_SKU_STAT_CDE                                26410099
                       ,REGN_PRIC_IND                                   26420099
                 INTO   :XST00008-LOC-SKU-STAT-CDE                      26430099
                       ,:XST00010-REGN-PRIC-IND                         26440099
                 FROM   XST_SKU      A                                  26450099
                       ,XST_SKU_LOC  B                                  26460099
                 WHERE A.INTR_UPC_ID   = :PX031-INTR-UPC-ID             26470099
                   AND A.SKU_NBR       = :PX031-SKU-NBR                 26480099
                   AND A.INTR_UPC_ID   =  B.INTR_UPC_ID                 26490099
                   AND B.LOC_NBR       =  0                             26500099
                   AND B.END_TMST     IS  NULL                          26510099
                   AND B.LOC_SKU_STAT_CDE IN ('30', '25')               26520099
                 WITH UR                                                26530099
               END-EXEC                                                 26540099
                                                                        26550099
               EVALUATE TRUE                                            26560099
                   WHEN SQLCODE = 0                                     26570099
                       SET PS-SKU-FOUND-Y TO TRUE                       26580099
                   WHEN SQLCODE = +100                                  26590099
                       SET PS-SKU-NOT-FOUND-N TO TRUE                   26600099
                   WHEN SQLCODE = -911                                  26610099
                       CONTINUE                                         26620099
                   WHEN SQLWARN NOT = SPACES                            26630099
                   WHEN SQLCODE NOT = ZERO                              26640099
                       MOVE '2200-VERIFY-SKU-NUMBER    '                26650099
                                            TO PV-PARAGRAPH-NAME        26660099
                       MOVE 'SELECT XST_SKU  '                          26670099
                                            TO PV-DB2-OPERATION         26680099
                       PERFORM 9999-SQL-ABEND                           26690099
               END-EVALUATE                                             26700099
           END-PERFORM.                                                 26710099
                                                                        26720099
           IF PV-SQL-SUB GREATER PC-MAX-RETRIES                         26730099
             MOVE '2200-VERIFY-SKU-NUMBER'                              26740099
                                   TO  PV-PARAGRAPH-NAME                26750099
             MOVE '2200- SELECT RETRIES EXCEEDED'                       26760099
                                    TO  PV-DB2-OPERATION                26770099
             PERFORM 9999-SQL-ABEND                                     26780099
           END-IF.                                                      26780199
                                                                        26780200
      ****************************************************************  26780300
      **    PROCESS SKUS                                                26780400
      ****************************************************************  26780500
       3000-PROCESS-SKUS.                                               26780699
           PERFORM 6000-WRITE-OUTPUT-FILE.                              26780799
                                                                        26780800
           IF PS-END-OF-FILE-N                                          26780900
               PERFORM 5000-READ-INPUT-FILE                             26781099
               IF PX031-SKU-NBR NOT = PV-SAVE-SKU-NBR                   26782099
                   SET PS-SKUS-UNMATCHED-Y TO TRUE                      26783099
               END-IF                                                   26784099
           END-IF.                                                      26785099
                                                                        26785199
      ****************************************************************  26785200
      **    PROCESS SKU BY STORE                                        26785300
      ****************************************************************  26785400
       3200-PROCESS-SKU-BY-STORE.                                       26785599
           MOVE ' ' TO PS-SKU-STORE-FOUND-SW.                           26785699
           PERFORM 4000-SELECT-SKU-BY-STORE.                            26785799
                                                                        26785800
           IF PS-SKU-STORE-FOUND-Y                                      26785999
               PERFORM 6000-WRITE-OUTPUT-FILE                           26786099
           END-IF.                                                      26786199
                                                                        26786200
           IF PS-SKU-STORE-FOUND-N                                      26786399
               ADD 1 TO PA-TOT-SKU-BYPASSED                             26786499
           END-IF.                                                      26786599
                                                                        26786600
           IF PS-END-OF-FILE-N                                          26786700
               PERFORM 5000-READ-INPUT-FILE                             26786899
             IF PX031-SKU-NBR NOT = PV-SAVE-SKU-NBR                     26786999
                 SET PS-SKUS-UNMATCHED-Y TO TRUE                        26787099
             END-IF                                                     26788099
           END-IF.                                                      26788199
                                                                        26788200
      ****************************************************************  26788300
      **    BYPASS SKU                                                  26788400
      ****************************************************************  26788500
       3400-BYPASS-SKU.                                                 26788699
           ADD 1 TO PA-TOT-SKU-BYPASSED.                                26788799
                                                                        26788800
           IF PS-END-OF-FILE-N                                          26788900
               PERFORM 5000-READ-INPUT-FILE                             26789099
             IF PX031-SKU-NBR NOT = PV-SAVE-SKU-NBR                     26790099
                 SET PS-SKUS-UNMATCHED-Y TO TRUE                        26800099
             END-IF                                                     26810099
           END-IF.                                                      26820099
                                                                        26830099
      ******************************************************************26840099
      * SELECT SKU BY STORE                                             26850099
      ******************************************************************26860099
       4000-SELECT-SKU-BY-STORE.                                        26870099
           MOVE SPACES TO PS-SKU-FOUND-SW.                              26880099
           INITIALIZE DCLXST00008                                       26890099
                      DCLXST00010.                                      26900099
                                                                        26910099
           PERFORM                                                      26920099
               WITH TEST AFTER                                          26930099
               VARYING PV-SQL-SUB                                       26940099
               FROM 1 BY 1                                              26950099
               UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                 26960099
                         OR  SQLCODE = 0                                26970099
                         OR  SQLCODE = +100)                            26980099
                                                                        26990099
               EXEC SQL                                                 27000099
                 SELECT LOC_SKU_STAT_CDE                                27010099
                       ,REGN_PRIC_IND                                   27020099
                 INTO   :XST00008-LOC-SKU-STAT-CDE                      27030099
                       ,:XST00010-REGN-PRIC-IND                         27040099
                 FROM   XST_SKU      A                                  27050099
                       ,XST_SKU_LOC  B                                  27060099
                 WHERE A.INTR_UPC_ID   = :PX031-INTR-UPC-ID             27070099
                   AND A.SKU_NBR       = :PX031-SKU-NBR                 27080099
                   AND A.INTR_UPC_ID   =  B.INTR_UPC_ID                 27090099
                   AND B.LOC_NBR       = :PX031-LOC-NBR                 27100099
                   AND B.END_TMST     IS  NULL                          27110099
                 WITH UR                                                27120099
               END-EXEC                                                 27130099
                                                                        27140099
               EVALUATE TRUE                                            27150099
                   WHEN SQLCODE = 0                                     27160099
                       SET PS-SKU-STORE-FOUND-Y TO TRUE                 27170099
                   WHEN SQLCODE = +100                                  27180099
                       SET PS-SKU-STORE-FOUND-N TO TRUE                 27190099
                   WHEN SQLCODE = -911                                  27200099
                       CONTINUE                                         27210099
                   WHEN SQLWARN NOT = SPACES                            27220099
                   WHEN SQLCODE NOT = ZERO                              27230099
                       MOVE '4000-SELECT-SKU-BY-STORE '                 27240099
                                            TO PV-PARAGRAPH-NAME        27250099
                       MOVE 'SELECT XST_SKU  '                          27260099
                                            TO PV-DB2-OPERATION         27270099
                       PERFORM 9999-SQL-ABEND                           27280099
               END-EVALUATE                                             27290099
           END-PERFORM.                                                 27300099
                                                                        27310099
           IF PV-SQL-SUB GREATER PC-MAX-RETRIES                         27320099
             MOVE '4000-SELECT-SKU-BY-STORE '                           27330099
                                   TO  PV-PARAGRAPH-NAME                27340099
             MOVE '4000- SELECT RETRIES EXCEEDED'                       27350099
                                    TO  PV-DB2-OPERATION                27360099
             PERFORM 9999-SQL-ABEND                                     27370099
           END-IF.                                                      27370199
                                                                        27370200
      ****************************************************************  27370300
      *     READ INPUT FILE.                                            27370400
      ****************************************************************  27370500
       5000-READ-INPUT-FILE.                                            27370600
           READ IN-SKU-FILE INTO PX031-SKU-EXTRACT                      27370700
              AT END                                                    27370800
                 SET PS-END-OF-FILE-Y      TO TRUE                      27370900
                 SET PS-SKUS-UNMATCHED-Y   TO TRUE                      27371099
           END-READ.                                                    27371100
                                                                        27371200
                                                                        27371399
           IF PS-END-OF-FILE-N                                          27371400
               ADD 1 TO PA-TOT-SKU-RECS-READ                            27371599
           END-IF.                                                      27371699
                                                                        27371799
      ****************************************************************  27371800
      **    WRITE OUTPUT FILE                                           27371900
      ****************************************************************  27372000
       6000-WRITE-OUTPUT-FILE.                                          27373099
           INITIALIZE PX032-VALID-MOS-ITEMS.                            27374099
           MOVE PX031-SKU-NBR      TO PX032-SKU-NBR.                    27375099
           MOVE PX031-LOC-NBR      TO PX032-LOC-NBR.                    27376099
           MOVE PX031-MOS-EFF-DATE TO PX032-MOS-EFF-DATE.               27376199
           WRITE OUT-SKU-RECORD FROM PX032-VALID-MOS-ITEMS.             27376200
           ADD 1 TO  PA-TOT-SKU-WRITTEN.                                27376399
                                                                        27376400
      ****************************************************************  27376500
      *     CLOSE FILES.                                                27376600
      ****************************************************************  27376700
       9000-END-OF-JOB.                                                 27376800
           CLOSE IN-SKU-FILE                                            27376900
                 OUT-SKU-FILE.                                          27377000
                                                                        27378000
           DISPLAY ' '.                                                 27379099
           DISPLAY '*********************************************'.     27380099
           DISPLAY '    PXKUT026 SUMMARY STATISTICS              '      27390099
           DISPLAY '*********************************************'.     27400099
           DISPLAY 'INPUT RECORDS READ........: ' PA-TOT-SKU-RECS-READ  27410099
           DISPLAY 'SKU RECORDS BYPASSED......: ' PA-TOT-SKU-BYPASSED   27420099
           DISPLAY 'OUTPUT RECS WRITTEN.......: ' PA-TOT-SKU-WRITTEN    27430099
           DISPLAY '*********************************************'.     27430199
                                                                        27430200
      ******************************************************************27430300
      *     SQL ABEND PARAGRAPH.                                        27430400
      ******************************************************************27430500
       9999-SQL-ABEND.                                                  27430600
           DISPLAY '** ABEND     **'.                                   27430700
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        27430800
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              27430900
           DISPLAY '** OPERATION ** = ' PV-OPERATION-MSG.               27431000
                                                                        27431100
      ******************************************************************27431200
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     27431300
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      27431400
      ******************************************************************27431500
           COPY DPPD004.                                                27431600
           SET AC-DB2-ERROR TO TRUE.                                    27431700
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         27431800
