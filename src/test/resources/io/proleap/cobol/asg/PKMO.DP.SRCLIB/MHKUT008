       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MHKUT008.                                                 
       AUTHOR.        JIM LEIKNESS.                                             
       DATE-WRITTEN.  OCTOBER, 2005.                                            
      *****************************************************************         
      *  MH -  SERVICE LEVEL - TMRITST/TMRITEM MERGE                            
      *****************************************************************         
      *  THE PURPOSE OF THIS PROGRAM IS TO MATCH                      *         
      *   TSKST.TUPC.SKULOC.MERGE FILE TO AN UNLOAD OF TMRITST/TMRITEM          
      *  ALL MATCHES ARE WRITTEN OUT AS REPLENISHMENT RECORDS         *         
      *  ALL RECORDS ONLY ON FS FILE ARE WRITTEN OUT AS NON-MR RECORDS*         
      *  ALL RECORDS ONLY ON MR FILE ARE BYPASSED                     *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT FS-UNLOAD-FILE                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT ITST-ITEM-FILE                                                
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT SVC-LVL-EXTRACT-FILE                                          
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
       FD  FS-UNLOAD-FILE                                                       
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  FS-UNLOAD-RECORD         PIC  X(126).                                
                                                                                
       FD  ITST-ITEM-FILE                                                       
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ITST-ITEM-RECORD         PIC  X(024).                                
                                                                                
       FD  SVC-LVL-EXTRACT-FILE                                                 
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SVC-LVL-EXTRACT-RECORD   PIC  X(40).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-OUTPUT-RECS       PIC  9(11) VALUE ZERO.                      
           05  PV-CLRNCE-RECS       PIC  9(11) VALUE ZERO.                      
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-FS-SW         PIC  X(01) VALUE 'N'.                       
               88  PS-END-FS-FILE              VALUE 'Y'.                       
           05  PS-END-ITST-ITEM-SW  PIC  X(01) VALUE 'N'.                       
               88  PS-END-ITST-ITEM-FILE       VALUE 'Y'.                       
                                                                                
      *-----------------------------------------------------------------        
      *  RECORD LAYOUT FOR THE TSKST.TUPC.SKULOC.MERGE INPUT FILE               
      *-----------------------------------------------------------------        
           COPY FSRD122.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  INPUT RECORD LAYOUT FOR THE TMRITST/TMRITEM UNLOAD FILE                
      *-----------------------------------------------------------------        
           COPY MHRD005.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  OUTPUT RECORD LAYOUT FOR THE SVC LEVEL DATA FILE                       
      *-----------------------------------------------------------------        
           COPY MHRD007.                                                        
                                                                                
               EJECT                                                            
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE.                                                           
      *-----------------------------------------------------------------        
           OPEN OUTPUT SVC-LVL-EXTRACT-FILE                                     
                 INPUT FS-UNLOAD-FILE                                           
                       ITST-ITEM-FILE.                                          
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS UNTIL PS-END-FS-FILE.                           
                                                                                
           CLOSE SVC-LVL-EXTRACT-FILE                                           
                 FS-UNLOAD-FILE                                                 
                 ITST-ITEM-FILE.                                                
                                                                                
           DISPLAY 'EXTRACT RECORDS WRITTEN -- '                                
                   PV-OUTPUT-RECS.                                              
                                                                                
           DISPLAY 'EXTRACT RECORDS SKIPPED -- '                                
                   PV-CLRNCE-RECS.                                              
                                                                                
           STOP RUN.                                                            
                                                                                
      *----------------------------------------------------------------*        
       1000-INITIALIZE.                                                         
      *----------------------------------------------------------------*        
           PERFORM 7000-READ-FS-UNLOAD.                                         
                                                                                
           PERFORM 7100-READ-ITST-ITEM.                                         
                                                                                
      *----------------------------------------------------------------*        
       2000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
      *    --- BYPASS ALL STORE 0, NO FIRST RECEIVED DATE, AND                  
      *    --- CLEARANCE ITEMS                                                  
           EVALUATE TRUE                                                        
              WHEN FS122-LOC-NBR  = 0                                           
              WHEN FS122-FIRST-RCVD-DATE  = 0                                   
              WHEN FS122-UPC-STAT-CDE = '30'                                    
              WHEN FS122-UPC-STAT-CDE = '90'                                    
                 ADD 1 TO PV-CLRNCE-RECS                                        
                 PERFORM 7000-READ-FS-UNLOAD                                    
              WHEN OTHER                                                        
                 PERFORM 2500-MATCH-MERGE                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *  MATCH RECORDS FROM TSKST AND MR UNLOADS                                
      *----------------------------------------------------------------*        
       2500-MATCH-MERGE.                                                        
      *----------------------------------------------------------------*        
                                                                                
           IF PS-END-ITST-ITEM-FILE                                             
              MOVE SPACE  TO MH007-FT-CDE                                       
              MOVE    1   TO MH007-CSTOCK-QTY                                   
              MOVE    1   TO MH007-USERMIN-QTY                                  
              MOVE   'N'  TO MH007-MR-IND                                       
                                                                                
              PERFORM 8000-WRITE-EXTRACT                                        
              PERFORM 7000-READ-FS-UNLOAD                                       
           ELSE                                                                 
                                                                                
           IF FS122-ITEM-NMBR = MH005-ITEM-NBR                                  
              IF FS122-LOC-NBR = MH005-STR-NBR                                  
                                                                                
      ***        --- FS & MR UNLOADS MATCH (REPLENISHMENT ITEM) ---             
                 MOVE MH005-FT-CDE       TO MH007-FT-CDE                        
                 MOVE MH005-CSTOCK-QTY   TO MH007-CSTOCK-QTY                    
                 MOVE MH005-USERMIN-QTY  TO MH007-USERMIN-QTY                   
                 MOVE    'Y'             TO MH007-MR-IND                        
                                                                                
                 PERFORM 8000-WRITE-EXTRACT                                     
                                                                                
                 PERFORM 7000-READ-FS-UNLOAD                                    
                 PERFORM 7100-READ-ITST-ITEM                                    
                                                                                
              ELSE                                                              
      *          --- ITEMS EQUAL, STORE NUMBERS NOT ---                         
                 IF ((FS122-LOC-NBR > MH005-STR-NBR)                            
                    AND NOT PS-END-ITST-ITEM-FILE)                              
                       PERFORM 7100-READ-ITST-ITEM                              
                 ELSE                                                           
      ***           --- NO MATCH (NOT REPLENISHMENT ITEM) ---                   
                    MOVE SPACE            TO MH007-FT-CDE                       
                    MOVE    1             TO MH007-CSTOCK-QTY                   
                    MOVE    1             TO MH007-USERMIN-QTY                  
                    MOVE   'N'            TO MH007-MR-IND                       
                                                                                
                    PERFORM 8000-WRITE-EXTRACT                                  
                    PERFORM 7000-READ-FS-UNLOAD                                 
                 END-IF                                                         
              END-IF                                                            
                                                                                
           ELSE                                                                 
      ***     --- ITEM NUMBERS DO NOT MATCH ---                                 
              IF FS122-ITEM-NMBR > MH005-ITEM-NBR                               
                 IF PS-END-ITST-ITEM-FILE                                       
                                                                                
      ***           --- NO MATCH (NOT REPLENISHMENT ITEM) ---                   
                    MOVE SPACE            TO MH007-FT-CDE                       
                    MOVE    1             TO MH007-CSTOCK-QTY                   
                    MOVE    1             TO MH007-USERMIN-QTY                  
                    MOVE   'N'            TO MH007-MR-IND                       
                                                                                
                    PERFORM 8000-WRITE-EXTRACT                                  
                    PERFORM 7000-READ-FS-UNLOAD                                 
                 ELSE                                                           
                    PERFORM 7100-READ-ITST-ITEM                                 
                 END-IF                                                         
              ELSE                                                              
      ***        --- NO MATCH (NOT REPLENISHMENT ITEM) ---                      
                 MOVE SPACE            TO MH007-FT-CDE                          
                 MOVE    1             TO MH007-CSTOCK-QTY                      
                 MOVE    1             TO MH007-USERMIN-QTY                     
                 MOVE   'N'            TO MH007-MR-IND                          
                                                                                
                 PERFORM 8000-WRITE-EXTRACT                                     
                 PERFORM 7000-READ-FS-UNLOAD                                    
              END-IF                                                            
           END-IF                                                               
           END-IF.                                                              
                                                                                
      *-----------------------------------------------------------------        
       7000-READ-FS-UNLOAD.                                                     
      *-----------------------------------------------------------------        
           READ FS-UNLOAD-FILE                                                  
               INTO FS122-FS-MERGED-RECORD                                      
             AT END                                                             
                SET PS-END-FS-FILE TO TRUE.                                     
                                                                                
      *-----------------------------------------------------------------        
       7100-READ-ITST-ITEM.                                                     
      *-----------------------------------------------------------------        
           READ ITST-ITEM-FILE                                                  
               INTO MH005-MR-UNLOAD-RECORD                                      
             AT END                                                             
                SET PS-END-ITST-ITEM-FILE TO TRUE.                              
                                                                                
      *-----------------------------------------------------------------        
       8000-WRITE-EXTRACT.                                                      
      *-----------------------------------------------------------------        
                                                                                
           MOVE FS122-ITEM-NMBR    TO MH007-ITEM-NBR.                           
           MOVE FS122-INTR-UPC-ID  TO MH007-INTR-UPC-ID.                        
           MOVE FS122-LOC-NBR      TO MH007-LOC-NBR.                            
           MOVE FS122-UPC-STAT-CDE TO MH007-UPC-STAT-CDE.                       
           MOVE FS122-ONHAND-QTY   TO MH007-ONHAND-QTY.                         
                                                                                
           WRITE SVC-LVL-EXTRACT-RECORD                                         
               FROM MH007-SVC-LVL-FINAL-RECORD.                                 
                                                                                
           INITIALIZE SVC-LVL-EXTRACT-RECORD.                                   
           INITIALIZE MH007-SVC-LVL-FINAL-RECORD.                               
           ADD +1 TO PV-OUTPUT-RECS.                                            
                                                                                
