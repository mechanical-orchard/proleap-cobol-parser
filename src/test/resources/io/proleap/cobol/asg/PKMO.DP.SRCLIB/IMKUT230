      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    IMKUT230.                                                 
       AUTHOR.        DAVID FELLENZ.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  12/20/11.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *    IM DELIMIT THE CUSTOMER CHOICE SKU ASSN EXTRACT FILE.                
      ******************************************************************        
      *    THIS PROGRAM WILL CREATE A DELIMITED, FILE FROM THE                  
      *    ALREADY EXTRACTED CCSA FILE.                                         
      ******************************************************************        
      *-----------------------------------------------------------------        
      *    CRQ#     DATE        DESCRIPTION                                     
      *    --------   --------     ---------------------------------            
      *    CRQ20260   02/23/2012   D. FELLENZ  CHANGE PGM TO CREATE             
      *               A EXTRACT FILE FROM THE ALREADY EXTRACTED CC              
      *               FILE. FILTER CRITERIA AS FOLLOWS:                         
      *      1. IF CC EFF_BEG_TMST = CC EFF_END_TMST DO NOT WRITE               
      *          TO PROD FILE (GDG).                                            
      *      2. IF IM121-CUST-CHC-ID = IM123-CUST-CHC-ID                        
      *          WRITE TO EXTRACT FILE.                                         
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT IM-CCSA-EXT-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT IM-CC-EXT-FILE                                                
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT IM-CCSA-DELIM-FILE                                            
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT IM-CCSA-DELIM-COUNT                                           
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT IM-CCSA-ERROR-FILE                                            
               ASSIGN TO OUT03.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  IM-CCSA-EXT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CCSA-EXT-REC                  PIC X(81).                          
                                                                                
       FD  IM-CC-EXT-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CC-EXT-REC                    PIC X(160).                         
                                                                                
       FD  IM-CCSA-DELIM-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CCSA-DELIM-REC                PIC X(93).                          
                                                                                
       FD  IM-CCSA-DELIM-COUNT                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CCSA-DELIM-CNT                PIC X(80).                          
                                                                                
       FD  IM-CCSA-ERROR-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CCSA-ERROR-REC                PIC X(93).                          
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      ***************************************************************           
      *   INPUT RECORD LAYOUT                                                   
      ***************************************************************           
       01  IM-CC-SKU-ASSN-REC.                                                  
           05 IM-CUST-CHC-ID                PIC  S9(9) BINARY.                  
           05 IM-SKU-NBR                    PIC  S9(8)V COMP-3.                 
           05 IM-EFF-BEG-TMST               PIC  X(26).                         
           05 IM-EFF-BEG-BREAKOUT-TMST REDEFINES IM-EFF-BEG-TMST.               
              10 IM-EFF-BEG-DATE            PIC  X(10).                         
              10 IM-EFF-BEG-TIME            PIC  X(16).                         
           05 IM-CRE-DTE                    PIC  X(10).                         
           05 IM-CRE-BY-USR-ID              PIC  X(09).                         
           05 IM-EFF-END-TMST               PIC  X(26).                         
           05 IM-EFF-END-BREAKOUT-TMST REDEFINES IM-EFF-END-TMST.               
              10 IM-EFF-END-DATE            PIC  X(10).                         
              10 IM-EFF-END-TIME            PIC  X(16).                         
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(8)   VALUE 'IMKUT230'.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW            PIC X      VALUE 'N'.               
               88  PS-END-OF-FILE                      VALUE 'Y'.               
               88  PS-NOT-END-OF-FILE                  VALUE 'N'.               
           05  PS-END-OF-CC-FILE-SW         PIC X      VALUE 'N'.               
               88  PS-END-OF-CC-FILE                   VALUE 'Y'.               
               88  PS-NOT-END-OF-CC-FILE               VALUE 'N'.               
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORD-COUNT        PIC 9(09)  VALUE 0.                 
           05  PA-OUTPUT-RECORD-COUNT1      PIC 9(12)  VALUE 0.                 
           05  PA-OUTPUT-RECORD-COUNT2      PIC 9(09)  VALUE 0.                 
                                                                                
       01  PROGRAM-VARIABLES.                                                   
                                                                                
           05  PV-IN-COUNTER1               PIC 9(9)    VALUE 0.                
           05  PV-OUT-COUNTER1              PIC 9(9)    VALUE 0.                
                                                                                
           05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
                                                                                
      ******************************************************************        
      * INPUT FILE LAYOUT FOR IM CC DAILY DELIM FILE                            
      ******************************************************************        
           COPY IMRD123.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT FOR IM CCSA DAILY DELIM FILE                         
      ******************************************************************        
           COPY IMRD121.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-FILE                                            
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           PERFORM 8200-WRITE-CCSA-DELIM-CNT.                                   
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  OPEN ALL FILES.                                                        
      *  READ THE FIRST RECORD FROM THE INPUT FILE.                             
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  IM-CCSA-EXT-FILE                                         
                       IM-CC-EXT-FILE                                           
                OUTPUT IM-CCSA-DELIM-FILE                                       
                       IM-CCSA-DELIM-COUNT                                      
                       IM-CCSA-ERROR-FILE.                                      
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 8000-READ-CCSA-EXT-FILE.                                     
           PERFORM 8400-READ-CC-EXT-FILE.                                       
                                                                                
      ******************************************************************        
      *  FORMAT AND WRITE THE OUTPUT RECORDS.                                   
      *  READ THE NEXT INPUT RECORD.                                            
      ******************************************************************        
       2000-PROCESS-FILE.                                                       
                                                                                
           INITIALIZE IM121-CC-SKU-ASSN-DELIM-REC.                              
                                                                                
           MOVE IM-CUST-CHC-ID       TO  IM121-CUST-CHC-ID.                     
                                                                                
           MOVE IM-SKU-NBR           TO  IM121-SKU-NBR.                         
                                                                                
           MOVE IM-EFF-BEG-TMST      TO  IM121-EFF-BEG-TMST.                    
                                                                                
           MOVE IM-CRE-DTE           TO  IM121-CRE-DTE.                         
                                                                                
           MOVE IM-CRE-BY-USR-ID     TO  IM121-CRE-BY-USR-ID.                   
                                                                                
           MOVE IM-EFF-END-TMST      TO  IM121-EFF-END-TMST.                    
                                                                                
           IF IM121-CUST-CHC-ID = IM123-CUST-CHC-ID                             
              IF IM-EFF-BEG-DATE = IM-EFF-END-DATE                              
                 PERFORM 8300-WRITE-CCSA-ERROR-REC                              
              ELSE                                                              
                 PERFORM 8100-WRITE-CCSA-DELIM-REC                              
              END-IF                                                            
              PERFORM 8000-READ-CCSA-EXT-FILE                                   
           ELSE                                                                 
              IF IM121-CUST-CHC-ID < IM123-CUST-CHC-ID                          
                PERFORM 8000-READ-CCSA-EXT-FILE                                 
              ELSE                                                              
                PERFORM 8400-READ-CC-EXT-FILE                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
      ************************************************************              
      *  READ THE CCSA EXTRACT FILE                                             
      ************************************************************              
       8000-READ-CCSA-EXT-FILE.                                                 
                                                                                
           READ IM-CCSA-EXT-FILE INTO IM-CC-SKU-ASSN-REC                        
               AT END                                                           
                   SET PS-END-OF-FILE TO TRUE                                   
               NOT AT END                                                       
                   ADD 1          TO   PA-INPUT-RECORD-COUNT.                   
                                                                                
      ******************************************************************        
      *  WRITE A CCSA DELIM RECORD.                                             
      ******************************************************************        
       8100-WRITE-CCSA-DELIM-REC.                                               
                                                                                
           WRITE IM-CCSA-DELIM-REC                                              
               FROM IM121-CC-SKU-ASSN-DELIM-REC.                                
                                                                                
           ADD 1 TO PA-OUTPUT-RECORD-COUNT1.                                    
                                                                                
      ******************************************************************        
      *  WRITE A CCSA DELIM COUNT RECORD                                        
      ******************************************************************        
       8200-WRITE-CCSA-DELIM-CNT.                                               
                                                                                
           WRITE IM-CCSA-DELIM-CNT                                              
               FROM PA-OUTPUT-RECORD-COUNT1.                                    
                                                                                
      ******************************************************************        
      *  WRITE A CCSA ERROR RECORD.                                             
      ******************************************************************        
       8300-WRITE-CCSA-ERROR-REC.                                               
                                                                                
           WRITE IM-CCSA-ERROR-REC                                              
               FROM IM121-CC-SKU-ASSN-DELIM-REC.                                
                                                                                
      ************************************************************              
      *  READ THE CC EXTRACT FILE                                               
      ************************************************************              
       8400-READ-CC-EXT-FILE.                                                   
                                                                                
           READ IM-CC-EXT-FILE INTO IM123-CC-DELIM-RECORD                       
               AT END                                                           
               MOVE '9999999999' TO IM123-CUST-CHC-ID                           
                SET PS-END-OF-CC-FILE TO TRUE.                                  
                                                                                
      ************************************************************              
      *  DISPLAY RECORD COUNTS AT END OF JOB                                    
      *  CLOSE THE FILES.                                                       
      ************************************************************              
       9000-END-OF-JOB.                                                         
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
                                                                                
           DISPLAY '   IM CCSA RECORDS INPUT         = '                        
                    PA-INPUT-RECORD-COUNT.                                      
                                                                                
           DISPLAY '   IM CCSA DELIM RECS OUTPUT      = '                       
                    PA-OUTPUT-RECORD-COUNT1.                                    
                                                                                
                                                                                
           CLOSE IM-CCSA-EXT-FILE                                               
                 IM-CC-EXT-FILE                                                 
                 IM-CCSA-DELIM-FILE                                             
                 IM-CCSA-DELIM-COUNT                                            
                 IM-CCSA-ERROR-FILE.                                            
                                                                                
