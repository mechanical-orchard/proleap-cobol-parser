00001 ******************************************************************01/26/90
00002 *  COPY MEMBER  DPPD000.                                         *DPPD000 
00003 *                                                                *   LV018
00004 *  THIS COPY MEMBER CONTAINS THE PROCEDURE DIVISION STATMENTS    *DPPD000 
00005 *  THAT ARE REQUIRED TO INVOKE THE TRANSACTION PREPARATION       *   CL*10
00006 *  MODULE (DPKUT900) OF THE DB2 CHECKPOINT RESTART SYSTEM.       *   CL*10
00007 *                                                                *DPPD000 
00008 ******************************************************************DPPD000 
00009 *                                                                *DPPD000 
00010 *  LISTED BELOW ARE NOTES ON WHAT NEEDS TO BE DEFINED IN THE     *   CL**9
00011 *  DATA AND PROCEDURE DIVISIONS TO USE THIS COPY MEMEBER:        *   CL**9
00012 *                                                                *   CL**9
00013 *  DEFINE A FIELD IN WORKING-STORAGE CALLED PC-PROGRAM-NAME      *   CL*18
00014 *  THAT CONTAINS THE NAME OF THE TRANSACTION PREPARATION PRO-    *   CL*10
00015 *  GRAM.                                                         *   CL*10
00016 *                                                                *   CL*10
00017 *  ENSURE THAT THE ASSOCIATED WORKING-STORAGE COPY MEMBER        *   CL*10
00018 *  (DPLS000) IS COPIED INTO THE WORKING-STORAGE SECTION OF YOUR  *   CL*10
00019 *  TRANSACTION PREPARATION PROGRAM.                              *   CL*10
00020 *                                                                *   CL**7
00021 *  ENSURE THAT COPY MEMBER (SQLCA2) IS COPIED INTO THE WORKING-  *   CL*15
00022 *  STORAGE SECTION OF YOUR TRANSACTION PREPARATION PROGRAM.      *   CL*15
00023 *                                                                *   CL*15
00024 *  ENSURE THAT THE FOLLOWING STATEMENTS ARE CODED WITHIN THE     *   CL**5
00025 *  INITIALIZATION ROUTINE OF YOUR PROGRAM:                       *   CL**5
00026 *                                                                *DPPD000 
00027 *    1.  MOVE 'PPPPPPPP' TO DP000-PLAN-NAME.                     *   CL*10
00028 *        (INSERT THE PLAN NAME OF THE DB2 MAINTENANCE PROGRAM    *   CL*10
00029 *         THAT THE TRANSACTIONS ARE BEING PREPARED FOR IN PLACE  *   CL*10
00030 *         OF PPPPPPPP).                                          *   CL*10
00031 *    2.  MOVE 'JJJJJJJJ' TO DP000-JOB-NAME.                      *   CL*10
00032 *        (INSERT THE JOB NAME OF THE JOB THAT THE DB2 MAINTEN-   *   CL*10
00033 *         ANCE PROGRAM IS EXECUTED IN, IN PLACE OF JJJJJJJJ).    *   CL*10
00034 *                                                                *DPPD000 
00035 *  CODE THE FOLLOWING STATEMENTS IN YOUR PROGRAM WHENEVER YOU    *   CL**5
00036 *  ARE INVOKING THE TRANSACTION PRESENTATION MODULE:             *   CL**5
00037 *                                                                *   CL**5
00038 *    1.  MOVE 'FFFFF' TO DP001-FUNC-CODE.                        *   CL**5
00039 *        (INSERT THE FUNCTION YOU WANT THE TRANSACTION PREPAR-   *   CL*10
00040 *         ATION MODULE TO PERFORM IN PLACE OF FFFFF.  VALID      *   CL*10
00041 *         FUNCTION CODES ARE: OPEN, WRITE & CLOSE).              *   CL*10
00042 *    2.  PERFORM Z900-CALL-TRANS-PREP-MODULE.                    *   CL*10
00043 *                                                                *   CL**5
00044 ******************************************************************DPPD000 
00043 * CHG0260107 - 8/24/2021 - CONVERTING CALL OF DPKUT900 FROM      *        
00043 *                          STATIC TO DYNAMIC.                    *        
00044 ******************************************************************DPPD000 
00045  EJECT                                                               CL**9
00046  Z900-CALL-TRANS-PREP-MODULE.                                        CL*10
00047 ******************************************************************DPPD000 
00048 *  CALL THE TRANSACTION PREPARATION MODULE.                      *   CL*10
00049 ******************************************************************DPPD000 
00050                                                                      CL**7
00051      CALL DP990-TRANS-PREP-SUBROUTINE                                     
00076                              USING DP000-TRANS-PREP-PARAMETERS.           
00052                                                                      CL**7
00053      IF DP000-BAD-SQLCODE                                            CL*10
00054          PERFORM Z999-SQL-ERROR                                      CL**9
00055      ELSE                                                            CL**9
00056          IF (DP000-INVALID-FUNC-CODE                                 CL*18
00057           OR DP000-TRAN-PREP-EXISTS                                  CL*10
00058           OR DP000-CKPT-CNTLS-IN-USE                                 CL*12
00059           OR DP000-JOB-PLAN-DOESNT-EXIST)                            CL*18
00060              PERFORM Z910-FORMAT-MSG-AND-ABEND                       CL**9
00061          END-IF                                                      CL**9
00062      END-IF.                                                         CL**9
00063  EJECT                                                               CL**8
00064  Z910-FORMAT-MSG-AND-ABEND.                                          CL**8
00065 ******************************************************************DPPD000 
00066 *  FORMAT AND DISPLAY AN ABEND MESSAGE CONTAINING PERTINANT      *   CL**8
00067 *  INFORMATION RETURNED FROM THE TRANSACTION PREPARATION MODULE  *   CL*10
00068 *  IN REGARD TO THE BAD RETURN CODE ENCOUNTERED AND TERMINATE    *   CL**8
00069 *  THE PROGRAM.                                                  *   CL**8
00070 ******************************************************************DPPD000 
00071                                                                      CL**9
00072      DISPLAY '** ABEND       ** = ' DP000-MESSAGE.                   CL*10
00073      DISPLAY '** PROGRAM     ** = ' PC-PROGRAM-NAME.                 CL*18
00074      DISPLAY '** PARAMETERS  **'.                                    CL**9
00075      DISPLAY '    FUNCTION CODE = ' DP000-FUNC-CODE.                 CL*10
00076      DISPLAY '         JOB NAME = ' DP000-JOB-NAME.                  CL*10
00077      DISPLAY '        PLAN NAME = ' DP000-PLAN-NAME.                 CL*10
00078      MOVE DP000-RET-CODE TO ABEND-CODE.                              CL*10
00079      CALL 'ILBOABN0' USING ABEND-CODE.                               CL**9
00080 *                                                                 DPPD000 
00081  Z999-SQL-ERROR.                                                  DPPD000 
00082 ******************************************************************DPPD000 
00083 *  THIS MODULE PERFORMS THE ABEND IN THE EVENT THE RETURN CODE   *   CL**9
00084 *  FROM THE TRANSACTION PRESENTATION MODULE INDICATES AN SQL     *   CL**9
00085 *  ERROR.                                                        *   CL**9
00086 ******************************************************************DPPD000 
00087                                                                      CL**9
00088      MOVE DP000-SQLCODE TO SQLCODE2.                                 CL*10
00089      MOVE DP000-SQLERRD3 TO SQLERRD3.                                CL*10
00090      DISPLAY '** ABEND         ** = ' DP000-MESSAGE.                 CL*10
00091      DISPLAY '** PROGRAM       ** = ' PC-PROGRAM-NAME.               CL*18
00092      DISPLAY '** FUNCTION CODE ** = ' DP000-FUNC-CODE.               CL*10
00093      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                DPPD000 
00094      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                DPPD000 
00095      DISPLAY '** SQLERRMC         ** = ' DP000-SQLERRMC.             CL*10
00096      DISPLAY '**         SQLWARN0 ** = ' DP000-SQLWARN0.             CL*13
00097      DISPLAY '**         SQLWARN1 ** = ' DP000-SQLWARN1.             CL*13
00098      DISPLAY '**         SQLWARN2 ** = ' DP000-SQLWARN2.             CL*13
00099      DISPLAY '**         SQLWARN3 ** = ' DP000-SQLWARN3.             CL*13
00100      DISPLAY '**         SQLWARN4 ** = ' DP000-SQLWARN4.             CL*13
00101      DISPLAY '**         SQLWARN5 ** = ' DP000-SQLWARN5.             CL*13
00102      DISPLAY '**         SQLWARN6 ** = ' DP000-SQLWARN6.             CL*13
00103      DISPLAY '**         SQLWARN7 ** = ' DP000-SQLWARN7.             CL*13
00104                                                                      CL*16
00105      MOVE DP000-RET-CODE TO ABEND-CODE.                              CL*10
00106      CALL 'ILBOABN0' USING ABEND-CODE.                               CL**9
00107  EJECT                                                               CL**9
