       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP353                                          00030017
       AUTHOR.        ERIN SEARL.                                       00040019
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  8-09-2004.                                        00060019
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP353 - MONTHLY DETAILED MERCHANDISE LEDGER       *00100059
      *                      TABLE UPDATE (MLT_MNLY_SUM)               *00110062
      ******************************************************************00110113
      ******************************************************************00110283
      *    WR/PITS#     DATE        DESCRIPTION                        *00110383
      *    --------   --------     ---------------------------------   *00110483
      *    W28582     12/14/2005   CHANGED ENT-ID TO ML-LWST-ID OR     *00110583
      *                            ML-LO-MDSE-LVL-ID.      C. MCCAMMON *00110683
R28582* W28582 11-21-2006 ROD JANSSEN  ADD ICA1, ICA2, EIC1, EIC2, GM$  00110784
      ******************************************************************00111083
                                                                        00140000
       ENVIRONMENT DIVISION.                                            00150000
                                                                        00160000
       CONFIGURATION SECTION.                                           00170000
       SOURCE-COMPUTER.        IBM-3090.                                00180000
       OBJECT-COMPUTER.        IBM-3090.                                00190000
       INPUT-OUTPUT SECTION.                                            00200000
       FILE-CONTROL.                                                    00210000
           SELECT CONDENSED-FILE                                        00220000
               ASSIGN TO INP01.                                         00230000
                                                                        00240000
                                                                        00250000
       DATA DIVISION.                                                   00260000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  CONDENSED-FILE                                               00290000
           LABEL RECORDS ARE STANDARD                                   00300000
           RECORDING MODE IS F                                          00310000
           BLOCK CONTAINS 0 RECORDS.                                    00320000
       01  CONDENSED-RECORD            PIC X(400).                      00330072
      *                                                                 00340000
                                                                        00350000
       WORKING-STORAGE SECTION.                                         00360000
                                                                        00370000
       01  FILLER                      PIC X(28)   VALUE                00384000
                'BEGINNING OF WORKING STORAGE'.                         00390000
                                                                        00400000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00410000
                                                                        00420000
       01  PC-PROGRAM-CONSTANTS.                                        00430000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK183'.    00440056
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP353'.  00450020
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00460000
                                                                        00470000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00480000
           05  PE-MSG-01                       PIC X(50) VALUE          00490000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00500000
           05  PE-MSG-02                       PIC X(50) VALUE          00510000
                'ATTEMPT TO UPDATE ROW ON TABLE MLT_MNLY_SUM FAILED'.   00520062
           05  PE-MSG-03                       PIC X(50) VALUE          00530000
                'ATTEMPT TO INSERT ROW ON TABLE MLT_MNLY_SUM FAILED'.   00540062
           05  PE-MSG-04                       PIC X(50) VALUE          00550000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00560000
           05  PE-MSG-05                       PIC X(50) VALUE          00570000
                'UPDATE TO TCKRSTINF FAILED                      '.     00580000
           05  PE-MSG-06                       PIC X(50) VALUE          00590000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00600000
           05  PE-MSG-07                       PIC X(50) VALUE          00610000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00620000
      *                                                                 00630000
       01  PS-PROGRAM-SWITCHES.                                         00640000
           05  PS-EOF-CONDENSED-FILE-SW     PIC X(01)     VALUE 'N'.    00650000
               88  PS-EOF-CONDENSED-FILE                  VALUE 'Y'.    00660000
           05  PS-MLT00004-CURSOR-EOF-SW    PIC X(01)     VALUE 'N'.    00670065
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00680000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00690000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00700000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00710000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00720000
                                                                        00730000
       01  PI-PROGRAM-INDICATORS.                                       00740000
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00750000
               88  PI-UPDATE-MLT00004-ROW                 VALUE 'U'.    00760065
               88  PI-INSERT-MLT00004-ROW                 VALUE 'I'.    00770065
      *                                                                 00780000
       01  PROGRAM-VARIABLES.                                           00790000
           05  PV-RETRY-COUNT            PIC S9(04) VALUE +0.           00800000
           05  PV-DEADLOCK-COUNT         PIC S9(04) VALUE +0.           00810000
W28582     05  PV-ML-LWST-ID             PIC 9(09)  VALUE 0.            00820083
W28582***  05  PV-ENT-ID                 PIC 9(09)  VALUE 0.            00821083
      *                                                                 00830000
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACES.       00840000
           05  PV-ABEND-MESSAGE          PIC  X(75) VALUE SPACES.       00850000
           05  PV-OPERATION-ATTEMPTED    PIC  X(50) VALUE SPACES.       00860000
           05  PV-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.       00870000
           05  PV-COMMIT-TIMESTAMP       PIC  X(26) VALUE SPACES.       00880000
           05  PV-MKUP-NET-AMT           PIC S9(11)V99 VALUE +0 COMP-3. 00890027
           05  PV-MKUP-INIT-AMT          PIC S9(11)V99 VALUE +0 COMP-3. 00900027
           05  PV-NET-COST               PIC S9(11)V99 VALUE +0 COMP-3. 00900127
           05  PV-NET-RETAIL             PIC S9(11)V99 VALUE +0 COMP-3. 00901027
                                                                        00901173
      *    05  PV-DEPT-NBR               PIC 9(03)   VALUE ZEROES.      00902073
      *    05  PV-DEPT-NBR-2 REDEFINES                                  00903073
      *        PV-DEPT-NBR               PIC S9(4)V  COMP-3.            00904073
      *    05  PV-SUB-CL-NBR             PIC 9(02)   VALUE ZEROES.      00905073
      *    05  PV-DEPT-NBR-2 REDEFINES                                  00906073
      *        PV-DEPT-NBR               PIC S9(4)V  COMP-3.            00907073
      *                                                                 00910000
       01  PA-PROGRAM-ACCUMULATORS.                                     00920000
           05  PA-RECORD-COUNTS.                                        00930000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      00940000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      00950000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      00960000
               10  PA-MAJ-CL-PROCESSED   PIC  9(09)  VALUE ZEROES.      00970000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      00980000
                                                                        00990000
       01  PD-PROGRAM-DISPLAYS.                                         01000000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01010000
       01  SAVE-AREA.                                                   01020000
           05  SV-PRIMARY-KEY.                                          01030000
               10  SV-PARTN-NBR          PIC S9(4)  COMP     VALUE +0.  01040060
               10  SV-DEPT-NBR           PIC S9(4)V COMP-3   VALUE +0.  01060028
               10  SV-MAJ-CL-NBR         PIC S9(4)V COMP-3   VALUE +0.  01070028
               10  SV-SUB-CL-NBR         PIC S9(4)V COMP-3   VALUE +0.  01071075
               10  SV-LOC-NBR            PIC S9(4)  COMP     VALUE +0.  01071175
W28582         10  SV-ML-LWST-ID         PIC S9(9)  COMP     VALUE +0.  01071283
W28582***      10  SV-ENT-ID             PIC S9(9)  COMP     VALUE +0.  01071383
           05  SV-UPDATE-FIELDS.                                        01080000
               10  SV-SLS-REG-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01081048
               10  SV-SLS-PROMO-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01082048
               10  SV-SLS-PROCLR-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01083048
               10  SV-SLS-CLR-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01084048
               10  SV-SLS-OTH-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01085048
               10  SV-MKDN-PLU-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01086048
               10  SV-MKDN-PROMO-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01087048
               10  SV-MKDN-RGST-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01088048
               10  SV-MKDN-GLDST-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01089048
               10  SV-MKDN-UMTCH-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01089148
               10  SV-MKDN-BYR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01089248
               10  SV-MKDN-STR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01089348
               10  SV-MKDN-PRPT-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01089448
               10  SV-MKDN-OTH-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01089548
               10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01089648
               10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01089748
               10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01089848
               10  SV-XFER-OUT-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01089948
               10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01090048
               10  SV-RCPT-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01090148
               10  SV-RCPT-GRO-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01090248
               10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01090348
               10  SV-PADJ-FRGT-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01090448
               10  SV-PADJ-DISC-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01090548
               10  SV-PADJ-RTV-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01090648
               10  SV-PADJ-RTV-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01090748
               10  SV-PADJ-DA-COST-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01090848
               10  SV-PADJ-DA-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01090948
               10  SV-PADJ-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01091048
               10  SV-PADJ-OTH-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01091148
               10  SV-PADJ-OTH-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01091248
               10  SV-DR-ALW-RTL-AMT     PIC S9(11)V99  COMP-3 VALUE +0.01091348
               10  SV-RTV-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01091448
               10  SV-MKUP-INIT-AMT      PIC S9(11)V99  COMP-3 VALUE +0.01091548
               10  SV-MKUP-NET-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01091648
               10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01091748
               10  SV-ONORD-RTL-AMT      PIC S9(11)V99  COMP-3 VALUE +0.01091848
               10  SV-ONORD-COST-AMT     PIC S9(11)V99  COMP-3 VALUE +0.01091948
               10  SV-SHNK-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01092048
               10  SV-END-INV-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01092148
      *                                                                 01360000
      ******************************************************************01370000
      * MLRD156 - MTH DETAILED MERCH LEDGER UPDATE TXN LAYOUT           01380079
      ******************************************************************01390000
           COPY MLRD156.                                                01400037
      *                                                                 01410000
      ******************************************************************01420000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01430000
      ******************************************************************01440000
           COPY DPWS004.                                                01450000
      *                                                                 01460000
      ******************************************************************01470000
      *  USED FOR DB2 ERRORS                                            01480000
      ******************************************************************01490000
           EXEC SQL                                                     01500000
               INCLUDE SQLCA                                            01510000
           END-EXEC.                                                    01520000
      *                                                                 01530000
      ******************************************************************01540000
      *  MERCHANDISE LEDGER MONTHLY DETAIL TABLE                        01550079
      ******************************************************************01560000
           EXEC SQL                                                     01570052
               INCLUDE MLT00004                                         01580065
           END-EXEC.                                                    01590052
      *                                                                 01600000
      ***************************************************************** 01614000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01620000
      ***************************************************************** 01630000
           EXEC SQL                                                     01640000
               INCLUDE TCKRSTCN                                         01650000
           END-EXEC.                                                    01660000
      *                                                                 01670000
           EXEC SQL                                                     01680000
               INCLUDE TCKRSTIN                                         01690000
           END-EXEC.                                                    01700000
      *                                                                 01710000
      ******************************************************************01720000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01730000
      ******************************************************************01740000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01750000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +35  COMP. 01760007
           05  CR-CHECKPOINT-RESTART-VAR.                               01770000
               10  CR-PREV-DTL-KEY.                                     01780082
                   15  CR-PARTN-NBR         PIC  S9(4)  COMP            01800082
                                                         VALUE ZEROES.  01810082
                   15  CR-DEPT-NBR          PIC  S9(4)V COMP-3          01820082
                                                         VALUE ZEROES.  01821082
                   15  CR-MAJ-CL-NBR        PIC  S9(4)V COMP-3          01830082
                                                         VALUE ZEROES.  01830182
                   15  CR-SUB-CL-NBR        PIC  S9(4)V COMP-3          01831082
                                                         VALUE ZEROES.  01831182
                   15  CR-LOC-NBR           PIC  S9(4)  COMP            01831282
                                                         VALUE ZEROES.  01831382
W28582             15  CR-ML-LWST-ID        PIC  S9(9)  COMP            01831483
W28582***          15  CR-ENT-ID            PIC  S9(9)  COMP            01831583
                                                         VALUE ZEROES.  01831683
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01850000
      *                                                                 01860000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01870000
           05  CK-SAVE-PREV-KEY.                                        01880082
               10  CK-PARTN-NBR         PIC  S9(4)  COMP VALUE ZEROES.  01900082
               10  CK-DEPT-NBR          PIC  S9(4)V COMP-3              01920082
                                                         VALUE ZEROES.  01921082
               10  CK-MAJ-CL-NBR        PIC  S9(4)V COMP-3              01930082
                                                         VALUE ZEROES.  01931082
               10  CK-SUB-CL-NBR        PIC  S9(4)V COMP-3              01940082
                                                         VALUE ZEROES.  01940182
               10  CK-LOC-NBR           PIC  S9(4)  COMP VALUE ZEROES.  01940282
W28582         10  CK-ML-LWST-ID        PIC  S9(9)  COMP VALUE ZEROES.  01940383
W28582***      10  CK-ENT-ID            PIC  S9(9)  COMP VALUE ZEROES.  01940483
                                                                        01950000
                                                                        01960000
      *-------------------*                                             01970000
       PROCEDURE DIVISION.                                              01980000
      *-------------------*                                             01990000
                                                                        02000000
       0000-MLKUP353.                                                   02010020
                                                                        02020000
           PERFORM 1000-INITIALIZATION.                                 02030000
      *                                                                 02040000
           PERFORM 2000-PROCESS-INPUT                                   02050000
                UNTIL PS-EOF-CONDENSED-FILE.                            02060000
      *                                                                 02070000
           PERFORM 8000-EOJ-ROUTINE.                                    02080000
                                                                        02090000
           STOP RUN.                                                    02100000
                                                                        02110000
      ******************************************************************02120000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02130000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02140000
      ******************************************************************02150000
      *                                                                 02160000
       1000-INITIALIZATION.                                             02170000
      *                                                                 02180000
           OPEN INPUT CONDENSED-FILE.                                   02190000
                                                                        02200000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02210000
                                                                        02220000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02230000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02240000
               PERFORM 7500-SELECT-RESTART-INFO                         02250000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02260000
                                            CR-CHECKPOINT-RESTART-VAR   02270000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        02280000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02290000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   02320000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 02330000
               DISPLAY 'SUB CLASS NBR   ' CR-SUB-CL-NBR                 02340075
               DISPLAY 'LOCATION NBR    ' CR-LOC-NBR                    02340175
W28582         DISPLAY 'ML-LWST-ID      ' CR-ML-LWST-ID                 02340283
W28582***      DISPLAY 'ENT-ID          ' CR-ENT-ID                     02340383
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02350000
           ELSE                                                         02360000
               SET PS-FIRST-COMMIT TO TRUE                              02370000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02380000
           END-IF.                                                      02390000
      *                                                                 02400000
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02410000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02420000
            OR PS-EOF-CONDENSED-FILE.                                   02430000
      *                                                                 02440000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02450000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02460000
                        PA-INPUT-COUNT                                  02470000
               DISPLAY 'DEPARTMENT NBR        ' ML156-DEPT-NBR          02500037
               DISPLAY 'MAJOR CLASS NBR       ' ML156-MAJ-CL-NBR        02510037
               DISPLAY 'SUB CLASS NBR         ' ML156-SUB-CL-NBR        02511075
               DISPLAY 'LOCATION NBR          ' ML156-LOC-NBR           02511175
W28582         DISPLAY 'ML-LWST-ID            ' ML156-ML-LWST-ID        02511283
W28582         MOVE ML156-ML-LWST-ID   TO PV-ML-LWST-ID                 02520083
W28582         DISPLAY 'ML-LWST-ID            ' PV-ML-LWST-ID           02530083
W28582***      DISPLAY 'ENT-ID                ' ML156-ENT-ID            02531083
W28582***      MOVE ML156-ENT-ID   TO PV-ENT-ID                         02532083
W28582***      DISPLAY 'ENT-ID                ' PV-ENT-ID               02533083
           END-IF.                                                      02540000
                                                                        02550000
           IF PS-EOF-CONDENSED-FILE                                     02560000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02570000
                   CONTINUE                                             02580000
               ELSE                                                     02590000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02600000
           ELSE                                                         02610000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02620000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02630000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02640000
           END-IF.                                                      02650000
                                                                        02660000
      ******************************************************************02670000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02680000
      ******************************************************************02690000
       2000-PROCESS-INPUT.                                              02700000
      *                                                                 02710000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02720000
      *                                                                 02730000
           PERFORM 7000-SELECT-DB-ROW.                                  02740000
      *                                                                 02750000
           IF PS-PROGRAM-DEADLOCKED                                     02760000
               CONTINUE                                                 02770000
           ELSE                                                         02780000
               EVALUATE TRUE                                            02790000
                   WHEN PI-UPDATE-MLT00004-ROW                          02800065
                       PERFORM 7100-UPDATE-MLT00004-ROW                 02810065
                   WHEN PI-INSERT-MLT00004-ROW                          02820065
                       PERFORM 7200-INSERT-MLT00004-ROW                 02830065
                   WHEN OTHER                                           02840000
                       MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   02850000
                       MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         02860000
                       PERFORM 9999-SQL-ABEND                           02870000
               END-EVALUATE                                             02880000
           END-IF.                                                      02890000
      *                                                                 02900000
           IF PS-PROGRAM-DEADLOCKED                                     02910000
               CONTINUE                                                 02920000
           ELSE                                                         02930000
               PERFORM 7700-COMMIT-PROCESSING                           02940000
               PERFORM 4000-READ-CONDENSED-FILE                         02950000
           END-IF.                                                      02960000
      *                                                                 02970000
      *                                                                 02980000
      ******************************************************************02990000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         03000000
      ******************************************************************03010000
       2100-CALCULATE-UPDATES.                                          03011080
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     03012080
      *                                                                 03013080
           COMPUTE SV-SLS-REG-RTL-AMT =                                 03014080
                   MLT00004-SLS-REG-RTL-AMT + ML156-SLS-REG-RTL-AMT.    03015080
           COMPUTE SV-SLS-PROMO-RTL-AMT =                               03016080
                   MLT00004-SLS-PROMO-RTL-AMT + ML156-SLS-PROMO-RTL-AMT.03017080
           COMPUTE SV-SLS-PROCLR-RTL-AMT =                              03018080
                 MLT00004-SLS-PROCLR-RTL-AMT + ML156-SLS-PROCLR-RTL-AMT.03019080
           COMPUTE SV-SLS-CLR-RTL-AMT =                                 03019180
                   MLT00004-SLS-CLR-RTL-AMT + ML156-SLS-CLR-RTL-AMT.    03019280
           COMPUTE SV-SLS-OTH-RTL-AMT =                                 03019380
                   MLT00004-SLS-OTH-RTL-AMT + ML156-SLS-OTH-RTL-AMT.    03019480
           COMPUTE SV-MKDN-PLU-RTL-AMT  =                               03019580
                   MLT00004-MKDN-PLU-RTL-AMT + ML156-MKDN-PLU-RTL-AMT.  03019680
           COMPUTE SV-MKDN-RGST-RTL-AMT =                               03019780
                   MLT00004-MKDN-RGST-RTL-AMT + ML156-MKDN-RGST-RTL-AMT.03019880
           COMPUTE SV-MKDN-UMTCH-RTL-AMT =                              03019980
                 MLT00004-MKDN-UMTCH-RTL-AMT + ML156-MKDN-UMTCH-RTL-AMT.03020080
           COMPUTE SV-MKDN-BYR-RTL-AMT  =                               03020180
                   MLT00004-MKDN-BYR-RTL-AMT + ML156-MKDN-BYR-RTL-AMT.  03020280
           COMPUTE SV-MKDN-STR-RTL-AMT  =                               03020380
                   MLT00004-MKDN-STR-RTL-AMT + ML156-MKDN-STR-RTL-AMT.  03020480
           COMPUTE SV-MKDN-RTL-AMT      =                               03020581
                   MLT00004-MKDN-RTL-AMT    + ML156-MKDN-RTL-AMT.       03020681
           COMPUTE SV-MKUP-RTL-AMT      =                               03020781
                   MLT00004-MKUP-RTL-AMT    + ML156-MKUP-RTL-AMT.       03020881
           COMPUTE SV-XFER-IN-RTL-AMT   =                               03020981
                   MLT00004-XFER-IN-RTL-AMT + ML156-XFER-IN-RTL-AMT.    03021081
           COMPUTE SV-XFER-OUT-RTL-AMT  =                               03021181
                   MLT00004-XFER-OUT-RTL-AMT + ML156-XFER-OUT-RTL-AMT.  03021281
           COMPUTE SV-RCPT-RTL-AMT      =                               03021381
                   MLT00004-RCPT-RTL-AMT    + ML156-RCPT-RTL-AMT.       03021481
           COMPUTE SV-RCPT-NET-COST-AMT =                               03021581
                   MLT00004-RCPT-NET-COST-AMT + ML156-RCPT-NET-COST-AMT.03021681
           COMPUTE SV-RCPT-GRO-COST-AMT =                               03021781
                   MLT00004-RCPT-GRO-COST-AMT + ML156-RCPT-GRO-COST-AMT.03021881
           COMPUTE SV-PADJ-FRGT-COST-AMT =                              03021981
                 MLT00004-PADJ-FRGT-COST-AMT + ML156-PADJ-FRGT-COST-AMT.03022081
           COMPUTE SV-PADJ-DISC-COST-AMT =                              03022181
                 MLT00004-PADJ-DISC-COST-AMT + ML156-PADJ-DISC-COST-AMT.03022281
           COMPUTE SV-PADJ-RTL-AMT      =                               03022381
                   MLT00004-PADJ-RTL-AMT    + ML156-PADJ-RTL-AMT.       03022481
           COMPUTE SV-PADJ-NET-COST-AMT =                               03022581
                   MLT00004-PADJ-NET-COST-AMT + ML156-PADJ-NET-COST-AMT.03022681
           COMPUTE SV-PADJ-FRGT-COST-AMT =                              03022781
                 MLT00004-PADJ-FRGT-COST-AMT + ML156-PADJ-FRGT-COST-AMT.03022881
           COMPUTE SV-DR-ALW-RTL-AMT    =                               03022981
                   MLT00004-DR-ALW-RTL-AMT  + ML156-DR-ALW-RTL-AMT.     03023081
           COMPUTE SV-RTV-RTL-AMT       =                               03023181
                   MLT00004-RTV-RTL-AMT     + ML156-RTV-RTL-AMT.        03023281
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03023381
                   MLT00004-INV-ADJ-RTL-AMT + ML156-INV-ADJ-RTL-AMT.    03023481
           COMPUTE SV-PADJ-RTV-COST-AMT  = MLT00004-PADJ-RTV-COST-AMT   03023581
                                         + ML156-PADJ-RTV-COST-AMT.     03023681
           COMPUTE SV-PADJ-RTV-RTL-AMT   = MLT00004-PADJ-RTV-RTL-AMT    03023781
                                         + ML156-PADJ-RTV-RTL-AMT.      03023881
           COMPUTE SV-PADJ-DA-COST-AMT   = MLT00004-PADJ-DA-COST-AMT    03023981
                                         + ML156-PADJ-DA-COST-AMT.      03024081
           COMPUTE SV-PADJ-DA-RTL-AMT    = MLT00004-PADJ-DA-RTL-AMT     03024181
                                         + ML156-PADJ-DA-RTL-AMT.       03024281
           COMPUTE SV-PADJ-OTH-COST-AMT  = MLT00004-PADJ-OTH-COST-AMT   03024381
                                         + ML156-PADJ-OTH-COST-AMT.     03024481
           COMPUTE SV-PADJ-OTH-RTL-AMT   = MLT00004-PADJ-OTH-RTL-AMT    03024581
                                         + ML156-PADJ-OTH-RTL-AMT.      03024681
           COMPUTE SV-MKDN-PROMO-RTL-AMT = MLT00004-MKDN-PROMO-RTL-AMT  03024781
                                         + ML156-MKDN-PROMO-RTL-AMT.    03024881
           COMPUTE SV-MKDN-PRPT-RTL-AMT  = MLT00004-MKDN-PRPT-RTL-AMT   03024981
                                         + ML156-MKDN-PRPT-RTL-AMT.     03025081
           COMPUTE SV-MKDN-GLDST-RTL-AMT = MLT00004-MKDN-GLDST-RTL-AMT  03025181
                                         + ML156-MKDN-GLDST-RTL-AMT.    03025281
           COMPUTE SV-MKDN-OTH-RTL-AMT   = MLT00004-MKDN-OTH-RTL-AMT    03025381
                                         + ML156-MKDN-OTH-RTL-AMT.      03025481
                                                                        03521014
      *--- INITIAL MARKUP AMOUNT                                        03530014
           COMPUTE PV-MKUP-INIT-AMT =                                   03540078
                   ML156-RCPT-RTL-AMT - ML156-RCPT-NET-COST-AMT.        03550078
      *            ML156-RCPT-RTL-AMT - ML156-RCPT-GRO-COST-AMT.        03551037
           COMPUTE SV-MKUP-INIT-AMT   =                                 03560078
                   MLT00004-MKUP-INIT-AMT + PV-MKUP-INIT-AMT.           03570078
                                                                        03580000
      *--- NET MARKUP AMOUNT                                            03581014
      *    COMPUTE PV-MKUP-NET-AMT =                                    03590027
      *           (ML156-RCPT-RTL-AMT -                                 03600037
      *           (ML156-RCPT-GRO-COST-AMT +                            03610037
      *            ML156-PADJ-FRGT-COST-AMT) -                          03620037
      *            ML156-PADJ-DISC-COST-AMT).                           03630037
           COMPUTE PV-NET-RETAIL =                                      03630127
                  (ML156-RCPT-RTL-AMT +                                 03630237
                   ML156-PADJ-RTL-AMT +                                 03630337
                   ML156-MKUP-RTL-AMT +                                 03630437
                  (ML156-XFER-IN-RTL-AMT -                              03630537
                   ML156-XFER-OUT-RTL-AMT))                             03630637
           END-COMPUTE.                                                 03631027
           COMPUTE PV-NET-COST = ML156-RCPT-NET-COST-AMT +              03631137
                                 ML156-PADJ-NET-COST-AMT                03631237
           END-COMPUTE.                                                 03632027
                                                                        03632114
           COMPUTE PV-MKUP-NET-AMT = PV-NET-RETAIL - PV-NET-COST        03632278
           END-COMPUTE.                                                 03633078
                                                                        03634014
           COMPUTE SV-MKUP-NET-AMT     =                                03640078
                   MLT00004-MKUP-NET-AMT + PV-MKUP-NET-AMT.             03650078
                                                                        03660000
           COMPUTE SV-INV-ADJ-RTL-AMT  =                                03670000
                   MLT00004-INV-ADJ-RTL-AMT + ML156-INV-ADJ-RTL-AMT.    03680065
      *                                                                 03690000
      ******************************************************************03700000
      * READS THE CONDENSED FILE INTO THE MLT_MNLY_SUM LAYOUT           03710062
      ******************************************************************03720000
       4000-READ-CONDENSED-FILE.                                        03730000
           READ CONDENSED-FILE INTO ML156-RECORD                        03740037
              AT END                                                    03750000
                MOVE 'Y' TO PS-EOF-CONDENSED-FILE-SW.                   03760000
     *                                                                  03770000
           IF PS-EOF-CONDENSED-FILE                                     03780000
               MOVE ML156-MTH-PARTN-NBR       TO CR-PARTN-NBR           03790067
               MOVE ML156-DEPT-NBR            TO CR-DEPT-NBR            03810037
               MOVE ML156-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03820037
               MOVE ML156-SUB-CL-NBR          TO CR-SUB-CL-NBR          03830075
               MOVE ML156-LOC-NBR             TO CR-LOC-NBR             03830175
W28582         MOVE ML156-ML-LWST-ID          TO PV-ML-LWST-ID          03830283
W28582         MOVE PV-ML-LWST-ID             TO CR-ML-LWST-ID          03830383
W28582***      MOVE ML156-ENT-ID              TO PV-ENT-ID              03830483
W28582***      MOVE PV-ENT-ID                 TO CR-ENT-ID              03830583
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03850000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03860000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03870000
                                             TCKRSTINF-WORK-STRG-DESC   03880000
               EXEC SQL                                                 03890000
                 UPDATE TCKRSTINF                                       03900000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03910000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03920000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03930000
               END-EXEC                                                 03940000
               IF SQLCODE = 0                                           03950000
                   CONTINUE                                             03960000
               ELSE                                                     03970000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    03980000
                   MOVE '* 4000-READ-CONDENSED *' TO PV-PARAGRAPH-NAME  03990000
                   PERFORM 9999-SQL-ABEND                               04000000
               END-IF                                                   04010000
            ELSE                                                        04020000
                ADD 1                        TO PA-INPUT-COUNT          04030000
            END-IF.                                                     04040000
      *                                                                 04050000
      ******************************************************************04060000
      * SELECT A ROW FROM THE MONTHLY DTL TABLE THAT MATCHES INPUT KEY  04070079
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      04080000
      ******************************************************************04090000
       7000-SELECT-DB-ROW.                                              04100000
      *                                                                 04110000
           EXEC SQL                                                     04120000
               SELECT SLS_REG_RTL_AMT                                   04130000
                     ,SLS_PROMO_RTL_AMT                                 04140000
                     ,SLS_PROCLR_RTL_AMT                                04150000
                     ,SLS_CLR_RTL_AMT                                   04160000
                     ,SLS_OTH_RTL_AMT                                   04170000
                     ,MKDN_PLU_RTL_AMT                                  04180000
                     ,MKDN_PROMO_RTL_AMT                                04190043
                     ,MKDN_RGST_RTL_AMT                                 04191043
                     ,MKDN_GLDST_RTL_AMT                                04192043
                     ,MKDN_UMTCH_RTL_AMT                                04200000
                     ,MKDN_BYR_RTL_AMT                                  04210000
                     ,MKDN_STR_RTL_AMT                                  04220000
                     ,MKDN_PRPT_RTL_AMT                                 04221043
                     ,MKDN_OTH_RTL_AMT                                  04222043
                     ,MKDN_RTL_AMT                                      04223044
                     ,MKUP_RTL_AMT                                      04230000
                     ,XFER_IN_RTL_AMT                                   04240000
                     ,XFER_OUT_RTL_AMT                                  04250000
                     ,RCPT_RTL_AMT                                      04260000
                     ,RCPT_NET_COST_AMT                                 04270000
                     ,RCPT_GRO_COST_AMT                                 04280000
                     ,PADJ_RTL_AMT                                      04290043
                     ,PADJ_FRGT_COST_AMT                                04291043
                     ,PADJ_DISC_COST_AMT                                04300012
                     ,PADJ_RTV_COST_AMT                                 04310043
                     ,PADJ_RTV_RTL_AMT                                  04311043
                     ,PADJ_DA_COST_AMT                                  04312043
                     ,PADJ_DA_RTL_AMT                                   04313043
                     ,PADJ_NET_COST_AMT                                 04314043
                     ,PADJ_OTH_COST_AMT                                 04315043
                     ,PADJ_OTH_RTL_AMT                                  04316043
                     ,DR_ALW_RTL_AMT                                    04330000
                     ,RTV_RTL_AMT                                       04340000
                     ,MKUP_INIT_AMT                                     04351000
                     ,MKUP_NET_AMT                                      04352000
                     ,INV_ADJ_RTL_AMT                                   04352143
                 INTO :MLT00004-SLS-REG-RTL-AMT                         04353065
                     ,:MLT00004-SLS-PROMO-RTL-AMT                       04354065
                     ,:MLT00004-SLS-PROCLR-RTL-AMT                      04355065
                     ,:MLT00004-SLS-CLR-RTL-AMT                         04356065
                     ,:MLT00004-SLS-OTH-RTL-AMT                         04357065
                     ,:MLT00004-MKDN-PLU-RTL-AMT                        04358065
                     ,:MLT00004-MKDN-PROMO-RTL-AMT                      04359065
                     ,:MLT00004-MKDN-RGST-RTL-AMT                       04359165
                     ,:MLT00004-MKDN-GLDST-RTL-AMT                      04359265
                     ,:MLT00004-MKDN-UMTCH-RTL-AMT                      04360065
                     ,:MLT00004-MKDN-BYR-RTL-AMT                        04370065
                     ,:MLT00004-MKDN-STR-RTL-AMT                        04380065
                     ,:MLT00004-MKDN-PRPT-RTL-AMT                       04381065
                     ,:MLT00004-MKDN-OTH-RTL-AMT                        04382065
                     ,:MLT00004-MKDN-RTL-AMT                            04383065
                     ,:MLT00004-MKUP-RTL-AMT                            04390065
                     ,:MLT00004-XFER-IN-RTL-AMT                         04400065
                     ,:MLT00004-XFER-OUT-RTL-AMT                        04410065
                     ,:MLT00004-RCPT-RTL-AMT                            04420065
                     ,:MLT00004-RCPT-NET-COST-AMT                       04430065
                     ,:MLT00004-RCPT-GRO-COST-AMT                       04440065
                     ,:MLT00004-PADJ-RTL-AMT                            04450065
                     ,:MLT00004-PADJ-FRGT-COST-AMT                      04451065
                     ,:MLT00004-PADJ-DISC-COST-AMT                      04460065
                     ,:MLT00004-PADJ-RTV-COST-AMT                       04470065
                     ,:MLT00004-PADJ-RTV-RTL-AMT                        04471065
                     ,:MLT00004-PADJ-DA-COST-AMT                        04480065
                     ,:MLT00004-PADJ-DA-RTL-AMT                         04481065
                     ,:MLT00004-PADJ-NET-COST-AMT                       04482065
                     ,:MLT00004-PADJ-OTH-COST-AMT                       04483065
                     ,:MLT00004-PADJ-OTH-RTL-AMT                        04484065
                     ,:MLT00004-DR-ALW-RTL-AMT                          04490065
                     ,:MLT00004-RTV-RTL-AMT                             04500065
                     ,:MLT00004-MKUP-INIT-AMT                           04501065
                     ,:MLT00004-MKUP-NET-AMT                            04502065
                     ,:MLT00004-INV-ADJ-RTL-AMT                         04510065
               FROM MLT_MNLY_SUM                                        04540062
              WHERE   PARTN_NBR           = :ML156-MTH-PARTN-NBR        04550067
                AND   DEPT_NBR            = :ML156-DEPT-NBR             04570037
                AND   MAJ_CL_NBR          = :ML156-MAJ-CL-NBR           04580037
                AND   SUB_CL_NBR          = :ML156-SUB-CL-NBR           04590074
                AND   LOC_NBR             = :ML156-LOC-NBR              04590174
W28582          AND   ML_LO_MDSE_LVL_ID   = :ML156-ML-LWST-ID           04590283
W28582***       AND   ENT_ID              = :ML156-ENT-ID               04590383
           END-EXEC.                                                    04600000
      *                                                                 04610000
           EVALUATE SQLCODE                                             04620000
               WHEN 0                                                   04630000
                   SET PI-UPDATE-MLT00004-ROW     TO TRUE               04640065
               WHEN +100                                                04650000
                   SET PI-INSERT-MLT00004-ROW     TO TRUE               04660065
               WHEN -911                                                04670000
                   DISPLAY '*** -911 OCCURRED ON SELECT ***'            04680000
                   ADD +1             TO PV-DEADLOCK-COUNT              04690000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04700000
                       MOVE '7000-SELECT-DB-ROW' TO                     04710000
                                                    PV-PARAGRAPH-NAME   04720000
                       PERFORM 9000-DEADLOCK-ERROR                      04730000
                   ELSE                                                 04740000
                       PERFORM 7999-RESTART-PROCESS                     04750000
                   END-IF                                               04760000
               WHEN OTHER                                               04770000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     04780000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             04790000
                   PERFORM 9999-SQL-ABEND                               04800000
           END-EVALUATE.                                                04810000
      *                                                                 04820000
      *                                                                 04830000
      ***************************************************************** 04840000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON PARTITION NBR,   04850079
      * DEPT, MAJ CL, SUB CL, LOC NBR, AND ENT ID.  CURRENT TABLE       04860079
      * VALUES UPDATED TO INCLUDE VALUES FROM INPUT REC MLRD154         04870079
      ***************************************************************** 04880000
       7100-UPDATE-MLT00004-ROW.                                        04890065
      *                                                                 04900000
           PERFORM 2100-CALCULATE-UPDATES.                              04910000
      *                                                                 04920000
           EXEC SQL                                                     04930000
               UPDATE MLT_MNLY_SUM                                      04940062
                   SET SLS_REG_RTL_AMT     = :SV-SLS-REG-RTL-AMT        04941048
                     , SLS_PROMO_RTL_AMT   = :SV-SLS-PROMO-RTL-AMT      04942048
                     , SLS_PROCLR_RTL_AMT  = :SV-SLS-PROCLR-RTL-AMT     04943048
                     , SLS_CLR_RTL_AMT     = :SV-SLS-CLR-RTL-AMT        04944048
                     , SLS_OTH_RTL_AMT     = :SV-SLS-OTH-RTL-AMT        04945048
                     , MKDN_PLU_RTL_AMT    = :SV-MKDN-PLU-RTL-AMT       04946048
                     , MKDN_PROMO_RTL_AMT  = :SV-MKDN-PROMO-RTL-AMT     04947048
                     , MKDN_RGST_RTL_AMT   = :SV-MKDN-RGST-RTL-AMT      04948048
                     , MKDN_GLDST_RTL_AMT  = :SV-MKDN-GLDST-RTL-AMT     04949048
                     , MKDN_UMTCH_RTL_AMT  = :SV-MKDN-UMTCH-RTL-AMT     04949148
                     , MKDN_BYR_RTL_AMT    = :SV-MKDN-BYR-RTL-AMT       04949248
                     , MKDN_STR_RTL_AMT    = :SV-MKDN-STR-RTL-AMT       04949348
                     , MKDN_PRPT_RTL_AMT   = :SV-MKDN-PRPT-RTL-AMT      04949448
                     , MKDN_OTH_RTL_AMT    = :SV-MKDN-OTH-RTL-AMT       04949548
                     , MKDN_RTL_AMT        = :SV-MKDN-RTL-AMT           04949648
                     , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT           04949748
                     , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT        04949848
                     , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL-AMT       04949948
                     , RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT           04950048
                     , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST-AMT      04950151
                     , RCPT_GRO_COST_AMT   = :SV-RCPT-GRO-COST-AMT      04950248
                     , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT           04950348
                     , PADJ_FRGT_COST_AMT  = :SV-PADJ-FRGT-COST-AMT     04950448
                     , PADJ_DISC_COST_AMT  = :SV-PADJ-DISC-COST-AMT     04950548
                     , PADJ_RTV_COST_AMT   = :SV-PADJ-RTV-COST-AMT      04950648
                     , PADJ_RTV_RTL_AMT    = :SV-PADJ-RTV-RTL-AMT       04950748
                     , PADJ_DA_COST_AMT    = :SV-PADJ-DA-COST-AMT       04950848
                     , PADJ_DA_RTL_AMT     = :SV-PADJ-DA-RTL-AMT        04950948
                     , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST-AMT      04951048
                     , PADJ_OTH_COST_AMT   = :SV-PADJ-OTH-COST-AMT      04951148
                     , PADJ_OTH_RTL_AMT    = :SV-PADJ-OTH-RTL-AMT       04951248
                     , DR_ALW_RTL_AMT      = :SV-DR-ALW-RTL-AMT         04951348
                     , RTV_RTL_AMT         = :SV-RTV-RTL-AMT            04951448
                     , MKUP_INIT_AMT       = :SV-MKUP-INIT-AMT          04951548
                     , MKUP_NET_AMT        = :SV-MKUP-NET-AMT           04951648
                     , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT        04951748
              WHERE   PARTN_NBR            = :ML156-MTH-PARTN-NBR       05200067
                AND   DEPT_NBR             = :ML156-DEPT-NBR            05220037
                AND   MAJ_CL_NBR           = :ML156-MAJ-CL-NBR          05230037
                AND   SUB_CL_NBR           = :ML156-SUB-CL-NBR          05240075
                AND   LOC_NBR              = :ML156-LOC-NBR             05240175
W28582          AND   ML_LO_MDSE_LVL_ID    = :ML156-ML-LWST-ID          05240283
W28582***       AND   ENT_ID               = :ML156-ENT-ID              05240383
           END-EXEC                                                     05250000
      *                                                                 05260000
           EVALUATE SQLCODE                                             05270000
               WHEN 0                                                   05280000
                   CONTINUE                                             05290000
               WHEN -911                                                05300000
                   ADD +1             TO PV-DEADLOCK-COUNT              05310000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            05320000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05330000
                       MOVE '7100-UPDATE-MLT00004-ROW' TO               05340065
                                                    PV-PARAGRAPH-NAME   05350000
                       PERFORM 9000-DEADLOCK-ERROR                      05360000
                   ELSE                                                 05370000
                       PERFORM 7999-RESTART-PROCESS                     05380000
                   END-IF                                               05390000
               WHEN OTHER                                               05400000
                   MOVE '7100-UPDATE-MLT00004-ROW' TO PV-PARAGRAPH-NAME 05410065
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED05420000
                   PERFORM 9999-SQL-ABEND                               05430000
           END-EVALUATE.                                                05440000
      *                                                                 05450000
           ADD 1 TO PA-UPDATE-COUNT.                                    05460000
      *                                                                 05470000
      ***************************************************************** 05480000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY DTL TABLE. INITIAL   * 05490079
      * ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW.             * 05500014
      * CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN RECALC* 05510014
      ***************************************************************** 05520000
       7200-INSERT-MLT00004-ROW.                                        05530065
      *                                                                 05540000
           COMPUTE PV-MKUP-INIT-AMT =                                   05541078
                   ML156-RCPT-RTL-AMT - ML156-RCPT-NET-COST-AMT.        05541178
      *            ML156-RCPT-RTL-AMT - ML156-RCPT-GRO-COST-AMT.        05542037
                                                                        05545003
      *    COMPUTE PV-MKUP-NET-AMT =                                    05546027
      *           (ML156-RCPT-RTL-AMT -                                 05547037
      *           (ML156-RCPT-GRO-COST-AMT +                            05548037
      *            ML156-PADJ-FRGT-COST-AMT) -                          05549037
      *            ML156-PADJ-DISC-COST-AMT).                           05549137
      *                                                                 05550000
           COMPUTE PV-NET-RETAIL =                                      05550127
                  (ML156-RCPT-RTL-AMT +                                 05550237
                   ML156-PADJ-RTL-AMT +                                 05550337
                   ML156-MKUP-RTL-AMT +                                 05550437
                  (ML156-XFER-IN-RTL-AMT -                              05550537
                   ML156-XFER-OUT-RTL-AMT))                             05550637
           END-COMPUTE.                                                 05550727
           COMPUTE PV-NET-COST = ML156-RCPT-NET-COST-AMT +              05550837
                                 ML156-PADJ-NET-COST-AMT                05550937
           END-COMPUTE.                                                 05551027
                                                                        05551114
           COMPUTE PV-MKUP-NET-AMT = PV-NET-RETAIL - PV-NET-COST        05551278
           END-COMPUTE.                                                 05552078
           EXEC SQL                                                     05560000
               INSERT INTO MLT_MNLY_SUM                                 05570062
                    (  PARTN_NBR                                        05580061
                     , DEPT_NBR                                         05600000
                     , MAJ_CL_NBR                                       05610000
                     , SUB_CL_NBR                                       05611026
                     , LOC_NBR                                          05611146
W28582               , ML_LO_MDSE_LVL_ID                                05611283
W28582***            , ENT_ID                                           05611383
                     , FSCL_MN_END_DTE                                  05611483
                     , SLS_REG_RTL_AMT                                  05630000
                     , SLS_PROMO_RTL_AMT                                05640000
                     , SLS_PROCLR_RTL_AMT                               05650000
                     , SLS_CLR_RTL_AMT                                  05660000
                     , SLS_OTH_RTL_AMT                                  05670000
                     , MKDN_PLU_RTL_AMT                                 05680000
                     , MKDN_PROMO_RTL_AMT                               05690027
                     , MKDN_RGST_RTL_AMT                                05695027
                     , MKDN_GLDST_RTL_AMT                               05700026
                     , MKDN_UMTCH_RTL_AMT                               05701026
                     , MKDN_BYR_RTL_AMT                                 05710000
                     , MKDN_STR_RTL_AMT                                 05720000
                     , MKDN_PRPT_RTL_AMT                                05721026
                     , MKDN_OTH_RTL_AMT                                 05722026
                     , MKDN_RTL_AMT                                     05723026
                     , MKUP_RTL_AMT                                     05730000
                     , XFER_IN_RTL_AMT                                  05740000
                     , XFER_OUT_RTL_AMT                                 05750000
                     , RCPT_RTL_AMT                                     05760000
                     , RCPT_NET_COST_AMT                                05770000
                     , RCPT_GRO_COST_AMT                                05780000
                     , PADJ_RTL_AMT                                     05790026
                     , PADJ_FRGT_COST_AMT                               05791026
                     , PADJ_DISC_COST_AMT                               05800012
                     , PADJ_RTV_COST_AMT                                05810026
                     , PADJ_RTV_RTL_AMT                                 05811026
                     , PADJ_DA_COST_AMT                                 05812026
                     , PADJ_DA_RTL_AMT                                  05813026
                     , PADJ_NET_COST_AMT                                05814026
                     , PADJ_OTH_COST_AMT                                05815026
                     , PADJ_OTH_RTL_AMT                                 05816026
                     , DR_ALW_RTL_AMT                                   05830000
                     , RTV_RTL_AMT                                      05840000
                     , MKUP_INIT_AMT                                    05870068
                     , MKUP_NET_AMT                                     05880068
                     , INV_ADJ_RTL_AMT                                  05890026
                     , ONORD_RTL_AMT                                    05900026
                     , ONORD_COST_AMT                                   05910069
                     , SHNK_RTL_AMT                                     05920069
                     , END_INV_RTL_AMT                                  05930084
R28582               , END_INV_COST_AMT                                 05931084
R28582               , INV_COST_ADJ1_AMT                                05932084
R28582               , INV_COST_ADJ2_AMT                                05933084
R28582               , END_INV_COST1_AMT                                05934084
R28582               , END_INV_COST2_AMT                                05935084
R28582               , ADJ_GRO_MRGN_AMT   )                             05936084
               VALUES                                                   05940000
                   (   :ML156-MTH-PARTN-NBR                             05950067
                     , :ML156-DEPT-NBR                                  05970037
                     , :ML156-MAJ-CL-NBR                                05980037
                     , :ML156-SUB-CL-NBR                                05981037
                     , :ML156-LOC-NBR                                   05981146
W28582               , :ML156-ML-LWST-ID                                05981283
W28582***            , :ML156-ENT-ID                                    05981383
                     , :ML156-FSCL-MN-END-DTE                           05981483
                     , :ML156-SLS-REG-RTL-AMT                           06000037
                     , :ML156-SLS-PROMO-RTL-AMT                         06010037
                     , :ML156-SLS-PROCLR-RTL-AMT                        06020037
                     , :ML156-SLS-CLR-RTL-AMT                           06030037
                     , :ML156-SLS-OTH-RTL-AMT                           06040037
                     , :ML156-MKDN-PLU-RTL-AMT                          06050037
                     , :ML156-MKDN-PROMO-RTL-AMT                        06060037
                     , :ML156-MKDN-RGST-RTL-AMT                         06061037
                     , :ML156-MKDN-GLDST-RTL-AMT                        06062037
                     , :ML156-MKDN-UMTCH-RTL-AMT                        06070037
                     , :ML156-MKDN-BYR-RTL-AMT                          06080037
                     , :ML156-MKDN-STR-RTL-AMT                          06090037
                     , :ML156-MKDN-PRPT-RTL-AMT                         06091037
                     , :ML156-MKDN-OTH-RTL-AMT                          06092037
                     , :ML156-MKDN-RTL-AMT                              06093037
                     , :ML156-MKUP-RTL-AMT                              06100037
                     , :ML156-XFER-IN-RTL-AMT                           06110037
                     , :ML156-XFER-OUT-RTL-AMT                          06120037
                     , :ML156-RCPT-RTL-AMT                              06130037
                     , :ML156-RCPT-NET-COST-AMT                         06140037
                     , :ML156-RCPT-GRO-COST-AMT                         06150037
                     , :ML156-PADJ-RTL-AMT                              06160037
                     , :ML156-PADJ-FRGT-COST-AMT                        06161037
                     , :ML156-PADJ-DISC-COST-AMT                        06170037
                     , :ML156-PADJ-RTV-COST-AMT                         06171037
                     , :ML156-PADJ-RTV-RTL-AMT                          06172037
                     , :ML156-PADJ-DA-COST-AMT                          06173037
                     , :ML156-PADJ-DA-RTL-AMT                           06174037
                     , :ML156-PADJ-NET-COST-AMT                         06175037
                     , :ML156-PADJ-OTH-COST-AMT                         06176037
                     , :ML156-PADJ-OTH-RTL-AMT                          06180037
                     , :ML156-DR-ALW-RTL-AMT                            06200037
                     , :ML156-RTV-RTL-AMT                               06210037
                     , :PV-MKUP-INIT-AMT                                06220068
                     , :PV-MKUP-NET-AMT                                 06230068
                     , :ML156-INV-ADJ-RTL-AMT                           06240037
                     , 0                                                06250078
                     , 0                                                06260078
                     , 0                                                06270069
                     , 0                                                06280084
R28582               , 0                                                06290084
R28582               , 0                                                06300084
R28582               , 0                                                06301084
R28582               , 0                                                06302084
R28582               , 0                                                06303084
R28582               , 0                       )                        06304084
           END-EXEC                                                     06310000
      *                                                                 06320000
           EVALUATE SQLCODE                                             06330000
               WHEN 0                                                   06340000
                   CONTINUE                                             06350000
               WHEN -911                                                06360000
                   ADD +1             TO PV-DEADLOCK-COUNT              06370000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            06380000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              06390000
                       MOVE '7200-INSERT-MLT00004-ROW' TO               06400065
                                                    PV-PARAGRAPH-NAME   06410000
                       PERFORM 9000-DEADLOCK-ERROR                      06420000
                   ELSE                                                 06430000
                       PERFORM 7999-RESTART-PROCESS                     06440000
                   END-IF                                               06450000
               WHEN OTHER                                               06460000
                   MOVE '7200-INSERT-MLT00004-ROW' TO PV-PARAGRAPH-NAME 06470065
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED06480000
                   PERFORM 9999-SQL-ABEND                               06490000
           END-EVALUATE.                                                06500000
                                                                        06510000
           ADD 1 TO PA-INSERT-COUNT.                                    06520000
                                                                        06530000
      ******************************************************************06540000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *06550000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *06560000
      *            CONTROL TABLE                                       *06570000
      ******************************************************************06580000
       7300-GET-COMMIT-TIMESTAMP.                                       06590000
                                                                        06600000
           EXEC SQL                                                     06610000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        06620000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       06630000
               INTO :PV-COMMIT-TIMESTAMP                                06640000
               FROM TCKRSTCNTL                                          06650000
              WHERE JOB_NAME  = :PC-JOB-NAME                            06660000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        06670000
           END-EXEC.                                                    06680000
                                                                        06690000
           EVALUATE TRUE                                                06700000
               WHEN SQLCODE = 0                                         06710000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  06720000
                   CONTINUE                                             06730000
               WHEN SQLCODE NOT EQUAL ZERO                              06740000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME06750000
                   PERFORM 9999-SQL-ABEND                               06760000
           END-EVALUATE.                                                06770000
      *                                                                 06780000
      *                                                                 06790000
      ******************************************************************06800000
      * 7400-INIT-CHECKPOINT-SELECT                                    *06810000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *06820000
      *            IF WE ARE IN A RESTART CONDITION.                   *06830000
      ******************************************************************06840000
       7400-INIT-CHECKPOINT-SELECT.                                     06850000
                                                                        06860000
           EXEC SQL                                                     06870000
             SELECT  CKPT_STAT_CODE                                     06880000
                    ,CKPT_FRQNCY_QTY                                    06890000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         06900000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        06910000
               FROM  TCKRSTCNTL                                         06920000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06930000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06940000
           END-EXEC.                                                    06950000
                                                                        06960000
           IF SQLCODE = 0                                               06970000
               CONTINUE                                                 06980000
           ELSE                                                         06990000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  07000000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     07010000
               PERFORM 9999-SQL-ABEND                                   07020000
           END-IF.                                                      07030000
      *                                                                 07040000
      *                                                                 07050000
      ******************************************************************07060000
      * 7500-SELECT-RESTART-INFO                                       *07070000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *07080000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *07090000
      ******************************************************************07100000
       7500-SELECT-RESTART-INFO.                                        07110000
                                                                        07120000
           EXEC SQL                                                     07130000
             SELECT  WORK_STRG_DESC                                     07140000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          07150000
               FROM  TCKRSTINF                                          07160000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07170000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07180000
           END-EXEC.                                                    07190000
                                                                        07200000
           IF SQLCODE = 0                                               07210000
               CONTINUE                                                 07220000
           ELSE                                                         07230000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   07240000
               PERFORM 9999-SQL-ABEND                                   07250000
           END-IF.                                                      07260000
                                                                        07270000
      ******************************************************************07280000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *07290000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *07300000
      ******************************************************************07310000
       7600-SET-CURRENT-TIMESTAMP.                                      07320000
                                                                        07330000
           EXEC SQL                                                     07340000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           07350000
           END-EXEC.                                                    07360000
                                                                        07370000
           IF SQLCODE = 0                                               07380000
               CONTINUE                                                 07390000
           ELSE                                                         07400000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 07410000
               PERFORM 9999-SQL-ABEND                                   07420000
           END-IF.                                                      07430000
      *                                                                 07440000
      *                                                                 07450000
      ******************************************************************07460000
      * 7700-COMMIT-PROCESSING.                                        *07470000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *07480000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*07490000
      ******************************************************************07500000
       7700-COMMIT-PROCESSING.                                          07510000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     07520000
                                                                        07530000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          07540000
                                                                        07550000
      * PREPARE COMMIT RECORD FOR UPDATE                                07560000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                07570000
               MOVE ML156-MTH-PARTN-NBR       TO CR-PARTN-NBR           07580067
               MOVE ML156-DEPT-NBR            TO CR-DEPT-NBR            07600037
               MOVE ML156-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          07610037
               MOVE ML156-SUB-CL-NBR          TO CR-SUB-CL-NBR          07620075
               MOVE ML156-LOC-NBR             TO CR-LOC-NBR             07620175
W28582         MOVE ML156-ML-LWST-ID          TO PV-ML-LWST-ID          07620283
W28582         MOVE PV-ML-LWST-ID             TO CR-ML-LWST-ID          07620383
W28582***      MOVE ML156-ENT-ID              TO PV-ENT-ID              07620483
W28582***      MOVE PV-ENT-ID                 TO CR-ENT-ID              07620583
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    07640000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  07650000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       07660000
                                             TCKRSTINF-WORK-STRG-DESC   07670000
               IF PS-FIRST-COMMIT                                       07680000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                07690000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                07700000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       07710000
               END-IF                                                   07720000
               PERFORM 7800-COMMIT-CHANGES                              07730000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        07740000
           END-IF.                                                      07750000
      *                                                                 07760000
      *                                                                 07770000
      ******************************************************************07780000
      * 7800-COMMIT-CHANGES.                                            07790000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      07800000
      *            TAKE A COMMIT.                                       07810000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    07820000
      ******************************************************************07830000
       7800-COMMIT-CHANGES.                                             07840000
                                                                        07850000
           EXEC SQL                                                     07860000
             UPDATE TCKRSTINF                                           07870000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          07880000
              WHERE JOB_NAME       = :PC-JOB-NAME                       07890000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   07900000
           END-EXEC.                                                    07910000
                                                                        07920000
           IF SQLCODE = 0                                               07930000
               CONTINUE                                                 07941006
           ELSE                                                         07950000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED07960000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07970000
               PERFORM 9999-SQL-ABEND                                   07980000
           END-IF.                                                      07990000
                                                                        08000000
           EXEC SQL                                                     08010000
               COMMIT                                                   08020000
           END-EXEC.                                                    08030000
                                                                        08040000
           IF SQLCODE = 0                                               08050000
               ADD 1 TO PA-COMMIT-COUNT                                 08051006
           ELSE                                                         08070000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED08080000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     08090000
               PERFORM 9999-SQL-ABEND                                   08100000
           END-IF.                                                      08110000
      *                                                                 08120000
      ******************************************************************08130000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   08140000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   08150000
      ******************************************************************08160000
       7900-UPDATE-CHECKPOINT-STATUS.                                   08170000
                                                                        08180000
           EXEC SQL                                                     08190000
             UPDATE  TCKRSTCNTL                                         08200000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        08210000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           08220000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       08230000
           END-EXEC.                                                    08240000
                                                                        08250000
           IF SQLCODE = 0                                               08260000
               CONTINUE                                                 08270000
           ELSE                                                         08280000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME08290000
               PERFORM 9999-SQL-ABEND                                   08300000
           END-IF.                                                      08310000
                                                                        08320000
           EJECT                                                        08330000
      *                                                                 08340000
      *                                                                 08350000
      ******************************************************************08360000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST MLT_MNLY_SUM ROW FOR  08370062
      * UPDATE.  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST  08380062
      * COMMIT.                                                         08381062
      ******************************************************************08390000
       7999-RESTART-PROCESS.                                            08400000
      *                                                                 08410000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           08420000
      *                                                                 08430000
           CLOSE CONDENSED-FILE.                                        08440000
      *                                                                 08450000
           PERFORM 7500-SELECT-RESTART-INFO.                            08460000
      *                                                                 08470000
           DISPLAY '**** RESTART **** '.                                08480000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 08490000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   08500000
                                                                        08510000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      08520000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        08530000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       08540000
           IF PS-FIRST-COMMIT                                           08550000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               08560000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     08570000
                      ' BEGINNING'                                      08580000
           ELSE                                                         08590000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           08600000
                        TCKRSTINF-WORK-STRG-DESC (1:35)                 08610010
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 08620000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         08630000
                    CR-CHECKPOINT-RESTART-AREA                          08640000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                08650000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   08680000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 08690000
               DISPLAY 'SUB CLASS NBR   ' CR-SUB-CL-NBR                 08700075
               DISPLAY 'LOCATION NBR    ' CR-LOC-NBR                    08700175
W28582         DISPLAY 'ML-LWST-ID      ' CR-ML-LWST-ID                 08700283
W28582***      DISPLAY 'ENT-ID          ' CR-ENT-ID                     08700383
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           08710000
           END-IF.                                                      08720000
                                                                        08730000
           OPEN INPUT CONDENSED-FILE.                                   08740000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      08750000
      *                                                                 08760000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          08770000
           PERFORM 4000-READ-CONDENSED-FILE                             08780000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              08790000
           DISPLAY 'INPUT RECORD RESTARTED ON RECORD '                  08800000
                    PA-INPUT-COUNT                                      08810000
           DISPLAY 'PARTITION NBR         ' ML156-MTH-PARTN-NBR.        08820067
           DISPLAY 'DEPARTMENT NBR        ' ML156-DEPT-NBR.             08840037
           DISPLAY 'MAJOR CLASS NBR       ' ML156-MAJ-CL-NBR.           08850037
           DISPLAY 'SUB CLASS NBR         ' ML156-SUB-CL-NBR.           08851075
           DISPLAY 'LOCATION NBR          ' ML156-LOC-NBR.              08851175
W28582     DISPLAY 'ML-LWST-ID            ' ML156-ML-LWST-ID.           08851283
W28582     MOVE ML156-ML-LWST-ID TO PV-ML-LWST-ID.                      08860083
W28582     DISPLAY 'ML-LWST-ID            ' PV-ML-LWST-ID.              08870083
W28582***  DISPLAY 'ENT-ID                ' ML156-ENT-ID.               08871083
W28582***  MOVE ML156-ENT-ID TO PV-ENT-ID.                              08872083
W28582***  DISPLAY 'ENT-ID                ' PV-ENT-ID.                  08873083
      ******************************************************************08880000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *08890000
      ******************************************************************08900000
       8000-EOJ-ROUTINE.                                                08910000
      *                                                                 08920000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       08930000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       08940000
                                                                        08950000
      *                                                                 08960000
           DISPLAY SPACES.                                              08970000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         08980000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        08990000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        09000000
           DISPLAY SPACES.                                              09010000
      *                                                                 09020000
           CLOSE CONDENSED-FILE.                                        09030000
      *                                                                 09040000
      *                                                                 09050000
      ******************************************************************09060000
      *  PERFORMED BY 7100-UPDATE AND 7200-INSERT                       09070003
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   09080000
      ******************************************************************09090000
       9000-DEADLOCK-ERROR.                                             09100000
      *                                                                 09110000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     09120000
               PERFORM 9999-SQL-ABEND.                                  09130000
      *                                                                 09140000
      ******************************************************************09150000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               09160000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             09170000
      ******************************************************************09180000
       9999-SQL-ABEND.                                                  09190000
                                                                        09200000
           MOVE +4013                 TO ABEND-CODE.                    09210000
           DISPLAY '**  ABEND     **'.                                  09220000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              09230000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            09240000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       09250000
                                                                        09260000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         09270000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        09280000
                                                                        09290000
           COPY DPPD004.                                                09300000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           09310000
