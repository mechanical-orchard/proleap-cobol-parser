       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP021.                                         00020014
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/29/2002.                                       00050014
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070017
      **       VSM ITEM MESSAGE - NUMBER 015 - STYLE ATTRIBUTES CHANGE  00080017
      ******************************************************************00090017
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO UPDATE THE FOLLOWING   00100026
      ***     ATTRIBUTES ON XST_VND_STYL:                               00110026
      ***     1. STKR_NBR                                               00120026
      ***     2. RTL_CLSN_DESC                                          00130026
      ***     3. LBL_NM                                                 00140026
      ***     4. HNG_FOLD_DESC                                          00150026
      ******************************************************************00160017
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00170017
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00180024
092704**09/27/2004 CHANGED BY:JULIE TEWES  ADD VS_DESC                  00190027
      ******************************************************************00200017
       ENVIRONMENT DIVISION.                                            00210000
       CONFIGURATION SECTION.                                           00220000
       SOURCE-COMPUTER.  IBM-3090.                                      00230000
       OBJECT-COMPUTER.  IBM-3090.                                      00240000
       DATA DIVISION.                                                   00250000
                                                                        00260005
       WORKING-STORAGE SECTION.                                         00270000
      ******************************************************************00280005
      **PROGRAM SWITCHES                                                00290005
      ******************************************************************00300005
       01  PS-PROGRAM-SWITCHES.                                         00310005
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00320005
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00330005
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00340005
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00350021
               88  PS-ACTION-REVERSE                  VALUE 01.         00360021
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00370021
               88  PS-ACTION-NOTHING                  VALUE 02.         00380021
      ******************************************************************00390000
      **PROGRAM VARIABLES                                               00400000
      ******************************************************************00410000
       01  PC-PROGRAM-COUNTERS.                                         00420007
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00430007
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00440007
      ******************************************************************00450015
      **PROGRAM CALCULATIONS                                            00460015
      ******************************************************************00470015
       01  PC-PROGRAM-CALCULATIONS.                                     00480015
           05  PC-10-DIGIT                 PIC 9(10)  VALUE ZEROS.      00490015
           05  PC-18-DIGIT                 PIC S9(18) COMP VALUE ZEROS. 00500015
           05  PC-09-DIGIT                 PIC S9(09) COMP VALUE ZEROS. 00510015
      ******************************************************************00520000
      **PROGRAM VARIABLES                                               00530019
      ******************************************************************00540000
       01  PV-PROGRAM-VARIABLES.                                        00550019
           05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACE.      00560019
      ******************************************************************00570000
      **COPYBOOKS                                                       00580000
      ******************************************************************00590000
      ***ABEND COPYBOOK                                                 00600005
           COPY DPWS004.                                                00610027
      ***DB2 MESSAGE AREA                                               00620000
           COPY SQLCA2.                                                 00630027
      ***VSM ITEM COPYBOOK                                              00640017
           COPY IMRD008.                                                00650027
      ******************************************************************00660000
      **TABLE INCLUSIONS                                                00670000
      ******************************************************************00680000
      ***DB2 COMMUNICATIONS AREA                                        00690000
           EXEC SQL                                                     00700000
                INCLUDE SQLCA                                           00710000
           END-EXEC.                                                    00720000
      ***XST_VND_STYL TABLE                                             00730000
           EXEC SQL                                                     00740000
               INCLUDE XST00013                                         00750000
           END-EXEC.                                                    00760000
                                                                        00770000
       LINKAGE SECTION.                                                 00780000
      ******************************************************************00790000
      **LINKAGE VARIABLES                                               00800000
      ******************************************************************00810000
       01  LV-IMRD008-RECORD               PIC X(450).                  00820005
       01  LV-RETURN-CODE                  PIC S9(04).                  00830005
       01  LV-ACTION-IND                   PIC 9(02).                   00840021
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00850021
                                                                        00860000
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00870005
                                LV-RETURN-CODE,                         00880021
                                LV-ACTION-IND,                          00890021
                                LV-CURRENT-TIMESTAMP.                   00900021
      ******************************************************************00910000
       0000-MAINLINE.                                                   00920000
                                                                        00930000
            PERFORM 1000-INITIALIZATION.                                00940000
            PERFORM 2000-PROCESS-MESSAGE                                00950014
                    UNTIL PS-PROCESS-COMPLETE.                          00960005
            PERFORM 9999-WRAPUP.                                        00970000
                                                                        00980000
       EJECT.                                                           00990005
      ***************************************************************** 01000000
      **MOVES ALL COPYBOOK FIELDS TO THE DCLGEN FIELDS                  01010007
      ***************************************************************** 01020000
       1000-INITIALIZATION.                                             01030000
                                                                        01040000
           MOVE '1000-INITIALIZATION'          TO PV-PARAGRAPH-NAME.    01050019
                                                                        01060000
           INITIALIZE PS-PROCESS-CONTINUE-SW.                           01070008
           INITIALIZE PC-PROGRAM-COUNTERS.                              01080020
                                                                        01090008
           MOVE LV-IMRD008-RECORD             TO IMRD008-RECORD.        01100014
           MOVE IM008-ITEM-KEY-RMS-STYL-ID    TO XST00013-STYL-ID.      01110014
                                                                        01120007
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01130015
      *     COMP FIELDS.                                                01140015
           MOVE IM008-STYL-ATTR-STKR-NBR      TO PC-10-DIGIT.           01150015
           MOVE PC-10-DIGIT                   TO PC-18-DIGIT.           01160015
           MOVE PC-18-DIGIT                   TO PC-09-DIGIT.           01170015
           MOVE PC-09-DIGIT                   TO XST00013-STKR-NBR.     01180015
                                                                        01190015
           MOVE IM008-STYL-ATTR-RTL-CLSN-DESC TO XST00013-RTL-CLSN-DESC.01200018
           MOVE IM008-STYL-ATTR-LBL-DESC      TO XST00013-LBL-NM.       01210018
           MOVE IM008-STYL-ATTR-HNG-FOLD-DESC TO XST00013-HNG-FOLD-DESC.01220018
121402     MOVE LV-CURRENT-TIMESTAMP          TO XST00013-LAST-UPD-TMST.01230024
121402     MOVE IM008-CRE-TMST            TO XST00013-SOR-LAST-MSG-TMST.01240024
092704     MOVE IM008-STYL-ATTR-VS-DESC   TO XST00013-VS-DESC.          01250027
                                                                        01260000
       EJECT.                                                           01270005
      ***************************************************************** 01280000
      **UPDATES SPECIFIED STYLE ATTRIBUTES ON XST_VND_STYL              01290026
      ***************************************************************** 01300000
       2000-PROCESS-MESSAGE.                                            01310014
                                                                        01320000
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01330019
                                                                        01340005
           EXEC SQL                                                     01350005
              UPDATE XST_VND_STYL                                       01360005
                  SET   STKR_NBR      = :XST00013-STKR-NBR              01370016
                      , RTL_CLSN_DESC = :XST00013-RTL-CLSN-DESC         01380016
                      , LBL_NM        = :XST00013-LBL-NM                01390016
                      , HNG_FOLD_DESC = :XST00013-HNG-FOLD-DESC         01400016
                      , LAST_UPD_TMST = :XST00013-LAST-UPD-TMST         01410016
121402                , SOR_LAST_MSG_TMST = :XST00013-SOR-LAST-MSG-TMST 01420025
092704                , VS_DESC       = :XST00013-VS-DESC               01430027
                  WHERE STYL_ID       = :XST00013-STYL-ID               01440007
           END-EXEC.                                                    01450005
                                                                        01460005
           EVALUATE TRUE                                                01470005
      ***SUCCESSFUL PROCESSING                                          01480026
              WHEN SQLCODE = ZERO                                       01490005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01500008
      ***ROW/TABLE LOCK                                                 01510026
              WHEN SQLCODE = -911                                       01520005
              WHEN SQLCODE = -913                                       01530007
                   ADD +1                      TO PC-RETRY-COUNTER      01540007
                   IF  PC-RETRY-COUNTER = PC-RETRY-MAX                  01550007
                       SET PS-PROCESS-COMPLETE TO TRUE                  01560007
                   ELSE                                                 01570007
                       ADD +1                  TO PC-RETRY-COUNTER      01580007
                   END-IF                                               01590007
              WHEN OTHER                                                01600005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01610005
           END-EVALUATE.                                                01620005
                                                                        01630000
       EJECT.                                                           01640005
      ******************************************************************01650000
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01660000
      ******************************************************************01670000
       9999-WRAPUP.                                                     01680000
                                                                        01690000
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME              01700023
                                                                        01710000
           MOVE SQLCODE               TO LV-RETURN-CODE.                01720023
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01730021
                                                                        01740021
           EXIT PROGRAM.                                                01750000
                                                                        01760000
       EJECT.                                                           01770005
