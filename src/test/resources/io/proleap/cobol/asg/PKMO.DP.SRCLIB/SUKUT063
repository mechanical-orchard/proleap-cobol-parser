000100 TITLE 'EXTRACT AUDIT INFO PER CONTROL DATES'.                            
000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    SUKUT063.                                                 
000400 AUTHOR.        DENNIS STEFFEN.                                           
000500 DATE-WRITTEN.  WINTER 2002.                                              
000600 ENVIRONMENT DIVISION.                                                    
000700 INPUT-OUTPUT SECTION.                                                    
000800                                                                          
001600******************************************************************        
000800                                                                          
000900* THIS PROGRAM EXTRACT AUDIT INFO PER CONTROL DATES.                      
      * /*VSH - CN FILE LAYOUT CHANGES                                          
001000* THE PROGRAM WAS MODIFIED TO COMPARE THE CORRECT QTY AGAINST             
      * RFID TAGGED QTY WHICH GETS MODFIED FOR DIFFERENT SCENARIOS              
      * SUCH RFID ONLY, COMBO AUDIT AND RFID AUDIT.                             
001100* */VSH - CN FILE LAYOUT CHANGES                                          
001600******************************************************************        
000900 FILE-CONTROL.                                                            
001000     SELECT NEW-CONTROL-FILE        ASSIGN TO INP01.                      
001100     SELECT AUDIT-EXTRACT-FILE      ASSIGN TO OUT01.                      
001200                                                                          
001300 DATA DIVISION.                                                           
001400 FILE SECTION.                                                            
001500                                                                          
001600 FD  NEW-CONTROL-FILE                                                     
001700     RECORDING MODE IS F                                                  
001800     LABEL RECORDS ARE STANDARD                                           
001900     RECORD CONTAINS 080                                                  
002000     BLOCK CONTAINS 0 RECORDS                                             
002100     DATA RECORD IS OLD-CONTROL-RECORD.                                   
002200 01  NEW-CONTROL-RECORD               PIC X(080).                         
002300                                                                          
002400 FD  AUDIT-EXTRACT-FILE                                                   
002500     RECORDING MODE IS F                                                  
002600     LABEL RECORDS ARE STANDARD                                           
002700     RECORD CONTAINS 150                                                  
002800     BLOCK CONTAINS 0 RECORDS                                             
002900     DATA RECORD IS AUDIT-EXTRACT-RECORD.                                 
003000 01  AUDIT-EXTRACT-RECORD             PIC X(150).                         
003100                                                                          
003200 WORKING-STORAGE SECTION.                                                 
003300                                                                          
003400 01  PV-ABEND-CODE                    PIC S9(04)   VALUE ZERO             
003500                                                   COMP SYNC.             
003600                                                                          
003700 01  PS-PROGRAM-SWITCHES.                                                 
003800     05  PS-END-EXTRACT-SW            PIC X(1)   VALUE 'N'.               
003900         88  PS-END-EXTRACT                      VALUE 'Y'.               
004000     05  PS-END-CSR-SW                PIC X(1)   VALUE 'N'.               
004100         88  PS-END-CSR                          VALUE 'Y'.               
004000     05  PS-AUDIT                     PIC X(1).                           
004100         88  PS-RFID-ONLY-AUDIT                  VALUE '1'.               
004100         88  PS-COMBO-AUDIT                      VALUE '2'.               
004200                                                                          
004300                                                                          
004400 01  PV-PROGRAM-VARIABLES.                                                
004500     05  PV-RETRY-COUNT               PIC S9(04)  VALUE ZERO              
004600                                                  COMP SYNC.              
004700     05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
004800     05  PV-ERROR-MESSAGE-1           PIC X(60)   VALUE SPACES.           
004900     05  PV-ERROR-MESSAGE-2           PIC X(60)   VALUE SPACES.           
005000     05  PV-SQLCODE                   PIC 9(9)    VALUE ZERO.             
005100     05  PV-TYPE                      PIC X(01)   VALUE SPACES.           
005200     05  PV-AUDIT-DATE                PIC X(10)   VALUE SPACES.           
005600     05  PV-DISP-NBR                  PIC Z(8)9.                          
005610     05  PV-AUDIT-COUNT               PIC S9(9) COMP-3 VALUE ZERO.        
      *VSH CN FILE LAYOUT CHANGES BEGIN                                         
005610     05  PV-COUNT                     PIC S9(9) COMP-3 VALUE ZERO.        
012000*VSH CN FILE LAYOUT CHANGES END                                           
005700                                                                          
005800 01  PV-COUNTERS COMP-3.                                                  
005900     05  PV-READ-COUNT                PIC S9(9)    VALUE ZERO.            
006000     05  PV-WRITE-COUNT               PIC S9(9)    VALUE ZERO.            
006100     05  PV-FETCH-COUNT               PIC S9(9)    VALUE ZERO.            
006210     05  PV-A-COUNT                   PIC S9(9)    VALUE ZERO.            
006220     05  PV-B-COUNT                   PIC S9(9)    VALUE ZERO.            
006230     05  PV-C-COUNT                   PIC S9(9)    VALUE ZERO.            
006300                                                                          
006400 01  PC-PROGRAM-CONSTANTS.                                                
006500     05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'SUKUT063'.        
006600     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
006700                                                 COMP SYNC.               
006800 01  PV-ABEND-FIELDS.                                                     
006900     05  PV-ABEND-PROGRAM             PIC X(8)   VALUE 'SUKUT063'.        
007000     05  PV-ABEND-PARAGRAPH           PIC X(50)  VALUE SPACES.            
007100     05  PV-ABEND-OPERATION           PIC X(50)  VALUE SPACES.            
007200     05  PV-ABEND-STRUCTURE-1         PIC X(15)  VALUE SPACES.            
007300     05  PV-ABEND-STRUCTURE-2         PIC X(15)  VALUE SPACES.            
007400     05  PV-ABEND-STRUCTURE-3         PIC X(15)  VALUE SPACES.            
007500     05  PV-ABEND-STATUS              PIC XX     VALUE SPACES.            
007600     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
007700     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
007800     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
007900     05  PV-DB2-OPERATION-MESSAGE-4   PIC  X(79) VALUE SPACES.            
008000                                                                          
008100/                                                                         
008200*    DATE CONTROL CARD                                                    
008300     COPY SURD032.                                                        
008400                                                                          
008500*    AUDIT-EXTRACT RECORD                                                 
008600     COPY SURD031.                                                        
008700/                                                                         
008800*    SUT_PO_AUD_REQ                                                       
008900     EXEC SQL                                                             
009000         INCLUDE SUT00015                                                 
009100     END-EXEC                                                             
009200                                                                          
009300*    SHIP UNIT AUDITS                                                     
009400     EXEC SQL                                                             
009500         INCLUDE TSUNAUD                                                  
009600     END-EXEC                                                             
009700                                                                          
009800     EXEC SQL                                                             
009900         INCLUDE TSUATRN                                                  
010000     END-EXEC                                                             
010010                                                                          
010020     EXEC SQL                                                             
010030         INCLUDE TDTATTR                                                  
010040     END-EXEC                                                             
010100/                                                                         
010200     EXEC SQL                                                             
010300         DECLARE AUDIT-CSR CURSOR FOR                                     
010400             SELECT                                                       
010500                   'A' AS TYPE                                            
010600              , A.OPER_UNT_ID                                             
010700              , A.AUD_CNTL_NBR                                            
010800              , A.PO_NBR                                                  
010900              , A.RCVR_SEQ_NBR                                            
011000              , A.PO_LNE_NBR                                              
011100              , A.PO_PREPK_ID                                             
011200              , A.PKGNG_TYP_CDE                                           
011300              , A.VND_RNKG_CDE                                            
011400              , A.CART_QTY                                                
011500              , A.UNT_QTY                                                 
011600              , A.AUD_CART_QTY                                            
011700              , A.AUD_UNT_QTY                                             
011800              , A.AUD_STAT_CDE                                            
011900              , DATE(A.CRE_TMST)                                          
011910              , A.ENT_ID                                                  
012100             FROM                                                         
012200                   SUT_PO_AUD_REQ A                                       
012300                                                                          
012400             WHERE                                                        
012500                 A.AUD_STAT_CDE IN ('P', 'F', 'N')                        
012600             AND DATE(A.CRE_TMST) BETWEEN                                 
012700             :SU032-FROM-DATE AND :SU032-TO-DATE                          
012900                                                                          
013000             UNION                                                        
013100                                                                          
013200             SELECT                                                       
013300                   'B' AS TYPE                                            
013400              , A.OPER_UNT_ID                                             
013500              , A.AUD_CNTL_NBR                                            
013600              , A.PO_NBR                                                  
013700              , A.RCVR_SEQ_NBR                                            
013800              , A.PO_LNE_NBR                                              
013900              , A.PO_PREPK_ID                                             
014000              , A.PKGNG_TYP_CDE                                           
014100              , A.VND_RNKG_CDE                                            
014200              , A.CART_QTY                                                
014300              , A.UNT_QTY                                                 
014400              , A.AUD_CART_QTY                                            
014500              , A.AUD_UNT_QTY                                             
014600              , A.AUD_STAT_CDE                                            
014700              , DATE(A.CRE_TMST)                                          
014710              , A.ENT_ID                                                  
014800                                                                          
014900             FROM                                                         
015000                   SUT_PO_AUD_REQ A                                       
015100                                                                          
015200             WHERE                                                        
015300                 A.AUD_STAT_CDE NOT IN ('P', 'F', 'N')                    
015400             AND DATE(A.CRE_TMST) BETWEEN                                 
015500               :SU032-FROM-DATE AND :SU032-TO-DATE                        
015700                                                                          
015800             UNION                                                        
015900                                                                          
016000             SELECT                                                       
016100                   'C' AS TYPE                                            
016200              , A.OPER_UNT_ID                                             
016300              , A.AUD_CNTL_NBR                                            
016400              , A.PO_NBR                                                  
016500              , A.RCVR_SEQ_NBR                                            
016600              , A.PO_LNE_NBR                                              
016700              , A.PO_PREPK_ID                                             
016800              , A.PKGNG_TYP_CDE                                           
016900              , A.VND_RNKG_CDE                                            
017000              , A.CART_QTY                                                
017100              , A.UNT_QTY                                                 
017200              , A.AUD_CART_QTY                                            
017300              , A.AUD_UNT_QTY                                             
017400              , A.AUD_STAT_CDE                                            
017500              , DATE(A.CRE_TMST)                                          
017510              , A.ENT_ID                                                  
017600                                                                          
017700             FROM                                                         
017800                SUT_PO_AUD_REQ A                                          
017900              , TSUNAUD        B                                          
018000                                                                          
018100             WHERE                                                        
018200                 A.AUD_CNTL_NBR = B.AUD_CNTL_NBR                          
018300             AND A.AUD_STAT_CDE IN ('P', 'F', 'N')                        
018400             AND DATE(B.AUD_STP_TMST) BETWEEN                             
018500               :SU032-FROM-DATE AND :SU032-TO-DATE                        
018600             AND DATE(A.CRE_TMST) < :SU032-FROM-DATE                      
018800                                                                          
018900         ORDER BY                                                         
018910             AUD_CNTL_NBR                                                 
019000         ,   TYPE                                                         
019100     END-EXEC                                                             
019200/                                                                         
019300*  USED FOR DB2 ERROR MESSAGE BUILDING                                    
019400     COPY DPWS004.                                                        
019500                                                                          
019600*   DB2 SQL COMMUNICATION AREA                                            
019700     EXEC SQL                                                             
019800          INCLUDE SQLCA                                                   
019900     END-EXEC.                                                            
020000/                                                                         
020100 PROCEDURE DIVISION.                                                      
020200                                                                          
020300     PERFORM 1000-INIT.                                                   
020400                                                                          
020500     PERFORM 2000-PROCESS UNTIL PS-END-CSR.                               
020600                                                                          
020700     PERFORM 9000-TERMINATE.                                              
020800                                                                          
020900     GOBACK.                                                              
021000                                                                          
021100 1000-INIT.                                                               
021200                                                                          
021300     OPEN INPUT  NEW-CONTROL-FILE                                         
021400          OUTPUT AUDIT-EXTRACT-FILE                                       
021500                                                                          
021600     PERFORM 8000-READ-CONTROL-REC.                                       
021700     PERFORM 7000-OPEN-AUDIT-CSR.                                         
021800     PERFORM 7010-FETCH-AUDIT-CSR.                                        
021900                                                                          
022000 2000-PROCESS.                                                            
022100                                                                          
022200     PERFORM 3000-BUILD-EXTRACT.                                          
022300                                                                          
022400     PERFORM 8100-WRITE-EXTRACT-REC.                                      
022500                                                                          
022600     PERFORM 7010-FETCH-AUDIT-CSR.                                        
022700/                                                                         
022800 3000-BUILD-EXTRACT.                                                      
022900                                                                          
023000     PERFORM 7100-GET-FISCAL-INFO.                                        
023100                                                                          
023200     PERFORM 7150-SELECT-TSUATRN.                                         
023210                                                                          
023300     INITIALIZE SU031-RECORD.                                             
023400                                                                          
025000     MOVE PV-TYPE                     TO SU031-RECORD-TYPE                
025100     MOVE PV-AUDIT-DATE               TO SU031-DATE                       
025200     MOVE DTATTR-FSCL-YR-NBR          TO SU031-FISCAL-YEAR                
025300     MOVE DTATTR-FSCL-WK-NBR          TO SU031-FISCAL-WEEK                
025400     MOVE DTATTR-SEA-DESC             TO SU031-FISCAL-SEASON              
025500     MOVE DTATTR-FSCL-MN-NBR          TO SU031-FISCAL-MONTH               
025600     MOVE SUT00015-OPER-UNT-ID        TO SU031-OPER-UNT-ID                
025700     MOVE SUT00015-PKGNG-TYP-CDE      TO SU031-PKGNG-TYPE                 
025800     MOVE SUT00015-VND-RNKG-CDE       TO SU031-VENDOR-RANK                
025900     MOVE SUT00015-AUD-CNTL-NBR       TO SU031-AUD-CNTL-NBR               
026000     MOVE SUT00015-PO-NBR             TO SU031-PO-NBR                     
026100     MOVE SUT00015-RCVR-SEQ-NBR       TO SU031-RCVR-SEQ-NBR               
026200     MOVE SUT00015-PO-LNE-NBR         TO SU031-PO-LNE-NBR                 
026300     MOVE SUT00015-PO-PREPK-ID        TO SU031-PO-PREPK-ID                
026400     MOVE SUT00015-CART-QTY           TO SU031-CART-QTY                   
                                                                                
      *VSH CN FILE LAYOUT CHANGES BEGIN                                         
                                                                                
           IF SUT00015-PO-PREPK-ID < 0 AND                                      
              SUT00015-PKGNG-TYP-CDE = 'RF'                                     
                                                                                
              EXEC SQL                                                          
               SELECT VALUE(COUNT(*), 0)                                        
                 INTO :PV-COUNT                                                 
                 FROM SUT_PO_AUD_REQ                                            
                WHERE RWRK_AUD_CNTL_NBR = :SUT00015-AUD-CNTL-NBR                
              END-EXEC                                                          
                                                                                
036600         EVALUATE TRUE                                                    
                                                                                
036700            WHEN SQLCODE = ZERO                                           
                    IF PV-COUNT > 0                                             
036800                SET PS-COMBO-AUDIT TO TRUE                                
                    ELSE                                                        
036800                SET PS-RFID-ONLY-AUDIT TO TRUE                            
                    END-IF                                                      
                                                                                
                  WHEN SQLCODE < 0                                              
                     MOVE '3000-BUILD-EXTRACT'                                  
                                            TO PV-ABEND-PARAGRAPH               
                     MOVE 'SELECT'            TO PV-ABEND-OPERATION             
                     MOVE 'SUT_PO_AUD_REQ'    TO PV-ABEND-STRUCTURE-1           
                     PERFORM 9900-DB2-ABEND                                     
                                                                                
               END-EVALUATE                                                     
                                                                                
              IF PS-COMBO-AUDIT                                                 
                 MOVE SUT00015-AUD-UNT-QTY TO SU031-UNT-QTY                     
              ELSE                                                              
                  IF PS-RFID-ONLY-AUDIT                                         
                       MOVE SUT00015-UNT-QTY TO SU031-UNT-QTY                   
                  END-IF                                                        
              END-IF                                                            
           ELSE                                                                 
026500        MOVE SUT00015-UNT-QTY            TO SU031-UNT-QTY                 
           END-IF                                                               
                                                                                
012000*VSH CN FILE LAYOUT CHANGES END                                           
                                                                                
026600     MOVE SUT00015-AUD-CART-QTY       TO SU031-AUD-CART-QTY               
026700     MOVE SUT00015-AUD-UNT-QTY        TO SU031-AUD-UNT-QTY                
026800     MOVE SUT00015-AUD-STAT-CDE       TO SU031-AUD-STAT-CDE               
026901     IF PV-AUDIT-COUNT > ZERO                                             
026910         MOVE 1                       TO                                  
026920                                   SU031-AUD-CNT-COMPLT-REOPEN            
026930     END-IF.                                                              
026931                                                                          
026940     MOVE SUT00015-ENT-ID             TO SU031-ENT-ID.                    
027200/                                                                         
027300 7000-OPEN-AUDIT-CSR.                                                     
027400     EXEC SQL                                                             
027500         OPEN AUDIT-CSR                                                   
027600     END-EXEC.                                                            
027700                                                                          
027800     EVALUATE TRUE                                                        
027900         WHEN SQLCODE = ZERO                                              
028000             CONTINUE                                                     
028100         WHEN SQLWARN0 NOT = SPACE                                        
028200         WHEN SQLCODE  NOT = ZERO                                         
028300             MOVE '7000-OPEN-AUDIT-CSR'                                   
028400                                       TO PV-ABEND-PARAGRAPH              
028500             MOVE 'OPEN'               TO PV-ABEND-OPERATION              
028600             MOVE 'SUT_PO_AUD_REQ'     TO PV-ABEND-STRUCTURE-1            
028700             MOVE 'TSUNAUD'            TO PV-ABEND-STRUCTURE-2            
028800             PERFORM 9900-DB2-ABEND                                       
028900     END-EVALUATE.                                                        
029000                                                                          
029100 7010-FETCH-AUDIT-CSR.                                                    
029200     EXEC SQL                                                             
029300         FETCH AUDIT-CSR                                                  
029400         INTO                                                             
029500             :PV-TYPE                                                     
029600         ,   :SUT00015-OPER-UNT-ID                                        
029700         ,   :SUT00015-AUD-CNTL-NBR                                       
029800         ,   :SUT00015-PO-NBR                                             
029900         ,   :SUT00015-RCVR-SEQ-NBR                                       
030000         ,   :SUT00015-PO-LNE-NBR                                         
030100         ,   :SUT00015-PO-PREPK-ID                                        
030200         ,   :SUT00015-PKGNG-TYP-CDE                                      
030300         ,   :SUT00015-VND-RNKG-CDE                                       
030400         ,   :SUT00015-CART-QTY                                           
030500         ,   :SUT00015-UNT-QTY                                            
030600         ,   :SUT00015-AUD-CART-QTY                                       
030700         ,   :SUT00015-AUD-UNT-QTY                                        
030800         ,   :SUT00015-AUD-STAT-CDE                                       
030900         ,   :PV-AUDIT-DATE                                               
030910         ,   :SUT00015-ENT-ID                                             
031000     END-EXEC.                                                            
031100                                                                          
031200     EVALUATE TRUE                                                        
031300         WHEN SQLCODE = ZERO                                              
031400             ADD 1                    TO PV-FETCH-COUNT                   
031500             CONTINUE                                                     
031600         WHEN SQLCODE = +100                                              
031700             SET PS-END-CSR           TO TRUE                             
031800         WHEN SQLWARN0 NOT = SPACE                                        
031900         WHEN SQLCODE  NOT = ZERO                                         
032000             MOVE '7010-FETCH-AUDIT-CSR'                                  
032100                                       TO PV-ABEND-PARAGRAPH              
032200             MOVE 'FETCH'              TO PV-ABEND-OPERATION              
032300             MOVE 'SUT_PO_AUD_REQ'     TO PV-ABEND-STRUCTURE-1            
032400             MOVE 'TSUNAUD'            TO PV-ABEND-STRUCTURE-2            
032500             PERFORM 9900-DB2-ABEND                                       
032600     END-EVALUATE.                                                        
032700                                                                          
032800 7020-CLOSE-AUDIT-CSR.                                                    
032900     EXEC SQL                                                             
033000         CLOSE AUDIT-CSR                                                  
033100     END-EXEC.                                                            
033200                                                                          
033300     EVALUATE TRUE                                                        
033400         WHEN SQLCODE = ZERO                                              
033500             CONTINUE                                                     
033600         WHEN SQLWARN0 NOT = SPACE                                        
033700         WHEN SQLCODE  NOT = ZERO                                         
033800             MOVE '7020-CLOSE-AUDIT-CSR'                                  
033900                                       TO PV-ABEND-PARAGRAPH              
034000             MOVE 'CLOSE'              TO PV-ABEND-OPERATION              
034100             MOVE 'SUT_PO_AUD_REQ'     TO PV-ABEND-STRUCTURE-1            
034200             MOVE 'TSUNAUD'            TO PV-ABEND-STRUCTURE-2            
034300             PERFORM 9900-DB2-ABEND                                       
034400     END-EVALUATE.                                                        
034500/                                                                         
034600 7100-GET-FISCAL-INFO.                                                    
034700                                                                          
034800     EXEC SQL                                                             
034900         SELECT                                                           
035000             DTE.FSCL_YR_NBR                                              
035100         ,   DTE.FSCL_MN_NBR                                              
035200         ,   DTE.FSCL_WK_NBR                                              
035300         ,   DTE.SEA_DESC                                                 
035400         INTO                                                             
035500             :DTATTR-FSCL-YR-NBR                                          
035600         ,   :DTATTR-FSCL-MN-NBR                                          
035700         ,   :DTATTR-FSCL-WK-NBR                                          
035800         ,   :DTATTR-SEA-DESC                                             
035900         FROM                                                             
036000             TDTATTR DTE                                                  
036100         WHERE                                                            
036200             DTE.KY_DTE  = :PV-AUDIT-DATE                                 
036300         WITH UR                                                          
036400     END-EXEC.                                                            
036500                                                                          
036600     EVALUATE TRUE                                                        
036700         WHEN SQLCODE = ZERO                                              
036800             CONTINUE                                                     
036900         WHEN SQLWARN0 NOT = SPACE                                        
037000         WHEN SQLCODE  NOT = ZERO                                         
037100             MOVE '7100-GET-FISCAL-INFO'                                  
037200                                      TO PV-ABEND-PARAGRAPH               
037400             MOVE 'SELECT'            TO PV-ABEND-OPERATION               
037500             MOVE 'TDTATTR'           TO PV-ABEND-STRUCTURE-1             
037510             STRING 'PV-AUDIT-DATE = ' PV-AUDIT-DATE                      
037520                 DELIMITED BY SIZE INTO PV-ABEND-STRUCTURE-2              
037600             PERFORM 9900-DB2-ABEND                                       
037700     END-EVALUATE.                                                        
037800                                                                          
037900 7150-SELECT-TSUATRN.                                                     
038000                                                                          
038100     EXEC SQL                                                             
038200         SELECT                                                           
038300             COUNT(*)                                                     
038400         INTO                                                             
038500             :PV-AUDIT-COUNT                                              
038600         FROM                                                             
038700             TSUATRN TRN                                                  
038800         WHERE                                                            
038900             TRN.AUD_CNTL_NBR = :SUT00015-AUD-CNTL-NBR                    
038910         AND TRN.UNT_RSTRT_MNIT_QTY > 0                                   
039000         WITH UR                                                          
039100     END-EXEC.                                                            
039200                                                                          
039300     EVALUATE TRUE                                                        
039400         WHEN SQLCODE = ZERO                                              
039500             CONTINUE                                                     
039600         WHEN SQLWARN0 NOT = SPACE                                        
039700         WHEN SQLCODE  NOT = ZERO                                         
039800             MOVE '7150-SELECT-TSUATRN'                                   
039900                                       TO PV-ABEND-PARAGRAPH              
040000             MOVE 'SELECT'             TO PV-ABEND-OPERATION              
040100             MOVE 'TSUATRN' TO PV-ABEND-STRUCTURE-1                       
040110             MOVE SUT00015-AUD-CNTL-NBR TO PV-DISP-NBR                    
040111             STRING 'SUT00015-AUD-CNTL-NBR= '  PV-DISP-NBR                
040120                 DELIMITED BY SIZE INTO PV-ABEND-STRUCTURE-2              
040200             PERFORM 9900-DB2-ABEND                                       
040300     END-EVALUATE.                                                        
040400                                                                          
040500 8000-READ-CONTROL-REC.                                                   
040600                                                                          
040700     READ NEW-CONTROL-FILE INTO                                           
040800           SU032-DATE-CONTROL-REC                                         
040900     AT END                                                               
041000         SET PS-END-EXTRACT           TO TRUE                             
041100     NOT AT END                                                           
041200         ADD 1                        TO PV-READ-COUNT                    
041300         DISPLAY 'DATE CONTROL CARD:'                                     
041400         DISPLAY SU032-DATE-CONTROL-REC                                   
041500         DISPLAY SPACES                                                   
041600     END-READ.                                                            
041700                                                                          
041800 8100-WRITE-EXTRACT-REC.                                                  
041900                                                                          
042000     ADD 1                         TO PV-WRITE-COUNT                      
042010     EVALUATE SU031-RECORD-TYPE                                           
042020     WHEN 'A'                                                             
042021         ADD 1                        TO PV-A-COUNT                       
042030     WHEN 'B'                                                             
042031         ADD 1                        TO PV-B-COUNT                       
042040     WHEN 'C'                                                             
042041         ADD 1                        TO PV-C-COUNT                       
042050     END-EVALUATE.                                                        
042060                                                                          
042100     WRITE AUDIT-EXTRACT-RECORD FROM  SU031-RECORD.                       
042300/                                                                         
042400 9000-TERMINATE.                                                          
042500                                                                          
042600     PERFORM 7020-CLOSE-AUDIT-CSR.                                        
042700                                                                          
042800     CLOSE NEW-CONTROL-FILE                                               
042900           AUDIT-EXTRACT-FILE                                             
043000                                                                          
043100     DISPLAY '************************'.                                  
043200     DISPLAY '******* SUKUT063 *******'.                                  
043300     DISPLAY '************************'.                                  
043400     DISPLAY SPACES                                                       
043500                                                                          
043600     DISPLAY 'CONTROL CARD:'                                              
043700     DISPLAY SU032-DATE-CONTROL-REC                                       
043800     DISPLAY SPACES                                                       
043900                                                                          
044000     MOVE PV-READ-COUNT               TO PV-DISP-NBR                      
044100     DISPLAY 'CONTROL CARDS READ         = ' PV-DISP-NBR                  
044200                                                                          
044300     MOVE PV-WRITE-COUNT              TO PV-DISP-NBR                      
044400     DISPLAY 'EXTRACT RECS  WRITTEN      = ' PV-DISP-NBR                  
044500     DISPLAY SPACES                                                       
044510                                                                          
044520     MOVE PV-A-COUNT                  TO PV-DISP-NBR                      
044530     DISPLAY 'TYPE  A RECS  WRITTEN      = ' PV-DISP-NBR                  
044540     DISPLAY SPACES                                                       
044550     MOVE PV-B-COUNT                  TO PV-DISP-NBR                      
044560     DISPLAY 'TYPE  B RECS  WRITTEN      = ' PV-DISP-NBR                  
044570     DISPLAY SPACES                                                       
044580     MOVE PV-C-COUNT                  TO PV-DISP-NBR                      
044590     DISPLAY 'TYPE  C RECS  WRITTEN      = ' PV-DISP-NBR                  
044591     DISPLAY SPACES                                                       
044600     DISPLAY '************************'.                                  
044700                                                                          
044800 9900-DB2-ABEND.                                                          
044900                                                                          
045000     MOVE SQLCODE TO PV-SQLCODE.                                          
045100     DISPLAY '*** DB2 ERROR '.                                            
045200     DISPLAY '*** SQLCODE   = ' PV-SQLCODE.                               
045300     DISPLAY '*** PROGRAM   = ' PV-ABEND-PROGRAM.                         
045400     DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.                       
045500     DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.                       
045600     DISPLAY '*** MESSAGE   = ' PV-DB2-OPERATION-MESSAGE-1.               
045700     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-2.               
045800     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-3.               
045900     DISPLAY '*** TABLE 1   = ' PV-ABEND-STRUCTURE-1.                     
046000                                                                          
046100     IF PV-ABEND-STRUCTURE-2 > SPACES                                     
046200         DISPLAY '*** TABLE 2   = ' PV-ABEND-STRUCTURE-2                  
046300     END-IF.                                                              
046400                                                                          
046500                                                                          
046600     COPY DPPD004.                                                        
046700                                                                          
046800     MOVE +4013                  TO PV-ABEND-CODE.                        
046900     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
047000                                                                          
047100*9200-ABEND.                                                              
047200******************************************************************        
047300*  PERFORMS A NON-SQL ABEND FOR THE PROGRAM                               
047400******************************************************************        
047500*    DISPLAY '***************************'.                               
047600*    DISPLAY '**       ABEND           **'.                               
047700*    DISPLAY '**  IN PROGRAM SUKUT063  **'.                               
047800*    DISPLAY '***************************'.                               
047900*    DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
048000*    DISPLAY '**                 ** = ' PV-ERROR-MESSAGE-1.               
048100*    DISPLAY '**                 ** = ' PV-ERROR-MESSAGE-2.               
048200*                                                                         
048300*    MOVE +4014                  TO PV-ABEND-CODE.                        
048400*    CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
048500                                                                          
