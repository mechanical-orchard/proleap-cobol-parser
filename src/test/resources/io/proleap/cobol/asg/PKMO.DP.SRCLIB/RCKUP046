000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    RCKUP046.                                                 
000400 AUTHOR.        VIRGENE LENNEMAN.                                         
000500 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000600 DATE-WRITTEN   FEBRUARY 2008.                                            
000700 DATE-COMPILED.                                                           
000800 ENVIRONMENT DIVISION.                                                    
000900 CONFIGURATION SECTION.                                                   
001000 SOURCE-COMPUTER.        IBM-3090.                                        
001100 OBJECT-COMPUTER.        IBM-3090.                                        
001200                                                                          
001300 INPUT-OUTPUT SECTION.                                                    
001400 FILE-CONTROL.                                                            
001500                                                                          
001600     SELECT EXTRACT-FILE                                                  
001700         ASSIGN TO INP01.                                                 
001800                                                                          
001900                                                                          
002000******************************************************************        
002100 DATA DIVISION.                                                           
002200 FILE SECTION.                                                            
002300                                                                          
002400 FD  EXTRACT-FILE                                                         
002500     RECORDING MODE IS F                                                  
002600     LABEL RECORDS ARE STANDARD                                           
002700     BLOCK CONTAINS 0 RECORDS.                                            
002800                                                                          
003000 01  EXTRACT-REC             PIC X(25).                                   
003100/                                                                         
003200 WORKING-STORAGE SECTION.                                                 
003300                                                                          
      *  INPUT FILE RECORD LAYOUT                                               
003400     COPY RCRD042.                                                        
003500                                                                          
003800     COPY DPWS004.                                                        
003900*                                                                         
003900*  RCT_LOC_SUB_RNK                                                        
004000     EXEC SQL                                                             
004100         INCLUDE RCT00007                                                 
004200     END-EXEC.                                                            
004300                                                                          
004400     EXEC SQL                                                             
004500          INCLUDE SQLCA                                                   
004600     END-EXEC.                                                            
004700                                                                          
004800 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
004900                                                   COMP SYNC.             
005000                                                                          
005100 01  PS-PROGRAM-SWITCHES.                                                 
005200     05  PS-END-OF-DRIVE              PIC  X(01) VALUE 'N'.               
005300         88  PS-END-DRIVE-YES                    VALUE 'Y'.               
005400         88  PS-END-DRIVE-NO                     VALUE 'N'.               
005500                                                                          
005600 01  PC-PROGRAM-CONSTANTS.                                                
005700     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08) VALUE 'RCKUP046'.         
005800                                                                          
005900 01  DK-DB2-VARIABLES.                                                    
006000     05  DK-PRCG-NBR                  PIC S9(04) COMP.                    
006100                                                                          
006200 01  PV-PROGRAM-VARIABLES.                                                
006300                                                                          
006400     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
006500     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
006600     05  PV-READ-COUNTER             PIC  S9(9) COMP VALUE ZERO.          
006700     05  PV-COMMIT-COUNTER           PIC  S9(9) COMP VALUE +1.            
006800     05  PV-DELETE-COUNTER           PIC  S9(9) COMP VALUE ZERO.          
006800     05  PV-DELETE-COMMIT-COUNT      PIC  S9(9) COMP VALUE ZERO.          
006900     05  PV-NOTFND-COUNTER           PIC  S9(9) COMP VALUE ZERO.          
007000     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
007000     05  PV-DISPLAY-DATA             PIC ZZZZZZZZ9.                       
007000     05  PV-DISPLAY-KEY.                                                  
007000         10  FILLER                  PIC X(32) VALUE                      
                   'COMMITTED PRCG/LOC/DEPT/MAJ/SUB '.                          
007000         10  PV-DISPLAY-PRCG         PIC ZZZZZ9.                          
007000         10  FILLER                  PIC X     VALUE SPACE.               
007000         10  PV-DISPLAY-LOC          PIC ZZZZZ9.                          
007000         10  FILLER                  PIC X     VALUE SPACE.               
007000         10  PV-DISPLAY-DEPT         PIC ZZZZZ9.                          
007000         10  FILLER                  PIC X     VALUE SPACE.               
007000         10  PV-DISPLAY-MAJ          PIC ZZZZZ9.                          
007000         10  FILLER                  PIC X     VALUE SPACE.               
007000         10  PV-DISPLAY-SUB          PIC ZZZZZ9.                          
007100     05  PV-FUNC-QTY                 PIC  S9(4) COMP VALUE ZERO.          
007200/                                                                         
007300 PROCEDURE DIVISION.                                                      
007400                                                                          
007500     PERFORM 1000-INITIALIZE.                                             
007600                                                                          
007700     PERFORM 2000-PROCESS                                                 
007800         UNTIL PS-END-DRIVE-YES                                           
007900                                                                          
017200     PERFORM 7900-COMMIT                                                  
017300                                                                          
008000     PERFORM 9000-EOJ-ROUTINE.                                            
008100                                                                          
008200     STOP RUN.                                                            
008300/                                                                         
008400 1000-INITIALIZE.                                                         
008500                                                                          
008600     DISPLAY '* * * * * * * * * * * '.                                    
008700     DISPLAY 'CURRENT PROGRAM ' PC-CURRENT-PROGRAM-NAME.                  
008800                                                                          
008900     OPEN INPUT EXTRACT-FILE.                                             
009000     PERFORM 8000-READ-RC042-REC.                                         
009100                                                                          
009200 2000-PROCESS.                                                            
009300                                                                          
011200     EXEC SQL                                                             
011300         DELETE                                                           
011400         FROM                                                             
011500             RCT_LOC_SUB_RNK                                              
011600         WHERE                                                            
011700             PRCG_NBR              = :RC042-PRCG-NBR                      
               AND LOC_NBR               = :RC042-LOC-NBR                       
               AND DEPT_NBR              = :RC042-DEPT-NBR                      
               AND MAJ_CL_NBR            = :RC042-MAJ-CL-NBR                    
               AND SUB_CL_NBR            = :RC042-SUB-CL-NBR                    
012200     END-EXEC.                                                            
012300                                                                          
012400     EVALUATE TRUE                                                        
012500         WHEN SQLCODE = ZERO                                              
012600             ADD SQLERRD(3)           TO PV-DELETE-COUNTER                
012700             ADD 1                    TO PV-COMMIT-COUNTER                
012800         WHEN SQLCODE = +100                                              
012900             ADD 1                    TO PV-NOTFND-COUNTER                
013000         WHEN SQLWARN0 NOT = SPACES                                       
013100         WHEN SQLCODE < ZERO                                              
013200         WHEN SQLCODE > ZERO                                              
013300             DISPLAY 'DELETE FAILED '                                     
011700             MOVE RC042-PRCG-NBR   TO PV-DISPLAY-DATA                     
                   DISPLAY 'PRCG NBR     = ' PV-DISPLAY-DATA                    
                   MOVE RC042-LOC-NBR    TO PV-DISPLAY-DATA                     
                   DISPLAY 'LOC NBR      = ' PV-DISPLAY-DATA                    
                   MOVE RC042-DEPT-NBR   TO PV-DISPLAY-DATA                     
                   DISPLAY 'DEPT NBR     = ' PV-DISPLAY-DATA                    
                   MOVE RC042-MAJ-CL-NBR TO PV-DISPLAY-DATA                     
                   DISPLAY 'MAJ CL NBR   = ' PV-DISPLAY-DATA                    
                   MOVE RC042-SUB-CL-NBR TO PV-DISPLAY-DATA                     
                   DISPLAY 'SUB CL NBR   = ' PV-DISPLAY-DATA                    
010400             MOVE '2000-PROCESS'   TO PV-PARAGRAPH-NAME                   
013900             MOVE 'DELETE'         TO PV-DB2-OPERATION-ATTEMPTED          
008000             PERFORM 9000-EOJ-ROUTINE                                     
014000             PERFORM 9100-SQL-ABEND                                       
014100     END-EVALUATE.                                                        
009500     IF PV-COMMIT-COUNTER > 500                                           
009600*        MOVE ZERO                 TO PV-COMMIT-COUNTER                   
009700         PERFORM 7900-COMMIT                                              
009800     END-IF.                                                              
009900                                                                          
010000     PERFORM 8000-READ-RC042-REC.                                         
010100                                                                          
014300 7900-COMMIT.                                                             
014400     MOVE '7900-COMMIT'               TO PV-PARAGRAPH-NAME.               
014500                                                                          
014600     EXEC SQL                                                             
014700         COMMIT                                                           
014800     END-EXEC.                                                            
014900                                                                          
015000     EVALUATE TRUE                                                        
015100         WHEN SQLCODE = ZERO                                              
012600             ADD PV-DELETE-COUNTER TO PV-DELETE-COMMIT-COUNT              
009600             MOVE ZERO                TO PV-DELETE-COUNTER                
009600             MOVE +1                  TO PV-COMMIT-COUNTER                
                   MOVE RC042-PRCG-NBR      TO PV-DISPLAY-PRCG                  
                   MOVE RC042-LOC-NBR       TO PV-DISPLAY-LOC                   
                   MOVE RC042-DEPT-NBR      TO PV-DISPLAY-DEPT                  
                   MOVE RC042-MAJ-CL-NBR    TO PV-DISPLAY-MAJ                   
                   MOVE RC042-SUB-CL-NBR    TO PV-DISPLAY-SUB                   
                   DISPLAY PV-DISPLAY-KEY                                       
015300         WHEN SQLWARN0 NOT = SPACES                                       
015400         WHEN SQLCODE < ZERO                                              
015500         WHEN SQLCODE > ZERO                                              
015600             MOVE 'COMMIT'        TO PV-DB2-OPERATION-ATTEMPTED           
015700             PERFORM 9100-SQL-ABEND                                       
015800     END-EVALUATE.                                                        
015900                                                                          
016000                                                                          
016100 8000-READ-RC042-REC.                                                     
016200     READ EXTRACT-FILE INTO                                               
016300         RC042-RECORD                                                     
016400     AT END                                                               
016500         SET PS-END-DRIVE-YES         TO TRUE                             
016600     NOT AT END                                                           
016700         ADD +1                       TO PV-READ-COUNTER                  
016800     END-READ.                                                            
016900/                                                                         
017000 9000-EOJ-ROUTINE.                                                        
017100                                                                          
017400     CLOSE EXTRACT-FILE                                                   
017500                                                                          
017600     MOVE PV-READ-COUNTER             TO PV-DISPLAY-COUNT.                
017700     DISPLAY 'TOTAL RECORDS READ             = ' PV-DISPLAY-COUNT.        
017800                                                                          
017900*    MOVE PV-DELETE-COUNTER           TO PV-DISPLAY-COUNT.                
017900     MOVE PV-DELETE-COMMIT-COUNT      TO PV-DISPLAY-COUNT.                
018000     DISPLAY 'ROWS DELETED AND COMMITED      = ' PV-DISPLAY-COUNT.        
018100                                                                          
018200     MOVE PV-NOTFND-COUNTER           TO PV-DISPLAY-COUNT.                
018300     DISPLAY 'ROWS NOT FOUND FOR DELETION    = ' PV-DISPLAY-COUNT.        
018400                                                                          
018500     DISPLAY '* * * * * * * * * * * '.                                    
018600                                                                          
018700 9100-SQL-ABEND.                                                          
018800     DISPLAY '9100-SQL-ABEND'.                                            
018900     DISPLAY '** ABEND     **'.                                           
019000     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
019100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
019200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
019300                                                                          
019400     COPY DPPD004.                                                        
019500                                                                          
019600     MOVE +4013                  TO ABEND-CODE.                           
019700     CALL 'ILBOABN0' USING ABEND-CODE.                                    
019800                                                                          
