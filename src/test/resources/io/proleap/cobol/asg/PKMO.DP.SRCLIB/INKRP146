       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKRP146.                                                 
       AUTHOR.        J SEIDLER.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  08-14-98.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      * INKRP146 - THIS PROGRAM PRODUCES THE SHORTAGE REPORT           *        
      *            TOTALS ARE ACCUMULATED                                       
      *                FOR BUYER, DIVISION AND COMPANY LEVELS.                  
      *            THE REPORT BREAKS ON                                         
      *                CHANGES OF CLASS, DEPT, BUYER AND DIVISION               
      *****************************************************************         
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      *    MODIFIED  03/04/99   PROJECT:   WR9999.   SCOTT JACKSON     *        
      *            ADD STORE NUMBER TO EACH DETAIL RECORD                       
      *                                                                         
      *    MODIFIED  02/01/2000 PROJECT:   WR9999.   BOB MCASEY        *        
      *            EXPAND PHYSICAL INVENTORY W/S AND REPORT FIELDS              
      *                                                                         
      *    MODIFIED  02/01/2000 PROJECT:   WR9999.   BOB MCASEY        *        
      *            EXPAND PHYSICAL INVENTORY W/S AND REPORT FIELDS     *        
      *                                                                         
      *    MODIFIED  04/04/2000 PROJECT:   WR:20839  BOB MCASEY        *        
      *    ADD NEW ESTIMATED ADJUSTMENT COLUMN TO REPORT. CHANGED      *        
      *    REPORT WIDTH TO 150 TO ALLOW FOR NEW COLUMN.                *        
      *                                                                         
SJ0600*    MODIFIED  06/25/2000 PROJECT:  CHGID:SJ0600 SCOTT JACKSON   *        
SJ0600*    ADD 2ND INPUT FOR CONTROL CARD TO BE USED IN REPORT HEADER. *        
SJ0600*                                                                 *       
RB0900*    MODIFIED  09/14/2000 PROJECT:   WR:99999  RONNA BUTZ        *        
RB0900*    MODIFIED REPORT NUMBER.                                     *        
RB0900*                                                                *        
RB0900*        INKRP146-1 -- PHYSICAL MCL                              *        
RB0900*        INKRP146-2 -- PHYSICAL SCLS                             *        
RB0900*        INKRP146-3 -- ESTIMATED MCL                             *        
RB0900*        INKRP146-4 -- ESTIMATED SCLS                            *        
RB0900*                                                                *        
      *  21015   02/01/2001  CHANGE THE CALCULATION AND REPORTING      *        
      *                      OF SHORTAGE PERCENTAGE TO 2 DECIMAL       *        
      *                      ALSO, CHANGE THE PROGRAM TO LOOK UP THE   *        
      *                      CLASS DESCRIPTION FROM TMDSEAR ONLY ON    *        
      *                      CHANGE IN CLASS, DEPT, OR DIV. -LEE YANG  *        
      *  22842   10/23/2001  FOLLOWING CHGS MADE TO SHORT PERCENT CALC *        
      *                      - IF SHORT_RETAIL = 0,THEN SHORT % = 0    *        
      *          ID:SJ1001   - IF TOTAL_RETAIL = 0,THEN SHORT % = 999.9*        
      *                      - FORCE SHORT % PERCENT SIGN (+/-) TO BE  *        
      *                        THE SAME SIGN AS SHORT_RETAIL FIELD.    *        
W26600* W26600  04-05-2004 ROD JANSSEN INV LEDGER STORE EXPANSION               
W20445* W20445  09-01-2004 ROD JANSSEN DETAILED INVNENTORY ADJUSTMENT           
W28545* W28545  08-10-2006 B. GRAHAM - STRATAGEM                                
W28545* W28545             DEPARTMENT INVENTORY PROJECT                         
W28545* W28545             TINVPAR/TINCNTL JOIN                                 
S28545* P926751 09-17-2006 S.JACKSON - SQL -180 IN C050-GET-LAST-INV-DAT        
S28545* P926751            CHANGE HOW NULL INDICATOR IS HANDLED                 
W33304* W33304  09-17-2008  VIGNESH SUBRAMANIAN/INFOSYS                         
W33304*                     HOLDING INVENTORY LEDGER OPEN (HILO).               
      ******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT EXTRACT-FILE                                                  
               ASSIGN TO  INP01.                                                
      *                                                                         
SJ0600     SELECT CONTROL-FILE                                                  
SJ0600         ASSIGN TO  INP02.                                                
      *                                                                         
           SELECT SHORTAGE-REPORT                                               
               ASSIGN TO  RPT01.                                                
      *                                                                         
      ******************************************************************        
       DATA DIVISION.                                                           
      *                                                                         
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
      *                                                                         
       FD  EXTRACT-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 CHARACTERS.                                         
                                                                                
W33304 01  EXTRACT-RECORD                 PIC X(170).                           
                                                                                
SJ0600 FD  CONTROL-FILE                                                         
SJ0600     RECORDING MODE IS F                                                  
SJ0600     LABEL RECORDS ARE STANDARD                                           
SJ0600     BLOCK CONTAINS 0 CHARACTERS.                                         
SJ0600                                                                          
SJ0600 01  CONTROL-RECORD                 PIC X(80).                            
                                                                                
       FD  SHORTAGE-REPORT                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 CHARACTERS.                                         
                                                                                
0400RM 01  SHORTAGE-LINE                  PIC X(150).                           
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DB2-OPER-ATTEMPTED      PIC X(30).                            
           05  PV-SQL-SUB                 PIC S9(4)      VALUE ZERO             
                                                         COMP SYNC.             
           05  PV-NULL                    PIC S9(4)      VALUE ZERO             
                                                         COMP SYNC.             
           05  PV-PARAGRAPH-NAME          PIC X(30)   VALUE SPACE.              
           05  PV-ACTL-INV-DTE.                                                 
               10  PV-ACTL-INV-DTE-CCYY   PIC  9(04).                           
               10  FILLER                 PIC  X.                               
               10  PV-ACTL-INV-DTE-MM     PIC  9(02).                           
               10  FILLER                 PIC  X.                               
               10  PV-ACTL-INV-DTE-DD     PIC  9(02).                           
S28545     05  PV-CURRENT-DATE.                                                 
S28545         10  PV-CURRENT-CC          PIC  9(02) VALUE 20.                  
S28545         10  PV-CURRENT-YY          PIC  9(02).                           
S28545         10  FILLER                 PIC  X     VALUE '-'.                 
S28545         10  PV-CURRENT-MM          PIC  9(02).                           
S28545         10  FILLER                 PIC  X     VALUE '-'.                 
S28545         10  PV-CURRENT-DD          PIC  9(02).                           
           05  PV-RPT-LVL                 PIC X(3)        VALUE SPACES.         
               88  PV-MAJ-CLASS                           VALUE 'MCL'.          
               88  PV-SCL-CLASS                           VALUE 'SCL'.          
W33304     05  PV-IN014-FILE-CHECK         PIC X(01)      VALUE 'Y'.            
W33304         88  PV-IN014-FILE-EMPTY                    VALUE 'Y'.            
W33304         88  PV-IN014-FILE-NOT-EMPTY                VALUE 'N'.            
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES              PIC S9(3)    VALUE +3.               
           05  PC-FUTURE-DATE              PIC  X(10)   VALUE                   
                                                           '9999-09-09'.        
W26600     05  PC-TABLE-MAX                PIC S9(04) COMP VALUE 9999.          
           05  PC-TOTAL-WORD               PIC X(10)    VALUE                   
           '  TOTAL  '.                                                         
           05  PC-DEPT                     PIC X(06)    VALUE                   
           'DEPT  '.                                                            
           05  PC-COMPANY                  PIC X(17)    VALUE                   
           '  COMPANY TOTAL  '.                                                 
           05  FILLER                      PIC X(12)    VALUE SPACES.           
           05  FILLER                      PIC X(12)    VALUE SPACES.           
           05  PC-CLASS                    PIC X(06)    VALUE                   
           'CLASS '.                                                            
           05  PC-DASH                     PIC X(01)    VALUE '-'.              
           05  PC-BUYER                    PIC X(08)    VALUE                   
           'BUYER   '.                                                          
           05  PC-DIVISION                 PIC X(08)    VALUE                   
           'DIVISION'.                                                          
W33304     05  PC-CYCLE                    PIC X(08)    VALUE                   
W33304     '   CYCLE'.                                                          
W33304     05  PC-MONTH                    PIC X(08)    VALUE                   
W33304     '   MONTH'.                                                          
           05  PC-FINAL                    PIC X(08)    VALUE                   
           '   FINAL'.                                                          
           05  PC-MAX-DCS                  PIC 9(02)    VALUE 1.                
           05  PC-CLASS-LEVEL              PIC S9(04)   VALUE +1                
                                                        COMP SYNC.              
           05  PC-DEPARTMENT-LEVEL         PIC S9(04)   VALUE +2                
                                                        COMP SYNC.              
           05  PC-BUYER-LEVEL              PIC S9(04)   VALUE +3                
                                                        COMP SYNC.              
           05  PC-DIVISION-LEVEL           PIC S9(04)   VALUE +4                
                                                        COMP SYNC.              
W33304     05  PC-CYCLE-LEVEL              PIC S9(04)   VALUE +5                
W33304                                                  COMP SYNC.              
W33304     05  PC-MONTH-LEVEL              PIC S9(04)   VALUE +6                
W33304                                                  COMP SYNC.              
           05  PC-COMPANY-LEVEL-STORE-INDEX                                     
W33304                                     PIC S9(04)   VALUE +7                
                                                        COMP SYNC.              
           05  PC-DC-LEVEL                 PIC S9(04)   VALUE +1                
                                                        COMP SYNC.              
           05  PC-COMPANY-LEVEL-TOTAL-INDEX                                     
                                           PIC S9(04)   VALUE +2                
                                                        COMP SYNC.              
           05  PC-PAGE-BREAK-SW            PIC X           VALUE 'N'.           
               88  PC-PAGE-BREAK                           VALUE 'Y'.           
      *                                                                         
      ******************************************************************        
      * SHORTAGE EXTRACT RECORD LAYOUT                                          
      ******************************************************************        
W20445     COPY INRD014.                                                        
      *                                                                         
      ******************************************************************        
0400RM* STANDARD 132 COL HEADER                                                 
0400RM* STANDARD 150 COL HEADER                                                 
      ******************************************************************        
0400RM     COPY DPWS150O.                                                       
                                                                                
      ****************************************************************          
      *  ERROR MESSAGE AREA FOR DB2                                             
      ****************************************************************          
           COPY DPWS004.                                                        
                                                                                
      *                                                                         
      *  DCLGEN FOR THE DB2 DATE ATTRIBUTE TABLE                                
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *  DCLGEN FOR THE DB2 STORE TABLE                                         
      *                                                                         
           EXEC SQL                                                             
W26600         INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
28545 *                                                                 01380000
28545 ******************************************************************01320000
28545 *  DCLGEN FOR THE INVENTORY CONTROL TABLE.                        01330000
28545 ******************************************************************01340000
28545      EXEC SQL                                                     01350000
28545          INCLUDE TINCNTL                                          01360000
28545      END-EXEC.                                                    01370000
28545 *                                                                 01380000
      *                                                                         
      *  DCLGEN FOR THE DB2 INVENTORY PARAMETERS TABLE                          
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *  DCLGEN FOR THE DB2 MBS MERCHANDISE AREA TABLE                          
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
      *                                                                         
      *  COMMUNICATION AREAS FOR DB2                                            
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *  DB2 COMMUNICATION AREA 2                                               
      *                                                                         
           COPY SQLCA2.                                                         
                                                                                
      *                                                                         
      *  SQL DB2 STORE TABLE CURSOR DECLARATION                                 
      *                                                                         
           EXEC SQL                                                             
               DECLARE STORE_CURSOR CURSOR FOR                                  
W26600             SELECT LOC_NBR,                                              
W26600                    LOC_TYP_CDE,                                          
W26600                    LOC_10_ABBR_NM                                        
W26600             FROM XMT_LOC                                                 
W26600             WHERE (LOC_TYP_CDE = 'DS'  OR                                
W26600                    LOC_TYP_CDE= 'WH'  OR                                 
W26600                    LOC_TYP_CDE= 'DC'  OR                                 
W26600                    LOC_TYP_CDE= 'RV'  OR                                 
W26600                    LOC_TYP_CDE= 'CS'  OR                                 
W26600                    LOC_TYP_CDE= 'DA') AND                                
W26600                   (DATE (CURRENT TIMESTAMP) < FINCL_CLO_DTE              
W26600                      OR FINCL_CLO_DTE IS NULL)                           
W26600             ORDER BY LOC_NBR                                             
           END-EXEC.                                                            
      *                                                                         
       01  ABEND-CODE                      PIC S9(04)   VALUE ZERO              
                                                        COMP SYNC.              
      *                                                                         
       01  PA-PROGRAM-ACCUMULATORS                      COMP-3.                 
           05  PA-PAGE-NUMBER              PIC S9(05)   VALUE ZEROS.            
21015      05  PA-PERCENT                  PIC S9(03)V99 VALUE ZEROS.           
      *                                                                         
       01  CP-CONTROL-PRINT.                                                    
           05  CP-LINE-MAX                 PIC S9(02)   VALUE +60.              
           05  CP-LINE-COUNT               PIC S9(02)   VALUE ZEROS.            
           05  CP-ADVANCE-LINE-COUNT       PIC S9(02)   VALUE ZEROS.            
      *                                                                         
SJ0600 01  CC-CONTROL-HEADER.                                                   
SJ0600     05  CC-CONTROL-LITERAL          PIC X(09).                           
CJ0600         88  CC-PHYSICAL-INV              VALUE 'PHYSICAL'.               
SJ0600         88  CC-ESTIMATED-INV             VALUE 'ESTIMATED'.              
RB0900     05  FILLER                      PIC X(01).                           
RB0900     05  CC-MCL-SCLS-SW              PIC X(04).                           
RB0900         88  CC-MCL-INV                   VALUE 'MCL '.                   
RB0900         88  CC-SCLS-INV                  VALUE 'SCLS'.                   
      *                                                                         
      ******************************************************************        
      * HEADING LINES                                                           
      ******************************************************************        
       01  H4-HEADING4.                                                         
SJ0600**   05  FILLER                      PIC X(30)    VALUE SPACE.            
SJ0600     05  FILLER                      PIC X(40)    VALUE SPACE.            
           05  FILLER                      PIC X(11)    VALUE                   
           'CUMULATIVE '.                                                       
           05  FILLER                      PIC X(09)    VALUE                   
           'PHYSICAL '.                                                         
           05  FILLER                      PIC X(10)    VALUE                   
           'INVENTORY '.                                                        
           05  FILLER                      PIC X(09)    VALUE                   
           'SHORTAGE '.                                                         
           05  FILLER                      PIC X(15)    VALUE                   
           'REPORT THROUGH '.                                                   
           05  H4-MONTH-NAME               PIC X(09)    VALUE SPACE.            
           05  FILLER                      PIC X(01)    VALUE SPACE.            
           05  H4-DD                       PIC 9(02).                           
           05  FILLER                      PIC X        VALUE SPACE.            
           05  H4-CCYY                     PIC 9(4).                            
           05  FILLER                      PIC X(5)     VALUE SPACE.            
      *                                                                         
      ******************************************************************        
SJ0600** HEADING LINES - FOR INVENTORY TYPE - 'PHYSICAL' OR 'ESTIMATED'         
      ******************************************************************        
SJ0600 01  H4-HEADING-TYPE.                                                     
W33304     05  FILLER                      PIC X(06)    VALUE 'CYCLE '.         
W33304     05  H4-CYCLE                    PIC 9(05)    VALUE ZEROES.           
W33304     05  FILLER                      PIC X(46)    VALUE SPACE.            
SJ0600     05  FILLER                      PIC X(17)    VALUE                   
SJ0600     'FOR STORES DOING '.                                                 
SJ0600     05  H4-INV-TYPE                 PIC X(09)    VALUE SPACE.            
SJ0600*    'PHYSICAL '.                                                         
SJ0600*    'ESTIMATED'.                                                         
SJ0600     05  FILLER                      PIC X(10)    VALUE                   
SJ0600     ' INVENTORY'.                                                        
W33304     05  FILLER                      PIC X(02)    VALUE ', '.             
W33304     05  H4-CYCLE-NAME               PIC X(20)    VALUE SPACE.            
W33304     05  FILLER                      PIC X(35)    VALUE SPACE.            
W33304                                                                          
W33304 01  H4-HEADING-MONTH.                                                    
W33304     05  FILLER                      PIC X(06)    VALUE 'MONTH '.         
W33304     05  H4-MONTH                    PIC X(03)    VALUE SPACE.            
W33304     05  FILLER                      PIC X(48)    VALUE SPACE.            
W33304     05  FILLER                      PIC X(17)    VALUE                   
W33304     'FOR STORES DOING '.                                                 
W33304     05  H4-INV-TYPE-MN              PIC X(09)    VALUE SPACE.            
W33304*    'PHYSICAL '.                                                         
W33304*    'ESTIMATED'.                                                         
W33304     05  FILLER                      PIC X(10)    VALUE                   
W33304     ' INVENTORY'.                                                        
W33304     05  FILLER                      PIC X(02)    VALUE ', '.             
W33304     05  H4-MONTH-CHAR               PIC X(02)    VALUE SPACE.            
W33304     05  H4-MONTH-NUM REDEFINES H4-MONTH-CHAR                             
W33304                                     PIC 9(02).                           
W33304     05  FILLER                      PIC X        VALUE '-'.              
W33304     05  H4-MONTH-FULL               PIC X(10)    VALUE SPACE.            
W33304     05  FILLER                      PIC X(42)    VALUE SPACE.            
                                                                                
       01  H5-HEADING5.                                                         
W33304     05  H5-DIV-NAME                 PIC X(09)    VALUE SPACE.            
           05  H5-DIV-NO                   PIC X(02)    VALUE SPACE.            
           05  FILLER                      PIC X(44)    VALUE SPACE.            
           05  FILLER                      PIC X(21)    VALUE SPACE.            
           05  FILLER                      PIC X(01)    VALUE SPACE.            
           05  FILLER                      PIC X(02)    VALUE SPACE.            
           05  FILLER                      PIC X(54)    VALUE SPACE.            
      *                                                                         
       01  H6-HEADING6.                                                         
           05  H6-BUYER-NO                 PIC X(03)    VALUE SPACE.            
           05  FILLER                      PIC X(01)    VALUE SPACE.            
           05  H6-BUYER-NAME               PIC X(30)    VALUE SPACE.            
      *                                                                         
       01  H7-HEADING7.                                                         
W26600     05  FILLER                      PIC X(24)    VALUE SPACE.            
           05  FILLER                      PIC X(04)    VALUE 'BOOK'.           
           05  FILLER                      PIC X(11)    VALUE SPACE.            
           05  FILLER                      PIC X(04)    VALUE 'BASE'.           
           05  FILLER                      PIC X(10)    VALUE SPACE.            
           05  FILLER                      PIC X(09)    VALUE                   
           'SHRINKAGE'.                                                         
           05  FILLER                      PIC X(08)    VALUE SPACE.            
0400RM     05  FILLER                      PIC X(09)    VALUE                   
0400RM     'ESTIMATED'.                                                         
0400RM     05  FILLER                      PIC X(12)    VALUE SPACE.            
           05  FILLER                      PIC X(05)    VALUE                   
           'TOTAL'.                                                             
0200RM     05  FILLER                      PIC X(08)    VALUE SPACE.            
           05  FILLER                      PIC X(09)    VALUE                   
           'PHY. INV.'.                                                         
0200RM     05  FILLER                      PIC X(08)    VALUE SPACE.            
           05  FILLER                      PIC X(08)    VALUE                   
           'SHORTAGE'.                                                          
      *                                                                         
       01  H8-HEADING8.                                                         
W26600     05  FILLER                      PIC X(21)    VALUE SPACE.            
           05  FILLER                      PIC X(11)    VALUE                   
           'INVENTORY'.                                                         
           05  FILLER                      PIC X(07)    VALUE SPACE.            
           05  FILLER                      PIC X(07)    VALUE                   
           'SALES'.                                                             
           05  FILLER                      PIC X(07)    VALUE SPACE.            
           05  FILLER                      PIC X(09)    VALUE                   
           'ALLOWANCE'.                                                         
0200RM     05  FILLER                      PIC X(09)    VALUE SPACE.            
0400RM     05  FILLER                      PIC X(07)    VALUE                   
0400RM     'INV ADJ'.                                                           
0400RM     05  FILLER                      PIC X(12)    VALUE SPACE.            
           05  FILLER                      PIC X(06)    VALUE                   
           'RETAIL'.                                                            
0200RM     05  FILLER                      PIC X(09)    VALUE SPACE.            
           05  FILLER                      PIC X(06)    VALUE                   
           'RETAIL'.                                                            
0200RM     05  FILLER                      PIC X(10)    VALUE SPACE.            
           05  FILLER                      PIC X(06)    VALUE                   
           'RETAIL'.                                                            
0200RM     05  FILLER                      PIC X(07)    VALUE SPACE.            
           05  FILLER                      PIC X(01)    VALUE '%'.              
      *                                                                         
       01  DL-DESCRIPTION-LINE.                                                 
           05  DL-TYPE-HOLD                PIC X(6)     VALUE SPACE.            
           05  DL-TYPE-VALUE.                                                   
               10  DL-DEPT-VALUE           PIC X(3)     VALUE ZEROS.            
               10  DL-DASH-HOLD            PIC X(01)    VALUE SPACE.            
               10  DL-CLASS-VALUE          PIC X(2)     VALUE ZEROS.            
           05  FILLER                      PIC X(04)    VALUE SPACE.            
           05  DL-TYPE-NAME                PIC X(20)    VALUE SPACE.            
           05  FILLER                      PIC X(96)    VALUE SPACE.            
      *                                                                         
       01  DL-DETAIL-LINE.                                                      
WR9999     05  DL-STORE.                                                        
WR9999         10  FILLER                  PIC X(01)    VALUE SPACE.            
W26600         10  DL-STORE-NBR            PIC 9(05)    VALUE ZEROS.            
WR9999         10  FILLER                  PIC X(01)    VALUE SPACE.            
               10  DL-STORE-NAME           PIC X(10)    VALUE SPACE.            
               10  FILLER                  PIC X(02)    VALUE SPACE.            
           05  FILLER REDEFINES DL-STORE.                                       
W26600         10 DL-TOTAL-DESCRIPTION     PIC X(19).                           
0200RM     05  DL-BOOK-RETAIL              PIC ZZ,ZZZ,ZZZ,ZZZ-                  
                                                        VALUE ZEROS.            
0200RM     05  FILLER                      PIC X(01)    VALUE SPACE.            
0200RM     05  DL-BASE-SALES               PIC ZZ,ZZZ,ZZZ,ZZZ-                  
                                                        VALUE ZEROS.            
0200RM     05  FILLER                      PIC X(01)    VALUE SPACE.            
0200RM     05  DL-SHRINK                   PIC ZZ,ZZZ,ZZZ,ZZZ-                  
                                                        VALUE ZEROS.            
0200RM     05  FILLER                      PIC X(01)    VALUE SPACE.            
0400RM     05  DL-EST-INV-ADJ              PIC ZZ,ZZZ,ZZZ,ZZZ-                  
0400RM                                                  VALUE ZEROS.            
0400RM     05  FILLER                      PIC X(01)    VALUE SPACE.            
0200RM     05  DL-TOTAL-BOOK               PIC ZZ,ZZZ,ZZZ,ZZZ-                  
                                                        VALUE ZEROS.            
0200RM     05  FILLER                      PIC X(01)    VALUE SPACE.            
0200RM     05  DL-PHY-RETAIL               PIC ZZ,ZZZ,ZZZ,ZZZ-                  
                                                        VALUE ZEROS.            
0200RM     05  FILLER                      PIC X(01)    VALUE SPACE.            
           05  DL-SHORT-RET                PIC ZZ,ZZZ,ZZZ,ZZZ-                  
                                                        VALUE ZEROS.            
0200RM     05  FILLER                      PIC X(01)    VALUE SPACE.            
21015      05  DL-SHORT-PER                PIC ZZ9.99-  VALUE ZEROS.            
      *                                                                         
       01  TOTAL-CONSTANT-LINE.                                                 
W26600     05  FILLER                      PIC X(07)    VALUE SPACE.            
           05  FILLER                      PIC X(12)    VALUE SPACE.            
0200RM     05  FILLER                      PIC X(15)    VALUE ALL '-'.          
0200RM     05  FILLER                      PIC X(01)    VALUE SPACE.            
0200RM     05  FILLER                      PIC X(15)    VALUE ALL '-'.          
0200RM     05  FILLER                      PIC X(01)    VALUE SPACE.            
0200RM     05  FILLER                      PIC X(15)    VALUE ALL '-'.          
0200RM     05  FILLER                      PIC X(01)    VALUE SPACE.            
0200RM     05  FILLER                      PIC X(15)    VALUE ALL '-'.          
0200RM     05  FILLER                      PIC X(01)    VALUE SPACE.            
0200RM     05  FILLER                      PIC X(15)    VALUE ALL '-'.          
           05  FILLER                      PIC X        VALUE SPACE.            
0400RM     05  FILLER                      PIC X(15)    VALUE ALL '-'.          
0400RM     05  FILLER                      PIC X        VALUE SPACE.            
0200RM     05  FILLER                      PIC X(15)    VALUE ALL '-'.          
           05  FILLER                      PIC X(01)    VALUE SPACE.            
           05  FILLER                      PIC X(05)    VALUE ALL '-'.          
      *                                                                         
       01  TOTAL-LABEL-LINE.                                                    
           05  TL-TOTAL-HOLD               PIC X(08)    VALUE SPACE.            
           05  FILLER                      PIC X(01)    VALUE SPACE.            
           05  TL-TYPE-VALUE               PIC X(3)     VALUE ZEROS.            
W33304     05  FILLER                      PIC X(03)    VALUE SPACE.            
W33304     05  FILLER                      PIC X(05)    VALUE                   
W33304     'TOTAL'.                                                             
W33304     05  FILLER                      PIC X(117)   VALUE SPACE.            
      *                                                                         
W33304 01  CYCLE-LABEL-LINE.                                                    
W33304     05  CY-TOTAL-HOLD               PIC X(08)    VALUE SPACE.            
W33304     05  FILLER                      PIC X(01)    VALUE SPACE.            
W33304     05  CY-TYPE-VALUE               PIC X(5)     VALUE ZEROS.            
W33304     05  CY-CYCLE REDEFINES CY-TYPE-VALUE                                 
W33304                                     PIC 9(05).                           
W33304     05  FILLER                      PIC X(01)    VALUE SPACE.            
W33304     05  FILLER                      PIC X(05)    VALUE                   
W33304     'TOTAL'.                                                             
W33304     05  FILLER                      PIC X(117)   VALUE SPACE.            
W33304                                                                          
W33304 01  MONTH-LABEL-LINE.                                                    
W33304     05  MN-TOTAL-HOLD               PIC X(08)    VALUE SPACE.            
W33304     05  FILLER                      PIC X(01)    VALUE SPACE.            
W33304     05  MN-TYPE-VALUE               PIC X(03)    VALUE ZEROS.            
W33304     05  FILLER                      PIC X(03)    VALUE SPACE.            
W33304     05  FILLER                      PIC X(05)    VALUE                   
W33304     'TOTAL'.                                                             
W33304     05  FILLER                      PIC X(117)   VALUE SPACE.            
W33304                                                                          
       01  IW-INTERMEDIATE-WORKAREAS.                                           
           05  IW-DATE.                                                         
               10  IW-YEAR                 PIC X(02)    VALUE SPACE.            
               10  IW-MONTH                PIC X(02)    VALUE SPACE.            
               10  IW-DAY                  PIC X(02)    VALUE SPACE.            
           05  IW-TIME.                                                         
               10  IW-HOUR                 PIC X(02)    VALUE SPACE.            
               10  IW-MINUTE               PIC X(02)    VALUE SPACE.            
               10  FILLER                  PIC X(02)    VALUE SPACE.            
               10  FILLER                  PIC X(02)    VALUE SPACE.            
      *                                                                         
       01  SA-SAVE-AREAS.                                                       
           05  SA-BUYER-NAME               PIC X(20)    VALUE SPACE.            
W33304     05  SA-INV-ID                   PIC S9(9) COMP                       
W33304                                                  VALUE ZERO.             
W33304     05  SA-OPN-FSCL-MN-DTE          PIC X(10)    VALUE SPACE.            
           05  SA-GMA-NUMBER               PIC X(02)    VALUE SPACE.            
           05  SA-DIVISION-NUMBER          PIC X(02)    VALUE SPACE.            
           05  SA-BUYER-NUMBER             PIC X(03)    VALUE SPACE.            
           05  SA-DEPT-CLASS-NUMBER        PIC X(05)    VALUE SPACE.            
           05  FILLER REDEFINES SA-DEPT-CLASS-NUMBER.                           
               10  SA-DEPT-NUMBER          PIC X(03).                           
               10  SA-CLASS-NUMBER         PIC X(02).                           
           05  SA-PRINT-DEPT-DESC          PIC X(20)    VALUE SPACE.            
           05  SA-PRINT-CLASS-DESC         PIC X(20)    VALUE SPACE.            
           05  SA-PERCENT                  PIC 9(03)V99 VALUE ZEROS.            
0400RM     05  SA-PRINT-LINE               PIC X(150)   VALUE SPACE.            
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-FIND-MONTH               PIC X(01)    VALUE 'N'.              
               88  PS-MONTH-FOUND                       VALUE 'Y'.              
           05  PS-STORE-MATCH              PIC X(01)    VALUE 'N'.              
               88  PS-STORE-MATCHED                     VALUE 'Y'.              
           05  PS-PRINT-DEPT-HEADER        PIC X(01)    VALUE 'Y'.              
               88  PS-NEED-DEPT-HEADER                  VALUE 'Y'.              
           05  PS-PRINT-LABEL              PIC X(01)    VALUE 'N'.              
               88  PS-NO-LABEL-PRINTED                  VALUE 'N'.              
               88  PS-NEED-LABEL-PRINT                  VALUE 'Y'.              
           05  PS-NEW-PAGE                 PIC X(01)    VALUE 'N'.              
               88  PS-PAGE-BREAK                        VALUE 'Y'.              
           05  PS-END-OF-EXTRACT-FILE      PIC X(01)    VALUE 'N'.              
               88  PS-END-OF-EXTRACT                    VALUE 'Y'.              
               88  PS-NOT-END-OF-EXTRACT                VALUE 'N'.              
      *                                                                         
       01  DS-DC-SUB                       PIC S9(04)   VALUE +0                
                                                        COMP SYNC.              
                                                                        05900606
WR9999 01  SV-STORE-NBR.                                                02730000
W26600     05  SV-STORE-NBR-NUM             PIC 9(5).                   02730000
WR9999                                                                  02730000
WR9999 01  SV-STORE-NBR-XXX.                                            02730000
W26600     05  SV-STORE-NBR-2-4             PIC  9(5).                  02730000
WR9999                                                                  02730000
      *                                                                         
       01  TT-TOTAL-TABLE.                                                      
           05  TT-TOTAL-ENTRY              OCCURS 2 TIMES                       
                                           INDEXED BY TT-TOTAL-INDEX.           
W26600         10  TT-BOOK-INVENTORY       PIC S9(13)V99                        
                                           COMP-3.                              
W26600         10  TT-SHRINK-ALLOWANCE     PIC S9(13)V99                        
                                           COMP-3.                              
W26600         10  TT-TOTAL-RETAIL         PIC S9(13)V99                        
                                           COMP-3.                              
W26600         10  TT-PHY-INV-RETAIL       PIC S9(13)V99                        
                                           COMP-3.                              
W26600         10  TT-SHORT-RETAIL         PIC S9(13)V99                        
                                           COMP-3.                              
W26600         10  TT-TOTAL-SALES          PIC S9(13)V99                        
                                           COMP-3.                              
W26600         10  TT-EST-INV-ADJ          PIC S9(13)V99                        
0400RM                                     COMP-3.                              
      *                                                                         
       01  ST-STORE-TOTAL-TABLE.                                                
W26600     05  ST-STORE-ENTRY              OCCURS 9999 TIMES                    
                                           INDEXED BY ST-STORE-INDEX.           
W33304         10  ST-INV-ID               PIC S9(09) COMP.                     
               10  ST-STORE-NUMBER         PIC S9(04) COMP.                     
               10  ST-PRINT-STORE-IND      PIC X.                               
                   88  ST-PRINT-STORE                         VALUE 'Y'.        
                   88  ST-DONT-PRINT-STORE                    VALUE 'N'.        
               10  ST-STORE-NAME           PIC X(10).                           
W33304         10  ST-LEVEL-ENTRY          OCCURS 7 TIMES                       
                                           INDEXED BY ST-LEVEL-INDEX.           
W26600             15  ST-BOOK-INVENTORY   PIC S9(13)V99                        
                                           COMP-3.                              
W26600             15  ST-SHRINK-ALLOWANCE PIC S9(13)V99                        
                                           COMP-3.                              
W26600             15  ST-TOTAL-RETAIL     PIC S9(13)V99                        
                                           COMP-3.                              
W26600             15  ST-PHY-INV-RETAIL   PIC S9(13)V99                        
                                           COMP-3.                              
W26600             15  ST-SHORT-RETAIL     PIC S9(13)V99                        
                                           COMP-3.                              
W26600             15  ST-TOTAL-SALES      PIC S9(13)V99                        
                                           COMP-3.                              
W26600             15  ST-EST-INV-ADJ      PIC S9(13)V99                        
0400RM                                     COMP-3.                              
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      *                                                                         
      ******************************************************************        
      * A100-MAIN-MODULE - HIGHEST LEVEL CONTROL MODULE                *        
      ******************************************************************        
       A100-MAIN-MODULE.                                                        
           ACCEPT IW-DATE FROM DATE.                                            
           ACCEPT IW-TIME FROM TIME.                                            
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-INITIALIZE-DUTIES.                                      
           INITIALIZE ST-STORE-TOTAL-TABLE                                      
                      TT-TOTAL-TABLE.                                           
           SET ST-STORE-INDEX TO 1.                                             
           PERFORM B300-LOAD-STORE-ENTRIES.                                     
           PERFORM Z100-READ-EXTRACT-FILE.                                      
           IF PS-END-OF-EXTRACT                                                 
               CONTINUE                                                         
           ELSE                                                                 
W33304         SET PV-IN014-FILE-NOT-EMPTY TO TRUE                              
               MOVE IN014-GMA-NBR            TO SA-DIVISION-NUMBER              
W33304         MOVE IN014-INV-ID             TO SA-INV-ID                       
W33304         MOVE IN014-OPN-FSCL-MN-DTE    TO SA-OPN-FSCL-MN-DTE              
               MOVE IN014-GMA-NBR            TO SA-GMA-NUMBER                   
               MOVE IN014-BYR-NBR            TO SA-BUYER-NUMBER                 
               MOVE IN014-BYR-NAME           TO SA-BUYER-NAME                   
               MOVE IN014-RPT-CL-NBR         TO SA-CLASS-NUMBER                 
               MOVE IN014-DEPT-NBR           TO SA-DEPT-NUMBER                  
               MOVE IN014-DEPT-MDSEAR-DESC   TO SA-PRINT-DEPT-DESC              
                                                                                
               PERFORM B500-SELECT-DESCRIPTION                                  
               MOVE MDSEAR-DESC              TO SA-PRINT-CLASS-DESC             
           END-IF.                                                              
                                                                                
           PERFORM B400-CREATE-SHORTAGE-REPORT                                  
               UNTIL PS-END-OF-EXTRACT.                                         
                                                                                
W33304     IF PV-IN014-FILE-NOT-EMPTY                                           
W33304         PERFORM                                                          
W33304             VARYING ST-LEVEL-INDEX                                       
W33304             FROM PC-CLASS-LEVEL BY 1                                     
W33304             UNTIL ST-LEVEL-INDEX > PC-COMPANY-LEVEL-STORE-INDEX          
W33304             PERFORM C300-PRODUCE-DC-TOTALS                               
                                                                                
W33304             SET TT-TOTAL-INDEX TO PC-COMPANY-LEVEL-TOTAL-INDEX           
W33304             PERFORM D200-PRODUCE-TOTAL-LINE                              
W33304             PERFORM D300-INIT-TOTALS                                     
W33304             INITIALIZE CP-LINE-COUNT                                     
W33304             IF (ST-LEVEL-INDEX = PC-BUYER-LEVEL OR                       
                       ST-LEVEL-INDEX = PC-DIVISION-LEVEL)                      
W33304                 MOVE 'Y' TO PC-PAGE-BREAK-SW                             
W33304             END-IF                                                       
W33304         END-PERFORM                                                      
W33304     END-IF.                                                              
           PERFORM B600-CLOSE-FILES.                                            
           STOP RUN.                                                            
      *                                                                         
      ******************************************************************        
      * B100-OPEN-FILES - OPENS FILES                                           
      ******************************************************************        
       B100-OPEN-FILES.                                                         
           OPEN  INPUT EXTRACT-FILE                                             
SJ0600                 CONTROL-FILE                                             
                OUTPUT SHORTAGE-REPORT.                                         
      *                                                                         
      ******************************************************************        
      * B200-INITIALIZE-DUTIES - PERFORM ONE TIME HEADING FUNCTIONS.            
      ******************************************************************        
       B200-INITIALIZE-DUTIES.                                                  
0400RM     MOVE IW-MONTH    TO DP150O-RUN-MONTH.                                
0400RM     MOVE IW-DAY      TO DP150O-RUN-DAY.                                  
0400RM     MOVE IW-YEAR     TO DP150O-RUN-YEAR.                                 
0400RM     MOVE IW-HOUR     TO DP150O-RUN-HOUR.                                 
0400RM     MOVE IW-MINUTE   TO DP150O-RUN-MINUTE.                               
0400RM     MOVE 'INKRP146'  TO DP150O-PROGRAM-NAME.                             
                                                                                
           PERFORM C050-GET-LAST-INV-DATE.                                      
W33304     IF PV-NULL NOT EQUAL ZEROES                                          
S28545         MOVE IW-YEAR  TO PV-CURRENT-YY                                   
S28545         MOVE IW-MONTH TO PV-CURRENT-MM                                   
S28545         MOVE IW-DAY   TO PV-CURRENT-DD                                   
S28545         MOVE PV-CURRENT-DATE TO PV-ACTL-INV-DTE                          
S28545                                 INVPAR-ACTL-INV-DTE                      
S28545*        MOVE ZERO TO PV-ACTL-INV-DTE-CCYY                                
S28545*        MOVE ZERO TO PV-ACTL-INV-DTE-MMYY                                
S28545*        MOVE ZERO TO PV-ACTL-INV-DTE-DDYY                                
           ELSE                                                                 
               MOVE INVPAR-ACTL-INV-DTE TO PV-ACTL-INV-DTE                      
           END-IF.                                                              
                                                                                
           PERFORM C100-FIND-HEADING-MONTH                                      
           MOVE PV-ACTL-INV-DTE-DD   TO H4-DD.                                  
           MOVE PV-ACTL-INV-DTE-CCYY TO H4-CCYY.                                
                                                                                
SJ0600     PERFORM C200-FIX-TYPE-HEADING.                                       
                                                                                
RB0900     IF CC-PHYSICAL-INV                                                   
RB0900         IF CC-MCL-INV                                                    
RB0900             MOVE 1      TO DP150O-REPORT-NUMBER                          
RB0900         ELSE                                                             
RB0900             MOVE 2      TO DP150O-REPORT-NUMBER                          
RB0900         END-IF                                                           
RB0900     ELSE                                                                 
RB0900         IF CC-MCL-INV                                                    
RB0900             MOVE 3      TO DP150O-REPORT-NUMBER                          
RB0900         ELSE                                                             
RB0900             MOVE 4      TO DP150O-REPORT-NUMBER                          
RB0900         END-IF                                                           
RB0900     END-IF.                                                              
                                                                                
      ******************************************************************        
      * B300-LOAD-STORE-ENTRIES - CONTROL FOR LOADING STORES           *        
      ******************************************************************        
       B300-LOAD-STORE-ENTRIES.                                                 
W26600     INITIALIZE DCLXMT00001.                                              
                                                                                
           EXEC SQL                                                             
               OPEN STORE_CURSOR                                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE < ZERO                                                    
               DISPLAY '** ABEND - INKRP146 **'                                 
               DISPLAY '** OPEN ERROR ON STORE TABLE'                           
               MOVE 'B300-LOAD-STORE-ENTRIES' TO PV-PARAGRAPH-NAME              
               GO TO Z900-SQL-ERROR                                             
           ELSE                                                                 
               IF ((SQLCODE > 0 ) AND (SQLCODE NOT = 100)) OR                   
                         (SQLWARN0 = 'W')                                       
                     DISPLAY '** ABEND - INKRP146 **'                           
                     DISPLAY '** OPEN WARNING ON STORE TABLE'                   
                    MOVE 'B300-LOAD-STORE-ENTRIES' TO PV-PARAGRAPH-NAME         
                     GO TO Z900-SQL-ERROR                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM B325-FETCH-STORE-TABLE.                                      
           PERFORM                                                              
               UNTIL SQLCODE = +100                                             
               IF ST-STORE-INDEX IS LESS THAN PC-TABLE-MAX                      
                   PERFORM B350-LOAD-STORES                                     
                   SET ST-STORE-INDEX UP BY 1                                   
               ELSE                                                             
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PGM = INKRP146 **'                               
                   DISPLAY '** STORE TABLE IS TOO SMALL'                        
                   DISPLAY '** INCREASE STORE TABLE SIZE'                       
                   MOVE +4004 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           EXEC SQL                                                             
               CLOSE STORE_CURSOR                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE < ZERO                                                    
               DISPLAY '** ABEND - INKRP146 **'                                 
               DISPLAY '** CLOSE ERROR ON STORE TABLE'                          
               MOVE 'B300-LOAD-STORE-ENTRIES' TO PV-PARAGRAPH-NAME              
               GO TO Z900-SQL-ERROR                                             
           ELSE                                                                 
               IF ((SQLCODE > 0 ) AND (SQLCODE NOT = 100)) OR                   
                         (SQLWARN0 = 'W')                                       
                     DISPLAY '** ABEND - INKRP146 **'                           
                     DISPLAY '** CLOSE WARNING ON STORE TABLE'                  
                    MOVE 'B300-LOAD-STORE-ENTRIES' TO PV-PARAGRAPH-NAME         
                     GO TO Z900-SQL-ERROR                                       
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
      *****************************************************************         
      * B325-FETCH-STORE-TABLE - FETCH THE DB2 STORE TABEL INTO THE   *         
      * DCLGEN                                                        *         
      *****************************************************************         
       B325-FETCH-STORE-TABLE.                                                  
           EXEC SQL                                                             
               FETCH STORE_CURSOR                                               
W26600         INTO   :XMT00001-LOC-NBR,                                        
W26600                :XMT00001-LOC-TYP-CDE,                                    
W26600                :XMT00001-LOC-10-ABBR-NM                                  
           END-EXEC.                                                            
                                                                                
           IF SQLCODE < ZERO                                                    
               DISPLAY '** ABEND - INKRP146 **'                                 
               DISPLAY '** FETCH ERROR ON STORE TABLE'                          
               MOVE 'B325-FETCH-STORE-TABLE' TO PV-PARAGRAPH-NAME               
               GO TO Z900-SQL-ERROR                                             
           ELSE                                                                 
               IF ((SQLCODE > 0 ) AND (SQLCODE NOT = 100)) OR                   
                         (SQLWARN0 = 'W')                                       
                     DISPLAY '** ABEND - INKRP146 **'                           
                     DISPLAY '** FETCH WARNING ON STORE TABLE'                  
                     MOVE 'B325-FETCH-STORE-TABLE' TO PV-PARAGRAPH-NAME         
                     GO TO Z900-SQL-ERROR                                       
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
      *****************************************************************         
      * B350-LOAD-STORES - MOVES DB2 STORE INFORMATION INTO A WORKING *         
      * STORAGE TABLE                                                 *         
      *****************************************************************         
       B350-LOAD-STORES.                                                        
W26600     MOVE XMT00001-LOC-NBR                                                
               TO ST-STORE-NUMBER (ST-STORE-INDEX)                              
W26600     MOVE XMT00001-LOC-10-ABBR-NM                                         
               TO ST-STORE-NAME (ST-STORE-INDEX)                                
           SET ST-DONT-PRINT-STORE (ST-STORE-INDEX) TO TRUE.                    
           PERFORM B325-FETCH-STORE-TABLE.                                      
      *                                                                         
      ******************************************************************        
      * B400-CREATE-SHORTAGE-REPORT    - THIS MODULE CHECKS FOR A      *        
      *  CHANGE IN CLASS, DEPARTMENT, BUYER OR DIVISION.  IT THEN PRO- *        
      *  CESSES THE PREVIOUS CHANGE AND ACCUMULATES THE RECORD THAT    *        
      *  CAUSED THE BREAK                                              *        
      ******************************************************************        
       B400-CREATE-SHORTAGE-REPORT.                                             
           IF IN014-RPT-CL-NBR = SA-CLASS-NUMBER                                
             AND IN014-DEPT-NBR = SA-DEPT-NUMBER                                
               CONTINUE                                                         
           ELSE                                                                 
               SET ST-LEVEL-INDEX TO PC-CLASS-LEVEL                             
               PERFORM C300-PRODUCE-DC-TOTALS                                   
               SET TT-TOTAL-INDEX TO PC-COMPANY-LEVEL-TOTAL-INDEX               
               PERFORM D200-PRODUCE-TOTAL-LINE                                  
               PERFORM D300-INIT-TOTALS                                         
               INITIALIZE CP-LINE-COUNT                                         
           END-IF.                                                              
                                                                                
           IF IN014-DEPT-NBR NOT = SA-DEPT-NUMBER                               
W33304       OR IN014-BYR-NBR NOT = SA-BUYER-NUMBER                             
W33304       OR IN014-GMA-NBR NOT = SA-GMA-NUMBER                               
W33304       OR IN014-INV-ID NOT  = SA-INV-ID                                   
W33304       OR IN014-OPN-FSCL-MN-DTE NOT = SA-OPN-FSCL-MN-DTE                  
               SET ST-LEVEL-INDEX TO PC-DEPARTMENT-LEVEL                        
               PERFORM C300-PRODUCE-DC-TOTALS                                   
               SET TT-TOTAL-INDEX TO PC-COMPANY-LEVEL-TOTAL-INDEX               
               PERFORM D200-PRODUCE-TOTAL-LINE                                  
               PERFORM D300-INIT-TOTALS                                         
               INITIALIZE CP-LINE-COUNT                                         
           END-IF.                                                              
                                                                                
           IF IN014-BYR-NBR NOT = SA-BUYER-NUMBER                               
W33304       OR IN014-GMA-NBR NOT = SA-GMA-NUMBER                               
W33304       OR IN014-INV-ID NOT  = SA-INV-ID                                   
W33304       OR IN014-OPN-FSCL-MN-DTE NOT = SA-OPN-FSCL-MN-DTE                  
               SET ST-LEVEL-INDEX TO PC-BUYER-LEVEL                             
               PERFORM C300-PRODUCE-DC-TOTALS                                   
               SET TT-TOTAL-INDEX TO PC-COMPANY-LEVEL-TOTAL-INDEX               
               PERFORM D200-PRODUCE-TOTAL-LINE                                  
               PERFORM D300-INIT-TOTALS                                         
               INITIALIZE CP-LINE-COUNT                                         
               MOVE 'Y' TO PC-PAGE-BREAK-SW                                     
           END-IF.                                                              
                                                                                
           IF IN014-GMA-NBR NOT = SA-GMA-NUMBER                                 
W33304       OR IN014-INV-ID NOT  = SA-INV-ID                                   
W33304       OR IN014-OPN-FSCL-MN-DTE NOT = SA-OPN-FSCL-MN-DTE                  
               SET ST-LEVEL-INDEX TO PC-DIVISION-LEVEL                          
               PERFORM C300-PRODUCE-DC-TOTALS                                   
               SET TT-TOTAL-INDEX TO PC-COMPANY-LEVEL-TOTAL-INDEX               
               PERFORM D200-PRODUCE-TOTAL-LINE                                  
               PERFORM D300-INIT-TOTALS                                         
               INITIALIZE CP-LINE-COUNT                                         
               MOVE 'Y' TO PC-PAGE-BREAK-SW                                     
           END-IF.                                                              
                                                                                
W33304     IF IN014-INV-ID NOT = SA-INV-ID                                      
W33304       OR IN014-OPN-FSCL-MN-DTE NOT = SA-OPN-FSCL-MN-DTE                  
W33304         SET ST-LEVEL-INDEX TO PC-CYCLE-LEVEL                             
W33304         PERFORM C300-PRODUCE-DC-TOTALS                                   
W33304         SET TT-TOTAL-INDEX TO PC-COMPANY-LEVEL-TOTAL-INDEX               
W33304         PERFORM D200-PRODUCE-TOTAL-LINE                                  
W33304         PERFORM D300-INIT-TOTALS                                         
W33304         INITIALIZE CP-LINE-COUNT                                         
W33304         MOVE 'Y' TO PC-PAGE-BREAK-SW                                     
W33304     END-IF.                                                              
W33304                                                                          
W33304     IF IN014-OPN-FSCL-MN-DTE NOT = SA-OPN-FSCL-MN-DTE                    
W33304         SET ST-LEVEL-INDEX TO PC-MONTH-LEVEL                             
W33304         PERFORM C300-PRODUCE-DC-TOTALS                                   
W33304         SET TT-TOTAL-INDEX TO PC-COMPANY-LEVEL-TOTAL-INDEX               
W33304         PERFORM D200-PRODUCE-TOTAL-LINE                                  
W33304         PERFORM D300-INIT-TOTALS                                         
W33304         INITIALIZE CP-LINE-COUNT                                         
W33304         MOVE 'Y' TO PC-PAGE-BREAK-SW                                     
W33304     END-IF.                                                              
W33304                                                                          
           MOVE 'N' TO PS-STORE-MATCH.                                          
           PERFORM C400-ACCUM-STORE-TOTALS                                      
               VARYING ST-STORE-INDEX                                           
               FROM 1 BY 1                                                      
               UNTIL (PS-STORE-MATCHED OR                                       
                      ST-STORE-INDEX > PC-TABLE-MAX).                           
                                                                                
           MOVE IN014-GMA-NBR            TO SA-DIVISION-NUMBER.                 
21015      IF IN014-RPT-CL-NBR = SA-CLASS-NUMBER                                
21015          AND IN014-GMA-NBR = SA-GMA-NUMBER                                
21015          AND IN014-DEPT-NBR = SA-DEPT-NUMBER                              
21015          CONTINUE                                                         
21015      ELSE                                                                 
21015          MOVE IN014-GMA-NBR        TO SA-GMA-NUMBER                       
21015          PERFORM B500-SELECT-DESCRIPTION                                  
21015          MOVE MDSEAR-DESC          TO SA-PRINT-CLASS-DESC                 
21015      END-IF.                                                              
W33304     MOVE IN014-INV-ID             TO SA-INV-ID.                          
W33304     MOVE IN014-OPN-FSCL-MN-DTE    TO SA-OPN-FSCL-MN-DTE.                 
           MOVE IN014-BYR-NBR            TO SA-BUYER-NUMBER.                    
           MOVE IN014-BYR-NAME           TO SA-BUYER-NAME.                      
           MOVE IN014-RPT-CL-NBR         TO SA-CLASS-NUMBER.                    
           MOVE IN014-DEPT-NBR           TO SA-DEPT-NUMBER.                     
           MOVE IN014-DEPT-MDSEAR-DESC   TO SA-PRINT-DEPT-DESC.                 
                                                                                
           PERFORM Z100-READ-EXTRACT-FILE.                                      
      *                                                                         
      ******************************************************************        
      * B500-SELECT-DESCRIPTION GETS THE DESCRIPTION AT THE CLASS               
      ******************************************************************        
       B500-SELECT-DESCRIPTION.                                                 
                                                                                
           MOVE IN014-RPT-LVL TO PV-RPT-LVL.                                    
                                                                                
           IF PV-MAJ-CLASS                                                      
               EXEC SQL                                                         
                   SELECT   A.MDSEAR_DESC                                       
                   INTO     :MDSEAR-DESC                                        
                   FROM     TMDSEAR A                                           
                   WHERE                                                        
                          A.GMA_NBR    = :SA-GMA-NUMBER                         
                     AND  A.DEPT_NBR   = :IN014-DEPT-NBR                        
                     AND  A.MAJ_CL_NBR = :IN014-MAJ-CL-NBR                      
                     AND  A.MDSELV_CDE = :IN014-RPT-LVL                         
                     AND  CURRENT DATE BETWEEN A.STRT_DTE AND A.END_DTE         
               END-EXEC                                                         
           ELSE                                                                 
               EXEC SQL                                                         
                   SELECT   A.MDSEAR_DESC                                       
                   INTO     :MDSEAR-DESC                                        
                   FROM     TMDSEAR A                                           
                   WHERE                                                        
                          A.GMA_NBR    = :SA-GMA-NUMBER                         
                     AND  A.DEPT_NBR   = :IN014-DEPT-NBR                        
                     AND  SUBSTR(A.SUB_CL_NBR,2,2) =                            
                                            :IN014-SUB-CL-NBR                   
                     AND  A.MDSELV_CDE = :IN014-RPT-LVL                         
                     AND  CURRENT DATE BETWEEN A.STRT_DTE AND A.END_DTE         
               END-EXEC.                                                        
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       MOVE '*** NOT ON MBS ***' TO MDSEAR-DESC                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       DISPLAY 'GMA ' SA-GMA-NUMBER                             
                       DISPLAY 'DPT ' IN014-DEPT-NBR                            
                       DISPLAY 'CLASS ' IN014-SUB-CL-NBR                        
                       DISPLAY 'RPT LVL ' IN014-RPT-LVL                         
                       DISPLAY '** READ FOR MDSEAR '                            
                       MOVE 'B500-SELECT-DESCRIPTION' TO                        
                             PV-PARAGRAPH-NAME                                  
                       MOVE 'SELECT ROW FROM TMDSEAR'                           
                                       TO PV-DB2-OPER-ATTEMPTED                 
                       PERFORM Z900-SQL-ERROR                                   
               END-EVALUATE.                                                    
                                                                                
      *                                                                         
      ******************************************************************        
      * B600-CLOSE-FILES - CLOSES FILES AND DOES STATUS CHECK                   
      ******************************************************************        
       B600-CLOSE-FILES.                                                        
           CLOSE EXTRACT-FILE                                                   
                 SHORTAGE-REPORT.                                               
      *                                                                         
      ******************************************************************        
      * C050-GET-LAST-INV-DATE. RETRIEVE THE MOST RECENT ACTUAL                 
      * INVENTORY DATE FROM THE INVENTORY PARAMETERS TABLE. IT WILL             
      * BE USED IN THE REPORT HEADING.                                          
      ******************************************************************        
       C050-GET-LAST-INV-DATE.                                                  
           INITIALIZE DCLTINVPAR.                                               
           PERFORM WITH TEST AFTER                                              
              VARYING PV-SQL-SUB FROM 1 BY 1                                    
              UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                 
              OR    SQLCODE = 0                                                 
                                                                                
                   EXEC SQL                                                     
                       SELECT MAX (ACTL_INV_DTE)                                
                       INTO  :INVPAR-ACTL-INV-DTE :PV-NULL                      
28545                  FROM TINVPAR A                                           
28545                      ,TINCNTL B                                           
                       WHERE NOT ACTL_INV_DTE = :PC-FUTURE-DATE                 
28545                    AND A.INV_ID = B.INV_ID                                
28545                    AND B.ACTV_IND = 'Y'                                   
                   END-EXEC                                                     
                                                                                
                   EVALUATE TRUE                                                
                       WHEN SQLCODE = 0                                         
                       WHEN SQLCODE = +100                                      
                       WHEN SQLCODE = -911                                      
                       WHEN SQLCODE = -913                                      
                           CONTINUE                                             
                       WHEN SQLCODE < ZERO                                      
                           DISPLAY '** ABEND - INKRP146 **'                     
                           DISPLAY '** READ FOR TINVPAR'                        
                           MOVE 'C050-GET-LAST-INV-DATE'                        
                                TO PV-PARAGRAPH-NAME                            
                           MOVE 'SELECT ROW FROM TINVPAR'                       
                                       TO PV-DB2-OPER-ATTEMPTED                 
                           GO TO Z900-SQL-ERROR                                 
                       WHEN ((SQLCODE > 0 ) AND (SQLCODE NOT = 100)) OR         
                             (SQLWARN0 = 'W')                                   
                           DISPLAY '** ABEND - INKRP146 **'                     
                           DISPLAY '** READ FOR TINVPAR'                        
                           MOVE 'SELECT ROW FROM TINVPAR'                       
                                       TO PV-DB2-OPER-ATTEMPTED                 
                           MOVE 'C050-GET-LAST-INV-DATE'                        
                                TO PV-PARAGRAPH-NAME                            
                           GO TO Z900-SQL-ERROR                                 
                   END-EVALUATE                                                 
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               DISPLAY '** ABEND - INKRP146 **'                                 
               DISPLAY '** READ FOR TINVPAR'                                    
               DISPLAY '** READ RETRIES EXCEEDED'                               
               MOVE 'C050-GET-LAST-INV-DATE'                                    
                    TO PV-PARAGRAPH-NAME                                        
               MOVE 'SELECT ROW FROM TINVPAR'                                   
                    TO PV-DB2-OPER-ATTEMPTED                                    
               GO TO Z900-SQL-ERROR                                             
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * C100-FIND-HEADING-MONTH - FIND MONTH NAME IN TABLE             *        
      ******************************************************************        
                                                                                
       C100-FIND-HEADING-MONTH.                                                 
           EXEC SQL                                                             
                SELECT  FSCL_MN_NM                                              
W33304                 ,FSCL_MN_NBR                                             
                INTO   :DTATTR-FSCL-MN-NM                                       
W33304                ,:DTATTR-FSCL-MN-NBR                                      
                FROM    TDTATTR                                                 
                WHERE KY_DTE = :INVPAR-ACTL-INV-DTE                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE        = +000                                       
                   MOVE DTATTR-FSCL-MN-NM TO H4-MONTH-NAME                      
W33304                                       H4-MONTH                           
W33304                                       H4-MONTH-FULL                      
W33304             MOVE DTATTR-FSCL-MN-NBR                                      
W33304                                    TO H4-MONTH-NUM                       
               WHEN OTHER                                                       
                   DISPLAY 'INVPAR-ACTL-INV-DTE:  '                             
                            INVPAR-ACTL-INV-DTE                                 
                   MOVE    'C100-FIND-HEADING-MONTH'                            
                                            TO PV-PARAGRAPH-NAME                
                   MOVE    'SELECT ROW FROM TDTATTR'                            
                                       TO PV-DB2-OPER-ATTEMPTED                 
                   PERFORM Z900-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
W33304******************************************************************        
W33304* C150-FIND-HEADING-CYCLE - FIND CYCLE NAME IN TABLE             *        
W33304******************************************************************        
W33304                                                                          
W33304 C150-FIND-HEADING-CYCLE.                                                 
W33304                                                                          
W33304     INITIALIZE INCNTL-INV-NM-TEXT                                        
W33304                                                                          
W33304     EXEC SQL                                                             
W33304          SELECT  INV_NM                                                  
W33304          INTO   :INCNTL-INV-NM                                           
W33304          FROM    TINCNTL                                                 
W33304          WHERE INV_ID = :SA-INV-ID                                       
W33304     END-EXEC.                                                            
W33304                                                                          
W33304     EVALUATE TRUE                                                        
W33304         WHEN SQLCODE        = +000                                       
W33304             MOVE INCNTL-INV-NM-TEXT TO H4-CYCLE-NAME                     
W33304         WHEN SQLCODE        = +100                                       
W33304             MOVE 'CYL INFO NOT AVLBLE' TO H4-CYCLE-NAME                  
W33304         WHEN OTHER                                                       
W33304             MOVE    'C150-FIND-HEADING-CYCLE'                            
W33304                                      TO PV-PARAGRAPH-NAME                
W33304             MOVE    'SELECT ROW FROM TINCNTL'                            
W33304                                 TO PV-DB2-OPER-ATTEMPTED                 
W33304             PERFORM Z900-SQL-ERROR                                       
W33304     END-EVALUATE.                                                        
W33304                                                                          
      *                                                                         
      ******************************************************************        
      * C200-FIX-TYPE-HEADING  -                                       *        
      *    READ INP02 TO DETERMINE WHAT TYPE OF HEADING TO PRINT       *        
      ******************************************************************        
SJ0600 C200-FIX-TYPE-HEADING.                                                   
      *                                                                         
           READ CONTROL-FILE INTO CC-CONTROL-HEADER.                            
      *                                                                         
W33304     MOVE CC-CONTROL-LITERAL TO H4-INV-TYPE                               
W33304                                H4-INV-TYPE-MN.                           
      *                                                                         
      ******************************************************************        
      * C300-PRODUCE-DC-TOTALS - THIS MODULE IS LOOPED THROUGH THE     *        
      * MAXIMUM NUMBER OF DC'S TO PRODUCE STORE TOTALS WITHIN A DC     *        
      ******************************************************************        
       C300-PRODUCE-DC-TOTALS.                                                  
           PERFORM Z200-PRODUCE-HEADINGS.                                       
           PERFORM E400-LABEL-CHECK.                                            
                                                                                
           PERFORM D100-UNLOAD-STORE-TABLE                                      
               VARYING ST-STORE-INDEX                                           
               FROM 1 BY 1                                                      
               UNTIL (ST-STORE-NAME (ST-STORE-INDEX) = SPACES OR                
                      ST-STORE-INDEX > PC-TABLE-MAX).                           
                                                                                
      *                                                                         
      ******************************************************************        
      * C400-ACCUM-STORE-TOTALS - ACCUMULATES STORE TOTALS AT THE      *        
      * MAJOR CLASS LEVEL                                              *        
      ******************************************************************        
       C400-ACCUM-STORE-TOTALS.                                                 
           SET ST-LEVEL-INDEX TO PC-CLASS-LEVEL.                                
                                                                                
           IF IN014-STR-NBR = ST-STORE-NUMBER (ST-STORE-INDEX)                  
               SET ST-PRINT-STORE (ST-STORE-INDEX) TO TRUE                      
W33304         MOVE IN014-INV-ID TO ST-INV-ID (ST-STORE-INDEX)                  
               ADD IN014-BOOK-INV-RTL-AMT TO                                    
                   ST-BOOK-INVENTORY (ST-STORE-INDEX, ST-LEVEL-INDEX)           
               ADD IN014-SHNK-RTL-AMT TO                                        
                   ST-SHRINK-ALLOWANCE (ST-STORE-INDEX, ST-LEVEL-INDEX)         
               ADD IN014-TOTAL-BOOK-RTL-AMT TO                                  
                   ST-TOTAL-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)             
               ADD IN014-PHY-INV-RTL-AMT TO                                     
                   ST-PHY-INV-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)           
               ADD IN014-SHORTAGE-RTL-AMT TO                                    
                   ST-SHORT-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)             
               ADD IN014-SLS-BSE-RTL-AMT TO                                     
                   ST-TOTAL-SALES (ST-STORE-INDEX, ST-LEVEL-INDEX)              
0400RM         ADD IN014-ESTIMATED-INV-ADJ-AMT TO                               
0400RM             ST-EST-INV-ADJ (ST-STORE-INDEX, ST-LEVEL-INDEX)              
               SET PS-STORE-MATCHED TO TRUE                                     
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * D100-UNLOAD-STORE-TABLE - UNLOADS AN OCCURANCE OF THE STORE    *        
      * TABLE IF THE FINANCE CORPORATION NUMBER IS EQUAL TO THE DC     *        
      * SUBSCRIPT.  THIS IS VIABLE BECAUSE THE FINANCE CORPORATION     *        
      * NUMBERS ARE 01 FOR KOHLS AND 02 FOR COLUMBUS.  A DETAIL LINE   *        
      * IS WRITTEN FOR EACH STORE                                      *        
      ******************************************************************        
       D100-UNLOAD-STORE-TABLE.                                                 
           IF  ST-PRINT-STORE (ST-STORE-INDEX)                                  
WR9999***      ADD THE STORE NUMBER TO THE REPORT                               
WR9999         MOVE ST-STORE-NUMBER (ST-STORE-INDEX)                            
WR9999             TO SV-STORE-NBR-NUM                                          
WR9999         MOVE SV-STORE-NBR-NUM                                            
WR9999             TO SV-STORE-NBR-2-4                                          
WR9999         MOVE SV-STORE-NBR-2-4                                            
WR9999             TO DL-STORE-NBR                                              
               MOVE ST-STORE-NAME (ST-STORE-INDEX)                              
                   TO DL-STORE-NAME                                             
               MOVE ST-BOOK-INVENTORY (ST-STORE-INDEX, ST-LEVEL-INDEX)          
                   TO DL-BOOK-RETAIL                                            
               MOVE ST-TOTAL-SALES (ST-STORE-INDEX, ST-LEVEL-INDEX)             
                   TO DL-BASE-SALES                                             
               MOVE ST-SHRINK-ALLOWANCE (ST-STORE-INDEX, ST-LEVEL-INDEX)        
                   TO DL-SHRINK                                                 
0400RM         MOVE ST-EST-INV-ADJ (ST-STORE-INDEX, ST-LEVEL-INDEX)             
0400RM             TO DL-EST-INV-ADJ                                            
               MOVE ST-TOTAL-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)            
                   TO DL-TOTAL-BOOK                                             
               MOVE ST-PHY-INV-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)          
                   TO DL-PHY-RETAIL                                             
               MOVE ST-SHORT-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)            
                   TO DL-SHORT-RET                                              
               COMPUTE PA-PERCENT ROUNDED =                                     
               (ST-SHORT-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX) * 100)         
                   / ST-TOTAL-SALES (ST-STORE-INDEX, ST-LEVEL-INDEX)            
                  ON SIZE ERROR MOVE 999.99 TO PA-PERCENT                       
               END-COMPUTE                                                      
SJ1001*                                                                         
SJ1001*        SHRINK PERCENT MUST BE SAME SIGN (+/-) AS SHORT-RETAIL           
SJ1001         IF ST-SHORT-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX) > 0          
SJ1001             IF PA-PERCENT < 0                                            
SJ1001                 COMPUTE PA-PERCENT = (PA-PERCENT * -1)                   
SJ1001             END-IF                                                       
SJ1001         ELSE                                                             
SJ1001            IF ST-SHORT-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)< 0        
SJ1001                IF PA-PERCENT > 0                                         
SJ1001                    COMPUTE PA-PERCENT = (PA-PERCENT * -1)                
SJ1001                END-IF                                                    
SJ1001            ELSE                                                          
SJ1001                MOVE ZERO TO PA-PERCENT                                   
SJ1001            END-IF                                                        
SJ1001         END-IF                                                           
SJ1001*                                                                         
               MOVE PA-PERCENT TO DL-SHORT-PER                                  
               MOVE 'N' TO PS-PRINT-LABEL                                       
               PERFORM F100-CHECK-LINE-COUNT-TOTAL                              
               IF PS-NEED-LABEL-PRINT                                           
                   PERFORM E400-LABEL-CHECK                                     
               END-IF                                                           
W33304         IF ST-LEVEL-INDEX NOT = PC-MONTH-LEVEL                           
W33304             IF ST-LEVEL-INDEX NOT = PC-COMPANY-LEVEL-STORE-INDEX         
W33304                 IF ST-INV-ID (ST-STORE-INDEX) = SA-INV-ID                
W33304                     WRITE SHORTAGE-LINE                                  
W33304                         FROM DL-DETAIL-LINE                              
W33304                           AFTER ADVANCING 1 LINES                        
W33304                         ADD 1 TO CP-LINE-COUNT                           
W33304                 END-IF                                                   
W33304             ELSE                                                         
W33304                 WRITE SHORTAGE-LINE                                      
W33304                     FROM DL-DETAIL-LINE                                  
W33304                        AFTER ADVANCING 1 LINES                           
W33304                 ADD 1 TO CP-LINE-COUNT                                   
W33304             END-IF                                                       
W33304         END-IF                                                           
               IF ST-LEVEL-INDEX = PC-COMPANY-LEVEL-STORE-INDEX                 
                   CONTINUE                                                     
               ELSE                                                             
                   PERFORM E100-ACCUM-HIGHER-LEVEL                              
               END-IF                                                           
               PERFORM E200-ACCUM-TOTALS                                        
               PERFORM E300-INIT-STORE-ENTRIES                                  
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * D200-PRODUCE-TOTAL-LINE - PRODUCE A TOTAL LINE AT DC OR        *        
      * COMPANY LEVEL, DEPENDING UPON THE LEVEL AT WHICH THE TOTAL IN- *        
      * DEX HAS BEEN SET                                               *        
      ******************************************************************        
       D200-PRODUCE-TOTAL-LINE.                                                 
                                                                                
           MOVE PC-COMPANY TO DL-TOTAL-DESCRIPTION                              
                                                                                
           MOVE TT-BOOK-INVENTORY (TT-TOTAL-INDEX)                              
               TO DL-BOOK-RETAIL.                                               
           MOVE TT-TOTAL-SALES (TT-TOTAL-INDEX)                                 
               TO DL-BASE-SALES.                                                
           MOVE TT-SHRINK-ALLOWANCE (TT-TOTAL-INDEX)                            
               TO DL-SHRINK.                                                    
0400RM     MOVE TT-EST-INV-ADJ (TT-TOTAL-INDEX)                                 
0400RM         TO DL-EST-INV-ADJ                                                
           MOVE TT-TOTAL-RETAIL (TT-TOTAL-INDEX)                                
               TO DL-TOTAL-BOOK.                                                
           MOVE TT-PHY-INV-RETAIL (TT-TOTAL-INDEX)                              
               TO DL-PHY-RETAIL.                                                
           MOVE TT-SHORT-RETAIL (TT-TOTAL-INDEX)                                
               TO DL-SHORT-RET.                                                 
           COMPUTE PA-PERCENT ROUNDED =                                         
               (TT-SHORT-RETAIL (TT-TOTAL-INDEX) * 100)                         
                   / TT-TOTAL-SALES (TT-TOTAL-INDEX)                            
               ON SIZE ERROR MOVE 999.99 TO PA-PERCENT                          
           END-COMPUTE.                                                         
SJ1001*                                                                         
SJ1001*    SHRINK PERCENT MUST BE SAME SIGN (+/-) AS SHORT-RETAIL               
SJ1001     IF TT-SHORT-RETAIL (TT-TOTAL-INDEX) > 0                              
SJ1001         IF PA-PERCENT < 0                                                
SJ1001             COMPUTE PA-PERCENT = (PA-PERCENT * -1)                       
SJ1001         END-IF                                                           
SJ1001     ELSE                                                                 
SJ1001         IF TT-SHORT-RETAIL (TT-TOTAL-INDEX) < 0                          
SJ1001            IF PA-PERCENT > 0                                             
SJ1001                COMPUTE PA-PERCENT = (PA-PERCENT * -1)                    
SJ1001            END-IF                                                        
SJ1001         ELSE                                                             
SJ1001            MOVE ZERO TO PA-PERCENT                                       
SJ1001         END-IF                                                           
SJ1001     END-IF                                                               
SJ1001*                                                                         
           MOVE PA-PERCENT TO DL-SHORT-PER.                                     
           COMPUTE CP-ADVANCE-LINE-COUNT =                                      
               CP-LINE-COUNT + 2.                                               
           IF CP-ADVANCE-LINE-COUNT > CP-LINE-MAX                               
               INITIALIZE CP-LINE-COUNT                                         
               PERFORM Z200-PRODUCE-HEADINGS                                    
               PERFORM E400-LABEL-CHECK                                         
           END-IF.                                                              
                                                                                
           WRITE SHORTAGE-LINE                                                  
               FROM TOTAL-CONSTANT-LINE                                         
                  AFTER ADVANCING 1 LINES                                       
                                                                                
           WRITE SHORTAGE-LINE                                                  
               FROM DL-DETAIL-LINE                                              
                  AFTER ADVANCING 3 LINES.                                      
           MOVE SPACES TO DL-TOTAL-DESCRIPTION.                                 
      *                                                                         
      ******************************************************************        
      * D300-INIT-TOTALS - INITIALIZE TOTALS BASED UPON THE OCCURANCE  *        
      * OF THE INDEX.                                                  *        
      ******************************************************************        
       D300-INIT-TOTALS.                                                        
           MOVE ZEROS                                                           
               TO TT-BOOK-INVENTORY (TT-TOTAL-INDEX)                            
                  TT-SHRINK-ALLOWANCE (TT-TOTAL-INDEX)                          
0400RM            TT-EST-INV-ADJ (TT-TOTAL-INDEX)                               
                  TT-TOTAL-RETAIL (TT-TOTAL-INDEX)                              
                  TT-PHY-INV-RETAIL (TT-TOTAL-INDEX)                            
                  TT-SHORT-RETAIL (TT-TOTAL-INDEX)                              
                  TT-TOTAL-SALES (TT-TOTAL-INDEX).                              
      *                                                                         
      ******************************************************************        
      * E100-ACCUM-HIGHER-LEVEL  - ACCUMULATES TOTALS BY ADDING THEM   *        
      * TO THE NEXT HIGHER LEVEL OF BREAK                              *        
      ******************************************************************        
       E100-ACCUM-HIGHER-LEVEL.                                                 
           ADD ST-BOOK-INVENTORY (ST-STORE-INDEX, ST-LEVEL-INDEX)               
             TO ST-BOOK-INVENTORY (ST-STORE-INDEX, ST-LEVEL-INDEX + 1).         
           ADD ST-TOTAL-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)                 
             TO ST-TOTAL-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX + 1).           
           ADD ST-SHRINK-ALLOWANCE (ST-STORE-INDEX, ST-LEVEL-INDEX)             
            TO ST-SHRINK-ALLOWANCE (ST-STORE-INDEX, ST-LEVEL-INDEX + 1).        
0400RM     ADD ST-EST-INV-ADJ (ST-STORE-INDEX, ST-LEVEL-INDEX)                  
0400RM      TO ST-EST-INV-ADJ (ST-STORE-INDEX, ST-LEVEL-INDEX + 1).             
           ADD ST-TOTAL-SALES (ST-STORE-INDEX, ST-LEVEL-INDEX)                  
             TO ST-TOTAL-SALES (ST-STORE-INDEX, ST-LEVEL-INDEX + 1).            
           ADD ST-PHY-INV-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)               
             TO ST-PHY-INV-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX + 1).         
           ADD ST-SHORT-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)                 
             TO ST-SHORT-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX + 1).           
      *                                                                         
      ******************************************************************        
      * E200-ACCUM-TOTALS - ADDS CURRENT BREAK LEVEL TO TOTAL          *        
      * ACCUMULATORS FOR DC AND COMPANY                                *        
      ******************************************************************        
       E200-ACCUM-TOTALS.                                                       
           ADD ST-BOOK-INVENTORY (ST-STORE-INDEX, ST-LEVEL-INDEX)               
               TO TT-BOOK-INVENTORY (1)                                         
                  TT-BOOK-INVENTORY (2).                                        
           ADD ST-TOTAL-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)                 
               TO TT-TOTAL-RETAIL (1)                                           
                  TT-TOTAL-RETAIL (2).                                          
           ADD ST-SHRINK-ALLOWANCE (ST-STORE-INDEX, ST-LEVEL-INDEX)             
               TO TT-SHRINK-ALLOWANCE(1)                                        
                  TT-SHRINK-ALLOWANCE(2).                                       
0400RM     ADD ST-EST-INV-ADJ (ST-STORE-INDEX, ST-LEVEL-INDEX)                  
0400RM         TO TT-EST-INV-ADJ (1)                                            
0400RM            TT-EST-INV-ADJ (2).                                           
           ADD ST-TOTAL-SALES (ST-STORE-INDEX, ST-LEVEL-INDEX)                  
               TO TT-TOTAL-SALES (1)                                            
                  TT-TOTAL-SALES(2).                                            
           ADD ST-PHY-INV-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)               
               TO TT-PHY-INV-RETAIL (1)                                         
                  TT-PHY-INV-RETAIL(2).                                         
           ADD ST-SHORT-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)                 
               TO TT-SHORT-RETAIL (1)                                           
                  TT-SHORT-RETAIL (2).                                          
      *                                                                         
      ******************************************************************        
      * E300-INIT-STORE-ENTRIES -  INITIALIZES STORE ENTRIES BASED     *        
      * UPON THE CURRENT BREAK LEVEL                                   *        
      ******************************************************************        
       E300-INIT-STORE-ENTRIES.                                                 
           MOVE ZEROS                                                           
               TO ST-BOOK-INVENTORY (ST-STORE-INDEX, ST-LEVEL-INDEX)            
                  ST-SHRINK-ALLOWANCE (ST-STORE-INDEX, ST-LEVEL-INDEX)          
0400RM            ST-EST-INV-ADJ (ST-STORE-INDEX, ST-LEVEL-INDEX)               
                  ST-TOTAL-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)              
                  ST-PHY-INV-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)            
                  ST-SHORT-RETAIL (ST-STORE-INDEX, ST-LEVEL-INDEX)              
                  ST-TOTAL-SALES (ST-STORE-INDEX, ST-LEVEL-INDEX).              
      *                                                                         
      ******************************************************************        
      * E400-LABEL-CHECK - FORMATS A LABEL BASED UPON THE CURRENT      *        
      * LEVEL OF BREAK                                                 *        
      ******************************************************************        
       E400-LABEL-CHECK.                                                        
           EVALUATE TRUE                                                        
               WHEN ST-LEVEL-INDEX = PC-CLASS-LEVEL                             
                   PERFORM F200-PRODUCE-CLASS-LABEL                             
               WHEN ST-LEVEL-INDEX = PC-DEPARTMENT-LEVEL                        
                   PERFORM F300-PRODUCE-DEPARTMENT-LABEL                        
               WHEN ST-LEVEL-INDEX = PC-BUYER-LEVEL                             
                   PERFORM F400-PRODUCE-BUYER-LABEL                             
               WHEN ST-LEVEL-INDEX = PC-DIVISION-LEVEL                          
                   PERFORM F500-PRODUCE-DIVISION-LABEL                          
W33304         WHEN ST-LEVEL-INDEX = PC-CYCLE-LEVEL                             
W33304             PERFORM F700-PRODUCE-CYCLE-LABEL                             
W33304         WHEN ST-LEVEL-INDEX = PC-MONTH-LEVEL                             
W33304             PERFORM F800-PRODUCE-MONTH-LABEL                             
               WHEN ST-LEVEL-INDEX = PC-COMPANY-LEVEL-STORE-INDEX               
                   PERFORM F600-PRODUCE-COMPANY-LABEL                           
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * F100-CHECK-LINE-COUNT-TOTAL - CHECK LINE COUNT TO SEE IF LINE  *        
      * MAX HAS BEEN EXCEEDED                                          *        
      ******************************************************************        
       F100-CHECK-LINE-COUNT-TOTAL.                                             
           IF CP-LINE-COUNT IS LESS THAN CP-LINE-MAX                            
               CONTINUE                                                         
           ELSE                                                                 
               INITIALIZE CP-LINE-COUNT                                         
               PERFORM Z200-PRODUCE-HEADINGS                                    
               MOVE 'Y' TO PS-PRINT-LABEL                                       
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * F200-PRODUCE-CLASS-LABEL - PRODUCES LABEL FOR CLASS BEING RE-  *        
      * PORTED ON                                                      *        
      ******************************************************************        
       F200-PRODUCE-CLASS-LABEL.                                                
           MOVE SA-PRINT-CLASS-DESC TO DL-TYPE-NAME.                            
           MOVE PC-CLASS            TO DL-TYPE-HOLD.                            
           MOVE SA-DEPT-NUMBER      TO DL-DEPT-VALUE.                           
           MOVE PC-DASH             TO DL-DASH-HOLD.                            
           MOVE SA-CLASS-NUMBER     TO DL-CLASS-VALUE.                          
           WRITE SHORTAGE-LINE                                                  
               FROM DL-DESCRIPTION-LINE                                         
                  AFTER ADVANCING 2 LINES.                                      
           MOVE SPACES TO SA-PRINT-LINE.                                        
           WRITE SHORTAGE-LINE                                                  
               FROM SA-PRINT-LINE                                               
                  AFTER ADVANCING 1 LINES.                                      
           ADD 3 TO CP-LINE-COUNT.                                              
      *                                                                         
      ******************************************************************        
      * F300-PRODUCE-DEPT-LABEL -  PRODUCES LABEL FOR DEPARTMENT BEING *        
      * REPORTED ON                                                    *        
      ******************************************************************        
       F300-PRODUCE-DEPARTMENT-LABEL.                                           
           MOVE SA-PRINT-DEPT-DESC  TO DL-TYPE-NAME.                            
           MOVE PC-DEPT             TO DL-TYPE-HOLD.                            
           MOVE SA-DEPT-NUMBER      TO DL-DEPT-VALUE.                           
           MOVE SPACES              TO DL-DASH-HOLD.                            
           MOVE SPACES              TO DL-CLASS-VALUE.                          
           WRITE SHORTAGE-LINE                                                  
               FROM DL-DESCRIPTION-LINE                                         
                  AFTER ADVANCING 2 LINES.                                      
           MOVE SPACES TO SA-PRINT-LINE.                                        
           WRITE SHORTAGE-LINE                                                  
               FROM SA-PRINT-LINE                                               
                  AFTER ADVANCING 1 LINES.                                      
           ADD 3 TO CP-LINE-COUNT.                                              
      *                                                                         
      ******************************************************************        
      * F400-PRODUCE-BUYER-LABEL - PRODUCES LABEL FOR BUYER BEING RE-  *        
      * PORTED ON                                                      *        
      ******************************************************************        
       F400-PRODUCE-BUYER-LABEL.                                                
           MOVE PC-BUYER            TO TL-TOTAL-HOLD.                           
           MOVE SA-BUYER-NUMBER     TO TL-TYPE-VALUE.                           
           WRITE SHORTAGE-LINE                                                  
               FROM TOTAL-LABEL-LINE                                            
                  AFTER ADVANCING 2 LINES.                                      
           MOVE SPACES TO SA-PRINT-LINE.                                        
           WRITE SHORTAGE-LINE                                                  
               FROM SA-PRINT-LINE                                               
                  AFTER ADVANCING 1 LINES.                                      
           ADD 3 TO CP-LINE-COUNT.                                              
      *                                                                         
      ******************************************************************        
      * F500-PRODUCE-DIVISION-LABEL -  PRODUCES LABEL FOR DIVISION BE- *        
      * ING REPORTED ON                                                *        
      ******************************************************************        
       F500-PRODUCE-DIVISION-LABEL.                                             
           MOVE PC-DIVISION         TO TL-TOTAL-HOLD.                           
           MOVE SA-GMA-NUMBER  TO TL-TYPE-VALUE.                                
           WRITE SHORTAGE-LINE                                                  
               FROM TOTAL-LABEL-LINE                                            
                  AFTER ADVANCING 2 LINES.                                      
           MOVE SPACES TO SA-PRINT-LINE.                                        
           WRITE SHORTAGE-LINE                                                  
               FROM SA-PRINT-LINE                                               
                  AFTER ADVANCING 1 LINES.                                      
           ADD 3 TO CP-LINE-COUNT.                                              
      *                                                                         
W33304******************************************************************        
W33304* F700-PRODUCE-CYCLE-LABEL -  PRODUCES LABEL FOR CYCLE BEING     *        
W33304* REPORTED ON                                                    *        
W33304******************************************************************        
W33304 F700-PRODUCE-CYCLE-LABEL.                                                
W33304     MOVE PC-CYCLE            TO CY-TOTAL-HOLD.                           
W33304     MOVE SA-INV-ID           TO CY-CYCLE.                                
W33304     WRITE SHORTAGE-LINE                                                  
W33304         FROM CYCLE-LABEL-LINE                                            
W33304            AFTER ADVANCING 2 LINES.                                      
W33304     MOVE SPACES TO SA-PRINT-LINE.                                        
W33304     WRITE SHORTAGE-LINE                                                  
W33304         FROM SA-PRINT-LINE                                               
W33304            AFTER ADVANCING 1 LINES.                                      
W33304     ADD 3 TO CP-LINE-COUNT.                                              
W33304                                                                          
W33304******************************************************************        
W33304* F800-PRODUCE-MONTH-LABEL -  PRODUCES LABEL FOR MONTH BEING     *        
W33304* REPORTED ON                                                    *        
W33304******************************************************************        
W33304 F800-PRODUCE-MONTH-LABEL.                                                
W33304     MOVE PC-MONTH            TO MN-TOTAL-HOLD.                           
W33304     MOVE H4-MONTH            TO MN-TYPE-VALUE.                           
W33304     WRITE SHORTAGE-LINE                                                  
W33304         FROM MONTH-LABEL-LINE                                            
W33304            AFTER ADVANCING 2 LINES.                                      
W33304     MOVE SPACES TO SA-PRINT-LINE.                                        
W33304     WRITE SHORTAGE-LINE                                                  
W33304         FROM SA-PRINT-LINE                                               
W33304            AFTER ADVANCING 1 LINES.                                      
W33304     ADD 3 TO CP-LINE-COUNT.                                              
W33304                                                                          
      ******************************************************************        
      * F600-PRODUCE-COMPANY-LABEL -   PRODUCES LABEL FOR COMPANY      *        
      * TOTAL LEVEL                                                    *        
      ******************************************************************        
       F600-PRODUCE-COMPANY-LABEL.                                              
           MOVE PC-FINAL            TO TL-TOTAL-HOLD.                           
           MOVE ZEROS               TO TL-TYPE-VALUE.                           
           WRITE SHORTAGE-LINE                                                  
               FROM TOTAL-LABEL-LINE                                            
                  AFTER ADVANCING 2 LINES.                                      
           MOVE SPACES TO SA-PRINT-LINE.                                        
           WRITE SHORTAGE-LINE                                                  
               FROM SA-PRINT-LINE                                               
                  AFTER ADVANCING 1 LINES.                                      
           ADD 3 TO CP-LINE-COUNT.                                              
      *                                                                         
      ******************************************************************        
      * Z100-READ-EXTRACT-FILE - READS INPUT FILE                      *        
      ******************************************************************        
       Z100-READ-EXTRACT-FILE.                                                  
           READ EXTRACT-FILE                                                    
               INTO IN014-SHORTAGE-EXTR-REC                                     
                   AT END                                                       
                       SET PS-END-OF-EXTRACT TO TRUE                            
           END-READ.                                                            
                                                                                
      *                                                                         
      ******************************************************************        
      * Z200-PRODUCE-HEADINGS - PRODUCES REPORT HEADERS                *        
      ******************************************************************        
       Z200-PRODUCE-HEADINGS.                                                   
           IF PC-PAGE-BREAK                                                     
               MOVE SPACES TO SHORTAGE-LINE                                     
               WRITE SHORTAGE-LINE                                              
W33304           AFTER ADVANCING 1                                              
           END-IF.                                                              
           MOVE 'N' TO PC-PAGE-BREAK-SW.                                        
           ADD 1 TO PA-PAGE-NUMBER.                                             
0400RM*    MOVE PA-PAGE-NUMBER TO DP132O-PAGE-NUMBER.                           
0400RM     MOVE PA-PAGE-NUMBER TO DP150O-PAGE-NUMBER.                           
           WRITE SHORTAGE-LINE                                                  
0400RM*        FROM DP132O-STANDRD-HEADER-132-COLS                              
0400RM         FROM DP150O-STANDRD-HEADER-150-COLS                              
                   AFTER ADVANCING PAGE.                                        
0400RM*    MOVE SPACES TO DP132O-LASER-NEW-PAGE-INDICATR.                       
0400RM     MOVE SPACES TO DP150O-LASER-NEW-PAGE-INDICATR.                       
           WRITE SHORTAGE-LINE                                                  
               FROM H4-HEADING4                                                 
                   AFTER ADVANCING 2.                                           
W33304     IF  ST-LEVEL-INDEX = PC-MONTH-LEVEL                                  
W33304         MOVE SA-OPN-FSCL-MN-DTE TO INVPAR-ACTL-INV-DTE                   
W33304         PERFORM C100-FIND-HEADING-MONTH                                  
W33304         WRITE SHORTAGE-LINE                                              
W33304             FROM H4-HEADING-MONTH                                        
W33304                AFTER ADVANCING 1                                         
W33304     ELSE                                                                 
W33304         IF ST-LEVEL-INDEX = PC-COMPANY-LEVEL-STORE-INDEX                 
W33304             MOVE 99999     TO H4-CYCLE                                   
W33304             MOVE SPACES    TO H4-CYCLE-NAME                              
W33304         ELSE                                                             
W33304             MOVE SA-INV-ID TO H4-CYCLE                                   
W33304             PERFORM C150-FIND-HEADING-CYCLE                              
W33304         END-IF                                                           
W33304         WRITE SHORTAGE-LINE                                              
W33304             FROM H4-HEADING-TYPE                                         
W33304                AFTER ADVANCING 1                                         
W33304     END-IF.                                                              
W33304                                                                          
                                                                                
W33304     IF (ST-LEVEL-INDEX = PC-COMPANY-LEVEL-STORE-INDEX OR                 
W33304        ST-LEVEL-INDEX = PC-CYCLE-LEVEL OR                                
W33304        ST-LEVEL-INDEX = PC-MONTH-LEVEL)                                  
               MOVE SPACES TO H5-DIV-NO                                         
W33304         MOVE SPACES TO H5-DIV-NAME                                       
           ELSE                                                                 
W33304         MOVE 'DIVISION '   TO H5-DIV-NAME                                
               MOVE SA-GMA-NUMBER TO H5-DIV-NO                                  
           END-IF.                                                              
                                                                                
           WRITE SHORTAGE-LINE                                                  
               FROM H5-HEADING5                                                 
                   AFTER ADVANCING 2.                                           
                                                                                
           IF (ST-LEVEL-INDEX =  PC-DIVISION-LEVEL OR                           
W33304         ST-LEVEL-INDEX = PC-COMPANY-LEVEL-STORE-INDEX OR                 
W33304         ST-LEVEL-INDEX = PC-CYCLE-LEVEL OR                               
W33304         ST-LEVEL-INDEX = PC-MONTH-LEVEL)                                 
               MOVE SPACES TO H6-HEADING6                                       
           ELSE                                                                 
               MOVE SA-BUYER-NUMBER TO H6-BUYER-NO                              
               MOVE SA-BUYER-NAME TO H6-BUYER-NAME                              
           END-IF.                                                              
                                                                                
                                                                                
           WRITE SHORTAGE-LINE                                                  
               FROM H6-HEADING6                                                 
                   AFTER ADVANCING 1.                                           
           WRITE SHORTAGE-LINE                                                  
               FROM H7-HEADING7                                                 
                   AFTER ADVANCING 1.                                           
           WRITE SHORTAGE-LINE                                                  
               FROM H8-HEADING8                                                 
                   AFTER ADVANCING 1.                                           
SJ0600     ADD  8 TO CP-LINE-COUNT.                                             
      *                                                                         
      ******************************************************************        
      * Z900-SQL-ERROR - PERFORMS THE ABEND FOR THE PROGRAM IN THE     *        
      *  EVENT THAT AN SQL ERROR CODE OCCURS.                          *        
      ******************************************************************        
       Z900-SQL-ERROR.                                                          
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = INKRP146'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
