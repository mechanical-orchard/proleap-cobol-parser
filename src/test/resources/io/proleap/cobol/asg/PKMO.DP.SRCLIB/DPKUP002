       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    DPKUP002.                                                 
       AUTHOR.        CAL BUSH.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  10/01/91.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      ** THIS PROGRAM WILL LOAD MNEMONIC-RELATED INFORMATION INTO THE **        
      ** ARCHITECTURE TABLES.  THIS IS PART OF THE PROCESS USED TO    **        
      ** MOVE ARCHITECTURE INFORMATION FROM ONE REGION TO ANOTHER.    **        
      **                                                              **        
      **  CHANGES:                                                    **        
      **    12-09-1992  (CAL BUSH)  --  WR# 2170                      **        
      **    -  PASS RETURN-CODE OF 8 IF MENU DOES NOT EXIST FOR MENU- **        
      **       ITEM INSERT.                                           **        
      **    -  INCREMENT MENUITEM SEQUENCE NUMBER BY 1 IF THE SEQUENCE**        
      **       NUMBER IS IN USE FOR A DIFFERENT TARGET MNEMONIC.      **        
      **                                                              **        
      **    04-16-1996  (CAL BUSH)  --  WR# 3767                      **        
      **    -  ADDITIONAL FIELDS FOR TCICSROUTE AND TMNEMONIC.  SET   **        
      **       TMNEMONIC-UPD-VSAM-IND TO 'Y' WHEN RECORD IS ADDED/UPD.**        
      **                                                              **        
      **    08-28-1998  (JEFF ROUBIK)  --  WR# 10232                  **        
      **    -  DELETE ITEMS FROM MENU SO IT WILL BE REFRESHED INCASE  **        
      **       THERE WAS A SEQUENCE NUMBER CHANGE.                    **        
      **                                                              **        
      **    10-16-1998  (JEFF ROUBIK)  --  PITS# 236420               **        
      **    -  CHECK FORIEGN KEY BEFORE INSERTING MENU ITEMS.         **        
      **                                                              **        
      **    09-09-1999  (JEFF ROUBIK)  --  WR# 10808                  **        
      **    -  ADDED ACCURATE LOCATION REQUIRED INDICATOR TO MNEMONIC **        
      **       TABLE.                                                 **        
      **                                                              **        
      **    03-31-2000  (JEFF ROUBIK)  --  WR# 21021                  **        
      **    -  CREATE BACKOUT FILE SO DB2 TABLES CAN BE RESTORED TO   **        
      **       THEIR PRE-PROMOTE STATUS.                              **        
      **                                                              **        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT TMNEMONIC-EXT-FILE           ASSIGN TO INP01.                 
                                                                                
           SELECT TMENUITEM-EXT-FILE           ASSIGN TO INP02.                 
                                                                                
           SELECT TCICSROUTE-EXT-FILE          ASSIGN TO INP03.                 
                                                                                
           SELECT BACKOUT-FILE                 ASSIGN TO OUT01.                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  TMNEMONIC-EXT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 127 CHARACTERS.                                      
                                                                                
       01  TMNEMONIC-EXT-RECORD           PIC X(127).                           
                                                                                
       FD  TMENUITEM-EXT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 76 CHARACTERS.                                       
                                                                                
       01  TMENUITEM-EXT-RECORD           PIC X(76).                            
                                                                                
       FD  TCICSROUTE-EXT-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 56 CHARACTERS.                                       
                                                                                
       01  TCICSROUTE-EXT-RECORD           PIC X(56).                           
                                                                                
       FD  BACKOUT-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
                                                                                
       01  BACKOUT-RECORD                  PIC X(200).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  IV-INDICATOR-VARIABLES.                                              
           05  IV-DESC                 PIC S9(04)  VALUE +0                     
                                                   COMP.                        
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME       PIC  X(30)  VALUE SPACE.                 
           05  PV-DB2-OPERATION-ATTEMPTED                                       
                                       PIC  X(30)  VALUE SPACE.                 
           05  PV-HOLD-MNEMONIC        PIC  X(08)  VALUE SPACE.                 
           05  PV-HOLD-MENU-NAME       PIC  X(08)  VALUE SPACE.                 
           05  PV-WORK-MENU-NAME       PIC  X(08)  VALUE SPACE.                 
           05  PV-HOLD-FKEY-NAME       PIC  X(08)  VALUE SPACE.                 
           05  PV-WORK-FKEY-NAME       PIC  X(08)  VALUE SPACE.                 
           05  PV-WORK-SEQ-NUMBER      PIC  S9(4)  COMP-3.                      
           05  PV-PRINT-SEQUENCE       PIC  ZZ9    VALUE ZERO.                  
           05  PV-TEMP-HOLD-AREA       PIC X(1000) VALUE SPACES.                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-TMNEMONIC-FILE-SW                                      
                                       PIC X(01)   VALUE 'N'.                   
               88  PS-END-OF-TMNEMONIC-FILE        VALUE 'Y'.                   
           05  PS-END-OF-TMENUITEM-FILE-SW                                      
                                       PIC X(01)   VALUE 'N'.                   
               88  PS-END-OF-TMENUITEM-FILE        VALUE 'Y'.                   
           05  PS-END-OF-TCICSROUTE-FILE-SW                                     
                                       PIC X(01)   VALUE 'N'.                   
               88  PS-END-OF-TCICSROUTE-FILE       VALUE 'Y'.                   
           05  PS-MENU-EXISTS-SW       PIC X(01)   VALUE 'N'.                   
               88  PS-MENU-EXISTS                  VALUE 'Y'.                   
               88  PS-MENU-DOESNT-EXIST            VALUE 'N'.                   
           05  PS-FKEY-EXISTS-SW       PIC X(01)   VALUE 'N'.                   
               88  PS-FKEY-EXISTS                  VALUE 'Y'.                   
               88  PS-FKEY-DOESNT-EXIST            VALUE 'N'.                   
           05  PS-SET-RC-8-SW          PIC  X(01)  VALUE 'N'.                   
               88  PS-SET-RC-8                     VALUE 'Y'.                   
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE +0                     
                                                   COMP SYNC.                   
      *                                                                         
      *  BACKOUT FILE RECORD DESCRIPTION                                        
      *                                                                         
           COPY DPRD502.                                                        
                                                                                
      *                                                                         
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                
      *                                                                         
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  SQL AND COBOL DECLARATIONS FOR THE MNEMONIC TABLE                      
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TMNMNC                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SQL AND COBOL DECLARATIONS FOR THE MENUITEM TABLE                      
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TMNUITM                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SQL AND COBOL DECLARATIONS FOR THE CICSROUTE TABLE                     
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TCICSRTE                                                 
           END-EXEC.                                                            
                                                                                
      *                                                                         
      ** STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                        
      *                                                                         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SELECT FOR TCICSROUTE TABLE                                            
      ******************************************************************        
           EXEC SQL                                                             
               DECLARE TCICSROUTE_CURSOR CURSOR FOR                             
                SELECT AID_SEQUENCE_NMBR                                        
                     , DEST_MNEMONIC_CODE                                       
                     , ACTION_CODE                                              
                     , LAST_UPDATE_TMESTP                                       
                     , ROUTE_STACK_INDCT                                        
                     , FUNC_KEY_LBL_NAME                                        
                     , IN_APLCTN_COMM_CDE                                       
                     , FUNC_KY_DISPN_IND                                        
                  FROM TCICSROUTE                                               
                 WHERE MNEMONIC_CODE = :TCICSROUTE-MNEMONIC-CODE                
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SELECT FOR TMENUITEM TABLE                                             
      ******************************************************************        
           EXEC SQL                                                             
               DECLARE TMENUITEM_CURSOR CURSOR FOR                              
                SELECT MENU_NAME                                                
                     , ITEM_MNEMONIC_CODE                                       
                     , DESC                                                     
                     , SEQUENCE_NMBR                                            
                     , LAST_UPDATE_TMESTP                                       
                     , IN_APLCTN_COMM_CDE                                       
                  FROM TMENUITEM                                                
                 WHERE MENU_NAME = :TMNEMONIC-MNEMONIC-CODE                     
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '**** PROCESSING MNEMONICS'.                                 
           PERFORM 2000-PROCESS-TMNEMONIC-FILE                                  
               UNTIL PS-END-OF-TMNEMONIC-FILE.                                  
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '**** PROCESSING MENUITEMS'.                                 
           PERFORM 3000-PROCESS-TMENUITEM-FILE                                  
               UNTIL PS-END-OF-TMENUITEM-FILE.                                  
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '**** PROCESSING CICS ROUTING '.                             
           PERFORM 4000-PROCESS-TCICSROUTE-FILE                                 
               UNTIL PS-END-OF-TCICSROUTE-FILE.                                 
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
                                                                                
      ******************************************************************        
      ** OPEN INPUT FILES AND DO PRIMING READS ON THEM.               **        
      ******************************************************************        
       1000-INITIALIZE.                                                         
           DISPLAY ' **** DPKUP002 - ARCHITECTURE BATCH UPDATE ****'.           
           OPEN  INPUT TMNEMONIC-EXT-FILE                                       
                       TMENUITEM-EXT-FILE                                       
                       TCICSROUTE-EXT-FILE                                      
                OUTPUT BACKOUT-FILE.                                            
                                                                                
           PERFORM 2100-READ-MNEMONIC-INPUT.                                    
                                                                                
           PERFORM 3200-READ-MENUITEM-INPUT.                                    
                                                                                
           PERFORM 4100-READ-CICSROUTE-INPUT.                                   
                                                                                
                                                                                
      ******************************************************************        
      ** INSERT / UPDATE A MNEMONIC ON THE MNEMONIC TABLE.            **        
      ******************************************************************        
       2000-PROCESS-TMNEMONIC-FILE.                                             
           MOVE 'Y'                  TO TMNEMONIC-UPD-VSAM-IND.                 
           PERFORM 2500-CREATE-MNEMONIC-BACKOUT.                                
           PERFORM 2200-UPDATE-MNEMONIC.                                        
           IF  SQLCODE = +100                                                   
               PERFORM 2300-INSERT-MNEMONIC                                     
               DISPLAY 'MNEMONIC ' TMNEMONIC-MNEMONIC-CODE ' INSERTED '         
           ELSE                                                                 
               DISPLAY 'MNEMONIC ' TMNEMONIC-MNEMONIC-CODE ' UPDATED '          
               IF  TMNEMONIC-TRANS-ID-CODE = 'Z007'                             
               AND TMNEMONIC-SCRN-TYP-CDE  = 'M'                                
                   PERFORM 2600-CREATE-MENUITEM-BACKOUT                         
                   PERFORM 2400-DELETE-MENUITEMS                                
               END-IF                                                           
           END-IF.                                                              
           PERFORM 2100-READ-MNEMONIC-INPUT.                                    
                                                                                
                                                                                
       2100-READ-MNEMONIC-INPUT.                                                
           READ TMNEMONIC-EXT-FILE                                              
                INTO DCLTMNEMONIC                                               
                AT END                                                          
                    SET PS-END-OF-TMNEMONIC-FILE TO TRUE                        
           END-READ.                                                            
                                                                                
                                                                                
       2200-UPDATE-MNEMONIC.                                                    
           EXEC SQL                                                             
               UPDATE TMNEMONIC                                                 
               SET TRANS_ID_CODE      = :TMNEMONIC-TRANS-ID-CODE,               
                   AVAIL_INDCT        = :TMNEMONIC-AVAIL-INDCT,                 
                   CMAREA_FRMT_CODE   = :TMNEMONIC-CMAREA-FRMT-CODE,            
                   SAVE_CMAREA_INDCT  = :TMNEMONIC-SAVE-CMAREA-INDCT,           
                   CMAREA_LNGTH_MSRMT = :TMNEMONIC-CMAREA-LNGTH-MSRMT,          
                   PANEL_TITLE_NAME   = :TMNEMONIC-PANEL-TITLE-NAME,            
                   LAST_UPDATE_TMESTP = CURRENT TIMESTAMP,                      
                   GLBL_ACCS_IND      = :TMNEMONIC-GLBL-ACCS-IND,               
                   PGM_NAME           = :TMNEMONIC-PGM-NAME,                    
                   SCRN_TYP_CDE       = :TMNEMONIC-SCRN-TYP-CDE,                
                   UPD_VSAM_IND       = :TMNEMONIC-UPD-VSAM-IND,                
                   DB2_PKG_CDE        = :TMNEMONIC-DB2-PKG-CDE,                 
                   CMNT_TXT           = :TMNEMONIC-CMNT-TXT,                    
                   ACRT_LOC_REQR_IND  = :TMNEMONIC-ACRT-LOC-REQR-IND            
               WHERE MNEMONIC_CODE    = :TMNEMONIC-MNEMONIC-CODE                
           END-EXEC.                                                            
                                                                                
           IF  ((SQLCODE = ZERO)                                                
             OR (SQLCODE = +100))                                               
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2200-UPDATE-MNEMONIC'                                      
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'BAD UPDATE INTO TMNEMONIC TAB '                            
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       2300-INSERT-MNEMONIC.                                                    
           EXEC SQL                                                             
               INSERT                                                           
                   INTO    TMNEMONIC                                            
                          (MNEMONIC_CODE,                                       
                           TRANS_ID_CODE,                                       
                           AVAIL_INDCT,                                         
                           CMAREA_FRMT_CODE,                                    
                           SAVE_CMAREA_INDCT,                                   
                           CMAREA_LNGTH_MSRMT,                                  
                           PANEL_TITLE_NAME,                                    
                           LAST_UPDATE_TMESTP,                                  
                           GLBL_ACCS_IND,                                       
                           PGM_NAME,                                            
                           SCRN_TYP_CDE,                                        
                           UPD_VSAM_IND,                                        
                           DB2_PKG_CDE,                                         
                           CMNT_TXT,                                            
                           ACRT_LOC_REQR_IND)                                   
                   VALUES                                                       
                         (:TMNEMONIC-MNEMONIC-CODE,                             
                          :TMNEMONIC-TRANS-ID-CODE,                             
                          :TMNEMONIC-AVAIL-INDCT,                               
                          :TMNEMONIC-CMAREA-FRMT-CODE,                          
                          :TMNEMONIC-SAVE-CMAREA-INDCT,                         
                          :TMNEMONIC-CMAREA-LNGTH-MSRMT,                        
                          :TMNEMONIC-PANEL-TITLE-NAME,                          
                          :TMNEMONIC-LAST-UPDATE-TMESTP,                        
                          :TMNEMONIC-GLBL-ACCS-IND,                             
                          :TMNEMONIC-PGM-NAME,                                  
                          :TMNEMONIC-SCRN-TYP-CDE,                              
                          :TMNEMONIC-UPD-VSAM-IND,                              
                          :TMNEMONIC-DB2-PKG-CDE,                               
                          :TMNEMONIC-CMNT-TXT,                                  
                          :TMNEMONIC-ACRT-LOC-REQR-IND)                         
           END-EXEC.                                                            
                                                                                
           IF  SQLCODE = ZERO                                                   
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2300-INSERT-MNEMONIC'                                      
                       TO PV-PARAGRAPH-NAME                                     
               MOVE 'BAD INSERT INTO TMNEMONIC TAB '                            
                       TO PV-DB2-OPERATION-ATTEMPTED                            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       2400-DELETE-MENUITEMS.                                                   
           EXEC SQL                                                             
               DELETE FROM TMENUITEM                                            
               WHERE MENU_NAME = :TMNEMONIC-MNEMONIC-CODE                       
           END-EXEC.                                                            
                                                                                
           IF  ((SQLCODE = ZERO)                                                
             OR (SQLCODE = +100))                                               
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2400-DELETE-MENUITEMS'                                     
                       TO PV-PARAGRAPH-NAME                                     
               MOVE 'BAD DELETE FROM TMENUITEM TAB '                            
                       TO PV-DB2-OPERATION-ATTEMPTED                            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       2500-CREATE-MNEMONIC-BACKOUT.                                            
           MOVE DCLTMNEMONIC   TO PV-TEMP-HOLD-AREA.                            
           MOVE SPACES         TO DP502-BACKOUT-RECORD.                         
           SET  DP502-MNEMONIC TO TRUE.                                         
                                                                                
           EXEC SQL                                                             
               SELECT TRANS_ID_CODE                                             
                    , AVAIL_INDCT                                               
                    , CMAREA_FRMT_CODE                                          
                    , SAVE_CMAREA_INDCT                                         
                    , CMAREA_LNGTH_MSRMT                                        
                    , PANEL_TITLE_NAME                                          
                    , LAST_UPDATE_TMESTP                                        
                    , GLBL_ACCS_IND                                             
                    , PGM_NAME                                                  
                    , SCRN_TYP_CDE                                              
                    , UPD_VSAM_IND                                              
                    , DB2_PKG_CDE                                               
                    , CMNT_TXT                                                  
                    , ACRT_LOC_REQR_IND                                         
                 INTO :TMNEMONIC-TRANS-ID-CODE                                  
                    , :TMNEMONIC-AVAIL-INDCT                                    
                    , :TMNEMONIC-CMAREA-FRMT-CODE                               
                    , :TMNEMONIC-SAVE-CMAREA-INDCT                              
                    , :TMNEMONIC-CMAREA-LNGTH-MSRMT                             
                    , :TMNEMONIC-PANEL-TITLE-NAME                               
                    , :TMNEMONIC-LAST-UPDATE-TMESTP                             
                    , :TMNEMONIC-GLBL-ACCS-IND                                  
                    , :TMNEMONIC-PGM-NAME                                       
                    , :TMNEMONIC-SCRN-TYP-CDE                                   
                    , :TMNEMONIC-UPD-VSAM-IND                                   
                    , :TMNEMONIC-DB2-PKG-CDE                                    
                    , :TMNEMONIC-CMNT-TXT                                       
                    , :TMNEMONIC-ACRT-LOC-REQR-IND                              
                 FROM TMNEMONIC                                                 
                WHERE MNEMONIC_CODE = :TMNEMONIC-MNEMONIC-CODE                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   SET DP502-CHANGE TO TRUE                                     
               WHEN SQLCODE = +100                                              
                   SET DP502-DELETE TO TRUE                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < 0                                                 
               WHEN SQLCODE > 0                                                 
                   MOVE '2500-CREATE-MNEMONIC-BACKOUT   '                       
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'SELECT ROW ON TMNEMONIC TABLE  '                       
                       TO PV-DB2-OPERATION-ATTEMPTED                            
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
           MOVE DCLTMNEMONIC TO DP502-BACKOUT-DATA.                             
           WRITE BACKOUT-RECORD FROM DP502-BACKOUT-RECORD.                      
           MOVE PV-TEMP-HOLD-AREA TO DCLTMNEMONIC.                              
                                                                                
                                                                                
       2600-CREATE-MENUITEM-BACKOUT.                                            
           MOVE DCLTMENUITEM   TO PV-TEMP-HOLD-AREA.                            
           MOVE SPACES         TO DP502-BACKOUT-RECORD.                         
           SET  DP502-ADD      TO TRUE.                                         
           SET  DP502-MENUITEM TO TRUE.                                         
                                                                                
           PERFORM 2601-OPEN-TMENUITEM-CURSOR.                                  
                                                                                
           PERFORM WITH TEST AFTER                                              
             UNTIL SQLCODE = +100                                               
               PERFORM 2602-FETCH-TMENUITEM                                     
           END-PERFORM.                                                         
                                                                                
           PERFORM 2603-CLOSE-TMENUITEM-CURSOR.                                 
                                                                                
           MOVE PV-TEMP-HOLD-AREA TO DCLTMENUITEM.                              
                                                                                
                                                                                
       2601-OPEN-TMENUITEM-CURSOR.                                              
           EXEC SQL                                                             
               OPEN TMENUITEM_CURSOR                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < 0                                                 
               WHEN SQLCODE > 0                                                 
                   MOVE '2601-OPEN-TMENUITEM-CURSOR'                            
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'OPEN TMENUITEM CURSOR'                                 
                        TO PV-DB2-OPERATION-ATTEMPTED                           
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2602-FETCH-TMENUITEM.                                                    
           EXEC SQL                                                             
               FETCH TMENUITEM_CURSOR                                           
                INTO :TMENUITEM-MENU-NAME                                       
                   , :TMENUITEM-ITEM-MNEMONIC-CODE                              
                   , :TMENUITEM-DESC:IV-DESC                                    
                   , :TMENUITEM-SEQUENCE-NMBR                                   
                   , :TMENUITEM-LAST-UPDATE-TMESTP                              
                   , :TMENUITEM-IN-APLCTN-COMM-CDE                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   MOVE DCLTMENUITEM TO DP502-BACKOUT-DATA                      
                   WRITE BACKOUT-RECORD FROM DP502-BACKOUT-RECORD               
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < 0                                                 
               WHEN SQLCODE > 0                                                 
                   MOVE '2602-FETCH-TMENUITEM           '                       
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'FETCH ROW ON TMENUITEM TABLE   '                       
                       TO PV-DB2-OPERATION-ATTEMPTED                            
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2603-CLOSE-TMENUITEM-CURSOR.                                             
           EXEC SQL                                                             
               CLOSE TMENUITEM_CURSOR                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = 0                                                   
               MOVE '2603-CLOSE-TMENUITEM-CURSOR'                               
                   TO PV-PARAGRAPH-NAME                                         
               MOVE 'BAD CLOSE ON TMENUITEM CURSOR'                             
                   TO PV-DB2-OPERATION-ATTEMPTED                                
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      ** INSERT A ROW ONTO THE MENUITEM TABLE WHICH ADDS/REPLACES A AN**        
      ** ITEM ON A MENU.  NOTE THAT IF THE DESCRIPTION IS SPACES, THE **        
      ** NULL INDICATOR IS SET.  IN THE MENU ONLINE PROGRAM, A NULL   **        
      ** IN THIS FIELD INDICATES THE MENU ITEM DESCRIPTION SHOULD BE  **        
      ** THE SAME AS THE PANEL TITLE FOUND ON THE MNEMONIC TABLE.     **        
      ******************************************************************        
       3000-PROCESS-TMENUITEM-FILE.                                             
           IF  TMENUITEM-DESC = SPACE                                           
               MOVE -1    TO IV-DESC                                            
           ELSE                                                                 
               MOVE +1    TO IV-DESC                                            
           END-IF.                                                              
                                                                                
           IF  PV-HOLD-MENU-NAME NOT EQUAL TMENUITEM-MENU-NAME                  
               MOVE TMENUITEM-MENU-NAME                                         
                                     TO  PV-HOLD-MENU-NAME                      
               PERFORM 3500-ENSURE-MENU-EXISTS                                  
               IF  PS-MENU-DOESNT-EXIST                                         
                   SET PS-SET-RC-8 TO TRUE                                      
                   DISPLAY '==> ' TMENUITEM-MENU-NAME                           
                           ' MENU DOES NOT EXIST ON TMNEMONIC TABLE'            
                   PERFORM 3200-READ-MENUITEM-INPUT                             
                       UNTIL TMENUITEM-MENU-NAME NOT = PV-HOLD-MENU-NAME        
                        OR   PS-END-OF-TMENUITEM-FILE.                          
                                                                                
           IF PS-MENU-EXISTS                                                    
               MOVE TMENUITEM-ITEM-MNEMONIC-CODE                                
                                 TO  PV-HOLD-FKEY-NAME                          
               PERFORM 3550-ENSURE-FKEY-EXISTS                                  
               IF PS-FKEY-DOESNT-EXIST                                          
                   DISPLAY '==> ' TMENUITEM-ITEM-MNEMONIC-CODE                  
                           ' ITEM DOES NOT EXIST ON TMNEMONIC TABLE'            
                   PERFORM 3200-READ-MENUITEM-INPUT.                            
                                                                                
           IF  PS-MENU-EXISTS                                                   
           AND PS-FKEY-EXISTS                                                   
               PERFORM 3100-BEGIN-THE-UPDATE                                    
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      ** WE NOW BEGIN THE REAL UPDATE PROCESS.  WE HAVE ENSURED THAT  **        
      ** THE MENU NAME EXISTS, THEREFORE, UPDATE APPROPRIATELY.       **        
      ******************************************************************        
       3100-BEGIN-THE-UPDATE.                                                   
                                                                                
           PERFORM 3600-CHECK-SEQ-NUMBER.                                       
           PERFORM 3700-CREATE-MENUITEM-BACKOUT.                                
           PERFORM 3300-UPDATE-MENUITEM.                                        
           IF  SQLCODE = +100                                                   
               PERFORM 3400-INSERT-MENUITEM                                     
               MOVE TMENUITEM-SEQUENCE-NMBR TO PV-PRINT-SEQUENCE                
               DISPLAY 'MENUITEM: '                                             
                       TMENUITEM-ITEM-MNEMONIC-CODE                             
                       ' INSERTED ON MENU: '                                    
                       TMENUITEM-MENU-NAME                                      
                       ' SEQUENCE: '                                            
                       PV-PRINT-SEQUENCE                                        
           ELSE                                                                 
               MOVE TMENUITEM-SEQUENCE-NMBR TO PV-PRINT-SEQUENCE                
               DISPLAY 'MENUITEM: '                                             
                       TMENUITEM-ITEM-MNEMONIC-CODE                             
                       ' UPDATED ON MENU: '                                     
                       TMENUITEM-MENU-NAME                                      
                       ' SEQUENCE: '                                            
                       PV-PRINT-SEQUENCE                                        
           END-IF.                                                              
           PERFORM 3200-READ-MENUITEM-INPUT.                                    
                                                                                
                                                                                
       3200-READ-MENUITEM-INPUT.                                                
           READ TMENUITEM-EXT-FILE                                              
                INTO DCLTMENUITEM                                               
                AT END                                                          
                    SET PS-END-OF-TMENUITEM-FILE TO TRUE                        
           END-READ.                                                            
                                                                                
                                                                                
       3300-UPDATE-MENUITEM.                                                    
           EXEC SQL                                                             
               UPDATE TMENUITEM                                                 
               SET DESC                 = :TMENUITEM-DESC:IV-DESC,              
                   SEQUENCE_NMBR        = :TMENUITEM-SEQUENCE-NMBR,             
                   LAST_UPDATE_TMESTP   = CURRENT TIMESTAMP,                    
                   IN_APLCTN_COMM_CDE   = :TMENUITEM-IN-APLCTN-COMM-CDE         
               WHERE MENU_NAME          = :TMENUITEM-MENU-NAME                  
                 AND ITEM_MNEMONIC_CODE = :TMENUITEM-ITEM-MNEMONIC-CODE         
           END-EXEC.                                                            
                                                                                
           IF  ((SQLCODE = ZERO)                                                
             OR (SQLCODE = +100))                                               
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '3300-UPDATE-MENUITEM'                                      
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'BAD UPDATE INTO TMENUITEM TAB '                            
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       3400-INSERT-MENUITEM.                                                    
           EXEC SQL                                                             
               INSERT                                                           
                   INTO    TMENUITEM                                            
                          (MENU_NAME,                                           
                           ITEM_MNEMONIC_CODE,                                  
                           DESC,                                                
                           SEQUENCE_NMBR,                                       
                           LAST_UPDATE_TMESTP,                                  
                           IN_APLCTN_COMM_CDE)                                  
                   VALUES                                                       
                         (:TMENUITEM-MENU-NAME,                                 
                          :TMENUITEM-ITEM-MNEMONIC-CODE,                        
                          :TMENUITEM-DESC:IV-DESC,                              
                          :TMENUITEM-SEQUENCE-NMBR,                             
                          :TMENUITEM-LAST-UPDATE-TMESTP,                        
                          :TMENUITEM-IN-APLCTN-COMM-CDE)                        
           END-EXEC.                                                            
                                                                                
           IF  SQLCODE = ZERO                                                   
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '3400-INSERT-MENUITEM'                                      
                       TO PV-PARAGRAPH-NAME                                     
               MOVE 'BAD INSERT INTO TMENUITEM TAB '                            
                       TO PV-DB2-OPERATION-ATTEMPTED                            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       3500-ENSURE-MENU-EXISTS.                                                 
           EXEC SQL                                                             
                SELECT MNEMONIC_CODE                                            
                  INTO :PV-WORK-MENU-NAME                                       
                  FROM TMNEMONIC                                                
                 WHERE MNEMONIC_CODE = :PV-HOLD-MENU-NAME                       
           END-EXEC.                                                            
                                                                                
           IF  SQLCODE = ZERO                                                   
               SET PS-MENU-EXISTS    TO  TRUE                                   
           ELSE                                                                 
               IF  SQLCODE EQUAL +100                                           
                   SET PS-MENU-DOESNT-EXIST                                     
                                     TO  TRUE                                   
               ELSE                                                             
                   MOVE '3500-ENSURE-MENU-EXISTS'                               
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'BAD SELECT FROM TMNEMONIC TAB '                        
                       TO PV-DB2-OPERATION-ATTEMPTED                            
                   PERFORM 9100-SQL-ABEND                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
       3550-ENSURE-FKEY-EXISTS.                                                 
           EXEC SQL                                                             
                SELECT MNEMONIC_CODE                                            
                  INTO :PV-WORK-FKEY-NAME                                       
                  FROM TMNEMONIC                                                
                 WHERE MNEMONIC_CODE = :PV-HOLD-FKEY-NAME                       
           END-EXEC.                                                            
                                                                                
           IF  SQLCODE = ZERO                                                   
               SET PS-FKEY-EXISTS    TO  TRUE                                   
           ELSE                                                                 
               IF  SQLCODE EQUAL +100                                           
                   SET PS-FKEY-DOESNT-EXIST                                     
                                     TO  TRUE                                   
               ELSE                                                             
                   MOVE '3550-ENSURE-FKEY-EXISTS'                               
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'BAD SELECT FROM TMNEMONIC TAB '                        
                       TO PV-DB2-OPERATION-ATTEMPTED                            
                   PERFORM 9100-SQL-ABEND                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
       3600-CHECK-SEQ-NUMBER.                                                   
           EXEC SQL                                                             
                SELECT SEQUENCE_NMBR                                            
                  INTO :PV-WORK-SEQ-NUMBER                                      
                  FROM TMENUITEM                                                
                 WHERE MENU_NAME     = :TMENUITEM-MENU-NAME                     
                   AND SEQUENCE_NMBR = :TMENUITEM-SEQUENCE-NMBR                 
                   AND ITEM_MNEMONIC_CODE                                       
                                    ^= :TMENUITEM-ITEM-MNEMONIC-CODE            
           END-EXEC.                                                            
                                                                                
           IF  SQLCODE = ZERO                                                   
               ADD 1                 TO  TMENUITEM-SEQUENCE-NMBR                
               DISPLAY '==> CHANGED SEQUENCE NUMBER FOR '                       
                       TMENUITEM-MENU-NAME ' '                                  
                       TMENUITEM-ITEM-MNEMONIC-CODE                             
           ELSE                                                                 
               IF  SQLCODE EQUAL +100                                           
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE '3600-CHECK-SEQ-NUMBER'                                 
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'BAD SELECT FROM TMENUITEM TAB '                        
                       TO PV-DB2-OPERATION-ATTEMPTED                            
                   PERFORM 9100-SQL-ABEND                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
       3700-CREATE-MENUITEM-BACKOUT.                                            
           MOVE DCLTMENUITEM   TO PV-TEMP-HOLD-AREA.                            
           MOVE SPACES         TO DP502-BACKOUT-RECORD.                         
           SET  DP502-MENUITEM TO TRUE.                                         
                                                                                
           EXEC SQL                                                             
               SELECT DESC                                                      
                    , SEQUENCE_NMBR                                             
                    , LAST_UPDATE_TMESTP                                        
                    , IN_APLCTN_COMM_CDE                                        
                 INTO :TMENUITEM-DESC:IV-DESC                                   
                    , :TMENUITEM-SEQUENCE-NMBR                                  
                    , :TMENUITEM-LAST-UPDATE-TMESTP                             
                    , :TMENUITEM-IN-APLCTN-COMM-CDE                             
                 FROM TMENUITEM                                                 
                WHERE MENU_NAME          = :TMENUITEM-MENU-NAME                 
                  AND ITEM_MNEMONIC_CODE = :TMENUITEM-ITEM-MNEMONIC-CODE        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   SET DP502-CHANGE TO TRUE                                     
               WHEN SQLCODE = +100                                              
                   SET DP502-DELETE TO TRUE                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < 0                                                 
               WHEN SQLCODE > 0                                                 
                   MOVE '3700-CREATE-MENUITEM-BACKOUT   '                       
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'SELECT ROW ON TMENUITEM TABLE  '                       
                       TO PV-DB2-OPERATION-ATTEMPTED                            
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
           MOVE DCLTMENUITEM TO DP502-BACKOUT-DATA.                             
           WRITE BACKOUT-RECORD FROM DP502-BACKOUT-RECORD.                      
           MOVE PV-TEMP-HOLD-AREA TO DCLTMENUITEM.                              
                                                                                
                                                                                
      ******************************************************************        
      ** THIS MODULE WILL RELOAD ALL PFKEYS FOR A MNEMONIC (ONE ROW   **        
      ** IN CICSROUTE IS EQUIVALENT TO ONE PFKEY).  ALL PFKEYS FOR    **        
      ** A MNEMONIC ARE DELETED AND THEN RE-ADDED IN CASE ONE OR MORE **        
      ** OF THE PFKEYS FOR THE MNEMONIC BEING MOVED IS DELETED OR     **        
      ** CHANGED.                                                     **        
      ******************************************************************        
       4000-PROCESS-TCICSROUTE-FILE.                                            
           PERFORM 4400-CREATE-CICSROUTE-BACKOUT.                               
           PERFORM 4300-DELETE-CICSROUTE.                                       
           MOVE TCICSROUTE-MNEMONIC-CODE TO PV-HOLD-MNEMONIC.                   
           PERFORM                                                              
               UNTIL ((TCICSROUTE-MNEMONIC-CODE NOT = PV-HOLD-MNEMONIC)         
                  OR (PS-END-OF-TCICSROUTE-FILE))                               
               PERFORM 4200-INSERT-CICSROUTE                                    
               DISPLAY 'AID SEQUENCE NUMBER '                                   
                       TCICSROUTE-AID-SEQUENCE-NMBR                             
                       ' INSERTED ON MNEMONIC '                                 
                       TCICSROUTE-MNEMONIC-CODE                                 
               PERFORM 4100-READ-CICSROUTE-INPUT                                
           END-PERFORM.                                                         
           DISPLAY ' '.                                                         
                                                                                
                                                                                
       4100-READ-CICSROUTE-INPUT.                                               
                                                                                
           READ TCICSROUTE-EXT-FILE                                             
                INTO DCLTCICSROUTE                                              
                AT END                                                          
                    SET PS-END-OF-TCICSROUTE-FILE TO TRUE                       
           END-READ.                                                            
                                                                                
                                                                                
       4200-INSERT-CICSROUTE.                                                   
           EXEC SQL                                                             
               INSERT                                                           
                   INTO    TCICSROUTE                                           
                          (MNEMONIC_CODE,                                       
                           AID_SEQUENCE_NMBR,                                   
                           DEST_MNEMONIC_CODE,                                  
                           ACTION_CODE,                                         
                           LAST_UPDATE_TMESTP,                                  
                           ROUTE_STACK_INDCT,                                   
                           FUNC_KEY_LBL_NAME,                                   
                           IN_APLCTN_COMM_CDE,                                  
                           FUNC_KY_DISPN_IND)                                   
                   VALUES                                                       
                         (:TCICSROUTE-MNEMONIC-CODE,                            
                          :TCICSROUTE-AID-SEQUENCE-NMBR,                        
                          :TCICSROUTE-DEST-MNEMONIC-CODE,                       
                          :TCICSROUTE-ACTION-CODE,                              
                          :TCICSROUTE-LAST-UPDATE-TMESTP,                       
                          :TCICSROUTE-ROUTE-STACK-INDCT,                        
                          :TCICSROUTE-FUNC-KEY-LBL-NAME,                        
                          :TCICSROUTE-IN-APLCTN-COMM-CDE,                       
                          :TCICSROUTE-FUNC-KY-DISPN-IND)                        
           END-EXEC.                                                            
                                                                                
           IF  SQLCODE = ZERO                                                   
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '4200-INSERT-CICSROUTE'                                     
                       TO PV-PARAGRAPH-NAME                                     
               MOVE 'BAD INSERT INTO TCICSROUTE TAB '                           
                       TO PV-DB2-OPERATION-ATTEMPTED                            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       4300-DELETE-CICSROUTE.                                                   
           EXEC SQL                                                             
               DELETE FROM TCICSROUTE                                           
               WHERE MNEMONIC_CODE    = :TCICSROUTE-MNEMONIC-CODE               
           END-EXEC.                                                            
                                                                                
           IF  ((SQLCODE = ZERO)                                                
             OR (SQLCODE = +100))                                               
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '4300-DELETE-CICSROUTE'                                     
                       TO PV-PARAGRAPH-NAME                                     
               MOVE 'BAD DELETE FROM TCICSROUTE TAB '                           
                       TO PV-DB2-OPERATION-ATTEMPTED                            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       4400-CREATE-CICSROUTE-BACKOUT.                                           
           SET  DP502-CICSROUTE TO TRUE.                                        
           SET  DP502-DELETE    TO TRUE.                                        
           MOVE DCLTCICSROUTE   TO DP502-BACKOUT-DATA.                          
           WRITE BACKOUT-RECORD FROM DP502-BACKOUT-RECORD.                      
                                                                                
           MOVE DCLTCICSROUTE   TO PV-TEMP-HOLD-AREA.                           
           MOVE SPACES          TO DP502-BACKOUT-RECORD.                        
           SET  DP502-ADD       TO TRUE.                                        
           SET  DP502-CICSROUTE TO TRUE.                                        
                                                                                
           PERFORM 4401-OPEN-TCICSROUTE-CURSOR.                                 
                                                                                
           PERFORM WITH TEST AFTER                                              
             UNTIL SQLCODE = +100                                               
               PERFORM 4402-FETCH-TCICSROUTE                                    
           END-PERFORM.                                                         
                                                                                
           PERFORM 4403-CLOSE-TCICSROUTE-CURSOR.                                
                                                                                
           MOVE PV-TEMP-HOLD-AREA TO DCLTCICSROUTE.                             
                                                                                
                                                                                
       4401-OPEN-TCICSROUTE-CURSOR.                                             
           EXEC SQL                                                             
               OPEN TCICSROUTE_CURSOR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < 0                                                 
               WHEN SQLCODE > 0                                                 
                   MOVE '4401-OPEN-TCICSROUTE-CURSOR'                           
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'OPEN TCICSROUTE CURSOR'                                
                        TO PV-DB2-OPERATION-ATTEMPTED                           
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4402-FETCH-TCICSROUTE.                                                   
           EXEC SQL                                                             
               FETCH TCICSROUTE_CURSOR                                          
                   INTO :TCICSROUTE-AID-SEQUENCE-NMBR                           
                      , :TCICSROUTE-DEST-MNEMONIC-CODE                          
                      , :TCICSROUTE-ACTION-CODE                                 
                      , :TCICSROUTE-LAST-UPDATE-TMESTP                          
                      , :TCICSROUTE-ROUTE-STACK-INDCT                           
                      , :TCICSROUTE-FUNC-KEY-LBL-NAME                           
                      , :TCICSROUTE-IN-APLCTN-COMM-CDE                          
                      , :TCICSROUTE-FUNC-KY-DISPN-IND                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   MOVE DCLTCICSROUTE TO DP502-BACKOUT-DATA                     
                   WRITE BACKOUT-RECORD FROM DP502-BACKOUT-RECORD               
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < 0                                                 
               WHEN SQLCODE > 0                                                 
                   MOVE '4402-FETCH-TCICSROUTE          '                       
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'FETCH ROW ON TCICSROUTE TABLE  '                       
                       TO PV-DB2-OPERATION-ATTEMPTED                            
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4403-CLOSE-TCICSROUTE-CURSOR.                                            
           EXEC SQL                                                             
               CLOSE TCICSROUTE_CURSOR                                          
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = 0                                                   
               MOVE '4403-CLOSE-TCICSROUTE-CURSOR'                              
                   TO PV-PARAGRAPH-NAME                                         
               MOVE 'BAD CLOSE ON TCICSROUTE CURSOR'                            
                   TO PV-DB2-OPERATION-ATTEMPTED                                
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       9000-EOJ-ROUTINE.                                                        
           IF  PS-SET-RC-8                                                      
               MOVE +0008    TO RETURN-CODE                                     
               DISPLAY '****************************************'               
               DISPLAY '** AN ERROR OCCURRED WHILE UPDATING   **'               
               DISPLAY '** MNEMONIC(S) TO THE TARGET TABLES.  **'               
               DISPLAY '** SEE PREVIOUS MESSAGES FOR DETAILS. **'               
               DISPLAY '****************************************'               
           ELSE                                                                 
               MOVE +0000    TO RETURN-CODE                                     
               DISPLAY '****************************************'               
               DISPLAY '** ALL MNEMONICS WERE SUCCESSFULLY    **'               
               DISPLAY '** UPDATED ON THE TARGET TABLES.      **'               
               DISPLAY '****************************************'               
           END-IF.                                                              
      *                                                                         
           CLOSE       TMNEMONIC-EXT-FILE                                       
                       TMENUITEM-EXT-FILE                                       
                       TCICSROUTE-EXT-FILE                                      
                       BACKOUT-FILE.                                            
                                                                                
                                                                                
      ******************************************************************        
      **                                                              **        
      ******************************************************************        
       9100-SQL-ABEND.                                                          
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = DPKUP002'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
