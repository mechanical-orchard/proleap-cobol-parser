000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    RCKUT068.                                                 
000600 AUTHOR.        DENISE HAHN                                               
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   JULY, 1999.                                               
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM WILL CREATE A TIMESTAMP CONTROL FILE USED                 
001310*  TO CREATE SUMMARY ROWS IN THE LABEL TRACKING SUMMARY (TLBTKSM)         
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900                                                                          
002901     SELECT TIMESTAMP-FILE                                                
002902         ASSIGN TO OUT01.                                                 
002910                                                                          
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700                                                                          
003800                                                                          
003801 FD  TIMESTAMP-FILE                                                       
003802     RECORDING MODE IS F                                                  
003803     LABEL RECORDS ARE STANDARD                                           
003804     BLOCK CONTAINS 0 RECORDS.                                            
003805                                                                          
003810 01  TIMESTAMP-RECORD           PIC  X(50).                               
003900                                                                          
003901******************************************************************        
003910 WORKING-STORAGE SECTION.                                                 
004000******************************************************************        
004010*-----------------------------------------------------------------        
004020*                                                                         
004030*-----------------------------------------------------------------        
004040 01  ABEND-CODE                     PIC S9(04) VALUE +0                   
004050                                               COMP SYNC.                 
004060                                                                          
004061*-----------------------------------------------------------------        
004062* PROGRAM CONSTANTS                                                       
004063*-----------------------------------------------------------------        
004070 01  PC-PROGRAM-CONSTANTS.                                                
004080     05  PC-CURRENT-PROGRAM-NAME    PIC  X(08) VALUE                      
004090         'RCKUT068'.                                                      
004091     05  PC-USER-ID-LABEL           PIC  X(03) VALUE 'UID'.               
004092                                                                          
004093*-----------------------------------------------------------------        
004094* PROGRAM SWITCHES                                                        
004095*-----------------------------------------------------------------        
004096 01  PS-PROGRAM-SWITCHES.                                                 
004097     05  PS-WEEKS-TO-SUMMARIZE-SW   PIC  X(01) VALUE 'Y'.                 
004098         88  PS-MORE-WEEKS-TO-SUM              VALUE 'Y'.                 
004099         88  PS-NO-MORE-WEEKS-TO-SUM           VALUE 'N'.                 
004100     05  PS-LABEL-HISTORY-CSR-SW    PIC  X(01) VALUE 'N'.                 
004101         88  PS-EOF-HIST-CURSOR                VALUE 'Y'.                 
004102         88  PS-NOT-EOF-HIST-CURSOR            VALUE 'N'.                 
004103     05  PS-LABEL-USERID-CSR-SW     PIC  X(01) VALUE 'N'.                 
004110         88  PS-EOF-USER-CURSOR                VALUE 'Y'.                 
004120         88  PS-NOT-EOF-USER-CURSOR            VALUE 'N'.                 
004130                                                                          
004131*-----------------------------------------------------------------        
004132* PROGRAM VARIABLES                                                       
004133*-----------------------------------------------------------------        
004140 01  PV-PROGRAM-VARIABLES.                                                
004150     05  PV-CURRENT-DATE            PIC X(10).                            
004160     05  PV-PROCESS-DATE            PIC X(10).                            
004170     05  PV-CURRENT-SATURDAY        PIC X(10).                            
004180     05  PV-DELETE-FROM             PIC X(10).                            
004190     05  PV-DELETE-THRU             PIC X(10).                            
004191     05  PV-DAYS-BACK-FROM          PIC 9(05)  VALUE ZEROES.              
004192     05  PV-DAYS-BACK-THRU          PIC 9(05)  VALUE ZEROES.              
004193     05  PV-DB2-DELETE-COUNT        PIC S9(09) VALUE ZEROES               
004194                                               COMP.                      
004195                                                                          
004196     05  PV-CURRENT-PARAGRAPH       PIC X(30)  VALUE SPACES.              
004197     05  PV-PARAGRAPH-NAME          PIC  X(30) VALUE SPACES.              
004198     05  PV-DB2-OPERATION-ATTEMPTED PIC  X(30) VALUE SPACES.              
004199                                                                          
004200     05  PV-WEEK-DAY                PIC  X(09) VALUE SPACES.              
004210         88  PV-WEEK-DAY-SUNDAY                VALUE 'SUNDAY   '.         
004220         88  PV-WEEK-DAY-MONDAY                VALUE 'MONDAY   '.         
004230         88  PV-WEEK-DAY-TUESDAY               VALUE 'TUESDAY  '.         
004240         88  PV-WEEK-DAY-WEDNESDAY             VALUE 'WEDNESDAY'.         
004250         88  PV-WEEK-DAY-THRUSDAY              VALUE 'THRUSDAY '.         
004260         88  PV-WEEK-DAY-FRIDAY                VALUE 'FRIDAY   '.         
004270         88  PV-WEEK-DAY-SATURDAY              VALUE 'SATURDAY '.         
004280                                                                          
004290                                                                          
004291*----------------------------------------------------------------*        
004292* WEEKS TO SUMMARIZE TABLE                                       *        
004293*----------------------------------------------------------------*        
004294 01  PT-MAX-LBL-TABLE-ENTRIES      PIC S9(04)   VALUE +20                 
004295                                                COMP  SYNC.               
004296 01  PT-ACTUAL-TBL-ENTRIES         PIC S9(04)   VALUE ZEROES              
004297                                                COMP  SYNC.               
004298 01  PT-TABLE-FIELDS.                                                     
004299     05  PT-WEEK-START             PIC X(10)    VALUE SPACES.             
004300     05  FILLER                    PIC X(01)    VALUE SPACE.              
004310     05  PT-WEEK-END               PIC X(10)    VALUE SPACES.             
004320                                                                          
004330 01  PT-WEEK-SUMMARY-TABLE.                                               
004340     05  FILLER                    PIC X(21)    VALUE SPACES.             
004350     05  FILLER                    PIC X(21)    VALUE SPACES.             
004360     05  FILLER                    PIC X(21)    VALUE SPACES.             
004370     05  FILLER                    PIC X(21)    VALUE SPACES.             
004380     05  FILLER                    PIC X(21)    VALUE SPACES.             
004390     05  FILLER                    PIC X(21)    VALUE SPACES.             
004391     05  FILLER                    PIC X(21)    VALUE SPACES.             
004392     05  FILLER                    PIC X(21)    VALUE SPACES.             
004393     05  FILLER                    PIC X(21)    VALUE SPACES.             
004394     05  FILLER                    PIC X(21)    VALUE SPACES.             
004395     05  FILLER                    PIC X(21)    VALUE SPACES.             
004396     05  FILLER                    PIC X(21)    VALUE SPACES.             
004397     05  FILLER                    PIC X(21)    VALUE SPACES.             
004398     05  FILLER                    PIC X(21)    VALUE SPACES.             
004399     05  FILLER                    PIC X(21)    VALUE SPACES.             
004400     05  FILLER                    PIC X(21)    VALUE SPACES.             
004410     05  FILLER                    PIC X(21)    VALUE SPACES.             
004420     05  FILLER                    PIC X(21)    VALUE SPACES.             
004430     05  FILLER                    PIC X(21)    VALUE SPACES.             
004440     05  FILLER                    PIC X(21)    VALUE SPACES.             
004450*                                                                         
004460 01  PT-WEEK-SUMMARY-TABLE-ENTRIES REDEFINES                              
004470     PT-WEEK-SUMMARY-TABLE.                                               
004480     05  PT-WEEK-START-END         OCCURS 21 TIMES                        
004490                                   INDEXED BY PT-WEEK-IDX                 
004491                                   PIC X(21).                             
004496                                                                          
004500                                                                          
005800*-----------------------------------------------------------------        
005900*  RECORD LAYOUT FOR THE DATE FILE                                        
005910*-----------------------------------------------------------------        
006100     COPY RCRD049.                                                        
006200                                                                          
006300*-----------------------------------------------------------------        
006600*  DB2 FORMATTED MESSAGE AREA                                             
006610*-----------------------------------------------------------------        
006800     COPY DPWS004.                                                        
007101                                                                          
007102*-----------------------------------------------------------------        
007103*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE PROCESS CONTROL            
007104*  TABLE                                                                  
007105*-----------------------------------------------------------------        
007106     EXEC SQL                                                             
007107          INCLUDE TCOCNTL                                                 
007108     END-EXEC.                                                            
007109                                                                          
007110*-----------------------------------------------------------------        
007111*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE LABEL TRACKING             
007112*  HISTORY TABLE                                                          
007113*-----------------------------------------------------------------        
007114     EXEC SQL                                                             
007115          INCLUDE TLBTKHS                                                 
007116     END-EXEC.                                                            
007117                                                                          
007118*-----------------------------------------------------------------        
007119*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE LABEL TRACKING             
007120*  IDENTIFICATION TABLE                                                   
007121*-----------------------------------------------------------------        
007122     EXEC SQL                                                             
007123          INCLUDE TLBTKID                                                 
007124     END-EXEC.                                                            
007125                                                                          
007126*-----------------------------------------------------------------        
007127*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE LABEL TRACKING             
007128*  "SUMMARY" TABLE                                                        
007129*-----------------------------------------------------------------        
007130     EXEC SQL                                                             
007131          INCLUDE TLBTKSM                                                 
007132     END-EXEC.                                                            
007133                                                                          
007134*-----------------------------------------------------------------        
007135* LABEL TRACKING HISTORY CURSOR - THIS WILL RETRIEVE ALL ROWS             
007136* WITHIN A GIVEN DATE RANGE. --- THIS WILL NOT PULL USERID LABELS         
007137*-----------------------------------------------------------------        
007138     EXEC SQL                                                             
007139          DECLARE  HISTORY_CURSOR CURSOR FOR                              
007140           SELECT DISTINCT                                                
007141                  A.PRTR_OPER_UNT_ID                                      
007142                , A.PRTR_FCLTY_ID                                         
007143                , A.PRTR_ID                                               
007144                , B.LBL_TYP_CDE                                           
007145                , SUM(B.LBL_PRT_QTY)                                      
007146                , SUM(B.SEPOR_LBL_PRT_QTY)                                
007147             FROM  TLBTKHS A                                              
007148                ,  TLBTKID B                                              
007149            WHERE DATE(CRE_TMST)    >= :PT-WEEK-START                     
007150              AND DATE(CRE_TMST)    <= :PT-WEEK-END                       
007151              AND A.LBL_TYP_CDE     ^=  'UID'                             
007152              AND A.LBL_REQ_TRN_ID   =  B.LBL_REQ_TRN_ID                  
007153            GROUP BY  A.PRTR_OPER_UNT_ID                                  
007154                     ,A.PRTR_FCLTY_ID                                     
007155                     ,A.PRTR_ID                                           
007156                     ,B.LBL_TYP_CDE                                       
007157     END-EXEC.                                                            
007158                                                                          
007159                                                                          
007160*-----------------------------------------------------------------        
007161* LABEL TRACKING HISTORY USERID CURSOR - THIS WILL RETRIEVE ALL           
007162* USERID ROWS WITHIN A GIVEN DATE RANGE                                   
007163*-----------------------------------------------------------------        
007164     EXEC SQL                                                             
007165          DECLARE  USERID_CURSOR CURSOR FOR                               
007166           SELECT LBL_TYP_CDE                                             
007167                , SUM(LBL_PRT_QTY)                                        
007168                , PRTR_ID                                                 
007169                , PRTR_OPER_UNT_ID                                        
007170                , PRTR_FCLTY_ID                                           
007171             FROM  TLBTKHS                                                
007172            WHERE DATE(CRE_TMST)  >= :PT-WEEK-START                       
007173              AND DATE(CRE_TMST)  <= :PT-WEEK-END                         
007174              AND LBL_TYP_CDE      = :PC-USER-ID-LABEL                    
007175            GROUP BY  PRTR_OPER_UNT_ID                                    
007176                     ,PRTR_FCLTY_ID                                       
007177                     ,PRTR_ID                                             
007178                     ,LBL_TYP_CDE                                         
007179     END-EXEC.                                                            
007180                                                                          
007181                                                                          
007190*-----------------------------------------------------------------        
007300*  COMMUNICATIONS AREA FOR DB2                                            
007310*-----------------------------------------------------------------        
007500     EXEC SQL                                                             
007600          INCLUDE SQLCA                                                   
007700     END-EXEC.                                                            
007800                                                                          
012994                                                                          
012995*-----------------------------------------------------------------        
012996*  WORK AREA FOR DPKUT500 DATE ROUTINE                                    
012997*-----------------------------------------------------------------        
012998     COPY DPWS500.                                                        
013000                                                                          
013600                                                                          
013700******************************************************************        
013800 PROCEDURE DIVISION.                                                      
013900******************************************************************        
014000                                                                          
014010                                                                          
014020*-----------------------------------------------------------------        
014030* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
014040*      PROGRAM.                                                           
014050*-----------------------------------------------------------------        
014100 0000-PROGRAM-MAINLINE.                                                   
014600                                                                          
014700     PERFORM 1000-INITIALIZE.                                             
014710                                                                          
014711     DISPLAY ' MAX TABLE ENTRIES - ' PT-MAX-LBL-TABLE-ENTRIES.            
014712     DISPLAY ' ACTUAL ENTRIES  --- ' PT-ACTUAL-TBL-ENTRIES.               
014720                                                                          
014800     PERFORM 2000-SUMMARY-PROCESS                                         
015000             VARYING PT-WEEK-IDX  FROM 1 BY 1                             
015100               UNTIL PT-WEEK-IDX  > PT-ACTUAL-TBL-ENTRIES                 
015110                  OR PT-WEEK-IDX  > PT-MAX-LBL-TABLE-ENTRIES.             
015120                                                                          
015200     PERFORM 9000-EOJ-ROUTINE.                                            
015300                                                                          
015400     STOP RUN.                                                            
015500                                                                          
015501                                                                          
015510*-----------------------------------------------------------------        
015520*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
015530*-----------------------------------------------------------------        
015600 1000-INITIALIZE.                                                         
016200                                                                          
016300     OPEN OUTPUT TIMESTAMP-FILE.                                          
016310     PERFORM 1100-CREATE-CONTROL-CARD.                                    
016500                                                                          
016501                                                                          
016502*-----------------------------------------------------------------        
016503*  CREATE THE CONTROL CARD.                                               
016504*-----------------------------------------------------------------        
016505 1100-CREATE-CONTROL-CARD.                                                
016508                                                                          
016509     EXEC SQL                                                             
016510          SELECT DISTINCT                                                 
016512                 DATE(BTCH_PRC_DTE)                                       
016513            INTO                                                          
016515                 :PV-CURRENT-DATE                                         
016516            FROM                                                          
016517                 TCOCNTL                                                  
016518     END-EXEC.                                                            
016519                                                                          
016520     EVALUATE TRUE                                                        
016521         WHEN SQLCODE = ZERO                                              
016522             CONTINUE                                                     
016533                                                                          
016534         WHEN SQLWARN0 NOT = SPACES                                       
016535         WHEN SQLCODE < ZERO                                              
016536         WHEN SQLCODE > ZERO                                              
016537             MOVE '1100-CREATE CONTROL CARD'                              
016538                                 TO PV-PARAGRAPH-NAME                     
016539             MOVE 'ATTEMPTING TO SELECT CURRENT TIMESTAMP                 
016540-                 'FROM TCOCNTL'                                          
016541                                 TO PV-DB2-OPERATION-ATTEMPTED            
016542             PERFORM 9100-SQL-ABEND                                       
016543     END-EVALUATE.                                                        
016544                                                                          
016545     PERFORM 1200-DETERMINE-SATURDAY.                                     
016546     MOVE  21  TO  PV-DAYS-BACK-THRU                                      
016547     MOVE  27  TO  PV-DAYS-BACK-FROM                                      
016549     SET  PS-MORE-WEEKS-TO-SUM  TO  TRUE.                                 
016550                                                                          
016551     INITIALIZE  PT-ACTUAL-TBL-ENTRIES.                                   
016552                                                                          
016553     PERFORM 1250-4-WEEKS-PRIOR                                           
016554             VARYING PT-WEEK-IDX  FROM 1 BY 1                             
016555               UNTIL PS-NO-MORE-WEEKS-TO-SUM                              
016556                  OR PT-WEEK-IDX > PT-MAX-LBL-TABLE-ENTRIES.              
016558                                                                          
016559     INITIALIZE RC049-CONTROL-RECORD.                                     
016560     MOVE  PT-WEEK-START-END(1)  TO  PT-TABLE-FIELDS.                     
016561     MOVE  PT-WEEK-END           TO  RC049-CONTROL-DATE.                  
016562                                                                          
016567     DISPLAY ' RCKUT068 CONTROL CARD -- ' RC049-CONTROL-DATE.             
016568     PERFORM 8100-WRITE-TIMESTAMP-RECORD.                                 
016569                                                                          
016570                                                                          
016571*-----------------------------------------------------------------        
016572*  DETERMINE IF CURRENT DATE IS A SATURDAY - IF NOT FIND THE DATE         
016573*  FOR THE PRIOR SATURDAY                                                 
016574*-----------------------------------------------------------------        
016575 1200-DETERMINE-SATURDAY.                                                 
016576                                                                          
016577     MOVE  '1200-DETERMINE-SATURDAY'  TO  PV-CURRENT-PARAGRAPH.           
016578     INITIALIZE  DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                     
016579                                                                          
016580     DISPLAY 'CURRENT DATE  = '  PV-CURRENT-DATE.                         
016581     MOVE PV-CURRENT-DATE   TO  DPG52-LK-DATE-INPUT.                      
016582                                                                          
016583     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
016584         DPG51-DO-NOT-INCR-DECR-DATE                                      
016585         DPG52-LK-DTE-ISO-FMT  TO  TRUE.                                  
016586                                                                          
016587     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
016588         USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
016589                                                                          
016590     EVALUATE TRUE                                                        
016591         WHEN DPG54-WARNING-ERROR                                         
016592           DISPLAY '** ABEND **'                                          
016593           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016594           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016596           DISPLAY '** INVALID DATE SUPPLIED'                             
016597           DISPLAY '** DATE SUPPLIED = ' PV-CURRENT-DATE                  
016598           DISPLAY '** WARNING ERROR IN CALL TO DPKUT500'                 
016599           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016601           MOVE +4003 TO ABEND-CODE                                       
016602           CALL 'ILBOABN0' USING ABEND-CODE                               
016603         WHEN DPG54-SEVERE-ERROR                                          
016604           DISPLAY '** ABEND **'                                          
016605           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016606           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016607           DISPLAY '** ERROR IN CALL TO DPKUT500'                         
016608           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016609           MOVE +4003 TO ABEND-CODE                                       
016610           CALL 'ILBOABN0' USING ABEND-CODE                               
016611         WHEN DPG54-NO-ERROR                                              
016612           CONTINUE                                                       
016613     END-EVALUATE.                                                        
016614                                                                          
016615     IF  NOT DPG55-ALPHA-WEEK-DAY-SATURDAY                                
016616         PERFORM  1220-FIND-SATURDAY                                      
016617           UNTIL  DPG55-ALPHA-WEEK-DAY-SATURDAY                           
016620     END-IF.                                                              
016627                                                                          
016628                                                                          
016629 1220-FIND-SATURDAY.                                                      
016630                                                                          
016631     MOVE  DPG55-ALPHA-WEEK-DAY  TO  PV-WEEK-DAY.                         
016632                                                                          
016633     MOVE  '1220-FIND-SATURDAY'  TO  PV-CURRENT-PARAGRAPH.                
016634     INITIALIZE  DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                     
016635                                                                          
016636     MOVE PV-CURRENT-DATE   TO  DPG52-LK-DATE-INPUT.                      
016637                                                                          
016638     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
016639         DPG51-DECREMENT-DATE                                             
016640         DPG52-LK-DTE-ISO-FMT  TO  TRUE.                                  
016641                                                                          
016642     IF  PV-WEEK-DAY-SUNDAY                                               
016643         MOVE  1               TO  DPG51-INCR-DECR-DAYS-9                 
016644     ELSE                                                                 
016645     IF  PV-WEEK-DAY-MONDAY                                               
016646         MOVE  2               TO  DPG51-INCR-DECR-DAYS-9                 
016647     ELSE                                                                 
016648     IF  PV-WEEK-DAY-TUESDAY                                              
016649         MOVE  3               TO  DPG51-INCR-DECR-DAYS-9                 
016650     ELSE                                                                 
016651     IF  PV-WEEK-DAY-WEDNESDAY                                            
016652         MOVE  4               TO  DPG51-INCR-DECR-DAYS-9                 
016653     ELSE                                                                 
016654     IF  PV-WEEK-DAY-THRUSDAY                                             
016655         MOVE  5               TO  DPG51-INCR-DECR-DAYS-9                 
016656     ELSE                                                                 
016657     IF  PV-WEEK-DAY-FRIDAY                                               
016658         MOVE  6               TO  DPG51-INCR-DECR-DAYS-9                 
016659     END-IF                                                               
016660     END-IF                                                               
016661     END-IF                                                               
016662     END-IF                                                               
016663     END-IF                                                               
016664     END-IF.                                                              
016666                                                                          
016667     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
016668         USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
016669                                                                          
016670     EVALUATE TRUE                                                        
016671         WHEN DPG54-WARNING-ERROR                                         
016672           DISPLAY '** ABEND **'                                          
016673           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016674           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016675           DISPLAY '** INVALID DATE SUPPLIED'                             
016676           DISPLAY '** DATE SUPPLIED = ' PV-CURRENT-DATE                  
016677           DISPLAY '** WARNING ERROR IN CALL TO DPKUT500'                 
016678           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016679           MOVE +4003 TO ABEND-CODE                                       
016680           CALL 'ILBOABN0' USING ABEND-CODE                               
016681         WHEN DPG54-SEVERE-ERROR                                          
016682           DISPLAY '** ABEND **'                                          
016683           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016684           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016685           DISPLAY '** ERROR IN CALL TO DPKUT500'                         
016686           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016687           MOVE +4003 TO ABEND-CODE                                       
016688           CALL 'ILBOABN0' USING ABEND-CODE                               
016689         WHEN DPG54-NO-ERROR                                              
016690           CONTINUE                                                       
016691     END-EVALUATE.                                                        
016693                                                                          
016694     DISPLAY 'SATURDAY DATE = '  DPG55-DB2-ISO-DATE-FMT.                  
016695     MOVE  DPG55-DB2-ISO-DATE-FMT  TO  PV-CURRENT-SATURDAY.               
016696                                                                          
016697                                                                          
016698*-----------------------------------------------------------------        
016699* KEEP 4 WEEKS OF DATA ON HISTORY TABLE DETERMINE WEEKENDING DATES        
016700*-----------------------------------------------------------------        
016701 1250-4-WEEKS-PRIOR.                                                      
016702                                                                          
016703     MOVE  '1250-4-WEEKS-PRIOR'  TO  PV-CURRENT-PARAGRAPH.                
016704     INITIALIZE  DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                     
016705                                                                          
016706     MOVE PV-CURRENT-SATURDAY   TO  DPG52-LK-DATE-INPUT.                  
016707                                                                          
016708     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
016709         DPG51-DECREMENT-DATE                                             
016710         DPG52-LK-DTE-ISO-FMT  TO  TRUE.                                  
016711                                                                          
016712     ADD   7                   TO  PV-DAYS-BACK-THRU                      
016713     MOVE  PV-DAYS-BACK-THRU   TO  DPG51-INCR-DECR-DAYS-9.                
016714                                                                          
016715     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
016716         USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
016718                                                                          
016719     EVALUATE TRUE                                                        
016720         WHEN DPG54-WARNING-ERROR                                         
016721           DISPLAY '** ABEND **'                                          
016722           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016723           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016724           DISPLAY '** INVALID DATE SUPPLIED'                             
016725           DISPLAY '** DATE SUPPLIED = ' PV-CURRENT-DATE                  
016726           DISPLAY '** WARNING ERROR IN CALL TO DPKUT500'                 
016727           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016728           MOVE +4003 TO ABEND-CODE                                       
016729           CALL 'ILBOABN0' USING ABEND-CODE                               
016730         WHEN DPG54-SEVERE-ERROR                                          
016731           DISPLAY '** ABEND **'                                          
016732           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016733           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016734           DISPLAY '** ERROR IN CALL TO DPKUT500'                         
016735           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016736           MOVE +4003 TO ABEND-CODE                                       
016737           CALL 'ILBOABN0' USING ABEND-CODE                               
016738         WHEN DPG54-NO-ERROR                                              
016739           CONTINUE                                                       
016740     END-EVALUATE.                                                        
016741                                                                          
016742     MOVE  DPG55-DB2-ISO-DATE-FMT  TO  PV-DELETE-THRU.                    
016743                                                                          
016744     INITIALIZE  DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                     
016745                                                                          
016746     MOVE PV-CURRENT-SATURDAY   TO  DPG52-LK-DATE-INPUT.                  
016747                                                                          
016748     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
016749         DPG51-DECREMENT-DATE                                             
016750         DPG52-LK-DTE-ISO-FMT  TO  TRUE.                                  
016751                                                                          
016752     ADD   7                   TO  PV-DAYS-BACK-FROM                      
016753     MOVE  PV-DAYS-BACK-FROM   TO  DPG51-INCR-DECR-DAYS-9.                
016754                                                                          
016755     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
016756         USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
016757                                                                          
016758     EVALUATE TRUE                                                        
016759         WHEN DPG54-WARNING-ERROR                                         
016760           DISPLAY '** ABEND **'                                          
016761           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016762           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016763           DISPLAY '** INVALID DATE SUPPLIED'                             
016764           DISPLAY '** DATE SUPPLIED = ' PV-CURRENT-DATE                  
016765           DISPLAY '** WARNING ERROR IN CALL TO DPKUT500'                 
016766           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016767           MOVE +4003 TO ABEND-CODE                                       
016768           CALL 'ILBOABN0' USING ABEND-CODE                               
016769         WHEN DPG54-SEVERE-ERROR                                          
016770           DISPLAY '** ABEND **'                                          
016771           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME           
016772           DISPLAY '** PARAGRAPH ** = ' PV-CURRENT-PARAGRAPH              
016773           DISPLAY '** ERROR IN CALL TO DPKUT500'                         
016774           DISPLAY '** ' DPG54-ERROR-MESSAGE                              
016775           MOVE +4003 TO ABEND-CODE                                       
016776           CALL 'ILBOABN0' USING ABEND-CODE                               
016777         WHEN DPG54-NO-ERROR                                              
016778           CONTINUE                                                       
016779     END-EVALUATE.                                                        
016780                                                                          
016781     MOVE  DPG55-DB2-ISO-DATE-FMT  TO  PV-DELETE-FROM.                    
016782                                                                          
016783     MOVE  PV-DELETE-FROM   TO  PT-WEEK-START.                            
016784     MOVE  PV-DELETE-THRU   TO  PT-WEEK-END.                              
016785     MOVE  PT-TABLE-FIELDS  TO  PT-WEEK-START-END(PT-WEEK-IDX).           
016786     DISPLAY 'DELETE TABLE DATE RANGE ENTRY = '                           
016787              PT-WEEK-START-END(PT-WEEK-IDX).                             
016788                                                                          
016789     SET  PT-ACTUAL-TBL-ENTRIES  TO  PT-WEEK-IDX.                         
016790     PERFORM 1260-CHECK-MORE-WEEKS.                                       
016791                                                                          
016792                                                                          
016793*-----------------------------------------------------------------        
016794* CHECK TO SEE IF THERE ARE MORE WEEKS TO SUMMARIZE                       
016795*-----------------------------------------------------------------        
016796 1260-CHECK-MORE-WEEKS.                                                   
016797                                                                          
016798     EXEC SQL                                                             
016799          SELECT  COUNT(*)                                                
016800            INTO  :PV-DB2-DELETE-COUNT                                    
016801            FROM  TLBTKHS                                                 
016802           WHERE  DATE(CRE_TMST) < :PV-DELETE-FROM                        
016803     END-EXEC.                                                            
016804                                                                          
016805     EVALUATE TRUE                                                        
016806         WHEN SQLCODE = ZERO                                              
016807              IF  PV-DB2-DELETE-COUNT  >  0                               
016808                  CONTINUE                                                
016809              ELSE                                                        
016810                  SET  PS-NO-MORE-WEEKS-TO-SUM  TO  TRUE                  
016811              END-IF                                                      
016812                                                                          
016813         WHEN SQLWARN0 NOT = SPACES                                       
016814         WHEN SQLCODE < ZERO                                              
016815         WHEN SQLCODE > ZERO                                              
016816              SET  PS-NO-MORE-WEEKS-TO-SUM  TO  TRUE                      
016817              MOVE '1260-CHECK-MORE-WEEKS'                                
016818                                  TO PV-PARAGRAPH-NAME                    
016819              MOVE 'ATTEMPTING TO SELECT CURRENT TIMESTAMP                
016820-                  'FROM TCOCNTL'                                         
016821                                 TO PV-DB2-OPERATION-ATTEMPTED            
016822              PERFORM 9100-SQL-ABEND                                      
016823     END-EVALUATE.                                                        
016824                                                                          
016825                                                                          
016826*-----------------------------------------------------------------        
016827*  CREATE HISTORY SUMMARY RECORD PROCESS                                  
016828*-----------------------------------------------------------------        
016829 2000-SUMMARY-PROCESS.                                                    
016830                                                                          
016831     MOVE  PT-WEEK-START-END(PT-WEEK-IDX)  TO  PT-TABLE-FIELDS.           
016832                                                                          
016833     PERFORM 3000-OPEN-HISTORY-CSR.                                       
016834     PERFORM 3010-FETCH-HISTORY-CSR.                                      
016835                                                                          
016836     PERFORM 3050-PROCESS-HISTORY-CSR                                     
016837       UNTIL PS-EOF-HIST-CURSOR.                                          
016838                                                                          
016839     PERFORM 3020-CLOSE-HISTORY-CSR.                                      
016840                                                                          
016841                                                                          
016842     PERFORM 3100-OPEN-USERID-CSR.                                        
016843     PERFORM 3110-FETCH-USERID-CSR.                                       
016844                                                                          
016845     PERFORM 3150-PROCESS-USERID-CSR                                      
016846       UNTIL PS-EOF-USER-CURSOR.                                          
016847                                                                          
016848     PERFORM 3120-CLOSE-USERID-CSR.                                       
016849                                                                          
016850                                                                          
016851                                                                          
016852                                                                          
016853                                                                          
016854                                                                          
016855*-----------------------------------------------------------------        
016856*  OPEN LABEL HISTORY CURSOR                                              
016857*-----------------------------------------------------------------        
016858 3000-OPEN-HISTORY-CSR.                                                   
016859                                                                          
016860     EXEC SQL                                                             
016861         OPEN HISTORY_CURSOR                                              
016862     END-EXEC.                                                            
016863                                                                          
016864     EVALUATE TRUE                                                        
016865         WHEN SQLCODE = +0                                                
016866              SET PS-NOT-EOF-HIST-CURSOR  TO  TRUE                        
016867         WHEN SQLCODE  NOT =  +0                                          
016868              MOVE '3000-OPEN-HISTORY-CSR'                                
016869                                 TO PV-PARAGRAPH-NAME                     
016870              MOVE 'OPEN LABEL TRACKING HISTORY CURSOR'                   
016871                                 TO PV-DB2-OPERATION-ATTEMPTED            
016872             PERFORM 9100-SQL-ABEND                                       
016873     END-EVALUATE.                                                        
016874                                                                          
016875                                                                          
016876*-----------------------------------------------------------------        
016877*  FETCH LABEL HISTORY CURSOR                                             
016878*-----------------------------------------------------------------        
016879 3010-FETCH-HISTORY-CSR.                                                  
016880                                                                          
016881     EXEC SQL                                                             
016882          FETCH HISTORY_CURSOR                                            
016883          INTO  :LBTKHS-PRTR-OPER-UNT-ID                                  
016884              , :LBTKHS-PRTR-FCLTY-ID                                     
016885              , :LBTKHS-PRTR-ID                                           
016886              , :LBTKID-LBL-TYP-CDE                                       
016887              , :LBTKID-LBL-PRT-QTY                                       
016888              , :LBTKID-SEPOR-LBL-PRT-QTY                                 
016889     END-EXEC.                                                            
016890                                                                          
016891     EVALUATE TRUE                                                        
016892         WHEN SQLCODE = ZERO                                              
016893              CONTINUE                                                    
016894         WHEN SQLCODE = +100                                              
016895              SET PS-EOF-HIST-CURSOR TO TRUE                              
016896         WHEN SQLWARN0 NOT = +0                                           
016897         WHEN SQLCODE < ZERO                                              
016898         WHEN SQLCODE > ZERO                                              
016899              MOVE '3010-FETCH-HISTORY-CSR'                               
016900                                 TO PV-PARAGRAPH-NAME                     
016901              MOVE 'FETCH LABEL HISTORY CURSOR'                           
016902                                 TO PV-DB2-OPERATION-ATTEMPTED            
016903             PERFORM 9100-SQL-ABEND                                       
016904     END-EVALUATE.                                                        
016905                                                                          
016906                                                                          
016907*-----------------------------------------------------------------        
016908*  CLOSE LABEL HISTORY CURSOR                                             
016909*-----------------------------------------------------------------        
016910 3020-CLOSE-HISTORY-CSR.                                                  
016911                                                                          
016912     EXEC SQL                                                             
016913         CLOSE HISTORY_CURSOR                                             
016914     END-EXEC.                                                            
016915                                                                          
016916     EVALUATE TRUE                                                        
016917         WHEN SQLCODE = +0                                                
016918              CONTINUE                                                    
016919         WHEN SQLCODE  NOT =  +0                                          
016920              MOVE '3020-CLOSE-HISTORY-CSR'                               
016921                                 TO PV-PARAGRAPH-NAME                     
016922              MOVE 'CLOSE LABEL HISTORY CURSOR'                           
016923                                 TO PV-DB2-OPERATION-ATTEMPTED            
016924             PERFORM 9100-SQL-ABEND                                       
016935     END-EVALUATE.                                                        
016936                                                                          
016937                                                                          
016938*-----------------------------------------------------------------        
016939*  CLOSE LABEL HISTORY CURSOR                                             
016940*-----------------------------------------------------------------        
016941 3050-PROCESS-HISTORY-CSR.                                                
016942                                                                          
016943     INITIALIZE DCLTLBTKSM.                                               
016944                                                                          
016945     MOVE  PT-WEEK-END              TO  LBTKSM-WK-END-DTE.                
016946     MOVE  LBTKHS-PRTR-OPER-UNT-ID  TO  LBTKSM-PRTR-OPER-UNT-ID.          
016947     MOVE  LBTKHS-PRTR-FCLTY-ID     TO  LBTKSM-PRTR-FCLTY-ID.             
016948     MOVE  LBTKHS-PRTR-ID           TO  LBTKSM-PRTR-ID.                   
016949     MOVE  LBTKID-LBL-TYP-CDE       TO  LBTKSM-LBL-TYP-CDE.               
016950     MOVE  LBTKID-LBL-PRT-QTY       TO  LBTKSM-LBL-PRT-QTY.               
016951     MOVE  LBTKID-SEPOR-LBL-PRT-QTY TO  LBTKSM-SEPOR-LBL-PRT-QTY.         
016952     ADD   LBTKID-LBL-PRT-QTY                                             
016953           LBTKID-SEPOR-LBL-PRT-QTY                                       
016954                                GIVING  LBTKSM-TOT-LBL-REQ-QTY.           
016955                                                                          
016956     PERFORM 3200-INSERT-TLBTKSM.                                         
016957                                                                          
016958     PERFORM 3010-FETCH-HISTORY-CSR.                                      
016959                                                                          
016960                                                                          
016961*-----------------------------------------------------------------        
016962*  OPEN LABEL HISTORY USERID CURSOR                                       
016963*-----------------------------------------------------------------        
016964 3100-OPEN-USERID-CSR.                                                    
016965                                                                          
016966     EXEC SQL                                                             
016967         OPEN USERID_CURSOR                                               
016968     END-EXEC.                                                            
016969                                                                          
016970     EVALUATE TRUE                                                        
016971         WHEN SQLCODE = +0                                                
016972              SET PS-NOT-EOF-HIST-CURSOR  TO  TRUE                        
016973         WHEN SQLCODE  NOT =  +0                                          
016974              MOVE '3100-OPEN-USERID-CSR'                                 
016975                                 TO PV-PARAGRAPH-NAME                     
016976              MOVE 'OPEN USERID CURSOR'                                   
016977                                 TO PV-DB2-OPERATION-ATTEMPTED            
016978             PERFORM 9100-SQL-ABEND                                       
016990     END-EVALUATE.                                                        
016991                                                                          
016992                                                                          
016993*-----------------------------------------------------------------        
016994*  FETCH LABEL USERID CURSOR                                              
016995*-----------------------------------------------------------------        
016996 3110-FETCH-USERID-CSR.                                                   
016997                                                                          
016998     EXEC SQL                                                             
016999          FETCH USERID_CURSOR                                             
017000          INTO  :LBTKHS-LBL-TYP-CDE                                       
017001              , :LBTKHS-LBL-PRT-QTY                                       
017002              , :LBTKHS-PRTR-ID                                           
017003              , :LBTKHS-PRTR-OPER-UNT-ID                                  
017004              , :LBTKHS-PRTR-FCLTY-ID                                     
017005     END-EXEC.                                                            
017006                                                                          
017007     EVALUATE TRUE                                                        
017008         WHEN SQLCODE = ZERO                                              
017009              CONTINUE                                                    
017010         WHEN SQLCODE = +100                                              
017011              SET PS-EOF-USER-CURSOR TO TRUE                              
017012         WHEN SQLWARN0 NOT = +0                                           
017013         WHEN SQLCODE < ZERO                                              
017014         WHEN SQLCODE > ZERO                                              
017015              MOVE '3110-FETCH-USERID-CSR'                                
017016                                 TO PV-PARAGRAPH-NAME                     
017017              MOVE 'FETCH LABEL USERID CURSOR'                            
017018                                 TO PV-DB2-OPERATION-ATTEMPTED            
017019             PERFORM 9100-SQL-ABEND                                       
017030     END-EVALUATE.                                                        
017031                                                                          
017032                                                                          
017033*-----------------------------------------------------------------        
017034*  CLOSE LABEL HISTORY CURSOR                                             
017035*-----------------------------------------------------------------        
017036 3120-CLOSE-USERID-CSR.                                                   
017037                                                                          
017038     EXEC SQL                                                             
017039         CLOSE USERID_CURSOR                                              
017040     END-EXEC.                                                            
017041                                                                          
017042     EVALUATE TRUE                                                        
017043         WHEN SQLCODE = +0                                                
017044              CONTINUE                                                    
017045         WHEN SQLCODE  NOT =  +0                                          
017046              MOVE '3120-CLOSE-USERID-CSR'                                
017047                                 TO PV-PARAGRAPH-NAME                     
017048              MOVE 'CLOSE LABEL USERID CURSOR'                            
017049                                 TO PV-DB2-OPERATION-ATTEMPTED            
017050             PERFORM 9100-SQL-ABEND                                       
017061     END-EVALUATE.                                                        
017062                                                                          
017063                                                                          
017064*-----------------------------------------------------------------        
017065*  CLOSE LABEL HISTORY CURSOR                                             
017066*-----------------------------------------------------------------        
017067 3150-PROCESS-USERID-CSR.                                                 
017068                                                                          
017069     INITIALIZE DCLTLBTKSM.                                               
017070                                                                          
017071     MOVE  PT-WEEK-END              TO  LBTKSM-WK-END-DTE.                
017072     MOVE  LBTKHS-PRTR-OPER-UNT-ID  TO  LBTKSM-PRTR-OPER-UNT-ID.          
017073     MOVE  LBTKHS-PRTR-FCLTY-ID     TO  LBTKSM-PRTR-FCLTY-ID.             
017074     MOVE  LBTKHS-PRTR-ID           TO  LBTKSM-PRTR-ID.                   
017075     MOVE  LBTKHS-LBL-TYP-CDE       TO  LBTKSM-LBL-TYP-CDE.               
017076     MOVE  LBTKHS-LBL-PRT-QTY       TO  LBTKSM-LBL-PRT-QTY.               
017077     MOVE  ZEROES                   TO  LBTKSM-SEPOR-LBL-PRT-QTY.         
017078     MOVE  LBTKHS-LBL-PRT-QTY       TO  LBTKSM-TOT-LBL-REQ-QTY.           
017079                                                                          
017080     PERFORM 3200-INSERT-TLBTKSM.                                         
017081                                                                          
017082     PERFORM 3110-FETCH-USERID-CSR.                                       
017090                                                                          
017092                                                                          
017093*-----------------------------------------------------------------        
017094*  INSERT ROW INTO LABEL TRACKING SUMMARY TABLE                           
017095*-----------------------------------------------------------------        
017096 3200-INSERT-TLBTKSM.                                                     
017097                                                                          
017098     EXEC SQL                                                             
017099         INSERT INTO TLBTKSM                                              
017100               ( WK_END_DTE,                                              
017101                 PRTR_OPER_UNT_ID,                                        
017102                 PRTR_FCLTY_ID,                                           
017103                 PRTR_ID,                                                 
017104                 LBL_TYP_CDE,                                             
017105                 LBL_PRT_QTY,                                             
017106                 SEPOR_LBL_PRT_QTY,                                       
017107                 TOT_LBL_REQ_QTY)                                         
017108         VALUES                                                           
017109               (:LBTKSM-WK-END-DTE,                                       
017110                :LBTKSM-PRTR-OPER-UNT-ID,                                 
017111                :LBTKSM-PRTR-FCLTY-ID,                                    
017112                :LBTKSM-PRTR-ID,                                          
017113                :LBTKSM-LBL-TYP-CDE,                                      
017114                :LBTKSM-LBL-PRT-QTY,                                      
017115                :LBTKSM-SEPOR-LBL-PRT-QTY,                                
017116                :LBTKSM-TOT-LBL-REQ-QTY)                                  
017117     END-EXEC.                                                            
017118                                                                          
017119     EVALUATE TRUE                                                        
017120         WHEN SQLCODE = ZERO                                              
017121             CONTINUE                                                     
017122         WHEN SQLWARN0 NOT = SPACES                                       
017123         WHEN SQLCODE NOT EQUAL ZERO                                      
017124              MOVE '3200-INSERT-TLBTKSM'                                  
017125                                 TO PV-PARAGRAPH-NAME                     
017126              MOVE 'INSERT ROW LABEL SUMMARY TABLE'                       
017127                                 TO PV-DB2-OPERATION-ATTEMPTED            
017128             PERFORM 9100-SQL-ABEND                                       
017139     END-EVALUATE.                                                        
017140                                                                          
017141                                                                          
017142*-----------------------------------------------------------------        
017150*  WRITE THE TIMESTAMP RECORD                                             
017200*-----------------------------------------------------------------        
022606 8100-WRITE-TIMESTAMP-RECORD.                                             
022607                                                                          
022612     WRITE TIMESTAMP-RECORD                                               
022613         FROM RC049-CONTROL-RECORD.                                       
022700                                                                          
022710                                                                          
022800 9000-EOJ-ROUTINE.                                                        
022810*-----------------------------------------------------------------        
023000*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
023010*-----------------------------------------------------------------        
023200     CLOSE TIMESTAMP-FILE.                                                
023800                                                                          
023900                                                                          
023910*-----------------------------------------------------------------        
023920*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
023930*  ERROR OR WARNING CODE OCCURS.                                          
023940*-----------------------------------------------------------------        
024000 9100-SQL-ABEND.                                                          
024100                                                                          
024500     DISPLAY '9100-SQL-ABEND'.                                            
024600     DISPLAY '** ABEND     **'.                                           
024700     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
024800     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
024900     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
025000                                                                          
025100******************************************************************        
025200* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
025300* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
025400* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
025500* FORMAT.                                                                 
025600******************************************************************        
025700     COPY DPPD004.                                                        
025800                                                                          
025900     MOVE +4013                  TO ABEND-CODE.                           
026000     CALL 'ILBOABN0' USING ABEND-CODE.                                    
