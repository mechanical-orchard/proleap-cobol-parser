      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT023.                                                
       AUTHOR.         CARL RICHTER.                                            
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   05/15/03.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  ITEM VENDOR EXTRACT PROGRAM                                   *        
      *  THIS PROGRAM CREATES A CHANGE EXTRACT FILE BY COMPARING       *        
      *  TWO GENERATIONS OF THE RMS COMMON EXTRACT FILE.               *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT RMS-CURRENT-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT RMS-PREVIOUS-FILE                                             
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT CHANGE-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  RMS-CURRENT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RMS-CURRENT-RECORD                  PIC X(150).                      
                                                                                
       FD  RMS-PREVIOUS-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RMS-PREVIOUS-RECORD                 PIC X(150).                      
                                                                                
       FD  CHANGE-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CHANGE-RECORD                   PIC   X(105).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CR-CURRENT-REC.                                                      
           05  CR-CURR-KEY-9                  PIC   9(10).                      
           05  CR-CURR-KEY-X  REDEFINES                                         
               CR-CURR-KEY-9                  PIC   X(10).                      
                                                                                
           05  CR-CURR-DATA                   PIC   X(94).                      
                                                                                
       01  PR-PREVIOUS-REC.                                                     
           05  PR-PREV-KEY-9                  PIC   9(10).                      
           05  PR-PREV-KEY-X  REDEFINES                                         
               PR-PREV-KEY-9                  PIC   X(10).                      
                                                                                
           05  PR-PREV-DATA                   PIC   X(94).                      
                                                                                
       01  CHANGE-OUTPUT-REC.                                                   
           05  COR-CHANGE-KEY                 PIC   X(10).                      
           05  COR-CHANGE-DATA                PIC   X(94).                      
           05  COR-CHANGE-INDICATOR           PIC   X(01).                      
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'CWKUT023'.            
           05  PC-OPERCO                 PIC X(02) VALUE '03'.                  
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-PRE-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-CUR-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-OUTPUT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-INSERT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-UPDATE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-DELETE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-NO-CHANGE-RECORDS      PIC  9(9)       VALUE ZERO.            
                                                                                
       01  PS-RUN-TYPE                   PIC X     VALUE 'Y'.                   
           88  PS-INITIAL-RUN                      VALUE 'Y'.                   
           88  PS-NOT-INITIAL-RUN                  VALUE 'N'.                   
                                                                                
      ***************************************************************           
      *   TEMP STORAGE AREA                                                     
      ***************************************************************           
       01  WS-TEMP-REC.                                                         
           05  WS-TMP-ENT-ID                 PIC 9(10).                         
           05  WS-TMP-VND-NBR-10             PIC X(10).                         
           05  WS-TMP-PRIM-TYP-CDE           PIC X(1).                          
           05  WS-TMP-PRIMARY-NAME           PIC X(30).                         
           05  WS-TMP-STATUS-CDE             PIC X(1).                          
           05  WS-TMP-CRE-TMST               PIC X(26).                         
           05  WS-TMP-LAST-UPD-TMST          PIC X(26).                         
                                                                                
                                                                                
      ******************************************************************        
      * INPUT FILE LAYOUT FOR RMS VENDOR EXTRACT FILE                           
      ******************************************************************        
           COPY VNRD100.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT FOR CW VENDOR EXTRACT FILE                           
      ******************************************************************        
           COPY CWRD007.                                                        
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL CR-CURR-KEY-X = HIGH-VALUES                                
                     AND                                                        
                     PR-PREV-KEY-X = HIGH-VALUES.                               
                                                                                
           IF  PS-INITIAL-RUN                                                   
               PERFORM 5000-ADD-DUMMY-VENDOR                                    
           END-IF.                                                              
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  RMS-CURRENT-FILE                                         
                       RMS-PREVIOUS-FILE                                        
                OUTPUT CHANGE-FILE.                                             
                                                                                
           SET PS-INITIAL-RUN  TO  TRUE.                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
           PERFORM 8000-READ-CURRENT.                                           
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
                                                                                
           INITIALIZE CHANGE-OUTPUT-REC                                         
                                                                                
           IF CR-CURR-KEY-X  =  PR-PREV-KEY-X                                   
               PERFORM 2100-UPDATE-OR-NOCHANGE                                  
                                                                                
           ELSE                                                                 
                                                                                
               IF CR-CURR-KEY-X  <  PR-PREV-KEY-X                               
                   PERFORM 2200-INSERT-CURRENT                                  
                                                                                
               ELSE                                                             
                   PERFORM 2300-DELETE-PREVIOUS                                 
                                                                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS RECORD FOR EQUALITY                                             
      *  IF DATA IS NOT EQUAL, THEN RECORD IS AN UPDATE                         
      ******************************************************************        
       2100-UPDATE-OR-NOCHANGE.                                                 
                                                                                
           IF CR-CURR-DATA  =  PR-PREV-DATA                                     
                                                                                
               ADD +1                 TO PA-NO-CHANGE-RECORDS                   
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
                                                                                
           ELSE                                                                 
                                                                                
               MOVE CR-CURR-KEY-9     TO COR-CHANGE-KEY                         
               MOVE CR-CURR-DATA      TO COR-CHANGE-DATA                        
               MOVE 'U'               TO COR-CHANGE-INDICATOR                   
                                                                                
               ADD +1                 TO PA-UPDATE-RECORDS                      
                                                                                
               PERFORM 7100-WRITE                                               
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  INSERT CURRENT DATA                                                    
      ******************************************************************        
       2200-INSERT-CURRENT.                                                     
                                                                                
           MOVE CR-CURR-KEY-9 TO COR-CHANGE-KEY                                 
           MOVE CR-CURR-DATA  TO COR-CHANGE-DATA                                
           MOVE 'I'           TO COR-CHANGE-INDICATOR                           
                                                                                
           ADD +1             TO PA-INSERT-RECORDS                              
                                                                                
           PERFORM 7100-WRITE                                                   
           PERFORM 8000-READ-CURRENT.                                           
                                                                                
                                                                                
      ******************************************************************        
      *  DELETE PREVIOUS DATA                                                   
      ******************************************************************        
       2300-DELETE-PREVIOUS.                                                    
                                                                                
           MOVE PR-PREV-KEY-9 TO COR-CHANGE-KEY                                 
           MOVE PR-PREV-DATA  TO COR-CHANGE-DATA                                
           MOVE 'D'           TO COR-CHANGE-INDICATOR                           
                                                                                
           ADD +1             TO PA-DELETE-RECORDS                              
                                                                                
           PERFORM 7100-WRITE                                                   
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      *  REFORMAT RMS DATA                                             *        
      ******************************************************************        
       3000-REFORMAT-RMS-DATA.                                                  
                                                                                
           INITIALIZE        CW007-VENDOR-RECORD.                               
                                                                                
           MOVE VN100-ENT-ID            TO  CW007-ENT-ID-RED.                   
           MOVE VN100-VND-NBR           TO  CW007-VND-NBR-9.                    
           MOVE VN100-PRIM-TYP-CDE      TO  CW007-PRIM-TYP-CDE.                 
           MOVE VN100-PRIMARY-NAME      TO  CW007-PRIMARY-NAME.                 
           MOVE VN100-STATUS-CDE        TO  CW007-STATUS-CDE.                   
           MOVE VN100-CRE-TMST          TO  CW007-CRE-TMST.                     
           MOVE VN100-LAST-UPD-TMST     TO  CW007-LAST-UPD-TMST.                
                                                                                
                                                                                
      ******************************************************************        
      *  MOVE CWRD007 TO TEMP                                          *        
      ******************************************************************        
       4000-MOVE-TO-TEMP.                                                       
                                                                                
           INITIALIZE       WS-TEMP-REC.                                        
                                                                                
           MOVE CW007-ENT-ID           TO  WS-TMP-ENT-ID                        
           MOVE CW007-VND-NBR-10       TO  WS-TMP-VND-NBR-10                    
           MOVE CW007-PRIM-TYP-CDE     TO  WS-TMP-PRIM-TYP-CDE.                 
           MOVE CW007-PRIMARY-NAME     TO  WS-TMP-PRIMARY-NAME.                 
           MOVE CW007-STATUS-CDE       TO  WS-TMP-STATUS-CDE.                   
           MOVE CW007-CRE-TMST         TO  WS-TMP-CRE-TMST.                     
           MOVE CW007-LAST-UPD-TMST    TO  WS-TMP-LAST-UPD-TMST.                
                                                                                
                                                                                
      ******************************************************************        
      *  FORMAT AND WRITE A DUMMY VENDOR RECORD.                                
      ******************************************************************        
       5000-ADD-DUMMY-VENDOR.                                                   
                                                                                
           INITIALIZE WS-TEMP-REC.                                              
                                                                                
           MOVE 0                            TO  WS-TMP-ENT-ID.                 
           MOVE SPACES                       TO  WS-TMP-VND-NBR-10.             
           MOVE SPACES                       TO  WS-TMP-PRIM-TYP-CDE.           
           MOVE 'DUMMY VENDOR'               TO  WS-TMP-PRIMARY-NAME.           
           MOVE SPACES                       TO  WS-TMP-STATUS-CDE.             
           MOVE '0001-01-01-00.00.00.000000' TO  WS-TMP-CRE-TMST.               
           MOVE '0001-01-01-00.00.00.000000' TO  WS-TMP-LAST-UPD-TMST.          
                                                                                
           MOVE WS-TEMP-REC             TO  CHANGE-OUTPUT-REC.                  
           MOVE 'I'                     TO  COR-CHANGE-INDICATOR                
                                                                                
           PERFORM 7100-WRITE.                                                  
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE THE OUTPUT RECORD                                                
      ******************************************************************        
       7100-WRITE.                                                              
                                                                                
           WRITE  CHANGE-RECORD FROM CHANGE-OUTPUT-REC.                         
           ADD +1            TO PA-OUTPUT-RECORDS.                              
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE CURRENT FILE.                                        *        
      ******************************************************************        
       8000-READ-CURRENT.                                                       
                                                                                
           READ RMS-CURRENT-FILE INTO VN100-VENDOR-EXTRACT-RECD                 
                                                                                
               AT END                                                           
                   MOVE HIGH-VALUES  TO  CR-CURR-KEY-X                          
                                                                                
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-CUR-RECORDS                     
                                                                                
                   PERFORM 3000-REFORMAT-RMS-DATA                               
                                                                                
                   PERFORM 4000-MOVE-TO-TEMP                                    
                   MOVE WS-TEMP-REC       TO  CR-CURRENT-REC                    
                                                                                
           END-READ.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE PREVIOUS FILE.                                                
      ******************************************************************        
       8100-READ-PREVIOUS.                                                      
                                                                                
           READ RMS-PREVIOUS-FILE INTO VN100-VENDOR-EXTRACT-RECD                
                                                                                
               AT END                                                           
                   MOVE HIGH-VALUES  TO  PR-PREV-KEY-X                          
                                                                                
               NOT AT END                                                       
                   ADD 1                  TO  PA-INPUT-RECORDS                  
                   ADD 1                  TO  PA-INPUT-PRE-RECORDS              
                   SET PS-NOT-INITIAL-RUN TO  TRUE                              
                                                                                
                   PERFORM 3000-REFORMAT-RMS-DATA                               
                                                                                
                   PERFORM 4000-MOVE-TO-TEMP                                    
                   MOVE WS-TEMP-REC       TO  PR-PREVIOUS-REC                   
                                                                                
           END-READ.                                                            
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
           DISPLAY '   '.                                                       
                                                                                
           DISPLAY 'INPUT RECS READ    : ' PA-INPUT-RECORDS.                    
           DISPLAY 'INPUT PREVIOUS RECS: ' PA-INPUT-PRE-RECORDS.                
           DISPLAY 'INPUT CURRENT RECS : ' PA-INPUT-CUR-RECORDS.                
           DISPLAY '   '.                                                       
           DISPLAY 'OUTPUT RECS WRITTEN: ' PA-OUTPUT-RECORDS.                   
           DISPLAY 'INSERT RECS WRITTEN: ' PA-INSERT-RECORDS.                   
           DISPLAY 'UPDATE RECS WRITTEN: ' PA-UPDATE-RECORDS.                   
           DISPLAY 'DELETE RECS WRITTEN: ' PA-DELETE-RECORDS.                   
           DISPLAY 'NO CHANGE RECORDS  : ' PA-NO-CHANGE-RECORDS.                
                                                                                
           CLOSE RMS-CURRENT-FILE                                               
                 RMS-PREVIOUS-FILE                                              
                 CHANGE-FILE.                                                   
                                                                                
      ******************************************************************        
      ******************************************************************        
