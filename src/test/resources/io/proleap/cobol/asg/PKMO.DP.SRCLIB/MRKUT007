       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MRKUT007.                                                 
       AUTHOR.        PAUL KEBER.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07/01/2008.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************00080000
      * MRKUT007 - MDS SESSIONS PURGE EXTRACT                           00090002
      ******************************************************************00100000
      * THIS PROGRAM WILL EXTRACT ALL MDS SESSION IDS HAVING A SESSION  00110001
      * TIMESTAMP LESS THAN AN INPUT CONTROL CARD NUMBER OF YEARS PRIOR 00110001
      * TO THE CURRENT DATE AND THAT DO NOT HAVE ANY PTAG IDS ASSIGNED  00110001
      * TO THEM.  THESE SESSION IDS ARE ELIGIBLE TO BE PURGED.  SESSION 00111001
      * IDS WITH A SESSION STATUS OF 'P' AND THOSE WITH INVALID SESSION 00111001
      * TIMESTAMPS WILL NOT BE EXTRACTED.                               00111001
      ******************************************************************00100000
      * WR/INC#    DATE        DESCRIPTION                                      
      * ---------  ----------  ----------------------------------------         
      * WR35040    07/01/2008  INITIAL IMPLEMENTATION.                          
      *                                                                         
      * WR36300    05/05/2009  CHANGED BY T. LANG                               
      *                        MODIFIED CURSOR MDS_PURGE_EXTR_CSR TO            
      *                        INCLUDE ROWS IN PURGE WHERE                      
      *                        PROF_ID = 'N/A'                                  
      ******************************************************************00140000
                                                                                
      ******************************************************************00290094
       ENVIRONMENT DIVISION.                                            00280094
      ******************************************************************00290094
      *                                                                 00300094
       CONFIGURATION SECTION.                                           00310094
       SOURCE-COMPUTER.    IBM-3090.                                    00320094
       OBJECT-COMPUTER.    IBM-3090.                                    00330094
      *                                                                 00340094
       INPUT-OUTPUT SECTION.                                            00350094
       FILE-CONTROL.                                                    00360094
      *                                                                 00370094
           SELECT CONTROL-CARD-FILE                                     00400094
               ASSIGN TO INP01.                                         00410094
      *                                                                 00420094
           SELECT PURGE-EXTRACT-FILE                                    00430094
               ASSIGN TO OUT01.                                         00440094
      *                                                                 00470094
      ******************************************************************00480094
       DATA DIVISION.                                                   00490094
      ******************************************************************00500094
      *                                                                 00510094
       FILE SECTION.                                                    00520094
      *                                                                 00530094
       FD  CONTROL-CARD-FILE                                            00590094
           RECORDING MODE IS F                                          00600094
           BLOCK CONTAINS 0 RECORDS.                                    00610094
       01  CONTROL-CARD-RECORD     PIC X(80).                           00620094
      *                                                                 00630094
       FD  PURGE-EXTRACT-FILE                                           00640094
           RECORDING MODE IS F                                          00650094
           BLOCK CONTAINS 0 RECORDS.                                    00660094
       01  PURGE-EXTRACT-RECORD    PIC X(88).                           00670094
      *                                                                 00680094
      *                                                                 00730094
       WORKING-STORAGE SECTION.                                         00740094
      *                                                                 01110094
       01  PA-PROGRAM-ACCUMULATORS.                                     01120094
           05  PA-CURSOR-ROWS-READ         PIC 9(09)    VALUE ZERO.     01190094
           05  PA-WRITE-COUNT              PIC 9(09)    VALUE ZERO.     01190094
           05  PA-RETRY-COUNT              PIC S9(04)   VALUE 0         01390094
                                                        COMP SYNC.      01400094
      *                                                                 01410094
       01  PC-PROGRAM-CONSTANTS.                                        01420094
           05  PC-PROGRAM-NAME             PIC X(08)    VALUE           01430094
               'MRKUT007'.                                              01440094
           05  PC-MAX-RETRIES              PIC S9(04)   COMP SYNC               
                                                        VALUE +3.               
      *                                                                 01570094
       01  PS-PROGRAM-SWITCHES.                                         01580094
           05  PS-END-OF-MPE-CSR-SW        PIC X(01)    VALUE 'N'.      01590094
               88  PS-END-MPE-CSR                       VALUE 'Y'.      01600094
      *                                                                 01760094
       01  PV-PROGRAM-VARIABLES.                                        01770094
           05  PV-PREV-PRCHG-ID            PIC S9(9)    VALUE +0 COMP.          
           05  PV-SQL-SUB                  PIC 9(03)    VALUE ZERO.             
           05  PV-PARA-NAME                PIC X(35)    VALUE SPACES.   02030094
           05  PV-DB2-OPERATION-ATTEMPTED  PIC X(35)    VALUE SPACES.   02030094
           05  PV-SQLCODE                  PIC S9(9)    VALUE +0.               
           05  PV-SQLCODE-DISPLAY          PIC +(8)9    VALUE ZEROES.   00830000
           05  PV-DATE-YYMMDD.                                                  
               10  PV-YYMMDD-YY        PIC  X(02)        VALUE SPACE.           
               10  PV-YYMMDD-MM        PIC  X(02)        VALUE SPACE.           
               10  PV-YYMMDD-DD        PIC  X(02)        VALUE SPACE.           
           05  PV-PURGE-TIMESTAMP-BUILD.                                01840094
               10  PV-PTR-CCYY.                                         00870094
                   15  PV-PTR-CENTURY      PIC 9(02)   VALUE 20.        00870094
                   15  PV-PTR-YEAR         PIC 9(02)   VALUE ZEROES.    00870094
               10  PV-PTR-CENTURY-YEAR  REDEFINES PV-PTR-CCYY           00870094
                                           PIC 9(04).                   00870094
               10  PV-PTR-HYPHEN1          PIC X(01)   VALUE '-'.       00870094
               10  PV-PTR-MONTH            PIC 9(02)   VALUE ZEROES.    00870094
               10  PV-PTR-HYPHEN2          PIC X(01)   VALUE '-'.       00870094
               10  PV-PTR-DAY              PIC 9(02)   VALUE ZEROES.    00870094
               10  PV-PTR-TIMESTAMP        PIC X(16)    VALUE           00870094
                     '-00.00.00.000000'.                                01850094
           05  PV-PURGE-TIMESTAMP  REDEFINES PV-PURGE-TIMESTAMP-BUILD   01840094
                                           PIC X(26).                   01930094
           05  PV-SESS-TIMESTAMP.                                       01840094
               10  PV-ST-YEAR              PIC 9(04)   VALUE ZEROES.    00870094
               10  PV-ST-HYPHEN1           PIC X(01)   VALUE SPACE.     00870094
               10  PV-ST-MONTH             PIC 9(02)   VALUE ZEROES.    00870094
               10  PV-ST-HYPHEN2           PIC X(01)   VALUE SPACE.     00870094
               10  PV-ST-DAY               PIC 9(02)   VALUE ZEROES.    00870094
               10  PV-ST-HYPHEN3           PIC X(01)   VALUE SPACE.     00870094
               10  PV-ST-HOUR              PIC 9(02)   VALUE ZEROES.    00870094
               10  PV-ST-PERIOD1           PIC X(01)   VALUE SPACE.     00870094
               10  PV-ST-MINUTE            PIC 9(02)   VALUE ZEROES.    00870094
               10  PV-ST-PERIOD2           PIC X(01)   VALUE SPACE.     00870094
               10  PV-ST-SECOND            PIC 9(02)   VALUE ZEROES.    00870094
               10  PV-ST-PERIOD3           PIC X(01)   VALUE SPACE.     00870094
               10  PV-ST-SUBSECOND         PIC 9(06)   VALUE ZEROES.    00870094
                                                                        01930094
       01  ABEND-CODE                      PIC S9(04)   VALUE ZERO      00900094
                                                        COMP SYNC.      00910094
           88  PV-ABEND-4001                   VALUE +4001.                     
      *        EMPTY CONTROL CARD                                               
           88  PV-ABEND-4002                   VALUE +4002.                     
      *        INVALID CONTROL CARD PURGE YEARS                                 
           88  AC-ABEND-4013                   VALUE +4013.                     
      *        SQL ERROR                                                        
                                                                        02010094
      ****************************************************************  00830094
      *  RECORD LAYOUT FOR CONTROL CARD FILE.                           00840094
      ****************************************************************  00850094
       01  CC-CONTROL-CARD.                                             00860094
           05  CC-PURGE-YEARS              PIC 9(02)   VALUE ZEROES.    00870094
           05  FILLER                      PIC X(78)   VALUE SPACES.    00880094
      *                                                                 00890094
      ******************************************************************00790094
      *  MDS PURGE EXTRACT FILE RECORD LAYOUT                           00800094
      ******************************************************************00810094
           COPY MRRD003.                                                00820094
      *                                                                 00920094
           COPY SQLCA2.                                                 02110094
                                                                        02670099
      ***************************************************************** 02670199
      *  MDS_O_SESSIONS DCLGEN                                          02672099
      ***************************************************************** 02673099
           EXEC SQL                                                             
               INCLUDE MDS00000                                                 
           END-EXEC.                                                            
                                                                        02670099
      ***************************************************************** 02670199
      *  LOCALSAS DCLGEN                                                02672099
      ***************************************************************** 02673099
           EXEC SQL                                                             
               INCLUDE LOCALSAS                                                 
           END-EXEC.                                                            
                                                                        02670099
      ***************************************************************** 02670199
      *  TMRITST DCLGEN                                                 02672099
      ***************************************************************** 02673099
           EXEC SQL                                                             
               INCLUDE TMRITST                                                  
           END-EXEC.                                                            
                                                                        02670099
      *                                                                 02680094
           EXEC SQL                                                     02690094
                INCLUDE SQLCA                                           02700094
           END-EXEC.                                                    02710094
                                                                        02720094
      ***************************************************************** 02678099
      *   CURSOR TO SELECT ALL MDS SESSION IDS HAVING A SESSION                 
      *   TIMESTAMP LESS THAN AN INPUT CONTROL CARD NUMBER OF YEARS             
      *   PRIOR TO THE CURRENT DATE AND THAT DO NOT HAVE ANY PTAG IDS           
      *   IDS ASSIGNED TO THEM ON TMRITST.                                      
      ***************************************************************** 02678099
           EXEC SQL                                                             
               DECLARE MDS_PURGE_EXTR_CSR CURSOR FOR                            
                                                                                
                  SELECT DISTINCT M.SESS_ID                                     
                                 ,M.USERID                                      
                                 ,M.SESS_DESCRIP                                
                                 ,M.SESS_TIMESTAMP                              
                   FROM MDS_O_SESSIONS M                                        
                       ,LOCALSAS L                                              
                  WHERE L.SESS_ID = M.SESS_ID                                   
                    AND M.SESS_ID IS NOT NULL                                   
                    AND M.SESS_TIMESTAMP < :PV-PURGE-TIMESTAMP                  
                    AND M.SESS_STATUS <> 'P'                                    
                    AND L.PROF_ID > ' '                                         
                    AND NOT EXISTS                                              
                      (SELECT VT1.SESS_ID FROM                                  
                         (SELECT DISTINCT O.SESS_ID                             
                            FROM MDS_O_SESSIONS O                               
                                ,LOCALSAS S                                     
                           WHERE S.SESS_ID = O.SESS_ID                          
                             AND O.SESS_TIMESTAMP < :PV-PURGE-TIMESTAMP         
                             AND O.SESS_STATUS <> 'P'                           
                             AND S.PROF_ID > ' '                                
                             AND EXISTS                                         
                                (SELECT 1                                       
                                  FROM TMRITST T                                
                                 WHERE S.PROF_ID = T.PTAG_ID)) AS VT1           
                       WHERE M.SESS_ID = VT1.SESS_ID)                           
                   ORDER BY M.SESS_ID                                           
           END-EXEC.                                                            
      ******************************************************************02730094
      *  DB2 ERROR MESSAGES FORMAT AREA.                                02740094
      ******************************************************************02750094
           COPY DPWS004.                                                02760094
                                                                        02770094
      *                                                                 02790094
      ******************************************************************02800094
       PROCEDURE DIVISION.                                              02810094
      ******************************************************************02820094
      *                                                                 02830094
       0000-MRKUT007.                                                   02850094
      ******************************************************************02860094
      * MAIN PROCESSING ROUTINE                                        *02870094
      ******************************************************************02880094
                                                                        02890094
           PERFORM 1000-HOUSEKEEPING.                                   02900094
      *                                                                 02910094
           PERFORM 2000-PROCESS-PRICE-CHANGES                           02920094
               UNTIL PS-END-MPE-CSR.                                            
      *                                                                 02940094
           PERFORM 3000-END-OF-JOB.                                     02950094
                                                                                
           IF PA-WRITE-COUNT = 0                                                
               MOVE 4095 TO RETURN-CODE                                         
           END-IF.                                                              
                                                                                
           GOBACK.                                                      02960094
      *                                                                 02970094
                                                                        02980094
      ******************************************************************03710094
      *  OPEN FILES AND THE MDS PURGE EXTRACT CURSOR                    03720094
      ******************************************************************03730094
       1000-HOUSEKEEPING.                                               02990094
           MOVE '*1000-HOUSEKEEPING.' TO PV-PARA-NAME.                  03010094
                                                                        03020094
           OPEN INPUT  CONTROL-CARD-FILE                                03040094
                OUTPUT PURGE-EXTRACT-FILE.                              03050094
                                                                        03070094
           PERFORM 6000-READ-CONTROL-CARD.                              03240094
                                                                        03070094
           ACCEPT PV-DATE-YYMMDD FROM DATE.                                     
           MOVE PV-YYMMDD-YY TO PV-PTR-YEAR.                                    
           MOVE PV-YYMMDD-MM TO PV-PTR-MONTH.                                   
           MOVE PV-YYMMDD-DD TO PV-PTR-DAY.                                     
           SUBTRACT CC-PURGE-YEARS FROM PV-PTR-CENTURY-YEAR.            04010094
           DISPLAY ' PURGE TIMESTAMP: ' PV-PURGE-TIMESTAMP.                     
                                                                        03680094
           PERFORM 8000-OPEN-MPE-CSR.                                   03680094
           PERFORM 8100-FETCH-MPE-CSR.                                          
      *                                                                 03250094
                                                                        03680094
      ******************************************************************07160094
      * PROCESS MDS PURGE EXTRACT CURSOR                               *07170094
      ******************************************************************07180094
       2000-PROCESS-PRICE-CHANGES.                                      03910094
           MOVE '*2000-PROCESS-PRICE-CHANGES.'    TO PV-PARA-NAME.      03920094
                                                                                
      *-- CHECK FOR A VALID SESSION TIMESTAMP                                   
           MOVE MDS00000-SESS-TIMESTAMP TO PV-SESS-TIMESTAMP.                   
           IF PV-ST-YEAR IS NUMERIC                                             
             AND PV-ST-HYPHEN1 EQUAL '-'                                        
             AND PV-ST-MONTH IS NUMERIC                                         
             AND PV-ST-HYPHEN2 EQUAL '-'                                        
             AND PV-ST-DAY IS NUMERIC                                           
             AND PV-ST-HYPHEN3 EQUAL '-'                                        
             AND PV-ST-HOUR IS NUMERIC                                          
             AND PV-ST-PERIOD1 EQUAL '.'                                        
             AND PV-ST-MINUTE IS NUMERIC                                        
             AND PV-ST-PERIOD2 EQUAL '.'                                        
             AND PV-ST-SECOND IS NUMERIC                                        
             AND PV-ST-PERIOD3 EQUAL '.'                                        
             AND PV-ST-SUBSECOND IS NUMERIC                                     
               PERFORM 2500-WRITE-PURGE-EXTRACT-FILE                            
           END-IF.                                                              
                                                                                
           PERFORM 8100-FETCH-MPE-CSR.                                          
                                                                                
      ******************************************************************07320094
      * WRITE MDS PURGE EXTRACT RECORD                                 *07330094
      ******************************************************************07340094
       2500-WRITE-PURGE-EXTRACT-FILE.                                           
                                                                                
           MOVE MDS00000-SESS-ID        TO MR003-SESSION-ID.                    
           MOVE MDS00000-USERID         TO MR003-USERID.                        
           MOVE MDS00000-SESS-DESCRIP   TO MR003-SESS-DESCRIPTION.              
           MOVE MDS00000-SESS-TIMESTAMP TO MR003-SESS-TIMESTAMP.                
                                                                                
           WRITE PURGE-EXTRACT-RECORD FROM MR003-MDS-PURGE-EXTRACT.     07370094
           ADD +1 TO PA-WRITE-COUNT.                                    07380094
                                                                                
      ******************************************************************03710094
      *  CLOSE FILES AND DISPLAY COUNTS                                *03720094
      ******************************************************************03730094
       3000-END-OF-JOB.                                                 03690094
           MOVE '*3000-END-OF-JOB.' TO PV-PARA-NAME.                    03010094
                                                                        03740094
           PERFORM 8200-CLOSE-MPE-CSR.                                          
           CLOSE CONTROL-CARD-FILE                                      03750094
                 PURGE-EXTRACT-FILE.                                    03780094
                                                                        03790094
           DISPLAY '*******************************'.                           
           DISPLAY 'MRKUT007 - END OF PROGRAM STATS'.                           
           DISPLAY 'CURSOR ROWS READ: ' PA-CURSOR-ROWS-READ.                    
           DISPLAY 'RECORDS WRITTEN : ' PA-WRITE-COUNT.                         
           DISPLAY '*******************************'.                           
                                                                        03900094
      *                                                                 06760094
      ******************************************************************07160094
      * READ DATE CONTROL FILE.                                        *07170094
      ******************************************************************07180094
       6000-READ-CONTROL-CARD.                                          07140094
           MOVE '*6000-READ-CONTROL-CARD.         *' TO PV-PARA-NAME.   07150094
                                                                        07210094
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                  07190094
               AT END                                                   07200094
                   DISPLAY '*** NO PURGE YEARS CONTROL CARD ***'        07230094
                   SET PV-ABEND-4001 TO TRUE                                    
                   PERFORM 9999-ABEND                                   07240094
               NOT AT END                                               07250094
                   DISPLAY ' PURGE YEARS: ' CC-PURGE-YEARS                  0726
                   IF CC-PURGE-YEARS NOT NUMERIC                                
                       DISPLAY '*** INVALID CONTROL CARD PURGE YEARS'           
                       SET PV-ABEND-4002 TO TRUE                                
                       PERFORM 9999-ABEND                                       
                   END-IF                                                       
           END-READ.                                                    07280094
                                                                        07290094
      *                                                                 07390094
      ******************************************************************07320094
      * OPEN MDS PURGE EXTRACT CURSOR                                  *07330094
      ******************************************************************07340094
       8000-OPEN-MPE-CSR.                                                       
           MOVE '8000-OPEN-MPE-CSR.    '  TO PV-PARA-NAME.                      
                                                                                
           INITIALIZE PA-RETRY-COUNT.                                           
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
               EXEC SQL                                                         
                   OPEN MDS_PURGE_EXTR_CSR                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = -904                                          
                        CONTINUE                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '8000-OPEN-MPE-CSR.'                               
                                          TO PV-PARA-NAME                       
                        MOVE 'OPEN OPERATION'                                   
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                        PERFORM 9980-SQL-ERROR                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '8000-OPEN-MPE-CSR'                                         
                                      TO  PV-PARA-NAME                          
               MOVE 'OPEN CURSOR RETRIES EXCEEDED'                              
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9980-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ************************************************************              
      *  FETCH MDS PURGE EXTRACT CURSOR                         **              
      ************************************************************              
       8100-FETCH-MPE-CSR.                                                      
           MOVE '8100-FETCH-MPE-CSR.' TO PV-PARA-NAME.                          
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
               EXEC SQL                                                         
                   FETCH MDS_PURGE_EXTR_CSR                                     
                    INTO :MDS00000-SESS-ID                                      
                       , :MDS00000-USERID                                       
                       , :MDS00000-SESS-DESCRIP                                 
                       , :MDS00000-SESS-TIMESTAMP                               
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                        ADD 1 TO PA-CURSOR-ROWS-READ                            
                   WHEN SQLCODE = +100                                          
                        SET PS-END-MPE-CSR TO TRUE                              
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '8100-FETCH-MPE-CSR'                               
                                      TO  PV-PARA-NAME                          
                        MOVE 'FETCH MDS PURGE EXTRACT CURSOR'                   
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
                        PERFORM 9980-SQL-ERROR                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '8100-FETCH-MPE-CSR'                                        
                                      TO  PV-PARA-NAME                          
               MOVE 'FETCH MDS PURGE CURSOR RETRIES EXCEEDED'                   
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9980-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************07320094
      * CLOSE MDS PURGE EXTRACT CURSOR                                 *07330094
      ******************************************************************07340094
       8200-CLOSE-MPE-CSR.                                                      
           MOVE '8200-CLOSE-MPE-CSR.' TO PV-PARA-NAME.                          
                                                                                
           INITIALIZE PA-RETRY-COUNT.                                           
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  CLOSE MDS_PURGE_EXTR_CSR                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '8200-CLOSE-MPE-CSR'                               
                                      TO  PV-PARA-NAME                          
                        MOVE 'CLOSE MDS PURGE EXTRACT CURSOR'                   
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
                        PERFORM 9980-SQL-ERROR                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '8200-CLOSE-MPE-CSR'                                        
                                      TO  PV-PARA-NAME                          
               MOVE 'CLOSE MDS PURGE CURSOR RETRIES EXCEEDED'                   
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9980-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
       9980-SQL-ERROR.                                                  07500094
      ******************************************************************07510094
      * ABEND ROUTINE FOR BAD SQL CALLS                                *07520094
      ******************************************************************07530094
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
           DISPLAY '** ABEND **          = SQL ERROR'.                  07540094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07550094
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME.              07560094
           DISPLAY '** OPERATION **      = ' PV-DB2-OPERATION-ATTEMPTED.        
           DISPLAY '** SQL CODE **       = ' PV-SQLCODE-DISPLAY.                
           DISPLAY 'CURSOR ROWS READ     = ' PA-CURSOR-ROWS-READ.               
           DISPLAY 'RECORDS WRITTEN      = ' PA-WRITE-COUNT.                    
      *                                                                 07570094
      ******************************************************************07580094
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    07590094
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         07600094
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     07610094
      * FORMAT.                                                         07620094
      ******************************************************************07630094
           COPY DPPD004.                                                07640094
                                                                        07650094
             EXEC SQL                                                   07660094
                COMMIT                                                  07670094
             END-EXEC.                                                  07680094
      *                                                                 07690094
           SET AC-ABEND-4013 TO TRUE.                                   07700094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07710094
                                                                        07720094
       9999-ABEND.                                                      07730094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME.              07780094
           DISPLAY 'CURSOR ROWS READ     = ' PA-CURSOR-ROWS-READ.               
           DISPLAY 'RECORDS WRITTEN      = ' PA-WRITE-COUNT.                    
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
