000100******************************************************************01/30/96
000200 IDENTIFICATION DIVISION.                                         RCK00187
000300******************************************************************   LV010
000400                                                                     CL**2
000500 PROGRAM-ID.    RCKUT034.                                            CL**2
000600 AUTHOR.        MARY SARDINA.                                        CL**2
000700 INSTALLATION.  KOHLS.                                               CL**2
000800 DATE-WRITTEN   JANUARY, 1996.                                       CL**2
000900 DATE-COMPILED.                                                      CL**2
001000                                                                     CL**2
001100******************************************************************   CL**2
001200*  THIS PROGRAM CREATES AN EXTRACT FILE OF DATA FORM THE RECEIPT     CL**2
001300*  DISTRIBUTION AUDIT TABLE.                                         CL**2
001400******************************************************************   CL**2
001500                                                                     CL**2
001600******************************************************************   CL**2
001700 ENVIRONMENT DIVISION.                                               CL**2
001800******************************************************************   CL**2
001900                                                                     CL**2
002000 CONFIGURATION SECTION.                                              CL**2
002100                                                                     CL**2
002200 SOURCE-COMPUTER.        IBM-3090.                                   CL**2
002300 OBJECT-COMPUTER.        IBM-3090.                                   CL**2
002400                                                                     CL**2
002500 INPUT-OUTPUT SECTION.                                               CL**2
002600                                                                     CL**2
002700 FILE-CONTROL.                                                       CL**2
002800                                                                     CL**2
002900                                                                     CL**2
003000     SELECT TIMESTAMP-FILE                                           CL**2
003100         ASSIGN TO IN01.                                             CL**9
003200                                                                     CL**2
003300     SELECT RECEIPT-DIST-AUDIT-FILE                                  CL**3
003400         ASSIGN TO OUT02.                                            CL**2
003200                                                                     CL**2
003300     SELECT RECEIPT-DIST-AUDTRIG-FILE                                CL**3
003400         ASSIGN TO OUT03.                                            CL**2
003500******************************************************************   CL**2
003600 DATA DIVISION.                                                      CL**2
003700******************************************************************   CL**2
003800                                                                     CL**2
003900 FILE SECTION.                                                       CL**2
004000                                                                     CL**2
004100 FD  TIMESTAMP-FILE                                                  CL**2
004200     RECORDING MODE IS F                                             CL**2
004300     LABEL RECORDS ARE STANDARD                                      CL**2
004400     BLOCK CONTAINS 0 RECORDS.                                       CL**2
004500                                                                     CL**2
004600 01  TIMESTAMP-RECORD           PIC  X(50).                          CL**2
004700                                                                     CL**2
004800 FD  RECEIPT-DIST-AUDIT-FILE                                         CL**2
004900     RECORDING MODE IS F                                             CL**2
005000     LABEL RECORDS ARE STANDARD                                      CL**2
005100     BLOCK CONTAINS 0 RECORDS.                                       CL**2
005200                                                                     CL**2
005300 01  RECEIPT-DIST-AUDIT-RECORD  PIC  X(175).                         CL**7
004700                                                                     CL**2
004800 FD  RECEIPT-DIST-AUDTRIG-FILE                                       CL**2
004900     RECORDING MODE IS F                                             CL**2
005000     LABEL RECORDS ARE STANDARD                                      CL**2
005100     BLOCK CONTAINS 0 RECORDS.                                       CL**2
005200                                                                     CL**2
005300 01  RECEIPT-DIST-AUDTRIG-REC   PIC  X(175).                         CL**7
005400*                                                                    CL**2
005500*                                                                    CL**2
005600******************************************************************   CL**2
005700 WORKING-STORAGE SECTION.                                            CL**2
005800******************************************************************   CL**2
005900*                                                                    CL**2
006000*                                                                    CL**2
006500*                                                                    CL**2
006600*                                                                    CL**2
006700******************************************************************   CL**2
006800*  RECORD LAYOUT FOR THE TIMESTAMP FILE.                             CL**2
006900******************************************************************   CL**2
007000     COPY RCRD011.                                                   CL**2
007100*                                                                    CL**2
007200*                                                                    CL**2
007300******************************************************************   CL**2
007400*  RECORD LAYOUT FOR THE RECEIPT DIST  TABLE.                        CL**3
007500******************************************************************   CL**2
007600     COPY RCRD014.                                                   CL**2
007700*                                                                    CL**2
007800*                                                                    CL**2
007900******************************************************************   CL**2
008000*  DB2 FORMATTED MESSAGE AREA                                        CL**2
008100******************************************************************   CL**2
008200     COPY DPWS004.                                                   CL**2
008300*                                                                    CL**2
008400*                                                                    CL**2
008500*                                                                    CL**2
008600******************************************************************   CL**2
008700*  COMMUNICATIONS AREA FOR DB2                                       CL**2
008800******************************************************************   CL**2
008900     EXEC SQL                                                        CL**2
009000          INCLUDE SQLCA                                              CL**2
009100     END-EXEC.                                                       CL**2
009200*                                                                    CL**2
009300*                                                                    CL**2
009400******************************************************************   CL**2
009500*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE RECEIPT DIST AUDIT    CL**3
009600*  TABLE                                                             CL**2
009700******************************************************************   CL**2
009800     EXEC SQL                                                        CL**2
009900          INCLUDE TAURECD                                            CL**2
010000     END-EXEC.                                                       CL**2
010100*                                                                    CL**2
010200 01  ABEND-CODE                      PIC S9(04)    VALUE +0          CL**2
010300                                                   COMP SYNC.        CL**2
010400                                                                     CL**2
010500 01  PS-PROGRAM-SWITCHES.                                            CL**2
010600     05  PS-EOF-AUDIT-RECD-CSR-SW    PIC  X(01)    VALUE SPACE.      CL**3
010700         88  PS-EOF-AUDIT-RECD-CSR                 VALUE 'Y'.        CL**3
010800                                                                     CL**2
010900                                                                     CL**2
011000 01  PC-PROGRAM-CONSTANTS.                                           CL**2
011100     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100        CL**2
011200                                                   COMP SYNC.        CL**2
011300     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE             CL**2
011400         'RCKUT034'.                                                 CL**5
011500     05  PC-CURRENT-OPERATING-COMPANY                                CL**2
011600                                     PIC  X(02)    VALUE '03'.       CL**2
011700     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3          CL**2
011800                                                   COMP SYNC.        CL**2
011900     05  PC-CURRENT-TIMESTAMP        PIC  X(26)    VALUE SPACES.     CL**2
011910     05  PC-U                        PIC  X(01)    VALUE 'U'.        CL**2
012000                                                                     CL**2
012100 01  PV-PROGRAM-VARIABLES.                                           CL**2
012200     05  PV-ITEM-NBR                 PIC  9(15)    VALUE ZERO.       CL**2
012300     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.     CL**2
012400     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.     CL**2
012500     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO        CL**2
012600                                                   COMP SYNC.        CL**2
012700     05  PV-CLOSE-COUNTER            PIC S9(04)    VALUE ZERO        CL**2
012800                                                   COMP SYNC.        CL**2
012900     05  PV-FETCH-COUNTER            PIC S9(04)    VALUE ZERO        CL**2
013000                                                   COMP SYNC.        CL**5
013100     05  PV-WRITE-COUNTER            PIC S9(04)    VALUE ZERO        CL**5
013200                                                   COMP SYNC.        CL**5
013100     05  PV-WRITE-AUDTRIG-COUNTER    PIC S9(04)    VALUE ZERO        CL**5
013200                                                   COMP SYNC.        CL**5
013300     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                CL**5
013400                                                                     CL**5
013500     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO        CL**2
013600                                                   COMP-3.           CL**2
013700     05  PV-OPER-UNT-ID              PIC 9(04)     VALUE ZERO.       CL**2
013800                                                                     CL**2
013900     05  PV-RECD-SEQ-NBR             PIC 9(03)     VALUE ZERO.       CL**3
014000                                                                     CL**2
014100     05  PV-PO-NBR                   PIC S9(08)    VALUE ZERO.       CL**2
014200                                                                     CL**2
014300     05  PV-PO-LNE-NBR               PIC 9(03)     VALUE ZERO.       CL**2
014400                                                                     CL**2
014500*                                                                    CL**2
014600*                                                                    CL**2
014700 01  SD-SYSTEM-DATE.                                                 CL**2
014800     05  SD-SYSTEM-DATE-YY           PIC  9(02)      VALUE ZERO.     CL**2
014900     05  SD-SYSTEM-DATE-MM           PIC  9(02)      VALUE ZERO.     CL**2
015000     05  SD-SYSTEM-DATE-DD           PIC  9(02)      VALUE ZERO.     CL**2
015100                                                                     CL**2
015200*                                                                    CL**2
015300*  CURSOR DECLARATION FOR ALL EXCEPTION ACTIVITY ON THE              CL**2
015400*  UPC CERTIFICATION ACTIVITY LOG TABLE                              CL**2
015500*                                                                    CL**2
015600                                                                     CL**2
015700     EXEC SQL                                                        CL**2
015800          DECLARE AUDIT_RECD_CURSOR CURSOR FOR                       CL**3
015900           SELECT                                                    CL**2
016000             ORD_NBR                                                 CL**3
016100            ,ORD_LNE_NBR                                             CL**3
016200            ,REC_SEQ_NBR                                             CL**3
016300            ,OPER_UNT_ID                                             CL**3
016400            ,FCLTY_ID                                                CL**3
016500            ,CRE_TMST                                                CL**3
016600            ,CRE_UID                                                 CL**3
016700            ,VER_STATUS_CDE                                          CL**3
016800            ,EXPTED_ITM_QTY                                          CL**3
016900            ,VERIFY_ITM_QTY                                          CL**3
017000            ,AP_VERIFY_ITM_QTY                                       CL**3
017100            ,STR_NBR                                                 CL**3
017200            ,STR_FCLTY_ID                                            CL**3
017300            ,PLAN_ITM_QTY                                            CL**3
017500            ,AP_PRC_CDE                                              CL**3
017600            ,AP_PRC_DTE                                              CL**3
017700            ,UPD_TMST                                                CL**3
017800            ,UPD_UID                                                 CL**3
017900            ,UPD_PGM_ID                                              CL**3
018000            ,AUD_TYP_CDE                                             CL**3
018100            ,AUD_FUNC_CDE                                            CL**3
018200            ,NOTIF_NBR                                               CL*10
018210            ,ADJ_CYC_NBR                                             CL*10
018300             FROM TAURECD                                            CL**3
018400            WHERE UPD_TMST = :PC-CURRENT-TIMESTAMP                   CL**2
018500               OR UPD_TMST < :PC-CURRENT-TIMESTAMP                   CL**2
018510              AND NOT ROW_UNLD_SW = :PC-U                                 
018600                                                                     CL**2
018700     END-EXEC.                                                       CL**2
018800                                                                     CL**2
018900                                                                     CL**2
019000******************************************************************   CL**2
019100 PROCEDURE DIVISION.                                                 CL**2
019200******************************************************************   CL**2
019300                                                                     CL**2
019400 0000-PROGRAM-MAINLINE.                                              CL**2
019500******************************************************************   CL**2
019600* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE        CL**2
019700*      PROGRAM.                                                      CL**2
019800******************************************************************   CL**2
019900                                                                     CL**2
020000     PERFORM 1000-INITIALIZE.                                        CL**2
020100     PERFORM 2000-PROCESS-AUDIT-RECPT-DIST                           CL**3
020200       UNTIL PS-EOF-AUDIT-RECD-CSR.                                  CL**3
020300     PERFORM 9000-EOJ-ROUTINE.                                       CL**2
020400                                                                     CL**2
020500     STOP RUN.                                                       CL**2
020600*0000-EXIT.                                                          CL**2
020700*                                                                    CL**2
020800*                                                                    CL**2
020900 1000-INITIALIZE.                                                    CL**2
021000******************************************************************   CL**2
021100*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                       CL**2
021200*    -- OPEN OUTPUT FILE                                             CL**2
021300*    -- OPEN CURSOR                                                  CL**2
021400*    -- FETCH CURSOR                                                 CL**2
021500******************************************************************   CL**2
021600*                                                                    CL**2
021700     ACCEPT SD-SYSTEM-DATE FROM DATE.                                CL**2
021800*                                                                    CL**2
021900     OPEN INPUT  TIMESTAMP-FILE.                                     CL**2
022000     OPEN OUTPUT RECEIPT-DIST-AUDIT-FILE                             CL**4
022000                 RECEIPT-DIST-AUDTRIG-FILE.                          CL**4
022100                                                                     CL**2
022200     PERFORM 1100-READ-TIMESTAMP-FILE.                               CL**2
022300                                                                     CL**2
022400     PERFORM 7000-OPEN-AUDIT-RECD-CURSOR.                            CL**3
022500                                                                     CL**2
022600     PERFORM 7100-FETCH-AUDIT-RECD-CURSOR.                           CL**3
022700*1000-EXIT.                                                          CL**2
022800*                                                                    CL**2
022900 1100-READ-TIMESTAMP-FILE.                                           CL**2
023000      READ TIMESTAMP-FILE INTO                                       CL**8
023100          RC011-CONTROL-RECORD.                                      CL**2
023200      MOVE RC011-CONTROL-TMST TO PC-CURRENT-TIMESTAMP.               CL**2
023300     DISPLAY 'RCKUT034 CONTROL CARD -- ' RC011-CONTROL-TMST.         CL**5
023400 2000-PROCESS-AUDIT-RECPT-DIST.                                      CL**4
023500******************************************************************   CL**2
023600*  PROCESS THE CERTIFICATION REQUESTS (LOOP THROUGH THE RESULTS      CL**2
023700*  TABLE).  FORMAT THE OUTPUT RECORD.  WRITE THE RECORDS THAT        CL**2
023800*  MEET THE CRITERIA.                                                CL**2
023900******************************************************************   CL**2
024000                                                                     CL**2
024100     PERFORM 8000-WRITE-AUDIT-RECD-RECORD.                           CL**3
024200     PERFORM 7100-FETCH-AUDIT-RECD-CURSOR.                           CL**3
024300*2000-EXIT.                                                          CL**2
024400*                                                                    CL**2
024500 7000-OPEN-AUDIT-RECD-CURSOR.                                        CL**3
024600******************************************************************   CL**2
024700*  OPEN THE CURSOR.                                                  CL**2
024800******************************************************************   CL**2
024900     PERFORM WITH TEST AFTER                                         CL**2
025000       VARYING PV-OPEN-COUNTER FROM 1 BY 1                           CL**2
025100         UNTIL (PV-OPEN-COUNTER > PC-MAX-RETRIES                     CL**2
025200                OR                                                   CL**2
025300                SQLCODE = ZERO)                                      CL**2
025400                                                                     CL**2
025500         EXEC SQL                                                    CL**2
025600              OPEN AUDIT_RECD_CURSOR                                 CL**3
025700         END-EXEC                                                    CL**2
025800                                                                     CL**2
025900         EVALUATE TRUE                                               CL**2
026000             WHEN SQLCODE = -904                                     CL**2
026100             WHEN SQLCODE = -911                                     CL**2
026200                 CONTINUE                                            CL**2
026300             WHEN SQLWARN0 NOT = SPACES                              CL**2
026400             WHEN SQLCODE < ZERO                                     CL**2
026500             WHEN SQLCODE > ZERO                                     CL**2
026600                 MOVE '7000-OPEN-AUDIT-RECD-CURSOR    '              CL**3
026700                                 TO PV-PARAGRAPH-NAME                CL**2
026800                 MOVE 'OPEN THE AUDIT_RECD_CURSOR    '               CL**3
026900                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**2
027000                 PERFORM 9100-SQL-ABEND                              CL**2
027100             WHEN SQLCODE = ZERO                                     CL**2
027200                 CONTINUE                                            CL**2
027300         END-EVALUATE                                                CL**2
027400     END-PERFORM.                                                    CL**2
027500                                                                     CL**2
027600     IF PV-OPEN-COUNTER > PC-MAX-RETRIES                             CL**2
027700         MOVE '7000-OPEN-AUDIT-RECD-CURSOR   '                       CL**3
027800                                 TO PV-PARAGRAPH-NAME                CL**2
027900         MOVE 'OPEN MAX RETRIES EXCEEDED     '                       CL**2
028000                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**2
028100         PERFORM 9100-SQL-ABEND                                      CL**2
028200     END-IF.                                                         CL**2
028300*7000-EXIT.                                                          CL**2
028400*                                                                    CL**2
028500*                                                                    CL**2
028600 7100-FETCH-AUDIT-RECD-CURSOR.                                       CL**3
028700******************************************************************   CL**2
028800*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.            CL**2
028900******************************************************************   CL**2
029000     EXEC SQL                                                        CL**2
029100          FETCH AUDIT_RECD_CURSOR                                    CL**3
029200           INTO :AURECD-ORD-NBR,                                     CL**6
029300                :AURECD-ORD-LNE-NBR,                                 CL**4
029400                :AURECD-REC-SEQ-NBR,                                 CL**4
029500                :AURECD-OPER-UNT-ID,                                 CL**4
029600                :AURECD-FCLTY-ID,                                    CL**4
029700                :AURECD-CRE-TMST,                                    CL**4
029800                :AURECD-CRE-UID,                                     CL**4
029900                :AURECD-VER-STATUS-CDE,                              CL**4
030000                :AURECD-EXPTED-ITM-QTY,                              CL**4
030100                :AURECD-VERIFY-ITM-QTY,                              CL**4
030200                :AURECD-AP-VERIFY-ITM-QTY,                           CL**4
030300                :AURECD-STR-NBR,                                     CL**4
030400                :AURECD-STR-FCLTY-ID,                                CL**4
030500                :AURECD-PLAN-ITM-QTY,                                CL**4
030700                :AURECD-AP-PRC-CDE,                                  CL**4
030800                :AURECD-AP-PRC-DTE,                                  CL**4
030900                :AURECD-UPD-TMST,                                    CL**4
031000                :AURECD-UPD-UID,                                     CL**4
031100                :AURECD-UPD-PGM-ID,                                  CL**4
031200                :AURECD-AUD-TYP-CDE,                                 CL**4
031300                :AURECD-AUD-FUNC-CDE,                                CL**4
031400                :AURECD-NOTIF-NBR,                                   CL*10
031410                :AURECD-ADJ-CYC-NBR                                     10
031500     END-EXEC.                                                       CL**2
031600                                                                     CL**2
031700     EVALUATE TRUE                                                   CL**2
031800         WHEN SQLCODE = ZERO                                         CL**2
031900             CONTINUE                                                CL**2
032000         WHEN SQLCODE = +100                                         CL**2
032100             SET PS-EOF-AUDIT-RECD-CSR                               CL**3
032200                                 TO TRUE                             CL**2
032300         WHEN SQLWARN0 NOT = SPACES                                  CL**2
032400         WHEN SQLCODE < ZERO                                         CL**2
032500         WHEN SQLCODE > ZERO                                         CL**2
032600             MOVE '7100-FETCH-AUDIT-RECD-CURSOR  '                   CL**3
032700                                 TO PV-PARAGRAPH-NAME                CL**2
032800             MOVE 'FETCH THE AUDIT-RECD-CURSOR   '                   CL**3
032900                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**2
033000             PERFORM 9100-SQL-ABEND                                  CL**2
033100     END-EVALUATE.                                                   CL**2
033200*7100-EXIT.                                                          CL**2
033300*                                                                    CL**2
033400*                                                                    CL**2
033500*                                                                    CL**2
033600 8000-WRITE-AUDIT-RECD-RECORD.                                       CL**3
033700******************************************************************   CL**2
033800*  WRITE THE RECEIPT-DIST-AUDIT-RECORD                               CL**4
033900******************************************************************   CL**2
034000     INITIALIZE RECEIPT-DIST-AUDIT-RECORD.                           CL**4
034001                                                                          
034010     MOVE AURECD-ORD-NBR                TO RC014-ORD-NBR.                 
034020     MOVE AURECD-ORD-LNE-NBR            TO RC014-ORD-LNE-NBR.             
034030     MOVE AURECD-REC-SEQ-NBR            TO RC014-REC-SEQ-NBR.             
034040     MOVE AURECD-OPER-UNT-ID            TO RC014-OPER-UNT-ID.             
034050     MOVE AURECD-FCLTY-ID               TO RC014-FCLTY-ID.                
034060     MOVE AURECD-CRE-TMST               TO RC014-CRE-TMST.                
034070     MOVE AURECD-CRE-UID                TO RC014-CRE-UID.                 
034080     MOVE AURECD-VER-STATUS-CDE         TO RC014-VER-STATUS-CDE.          
034090     MOVE AURECD-EXPTED-ITM-QTY         TO RC014-EXPTED-ITM-QTY.          
034091     MOVE AURECD-VERIFY-ITM-QTY         TO RC014-VERIFY-ITM-QTY.          
034092     MOVE AURECD-AP-VERIFY-ITM-QTY      TO RC014-AP-VERIFY-ITM-QTY.       
034093     MOVE AURECD-STR-NBR                TO RC014-STR-NBR.                 
034094     MOVE AURECD-STR-FCLTY-ID           TO RC014-STR-FCLTY-ID.            
034095     MOVE AURECD-PLAN-ITM-QTY           TO RC014-PLAN-ITM-QTY.            
034096     MOVE AURECD-AP-PRC-CDE             TO RC014-AP-PRC-CDE.              
034097     MOVE AURECD-AP-PRC-DTE             TO RC014-AP-PRC-DTE.              
034098     MOVE AURECD-UPD-TMST               TO RC014-UPD-TMST.                
034099     MOVE AURECD-UPD-UID                TO RC014-UPD-UID.                 
034100     MOVE AURECD-UPD-PGM-ID             TO RC014-UPD-PGM-ID.              
034101     MOVE AURECD-AUD-TYP-CDE            TO RC014-AUD-TYP-CDE.             
034102     MOVE AURECD-AUD-FUNC-CDE           TO RC014-FUNC-CDE.                
034103     MOVE AURECD-NOTIF-NBR              TO RC014-NOTIF-NBR.               
034104     MOVE AURECD-ADJ-CYC-NBR            TO RC014-ADJ-CYC-NBR.             
034110     IF RC014-FUNC-CDE = 'T'                                         CL**4
034110         WRITE RECEIPT-DIST-AUDTRIG-REC                              CL**4
034200             FROM RC014-TRECDIS-AUDIT-RECORD                         CL**3
034400         ADD +1 TO PV-WRITE-AUDTRIG-COUNTER                          CL**5
034200     ELSE                                                            CL**3
034110         WRITE RECEIPT-DIST-AUDIT-RECORD                             CL**4
034200             FROM RC014-TRECDIS-AUDIT-RECORD                         CL**3
034400         ADD +1 TO PV-WRITE-COUNTER                                  CL**5
034500     END-IF.                                                         CL**2
034500                                                                     CL**2
034600*8000-EXIT.                                                          CL**2
034700*                                                                    CL**2
034800*                                                                    CL**2
034800***** PARAGRAPH 8100-WRITE-TIMESTAMP-RECORD NOT ACCESSED             CL**2
034800*                                                                    CL**2
034900*8100-WRITE-TIMESTAMP-RECORD.                                        CL**2
035000******************************************************************   CL**2
035100*  WRITE THE TIMESTAMP RECORD                                        CL**2
035200******************************************************************   CL**2
035300*    WRITE TIMESTAMP-RECORD                                          CL**2
035400*        FROM RC011-CONTROL-RECORD.                                  CL**2
035500                                                                     CL**2
035600*8100-EXIT.                                                          CL**2
035700*                                                                    CL**2
035800*                                                                    CL**2
035900 9000-EOJ-ROUTINE.                                                   CL**2
036000******************************************************************   CL**2
036100*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                             CL**2
036200******************************************************************   CL**2
036300     MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                      CL**5
036400     DISPLAY 'TOTAL AUDIT RECORDS SELECTED ' PV-DISPLAY-COUNT.       CL**5
036300*** DISPLAY COUNTER FOR AUDTRIG - TRIGGER FILE                       CL**5
036300     MOVE PV-WRITE-AUDTRIG-COUNTER TO PV-DISPLAY-COUNT.              CL**5
036400     DISPLAY 'TOTAL TRIGGER AUDIT RECORDS SELECTED '                 CL**5
036400              PV-DISPLAY-COUNT.                                      CL**5
036500     CLOSE RECEIPT-DIST-AUDIT-FILE                                   CL**4
036500           RECEIPT-DIST-AUDTRIG-FILE                                 CL**4
036600           TIMESTAMP-FILE.                                           CL**2
036700                                                                     CL**2
036800     PERFORM WITH TEST AFTER                                         CL**2
036900       VARYING PV-CLOSE-COUNTER FROM 1 BY 1                          CL**2
037000         UNTIL (PV-CLOSE-COUNTER > PC-MAX-RETRIES                    CL**2
037100                OR                                                   CL**2
037200                SQLCODE = ZERO)                                      CL**2
037300                                                                     CL**2
037400         EXEC SQL                                                    CL**2
037500              CLOSE AUDIT_RECD_CURSOR                                CL**3
037600         END-EXEC                                                    CL**2
037700                                                                     CL**2
037800         EVALUATE TRUE                                               CL**2
037900             WHEN SQLCODE = -904                                     CL**2
038000             WHEN SQLCODE = -911                                     CL**2
038100                 CONTINUE                                            CL**2
038200             WHEN SQLWARN0 NOT = SPACES                              CL**2
038300             WHEN SQLCODE < ZERO                                     CL**2
038400             WHEN SQLCODE > ZERO                                     CL**2
038500                 MOVE '9000-EOJ-ROUTINE               '              CL**2
038600                                 TO PV-PARAGRAPH-NAME                CL**2
038700                 MOVE 'CLOSE THE AUDIT_RECD_CURSOR    '              CL**3
038800                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**2
038900                 PERFORM 9100-SQL-ABEND                              CL**2
039000             WHEN SQLCODE = ZERO                                     CL**2
039100                 CONTINUE                                            CL**2
039200         END-EVALUATE                                                CL**2
039300     END-PERFORM.                                                    CL**2
039400                                                                     CL**2
039500     IF PV-CLOSE-COUNTER > PC-MAX-RETRIES                            CL**2
039600         MOVE '9000-EOJ-ROUTINE               '                      CL**2
039700                                 TO PV-PARAGRAPH-NAME                CL**2
039800         MOVE 'CLOSE MAX RETRIES EXCEEDED     '                      CL**2
039900                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**2
040000         PERFORM 9100-SQL-ABEND                                      CL**2
040100     END-IF.                                                         CL**2
040200*9000-EXIT.                                                          CL**2
040300*                                                                    CL**2
040400*                                                                    CL**2
040500 9100-SQL-ABEND.                                                     CL**2
040600******************************************************************   CL**2
040700*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL       CL**2
040800*  ERROR OR WARNING CODE OCCURS.                                     CL**2
040900******************************************************************   CL**2
041000     DISPLAY '9100-SQL-ABEND'.                                       CL**2
041100     DISPLAY '** ABEND     **'.                                      CL**2
041200     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.           CL**2
041300     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                 CL**2
041400     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.        CL**2
041500                                                                     CL**2
041600******************************************************************   CL**2
041700* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**2
041800* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE            CL**2
041900* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE        CL**2
042000* FORMAT.                                                            CL**2
042100******************************************************************   CL**2
042200     COPY DPPD004.                                                      *2
042300                                                                     CL**2
042400     MOVE +4013                  TO ABEND-CODE.                      CL**2
042500     CALL 'ILBOABN0' USING ABEND-CODE.                               CL**2
042600*9100-EXIT.                                                          CL**2
