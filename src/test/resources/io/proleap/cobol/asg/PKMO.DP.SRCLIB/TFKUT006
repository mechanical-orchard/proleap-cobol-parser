       IDENTIFICATION DIVISION.                                         00010002
       PROGRAM-ID.    TFKUT006.                                         00020002
       AUTHOR.        THOMAS HANSON.                                    00030003
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040002
       DATE-WRITTEN.  11/30/2009.                                       00050002
       DATE-COMPILED.                                                   00060002
      ******************************************************************00070002
      ******************************************************************00090002
      **THIS PROGRAM WILL READ IN AN INVENTORY SNAPSHOT EXTRACT AND     00100002
      **                       CREATES THE ML FILE.                     00120002
      ******************************************************************00121002
      **DATE:11/30/2009   BY: T.HANSON   INITIAL IMPLEMENTATION         00122002
      ******************************************************************00123002
      ******************************************************************00123105
SR3403**DATE:03/10/2012 MADE BY: ANUJ GUPTA LARGE RETAIL EXPANSION      00123205
      ******************************************************************00123305
                                                                        00124002
       ENVIRONMENT DIVISION.                                            00125002
       CONFIGURATION SECTION.                                           00126002
       SOURCE-COMPUTER.    IBM-3090.                                    00127002
       OBJECT-COMPUTER.    IBM-3090.                                    00128002
       INPUT-OUTPUT SECTION.                                            00129002
       FILE-CONTROL.                                                    00130002
                                                                        00140002
           SELECT INPUT-FILE                                            00150002
                  ASSIGN TO INP01.                                      00160002
           SELECT OUTPUT-FILE                                           00170002
                  ASSIGN TO OUT01.                                      00180002
                                                                        00190002
       DATA DIVISION.                                                   00200002
       FILE SECTION.                                                    00210002
                                                                        00220002
       FD  INPUT-FILE                                                   00230002
           RECORDING MODE IS F                                          00240002
           LABEL RECORDS OMITTED                                        00250002
           BLOCK CONTAINS 0 RECORDS                                     00260002
           DATA RECORD IS INPUT-RECORD.                                 00270002
SR3403 01  INPUT-RECORD                        PIC X(079).              00281006
                                                                        00290002
       FD  OUTPUT-FILE                                                  00300002
           RECORDING MODE IS F                                          00310002
           LABEL RECORDS OMITTED                                        00320002
           BLOCK CONTAINS 0 RECORDS                                     00330002
           DATA RECORD IS OUTPUT-RECORD.                                00340002
       01  OUTPUT-RECORD                       PIC X(097).              00350002
                                                                        00360002
       WORKING-STORAGE SECTION.                                         00370002
      ******************************************************************00380002
      **PROGRAM SWITCHES                                                00390002
      ******************************************************************00400002
       01  PS-SWITCH-AREAS.                                             00410002
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)  VALUE 'N'.    00420002
               88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.    00430002
               88  PS-NOT-END-OF-INPUT-FILE               VALUE 'N'.    00440002
                                                                        00450002
      ******************************************************************00460002
      **DATE TIME FIELDS                                                00470002
      ******************************************************************00480002
       01 DF-DATE-TIME-FIELDS.                                          00490002
          05  DF-ACCEPT-DATE.                                           00500002
              10  DF-ACCEPT-YEAR               PIC  9(02) VALUE ZEROS.  00510002
              10  DF-ACCEPT-MONTH              PIC  9(02) VALUE ZEROS.  00520002
              10  DF-ACCEPT-DAY                PIC  9(02) VALUE ZEROS.  00530002
          05  DF-ACCEPT-TIME                   PIC  X(08) VALUE SPACES. 00540002
          05  DF-FORMAT-DATE.                                           00550002
             10  FILLER                        PIC  9(02) VALUE 20.     00560002
             10  DF-FORMAT-DATE-YEAR           PIC  9(02) VALUE ZEROS.  00570002
             10  FILLER                        PIC  X(01) VALUE '-'.    00580002
             10  DF-FORMAT-DATE-MONTH          PIC  9(02) VALUE ZEROS.  00590002
             10  FILLER                        PIC  X(01) VALUE '-'.    00600002
             10  DF-FORMAT-DATE-DAY            PIC  9(02) VALUE ZEROS.  00610002
                                                                        00620002
      ******************************************************************00630002
      **PROGRAM COUNTERS                                                00640002
      ******************************************************************00650002
       01  PC-PROGRAM-COUNTERS.                                         00660002
           05  PC-INPUT-RECORDS-READ           PIC  9(09) VALUE ZEROS.  00670002
           05  PC-OUTPUT-RECORDS-WRITTEN       PIC  9(09) VALUE ZEROS.  00680002
                                                                        00850002
      ******************************************************************00860002
      **COPYBOOKS                                                       00870002
      ******************************************************************00880002
      ****INPUT RECORD LAYOUT - INVENTORY SNAPSHOT EXTRACT              00890002
           COPY TFRD060.                                                00900002
      ****OUTPUT RECORD LAYOUT- INVENTORY SNAPSHOT TO ML                00910002
           COPY TFRD053.                                                00920002
      ****KOHLS DATE ROUTINE COPYBOOK                                   00930002
           COPY DPWS500.                                                00940002
                                                                        01110002
       PROCEDURE DIVISION.                                              01120002
      ******************************************************************01130002
       000-MAINLINE-PROCESSING.                                         01140002
                                                                        01150002
           PERFORM 1000-INITIALIZATION.                                 01160002
           PERFORM 2000-PROCESS-RECORDS                                 01170002
                   UNTIL PS-END-OF-INPUT-FILE.                          01180002
           PERFORM 9999-WRAPUP.                                         01190002
           GOBACK.                                                      01200002
                                                                        01210002
      ******************************************************************01230002
      **INITIALIZATION ROUTINE                                          01240002
      ******************************************************************01250002
       1000-INITIALIZATION.                                             01260002
                                                                        01290002
           OPEN INPUT  INPUT-FILE                                       01300002
                OUTPUT OUTPUT-FILE.                                     01310002
                                                                        01320002
           PERFORM 9999-READ-INPUT-RECORD.                              01330002
                                                                        01340002
      ******************************************************************01360002
      **PROCESS INPUT RECORD TO CHECK ROWS                              01370002
      ******************************************************************01380002
       2000-PROCESS-RECORDS.                                            01390002
                                                                        01420002
           INITIALIZE TFRD053-INV-SNAPSHOT-FILE.                        01430002
                                                                        01440002
           MOVE TFRD060-BTCH-JLN-DAY      TO TFRD053-BATCH-NBR.         01470002
           MOVE TFRD060-TO-PSTG-LOC-NBR   TO TFRD053-TO-LOC-NBR.        01480002
           MOVE TFRD060-FR-LOC-NBR        TO TFRD053-DOC-FROM-LOC-NBR.  01490002
           MOVE TFRD060-XFER-SEQ-NBR      TO TFRD053-DOC-SEQ-NBR.       01500002
           MOVE TFRD060-CREATE-DATE(2:10) TO TFRD053-CREATE-DATE.       01510002
           MOVE TFRD060-XFER-LNE-NBR      TO TFRD053-LNE-NBR.           01520002
           MOVE TFRD060-SKU-NBR           TO TFRD053-SKU-NBR.           01530002
           MOVE TFRD060-ITM-QTY           TO TFRD053-ITEM-QTY.          01540002
           MOVE TFRD060-ITM-RTL-PRCE-AMT  TO TFRD053-ITEM-PRICE.        01550002
                                                                        01560002
           PERFORM 9999-WRITE-OUTPUT-RECORD.                            01570002
           PERFORM 9999-READ-INPUT-RECORD.                              01580002
                                                                        01590002
      ******************************************************************01990002
      **READ INPUT RECORD                                               02000002
      ******************************************************************02010002
       9999-READ-INPUT-RECORD.                                          02020002
                                                                        02030002
           READ INPUT-FILE INTO TFRD060-INV-SNAPSHOT-EXTRACT            02040002
                AT END                                                  02050002
                    SET PS-END-OF-INPUT-FILE TO TRUE                    02060002
           END-READ.                                                    02070002
                                                                        02080002
           IF  PS-NOT-END-OF-INPUT-FILE                                 02090002
               ADD +1                        TO PC-INPUT-RECORDS-READ   02100002
           END-IF.                                                      02110002
                                                                        02120002
      ****************************************************************  02140002
      **WRITE OUTPUT RECORD                                             02150002
      ****************************************************************  02160002
       9999-WRITE-OUTPUT-RECORD.                                        02170002
                                                                        02180002
           WRITE OUTPUT-RECORD FROM TFRD053-INV-SNAPSHOT-FILE.          02190002
                                                                        02200002
           ADD +1              TO PC-OUTPUT-RECORDS-WRITTEN.            02210002
                                                                        02220002
      ******************************************************************02240002
      **CLOSE INPUT FILES.                                              02250002
      ******************************************************************02260002
       9999-WRAPUP.                                                     02270002
                                                                        02280002
           CLOSE INPUT-FILE                                             02290002
                 OUTPUT-FILE.                                           02300002
                                                                        02310002
           PERFORM 9999-DISPLAY-STATISTICS.                             02320002
                                                                        02330002
      ******************************************************************02350002
      **DISPLAY PROGRAM STATS                                           02360002
      ******************************************************************02370002
       9999-DISPLAY-STATISTICS.                                         02380002
                                                                        02390002
           DISPLAY '*************************************************'. 02400002
           DISPLAY '* PROGRAM...........: TFKUT006'.                    02410002
           DISPLAY '* DATE..............: ' DF-ACCEPT-DATE.             02420002
           DISPLAY '* TIME..............: ' DF-ACCEPT-TIME.             02430002
           DISPLAY '* RECORDS READ......: ' PC-INPUT-RECORDS-READ.      02440002
           DISPLAY '* OUTPUT RECORDS WRITTEN...: '                      02450002
                                             PC-OUTPUT-RECORDS-WRITTEN. 02460002
           DISPLAY '*************************************************'. 02480002
                                                                        02490002
      ******************************************************************02500002
