       IDENTIFICATION DIVISION.                                         00010001
       PROGRAM-ID.    SUKUT198.                                         00020028
       AUTHOR.        JIM ARENDT.                                       00030001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040001
       DATE-WRITTEN.  SEPTEMBER, 2018.                                  00050028
       DATE-COMPILED.                                                   00060001
      ******************************************************************00070001
      *  DB2                                                            00080001
      * EXTRACT TRIPS THAT ARE IN STATUS DC "CLOSED AT DC' AS OF THE    00090040
      * RUN DATE                                                        00100028
      ******************************************************************00110001
      * DATE: 09/2018 MADE BY:JIM ARENDT  INITIAL IMPLEMENTATION        00120028
      ******************************************************************00130001
       ENVIRONMENT DIVISION.                                            00140001
       CONFIGURATION SECTION.                                           00150001
       SOURCE-COMPUTER.    IBM-3090.                                    00160001
       OBJECT-COMPUTER.    IBM-3090.                                    00170001
       INPUT-OUTPUT SECTION.                                            00180001
       FILE-CONTROL.                                                    00190001
                                                                        00200001
           SELECT INPUT-DATE-FILE                                       00210004
                  ASSIGN TO INP01.                                      00220001
                                                                        00230001
           SELECT OUTPUT-DC-FILE                                        00240028
                  ASSIGN TO OUT01.                                      00250001
                                                                        00260001
                                                                        00300001
       DATA DIVISION.                                                   00310001
       FILE SECTION.                                                    00320001
                                                                        00330001
       FD  INPUT-DATE-FILE                                              00340004
           RECORDING MODE IS F                                          00350001
           LABEL RECORDS OMITTED                                        00360001
           BLOCK CONTAINS 0 RECORDS                                     00370001
           DATA RECORD IS INPUT-DATE-RECORD.                            00380004
       01  INPUT-DATE-RECORD                   PIC  X(80).              00390004
                                                                        00400001
       FD  OUTPUT-DC-FILE                                               00410028
           RECORDING MODE IS F                                          00420001
           LABEL RECORDS OMITTED                                        00430001
           BLOCK CONTAINS 0 RECORDS                                     00440001
           DATA RECORD IS OUTPUT-DC-REC.                                00450028
       01  OUTPUT-DC-REC                       PIC  X(250).             00460028
                                                                        00470001
       WORKING-STORAGE SECTION.                                         00550001
                                                                        00560015
      ******************************************************************00570015
      * INPUT DATE FILE LAYOUT                                          00580027
      ******************************************************************00590015
       01  IN-DATE-FILE.                                                00600027
           05  IN-INPUT-DATE                   PIC  X(10) VALUE SPACE.  00610027
           05  FILLER                          PIC  X(70) VALUE SPACE.  00620015
                                                                        00630015
      ******************************************************************00640015
      * PROGRAM CONSTANTS                                               00650015
      ******************************************************************00660015
       01  PC-CONSTANTS-FIELDS.                                         00670015
           05  PC-PGM-NAME                     PIC  X(08) VALUE         00680015
                                                          'SUKUT198'.   00690028
           05  PC-MAX-RETRIES                  PIC S9(03) VALUE +3      00700015
                                                          COMP-3.       00710015
                                                                        00720015
      ******************************************************************00730015
      * PROGRAM VARIABLES                                               00740015
      ******************************************************************00750015
       01  PV-VARIABLE-FIELDS.                                          00760015
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACE.  00770015
           05  PV-DB2-OPERATION                PIC  X(30) VALUE SPACE.  00780015
           05  PV-RETRY-COUNTER                PIC S9(04) VALUE ZEROS   00790015
                                                          COMP SYNC.    00800015
           05  PV-EDIT-FIELD                   PIC  ZZZ,ZZZ,ZZ9.        00810026
           05  PV-TIMESTAMP-DATE               PIC  X(10) VALUE SPACES. 00820020
           05  PV-TIMESTAMP-TIME               PIC  X(08) VALUE SPACES. 00830023
                                                                        00900015
      ******************************************************************00910001
      * PROGRAM ACCUMULATORS                                            00920001
      ******************************************************************00930001
       01  PA-PROGRAM-ACCUMULATORS.                                     00940001
           05  PA-EXTRACT-RECORDS-READ         PIC  9(09) VALUE ZEROS.  00950001
           05  PA-OUTPUT-DC-WROTE              PIC  9(09) VALUE ZEROS.  00970028
                                                                        00990001
      ******************************************************************01000015
      * PROGRAM SWITCHES                                                01010015
      ******************************************************************01020015
       01  PS-SWITCH-AREAS.                                             01030015
           05  PS-END-OF-INPUT-FILE-SW         PIC  X(01) VALUE 'N'.    01040015
               88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.    01050015
               88  PS-NOT-END-OF-FILE                     VALUE 'N'.    01060015
           05  PS-EXTRACT-CSR-EOF-SW           PIC  X(01) VALUE 'N'.    01070015
               88  PS-EXTRACT-CSR-EOF                     VALUE 'Y'.    01080015
               88  PS-EXTRACT-CSR-NOT-EOF                 VALUE 'N'.    01090015
                                                                        01100015
      ******************************************************************01110001
      * ABEND INFORMATION                                               01120001
      ******************************************************************01130001
       01  ABEND-CODE                          PIC S9(04) VALUE ZEROS   01140001
                                                          COMP SYNC.    01150001
           88  ABEND-4001-WRITE-ERROR                     VALUE +4001.  01160001
           88  ABEND-4002-READ-ERROR                      VALUE +4002.  01170001
           88  ABEND-4003-CTRL-CARD-ERROR                 VALUE +4003.  01180001
           88  ABEND-4004-TABLE-ERROR                     VALUE +4004.  01190001
           88  ABEND-4005-OPEN-ERROR                      VALUE +4005.  01200001
           88  ABEND-4006-CLOSE-ERROR                     VALUE +4006.  01210001
           88  ABEND-4007-SEQUENCE-ERROR                  VALUE +4007.  01220001
           88  ABEND-4008-DATA-ERROR                      VALUE +4008.  01230001
           88  ABEND-4009-KEY-DATA-ERROR                  VALUE +4009.  01240001
           88  ABEND-4013-DB2-ERROR                       VALUE +4013.  01250001
           88  ABEND-4019-CAL-SUB-ERROR                   VALUE +4019.  01260001
           88  ABEND-4020-MQ-ERROR                        VALUE +4020.  01270001
           88  ABEND-4444-FORCED-ABEND                    VALUE +4444.  01280001
                                                                        01290008
      ******************************************************************01300001
      ****ABEND ROUTINE INFORMATION                                     01310001
           COPY SQLCA2.                                                 01320001
                                                                        01330001
      ****ABEND ROUTINE INFORMATION                                     01340001
           COPY DPWS004.                                                01350001
                                                                        01360001
      ****OUTPUT COPYBOOK                                               01370001
           COPY SURD163.                                                01380020
                                                                        01390001
      ******************************************************************01400001
      * TABLE INCLUSIONS                                                01410001
      ******************************************************************01420001
      ** TSUOBTR TABLE                                                  01430001
           EXEC SQL INCLUDE TSUOBTR  END-EXEC.                          01440001
                                                                        01450001
      ** TSUOBTS TABLE                                                  01460001
           EXEC SQL INCLUDE TSUOBTS  END-EXEC.                          01470001
                                                                        01480001
      ** TCNTRID TABLE                                                  01490001
           EXEC SQL INCLUDE TCNTRID  END-EXEC.                          01500001
                                                                        01510001
      ** XLT_LOC TABLE                                                  01520001
           EXEC SQL INCLUDE XLT00010 END-EXEC.                          01530001
                                                                        01540001
      ** DB2 TABLE INCLUSION                                            01580001
           EXEC SQL INCLUDE SQLCA    END-EXEC.                          01590001
                                                                        01600001
      ******************************************************************01610001
      * CURSOR TO RETRIEVE EXTRACT TRIPS THAT ARE IN 'DC'(CLOSED AT DC) 01620040
      * STATUS ON THE EXTRACT DATE                                      01630032
      ******************************************************************01640001
           EXEC SQL                                                     01650001
             DECLARE EXTRACT_CSR CURSOR FOR                             01660001
                SELECT  OBTR.TRLR_SRL_NBR                               01661029
                       ,OBTR.OTBND_TRIP_ID                              01662029
                       ,OBTR.DEST_OPER_UNT_ID                           01663029
                       ,OBTR.ORGN_OPER_UNT_ID                           01663132
                       ,OBTS.TRIP_STAT_CDE                              01664029
                       ,CHAR(DATE(OBTS.OTBND_STAT_TMST))                01665039
                       ,CHAR(TIME(OBTS.OTBND_STAT_TMST),JIS)            01665137
                       ,TRID.SCAC_CDE                                   01668029
                       ,TRID.TRLR_ID                                    01669035
                       ,LOC.E_BUS_IND                                   01669132
                  FROM  TSUOBTR OBTR                                    01669229
                       ,TSUOBTS OBTS                                    01670329
                       ,TCNTRID TRID                                    01670429
                       ,XLT_LOC LOC                                     01670532
                 WHERE OBTR.OTBND_TRIP_ID         = OBTS.OTBND_TRIP_ID  01670632
                   AND OBTR.TRLR_SRL_NBR          = TRID.TRLR_SRL_NBR   01670732
                   AND OBTR.TRIP_ACTV_CDE         = 'A'                 01670832
                   AND OBTS.TRIP_STAT_CDE         = 'DC'                01670931
                   AND OBTR.DEST_OPER_UNT_ID      = LOC.LOC_NBR         01671032
                   AND LOC.LOC_TYP_CDE            = 'DS'                01671132
                   AND NOT LOC.E_BUS_IND          = 'Y'                 01671232
                   AND DATE(OBTS.OTBND_STAT_TMST) = :IN-INPUT-DATE      01671531
                   AND OBTS.OTBND_STAT_TMST       =                     01671831
                      (SELECT MAX(OBTS1.OTBND_STAT_TMST)                01671929
                         FROM TSUOBTS OBTS1                             01672029
                        WHERE OBTS.OTBND_TRIP_ID  = OBTS1.OTBND_TRIP_ID)01672131
                 ORDER BY OBTR.DEST_OPER_UNT_ID                         01672241
                         ,OBTS.OTBND_STAT_TMST  DESC                    01672341
                         ,OBTR.TRLR_SRL_NBR                             01672441
               WITH UR                                                  01672529
          END-EXEC.                                                     01672629
                                                                        01672729
       PROCEDURE DIVISION.                                              02070001
      ******************************************************************02080001
       0000-MAINLINE-PROCESSING.                                        02090001
                                                                        02100001
           PERFORM 1000-INITIALIZATION.                                 02110001
                                                                        02120001
           PERFORM 2000-CREATE-EXTRACT-DATA                             02130001
             UNTIL PS-EXTRACT-CSR-EOF.                                  02140001
                                                                        02150001
           PERFORM 9000-WRAPUP.                                         02160001
           GOBACK.                                                      02170001
                                                                        02180001
      ******************************************************************02190001
      * INITIALIZATION ROUTINE                                          02200001
      ******************************************************************02210001
       1000-INITIALIZATION.                                             02220001
                                                                        02230001
           DISPLAY ' '.                                                 02240001
           DISPLAY '*******************************************'.       02250017
           DISPLAY '***  PROGRAM NAME:  '  PC-PGM-NAME.                 02260017
           DISPLAY ' '.                                                 02270001
                                                                        02280001
           OPEN INPUT  INPUT-DATE-FILE                                  02290004
                OUTPUT OUTPUT-DC-FILE.                                  02300028
                                                                        02320001
           PERFORM 1100-READ-INPUT-FILE.                                02330027
           IF PS-END-OF-INPUT-FILE                                      02340008
              SET PS-EXTRACT-CSR-EOF TO TRUE                            02350008
           ELSE                                                         02360008
              PERFORM 1200-OPEN-EXTRACT-CSR                             02370008
                                                                        02380001
              PERFORM 1300-FETCH-EXTRACT-CSR                            02390008
              IF PS-EXTRACT-CSR-EOF                                     02400008
                  DISPLAY ' '                                           02410008
                  DISPLAY '-- NO RECORDS FOUND TO PROCESS --'           02420026
                  DISPLAY ' '                                           02430008
              END-IF                                                    02440008
           END-IF.                                                      02450008
                                                                        02460001
      ******************************************************************02470008
      * READ THE INPUT RUN DATE FILE                                    02480027
      ******************************************************************02490008
       1100-READ-INPUT-FILE.                                            02500027
                                                                        02510008
           READ INPUT-DATE-FILE INTO IN-DATE-FILE                       02520027
              AT END                                                    02530008
                 SET PS-END-OF-INPUT-FILE TO TRUE.                      02540008
                                                                        02550008
           IF PS-END-OF-INPUT-FILE                                      02560008
               DISPLAY ' '                                              02570008
               DISPLAY 'INPUT DATE FILE MISSING'                        02580027
               DISPLAY ' '                                              02590008
           ELSE                                                         02600008
               DISPLAY ' '                                              02610008
               DISPLAY 'INPUT DATE - ' IN-INPUT-DATE                    02620027
               DISPLAY ' '                                              02630008
           END-IF.                                                      02640008
                                                                        02650008
      ******************************************************************02660001
      * OPEN THE EXTRACT CURSOR                                         02670015
      ******************************************************************02680001
       1200-OPEN-EXTRACT-CSR.                                           02690001
                                                                        02700001
           EXEC SQL                                                     02710001
               OPEN EXTRACT_CSR                                         02720027
           END-EXEC.                                                    02730001
                                                                        02740001
           EVALUATE TRUE                                                02750001
               WHEN SQLCODE = +0                                        02760001
               WHEN SQLCODE = -904                                      02770001
               WHEN SQLCODE = -911                                      02780001
               WHEN SQLCODE = -913                                      02790001
                    CONTINUE                                            02800001
                                                                        02810001
               WHEN SQLWARN0 NOT EQUAL SPACE                            02820001
               WHEN SQLCODE NOT EQUAL ZERO                              02830001
                    MOVE '1200-OPEN-EXTRACT-CSR'                        02840001
                                           TO PV-PARAGRAPH-NAME         02850001
                    MOVE 'ERROR ON OPEN OF EXTRACT_CSR'                 02860001
                                           TO PV-DB2-OPERATION          02870001
                    SET ABEND-4005-OPEN-ERROR TO TRUE                   02880001
                    PERFORM 9200-SQL-ABEND                              02890001
                                                                        02900001
           END-EVALUATE.                                                02910001
                                                                        02920001
      ******************************************************************02930001
      * RETRIEVE THE EXTRACT TRIP DATA                                  02940001
      ******************************************************************02950001
       1300-FETCH-EXTRACT-CSR.                                          02960001
                                                                        02970001
           PERFORM                                                      02980012
               WITH TEST AFTER                                          02990012
               VARYING PV-RETRY-COUNTER                                 03000012
               FROM 1 BY 1                                              03010012
               UNTIL (PV-RETRY-COUNTER > PC-MAX-RETRIES)                03020012
                   OR (SQLCODE = +0                                     03030012
                   OR  SQLCODE = +100)                                  03040012
                                                                        03050020
               EXEC SQL                                                 03060012
                  FETCH EXTRACT_CSR                                     03070012
                    INTO :CNTRID-TRLR-SRL-NBR                           03071035
                        ,:SUOBTR-OTBND-TRIP-ID                          03072035
                        ,:SUOBTR-DEST-OPER-UNT-ID                       03091035
                        ,:SUOBTR-ORGN-OPER-UNT-ID                       03092035
                        ,:SUOBTS-TRIP-STAT-CDE                          03092135
                        ,:PV-TIMESTAMP-DATE                             03093135
                        ,:PV-TIMESTAMP-TIME                             03093235
                        ,:CNTRID-SCAC-CDE                               03100035
                        ,:CNTRID-TRLR-ID                                03100135
                        ,:XLT00010-E-BUS-IND                            03101035
               END-EXEC                                                 03180012
                                                                        03190032
               EVALUATE TRUE                                            03200012
                   WHEN SQLCODE = ZERO                                  03210012
                       ADD 1                  TO PA-EXTRACT-RECORDS-READ03220012
                                                                        03230012
                   WHEN SQLCODE = +100                                  03240012
                       SET PS-EXTRACT-CSR-EOF TO TRUE                   03250012
                                                                        03260012
                   WHEN SQLCODE = -904                                  03270012
                   WHEN SQLCODE = -911                                  03280012
                   WHEN SQLCODE = -913                                  03290012
                       CONTINUE                                         03300012
                                                                        03310001
                   WHEN SQLWARN0 NOT EQUAL SPACE                        03320012
                   WHEN SQLCODE NOT EQUAL ZERO                          03330012
                       MOVE '1300-FETCH-EXTRACT-CSR'                    03340012
                                                 TO PV-PARAGRAPH-NAME   03350012
                       MOVE 'ERROR ON FETCHING EXTRACT_CSR'             03360012
                                                 TO PV-DB2-OPERATION    03370012
                       SET ABEND-4002-READ-ERROR TO TRUE                03380012
                       PERFORM 9200-SQL-ABEND                           03390012
                                                                        03400001
               END-EVALUATE                                             03410013
           END-PERFORM.                                                 03420013
                                                                        03430001
           IF (PV-RETRY-COUNTER > PC-MAX-RETRIES)                       03440015
               MOVE '1300-FETCH-EXTRACT-CSR'    TO PV-PARAGRAPH-NAME    03450012
               MOVE 'MAX RETRIES EXCEEDED'      TO PV-DB2-OPERATION     03460012
               SET ABEND-4002-READ-ERROR        TO TRUE                 03470012
               PERFORM 9200-SQL-ABEND                                   03480012
           END-IF.                                                      03490012
                                                                        03500012
      ******************************************************************03510001
      * PROCESSING DRIVER                                               03520001
      ******************************************************************03530001
       2000-CREATE-EXTRACT-DATA.                                        03540001
                                                                        03550001
           PERFORM 2300-PROCESS-RECORDS.                                03560001
                                                                        03570001
           PERFORM 1300-FETCH-EXTRACT-CSR.                              03580001
                                                                        03590001
      ******************************************************************03600001
      * POPULATE THE OUTPUT RECORD                                      03610001
      ******************************************************************03620001
       2300-PROCESS-RECORDS.                                            03630001
                                                                        03640001
           INITIALIZE SU163-EXTRACT-RECORD.                             03650020
           MOVE SUOBTR-ORGN-OPER-UNT-ID   TO SU163-ORGN-OPER-UNT-ID.    03660021
           MOVE SUOBTR-DEST-OPER-UNT-ID   TO SU163-DEST-OPER-UNT-ID.    03670021
           MOVE SUOBTR-OTBND-TRIP-ID      TO SU163-OTBND-TRIP-ID.       03680021
           MOVE SUOBTS-TRIP-STAT-CDE      TO SU163-TRIP-STAT-CDE.       03690021
           MOVE XLT00010-E-BUS-IND        TO SU163-E-BUS.               03700021
           MOVE CNTRID-SCAC-CDE           TO SU163-SCAC-CDE.            03710020
           MOVE CNTRID-TRLR-ID            TO SU163-TRLR-ID.             03720020
           MOVE CNTRID-TRLR-SRL-NBR       TO SU163-TRLR-SRL-NBR.        03730020
           IF PV-TIMESTAMP-DATE > SPACES                                03740025
              MOVE PV-TIMESTAMP-DATE      TO SU163-TRIP-STAT-DATE       03750025
              MOVE PV-TIMESTAMP-TIME      TO SU163-TRIP-STAT-TIME       03780025
           END-IF.                                                      03790025
                                                                        03800001
           PERFORM 2400-WRITE-RECORD.                                   03810028
                                                                        03820001
      ******************************************************************03830003
      * WRITE THE OUTPUT RECORD                                         03840028
      *  - WRITE RECORD TO OUTPUT                                       03850028
      ******************************************************************03870003
       2400-WRITE-RECORD.                                               03880028
                                                                        03890003
           WRITE OUTPUT-DC-REC  FROM SU163-EXTRACT-RECORD.              03931028
                                                                        03932028
           ADD +1              TO PA-OUTPUT-DC-WROTE.                   03933028
                                                                        03970004
      ******************************************************************03980001
      * CLOSE THE PRODUCT CURSOR                                        03990001
      ******************************************************************04000001
       3000-CLOSE-EXTRACT-CSR.                                          04010001
                                                                        04020001
           EXEC SQL                                                     04030001
              CLOSE EXTRACT_CSR                                         04040001
           END-EXEC.                                                    04050001
                                                                        04060001
           EVALUATE TRUE                                                04070001
               WHEN SQLCODE = +0                                        04080001
               WHEN SQLCODE = -904                                      04090001
               WHEN SQLCODE = -911                                      04100001
               WHEN SQLCODE = -913                                      04110001
                    CONTINUE                                            04120001
                                                                        04130001
               WHEN SQLWARN0 NOT EQUAL SPACE                            04140001
               WHEN SQLCODE NOT EQUAL ZERO                              04150001
                    MOVE '3000-CLOSE-EXTRACT-CSR'                       04160001
                                                TO PV-PARAGRAPH-NAME    04170001
                    MOVE 'ERROR ON CLOSE OF EXTRACT_CSR'                04180001
                                                TO PV-DB2-OPERATION     04190001
                    SET ABEND-4006-CLOSE-ERROR  TO TRUE                 04200001
                    PERFORM 9200-SQL-ABEND                              04210001
                                                                        04220001
           END-EVALUATE.                                                04230001
                                                                        04240001
      ******************************************************************04450001
      * CLOSE INPUT/OUTPUT FILES AND DISPLAY PROGRAM COUNTS             04460015
      ******************************************************************04470001
       9000-WRAPUP.                                                     04480001
                                                                        04490001
           CLOSE INPUT-DATE-FILE                                        04500008
                 OUTPUT-DC-FILE.                                        04510033
                                                                        04530001
           PERFORM 3000-CLOSE-EXTRACT-CSR.                              04540001
                                                                        04550001
           PERFORM 9100-DISPLAY-COUNTS.                                 04560001
                                                                        04570001
      ******************************************************************04580001
      * DISPLAY PROGRAM COUNTS                                          04590001
      ******************************************************************04600001
       9100-DISPLAY-COUNTS.                                             04610001
                                                                        04620001
           MOVE PA-EXTRACT-RECORDS-READ        TO PV-EDIT-FIELD.        04630028
           DISPLAY ' RECORDS READ....: '          PV-EDIT-FIELD.        04640028
                                                                        04650003
           MOVE PA-OUTPUT-DC-WROTE             TO PV-EDIT-FIELD.        04660028
           DISPLAY ' RECORDS WROTE...: '          PV-EDIT-FIELD.        04670028
                                                                        04680003
           DISPLAY ' '.                                                 04750001
           DISPLAY '*******************************************'.       04760017
           DISPLAY ' '.                                                 04770001
                                                                        04780001
      ******************************************************************04790001
      * ABEND ROUTINE FOR BAD SQL CALLS                                 04800001
      ******************************************************************04810001
       9200-SQL-ABEND.                                                  04820001
                                                                        04830001
           DISPLAY '** ABEND     **'.                                   04840001
           DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME.                    04850001
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04860001
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               04870001
           DISPLAY '** SQLCODE   ** = ' SQLCODE.                        04880001
                                                                        04890001
      ***************************************************************** 04900001
      *    COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED  04910001
      *    TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.   04920001
      ***************************************************************** 04930001
           COPY DPPD004.                                                04940001
                                                                        04950001
           CALL 'ILBOABN0' USING ABEND-CODE.                            04960001
