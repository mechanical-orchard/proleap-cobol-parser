      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.             IMKRP076.                                        
       AUTHOR.                 COGNIZANT.                                       
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            7/20/2010.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * Remarks                                                        *        
      * -------                                                        *        
      *                                                                *        
      * Title        - Updated clean up CSV file validation            *        
      *                                                                *        
      * Description  - This program will read the updated CSV file.    *        
      * The CSV file contains new alternate hierarchy fields and       *        
      * fashion pyramid.                                               *        
      *                                                                *        
      * The new alternate hierarchy fields will be validated against   *        
      * DFM databse.The valid attributes will be Written to valid file *        
      * and invalid will be written to error report file               *        
      *                                                                *        
      *  Change history                                                *        
      ******************************************************************        
      * WR Number     Programmer       Date                            *        
      * ---------------------------------------------------------------*        
      * 35681 & 37044 COGNIZANT        20-Jul-2010                     *        
      * Original Version                                               *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CSV-UPD-INP-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CSV-ERROR-REP                                                 
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT CSV-UPD-VALID-FILE                                            
               ASSIGN TO OUT02.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CSV-UPD-INP-FILE                                                     
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  CSV-UPD-INP-REC                    PIC X(791).                       
                                                                                
       FD  CSV-ERROR-REP                                                        
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  CSV-ERROR-REP-REC                  PIC X(791).                       
                                                                                
       FD  CSV-UPD-VALID-FILE                                                   
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  CSV-UPD-VALID-REC                  PIC X(80).                        
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
      *****************************************************************         
      *  Copybook for vendor style characteristic value                *        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TVSCHVL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  Copybook for vendor style characteristic value-IMT_RPT_HIER_VAL        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE IMT00014                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  Copybook for merchandise area vendor style table              *        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDARVS                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  Copybook for merchandise area vendor style table              *        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TDCVSVL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  Copybook for department class vendor style value table                 
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * Copybook for SQL communication area                           *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  Copybook of working storage for SQL abend routine                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      *  File Varialble declaration                                   *         
      *****************************************************************         
       01  FV-FILE-VARIABLES.                                                   
                                                                                
           03  FV-VENDOR-STYLE-ID      PIC  S9(9) COMP.                         
           03  FV-SUB-CLASS-MDSER-ID   PIC  S9(6)V COMP-3.                      
           03  FV-FSHN-PYRMD-SEQ-NBR   PIC  S9(4) COMP.                         
           03  FV-CTG-SEQ-NBR          PIC  S9(4) COMP.                         
           03  FV-MAJ-CTG-SEQ-NBR      PIC  S9(4) COMP.                         
           03  FV-MTRL-SEQ-NBR         PIC  S9(4) COMP.                         
           03  FV-PRIM-TRND-SEQ-NBR    PIC  S9(4) COMP.                         
           03  FV-SEC-TRND-SEQ-NBR     PIC  S9(4) COMP.                         
           03  FV-ATTR-1-SEQ-NBR       PIC  S9(4) COMP.                         
           03  FV-ATTR-2-SEQ-NBR       PIC  S9(4) COMP.                         
           03  FV-NW-SLHTT-SEQ-NBR     PIC  S9(4) COMP.                         
           03  FILLER                  PIC  X(56).                              
                                                                                
      ******************************************************************        
      *  PC-program constants                                          *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME             PIC  X(08)  VALUE 'IMKRP076'.            
           05  PC-FASHION-PYRAMID-CDE  PIC  X(02)  VALUE '35'.                  
           05  PC-CATEGORY-CDE         PIC  X(02)  VALUE '36'.                  
           05  PC-MAJOR-CATEGORY-CDE   PIC  X(02)  VALUE '37'.                  
           05  PC-NEW-SILHOUETTE-CDE   PIC  X(02)  VALUE '38'.                  
           05  PC-MATERIAL-CDE         PIC  X(02)  VALUE '39'.                  
           05  PC-CORPORATE-CDE        PIC  X(02)  VALUE '40'.                  
           05  PC-SECONDARY-CDE        PIC  X(02)  VALUE '41'.                  
           05  PC-ATTRIBUTE-1-CDE      PIC  X(02)  VALUE '42'.                  
           05  PC-ATTRIBUTE-2-CDE      PIC  X(02)  VALUE '43'.                  
                                                                                
      *****************************************************************         
      *  Report workarea                                              *         
      *****************************************************************         
       01  RV-DETAIL-LINE.                                                      
           05  RV-GMA-NBR              PIC X(02).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-DMA-NBR              PIC X(02).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-BUYER-NUMBER         PIC X(03).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-BUYER-NAME           PIC X(31).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-DEPT                 PIC X(05).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-CLASS-NBR            PIC X(05).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-SUB-CLASS-NBR        PIC X(05).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-VENDOR-STYLE-ID      PIC X(10).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-STYLE-NUMBER         PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-STYLE-DESC           PIC X(30).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-BRAND-DESC           PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-VENDOR-NAME          PIC X(200).                              
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-EOH-UNITS            PIC -ZZZ,ZZZ,ZZ9.                        
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-STYLE-CREATE-DT      PIC X(10).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-CORP-LAST-RCPT-DT    PIC X(08).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-HIERARCHY-DESC       PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-FUNCTION-DESC        PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-SILHOUETTE-DESC      PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-BODY-STYLE-DESC      PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-FABRICATION-DESC     PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-LBL-SEQ-NBR          PIC X(05).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-SUB-CL-MDSEAR-ID     PIC X(07).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-HIERARCHY-NBR        PIC X(05).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-FUNCTION-NBR         PIC X(05).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-SILHOUETTE-NBR       PIC X(05).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-BODY-STYLE-NBR       PIC X(05).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-FABRICATION-NBR      PIC X(05).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-MAJOR-CATEGORY-DESC  PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-CATEGORY-DESC        PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-NEW-SILHOUETTE-DESC  PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-MATERIAL-DESC        PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-CORPORATE-TREND-DESC PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-SECONDARY-TREND-DESC PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-ATTRIBUTE-1-DESC     PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-ATTRIBUTE-2-DESC     PIC X(20).                               
           05  FILLER                  PIC X(02)  VALUE '| '.                   
           05  RV-FASHION-PYRAMID      PIC X(20).                               
           05  FILLER                  PIC X(42)  VALUE SPACES.                 
                                                                                
      *****************************************************************         
      *  Switches                                                      *        
      *****************************************************************         
       01  PS-RVOGRAM-SWITCHES.                                                 
           05  PS-INPUT-IND            PIC X(01)   VALUE 'N'.                   
               88  END-OF-INPUT                    VALUE 'Y'.                   
           05  PS-CSV-ERROR-SW         PIC X(01)   VALUE 'N'.                   
               88  PS-CSV-ERROR                    VALUE 'Y'.                   
               88  PS-NO-CSV-ERROR                 VALUE 'N'.                   
           05  PS-CATEGORY-VALID-SW    PIC X(01)   VALUE 'N'.                   
               88  PS-CATEGORY-VALID               VALUE 'Y'.                   
               88  PS-CATEGORY-INVALID             VALUE 'N'.                   
           05  PS-FP-VALID-SW          PIC X(01)   VALUE 'N'.                   
               88  PS-VALID-FP                     VALUE 'Y'.                   
               88  PS-INVALID-FP                   VALUE 'N'.                   
           05  PS-CAT-DIV-DEPT-SW      PIC X(01)   VALUE 'N'.                   
               88  CAT-VALID-FOR-DIV-DEPT          VALUE 'Y'.                   
               88  CAT-NOT-VALID-FOR-DIV-DEPT      VALUE 'N'.                   
                                                                                
      *****************************************************************         
      *  General workarea                                             *         
      *****************************************************************         
       01  PV-PROGRAM-VARIABLES.                                                
                                                                                
           03  PV-PARAGRAPH-NAME       PIC  X(30).                              
           03  PV-DUMMY                PIC  S9(2)V COMP-3 VALUE 0.              
           03  PV-DB2-OPER-ATTEMPTED   PIC  X(30).                              
           03  PV-CSV-UPD-RECS-READ    PIC  S9(9)V COMP-3 VALUE 0.              
           03  PV-VALID-RECS-CNT       PIC  S9(9)V COMP-3 VALUE 0.              
           03  PV-CSV-ERR-RECS-CNT     PIC  S9(9)V COMP-3 VALUE 0.              
           03  PV-CSV-UPDATE-REC       PIC  X(791) VALUE SPACES.                
           03  PV-EOH-UNITS-ED         PIC -ZZZ,ZZZ,ZZ9.                        
           03  PV-EOH-UNITS-TEMP       PIC  9(12).                              
           03  PV-EOH-UNITS-TEMP1      PIC  X(12).                              
           03  PV-NUM-DEPT-NBR         PIC  9(03).                              
           03  PV-RPT-HIER-QUOTE       PIC  X(20).                              
           03  PV-RPT-HIER-ATTR        PIC  X(20).                              
           03  PV-STR-LEN              PIC  9(02).                              
           03  PV-SS-SUB1              PIC  9(02).                              
           03  PV-SS-SUB2              PIC  9(02).                              
           03  PV-ATTR-NAME            PIC  X(20).                              
           03  PV-QUOTES-ERROR         PIC  X(150).                             
                                                                                
      *****************************************************************         
      *  Work area copy book for input file                           *         
      *****************************************************************         
       01  PV-DETAIL-LINE.                                                      
           05  PV-GMA-NBR              PIC X(02).                               
           05  PV-DMA-NBR              PIC X(02).                               
           05  PV-BUYER-NUMBER         PIC X(03).                               
           05  PV-BUYER-NAME           PIC X(31).                               
           05  PV-DEPT                 PIC X(05).                               
           05  PV-CLASS-NBR            PIC X(05).                               
           05  PV-SUB-CLASS-NBR        PIC X(05).                               
           05  PV-VENDOR-STYLE-ID      PIC X(09).                               
           05  PV-STYLE-NUMBER         PIC X(20).                               
           05  PV-STYLE-DESC           PIC X(30).                               
           05  PV-BRAND-DESC           PIC X(20).                               
           05  PV-VENDOR-NAME          PIC X(200).                              
           05  PV-EOH-UNITS            PIC 9(12).                               
           05  PV-STYLE-CREATE-DT      PIC X(10).                               
           05  PV-CORP-LAST-RCPT-DT    PIC X(08).                               
           05  PV-HIERARCHY-DESC       PIC X(20).                               
           05  PV-FUNCTION-DESC        PIC X(20).                               
           05  PV-SILHOUETTE-DESC      PIC X(20).                               
           05  PV-BODY-STYLE-DESC      PIC X(20).                               
           05  PV-FABRICATION-DESC     PIC X(20).                               
           05  PV-LBL-SEQ-NBR          PIC X(05).                               
           05  PV-SUB-CL-MDSEAR-ID     PIC X(06).                               
           05  PV-HIERARCHY-NBR        PIC X(05).                               
           05  PV-FUNCTION-NBR         PIC X(05).                               
           05  PV-SILHOUETTE-NBR       PIC X(05).                               
           05  PV-BODY-STYLE-NBR       PIC X(05).                               
           05  PV-FABRICATION-NBR      PIC X(05).                               
           05  PV-MAJOR-CATEGORY-DESC  PIC X(20).                               
           05  PV-CATEGORY-DESC        PIC X(20).                               
           05  PV-NEW-SILHOUETTE-DESC  PIC X(20).                               
           05  PV-MATERIAL-DESC        PIC X(20).                               
           05  PV-CORPORATE-TREND-DESC PIC X(20).                               
           05  PV-SECONDARY-TREND-DESC PIC X(20).                               
           05  PV-ATTRIBUTE-1-DESC     PIC X(20).                               
           05  PV-ATTRIBUTE-2-DESC     PIC X(20).                               
           05  PV-FASHION-PYRAMID      PIC X(20).                               
                                                                                
      *****************************************************************         
      *  General workarea sql abend                                    *        
      *****************************************************************         
       01  ABEND-CODE                    PIC S9(04) VALUE +0 COMP SYNC.         
           88  ABEND-4013                           VALUE +4013.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-----------------------------------------------------------------        
      *  MAIN LINE OF THE PROGRAM PERFORMS THE                                  
      *  INITIALIZATION ROUTINE AND CONTROLS THE PROCESSING OF THE PGM.         
      *-----------------------------------------------------------------        
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 0100-HOUSEKEEPING.                                           
                                                                                
           PERFORM 1000-PROCESS-INPUT-FILE                                      
             UNTIL END-OF-INPUT.                                                
                                                                                
           PERFORM 9000-EOJ.                                                    
           GOBACK.                                                              
                                                                                
      *-----------------------------------------------------------------        
      *  OPEN FILES AND PERFORM FIRST READ FROM INPUT FILE                      
      *-----------------------------------------------------------------        
       0100-HOUSEKEEPING.                                                       
                                                                                
           MOVE "*0100-HOUSEKEEPING  *"     TO PV-PARAGRAPH-NAME                
                                                                                
           INITIALIZE                          PV-PROGRAM-VARIABLES             
                                               RV-DETAIL-LINE                   
                                               PV-DETAIL-LINE.                  
                                                                                
           OPEN INPUT                          CSV-UPD-INP-FILE                 
                OUTPUT                         CSV-ERROR-REP                    
                                               CSV-UPD-VALID-FILE.              
                                                                                
           PERFORM 6000-READ-CSV-UPDATE-FILE.                                   
                                                                                
           IF  END-OF-INPUT                                                     
               DISPLAY '*** '                                                   
               DISPLAY '*** IMKRP076 - INPUT CSV FILE IS EMPTY'                 
               DISPLAY '*** '                                                   
           END-IF.                                                              
                                                                                
      *-----------------------------------------------------------------        
      *  PROCESS ALL INPUT RECORDS OF THE CSV UPDATED FILE                      
      *  RECORDS UNTIL END OF INPUT FILE.                                       
      *-----------------------------------------------------------------        
       1000-PROCESS-INPUT-FILE.                                                 
                                                                                
           MOVE "*1000-PROCESS-INPUT-FILE "   TO PV-PARAGRAPH-NAME              
                                                                                
           SET PS-NO-CSV-ERROR                 TO TRUE                          
           PERFORM 1100-UNSTRING-CSV-UPD-INFILE                                 
           PERFORM 1150-HANDLE-DBL-QUOTE                                        
                                                                                
           PERFORM 1200-VALIDATE-FASH-PYR                                       
           PERFORM 1300-VALIDATE-ALT-HIER                                       
                                                                                
           IF  PS-CSV-ERROR                                                     
           OR  CAT-NOT-VALID-FOR-DIV-DEPT                                       
               PERFORM 2400-LOAD-ERROR-REPORT                                   
               PERFORM 7000-WRITE-CSV-ERROR-REC                                 
           END-IF                                                               
                                                                                
           INITIALIZE                             FV-FILE-VARIABLES             
                                                                                
           IF  PS-CATEGORY-VALID                                                
               MOVE MDARVS-FSHN-PYRMD-SEQ-NBR  TO FV-FSHN-PYRMD-SEQ-NBR         
               MOVE MDARVS-CTG-SEQ-NBR         TO FV-CTG-SEQ-NBR                
               MOVE MDARVS-MAJ-CTG-SEQ-NBR     TO FV-MAJ-CTG-SEQ-NBR            
               MOVE MDARVS-MTRL-SEQ-NBR        TO FV-MTRL-SEQ-NBR               
               MOVE MDARVS-PRIM-TRND-SEQ-NBR   TO FV-PRIM-TRND-SEQ-NBR          
               MOVE MDARVS-SEC-TRND-SEQ-NBR    TO FV-SEC-TRND-SEQ-NBR           
               MOVE MDARVS-ATTR-1-SEQ-NBR      TO FV-ATTR-1-SEQ-NBR             
               MOVE MDARVS-ATTR-2-SEQ-NBR      TO FV-ATTR-2-SEQ-NBR             
               MOVE MDARVS-NW-SLHTT-SEQ-NBR    TO FV-NW-SLHTT-SEQ-NBR           
           END-IF                                                               
                                                                                
           IF  PS-VALID-FP                                                      
               MOVE MDARVS-FSHN-PYRMD-SEQ-NBR  TO FV-FSHN-PYRMD-SEQ-NBR         
           END-IF                                                               
                                                                                
           IF  PS-CATEGORY-VALID                                                
           OR  PS-VALID-FP                                                      
               COMPUTE FV-VENDOR-STYLE-ID =                                     
                       FUNCTION NUMVAL(PV-VENDOR-STYLE-ID)                      
               END-COMPUTE                                                      
                                                                                
               COMPUTE FV-SUB-CLASS-MDSER-ID =                                  
                       FUNCTION NUMVAL(PV-SUB-CL-MDSEAR-ID)                     
               END-COMPUTE                                                      
                                                                                
               PERFORM 8000-WRITE-VALID-FILE                                    
           END-IF                                                               
                                                                                
           PERFORM 6000-READ-CSV-UPDATE-FILE                                    
           .                                                                    
                                                                                
       1100-UNSTRING-CSV-UPD-INFILE.                                            
      *-----------------------------------------------------------------        
      * UNSTRING THE UPDATED CSV FILE INTO THE PV VARIABLES                     
      *-----------------------------------------------------------------        
                                                                                
           MOVE "*1100-UNSTRING-CSV-UPD-INFILE" TO PV-PARAGRAPH-NAME            
                                                                                
           INITIALIZE  PV-DETAIL-LINE                                           
                                                                                
           UNSTRING PV-CSV-UPDATE-REC DELIMITED BY  '|' OR 'º'                  
               INTO PV-GMA-NBR                                                  
                    PV-DMA-NBR                                                  
                    PV-BUYER-NUMBER                                             
                    PV-BUYER-NAME                                               
                    PV-DEPT                                                     
                    PV-CLASS-NBR                                                
                    PV-SUB-CLASS-NBR                                            
                    PV-VENDOR-STYLE-ID                                          
                    PV-STYLE-NUMBER                                             
                    PV-STYLE-DESC                                               
                    PV-BRAND-DESC                                               
                    PV-VENDOR-NAME                                              
                    PV-EOH-UNITS                                                
                    PV-STYLE-CREATE-DT                                          
                    PV-CORP-LAST-RCPT-DT                                        
                    PV-HIERARCHY-DESC                                           
                    PV-FUNCTION-DESC                                            
                    PV-SILHOUETTE-DESC                                          
                    PV-BODY-STYLE-DESC                                          
                    PV-FABRICATION-DESC                                         
                    PV-LBL-SEQ-NBR                                              
                    PV-SUB-CL-MDSEAR-ID                                         
                    PV-HIERARCHY-NBR                                            
                    PV-FUNCTION-NBR                                             
                    PV-SILHOUETTE-NBR                                           
                    PV-BODY-STYLE-NBR                                           
                    PV-FABRICATION-NBR                                          
                    PV-MAJOR-CATEGORY-DESC                                      
                    PV-CATEGORY-DESC                                            
                    PV-NEW-SILHOUETTE-DESC                                      
                    PV-MATERIAL-DESC                                            
                    PV-CORPORATE-TREND-DESC                                     
                    PV-SECONDARY-TREND-DESC                                     
                    PV-ATTRIBUTE-1-DESC                                         
                    PV-ATTRIBUTE-2-DESC                                         
                    PV-FASHION-PYRAMID.                                         
                                                                                
       1150-HANDLE-DBL-QUOTE.                                                   
      *-----------------------------------------------------------------        
      * Handle Double Quotes in the input file                                  
      *-----------------------------------------------------------------        
                                                                                
           MOVE "*1150-HANDLE-DBL-QUOTE "   TO PV-PARAGRAPH-NAME                
                                                                                
      *-- Remove Double quotes from PV-MAJOR-CATEGORY-DESC                      
                                                                                
           IF  PV-MAJOR-CATEGORY-DESC(1:1) = '"'                                
               MOVE "MAJOR CATEGORY"        TO PV-ATTR-NAME                     
               INITIALIZE                      PV-RPT-HIER-QUOTE                
               MOVE PV-MAJOR-CATEGORY-DESC  TO PV-RPT-HIER-QUOTE                
               INITIALIZE                      PV-MAJOR-CATEGORY-DESC           
               PERFORM 1160-REMOVE-DOUBLE-QUOTE                                 
               MOVE PV-RPT-HIER-ATTR        TO PV-MAJOR-CATEGORY-DESC           
           END-IF.                                                              
                                                                                
      *-- Remove Double quotes from PV-CATEGORY-DESC                            
                                                                                
           IF  PV-CATEGORY-DESC(1:1) = '"'                                      
               MOVE "CATEGORY"              TO PV-ATTR-NAME                     
               INITIALIZE                      PV-RPT-HIER-QUOTE                
               MOVE PV-CATEGORY-DESC        TO PV-RPT-HIER-QUOTE                
               INITIALIZE                      PV-CATEGORY-DESC                 
               PERFORM 1160-REMOVE-DOUBLE-QUOTE                                 
               MOVE PV-RPT-HIER-ATTR        TO PV-CATEGORY-DESC                 
           END-IF.                                                              
                                                                                
      *-- Remove Double quotes from PV-NEW-SILHOUETTE-DESC                      
                                                                                
           IF  PV-NEW-SILHOUETTE-DESC(1:1) = '"'                                
               MOVE "NEW SILHOUTE"          TO PV-ATTR-NAME                     
               INITIALIZE                      PV-RPT-HIER-QUOTE                
               MOVE PV-NEW-SILHOUETTE-DESC  TO PV-RPT-HIER-QUOTE                
               INITIALIZE                      PV-NEW-SILHOUETTE-DESC           
               PERFORM 1160-REMOVE-DOUBLE-QUOTE                                 
               MOVE PV-RPT-HIER-ATTR        TO PV-NEW-SILHOUETTE-DESC           
           END-IF.                                                              
                                                                                
      *-- Remove Double quotes from PV-MATERIAL-DESC                            
                                                                                
           IF  PV-MATERIAL-DESC(1:1) = '"'                                      
               MOVE "MATERIAL"              TO PV-ATTR-NAME                     
               INITIALIZE                      PV-RPT-HIER-QUOTE                
               MOVE PV-MATERIAL-DESC        TO PV-RPT-HIER-QUOTE                
               INITIALIZE                      PV-MATERIAL-DESC                 
               PERFORM 1160-REMOVE-DOUBLE-QUOTE                                 
               MOVE PV-RPT-HIER-ATTR        TO PV-MATERIAL-DESC                 
           END-IF.                                                              
                                                                                
      *-- Remove Double quotes from PV-CORPORATE-TREND-DESC                     
                                                                                
           IF  PV-CORPORATE-TREND-DESC(1:1) = '"'                               
               MOVE "PRIMARY TREND"         TO PV-ATTR-NAME                     
               INITIALIZE                      PV-RPT-HIER-QUOTE                
               MOVE PV-CORPORATE-TREND-DESC TO PV-RPT-HIER-QUOTE                
               INITIALIZE                      PV-CORPORATE-TREND-DESC          
               PERFORM 1160-REMOVE-DOUBLE-QUOTE                                 
               MOVE PV-RPT-HIER-ATTR        TO PV-CORPORATE-TREND-DESC          
           END-IF.                                                              
                                                                                
      *-- Remove Double quotes from PV-SECONDARY-TREND-DESC                     
                                                                                
           IF  PV-SECONDARY-TREND-DESC(1:1) = '"'                               
               MOVE "SECONDARY TREND"       TO PV-ATTR-NAME                     
               INITIALIZE                      PV-RPT-HIER-QUOTE                
               MOVE PV-SECONDARY-TREND-DESC TO PV-RPT-HIER-QUOTE                
               INITIALIZE                      PV-SECONDARY-TREND-DESC          
               PERFORM 1160-REMOVE-DOUBLE-QUOTE                                 
               MOVE PV-RPT-HIER-ATTR        TO PV-SECONDARY-TREND-DESC          
           END-IF.                                                              
                                                                                
      *-- Remove Double quotes from PV-ATTRIBUTE-1-DESC                         
                                                                                
           IF  PV-ATTRIBUTE-1-DESC(1:1) = '"'                                   
               MOVE "ATTRIBUTE-1"           TO PV-ATTR-NAME                     
               INITIALIZE                      PV-RPT-HIER-QUOTE                
               MOVE PV-ATTRIBUTE-1-DESC     TO PV-RPT-HIER-QUOTE                
               INITIALIZE                      PV-ATTRIBUTE-1-DESC              
               PERFORM 1160-REMOVE-DOUBLE-QUOTE                                 
               MOVE PV-RPT-HIER-ATTR        TO PV-ATTRIBUTE-1-DESC              
           END-IF.                                                              
                                                                                
      *-- Remove Double quotes from PV-ATTRIBUTE-2-DESC                         
                                                                                
           IF  PV-ATTRIBUTE-2-DESC(1:1) = '"'                                   
               MOVE "ATTRIBUTE-2"           TO PV-ATTR-NAME                     
               INITIALIZE                      PV-RPT-HIER-QUOTE                
               MOVE PV-ATTRIBUTE-2-DESC     TO PV-RPT-HIER-QUOTE                
               INITIALIZE                      PV-ATTRIBUTE-2-DESC              
               PERFORM 1160-REMOVE-DOUBLE-QUOTE                                 
               MOVE PV-RPT-HIER-ATTR        TO PV-ATTRIBUTE-2-DESC              
           END-IF.                                                              
                                                                                
      *-- Remove Double quotes from PV-FASHION-PYRAMID                          
                                                                                
           IF  PV-FASHION-PYRAMID(1:1) = '"'                                    
               MOVE "FASHION PYRAMID"       TO PV-ATTR-NAME                     
               INITIALIZE                      PV-RPT-HIER-QUOTE                
               MOVE PV-FASHION-PYRAMID      TO PV-RPT-HIER-QUOTE                
               INITIALIZE                      PV-FASHION-PYRAMID               
               PERFORM 1160-REMOVE-DOUBLE-QUOTE                                 
               MOVE PV-RPT-HIER-ATTR        TO PV-FASHION-PYRAMID               
           END-IF.                                                              
                                                                                
       1160-REMOVE-DOUBLE-QUOTE.                                                
      *-----------------------------------------------------------------        
      * Find the last-non-blank char position in PV-RPT-HIER-QUOTE              
      * and move only the value                                                 
      *-----------------------------------------------------------------        
                                                                                
           MOVE "*1160-REMOVE-DOUBLE-QUOTE" TO PV-PARAGRAPH-NAME                
                                                                                
           INITIALIZE                          PV-SS-SUB1                       
                                               PV-SS-SUB2                       
                                               PV-RPT-HIER-ATTR                 
                                               PV-STR-LEN                       
                                                                                
           MOVE LENGTH OF PV-RPT-HIER-QUOTE TO PV-STR-LEN                       
                                                                                
           PERFORM VARYING PV-STR-LEN                                           
                      FROM PV-STR-LEN BY -1                                     
                     UNTIL PV-RPT-HIER-QUOTE(PV-STR-LEN:1) NOT = SPACES         
               CONTINUE                                                         
           END-PERFORM                                                          
                                                                                
           IF  PV-RPT-HIER-QUOTE(PV-STR-LEN:1) = '"'                            
               MOVE 1                       TO PV-SS-SUB2                       
                                                                                
               PERFORM VARYING PV-SS-SUB1                                       
                          FROM 2 By 1                                           
                         UNTIL PV-SS-SUB1 = PV-STR-LEN                          
                            OR PV-SS-SUB1 > 20                                  
                                                                                
                   IF  PV-RPT-HIER-QUOTE(PV-SS-SUB1:1) NOT = '"'                
                       MOVE PV-RPT-HIER-QUOTE(PV-SS-SUB1:1)                     
                                            TO                                  
                            PV-RPT-HIER-ATTR(PV-SS-SUB2 :1)                     
                       COMPUTE PV-SS-SUB2 = PV-SS-SUB2 + 1                      
                       END-COMPUTE                                              
                   ELSE                                                         
                       MOVE PV-RPT-HIER-QUOTE(PV-SS-SUB1:1)                     
                                            TO                                  
                            PV-RPT-HIER-ATTR(PV-SS-SUB2 :1)                     
                       COMPUTE PV-SS-SUB1 = PV-SS-SUB1 + 1                      
                       END-COMPUTE                                              
                       COMPUTE PV-SS-SUB2 = PV-SS-SUB2 + 1                      
                       END-COMPUTE                                              
                   END-IF                                                       
                                                                                
               END-PERFORM                                                      
           ELSE                                                                 
               IF  PV-STR-LEN >= 20                                             
                   DISPLAY ' '                                                  
                   DISPLAY ' VS_ID = ' PV-VENDOR-STYLE-ID                       
                   DISPLAY ' MDSEAR_CLASS_ID = ' PV-SUB-CL-MDSEAR-ID            
                   STRING  ' ATTRIBUTE ' PV-ATTR-NAME                           
                           ' CONTAINS DOUBLE QUOTES AND NOT PROCESSED.'         
                           'VALUE IN THE FILE : ' PV-RPT-HIER-QUOTE             
                           DELIMITED BY SIZE  INTO PV-QUOTES-ERROR              
                   END-STRING                                                   
                   DISPLAY PV-QUOTES-ERROR                                      
                   DISPLAY ' '                                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
       1200-VALIDATE-FASH-PYR.                                                  
      *-----------------------------------------------------------------        
      * CHECK FOR THE EXISTENCE OF FASHION PYRAMID                              
      *-----------------------------------------------------------------        
                                                                                
           MOVE "*1200-VALIDATE-FASH-PYR "  TO PV-PARAGRAPH-NAME                
                                                                                
           MOVE PV-FASHION-PYRAMID          TO VSCHVL-CHARTC-VAL-DESC           
           MOVE PC-FASHION-PYRAMID-CDE      TO VSCHVL-CHARTC-CDE                
                                                                                
           EXEC SQL                                                             
               SELECT  CHARTC_SEQ_NBR                                           
                 INTO :VSCHVL-CHARTC-SEQ-NBR                                    
                 FROM  TVSCHVL                                                  
                WHERE  CHARTC_CDE      = :VSCHVL-CHARTC-CDE                     
                  AND  UPPER(CHARTC_VAL_DESC)                                   
                                       = :VSCHVL-CHARTC-VAL-DESC                
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   MOVE PV-FASHION-PYRAMID    TO RV-FASHION-PYRAMID             
                   MOVE VSCHVL-CHARTC-SEQ-NBR TO                                
                        MDARVS-FSHN-PYRMD-SEQ-NBR                               
                   SET PS-VALID-FP            TO TRUE                           
               WHEN +100                                                        
                   MOVE PV-FASHION-PYRAMID    TO RV-FASHION-PYRAMID             
                   MOVE '*'             TO RV-FASHION-PYRAMID(20:1)             
                   MOVE ZERO                  TO                                
                        MDARVS-FSHN-PYRMD-SEQ-NBR                               
                   SET PS-CSV-ERROR           TO TRUE                           
                   SET PS-INVALID-FP          TO TRUE                           
               WHEN OTHER                                                       
                   MOVE "SELECT FROM TVSCHVL" TO PV-DB2-OPER-ATTEMPTED          
                   DISPLAY SPACES                                               
                   DISPLAY SPACES                                               
                   DISPLAY "CHARTC_CDE      = "  VSCHVL-CHARTC-CDE              
                   DISPLAY "CHARTC_VAL_DESC = "  VSCHVL-CHARTC-VAL-DESC         
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       1300-VALIDATE-ALT-HIER.                                                  
      *-----------------------------------------------------------------        
      * VALIDATE THE ALTERNATE HIERARCHY FIELDS BASED ON THE INPUT              
      * VALUE FROM THE UPDATED CSV FILE.                                        
      *-----------------------------------------------------------------        
                                                                                
           MOVE "*1300-VALIDATE-ALT-HIER "  TO PV-PARAGRAPH-NAME                
                                                                                
           IF  PV-CATEGORY-DESC NOT = SPACES                                    
           AND PV-CATEGORY-DESC NOT = "NONE"                                    
               PERFORM 1400-VALIDATE-CATEGORY                                   
           ELSE                                                                 
               SET PS-CATEGORY-INVALID    TO TRUE                               
               SET PS-CSV-ERROR           TO TRUE                               
           END-IF                                                               
                                                                                
           IF  PS-CATEGORY-VALID                                                
               PERFORM 1450-VAL-CATEGORY-DMA-LVL                                
               PERFORM 1500-VALIDATE-MAJ-CTG                                    
               PERFORM 1600-VALIDATE-NEW-SILHOUETTE                             
               PERFORM 1700-VALIDATE-MATERIAL                                   
               PERFORM 1800-VALIDATE-PRI-TREND                                  
               PERFORM 1900-VALIDATE-SEC-TREND                                  
               PERFORM 2000-VALIDATE-ATTR1                                      
               PERFORM 2100-VALIDATE-ATTR2                                      
           ELSE                                                                 
             MOVE PV-MAJOR-CATEGORY-DESC  TO RV-MAJOR-CATEGORY-DESC             
             MOVE PV-CATEGORY-DESC        TO RV-CATEGORY-DESC                   
             MOVE PV-NEW-SILHOUETTE-DESC  TO RV-NEW-SILHOUETTE-DESC             
             MOVE PV-MATERIAL-DESC        TO RV-MATERIAL-DESC                   
             MOVE PV-CORPORATE-TREND-DESC TO RV-CORPORATE-TREND-DESC            
             MOVE PV-SECONDARY-TREND-DESC TO RV-SECONDARY-TREND-DESC            
             MOVE PV-ATTRIBUTE-1-DESC     TO RV-ATTRIBUTE-1-DESC                
             MOVE PV-ATTRIBUTE-2-DESC     TO RV-ATTRIBUTE-2-DESC                
                                                                                
                                                                                
               MOVE '?' TO RV-MAJOR-CATEGORY-DESC(20:1)                         
                           RV-CATEGORY-DESC(20:1)                               
                           RV-NEW-SILHOUETTE-DESC(20:1)                         
                           RV-MATERIAL-DESC(20:1)                               
                           RV-CORPORATE-TREND-DESC(20:1)                        
                           RV-SECONDARY-TREND-DESC(20:1)                        
                           RV-ATTRIBUTE-1-DESC(20:1)                            
                           RV-ATTRIBUTE-2-DESC(20:1)                            
                                                                                
               MOVE ZEROES                TO MDARVS-CTG-SEQ-NBR                 
                                             MDARVS-MAJ-CTG-SEQ-NBR             
                                             MDARVS-MTRL-SEQ-NBR                
                                             MDARVS-PRIM-TRND-SEQ-NBR           
                                             MDARVS-SEC-TRND-SEQ-NBR            
                                             MDARVS-ATTR-1-SEQ-NBR              
                                             MDARVS-ATTR-2-SEQ-NBR              
                                             MDARVS-NW-SLHTT-SEQ-NBR            
           END-IF.                                                              
                                                                                
       1400-VALIDATE-CATEGORY.                                                  
      *-----------------------------------------------------------------        
      * VALIDATE THE CATEGORY FIELD READ FROM THE UPDATED CSV FILE              
      *-----------------------------------------------------------------        
                                                                                
           MOVE "*1400-VALIDATE-CATEGORY "  TO PV-PARAGRAPH-NAME                
                                                                                
           INITIALIZE                          VSCHVL-CHARTC-VAL-DESC           
                                               VSCHVL-CHARTC-SEQ-NBR            
                                               VSCHVL-CHARTC-CDE                
                                                                                
           MOVE PV-CATEGORY-DESC            TO VSCHVL-CHARTC-VAL-DESC           
           MOVE PC-CATEGORY-CDE             TO VSCHVL-CHARTC-CDE                
                                                                                
           EXEC SQL                                                             
               SELECT  CHARTC_SEQ_NBR                                           
                 INTO :VSCHVL-CHARTC-SEQ-NBR                                    
                 FROM  TVSCHVL                                                  
                WHERE  CHARTC_CDE      = :VSCHVL-CHARTC-CDE                     
                  AND  UPPER(CHARTC_VAL_DESC)                                   
                                       = :VSCHVL-CHARTC-VAL-DESC                
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   SET PS-CATEGORY-VALID      TO TRUE                           
                   MOVE VSCHVL-CHARTC-SEQ-NBR TO MDARVS-CTG-SEQ-NBR             
                                               IMT00014-RPT-HIER-SEQ-NBR        
                   MOVE PV-CATEGORY-DESC      TO RV-CATEGORY-DESC               
               WHEN +100                                                        
                   MOVE PV-CATEGORY-DESC      TO RV-CATEGORY-DESC               
                   MOVE '*'             TO RV-CATEGORY-DESC(20:1)               
                   SET PS-CATEGORY-INVALID    TO TRUE                           
                   SET PS-CSV-ERROR           TO TRUE                           
               WHEN OTHER                                                       
                   MOVE "SELECT FROM TVSCHVL" TO PV-DB2-OPER-ATTEMPTED          
                   DISPLAY SPACES                                               
                   DISPLAY SPACES                                               
                   DISPLAY "CHARTC_CDE      = "  VSCHVL-CHARTC-CDE              
                   DISPLAY "CHARTC_VAL_DESC = "  VSCHVL-CHARTC-VAL-DESC         
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       1450-VAL-CATEGORY-DMA-LVL.                                               
      *-----------------------------------------------------------------        
      * VALIDATE CATEGORY UNDER DIVISION (DMA_NBR) AND DEPARTMENT               
      * (DEPT_NBR) LEVEL                                                        
      *-----------------------------------------------------------------        
                                                                                
           MOVE "*1450-VAL-CATEGORY-DMA-LVL*"                                   
                                            TO PV-PARAGRAPH-NAME                
           INITIALIZE                          DCVSVL-CHARTC-CDE                
                                               DCVSVL-CHARTC-SEQ-NBR            
                                               MDSEAR-OPERCO-NBR                
                                               MDSEAR-GMA-NBR                   
                                               MDSEAR-DMA-NBR                   
                                               MDSEAR-DEPT-NBR                  
                                               MDSEAR-MDSELV-CDE                
                                                                                
           MOVE PC-CATEGORY-CDE             TO DCVSVL-CHARTC-CDE                
           MOVE MDARVS-CTG-SEQ-NBR          TO DCVSVL-CHARTC-SEQ-NBR            
           MOVE "03"                        TO MDSEAR-OPERCO-NBR                
           MOVE PV-GMA-NBR                  TO MDSEAR-GMA-NBR                   
           MOVE PV-DMA-NBR                  TO MDSEAR-DMA-NBR                   
           COMPUTE PV-NUM-DEPT-NBR = FUNCTION NUMVAL(PV-DEPT)                   
           END-COMPUTE                                                          
           MOVE PV-NUM-DEPT-NBR             TO MDSEAR-DEPT-NBR                  
           MOVE "DPT"                       TO MDSEAR-MDSELV-CDE                
                                                                                
           EXEC SQL                                                             
               SELECT  1                                                        
                 INTO :PV-DUMMY                                                 
                 FROM  TMDSEAR A                                                
                      ,TDCVSVL B                                                
                WHERE  B.CHARTC_CDE     = :DCVSVL-CHARTC-CDE                    
                  AND  B.CHARTC_SEQ_NBR = :DCVSVL-CHARTC-SEQ-NBR                
                  AND  A.OPERCO_NBR     = :MDSEAR-OPERCO-NBR                    
                  AND  A.GMA_NBR        = :MDSEAR-GMA-NBR                       
                  AND  A.DMA_NBR        = :MDSEAR-DMA-NBR                       
                  AND  A.DEPT_NBR       = :MDSEAR-DEPT-NBR                      
                  AND  A.MDSELV_CDE     = :MDSEAR-MDSELV-CDE                    
                  AND  A.MDSEAR_DKEY    =  B.MDSEAR_ID                          
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   SET CAT-VALID-FOR-DIV-DEPT TO TRUE                           
               WHEN +100                                                        
                   SET CAT-NOT-VALID-FOR-DIV-DEPT                               
                                              TO TRUE                           
                   MOVE '<'             TO RV-CATEGORY-DESC(20:1)               
               WHEN OTHER                                                       
                   MOVE "SELECT FROM TMDSEAR & TDCVSVL"                         
                                              TO PV-DB2-OPER-ATTEMPTED          
                   DISPLAY SPACES                                               
                   DISPLAY "CHARTC_CDE      = "  DCVSVL-CHARTC-CDE              
                   DISPLAY "CHARTC_SEQ_NBR  = "  DCVSVL-CHARTC-SEQ-NBR          
                   DISPLAY "OPERCO_NBR      = "  MDSEAR-OPERCO-NBR              
                   DISPLAY "GMA_NBR         = "  MDSEAR-GMA-NBR                 
                   DISPLAY "DMA_NBR         = "  MDSEAR-DMA-NBR                 
                   DISPLAY "DEPT_NBR        = "  MDSEAR-DEPT-NBR                
                   DISPLAY "MDSELV_CDE      = "  MDSEAR-MDSELV-CDE              
                   PERFORM 9999-SQL-ERROR                                       
                   DISPLAY SPACES                                               
           END-EVALUATE.                                                        
                                                                                
      *-----------------------------------------------------------------        
      * VALIDATE THE MAJOR CATEGORY FIELD FROM THE UPDATED CSV FILE             
      *-----------------------------------------------------------------        
       1500-VALIDATE-MAJ-CTG.                                                   
                                                                                
           MOVE "*1500-VALIDATE-MAJ-CTG "   TO PV-PARAGRAPH-NAME                
                                                                                
           INITIALIZE                          VSCHVL-CHARTC-VAL-DESC           
                                               VSCHVL-CHARTC-SEQ-NBR            
                                               VSCHVL-CHARTC-CDE                
                                                                                
           MOVE PV-MAJOR-CATEGORY-DESC      TO VSCHVL-CHARTC-VAL-DESC           
           MOVE PC-MAJOR-CATEGORY-CDE       TO VSCHVL-CHARTC-CDE                
                                                                                
           PERFORM 2200-GET-SEQ-NBR                                             
                                                                                
           IF  VSCHVL-CHARTC-SEQ-NBR NOT = 0                                    
               PERFORM 2300-VALIDATE-ATTR-IN-CATG                               
           END-IF                                                               
                                                                                
           MOVE VSCHVL-CHARTC-SEQ-NBR         TO MDARVS-MAJ-CTG-SEQ-NBR         
           MOVE VSCHVL-CHARTC-VAL-DESC        TO RV-MAJOR-CATEGORY-DESC         
           .                                                                    
                                                                                
      *-----------------------------------------------------------------        
      * VALIDATE THE NEW SILHOUETTE FROM THE UPDATED CSV INPUT FILE.            
      *-----------------------------------------------------------------        
       1600-VALIDATE-NEW-SILHOUETTE.                                            
                                                                                
           MOVE "*1600-VALIDATE-NEW-SILHOUETTE "                                
                                            TO PV-PARAGRAPH-NAME                
           INITIALIZE                          VSCHVL-CHARTC-VAL-DESC           
                                               VSCHVL-CHARTC-SEQ-NBR            
                                               VSCHVL-CHARTC-CDE                
                                                                                
           MOVE PV-NEW-SILHOUETTE-DESC      TO VSCHVL-CHARTC-VAL-DESC           
           MOVE PC-NEW-SILHOUETTE-CDE       TO VSCHVL-CHARTC-CDE                
                                                                                
           PERFORM 2200-GET-SEQ-NBR                                             
                                                                                
           IF  VSCHVL-CHARTC-SEQ-NBR NOT = 0                                    
               PERFORM 2300-VALIDATE-ATTR-IN-CATG                               
           END-IF                                                               
                                                                                
           MOVE VSCHVL-CHARTC-SEQ-NBR         TO MDARVS-NW-SLHTT-SEQ-NBR        
           MOVE VSCHVL-CHARTC-VAL-DESC        TO RV-NEW-SILHOUETTE-DESC         
           .                                                                    
                                                                                
      *-----------------------------------------------------------------        
      * VALIDATE THE MATERIAL FROM THE UPDATED CSV FILE                         
      *-----------------------------------------------------------------        
       1700-VALIDATE-MATERIAL.                                                  
                                                                                
           MOVE "*1700-VALIDATE-MATERIAL " TO PV-PARAGRAPH-NAME                 
                                                                                
           INITIALIZE                          VSCHVL-CHARTC-VAL-DESC           
                                               VSCHVL-CHARTC-SEQ-NBR            
                                               VSCHVL-CHARTC-CDE                
                                                                                
           MOVE PV-MATERIAL-DESC            TO VSCHVL-CHARTC-VAL-DESC           
           MOVE PC-MATERIAL-CDE             TO VSCHVL-CHARTC-CDE                
                                                                                
           PERFORM 2200-GET-SEQ-NBR                                             
                                                                                
           IF  VSCHVL-CHARTC-SEQ-NBR NOT = 0                                    
               PERFORM 2300-VALIDATE-ATTR-IN-CATG                               
           END-IF                                                               
                                                                                
           MOVE VSCHVL-CHARTC-SEQ-NBR         TO MDARVS-MTRL-SEQ-NBR            
           MOVE VSCHVL-CHARTC-VAL-DESC        TO RV-MATERIAL-DESC               
             .                                                                  
                                                                                
      *-----------------------------------------------------------------        
      * VALIDATE THE PRIMARY TREND FROM THE UPDATE CSV FILE                     
      *-----------------------------------------------------------------        
       1800-VALIDATE-PRI-TREND.                                                 
                                                                                
           MOVE "*1800-VALIDATE-PRI-TREND" TO PV-PARAGRAPH-NAME                 
                                                                                
           INITIALIZE                          VSCHVL-CHARTC-VAL-DESC           
                                               VSCHVL-CHARTC-SEQ-NBR            
                                               VSCHVL-CHARTC-CDE                
                                                                                
           MOVE PV-CORPORATE-TREND-DESC     TO VSCHVL-CHARTC-VAL-DESC           
           MOVE PC-CORPORATE-CDE            TO VSCHVL-CHARTC-CDE                
                                                                                
           PERFORM 2200-GET-SEQ-NBR                                             
                                                                                
           IF  VSCHVL-CHARTC-SEQ-NBR NOT = 0                                    
               PERFORM 2300-VALIDATE-ATTR-IN-CATG                               
           END-IF                                                               
                                                                                
           MOVE VSCHVL-CHARTC-SEQ-NBR       TO MDARVS-PRIM-TRND-SEQ-NBR         
           MOVE VSCHVL-CHARTC-VAL-DESC      TO RV-CORPORATE-TREND-DESC          
             .                                                                  
      *-----------------------------------------------------------------        
      * VALIDATE SECONDARY TREND FROM THE UPDATED CSV FILE.                     
      *-----------------------------------------------------------------        
       1900-VALIDATE-SEC-TREND.                                                 
                                                                                
           MOVE "*1900-VALIDATE-SEC-TREND" TO PV-PARAGRAPH-NAME                 
                                                                                
           INITIALIZE                          VSCHVL-CHARTC-VAL-DESC           
                                               VSCHVL-CHARTC-SEQ-NBR            
                                               VSCHVL-CHARTC-CDE                
                                                                                
           MOVE PV-SECONDARY-TREND-DESC     TO VSCHVL-CHARTC-VAL-DESC           
           MOVE PC-SECONDARY-CDE            TO VSCHVL-CHARTC-CDE                
                                                                                
           PERFORM 2200-GET-SEQ-NBR                                             
                                                                                
           IF  VSCHVL-CHARTC-SEQ-NBR NOT = 0                                    
               PERFORM 2300-VALIDATE-ATTR-IN-CATG                               
           END-IF                                                               
                                                                                
           MOVE VSCHVL-CHARTC-SEQ-NBR       TO MDARVS-SEC-TRND-SEQ-NBR          
           MOVE VSCHVL-CHARTC-VAL-DESC      TO RV-SECONDARY-TREND-DESC          
             .                                                                  
                                                                                
      *-----------------------------------------------------------------        
      * VALIDATE ATTRB1 FROM THE UPDATED CSV FILE                               
      *-----------------------------------------------------------------        
       2000-VALIDATE-ATTR1.                                                     
                                                                                
           MOVE "*2000-VALIDATE-ATTR1    " TO PV-PARAGRAPH-NAME                 
                                                                                
           INITIALIZE                          VSCHVL-CHARTC-VAL-DESC           
                                               VSCHVL-CHARTC-SEQ-NBR            
                                               VSCHVL-CHARTC-CDE                
                                                                                
           MOVE PV-ATTRIBUTE-1-DESC         TO VSCHVL-CHARTC-VAL-DESC           
           MOVE PC-ATTRIBUTE-1-CDE          TO VSCHVL-CHARTC-CDE                
                                                                                
           PERFORM 2200-GET-SEQ-NBR                                             
                                                                                
           IF  VSCHVL-CHARTC-SEQ-NBR NOT = 0                                    
               PERFORM 2300-VALIDATE-ATTR-IN-CATG                               
           END-IF                                                               
                                                                                
           MOVE VSCHVL-CHARTC-SEQ-NBR       TO MDARVS-ATTR-1-SEQ-NBR            
           MOVE VSCHVL-CHARTC-VAL-DESC      TO RV-ATTRIBUTE-1-DESC              
                                                                                
             .                                                                  
      *-----------------------------------------------------------------        
      * VALIDATE ATTRIBUTE FROM THE UPDATED CSV FILE                            
      *-----------------------------------------------------------------        
       2100-VALIDATE-ATTR2.                                                     
                                                                                
           MOVE "*2100-VALIDATE-ATTR2    " TO PV-PARAGRAPH-NAME                 
                                                                                
           INITIALIZE                          VSCHVL-CHARTC-VAL-DESC           
                                               VSCHVL-CHARTC-SEQ-NBR            
                                               VSCHVL-CHARTC-CDE                
                                                                                
           MOVE PV-ATTRIBUTE-2-DESC         TO VSCHVL-CHARTC-VAL-DESC           
           MOVE PC-ATTRIBUTE-2-CDE          TO VSCHVL-CHARTC-CDE                
                                                                                
           PERFORM 2200-GET-SEQ-NBR                                             
                                                                                
           IF  VSCHVL-CHARTC-SEQ-NBR NOT = 0                                    
               PERFORM 2300-VALIDATE-ATTR-IN-CATG                               
           END-IF                                                               
                                                                                
           MOVE VSCHVL-CHARTC-SEQ-NBR       TO MDARVS-ATTR-2-SEQ-NBR            
           MOVE VSCHVL-CHARTC-VAL-DESC      TO RV-ATTRIBUTE-2-DESC              
             .                                                                  
                                                                                
      *-----------------------------------------------------------------        
      * GET THE CHARTC SEQUENCE NUMBER FROM THE TVSCHVL TABLE                   
      *-----------------------------------------------------------------        
       2200-GET-SEQ-NBR.                                                        
                                                                                
           MOVE "*2200-GET-SEQ-NBR       "    TO PV-PARAGRAPH-NAME              
                                                                                
           EXEC SQL                                                             
               SELECT  CHARTC_SEQ_NBR                                           
                 INTO :VSCHVL-CHARTC-SEQ-NBR                                    
                 FROM  TVSCHVL                                                  
                WHERE  CHARTC_CDE      = :VSCHVL-CHARTC-CDE                     
                  AND  UPPER(CHARTC_VAL_DESC)                                   
                                       = :VSCHVL-CHARTC-VAL-DESC                
                WITH UR                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN 100                                                         
                   MOVE 0                     TO VSCHVL-CHARTC-SEQ-NBR          
                   MOVE '*'       TO VSCHVL-CHARTC-VAL-DESC(20:1)               
                   SET PS-CSV-ERROR           TO TRUE                           
               WHEN OTHER                                                       
                   MOVE "SELECT FROM TVSCHVL" TO PV-DB2-OPER-ATTEMPTED          
                   DISPLAY SPACES                                               
                   DISPLAY SPACES                                               
                   DISPLAY "CHARTC_CDE      = "  VSCHVL-CHARTC-CDE              
                   DISPLAY "CHARTC_VAL_DESC = "  VSCHVL-CHARTC-VAL-DESC         
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------         
      *  VALIDATE THE ATTRIBUTE UNDER THE CATEGORY                              
      *----------------------------------------------------------------         
       2300-VALIDATE-ATTR-IN-CATG.                                              
                                                                                
           MOVE "*2300-VALIDATE-ATTR-IN-CATG " TO PV-PARAGRAPH-NAME             
           MOVE VSCHVL-CHARTC-CDE             TO IMT00014-CHARTC-CDE            
           MOVE VSCHVL-CHARTC-SEQ-NBR         TO IMT00014-CHARTC-SEQ-NBR        
                                                                                
           EXEC SQL                                                             
               SELECT  1                                                        
                 INTO :PV-DUMMY                                                 
                 FROM IMT_RPT_HIER_VAL                                          
                WHERE RPT_HIER_SEQ_NBR = :IMT00014-RPT-HIER-SEQ-NBR             
                  AND CHARTC_CDE       = :IMT00014-CHARTC-CDE                   
                  AND CHARTC_SEQ_NBR   = :IMT00014-CHARTC-SEQ-NBR               
                WITH UR                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
               WHEN -811                                                        
                   CONTINUE                                                     
               WHEN 100                                                         
                   MOVE 0      TO VSCHVL-CHARTC-SEQ-NBR                         
                   MOVE '<'    TO VSCHVL-CHARTC-VAL-DESC(20:1)                  
                   SET PS-CSV-ERROR           TO TRUE                           
               WHEN OTHER                                                       
                   MOVE "SELECT FROM IMT_RPT_HIER_VAL"                          
                                              TO PV-DB2-OPER-ATTEMPTED          
                   DISPLAY SPACES                                               
                   DISPLAY SPACES                                               
                   DISPLAY "RPT-HIER-SEQ-NBR = "                                
                           IMT00014-RPT-HIER-SEQ-NBR                            
                   DISPLAY "CHARTC_CDE = "       IMT00014-CHARTC-CDE            
                   DISPLAY "CHARTC-SEQ-NBR = "                                  
                           IMT00014-CHARTC-SEQ-NBR                              
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------         
      *  LOAD THE ERROR REPORT                                                  
      *----------------------------------------------------------------         
       2400-LOAD-ERROR-REPORT.                                                  
                                                                                
           MOVE "*2400-LOAD-ERROR-REPORT    " TO PV-PARAGRAPH-NAME              
           MOVE PV-GMA-NBR                    TO RV-GMA-NBR                     
           MOVE PV-DMA-NBR                    TO RV-DMA-NBR                     
           MOVE PV-BUYER-NUMBER               TO RV-BUYER-NUMBER                
           MOVE PV-BUYER-NAME                 TO RV-BUYER-NAME                  
           MOVE PV-DEPT                       TO RV-DEPT                        
           MOVE PV-CLASS-NBR                  TO RV-CLASS-NBR                   
           MOVE PV-SUB-CLASS-NBR              TO RV-SUB-CLASS-NBR               
           MOVE PV-VENDOR-STYLE-ID            TO RV-VENDOR-STYLE-ID             
           MOVE PV-STYLE-NUMBER               TO RV-STYLE-NUMBER                
           MOVE PV-STYLE-DESC                 TO RV-STYLE-DESC                  
           MOVE PV-BRAND-DESC                 TO RV-BRAND-DESC                  
           MOVE PV-VENDOR-NAME                TO RV-VENDOR-NAME                 
                                                                                
           MOVE PV-EOH-UNITS                  TO PV-EOH-UNITS-ED                
           MOVE PV-EOH-UNITS-ED               TO PV-EOH-UNITS-TEMP              
           MOVE PV-EOH-UNITS-TEMP             TO PV-EOH-UNITS-TEMP1             
                                                                                
           COMPUTE RV-EOH-UNITS = FUNCTION NUMVAL(PV-EOH-UNITS-TEMP1)           
           END-COMPUTE                                                          
                                                                                
           MOVE PV-STYLE-CREATE-DT            TO RV-STYLE-CREATE-DT             
           MOVE PV-CORP-LAST-RCPT-DT          TO RV-CORP-LAST-RCPT-DT           
           MOVE PV-HIERARCHY-DESC             TO RV-HIERARCHY-DESC              
           MOVE PV-FUNCTION-DESC              TO RV-FUNCTION-DESC               
           MOVE PV-SILHOUETTE-DESC            TO RV-SILHOUETTE-DESC             
           MOVE PV-BODY-STYLE-DESC            TO RV-BODY-STYLE-DESC             
           MOVE PV-FABRICATION-DESC           TO RV-FABRICATION-DESC            
           MOVE PV-LBL-SEQ-NBR                TO RV-LBL-SEQ-NBR                 
           MOVE PV-SUB-CL-MDSEAR-ID           TO RV-SUB-CL-MDSEAR-ID            
           MOVE PV-HIERARCHY-NBR              TO RV-HIERARCHY-NBR               
           MOVE PV-FUNCTION-NBR               TO RV-FUNCTION-NBR                
           MOVE PV-SILHOUETTE-NBR             TO RV-SILHOUETTE-NBR              
           MOVE PV-BODY-STYLE-NBR             TO RV-BODY-STYLE-NBR              
           MOVE PV-FABRICATION-NBR            TO RV-FABRICATION-NBR.            
                                                                                
      *-----------------------------------------------------------------        
      * READ THE UPDATED CSV FILE                                               
      *-----------------------------------------------------------------        
       6000-READ-CSV-UPDATE-FILE.                                               
                                                                                
           MOVE "*6000-READ-CSV-UPDATE-FILE  " TO PV-PARAGRAPH-NAME             
                                                                                
           INITIALIZE                             PV-CSV-UPDATE-REC             
                                                                                
           READ CSV-UPD-INP-FILE             INTO PV-CSV-UPDATE-REC             
             AT END SET END-OF-INPUT           TO TRUE                          
             NOT AT END ADD +1                 TO PV-CSV-UPD-RECS-READ.         
                                                                                
      *-----------------------------------------------------------------        
      * WRITE THE INVALID RECORD TO ERROR REPORT FILE                           
      *-----------------------------------------------------------------        
       7000-WRITE-CSV-ERROR-REC.                                                
                                                                                
           MOVE "*7000-WRITE-CSV-ERROR-REC   " TO PV-PARAGRAPH-NAME             
                                                                                
           WRITE CSV-ERROR-REP-REC           FROM RV-DETAIL-LINE                
                                                                                
           ADD +1                              TO PV-CSV-ERR-RECS-CNT.          
                                                                                
      *-----------------------------------------------------------------        
      * WRITE THE INVALID RECORD TO ERROR REPORT FILE                           
      *-----------------------------------------------------------------        
       8000-WRITE-VALID-FILE.                                                   
                                                                                
           MOVE "*8000-WRITE-VALID-FILE      " TO PV-PARAGRAPH-NAME             
                                                                                
           WRITE CSV-UPD-VALID-REC           FROM FV-FILE-VARIABLES             
                                                                                
           ADD +1                              TO PV-VALID-RECS-CNT.            
                                                                                
      *-----------------------------------------------------------------        
      * 9000-EOJ-ROUTINE - CLOSES THE FILES ASSOCIATED WITH THE JOB.            
      *-----------------------------------------------------------------        
       9000-EOJ.                                                                
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '============================================='.             
           DISPLAY ' '.                                                         
           DISPLAY ' ******  IMKRP076 PROCESSING SUMMARY  ******  '.            
           PERFORM 9100-DISPLAY-PROCESSING-INFO.                                
           DISPLAY '============================================='.             
                                                                                
           CLOSE                               CSV-UPD-INP-FILE                 
                                               CSV-ERROR-REP                    
                                               CSV-UPD-VALID-FILE.              
                                                                                
      *-----------------------------------------------------------------        
      * DISPLAY PROCESSED RECORDS COUNTS                                        
      *-----------------------------------------------------------------        
       9100-DISPLAY-PROCESSING-INFO.                                            
                                                                                
            DISPLAY ' '.                                                        
            DISPLAY ' RECORDS READ     = '       PV-CSV-UPD-RECS-READ           
            DISPLAY ' VALID RECORDS WRITTEN  = ' PV-VALID-RECS-CNT              
            DISPLAY ' ERROR RECORDS WRITTEN  = ' PV-CSV-ERR-RECS-CNT            
            DISPLAY ' '.                                                        
                                                                                
       9999-SQL-ERROR.                                                          
      ******************************************************************        
      * SQL abend routine                                                       
      ******************************************************************        
                                                                                
           SET ABEND-4013                     TO TRUE.                          
                                                                                
           DISPLAY '**  ABEND     **'.                                          
                                                                                
           DISPLAY '**  PROGRAM   **  =  IMKRP076'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
      *                                                                         
      * The following copybook utilizes the DB2 abend module                    
      * DSNTIAR to convert SQLCA into understandable verbage.                   
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
                                                                                
           EXEC SQL                                                             
                COMMIT                                                          
           END-EXEC.                                                            
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
