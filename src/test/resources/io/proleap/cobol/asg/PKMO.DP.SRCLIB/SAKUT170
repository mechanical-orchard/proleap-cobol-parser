      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
                                                                        00040000
       PROGRAM-ID.             SAKUT170.                                00050000
       AUTHOR.                 JIM HUNTER.                              00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            NOVEMBER, 2023.                          00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ***************************************************************** 00110000
      *  CALCULATE COBRAND WEEKLY LATE TRANSACTION DOLLARS.             00120002
      *  FINANCE WOULD LIKE TO BALANCE THE COBRAND DOLLARS FROM THE     00120102
      *  THIRD PARTY CREDIT (TCK) REPORTS TO THE GL NUMBERS PASSED      00121000
      *  FROM COSA (SAK).  ONE OF THE ADJUSTMENTS THEY NEED TO ALLOW    00122000
      *  FOR ARE LATE TRANSACTIONS FLOWING INTO AND OUT OF THE WEEK.    00123000
      *                                                                 00124000
      *  THIS PROGRAM RUNS QUERIES AGAINST THE PREVIOUS FISCAL WEEK     00130000
      *  TO CALCULATE:                                                  00140000
      *  - COBRAND TENDERS (CVN) THAT CAME INTO THE PREV FISCAL WEEK    00151000
      *    LATE FROM AN EARLIER WEEK.                                   00152000
      *  - COBRAND TENDERS (CVN) THAT CAME IN FOR THE PREV FISCAL WEEK  00152100
      *    LATE AFTER THE END OF THE WEEK AND BEFORE THE SAK131 GL RUN. 00152200
      *  - COBRAND TRAILING TRANSACTIONS FOR THE WEEK FOR BOTH          00153000
      *    TENDERS (TTN) AND PAYMENTS (TTP).                            00154000
      *                                                                 00155000
      *  LATE PAYMENTS HAVEN'T BEEN AN ISSUE.                           00156000
      *                                                                 00157000
      *   INPUT: INP01 - CONTAINS THE START AND END DATES FOR THE       00160000
      *                  PREVIOUS FISCAL WEEK.                          00170000
      *  OUTPUT: OUT01 - EMAIL WITH COBRAND LATE AND TRAILING           00180000
      *                  TRANSACTION DOLLARS.                           00190000
      *                                                                 00191008
      *  01/04/24    JIM HUNTER      CHG0297418    INITIAL PROGRAM.     00192008
      ***************************************************************** 00200000
       ENVIRONMENT DIVISION.                                            00210000
      ***************************************************************** 00220000
                                                                        00230000
       CONFIGURATION SECTION.                                           00240000
                                                                        00250000
       SOURCE-COMPUTER.        IBM-3090.                                00260000
                                                                        00270000
       OBJECT-COMPUTER.        IBM-3090.                                00280000
                                                                        00290000
       INPUT-OUTPUT SECTION.                                            00300000
                                                                        00310000
       FILE-CONTROL.                                                    00320000
                                                                        00330000
           SELECT DATE-RANGE-CARD-FILE                                  00340000
               ASSIGN TO INP01.                                         00350000
                                                                        00360000
           SELECT EMAIL-FILE                                            00370000
               ASSIGN TO OUT01.                                         00380000
                                                                        00390000
      ******************************************************************00400000
       DATA DIVISION.                                                   00410000
      ******************************************************************00420000
                                                                        00430000
       FILE SECTION.                                                    00440000
                                                                        00450000
       FD  DATE-RANGE-CARD-FILE                                         00460000
           RECORDING MODE IS F                                          00470000
           BLOCK CONTAINS 0 RECORDS                                     00480000
           LABEL RECORDS ARE STANDARD.                                  00490000
                                                                        00500000
       01  DATE-RANGE-CARD-RECORD          PIC X(80).                   00510000
                                                                        00520000
       FD  EMAIL-FILE                                                   00571000
           RECORDING MODE IS F                                          00572000
           LABEL RECORDS ARE STANDARD                                   00573000
           BLOCK CONTAINS 0 RECORDS.                                    00574000
                                                                        00575000
       01  EMAIL-RECORD                     PIC X(80).                  00576000
                                                                        00577000
                                                                        00580000
      ******************************************************************00590000
       WORKING-STORAGE SECTION.                                         00600000
      ******************************************************************00610000
       01  FILLER                            PIC X(50)  VALUE           00620000
           ' *** WORKING STORAGE STARTS HERE FOR SAKUT170 *** '.        00630000
                                                                        00640000
      ******************************************************************00650000
      * RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                    00660000
      ******************************************************************00670000
       01  CC-CONTROL-CARD.                                             00680000
           05  CC-CONTROL-CARD-DISPLAY.                                 00690000
               10  CC-START-DATE       PIC X(10)    VALUE SPACES.       00700000
               10  FILLER              PIC X        VALUE SPACE.        00710000
               10  CC-END-DATE         PIC X(10)    VALUE SPACES.       00720000
               10  FILLER              PIC X(59)    VALUE SPACE.        00730000
                                                                        00740000
      ******************************************************************00750000
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)             00760000
      ******************************************************************00770000
           COPY DPWS500.                                                00780000
                                                                        00790000
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      00800000
                                                        COMP SYNC.      00810000
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    00820000
           88  AC-DB2-ERROR                             VALUE +4013.    00830000
           88  AC-CALENDAR-ROUTINE-ERROR                VALUE +4019.    00840000
                                                                        00850000
       01  PS-PROGRAM-SWITCHES.                                         00860000
           05  PS-END-OF-DATE-FILE-SW        PIC X(01)  VALUE 'N'.      00890000
               88  PS-END-OF-DATE-FILE-YES              VALUE 'Y'.      00900000
                                                                        00940000
       01  PC-CONSTANTS.                                                00950000
           05  PC-NOT-FOUND                  PIC S9(04)  VALUE +100     00960000
                                                         COMP SYNC.     00970000
           05  PC-CURRENT-PROGRAM-NAME       PIC  X(08)  VALUE          00980000
                                                         'SAKUT170'.    00990000
                                                                        01000000
       01  DK-DB2-KEYS.                                                 01010000
           05  DK-BEGIN-DATE-ISO             PIC  X(10)  VALUE SPACES.  01020000
           05  DK-END-DATE-ISO               PIC  X(10)  VALUE SPACES.  01030000
                                                                        01040000
       01  PV-MISC-FIELDS.                                              01050000
           05  PV-REC-CNT                    PIC  9(07)  VALUE ZERO.    01060000
           05  PV-PARAGRAPH-NAME             PIC  X(30)  VALUE SPACES.  01090000
           05  PV-OPERATION-MSG-1            PIC  X(40)  VALUE SPACES.  01100000
           05  PV-OPERATION-MSG-2            PIC  X(40)  VALUE SPACES.  01110000
           05  PV-DB2-OPERATION              PIC  X(30)  VALUE SPACES.  01120000
           05  PV-DPG52-LK-DATE-INPUT        PIC  X(10)  VALUE SPACES.  01150000
           05  PV-DB2-SUM              PIC S9(07)V99 COMP-3 VALUE ZERO. 01151001
                                                                        01160000
      ******************************************************************01170000
      * RECORD LAYOUTS FOR OUTPUT EMAIL FILE.                           01180000
      ******************************************************************01190000
                                                                        01200000
       01  EM-EMAIL-LINE1.                                              01210000
           05  FILLER                  PIC X(80)       VALUE            01220000
               ' SAKUT170 COBRAND LATE AND TRAILING DOLLARS.'.          01230005
                                                                        01230100
       01  EM-EMAIL-LINE2.                                              01231000
           05  FILLER                  PIC X(15)    VALUE               01232000
               ' FOR WEEK OF:  '.                                       01233000
           05  EM-START-DATE           PIC X(10)    VALUE SPACES.       01234000
           05  FILLER                  PIC X(08)    VALUE '  THRU  '.   01234100
           05  EM-END-DATE             PIC X(10)    VALUE SPACES.       01234200
           05  FILLER                  PIC X(37)    VALUE SPACES.       01236000
                                                                        01240000
       01  EM-EMAIL-BLANK-LINE         PIC X(80)    VALUE SPACES.       01250001
                                                                        01260000
       01  EM-EMAIL-LINE3.                                              01270000
           05  FILLER                  PIC X(52)          VALUE         01280007
               ' LATES FROM PREVIOUS WEEKS NEED TO BE SUBTRACTED..'.    01290007
           05  EM-BEFORE-LATES         PIC $$,$$$,$$9.99- VALUE ZERO.   01300006
           05  FILLER                  PIC X(14)          VALUE SPACES. 01310007
                                                                        01320003
       01  EM-EMAIL-LINE4.                                              01330003
           05  FILLER                  PIC X(52)          VALUE         01340007
               ' LATES IN FOLLOWING WEEKS NEED TO BE ADDED........'.    01350007
           05  EM-AFTER-LATES          PIC $$,$$$,$$9.99- VALUE ZERO.   01360006
           05  FILLER                  PIC X(14)          VALUE SPACES. 01370007
                                                                        01380003
       01  EM-EMAIL-LINE5.                                              01390003
           05  FILLER                  PIC X(52)          VALUE         01400007
               ' TRAILING COBRAND TENDERS.........................'.    01410007
           05  EM-TRL-TENDERS          PIC $$,$$$,$$9.99- VALUE ZERO.   01420006
           05  FILLER                  PIC X(14)          VALUE SPACES. 01430007
                                                                        01440003
       01  EM-EMAIL-LINE6.                                              01450003
           05  FILLER                  PIC X(52)          VALUE         01460007
               ' TRAILING COBRAND PAYMENTS........................'.    01470007
           05  EM-TRL-PAYMENTS         PIC $$,$$$,$$9.99- VALUE ZERO.   01480006
           05  FILLER                  PIC X(14)          VALUE SPACES. 01490007
                                                                        01500000
                                                                        01800000
      ******************************************************************01810000
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            01820000
      ******************************************************************01830000
           COPY DPWS004.                                                01840000
                                                                        01850000
      ******************************************************************01860000
      *  DB2 COMMUNICATION AREA.                                        01870000
      ******************************************************************01880000
           EXEC SQL                                                     01890000
                INCLUDE SQLCA                                           01900000
           END-EXEC.                                                    01910000
                                                                        01920000
                                                                        02410000
      ******************************************************************02490000
      *  DB2 TABLE DB2PDBA.TEPDKEY - CURRENT PROCESSING DAY KEYS TBL.   02500000
      ******************************************************************02510000
           EXEC SQL                                                     02520000
                INCLUDE TEPDKEY                                         02530000
           END-EXEC.                                                    02540000
                                                                        02550000
      ******************************************************************02630000
      *  DB2 TABLE DB2PDBA.TEPPART - SALES PARTITION TABLE.             02640000
      ******************************************************************02650000
           EXEC SQL                                                     02660000
                INCLUDE TEPPART                                         02670000
           END-EXEC.                                                    02680000
                                                                        02690000
      ******************************************************************02691000
      *  DB2 TABLE DB2PDBA.TEPTRN - TRANSACTION TABLE.                  02692000
      ******************************************************************02693000
           EXEC SQL                                                     02694000
                INCLUDE TEPTRN                                          02695000
           END-EXEC.                                                    02696000
                                                                        02697000
      ******************************************************************02698000
      *  DB2 TABLE DB2PDBA.TEPTND - TENDER TABLE.                       02699000
      ******************************************************************02699100
           EXEC SQL                                                     02699200
                INCLUDE TEPTND                                          02699300
           END-EXEC.                                                    02699400
                                                                        02699500
      ******************************************************************02699600
      *  DB2 TABLE DB2PDBA.TSASUM - SUMMARY TABLE.                      02699700
      ******************************************************************02699800
           EXEC SQL                                                     02699900
                INCLUDE TSASUM                                          02700000
           END-EXEC.                                                    02700100
                                                                        02700200
                                                                        03110000
      ***************************************************************** 03120000
       PROCEDURE DIVISION.                                              03130000
      ******************************************************************03140000
      * 0000-MAINLINE-PARAGRAPH                                         03150000
      *      CREATE COBRAND LATE AND TRAILING DOLLARS FOR THE WEEK.     03160000
      ******************************************************************03170000
       0000-MAINLINE-PARAGRAPH.                                         03180000
                                                                        03190000
           PERFORM 1000-INITIALIZATION.                                 03200000
                                                                        03210000
           PERFORM 2000-PROCESS-QUERIES.                                03220000
                                                                        03280000
           PERFORM 3000-WRITE-EMAIL-FILE.                               03290000
                                                                        03300000
           STOP RUN.                                                    03320000
                                                                        03330000
      ******************************************************************03340000
       1000-INITIALIZATION.                                             03350000
      *     OPEN FILES AND PROCESS DATE CONTROL CARD.                   03360000
      ******************************************************************03370000
                                                                        03380000
           OPEN INPUT  DATE-RANGE-CARD-FILE                             03390000
                OUTPUT EMAIL-FILE.                                      03400000
                                                                        03410000
           PERFORM 1100-PROCESS-CONTROL-CARD.                           03420000
           MOVE DK-BEGIN-DATE-ISO     TO   EM-START-DATE.               03421001
           MOVE DK-END-DATE-ISO       TO   EM-END-DATE.                 03423001
                                                                        03430000
                                                                        03440001
      ******************************************************************03460000
       1100-PROCESS-CONTROL-CARD.                                       03470000
      *     READ CONTROL CARD TO GET START AND END DATES.               03480000
      *     CALL THE DATE ROUTINE TO VALIDATE BOTH.                     03490000
      ******************************************************************03500000
                                                                        03510000
           PERFORM 1110-READ-DATE-RANGE-CARD-FILE.                      03520000
           CLOSE DATE-RANGE-CARD-FILE.                                  03530000
           IF PS-END-OF-DATE-FILE-YES                                   03540000
               MOVE '1100-PROCESS-CONTROL-CARD'  TO PV-PARAGRAPH-NAME   03550000
               MOVE 'NO CONTROL CARD TO PROCESS' TO PV-OPERATION-MSG-1  03560000
               SET AC-CONTROL-CARD-ERROR TO TRUE                        03570000
               PERFORM 9999-ABEND                                       03580000
           END-IF.                                                      03590000
           DISPLAY PC-CURRENT-PROGRAM-NAME                              03600000
                   ' - CONTROL CARD = (' CC-CONTROL-CARD-DISPLAY  ')'.  03610000
                                                                        03620000
      ******************************************************************03630000
       1110-READ-DATE-RANGE-CARD-FILE.                                  03640000
      *     READ DATE CONTROL CARD.                                     03650000
      *     CALL THE DATE ROUTINE TO VALIDATE START AND END DATES.      03660000
      ******************************************************************03670000
                                                                        03680000
           READ DATE-RANGE-CARD-FILE INTO CC-CONTROL-CARD               03690000
              AT END                                                    03700000
                 SET PS-END-OF-DATE-FILE-YES TO TRUE                    03710000
              NOT AT END                                                03720000
                  MOVE CC-START-DATE        TO PV-DPG52-LK-DATE-INPUT   03730000
                  PERFORM 1125-VALIDATE-INPUT-DATE                      03740000
                  MOVE CC-START-DATE        TO DK-BEGIN-DATE-ISO        03750000
                                                                        03760000
                  MOVE CC-END-DATE          TO PV-DPG52-LK-DATE-INPUT   03770000
                  PERFORM 1125-VALIDATE-INPUT-DATE                      03780000
                  MOVE CC-END-DATE          TO DK-END-DATE-ISO.         03790000
                                                                        03800000
                                                                        03810000
      ******************************************************************03820000
      * CALL KOHLS CALENDAR ROUTINE:                                    03830000
      *     PASS: CONTROL CARD POS DATE (CCYY-MM-DD FORMAT).            03840000
      *   RETURN: ACTUAL CALENDAR DB2 ISO DATE (CCYY-MM-DD FORMAT).     03850000
      ******************************************************************03860000
       1125-VALIDATE-INPUT-DATE.                                        03870000
                                                                        03880000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03890000
                                                                        03900000
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   03910000
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   03920000
           SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                   03930000
                                                                        03940000
           MOVE PV-DPG52-LK-DATE-INPUT       TO DPG52-LK-DATE-INPUT.    03950000
                                                                        03960000
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    03970000
                                                   DPG54 DPG55 DPG56.   03980000
           EVALUATE TRUE                                                03990000
               WHEN DPG54-SEVERE-ERROR                                  04000000
               WHEN DPG54-DATE-INVALID                                  04010000
                   MOVE '1125-VALIDATE-INPUT-DATE'                      04020000
                                             TO PV-PARAGRAPH-NAME       04030000
                   MOVE 'ERROR CONVERTING INPUT CARD DATE'              04040000
                                             TO PV-OPERATION-MSG-1      04050000
                   SET AC-CALENDAR-ROUTINE-ERROR                        04060000
                                             TO TRUE                    04070000
                   MOVE DPG54-ERROR-MESSAGE                             04080000
                                             TO PV-OPERATION-MSG-2      04090000
                   PERFORM 9999-ABEND                                   04100000
           END-EVALUATE.                                                04110000
                                                                        04120000
                                                                        04130000
      ******************************************************************04131001
       2000-PROCESS-QUERIES.                                            04140000
      ******************************************************************04140101
                                                                        04141001
           PERFORM 2100-SELECT-PREV-LATES.                              04150001
           PERFORM 2200-SELECT-AFTER-LATES.                             04160001
           PERFORM 2300-SELECT-TRAILING-TENDERS.                        04170001
           PERFORM 2400-SELECT-TRAILING-PAYMENTS.                       04180001
                                                                        04190001
      ******************************************************************05230000
        2100-SELECT-PREV-LATES.                                         05240001
      * LATES CREATED IN PRIOR WEEKS AND PROCESSED IN THE CURRENT WEEK. 05241001
      ******************************************************************05250000
                                                                        05251001
           EXEC SQL                                                     05252001
               SELECT SUM(N.TNDR_AMT)                                   05260501
                 INTO :PV-DB2-SUM                                       05260601
                 FROM  TEPDKEY D                                        05260801
                      ,TEPPART P                                        05260901
                      ,TEPTRN  T                                        05261001
                      ,TEPTND  N                                        05261101
                WHERE D.SLS_DTE          < :DK-BEGIN-DATE-ISO           05261201
                  AND D.PRCG_DTE   BETWEEN :DK-BEGIN-DATE-ISO           05261301
                                       AND :DK-END-DATE-ISO             05261401
                  AND D.SLS_DTE          = P.SLS_DTE                    05261701
                  AND P.PARTN_NBR        = T.PARTN_NBR                  05261801
                  AND D.STR_NBR          = T.STR_NBR                    05261901
                  AND D.RGST_ID          = T.RGST_ID                    05262001
                  AND D.TRN_NBR          = T.TRN_NBR                    05262101
                  AND D.STRT_TM          = T.STRT_TM                    05262201
                  AND D.SLS_CORR_SEQ_NBR = T.SLS_CORR_SEQ_NBR           05262301
                  AND T.PARTN_NBR        = N.PARTN_NBR                  05262401
                  AND T.STR_NBR          = N.STR_NBR                    05262501
                  AND T.RGST_ID          = N.RGST_ID                    05262601
                  AND T.TRN_NBR          = N.TRN_NBR                    05262701
                  AND T.STRT_TM          = N.STRT_TM                    05262801
                  AND T.SLS_CORR_SEQ_NBR = N.SLS_CORR_SEQ_NBR           05262901
                  AND T.TRN_STAT_CDE    IN ('Z', 'L', 'V', 'R')         05263001
                  AND T.VOID_ABRT_CDE    = 'N'                          05263101
                  AND N.TNDR_VOID_IND    = 'N'                          05263201
                  AND N.TNDR_TYP_CDE     = '24'                         05263301
                WITH UR                                                 05263401
                                                                        05263501
           END-EXEC.                                                    05263601
                                                                        05263701
           EVALUATE SQLCODE                                             05263801
              WHEN ZERO                                                 05263901
                 MOVE PV-DB2-SUM   TO  EM-BEFORE-LATES                  05264001
              WHEN -305                                                 05264201
                 MOVE ZERO         TO  EM-BEFORE-LATES                  05264301
              WHEN OTHER                                                05264401
                 MOVE '2100-SELECT-PREV-LATES'    TO PV-PARAGRAPH-NAME  05264501
                 MOVE SQLCODE                     TO PV-DB2-OPERATION   05264601
                 PERFORM 9100-SQL-ABEND                                 05264701
           END-EVALUATE.                                                05264801
                                                                        05264901
                                                                        05265000
      ******************************************************************05265101
        2200-SELECT-AFTER-LATES.                                        05265201
      * LATES CREATED IN CURRENT WEEK AND PROCESSED IN A LATER WEEK.    05265301
      ******************************************************************05265401
                                                                        05265501
           EXEC SQL                                                     05265601
               SELECT SUM(N.TNDR_AMT)                                   05265701
                 INTO :PV-DB2-SUM                                       05265801
                 FROM  TEPDKEY D                                        05265901
                      ,TEPPART P                                        05266001
                      ,TEPTRN  T                                        05266101
                      ,TEPTND  N                                        05266201
                WHERE D.SLS_DTE    BETWEEN :DK-BEGIN-DATE-ISO           05266401
                                       AND :DK-END-DATE-ISO             05266501
                  AND D.PRCG_DTE         > :DK-END-DATE-ISO             05266601
                  AND D.PRCG_DTE        <=  CURRENT DATE                05266701
                  AND D.SLS_DTE          = P.SLS_DTE                    05266801
                  AND P.PARTN_NBR        = T.PARTN_NBR                  05266901
                  AND D.STR_NBR          = T.STR_NBR                    05267001
                  AND D.RGST_ID          = T.RGST_ID                    05267101
                  AND D.TRN_NBR          = T.TRN_NBR                    05267201
                  AND D.STRT_TM          = T.STRT_TM                    05267301
                  AND D.SLS_CORR_SEQ_NBR = T.SLS_CORR_SEQ_NBR           05267401
                  AND T.PARTN_NBR        = N.PARTN_NBR                  05267501
                  AND T.STR_NBR          = N.STR_NBR                    05267601
                  AND T.RGST_ID          = N.RGST_ID                    05267701
                  AND T.TRN_NBR          = N.TRN_NBR                    05267801
                  AND T.STRT_TM          = N.STRT_TM                    05267901
                  AND T.SLS_CORR_SEQ_NBR = N.SLS_CORR_SEQ_NBR           05268001
                  AND T.TRN_STAT_CDE    IN ('Z', 'L', 'V', 'R')         05268101
                  AND T.VOID_ABRT_CDE    = 'N'                          05268201
                  AND N.TNDR_VOID_IND    = 'N'                          05268301
                  AND N.TNDR_TYP_CDE     = '24'                         05268401
                WITH UR                                                 05268501
                                                                        05268601
           END-EXEC.                                                    05268701
                                                                        05268801
           EVALUATE SQLCODE                                             05268901
              WHEN ZERO                                                 05269001
                 MOVE PV-DB2-SUM   TO  EM-AFTER-LATES                   05269101
              WHEN -305                                                 05269201
                 MOVE ZERO         TO  EM-AFTER-LATES                   05269301
              WHEN OTHER                                                05269401
                 MOVE '2200-SELECT-AFTER-LATES'   TO PV-PARAGRAPH-NAME  05269501
                 MOVE SQLCODE                     TO PV-DB2-OPERATION   05269601
                 PERFORM 9100-SQL-ABEND                                 05269701
           END-EVALUATE.                                                05269801
                                                                        05269901
                                                                        05270001
      ******************************************************************05270101
        2300-SELECT-TRAILING-TENDERS.                                   05270201
      * GET CURRENT WEEK'S TRAILING TENDERS (TTN).                      05270301
      ******************************************************************05270401
                                                                        05270601
           EXEC SQL                                                     05270701
               SELECT SUM(CTG_AMT)                                      05270801
                 INTO :PV-DB2-SUM                                       05270901
                 FROM TSASUM    SUM                                     05271001
                    , TEPPART   PRT                                     05271101
                WHERE PRT.SLS_DTE    BETWEEN :DK-BEGIN-DATE-ISO         05271201
                                         AND :DK-END-DATE-ISO           05271301
                  AND SUM.PARTN_NBR  =  PRT.PARTN_NBR                   05271401
                  AND SUM_CTG_CDE    = 'TTN'                            05271801
                  AND SUM_TYP_CDE   IN ('D', 'S')                       05272001
                  AND STR_NBR       <> 6666                             05272101
                  AND RGST_ID        = 0                                05272301
               WITH UR                                                  05272401
                                                                        05272501
           END-EXEC.                                                    05272601
                                                                        05272701
           EVALUATE SQLCODE                                             05272801
              WHEN ZERO                                                 05272901
                 MOVE PV-DB2-SUM   TO  EM-TRL-TENDERS                   05273001
              WHEN -305                                                 05273101
                 MOVE ZERO         TO  EM-TRL-TENDERS                   05273201
              WHEN OTHER                                                05273301
                 MOVE '2300-SELECT-TRAILING-TENDERS'                    05273401
                                                  TO PV-PARAGRAPH-NAME  05273501
                 MOVE SQLCODE                     TO PV-DB2-OPERATION   05273601
                 PERFORM 9100-SQL-ABEND                                 05273701
           END-EVALUATE.                                                05273801
                                                                        05273901
      ******************************************************************05274001
        2400-SELECT-TRAILING-PAYMENTS.                                  05274101
      * GET CURRENT WEEK'S TRAILING PAYMENTS (TTP).                     05275001
      ******************************************************************05280001
                                                                        05290001
           EXEC SQL                                                     05300001
               SELECT SUM(CTG_AMT)                                      05310001
                 INTO :PV-DB2-SUM                                       05311001
                 FROM TSASUM    SUM                                     05320001
                    , TEPPART   PRT                                     05330001
                WHERE PRT.SLS_DTE    BETWEEN :DK-BEGIN-DATE-ISO         05340001
                                         AND :DK-END-DATE-ISO           05350001
                  AND SUM.PARTN_NBR  =  PRT.PARTN_NBR                   05360001
                  AND SUM_CTG_CDE    = 'TTP'                            05370001
                  AND SUM_TYP_CDE   IN ('D', 'S')                       05380001
                  AND STR_NBR       <> 6666                             05390001
                  AND RGST_ID        = 0                                05400001
               WITH UR                                                  05410001
                                                                        05420001
           END-EXEC.                                                    05430001
                                                                        05440001
           EVALUATE SQLCODE                                             05450001
              WHEN ZERO                                                 05460001
                 MOVE PV-DB2-SUM   TO  EM-TRL-PAYMENTS                  05470001
              WHEN -305                                                 05480001
                 MOVE ZERO         TO  EM-TRL-PAYMENTS                  05490001
              WHEN OTHER                                                05500001
                 MOVE '2400-SELECT-TRAILING-PAYMENTS'                   05510001
                                                  TO PV-PARAGRAPH-NAME  05520001
                 MOVE SQLCODE                     TO PV-DB2-OPERATION   05530001
                 PERFORM 9100-SQL-ABEND                                 05540001
           END-EVALUATE.                                                05550001
                                                                        06210000
      ******************************************************************06210101
       3000-WRITE-EMAIL-FILE.                                           06211000
      ******************************************************************06211101
                                                                        06211301
           WRITE EMAIL-RECORD   FROM EM-EMAIL-BLANK-LINE                06211405
           WRITE EMAIL-RECORD   FROM EM-EMAIL-LINE1                     06211501
           WRITE EMAIL-RECORD   FROM EM-EMAIL-LINE2                     06211601
           WRITE EMAIL-RECORD   FROM EM-EMAIL-BLANK-LINE                06211701
           WRITE EMAIL-RECORD   FROM EM-EMAIL-LINE3                     06211801
           WRITE EMAIL-RECORD   FROM EM-EMAIL-BLANK-LINE                06211901
           WRITE EMAIL-RECORD   FROM EM-EMAIL-LINE4                     06212001
           WRITE EMAIL-RECORD   FROM EM-EMAIL-BLANK-LINE                06212101
           WRITE EMAIL-RECORD   FROM EM-EMAIL-LINE5                     06212201
           WRITE EMAIL-RECORD   FROM EM-EMAIL-BLANK-LINE                06212301
           WRITE EMAIL-RECORD   FROM EM-EMAIL-LINE6                     06212401
                                                                        06212501
           CLOSE EMAIL-FILE.                                            06214000
                                                                        06215001
                                                                        06216001
      ******************************************************************06220000
       9100-SQL-ABEND.                                                  07091001
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.07100000
      ******************************************************************07110000
           DISPLAY '** ABEND     **'.                                   07120000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        07130000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              07140000
           DISPLAY '** SQLCODE   ** = ' PV-DB2-OPERATION.               07150000
           DISPLAY '** STORE NBR ** = ' EPTRN-STR-NBR.                  07160000
           DISPLAY '** REGISTER  ** = ' EPTRN-RGST-ID.                  07170000
           DISPLAY '** TRAN NBR  ** = ' EPTRN-TRN-NBR.                  07180000
                                                                        07190000
      ******************************************************************07200000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     07210000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      07220000
      ******************************************************************07230000
           COPY DPPD004.                                                07240000
                                                                        07250000
           SET AC-DB2-ERROR TO TRUE.                                    07260000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07270000
                                                                        07280000
      ******************************************************************07290000
       9999-ABEND.                                                      07300000
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  07310000
      ******************************************************************07320000
           DISPLAY '** ABEND           **'.                             07330000
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  07340000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07350000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       07360000
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       07370000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07380000
      ******************************************************************07390000
      ******************************************************************07400000
