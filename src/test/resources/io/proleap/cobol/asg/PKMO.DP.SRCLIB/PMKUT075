      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT075.                                        
       AUTHOR.                 AMANDA NEWHART                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           FEBRUARY 2003.                                   
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT075 - ADHOC STORE LIST BUSINESS EVENT INTEGRITY CHECK. *        
      *                                                                *        
      *    THIS PROGRAM CREATES THE ADHOC STORE LIST BUSINESS FILE     *        
      *    USED IN THE DATA INTEGRITY PROCESS.  FIRST OPEN THE TWO     *        
      *    INPUT FILES FILE AND ONE OUTPUT FILE.  ONCE OPEN READ THE   *        
      *    TWO INPUT FILES.  IF NOTHING IS IN EITHER OF THESE FILES    *        
      *    ABEND THE PROGRAM. NEXT LOG ON TO ORACLE.  IF SUCCESSFUL    *        
      *    THEN FIND THE CURRENT SYS DATE USING A SELECT STATMENT.     *        
      *    ONCE THE SYSDATE HAS BEEN FOUND THEN SELECT THE ADHOC       *        
      *    RECORDS FROM THE PMV_RUSBL_ADHC_STR_LST VIEW THAT FALLS     *        
      *    WITHIN THE DATE RANGE AND HAVE NOT BEEN UPDATED SINCE THE   *        
      *    TIMESTAMP FILE TIME.  MOVE THE SELECTED VARIABLE TO A       *        
      *    COPYBOOK.  THEN WRITE THE RECORD TO THE OUTPUT FILE.  WHEN  *        
      *    YOU HAVE WRITTEN THE LAST RECORD RETURNED FROM THE SELECT   *        
      *    STATEMENT EXCUTE ANOTHER SELECT STATMENT THAT PULLS DATA    *        
      *    OFF THE PMT_PROMO_INTFC_ADHC_STR AND PMT_STR_SELN TABLES    *        
      *    TABLES THAT HAVE NOT UPDATED SINCE THE TIMESTAMP FILE       *        
      *    TIME.  MOVE THE SELECTED VARIABLES TO A COPYBOOK.  IF THE   *        
      *    START AND END DATE ARE NOT NULL AND ARE WITHIN THE DATE     *        
      *    RANGE THEN WRITE THAT RECORD TO THE TO THE OUTPUT FILE.     *        
      *    IF START AND END DATE DO NOT FOLLOW THIS RULE AND IF THE    *        
      *    STR-SELN-ID IS GREATER THEN 0 THEN CHECK TO SEE IF IT IS    *        
      *    ON THE ITM-ASSN TABLE.  IF SO THEN WRITE THE RECORD TO THE  *        
      *    FILE OTHERWISE CHECK THE CLEARANCE TABLES.  IF FOUND HERE   *        
      *    THEN WRITE TO THE OUTPUT FILE OTHERWISE CHECK THE TIERED    *        
      *    TABLES.IF FOUND HERE THEN WRITE TO THE OUTPUT FILE          *        
      *    OTHERWISE CHECK THE CMPTV TABLE.  IF FOUND HERE THEN WRITE  *        
      *    TO THE OUTPUT FILE OTHERWISE SKIP THIS RECORD AND CHECK ON  *        
      *    THE NEXT ADHOC RECORD.  AFTER THE LAST RECORD WAS WRITTEN   *        
      *    TO THE FILE WRITE A TOTAL LINE TO THE FILE.  FINALLY CLOSE  *        
      *    THE TWO INPUT FILES AND THE ONE OUTPUT FILE.                *        
      *                                                                *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    MM/DD/YYYY - NAME HERE - WR NO. HERE                        *        
      *    07/03/2002 - AMANDA NEWHART WR21770                         *        
      *    - ADDED A IS NULL TO THE END DATE IN THE SELECT STATEMENTS  *        
      *    07/07/2002 - COLLEEN CUMMINGS WR21770                       *        
      *    - ADDED PRICG_EFF_STRT_TMST/PRCG_EFF_END_TMST TO VIEWS AND  *        
      *      TO THE SELECT STMT FOR NRUSBL.  THESE TMST FIELDS         *        
      *      REPRESENT THE ACTUAL EFFECTIVE DATING OF THE PRICING      *        
      *      THAT THE ADHOC IS ATTACHED TO.                            *        
      ******************************************************************        
      * DATE:         07/30/2018                                       *        
      * WR/IR#:       CHG0204126                                       *        
      * PROGRAMMER:   COGNIZANT                                        *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-RANGE-FILE                                               
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT TMST-FILE                                                     
               ASSIGN                       TO INP02.                           
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
                                                                                
           SELECT ADHOC-STORE-LIST-FILE                                         
               ASSIGN                       TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DATE-RANGE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  DATE-RANGE-LINE                  PIC  X(80).                         
                                                                                
       FD  TMST-FILE                                                            
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  TMST-LINE                        PIC  X(80).                         
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
                                                                                
       FD  ADHOC-STORE-LIST-FILE                                                
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  ADHOC-STR-LIST-LINE              PIC X(125).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-DATE-RANGE-FILE.                                                  
           05  CC-START-DATE                PIC X(10).                          
           05  FILLER                       PIC X(01).                          
           05  CC-END-DATE                  PIC X(10).                          
                                                                                
       01  CC-TMST-FILE.                                                        
           05  CC-LAST-UPD-TMST             PIC X(19).                          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
070941     05  PS-END-OF-LOGON-FILE-SW      PIC X(01)   VALUE 'N'.              
070941         88  PS-END-OF-LOGON-FILE                 VALUE 'Y'.              
070941     05  PS-ORA-SRC-CTL-DONE-SW       PIC  X(01)  VALUE 'N'.              
070941         88  PS-ORA-SRC-EMPTY                     VALUE 'Y'.              
           05  PS-DATE-RANGE-FILE-SW        PIC  X(01)  VALUE 'N'.              
               88  PS-DATE-RANGE-FILE-END               VALUE 'Y'.              
               88  PS-DATE-RANGE-FILE-START             VALUE 'N'.              
           05  PS-TMST-FILE-SW              PIC  X(01)  VALUE 'N'.              
               88  PS-TMST-FILE-END                     VALUE 'Y'.              
               88  PS-TMST-FILE-START                   VALUE 'N'.              
           05  PS-END-OF-RUSBL-CSR-SWITCH   PIC X(1)    VALUE 'N'.              
               88  PS-END-OF-RUSBL-CSR                  VALUE 'Y'.              
               88  PS-NOT-END-OF-RUSBL-CSR              VALUE 'N'.              
           05  PS-NONRUS-PROMITM-CSR-SWITCH PIC X(1)    VALUE 'N'.              
               88  PS-END-OF-NONRUS-PROMITM-CSR         VALUE 'Y'.              
               88  PS-NOT-END-NONRUS-PROMITM-CSR        VALUE 'N'.              
           05  PS-NONRUSBL-CLRNC-CSR-SWITCH PIC X(1)    VALUE 'N'.              
               88  PS-END-OF-NONRUSBL-CLRNC-CSR         VALUE 'Y'.              
               88  PS-NOT-END-NONRUSBL-CLRNC-CSR        VALUE 'N'.              
           05  PS-NONRUSBL-CMPTV-CSR-SWITCH PIC X(1)    VALUE 'N'.              
               88  PS-END-OF-NONRUSBL-CMPTV-CSR         VALUE 'Y'.              
               88  PS-NOT-END-NONRUSBL-CMPTV-CSR        VALUE 'N'.              
           05  PS-NONRUSBL-PRTVR-CSR-SWITCH PIC X(1)    VALUE 'N'.              
               88  PS-END-OF-NONRUSBL-PRTVR-CSR         VALUE 'Y'.              
               88  PS-NOT-END-NONRUSBL-PRTVR-CSR        VALUE 'N'.              
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                         VALUE '/'.              
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
070941*    05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
070941     05  PC-AUTO-LOGON                PIC  X(1)   VALUE '/'.              
070941     05  PC-PROGRAM-NAME              PIC  X(08)  VALUE                   
                                                           'PMKUT075'.          
           05  PC-BUSINESS-EVNT-ID          PIC  9(03)  VALUE 050.              
           05  PC-ACTION-CDE                PIC  X(01)  VALUE 'U'.              
           05  PC-DASH                      PIC  X(01)  VALUE '-'.              
           05  PC-SPACE                     PIC  X(01)  VALUE ' '.              
           05  PC-BEG-TMST                  PIC  X(08)  VALUE                   
                                                          '00:00:00'.           
           05  PC-END-TMST                  PIC  X(08)  VALUE                   
                                                          '23:59:59'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PC-AUTO-LOGON                PIC  X(01)  VALUE '/'.              
           05  PV-SQL                       PIC S9(04)  VALUE ZEROS.            
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
           05  PV-SYS-DATE                  PIC  X(26)  VALUE SPACES.           
           05  PV-SYS-DATE-XS REDEFINES PV-SYS-DATE.                            
               10  PV-SYS-DATE-DATE         PIC  X(10).                         
               10  PV-PERIOD                PIC  X(01).                         
               10  PV-SYS-DATE-TIME         PIC  X(15).                         
           05  PV-TEMP-VALUE                PIC  X(01)  VALUE SPACES.           
           05  PV-NEW-STRT-DATE             PIC  X(26)  VALUE SPACES.           
           05  PV-NEW-END-DATE              PIC  X(26)  VALUE SPACES.           
                                                                                
       01  PV-SELECT-VARIABLES.                                                 
           05  PV-STR-SELN-ID               PIC S9(10)  VALUE ZEROS.            
           05  PV-ADHC-STR-GP-ID            PIC S9(10)  VALUE ZEROS.            
           05  PV-STR-NBR                   PIC S9(10)  VALUE ZEROS.            
           05  PV-STRT-TMST                 PIC  X(26)  VALUE SPACES.           
           05  PV-END-TMST                  PIC  X(26)  VALUE SPACES.           
           05  PV-STRT-DTE                  PIC  X(10)  VALUE SPACES.           
           05  PV-END-DTE                   PIC  X(10)  VALUE SPACES.           
           05  PV-STAT-CDE                  PIC  X(01)  VALUE SPACES.           
           05  PV-LAST-UPD-TMST             PIC  X(19)  VALUE SPACES.           
           05  PV-STRT-DTE-NULL             PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
           05  PV-END-DTE-NULL              PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
           05  PV-STRT-DTE-FMT-NULL         PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
           05  PV-END-DTE-FMT-NULL          PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
           05  PV-STR-SELN-ID-NULL          PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-WRITTEN              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROS.            
           05  PA-RUSBL-CNT                 PIC S9(11)  COMP-3                  
                                                        VALUE ZEROS.            
           05  PA-NONRUSBL-CNT              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROS.            
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE ADHOC STORE GROUP BUSINESS EVENT.                   
      ******************************************************************        
           COPY PMRD705.                                                        
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE TOTAL RECORD                                        
      ******************************************************************        
           COPY PMRD700.                                                        
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - RESET SWITCHES                                             *         
      *  - OPEN INPUT AND OUTPUT FILES                                *         
      *  - READ THE DATE RANGE FILE                                   *         
      *  - READ THE TIMESTAMP FILE                                    *         
      *  - SET UP ORACLE                                              *         
      *  - FIND THE CURRENT SYS DATE                                  *         
      *  - RETRIEVE RUSBL INFO FROM PMV_RUSBL_ADHC_STR_LST            *         
      *  - RETRIEVE NONRUSBL INFO FROM PMT_PROMO_INTFC_ADHC_STR       *         
      *  - WRITE THE TOTAL LINES                                      *         
      *  - CLOSE THE INPUT AND OUTPUT FILES                           *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           SET PS-DATE-RANGE-FILE-START TO TRUE.                                
           SET PS-TMST-FILE-START TO TRUE.                                      
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-READ-DATE-RANGE-FILE.                                   
           PERFORM B300-READ-TMST-FILE.                                         
                                                                                
070941*    PERFORM PM700-A000-ORACLE-SETUP.                                     
                                                                                
           PERFORM C100-FIND-SYSDATE.                                           
                                                                                
           PERFORM C200-DECLARE-RUSBL-CURSOR.                                   
           PERFORM C300-OPEN-RUSBL-CURSOR.                                      
           PERFORM C400-FETCH-RUSBL-CURSOR.                                     
           PERFORM C500-PROCESS-RUSBL-CURSOR                                    
               UNTIL PS-END-OF-RUSBL-CSR                                        
           PERFORM C600-CLOSE-RUSBL-CURSOR.                                     
                                                                                
           PERFORM C210-DECLAR-NONRUS-PROMITM-CSR.                              
           PERFORM C310-OPEN-NONRUS-PROMITM-CSR.                                
           PERFORM C410-FETCH-NONRUS-PROMITM-CSR.                               
           PERFORM C510-PROCES-NONRUS-PROMITM-CSR                               
               UNTIL PS-END-OF-NONRUS-PROMITM-CSR.                              
           PERFORM C610-CLOSE-NORUS-PROMITM-CSR.                                
                                                                                
           PERFORM C220-DECLAR-NONRUSBL-CLRNC-CSR.                              
           PERFORM C320-OPEN-NONRUSBL-CLRNC-CSR.                                
           PERFORM C420-FETCH-NONRUSBL-CLRNC-CSR.                               
           PERFORM C520-PROCES-NONRUSBL-CLRNC-CSR                               
               UNTIL PS-END-OF-NONRUSBL-CLRNC-CSR.                              
           PERFORM C620-CLOSE-NONRUSBL-CLRNC-CSR.                               
                                                                                
           PERFORM C230-DECLAR-NONRUSBL-CMPTV-CSR.                              
           PERFORM C330-OPEN-NONRUSBL-CMPTV-CSR.                                
           PERFORM C430-FETCH-NONRUSBL-CMPTV-CSR.                               
           PERFORM C510-PROCES-NONRUSBL-CMPTV-CSR                               
               UNTIL PS-END-OF-NONRUSBL-CMPTV-CSR.                              
           PERFORM C630-CLOSE-NONRUSBL-CMPTV-CSR.                               
                                                                                
           PERFORM C240-DECLAR-NONRUSBL-PRTVR-CSR.                              
           PERFORM C340-OPEN-NONRUSBL-PRTVR-CSR.                                
           PERFORM C440-FETCH-NONRUSBL-PRTVR-CSR.                               
           PERFORM C540-PROCES-NONRUSBL-PRTVR-CSR                               
               UNTIL PS-END-OF-NONRUSBL-PRTVR-CSR.                              
           PERFORM C640-CLOSE-NONRUSBL-PRTVR-CSR.                               
                                                                                
           PERFORM W200-WRITE-TOTAL-LINE.                                       
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           DISPLAY 'RUSBL CSR FETCHES FROM ORACLE  = ' PA-RUSBL-CNT.            
           DISPLAY 'NONRUSBL CSR FETCH FROM ORACLE  = ' PA-NONRUSBL-CNT.        
           DISPLAY 'RECORDS WRITTEN TO FILE = ' PA-RECS-WRITTEN.                
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN TWO INPUT FILES AND ONE OUTPUT FILE                   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
070941     DISPLAY 'ORACLE FILE OPEN'.                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     DISPLAY 'ORACLE FILE READ'.                                          
070941     PERFORM 7000-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     DISPLAY 'ORACLE FILE CLOSE'.                                         
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070941         PERFORM 9900-ORACLE-ABEND                                        
070941     END-IF.                                                              
                                                                                
           OPEN INPUT  DATE-RANGE-FILE                                          
                INPUT  TMST-FILE                                                
                OUTPUT ADHOC-STORE-LIST-FILE.                                   
                                                                                
      *****************************************************************         
      * B200-READ-DATE-RANGE-FILE.                                    *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE DATE RANGE FILE                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-READ-DATE-RANGE-FILE.                                               
                                                                                
           READ DATE-RANGE-FILE INTO CC-DATE-RANGE-FILE                         
              AT END                                                            
                 SET PS-DATE-RANGE-FILE-END TO TRUE                             
           END-READ.                                                            
                                                                                
           IF PS-DATE-RANGE-FILE-END                                            
               MOVE 'NO DATE RANGE FILE TO PROCESS'                             
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-START-DATE = ' CC-START-DATE                         
               DISPLAY 'CC-END-DATE   = ' CC-END-DATE                           
                                                                                
               STRING CC-START-DATE DELIMITED BY SPACE                          
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-BEG-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-STRT-DATE                                     
                                                                                
               STRING CC-END-DATE DELIMITED BY SPACE                            
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-END-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-END-DATE                                      
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B300-READ-TMST-FILE.                                          *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE TMST FILE                                      *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B300-READ-TMST-FILE.                                                     
                                                                                
           READ TMST-FILE INTO CC-TMST-FILE                                     
              AT END                                                            
                 SET PS-TMST-FILE-END TO TRUE                                   
           END-READ.                                                            
                                                                                
           IF PS-TMST-FILE-END                                                  
               MOVE 'NO TMST FILE TO PROCESS'                                   
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-LAST-UPD-TMST = ' CC-LAST-UPD-TMST                   
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE TWO INPUT FILES AND ONE OUTPUT FILE              *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  DATE-RANGE-FILE                                               
                  TMST-FILE                                                     
                  ADHOC-STORE-LIST-FILE.                                        
                                                                                
      *****************************************************************         
      * C100-FIND-SYSDATE:                                            *         
      * ==> PROCESSES:                                                *         
      *  - SINCE NO TABLE IS ACTUALLY USED WE USE THE DUAL TABLE      *         
      *  - FIND THE CURRENT DATE AND TIME                             *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C100-FIND-SYSDATE.                                                       
           EXEC SQL                                                             
               SELECT                                                           
                   SYSDATE                                                      
               INTO :PV-SYS-DATE                                                
               FROM                                                             
                   DUAL                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 MOVE PC-DASH TO PV-PERIOD                                      
                 STRING PV-SYS-DATE-XS DELIMITED BY SPACE                       
                        '.000000'      DELIMITED BY SIZE                        
                        INTO PV-SYS-DATE-XS                                     
                 DISPLAY 'SYS DATE = ' PV-SYS-DATE-XS                           
                 CONTINUE                                                       
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-FIND-SYSDATE'   TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C200-DECLARE-RUSBL-CURSOR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - USED A VIEW IN THE FROM CLAUSE INSTEAD OF IND TABLES       *         
      *  - DECLARE CSR THAT RETRIEVES RUSBL INFORMATION FROM          *         
      *    PMV_RUSBL_ADHC_STR_LST                                     *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C200-DECLARE-RUSBL-CURSOR.                                               
                                                                                
           EXEC SQL                                                             
                DECLARE RUSBLCSR                                                
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.ADHC_STR_GP_ID                                       
                        ,A.STORE                                                
                        ,TO_CHAR(A.STRT_TMST,                                   
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.END_TMST,                                    
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                    FROM PMV_RUSBL_ADHC_STR_LST A                               
                    WHERE (A.END_TMST >= TO_DATE(                               
                          :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')           
                          OR A.END_TMST IS NULL)                                
                      AND A.STRT_TMST <= TO_DATE(                               
                          :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')            
                      AND A.LAST_UPD_TMST <=                                    
                      TO_DATE(:CC-LAST-UPD-TMST,'YYYY-MM-DD-HH24.MI.SS')        
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C210-DECLAR-NONRUS-PROMITM-CSR                                *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE CSR THAT RETRIEVES NONRUSBL PROMO ITM INFORMATION  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C210-DECLAR-NONRUS-PROMITM-CSR.                                          
                                                                                
           EXEC SQL                                                             
                DECLARE NONRUSBLPROMITMCSR                                      
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.ADHC_STR_GP_ID                                       
                        ,A.STR_NBR                                              
                        ,TO_CHAR(A.EFF_STRT_TMST,                               
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.EFF_END_TMST,                                
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.EFF_STRT_TMST,                               
                                           'YYYY-MM-DD')                        
                        ,TO_CHAR(A.EFF_END_TMST,                                
                                           'YYYY-MM-DD')                        
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                        ,TO_CHAR(A.PRICG_EFF_STRT_TMST,                         
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.PRICG_EFF_END_TMST,                          
                                           'YYYY-MM-DD-HH24.MI.SS')             
                    FROM PMV_NRUSBL_ADHC_LST_PROMO_ITM A                        
                    WHERE A.PRICG_EFF_END_TMST >= TO_DATE(                      
                             :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')        
                      AND A.PRICG_EFF_STRT_TMST <= TO_DATE(                     
                             :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')         
                      AND A.LAST_UPD_TMST <=                                    
                      TO_DATE(:CC-LAST-UPD-TMST,'YYYY-MM-DD-HH24.MI.SS')        
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C220-DECLAR-NONRUSBL-CLRNC-CSR                                *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE CSR THAT RETRIEVES NONRUSBL CLRNC INFORMATION      *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C220-DECLAR-NONRUSBL-CLRNC-CSR.                                          
                                                                                
           EXEC SQL                                                             
                DECLARE NONRUSBLCLRNCCSR                                        
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.ADHC_STR_GP_ID                                       
                        ,A.STR_NBR                                              
                        ,TO_CHAR(A.EFF_STRT_TMST,                               
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.EFF_END_TMST,                                
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.EFF_STRT_TMST,                               
                                           'YYYY-MM-DD')                        
                        ,TO_CHAR(A.EFF_END_TMST,                                
                                           'YYYY-MM-DD')                        
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                        ,TO_CHAR(A.PRICG_EFF_STRT_TMST,                         
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.PRICG_EFF_END_TMST,                          
                                           'YYYY-MM-DD-HH24.MI.SS')             
                    FROM PMV_NRUSBL_ADHC_STR_LST_CLRNC A                        
                    WHERE A.PRICG_EFF_END_TMST >= TO_DATE(                      
                             :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')        
                      AND A.PRICG_EFF_STRT_TMST <= TO_DATE(                     
                             :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')         
                      AND A.LAST_UPD_TMST <=                                    
                      TO_DATE(:CC-LAST-UPD-TMST,'YYYY-MM-DD-HH24.MI.SS')        
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C230-DECLAR-NONRUSBL-CMPTV-CSR                                *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE CSR THAT RETRIEVES NONRUSBL CMPTV INFORMATION      *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C230-DECLAR-NONRUSBL-CMPTV-CSR.                                          
                                                                                
           EXEC SQL                                                             
                DECLARE NONRUSBLCMPTVCSR                                        
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.ADHC_STR_GP_ID                                       
                        ,A.STR_NBR                                              
                        ,TO_CHAR(A.EFF_STRT_TMST,                               
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.EFF_END_TMST,                                
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.EFF_STRT_TMST,                               
                                           'YYYY-MM-DD')                        
                        ,TO_CHAR(A.EFF_END_TMST,                                
                                           'YYYY-MM-DD')                        
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                        ,TO_CHAR(A.PRICG_EFF_STRT_TMST,                         
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.PRICG_EFF_END_TMST,                          
                                           'YYYY-MM-DD-HH24.MI.SS')             
                    FROM PMV_NRUSBL_ADHC_STR_LST_CMPTV A                        
                    WHERE A.PRICG_EFF_END_TMST >= TO_DATE(                      
                             :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')        
                      AND A.PRICG_EFF_STRT_TMST <= TO_DATE(                     
                             :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')         
                      AND A.LAST_UPD_TMST <=                                    
                      TO_DATE(:CC-LAST-UPD-TMST,'YYYY-MM-DD-HH24.MI.SS')        
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C240-DECLAR-NONRUSBL-PRTVR-CSR                                *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE CSR THAT RETRIEVES NONRUSBL CMPTV INFORMATION      *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C240-DECLAR-NONRUSBL-PRTVR-CSR.                                          
                                                                                
           EXEC SQL                                                             
                DECLARE NONRUSBLPRTVRCSR                                        
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.ADHC_STR_GP_ID                                       
                        ,A.STR_NBR                                              
                        ,TO_CHAR(A.EFF_STRT_TMST,                               
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.EFF_END_TMST,                                
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.EFF_STRT_TMST,                               
                                           'YYYY-MM-DD')                        
                        ,TO_CHAR(A.EFF_END_TMST,                                
                                           'YYYY-MM-DD')                        
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                        ,TO_CHAR(A.PRICG_EFF_STRT_TMST,                         
                                           'YYYY-MM-DD-HH24.MI.SS')             
                        ,TO_CHAR(A.PRICG_EFF_END_TMST,                          
                                           'YYYY-MM-DD-HH24.MI.SS')             
                    FROM PMV_NRUSBL_ADHC_STR_LST_PRTVER A                       
                    WHERE A.PRICG_EFF_END_TMST >= TO_DATE(                      
                             :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')        
                      AND A.PRICG_EFF_STRT_TMST <= TO_DATE(                     
                             :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')         
                      AND A.LAST_UPD_TMST <=                                    
                      TO_DATE(:CC-LAST-UPD-TMST,'YYYY-MM-DD-HH24.MI.SS')        
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C300-OPEN-RUSBL-CURSOR.                                       *         
      * ==> PROCESSES:                                                *         
      *  - OPEN RUSBLCSR                                              *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C300-OPEN-RUSBL-CURSOR.                                                  
                                                                                
           EXEC SQL                                                             
                 OPEN RUSBLCSR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C300-OPEN-RUSBL-CURSOR      '                          
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON RUSBL-CURSOR '                           
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C310-OPEN-NONNRUSBL-CURSOR.                                   *         
      * ==> PROCESSES:                                                *         
      *  - OPEN NONRUSBLPROMITMCSR                                    *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C310-OPEN-NONRUS-PROMITM-CSR.                                            
                                                                                
           EXEC SQL                                                             
                 OPEN NONRUSBLPROMITMCSR                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C310-OPEN-NONRUS-PROMITM-CSR '                         
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON NONRUSBLPROMITMCSR '                     
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C320-OPEN-NONRUSBL-CLRNC-CSR.                                 *         
      * ==> PROCESSES:                                                *         
      *  - OPEN NONRUSBLCLRNCCSR                                      *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C320-OPEN-NONRUSBL-CLRNC-CSR.                                            
                                                                                
           EXEC SQL                                                             
                 OPEN NONRUSBLCLRNCCSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C320-OPEN-NONRUSBL-CLRNC-CSR '                         
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON NONRUSBLCLRNCCSR '                       
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C330-OPEN-NONRUSBL-CMPTV-CSR.                                 *         
      * ==> PROCESSES:                                                *         
      *  - OPEN NONRUSBLCMPTVCSR                                      *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C330-OPEN-NONRUSBL-CMPTV-CSR.                                            
                                                                                
           EXEC SQL                                                             
                 OPEN NONRUSBLCMPTVCSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C330-OPEN-NONRUSBL-CMPTV-CSR '                         
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON NONRUSBLCMPTVCSR '                       
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C340-OPEN-NONRUSBL-PRTVR-CSR.                                 *         
      * ==> PROCESSES:                                                *         
      *  - OPEN NONRUSBLPRTVRCSR                                      *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C340-OPEN-NONRUSBL-PRTVR-CSR.                                            
                                                                                
           EXEC SQL                                                             
                 OPEN NONRUSBLPRTVRCSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C340-OPEN-NONRUSBL-PRTVR-CSR '                         
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON NONRUSBLPRTVRCSR '                       
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C400-FETCH-RUSBL-CURSOR.                                      *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1405 = FETCHED COLUMN VALUE IS NULL REASON FOR NULL VALUES *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C500-PROCESS-RUSBL-CURSOR  *         
      *****************************************************************         
       C400-FETCH-RUSBL-CURSOR.                                                 
                                                                                
           EXEC SQL                                                             
               FETCH RUSBLCSR                                                   
                INTO  :PV-ADHC-STR-GP-ID                                        
                    , :PV-STR-NBR                                               
                    , :PV-STRT-TMST                                             
                    , :PV-END-TMST                                              
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-RUSBL-CNT                                        
                   PERFORM D100-MOVE-TO-CPYBK                                   
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'RUSBL CNT FORM FETCH = ' PA-RUSBL-CNT               
                   DISPLAY 'END OF RUSBL CSR'                                   
                   SET PS-END-OF-RUSBL-CSR TO TRUE                              
               WHEN OTHER                                                       
                   DISPLAY 'ADHOC STR GP ID = ' PV-ADHC-STR-GP-ID               
                   MOVE 'C400-FETCH-RUSBL-CURSOR        '                       
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON RUSBL-CURSOR   '                        
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C410-FETCH-NONRUS-PROMITM-CSR                                 *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1405 = FETCHED COLUMN VALUE IS NULL REASON FOR NULL VALUES *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE,                            *         
      *                     C510-PROCES-NONRUS-PROMITM-CSR            *         
      *****************************************************************         
       C410-FETCH-NONRUS-PROMITM-CSR.                                           
                                                                                
           EXEC SQL                                                             
               FETCH NONRUSBLPROMITMCSR                                         
                INTO                                                            
                      :PV-ADHC-STR-GP-ID                                        
                    , :PV-STR-NBR                                               
                    , :PV-STRT-TMST            :PV-STRT-DTE-NULL                
                    , :PV-END-TMST             :PV-END-DTE-NULL                 
                    , :PV-STRT-DTE             :PV-STRT-DTE-FMT-NULL            
                    , :PV-END-DTE              :PV-END-DTE-FMT-NULL             
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-NONRUSBL-CNT                                     
                   PERFORM D100-MOVE-TO-CPYBK                                   
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'END OF NONRUSBL PROMO ITM CSR'                      
                   SET PS-END-OF-NONRUS-PROMITM-CSR TO TRUE                     
               WHEN OTHER                                                       
                   DISPLAY 'ADHOC STR GP ID = ' PV-ADHC-STR-GP-ID               
                   MOVE 'C410-FETCH-NONRUS-PROMITM-CSR     '                    
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON NONRUSBLPROMITMCSR'                     
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C420-FETCH-NONRUSBL-CLRNC-CSR                                 *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1405 = FETCHED COLUMN VALUE IS NULL REASON FOR NULL VALUES *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE,                            *         
      *                     C520-PROCES-NONRUSBL-CLRNC-CSR            *         
      *****************************************************************         
       C420-FETCH-NONRUSBL-CLRNC-CSR.                                           
                                                                                
           EXEC SQL                                                             
               FETCH NONRUSBLCLRNCCSR                                           
                INTO                                                            
                      :PV-ADHC-STR-GP-ID                                        
                    , :PV-STR-NBR                                               
                    , :PV-STRT-TMST            :PV-STRT-DTE-NULL                
                    , :PV-END-TMST             :PV-END-DTE-NULL                 
                    , :PV-STRT-DTE             :PV-STRT-DTE-FMT-NULL            
                    , :PV-END-DTE              :PV-END-DTE-FMT-NULL             
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-NONRUSBL-CNT                                     
                   PERFORM D100-MOVE-TO-CPYBK                                   
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'END OF NONRUSBLCLRNCCSR '                           
                   SET PS-END-OF-NONRUSBL-CLRNC-CSR TO TRUE                     
               WHEN OTHER                                                       
                   DISPLAY 'ADHOC STR GP ID = ' PV-ADHC-STR-GP-ID               
                   MOVE 'C420-FETCH-NONRUSBL-CLRNC-CSR  '                       
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON NONRUSBLCLRNCCSR'                       
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C430-FETCH-NONRUSBL-CMPTV-CSR                                 *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1405 = FETCHED COLUMN VALUE IS NULL REASON FOR NULL VALUES *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE,                            *         
      *                     C510-PROCES-NONRUSBL-CMPTV-CSR            *         
      *****************************************************************         
       C430-FETCH-NONRUSBL-CMPTV-CSR.                                           
                                                                                
           EXEC SQL                                                             
               FETCH NONRUSBLCMPTVCSR                                           
                INTO                                                            
                      :PV-ADHC-STR-GP-ID                                        
                    , :PV-STR-NBR                                               
                    , :PV-STRT-TMST            :PV-STRT-DTE-NULL                
                    , :PV-END-TMST             :PV-END-DTE-NULL                 
                    , :PV-STRT-DTE             :PV-STRT-DTE-FMT-NULL            
                    , :PV-END-DTE              :PV-END-DTE-FMT-NULL             
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-NONRUSBL-CNT                                     
                   PERFORM D100-MOVE-TO-CPYBK                                   
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'END OF NONRUSBLCMPTVCSR '                           
                   SET PS-END-OF-NONRUSBL-CMPTV-CSR TO TRUE                     
               WHEN OTHER                                                       
                   DISPLAY 'ADHOC STR GP ID = ' PV-ADHC-STR-GP-ID               
                   MOVE 'C430-FETCH-NONRUSBL-CMPTV-CSR  '                       
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON NONRUSBLCMPTVCSR'                       
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C440-FETCH-NONRUSBL-PRTVR-CSR                                 *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1405 = FETCHED COLUMN VALUE IS NULL REASON FOR NULL VALUES *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE,                            *         
      *                     C540-PROCES-NONRUSBL-PRTVR-CSR            *         
      *****************************************************************         
       C440-FETCH-NONRUSBL-PRTVR-CSR.                                           
                                                                                
           EXEC SQL                                                             
               FETCH NONRUSBLPRTVRCSR                                           
                INTO                                                            
                      :PV-ADHC-STR-GP-ID                                        
                    , :PV-STR-NBR                                               
                    , :PV-STRT-TMST            :PV-STRT-DTE-NULL                
                    , :PV-END-TMST             :PV-END-DTE-NULL                 
                    , :PV-STRT-DTE             :PV-STRT-DTE-FMT-NULL            
                    , :PV-END-DTE              :PV-END-DTE-FMT-NULL             
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-NONRUSBL-CNT                                     
                   PERFORM D100-MOVE-TO-CPYBK                                   
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'END OF NONRUSBLPRTVRCSR '                           
                   SET PS-END-OF-NONRUSBL-PRTVR-CSR TO TRUE                     
               WHEN OTHER                                                       
                   DISPLAY 'ADHOC STR GP ID = ' PV-ADHC-STR-GP-ID               
                   MOVE 'C440-FETCH-NONRUSBL-PRTVR-CSR  '                       
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON NONRUSBLPRTVRCSR'                       
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C500-PROCESS-RUSBL-CURSOR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C500-PROCESS-RUSBL-CURSOR.                                               
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C400-FETCH-RUSBL-CURSOR.                                     
                                                                                
      *****************************************************************         
      * C510-PROCES-NONRUS-PROMITM-CSR                                *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C510-PROCES-NONRUS-PROMITM-CSR.                                          
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C410-FETCH-NONRUS-PROMITM-CSR.                               
                                                                                
      *****************************************************************         
      * C520-PROCES-NONRUSBL-CLRNC-CSR                                *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C520-PROCES-NONRUSBL-CLRNC-CSR.                                          
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C420-FETCH-NONRUSBL-CLRNC-CSR.                               
                                                                                
      *****************************************************************         
      * C510-PROCES-NONRUSBL-CMPTV-CSR                                *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C510-PROCES-NONRUSBL-CMPTV-CSR.                                          
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C430-FETCH-NONRUSBL-CMPTV-CSR.                               
                                                                                
      *****************************************************************         
      * C540-PROCES-NONRUSBL-PRTVR-CSR                                *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C540-PROCES-NONRUSBL-PRTVR-CSR.                                          
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C440-FETCH-NONRUSBL-PRTVR-CSR.                               
                                                                                
      *****************************************************************         
      * C600-CLOSE-RUSBL-CURSOR.                                      *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE RUSBLCSR                                         *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C600-CLOSE-RUSBL-CURSOR.                                                 
                                                                                
           EXEC SQL                                                             
                CLOSE RUSBLCSR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C600-CLOSE-RUSBL-CURSOR      '                          
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON RUSBL-CSR     '                          
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C610-CLOSE-NORUS-PROMITM-CSR.                                 *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE NONRUSBLCSR                                      *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C610-CLOSE-NORUS-PROMITM-CSR.                                            
                                                                                
           EXEC SQL                                                             
                CLOSE NONRUSBLPROMITMCSR                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C610-CLOSE-NORUS-PROMITM-CSR '                          
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON NONRUSBLPROMITMCSR'                      
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C620-CLOSE-NONRUSBL-CLRNC-CSR.                                *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE NONRUSBLCLRNCCSR                                 *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C620-CLOSE-NONRUSBL-CLRNC-CSR.                                           
                                                                                
           EXEC SQL                                                             
                CLOSE NONRUSBLCLRNCCSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C620-CLOSE-NONRUSBL-CLRNC-CSR '                         
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON NONRUSBLCLRNCCSR '                       
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C630-CLOSE-NONRUSBL-CMPTV-CSR.                                *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE NONRUSBLCMPTVCSR                                 *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C630-CLOSE-NONRUSBL-CMPTV-CSR.                                           
                                                                                
           EXEC SQL                                                             
                CLOSE NONRUSBLCMPTVCSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C630-CLOSE-NONRUSBL-CMPTV-CSR '                         
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON NONRUSBLCMPTVCSR '                       
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C640-CLOSE-NONRUSBL-PRTVR-CSR.                                *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE NONRUSBLPRTVRCSR                                 *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C640-CLOSE-NONRUSBL-PRTVR-CSR.                                           
                                                                                
           EXEC SQL                                                             
                CLOSE NONRUSBLPRTVRCSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C640-CLOSE-NONRUSBL-PRTVR-CSR '                         
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON NONRUSBLPRTVRCSR '                       
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D100-MOVE-TO-CPYBK.                                           *         
      * ==> PROCESSES:                                                *         
      *  - MOVE THE VARIABLES TO THE COPYBOOK                         *         
      * ==> PERFORMED FROM:                                           *         
      *   C400-FETCH-RUSBL-CURSOR, C410-FETCH-NONRUS-PROMITM-CSR      *         
      *   C420-FETCH-NONRUSBL-CLRNC-CSR, C430-FETCH-NONRUSBL-CMPTV-CSR*         
      *   C440-FETCH-NONRUSBL-PRTVR-CSR                               *         
      *****************************************************************         
       D100-MOVE-TO-CPYBK.                                                      
                                                                                
           INITIALIZE PM705-ADHOC-STR-LST-RECORD.                               
           MOVE PC-BUSINESS-EVNT-ID  TO PM705-BUSINESS-EVNT-ID.                 
           MOVE PC-ACTION-CDE        TO PM705-ACTION-CDE.                       
           MOVE PV-SYS-DATE-XS       TO PM705-BUSINESS-EVNT-CRE-TMST.           
           MOVE PV-ADHC-STR-GP-ID    TO PM705-ADHC-STR-GP-ID.                   
           MOVE PV-STR-NBR           TO PM705-STR-NBR.                          
           IF PV-STRT-TMST > 0                                                  
                STRING PV-STRT-TMST DELIMITED BY SPACE                          
                      '.000000'     DELIMITED BY SIZE                           
                      INTO PM705-STRT-TMST                                      
           END-IF.                                                              
           IF PV-END-TMST > 0                                                   
                STRING PV-END-TMST DELIMITED BY SPACE                           
                      '.000000'     DELIMITED BY SIZE                           
                      INTO PM705-END-TMST                                       
           END-IF.                                                              
           MOVE PV-STAT-CDE          TO PM705-STAT-CDE.                         
           STRING PV-LAST-UPD-TMST DELIMITED BY SPACE                           
                  '.000000'     DELIMITED BY SIZE                               
                  INTO PM705-LAST-UPD-TMST.                                     
                                                                                
      *****************************************************************         
      * W100-WRITE-DETAIL.                                            *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DETAIL LINE TO THE ADHOC STORE GP OUTPUT FILE.   *         
      * ==> PERFORMED FROM: C110-FIND-ITM-ASSNT-CSR, C120-FIND-CLRNC, *         
      *    C130-FIND-TIERED, C140-FIND-CMPTV-PRICE,                   *         
      *    C500-PROCESS-RUSBL-CURSOR, C510-PROCES-NONNRUSBL-CURSOR    *         
      *****************************************************************         
       W100-WRITE-DETAIL.                                                       
                                                                                
           WRITE ADHOC-STR-LIST-LINE FROM PM705-ADHOC-STR-LST-RECORD.           
                                                                                
           ADD +1                    TO PA-RECS-WRITTEN.                        
                                                                                
      *****************************************************************         
      * W200-WRITE-TOTAL-LINE                                         *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE TOTAL LINE TO THE ADHOC OUTPUT FILE.             *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       W200-WRITE-TOTAL-LINE.                                                   
                                                                                
           MOVE PC-BUSINESS-EVNT-ID TO PM700-BUSINESS-EVNT-ID.                  
           MOVE PA-RECS-WRITTEN TO PM700-TOTAL-NBR-OF-RECS.                     
           WRITE ADHOC-STR-LIST-LINE FROM PM700-TOTAL-RECORD.                   
                                                                                
070941****************************************************************          
070941** READ THE ORACLE LOGON CONTROL CARD.                        **          
070941****************************************************************          
070941 7000-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
070941*    DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
070941     DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL.                            
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
070941*    DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
