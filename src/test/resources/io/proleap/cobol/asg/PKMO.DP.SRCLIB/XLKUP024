000100 TITLE 'CREATE DST_EVNT_MSG_LOG FROM PACK MESSAGES'.                      
000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    XLKUP024.                                                 
000400 AUTHOR.        DENNIS STEFFEN.                                           
000500 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000600 DATE-WRITTEN.  DAFFODIL TIME 2003.                                       
000700 DATE-COMPILED.                                                           
000800                                                                          
000900 ENVIRONMENT DIVISION.                                                    
001000 CONFIGURATION SECTION.                                                   
001100 SOURCE-COMPUTER.  IBM-3090.                                              
001200 OBJECT-COMPUTER.  IBM-3090.                                              
001300 DATA DIVISION.                                                           
001400                                                                          
001500 WORKING-STORAGE SECTION.                                                 
001600                                                                          
001700 01  PC-CONSTANT-STUFF.                                                   
001800     05  PC-CURRENT-PROGRAM-NAME      PIC X(08) VALUE 'XLKUP024'.         
001900 01  ABEND-CODE                       PIC S9(04) VALUE ZERO               
002000                                                  COMP SYNC.              
002100 01  PS-SWITCHES.                                                         
002200     05  PS-PROCESS-INDICATOR-SW      PIC X(01)   VALUE 'N'.              
002300         88  PS-PROCESS-CONTINUE-YES                 VALUE 'Y'.           
002400         88  PS-PROCESS-CONTINUE-NO                  VALUE 'N'.           
002500     05  PS-END-OF-MSG                PIC X(01)   VALUE 'N'.              
002600         88  PS-END-OF-MSG-YES                    VALUE 'Y'.              
002700         88  PS-END-OF-MSG-NO                     VALUE 'N'.              
002800     05  PS-END-OF-STORES-SW          PIC X(01)   VALUE 'N'.              
002900         88  PS-END-OF-STORES-YES                 VALUE 'Y'.              
003000         88  PS-END-OF-STORES-NO                  VALUE 'N'.              
003100*    05  PS-POERRQ-FOUND-SW           PIC X(01)   VALUE 'N'.              
003200*        88  PS-POERRQ-FOUND-YES                  VALUE 'Y'.              
003300*        88  PS-POERRQ-FOUND-NO                   VALUE 'N'.              
003400                                                                          
003500                                                                          
003600 01  PV-CRAPOLA.                                                          
003700     05  PV-START-TIMESTAMP           PIC X(26).                          
003800     05  PV-CURRENT-TIMESTAMP         PIC X(26).                          
003900     05  PV-START                     PIC S9(7) COMP-3 VALUE ZERO.        
004000     05  PV-LENGTH                    PIC S9(7) COMP-3 VALUE ZERO.        
004100     05  PV-SAVE-LENGTH               PIC S9(7) COMP-3 VALUE ZERO.        
004200     05  PV-SEGMENT-COUNT             PIC S9(7) COMP-3 VALUE ZERO.        
004300     05  PV-DISP-START                PIC 9(07).                          
004400     05  PV-DISP-LENGTH               PIC 9(07).                          
004500                                                                          
004600     05  PV-PO-CORRELID.                                                  
004700         10  PV-PO-NBR-TO-GET      PIC 9(08)  VALUE ZEROES.               
004800         10  FILLER                PIC X(16)  VALUE SPACES.               
004900                                                                          
005000                                                                          
005100                                                                          
005200     05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.              
005300     05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.              
005400     05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.              
005500     05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.              
005600     05  PV-DISPLAY-RETURN-CODE       PIC ZZZZ9- VALUE ZERO.              
005700/                                                                         
005800*                                                                         
005900*    MQ-SERIES STUFF                                                      
006000*                                                                         
006100 01  MQV-WORKING-STORAGE.                                                 
006200     05  MQV-MQCONN                 PIC  X(08) VALUE 'CSQBCONN'.          
006300     05  MQV-MQOPEN                 PIC  X(08) VALUE 'CSQBOPEN'.          
006400     05  MQV-MQPUT                  PIC  X(08) VALUE 'CSQBPUT'.           
006500     05  MQV-MQGET                  PIC  X(08) VALUE 'CSQBGET'.           
006600     05  MQV-MQCMIT                 PIC  X(08) VALUE 'CSQBCOMM'.          
006700     05  MQV-MQBACK                 PIC  X(08) VALUE 'CSQBBACK'.          
006800     05  MQV-MQCLOSE                PIC  X(08) VALUE 'CSQBCLOS'.          
006900     05  MQV-MQDISC                 PIC  X(08) VALUE 'CSQBDISC'.          
007000     05  MQV-MQCHECK                PIC  X(08) VALUE 'MQR2C'.             
007100     05  MQV-HCONN                  PIC S9(09) BINARY VALUE 0.            
007200     05  MQV-COMPLETION-CODE        PIC S9(09) BINARY.                    
007300     05  MQV-REASON                 PIC S9(09) BINARY.                    
007400     05  MQV-OPEN-OPTIONS           PIC S9(09) BINARY.                    
007500     05  MQV-LOC-HANDLE1            PIC S9(09) BINARY VALUE 0.            
007600     05  MQV-LOC-HANDLE2            PIC S9(09) BINARY VALUE 0.            
007700     05  MQV-ERROR-HANDLE           PIC S9(09) BINARY VALUE 0.            
007800     05  MQV-REASON-TEXT            PIC X(30)  VALUE SPACES.              
007900     05  MQV-MSGBUFFER            PIC X(1000000)  VALUE SPACES.           
008000     05  MQV-MSG                  PIC X(1000000)  VALUE SPACES.           
008100     05  MQV-ERRMSGBUF            PIC X(1026)  VALUE SPACES.              
008200     05  MQV-MSG-LENGTH           PIC S9(09)                              
008300                                     BINARY VALUE +1026.                  
008400     05  MQV-DATA-LENGTH          PIC S9(09) BINARY.                      
008500 01  MQM-OBJECT-DESCRIPTOR.                                               
008600     COPY CMQODV.                                                         
008700 01  MQM-MESSAGE-DESCRIPTOR.                                              
008800     COPY CMQMDV.                                                         
008900 01  MQM-GET-MESSAGE-OPTIONS.                                             
009000     COPY CMQGMOV.                                                        
009100 01  MQM-PUT-MESSAGE-OPTIONS.                                             
009200     COPY CMQPMOV.                                                        
009300 01  MQM-CONSTANTS.                                                       
009400     COPY CMQV.                                                           
009500/                                                                         
009600 01  PV-ABEND-VARIABLES.                                                  
009700     05  PV-ABEND-CODE         PIC S9(04) VALUE +0 COMP SYNC.             
009800         88  ABEND-4013-DB2-ERROR             VALUE +4013.                
009900         88  ABEND-4020-MQ-ERROR              VALUE +4020.                
010000         88  ABEND-4095-NOTIFY                VALUE +4095.                
010100                                                                          
010200 01  PV-WORK-AREA                     PIC X(528) VALUE SPACES.            
010300 01  PV-DEBUG-ID                      PIC X(20) VALUE SPACES.             
010400 01  PV-END-OF-PACK-SEGMENT.                                              
010500     05  FILLER                       PIC X(20) VALUE                     
010600         'END OF PACK MESSAGE '.                                          
010700     05  PV-END-ORDER-NBR             PIC 9(08) VALUE ZEROES.             
010800                                                                          
010900/                                                                         
011000 01  XLRD011-RECORD.                                                      
011100     COPY XLRD011.                                                        
011200/                                                                         
011300*    ERROR QUEUE FORMAT                                                   
011400     COPY XLRD033.                                                        
011500/                                                                         
011600*    NUMERIC EDITING ROUTINE                                              
011700     COPY DPWS010I.                                                       
011800/                                                                         
011900*    DST_EVNT_MSG_LOG                                                     
012000     EXEC SQL                                                             
012100          INCLUDE DST00004                                                
012200     END-EXEC.                                                            
012300                                                                          
012400     EXEC SQL                                                             
012500          INCLUDE SQLCA                                                   
012600     END-EXEC.                                                            
012700     COPY SQLCA2.                                                         
012800                                                                          
012900 LINKAGE SECTION.                                                         
013000     COPY XLRD012.                                                        
013100/                                                                         
013200 PROCEDURE DIVISION USING XLRD012-RECORD.                                 
013300                                                                          
013400     PERFORM 1000-INITIALIZATION.                                         
013500                                                                          
013600     PERFORM 2000-PROCESS                                                 
013700         UNTIL PS-PROCESS-CONTINUE-NO                                     
013800           OR  PS-END-OF-MSG-YES                                          
013900                                                                          
014000     PERFORM 9000-END.                                                    
014100     GOBACK.                                                              
014200/                                                                         
014300 1000-INITIALIZATION.                                                     
014400*    IF XL012-ACTIVITY-LOG                                                
014500*        PERFORM 1005-INIT-ACTIVITY-LOG                                   
014600*    ELSE                                                                 
014700*        PERFORM 1010-INIT-ERROR-LOG                                      
014800*    END-IF.                                                              
014900                                                                          
015000     IF XL012-DEBUG-YES                                                   
015100         DISPLAY SPACES                                                   
015200         DISPLAY 'XLKUP024 - XLKUP024 '                                   
015300         DISPLAY 'XLKUP024 - 1000-INITIALIZATION'                         
015400     END-IF                                                               
015500                                                                          
015600     INITIALIZE XL012-RETURN-CODE                                         
015700                                                                          
015800*    START TIME FOR ALL DST_EVNT_MSG_LOG ROWS INSERTED LATER              
015900     EXEC SQL                                                             
016000         SET :PV-START-TIMESTAMP = CURRENT TIMESTAMP                      
016100     END-EXEC                                                             
016200                                                                          
016300     MOVE PV-START-TIMESTAMP          TO XL012-PACK-BEG-TMST              
016400     MOVE 1                           TO PV-START                         
016500     MOVE 20                          TO PV-LENGTH.                       
016600                                                                          
016700     MOVE ZERO                        TO PV-SEGMENT-COUNT                 
016800                                                                          
016900     SET PS-PROCESS-CONTINUE-YES                                          
017000         PS-END-OF-MSG-NO             TO TRUE.                            
017100/                                                                         
017200 2000-PROCESS.                                                            
017300                                                                          
017400     IF XL012-DEBUG-YES                                                   
017500         DISPLAY 'XLKUP024 - 2000-PROCESS'                                
017600     END-IF                                                               
017700*    IN THE BEGINNING, WE START AT POSITION 1 FOR LENGTH OF 20            
017800*    WE ARE LOOKING FOR THE MESSAGE THAT IDENTIFIES A PACK                
017900*    ACTION.                                                              
018000                                                                          
018100*   MOVE THE ORDER NUMBER TO THE PACK ENDING MESSAGE                      
018200     MOVE XL012-PACK-BUFFER (21:8)    TO PV-END-ORDER-NBR                 
018300     IF XL012-DEBUG-YES                                                   
018400         DISPLAY 'XLKUP024 - ORDER NBR = '                                
018500                            XL012-PACK-BUFFER (21:8)                      
018600     END-IF                                                               
018700                                                                          
018800     MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH) TO PV-DEBUG-ID           
018900                                                    XL011-MSG-ID          
019000                                                                          
019100     IF XL012-DEBUG-YES                                                   
019200         DISPLAY 'XLKUP024 - XL011-MSG-ID = ' XL011-MSG-ID                
019300         DISPLAY 'PV-START = ' PV-START ' PV-LENGTH = ' PV-LENGTH         
019400     END-IF                                                               
019500                                                                          
019600     EVALUATE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                      
019700                                                                          
019800         WHEN 'ORD_PACK_HDR_CHANGE'                                       
019900*                                                                         
020000*            WRITE THE ORD_PACK_ADD SEGMENT TO THE LOG TABLE              
020100             COMPUTE PV-LENGTH = LENGTH OF XL011-PK-HDR-CHG + 28          
020200             MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                  
020300                                      TO PV-WORK-AREA                     
020400                                         XL011-DATA                       
020500             PERFORM 6000-INSERT-OR-PUT                                   
020600                                                                          
020700*            DO THIS ONLY ONCE                                            
020800             SET PS-PROCESS-CONTINUE-NO TO TRUE                           
020900                                                                          
021000         WHEN 'ORD_PACK_ADD'                                              
021100                                                                          
021200*            WRITE THE ORD_PACK_ADD SEGMENT TO THE LOG TABLE              
021300             MOVE XL012-PACK-BUFFER (01:28)                               
021400                                      TO PV-WORK-AREA                     
021500             MOVE PV-LENGTH           TO PV-SAVE-LENGTH                   
021600             MOVE 28                  TO PV-LENGTH                        
021700             PERFORM 6000-INSERT-OR-PUT                                   
021800             MOVE PV-SAVE-LENGTH      TO PV-LENGTH                        
021900                                                                          
022000*            ALSO ADD 8 WHICH THE LENGTH OF THE PO NBR PRECEDING          
022100*            EACH MESSAGE ID.                                             
022200             COMPUTE PV-START = PV-START + PV-LENGTH + 8                  
022300             PERFORM 2100-PARSE-ORD-PACK-ADD                              
022400                 UNTIL PS-PROCESS-CONTINUE-NO                             
022500                 OR                                                       
022600                 PS-END-OF-MSG-YES                                        
022700                 OR                                                       
022800                 PV-START >= XL012-DATA-LENGTH                            
023000                                                                          
024800                                                                          
024900         WHEN 'ORD_PACK_COST_CHANGE'                                      
025000*            WRITE THE ORD_PACK_COST_CHANGE                               
025100*                         SEGMENT TO THE LOG TABLE                        
025200             MOVE XL012-PACK-BUFFER (1:28)                                
025300                                      TO PV-WORK-AREA                     
025400             MOVE PV-LENGTH           TO PV-SAVE-LENGTH                   
025500             MOVE 28                  TO PV-LENGTH                        
025600             PERFORM 6000-INSERT-OR-PUT                                   
025700             MOVE PV-SAVE-LENGTH      TO PV-LENGTH                        
025800                                                                          
025900*            WRITE 'ORD_SKU_COST_CHANGE' TO LOG TABLE                     
026000                                                                          
026100             MOVE 29                  TO PV-START                         
026200             MOVE LENGTH OF XL011-SKU-COST-CHG                            
026300                                      TO PV-LENGTH                        
026400             ADD 28                   TO PV-LENGTH                        
026500             MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                  
026600                                      TO PV-WORK-AREA                     
026700                                         PV-DEBUG-ID                      
026800             PERFORM 6000-INSERT-OR-PUT                                   
026900                                                                          
027000*            FIND THE START OF THE NEXT SEGMENT                           
027100             COMPUTE PV-START = PV-START + PV-LENGTH                      
027200                                                                          
027300*            COMPUTE PV-START = PV-START + PV-LENGTH + 8                  
027400             PERFORM 2400-PARSE-ORD-PACK-COST-CHG                         
027500                 UNTIL PS-PROCESS-CONTINUE-NO                             
027600                 OR                                                       
027700                 PS-END-OF-MSG-YES                                        
027800                 OR                                                       
027900                 PV-START >= XL012-DATA-LENGTH                            
028000                                                                          
030100                                                                          
030200         WHEN 'ORD_PACK_REPLACE'                                          
030300                                                                          
030400*            WRITE THE ORD_PACK_REPLACE SEGMENT TO THE LOG TABLE          
030500             MOVE XL012-PACK-BUFFER (1:28)                                
030600                                      TO PV-WORK-AREA                     
030700             MOVE PV-LENGTH           TO PV-SAVE-LENGTH                   
030800             MOVE 28                  TO PV-LENGTH                        
030900             PERFORM 6000-INSERT-OR-PUT                                   
031000             MOVE PV-SAVE-LENGTH      TO PV-LENGTH                        
031100                                                                          
031200*            WRITE THE ORD_PACK_ADD SEGMENT TO THE LOG TABLE              
031300             MOVE XL012-PACK-BUFFER (29:28)                               
031400                                      TO PV-WORK-AREA                     
031500             MOVE PV-LENGTH           TO PV-SAVE-LENGTH                   
031600             MOVE 28                  TO PV-LENGTH                        
031700             PERFORM 6000-INSERT-OR-PUT                                   
031800             MOVE PV-SAVE-LENGTH      TO PV-LENGTH                        
031900                                                                          
032000*    FIRST EXTRACT THE ORD_SKU_ADD SEGMENT FROM THE MESSAGE               
032100*    IN ORDER TO GET THE PACK SKU NUMBER.                                 
032200*                                                                         
032300*    PV-START EQUALS 1 (ORD_PACK_REPLACE)                                 
032400*    ADD 56 FOR ALLOW FOR THE ORD_PACK_REPLACE AND ORD_PACK_ADD           
032500*        STUBS.                                                           
032600*    NOW PV-START EQUALS 57.  THIS IS SHOULD BE THE START OF              
032700*    THE ORD_SKU_ADD SEGMENT.  IF NOT THEN IT IS AN ERROR.                
032800*                                                                         
032900                                                                          
033000             ADD 56                   TO PV-START                         
033100             IF XL012-PACK-BUFFER (PV-START:20) = 'ORD_SKU_ADD'           
033200                 MOVE LENGTH OF XL011-SKU-ADD TO PV-LENGTH                
033300                 ADD 28               TO PV-LENGTH                        
033400                 MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)              
033500                                              TO PV-WORK-AREA             
033600                 MOVE PV-WORK-AREA    TO XL011-DATA                       
033700                 PERFORM 6000-INSERT-OR-PUT                               
033800                                                                          
033900             ELSE                                                         
034000                                                                          
034100*                STRING 'ORD_SKU_ADD SEGMENT NOT FOUND FOR PO '           
034200*                        XL012-PACK-BUFFER (21:08)                        
034300*                        DELIMITED BY SIZE INTO XL012-ERROR-TEXT          
034400                 SET XL012-SYSTEM-ERROR                                   
034500                     PS-PROCESS-CONTINUE-NO TO TRUE                       
034600             END-IF                                                       
034700                                                                          
034800                                                                          
034900             IF PS-PROCESS-CONTINUE-YES                                   
035000                 COMPUTE PV-START = PV-START + PV-LENGTH                  
035100                 PERFORM 2300-PARSE-ORD-PACK-REPLACE                      
035200                     UNTIL PS-PROCESS-CONTINUE-NO                         
035300                     OR                                                   
035400                     PV-START >= XL012-DATA-LENGTH                        
035500             END-IF                                                       
035600                                                                          
035700         WHEN 'PACK_COMP_CHANGE'                                          
035800                                                                          
035900*            WRITE THE PACK_COMP_CHANGE SEGMENT TO THE LOG TABLE          
036000             MOVE PV-LENGTH           TO PV-SAVE-LENGTH                   
036100             COMPUTE PV-LENGTH = LENGTH OF XL011-PK-COMP-CHG              
036200                                      + 28                                
036300             MOVE XL012-PACK-BUFFER (01:PV-LENGTH)                        
036400                                      TO PV-WORK-AREA                     
036500                                         PV-DEBUG-ID                      
036600             PERFORM 6000-INSERT-OR-PUT                                   
036700             MOVE PV-SAVE-LENGTH      TO PV-LENGTH                        
036800                                                                          
036900             MOVE PV-WORK-AREA        TO XL011-DATA                       
037000             COMPUTE                                                      
037100                 PV-START = PV-LENGTH + 1                                 
037200             END-COMPUTE                                                  
037300                                                                          
024900         WHEN 'ORD_PACK_RTL_CHANGE'                                       
025000*            WRITE THE ORD_PACK_RTL_CHANGE                                
025100*                         SEGMENT TO THE LOG TABLE                        
025200             MOVE XL012-PACK-BUFFER (1:28)                                
025300                                      TO PV-WORK-AREA                     
025400             MOVE PV-LENGTH           TO PV-SAVE-LENGTH                   
025500             MOVE 28                  TO PV-LENGTH                        
025600             PERFORM 6000-INSERT-OR-PUT                                   
025700             MOVE PV-SAVE-LENGTH      TO PV-LENGTH                        
025800                                                                          
025900*            WRITE 'ORD_SKU_ATTR_CHANGE' TO LOG TABLE                     
026000                                                                          
026100             MOVE 29                  TO PV-START                         
026200             MOVE LENGTH OF XL011-SKU-ATTR-CHG                            
026300                                      TO PV-LENGTH                        
026400             ADD 28                   TO PV-LENGTH                        
026500             MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                  
026600                                      TO PV-WORK-AREA                     
026700                                         PV-DEBUG-ID                      
026800             PERFORM 6000-INSERT-OR-PUT                                   
026900                                                                          
027000*            FIND THE START OF THE NEXT SEGMENT                           
027100             COMPUTE PV-START = PV-START + PV-LENGTH                      
027200                                                                          
027300*            COMPUTE PV-START = PV-START + PV-LENGTH + 8                  
027400             PERFORM 2450-PARSE-ORD-PACK-RTL-CHG                          
027500                 UNTIL PS-PROCESS-CONTINUE-NO                             
027600                 OR                                                       
027700                 PS-END-OF-MSG-YES                                        
027800                 OR                                                       
027900                 PV-START >= XL012-DATA-LENGTH                            
028000                                                                          
037400         WHEN OTHER                                                       
037500             SET PS-PROCESS-CONTINUE-NO  TO TRUE                          
037600     END-EVALUATE.                                                        
037700                                                                          
037800 2100-PARSE-ORD-PACK-ADD.                                                 
037900                                                                          
038000     IF XL012-DEBUG-YES                                                   
038100         DISPLAY 'XLKUP024 - 2100-PARSE-ORD-PACK-ADD'                     
038200     END-IF                                                               
038300     MOVE PV-START TO PV-DISP-START                                       
038400     MOVE PV-LENGTH TO  PV-DISP-LENGTH                                    
038500     IF XL012-DEBUG-YES                                                   
038600        DISPLAY 'START/LENGTH = ' PV-DISP-START '/' PV-DISP-LENGTH        
038700     END-IF                                                               
038800                                                                          
038900     MOVE 20                          TO PV-LENGTH                        
039000                                                                          
039100*    LOOK FOR THE RECORD-TYPE IN THE NEXT 20 BYTES                        
039200                                                                          
039300     MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH) TO PV-DEBUG-ID           
039400     EVALUATE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                      
039500         WHEN 'ORD_SKU_ADD'                                               
039600                                                                          
039700*            THIS IS A 'SKU-ADD' SO EXTRACT ENOUGH DATA FOR THAT          
039800*            REC-TYPE AND SEND IT DOWN THE PIPE                           
039900                                                                          
040000             MOVE LENGTH OF XL011-SKU-ADD                                 
040100                                      TO PV-LENGTH                        
040200             ADD 28                   TO PV-LENGTH                        
040300             MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                  
040400                                      TO PV-WORK-AREA                     
040500             MOVE PV-WORK-AREA        TO XL011-DATA                       
040600                                                                          
040700*            THESE FIELDS ARE USED TO BUILD PARTS OF THE PACK             
040800             MOVE XL011-SA-COMP-SKU-NBR TO XL011-PACK-SKU-NBR             
040900                                                                          
041000                                                                          
041100*                                                                         
041200*            FIND THE START OF THE NEXT SEGMENT                           
041300             COMPUTE PV-START = PV-START + PV-LENGTH                      
041400                                                                          
041500         WHEN 'PACK_COMP_ADD'                                             
041600*            EXTRACT ENOUGH DATA FOR THAT                                 
041700*            REC-TYPE AND SEND IT DOWN THE PIPE                           
041800             MOVE LENGTH OF XL011-PACK-COMP-ADD                           
041900                                      TO PV-LENGTH                        
042000             ADD 28                   TO PV-LENGTH                        
042100             MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                  
042200                                      TO PV-WORK-AREA                     
042300             MOVE PV-WORK-AREA        TO XL011-DATA                       
042400                                                                          
042500                                                                          
042600*            MOVE THE STARTING POINT FORWARD                              
042700             COMPUTE PV-START = PV-START + PV-LENGTH                      
042800                                                                          
042900         WHEN OTHER                                                       
043000             SET PS-END-OF-MSG-YES    TO TRUE                             
043100     END-EVALUATE.                                                        
043200                                                                          
043300     IF PS-PROCESS-CONTINUE-YES AND PS-END-OF-MSG-NO                      
043400         PERFORM 6000-INSERT-OR-PUT                                       
043500     END-IF.                                                              
043600                                                                          
043700/                                                                         
043800 2200-PARSE-ORD-LOC-PACK-ADD.                                             
043900     IF XL012-DEBUG-YES                                                   
044000         DISPLAY 'XLKUP024 - 2200-PARSE-ORD-LOC-PACK-ADD'                 
044100     END-IF                                                               
044200     MOVE PV-START TO PV-DISP-START                                       
044300     MOVE PV-LENGTH TO  PV-DISP-LENGTH                                    
044400     IF XL012-DEBUG-YES                                                   
044500        DISPLAY 'START/LENGTH = ' PV-DISP-START '/' PV-DISP-LENGTH        
044600     END-IF                                                               
044700                                                                          
044800     MOVE 20                          TO PV-LENGTH                        
044900                                                                          
045000*    LOOK FOR THE RECORD-TYPE IN THE NEXT 20 BYTES                        
045100                                                                          
045200     MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH) TO PV-DEBUG-ID           
045300     EVALUATE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                      
045400         WHEN 'ORD_LOCATION_ADD'                                          
045500                                                                          
045600*            EXTRACT ENOUGH DATA FOR THIS AND                             
045700*            REC-TYPE AND SEND IT DOWN THE PIPE                           
045800                                                                          
045900             MOVE LENGTH OF XL011-LOCATION-ADD                            
046000                                      TO PV-LENGTH                        
046100             ADD 28                   TO PV-LENGTH                        
046200             MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                  
046300                                      TO PV-WORK-AREA                     
046400             MOVE PV-WORK-AREA        TO XL011-DATA                       
046500                                                                          
046600*            THESE FIELDS ARE USED TO BUILD PARTS OF THE PACK             
046700             MOVE XL011-LA-COMP-SKU-NBR TO XL011-PACK-SKU-NBR             
046800                                                                          
046900                                                                          
047000*            FIND THE START OF THE NEXT SEGMENT                           
047100             COMPUTE PV-START = PV-START + PV-LENGTH                      
047I00                                                                          
048500                                                                          
049500         WHEN OTHER                                                       
049600             SET PS-END-OF-MSG-YES    TO TRUE                             
049700     END-EVALUATE.                                                        
049800                                                                          
049900     IF PS-PROCESS-CONTINUE-YES AND PS-END-OF-MSG-NO                      
050000         PERFORM 6000-INSERT-OR-PUT                                       
050100     END-IF.                                                              
050200                                                                          
050300                                                                          
050400 2300-PARSE-ORD-PACK-REPLACE.                                             
050500                                                                          
050600     IF XL012-DEBUG-YES                                                   
050700         DISPLAY 'XLKUP024 - 2300-PARSE-ORD-PACK-REPLACE'                 
050800     END-IF                                                               
050900     MOVE PV-START TO PV-DISP-START                                       
051000     MOVE PV-LENGTH TO  PV-DISP-LENGTH                                    
051100     IF XL012-DEBUG-YES                                                   
051200        DISPLAY 'START/LENGTH = ' PV-DISP-START '/' PV-DISP-LENGTH        
051300     END-IF                                                               
051400                                                                          
051500     MOVE 20                          TO PV-LENGTH                        
051600                                                                          
051700     MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH) TO PV-DEBUG-ID           
051800                                                                          
051900*    LOOK FOR THE RECORD-TYPE IN THE NEXT 20 BYTES                        
052000                                                                          
052100     EVALUATE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                      
052200         WHEN 'ORD_PACK_ADD'                                              
052300*            FIND THE START OF THE NEXT SEGMENT                           
052400*            COMPUTE PV-START = PV-START + PV-LENGTH + 8                  
052500*                                                                         
052600                                                                          
052700         WHEN 'ORD_SKU_ADD'                                               
052800                                                                          
052900             MOVE LENGTH OF XL011-SKU-ADD                                 
053000                                      TO PV-LENGTH                        
053100             ADD 28                   TO PV-LENGTH                        
053200             MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                  
053300                                      TO PV-WORK-AREA                     
053400             MOVE PV-WORK-AREA        TO XL011-DATA                       
053500                                                                          
053600*            THESE FIELDS ARE USED TO BUILD PARTS OF THE PACK             
053700                                                                          
053800                                                                          
053900*            MOVE THE STARTING POINT FORWARD                              
054000             COMPUTE PV-START = PV-START + PV-LENGTH                      
054100                                                                          
054200         WHEN 'PACK_COMP_ADD'                                             
054300*            EXTRACT ENOUGH DATA FOR THAT                                 
054400*            REC-TYPE AND SEND IT DOWN THE PIPE                           
054500             MOVE LENGTH OF XL011-PACK-COMP-ADD                           
054600                                      TO PV-LENGTH                        
054700             ADD 28                   TO PV-LENGTH                        
054800             MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                  
054900                                      TO PV-WORK-AREA                     
055000             MOVE PV-WORK-AREA        TO XL011-DATA                       
055100                                                                          
055200             PERFORM 6000-INSERT-OR-PUT                                   
055300                                                                          
055400*            MOVE THE STARTING POINT FORWARD                              
055500             COMPUTE PV-START = PV-START + PV-LENGTH                      
055600                                                                          
054200         WHEN 'ORD_LOCATION_ADD'                                          
054300*            EXTRACT ENOUGH DATA FOR THAT                                 
054400*            REC-TYPE AND SEND IT DOWN THE PIPE                           
054500             MOVE LENGTH OF XL011-LOCATION-ADD                            
054600                                      TO PV-LENGTH                        
054700             ADD 28                   TO PV-LENGTH                        
054800             MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                  
054900                                      TO PV-WORK-AREA                     
055000             MOVE PV-WORK-AREA        TO XL011-DATA                       
055100                                                                          
055200             PERFORM 6000-INSERT-OR-PUT                                   
055300                                                                          
055400*            MOVE THE STARTING POINT FORWARD                              
055500             COMPUTE PV-START = PV-START + PV-LENGTH                      
055700                                                                          
057900                                                                          
058000         WHEN 'STORE_ADD'                                                 
058100             SET PS-END-OF-MSG-NO                                         
058200                 PS-END-OF-STORES-NO  TO TRUE                             
058300             PERFORM 2600-PARSE-STORE-ADD                                 
058400                 UNTIL PS-PROCESS-CONTINUE-NO                             
058500                 OR                                                       
058600                 PS-END-OF-STORES-YES                                     
058700                 OR                                                       
058800                 PS-END-OF-MSG-YES                                        
058900                 OR                                                       
059000                 PV-START >= XL012-DATA-LENGTH                            
059100                                                                          
059200         WHEN OTHER                                                       
059300             SET PS-END-OF-MSG-YES                                        
059400                 PS-PROCESS-CONTINUE-NO                                   
059500                                      TO TRUE                             
059600     END-EVALUATE.                                                        
059700                                                                          
059800*    PERFORM 6000-INSERT-OR-PUT.                                          
059900/                                                                         
060000 2400-PARSE-ORD-PACK-COST-CHG.                                            
060100     IF XL012-DEBUG-YES                                                   
060200         DISPLAY 'XLKUP024 - 2400-PARSE-ORD-PACK-COST-CHG'                
060300     END-IF                                                               
060400     MOVE PV-START TO PV-DISP-START                                       
060500     MOVE PV-LENGTH TO  PV-DISP-LENGTH                                    
060600     IF XL012-DEBUG-YES                                                   
060700        DISPLAY 'START/LENGTH = ' PV-DISP-START '/' PV-DISP-LENGTH        
060800     END-IF                                                               
060900                                                                          
061000     MOVE 20                          TO PV-LENGTH                        
061100                                                                          
061200*    LOOK FOR THE RECORD-TYPE IN THE NEXT 20 BYTES                        
061300                                                                          
061400     MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH) TO PV-DEBUG-ID           
061500     EVALUATE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                      
061600         WHEN 'PACK_COMP_CHANGE'                                          
061700*            EXTRACT ENOUGH DATA FOR THAT                                 
061800*            REC-TYPE AND SEND IT DOWN THE PIPE                           
061900             MOVE LENGTH OF XL011-PK-COMP-CHG                             
062000                                      TO PV-LENGTH                        
062100             ADD 28                   TO PV-LENGTH                        
062200             MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                  
062300                                      TO PV-WORK-AREA                     
062400                                         PV-DEBUG-ID                      
062500             MOVE PV-WORK-AREA        TO XL011-DATA                       
062600                                                                          
062700*            MOVE THE STARTING POINT FORWARD                              
062800             COMPUTE PV-START = PV-START + PV-LENGTH                      
062900                                                                          
063000         WHEN OTHER                                                       
063100             SET PS-END-OF-MSG-YES    TO TRUE                             
063200     END-EVALUATE.                                                        
063300                                                                          
063400     IF PS-PROCESS-CONTINUE-YES AND PS-END-OF-MSG-NO                      
063500         PERFORM 6000-INSERT-OR-PUT                                       
063600     END-IF.                                                              
063700                                                                          
063800                                                                          
060000 2450-PARSE-ORD-PACK-RTL-CHG.                                             
060100     IF XL012-DEBUG-YES                                                   
060200         DISPLAY 'XLKUP024 - 2450-PARSE-ORD-PACK-RTL-CHG'                 
060300     END-IF                                                               
060400     MOVE PV-START TO PV-DISP-START                                       
060500     MOVE PV-LENGTH TO  PV-DISP-LENGTH                                    
060600     IF XL012-DEBUG-YES                                                   
060700        DISPLAY 'START/LENGTH = ' PV-DISP-START '/' PV-DISP-LENGTH        
060800     END-IF                                                               
060900                                                                          
061000     MOVE 20                          TO PV-LENGTH                        
061100                                                                          
061200*    LOOK FOR THE RECORD-TYPE IN THE NEXT 20 BYTES                        
061300                                                                          
061400     MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH) TO PV-DEBUG-ID           
061500     EVALUATE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                      
061600         WHEN 'PACK_COMP_CHANGE'                                          
061700*            EXTRACT ENOUGH DATA FOR THAT                                 
061800*            REC-TYPE AND SEND IT DOWN THE PIPE                           
061900             MOVE LENGTH OF XL011-PK-COMP-CHG                             
062000                                      TO PV-LENGTH                        
062100             ADD 28                   TO PV-LENGTH                        
062200             MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                  
062300                                      TO PV-WORK-AREA                     
062400                                         PV-DEBUG-ID                      
062500             MOVE PV-WORK-AREA        TO XL011-DATA                       
062600                                                                          
062700*            MOVE THE STARTING POINT FORWARD                              
062800             COMPUTE PV-START = PV-START + PV-LENGTH                      
062900                                                                          
063000         WHEN OTHER                                                       
063100             SET PS-END-OF-MSG-YES    TO TRUE                             
063200     END-EVALUATE.                                                        
063300                                                                          
063400     IF PS-PROCESS-CONTINUE-YES AND PS-END-OF-MSG-NO                      
063500         PERFORM 6000-INSERT-OR-PUT                                       
063600     END-IF.                                                              
063700                                                                          
069000/                                                                         
069100 2600-PARSE-STORE-ADD.                                                    
069200                                                                          
069300     IF XL012-DEBUG-YES                                                   
069400         DISPLAY 'XLKUP024 - 2600-PARSE-STORE-ADD'                        
069500     END-IF                                                               
069600     MOVE PV-START TO PV-DISP-START                                       
069700     MOVE PV-LENGTH TO  PV-DISP-LENGTH                                    
069800     IF XL012-DEBUG-YES                                                   
069900        DISPLAY 'START/LENGTH = ' PV-DISP-START '/' PV-DISP-LENGTH        
070000     END-IF                                                               
070100                                                                          
070200     MOVE 20                          TO PV-LENGTH                        
070300                                                                          
070400*    LOOK FOR THE RECORD-TYPE IN THE NEXT 20 BYTES                        
070500                                                                          
070600     MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH) TO PV-DEBUG-ID           
070700     EVALUATE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                      
070800         WHEN 'STORE_ADD'                                                 
070900                                                                          
071000             COMPUTE PV-LENGTH = LENGTH OF XL011-STORE-ADD                
071100                                      + 28                                
071200                                                                          
071300             MOVE XL012-PACK-BUFFER (PV-START:PV-LENGTH)                  
071400                                      TO PV-WORK-AREA                     
071500             MOVE PV-WORK-AREA        TO XL011-DATA                       
071600                                                                          
071700             PERFORM 6000-INSERT-OR-PUT                                   
071800                                                                          
071900             MOVE LENGTH OF XL011-STORE-ADD                               
072000                                      TO PV-LENGTH                        
072100             ADD 28                   TO PV-LENGTH                        
072200*            FIND THE START OF THE NEXT SEGMENT                           
072300             COMPUTE PV-START = PV-START + PV-LENGTH                      
072400                                                                          
072500         WHEN OTHER                                                       
073000             SET PS-END-OF-MSG-YES    TO TRUE                             
                                                                                
073200     END-EVALUATE.                                                        
073300                                                                          
073400*    IF PS-PROCESS-CONTINUE-YES AND PS-END-OF-MSG-NO                      
073500*        PERFORM 6000-INSERT-OR-PUT                                       
073600*    END-IF.                                                              
073700/                                                                         
073800 6000-INSERT-OR-PUT.                                                      
073900     IF XL012-DEBUG-YES                                                   
074000         DISPLAY 'XLKUP024 - 6000-INSERT-OR-PUT'                          
074100     END-IF                                                               
074200     MOVE PV-START TO PV-DISP-START                                       
074300     MOVE PV-LENGTH TO  PV-DISP-LENGTH                                    
074400     IF XL012-DEBUG-YES                                                   
074500        DISPLAY 'START/LENGTH = ' PV-DISP-START '/' PV-DISP-LENGTH        
074600        DISPLAY 'PV-WORK-AREA =' PV-WORK-AREA                             
074700     END-IF                                                               
074800                                                                          
074900     IF XL012-ACTIVITY-LOG                                                
075000         PERFORM 7000-INSERT-EVENT-LOG                                    
075100     ELSE                                                                 
075200         PERFORM 8000-PUT-ON-ERROR-QUEUE                                  
075300     END-IF.                                                              
075400                                                                          
075500 7000-INSERT-EVENT-LOG.                                                   
075600                                                                          
075700     IF XL012-DEBUG-YES                                                   
075800         DISPLAY 'XLKUP024 - 7000-INSERT-EVENT-LOG'                       
075900     END-IF                                                               
076000     MOVE PV-START TO PV-DISP-START                                       
076100     MOVE PV-LENGTH TO  PV-DISP-LENGTH                                    
076200     IF XL012-DEBUG-YES                                                   
076300        DISPLAY 'START/LENGTH = ' PV-DISP-START '/' PV-DISP-LENGTH        
076400     END-IF                                                               
076500                                                                          
076600     MOVE '7000-INSERT-EVENT-LOG'     TO PV-PARAGRAPH-NAME                
076700*    MOVE 'DST_EVNT_MSG_LOG'          TO XL012-DB2-TABLE-NAME(1)          
076800*    MOVE 'XLKUP017'                  TO XL012-ERROR-PROGRAM              
076900                                                                          
077000     MOVE PV-LENGTH                   TO DST00004-MSG-TXT-LEN             
077100     MOVE PV-WORK-AREA                TO DST00004-MSG-TXT-TEXT            
077200                                                                          
077300     EXEC SQL                                                             
077400         INSERT INTO DST_EVNT_MSG_LOG                                     
077500         (                                                                
077600             BEG_MSG_TMST                                                 
077700         ,   END_MSG_TMST                                                 
077800         ,   MSG_TXT                                                      
077900         )                                                                
078000         VALUES                                                           
078100         (                                                                
078200             :PV-START-TIMESTAMP                                          
078300         ,   CURRENT TIMESTAMP                                            
078400         ,   :DST00004-MSG-TXT                                            
078500         )                                                                
078600     END-EXEC.                                                            
078700                                                                          
078800     EVALUATE TRUE                                                        
078900         WHEN SQLCODE = ZERO                                              
079000             CONTINUE                                                     
079100         WHEN SQLWARN0 NOT = SPACES                                       
079200         WHEN SQLCODE  NOT = ZERO                                         
079300             SET XL012-SQL-ERROR                                          
079400                 PS-PROCESS-CONTINUE-NO  TO TRUE                          
079500*            MOVE 'DST_EVNT_MSG_LOG'  TO XL012-DB2-TABLE-NAME(1)          
079600     END-EVALUATE.                                                        
079700                                                                          
079800 7100-GET-ERROR-TMST.                                                     
079900                                                                          
080000     IF XL012-DEBUG-YES                                                   
080100         DISPLAY 'XLKUP024 - 7100-GET-ERROR-TMST'                         
080200     END-IF                                                               
080300     MOVE '7100-GET-ERROR-TMST'       TO PV-PARAGRAPH-NAME                
080400     EXEC SQL                                                             
080500         SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                    
080600     END-EXEC                                                             
080700                                                                          
080800     EVALUATE TRUE                                                        
080900        WHEN SQLCODE = ZERO                                               
081000            CONTINUE                                                      
081100                                                                          
081200        WHEN SQLWARN0 NOT = SPACES                                        
081300        WHEN SQLCODE  NOT = ZERO                                          
081400             SET XL012-SQL-ERROR                                          
081500                 PS-PROCESS-CONTINUE-NO  TO TRUE                          
081600*            MOVE 'CURRENT TIMESTAMP' TO XL012-DB2-TABLE-NAME(1)          
081700     END-EVALUATE.                                                        
081800/                                                                         
081900 8000-PUT-ON-ERROR-QUEUE.                                                 
082000                                                                          
082100     IF XL012-DEBUG-YES                                                   
082200         DISPLAY 'XLKUP024 - 8000-PUT-ON-ERROR-QUEUE'                     
082300     END-IF                                                               
082400     MOVE '8000-PUT-ON-ERROR-QUEUE' TO PV-PARAGRAPH-NAME                  
082500                                                                          
082600     MOVE XL012-LOC-ERROR-QUEUE-NAME TO MQOD-OBJECTNAME                   
082700     MOVE XL012-QUEUE-MANAGER-NAME   TO MQOD-OBJECTQMGRNAME               
082800                                                                          
082900     COMPUTE MQPMO-OPTIONS = MQPMO-NONE                                   
083000                           + MQPMO-FAIL-IF-QUIESCING                      
083100                                                                          
083200     PERFORM 7100-GET-ERROR-TMST                                          
083300     MOVE PV-CURRENT-TIMESTAMP        TO XL033-TIMESTAMP                  
083400                                                                          
083500*    MOVE THE ERROR INFO TO THE SEGMENT THAT CAUSED THE ERROR             
083600*    (IDENTIFY THE CLINKER)                                               
083700     ADD 1                            TO PV-SEGMENT-COUNT                 
083800     IF PV-SEGMENT-COUNT = XL012-FAULT-POINTER                            
083900         MOVE XL012-XLRD011-RECORD-AREA TO XL033-DATA                     
084000     ELSE                                                                 
084100         MOVE PV-WORK-AREA            TO XL033-DATA                       
084200     END-IF                                                               
084300                                                                          
084400     INITIALIZE MQV-ERRMSGBUF                                             
084500                                                                          
084600     MOVE XLRD033-RECORD              TO MQV-ERRMSGBUF                    
084700                                                                          
084800*    POPULATE CORRELID FIELD                                              
084900                                                                          
085000     MOVE XL011-ORDER-NBR     TO PV-PO-NBR-TO-GET                         
085100     MOVE PV-PO-CORRELID      TO MQMD-CORRELID                            
085200                                                                          
085300     CALL MQV-MQPUT USING XL012-CONNECT-HANDLE                            
085400                          XL012-OPEN-HANDLE                               
085500                          MQM-MESSAGE-DESCRIPTOR                          
085600                          MQM-PUT-MESSAGE-OPTIONS                         
085700                          MQV-MSG-LENGTH                                  
085800                          MQV-ERRMSGBUF                                   
085900                          MQV-COMPLETION-CODE                             
086000                          MQV-REASON.                                     
086100                                                                          
086200     EVALUATE TRUE                                                        
086300        WHEN MQV-COMPLETION-CODE = MQCC-OK                                
086400             CONTINUE                                                     
086500        WHEN OTHER                                                        
086600             DISPLAY 'UNABLE TO WRITE TO LOC ERROR QUEUE'                 
086700             CALL MQV-MQCHECK USING MQV-REASON                            
086800                                    MQV-REASON-TEXT                       
086900             MOVE 'MQPUT ERROR'      TO PV-OPERATION-MSG-1                
087000             PERFORM 9999-MQ-CROAK                                        
087100     END-EVALUATE.                                                        
087200/                                                                         
087300 8050-COMMIT-MQ.                                                          
087400     IF XL012-DEBUG-YES                                                   
087500         DISPLAY 'XLKUP024 - 8050-COMMIT-MQ'                              
087600     END-IF                                                               
087700     MOVE '8050-COMMIT-MQ'            TO PV-PARAGRAPH-NAME                
087800     CALL MQV-MQCMIT USING XL012-CONNECT-HANDLE                           
087900                           MQV-COMPLETION-CODE                            
088000                           MQV-REASON                                     
088100                                                                          
088200     EVALUATE TRUE                                                        
088300         WHEN MQV-COMPLETION-CODE = MQCC-OK                               
088400             CONTINUE                                                     
088500         WHEN OTHER                                                       
088600             DISPLAY 'UNABLE TO COMMIT LOC ERROR QUEUE'                   
088700             CALL MQV-MQCHECK USING MQV-REASON                            
088800                                    MQV-REASON-TEXT                       
088900             MOVE 'MQCMIT ERROR'     TO PV-OPERATION-MSG-1                
089000             PERFORM 9999-MQ-CROAK                                        
089100     END-EVALUATE.                                                        
089200/                                                                         
089300 9000-END.                                                                
089400                                                                          
089500     IF XL012-DEBUG-YES                                                   
089600         DISPLAY 'XLKUP024 - 9000-END'                                    
089700     END-IF                                                               
089800     MOVE '9000-END'                  TO PV-PARAGRAPH-NAME                
089900                                                                          
090000     EVALUATE XL012-PACK-BUFFER (1:20)                                    
090100     WHEN 'ORD_PACK_HDR_CHANGE'                                           
090200     WHEN 'PACK_COMP_CHANGE'                                              
090300         CONTINUE                                                         
090400     WHEN OTHER                                                           
090500*        WRITE THE SEGMENT TO THE LOG TABLE TO MARK THE END OF            
090600*        OF THE PACK DATA FOR THIS EXECUTION                              
090700         MOVE PV-END-OF-PACK-SEGMENT  TO PV-WORK-AREA                     
090800         MOVE 28                      TO PV-LENGTH                        
090900         PERFORM 6000-INSERT-OR-PUT                                       
091000     END-EVALUATE                                                         
091100                                                                          
091200     IF XL012-ACTIVITY-LOG                                                
091300         EXEC SQL                                                         
091400             COMMIT                                                       
091500         END-EXEC                                                         
091600                                                                          
091700         EVALUATE TRUE                                                    
091800             WHEN SQLCODE = ZERO                                          
091900                 CONTINUE                                                 
092000             WHEN OTHER                                                   
092100                 SET XL012-SQL-ERROR                                      
092200                     PS-PROCESS-CONTINUE-NO  TO TRUE                      
092300*            MOVE 'DST_EVNT_MSG_LOG'  TO XL012-DB2-TABLE-NAME(1)          
092400         END-EVALUATE                                                     
092500                                                                          
092600     ELSE                                                                 
092700                                                                          
092800         PERFORM 8050-COMMIT-MQ                                           
092900                                                                          
093000     END-IF.                                                              
093100/                                                                         
093200 9999-CROAK.                                                              
093300                                                                          
093400     DISPLAY '** ABEND     **'.                                           
093500     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
093600     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
093700                                                                          
093800     SET ABEND-4013-DB2-ERROR TO TRUE.                                    
093900     CALL 'ILBOABN0' USING ABEND-CODE.                                    
094000                                                                          
094100 9999-MQ-CROAK.                                                           
094200                                                                          
094300     SET XL012-MQPUT-ERROR            TO TRUE.                            
094400     MOVE +4020 TO ABEND-CODE.                                            
094500                                                                          
094600     DISPLAY '**** ABEND *************************************'.          
094700     DISPLAY '** MQSERIES ERROR **'.                                      
094800     DISPLAY '** PROGRAM:    ' PC-CURRENT-PROGRAM-NAME.                   
094900     DISPLAY '** ABEND CODE: ' ABEND-CODE.                                
095000     DISPLAY '** PARAGRAPH:  ' PV-PARAGRAPH-NAME.                         
095100     DISPLAY '** MESSAGE:    ' PV-OPERATION-MSG-1.                        
095200     DISPLAY '** COMP CODE:  ' MQV-COMPLETION-CODE.                       
095300     DISPLAY '** RSN CODE:   ' MQV-REASON.                                
095400     DISPLAY '** RSN TEXT:   ' MQV-REASON-TEXT.                           
095500     DISPLAY '************************************************'.          
