      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    TMKCS505.                                                 
       AUTHOR.        M. FURLICK - STRATAGEM                                    
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  4/94.                                                     
       DATE-COMPILED.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *    E505 - TROUBLE RESPONSE/RESOLUTION.                         *        
      *                                                                *        
      *    THIS PROGRAM IS USED TO EITHER RESPOND TO, OR RESOLVE A     *        
      *    TROUBLE ITEM.  PO NUMBER, PO LINE NUMBER, AND SEQUENCE      *        
      *    NUMBER ARE PASSED IN A TEMP STORAGE QUEUE.                  *        
      *                                                                *        
      *----------------------------------------------------------------*        
002600* Change History                                                *         
001300*    CHANGES                                                    *         
001300*     MADE BY: PRITHIVIRAJ SUBRAMANIAN (COGNIZANT)              *         
001300*     DATE:    10/06/2014                                       *         
001300*                                                               *         
001300*      CHANGES THRU OUT THE PROGRAM TO INCREASE THE SIZE OF PO# *         
001300*      FROM 7 DIGITS TO 8 DIGITS. TAGGED WITH *PS 10/06/2014    *         
002600*---------------------------------------------------------------*         
263627* CHANGED 11/04/2021    BY JIM HUNTER      WR: CHG0263627                 
263627* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263627* MAKING THE CALL DYNAMIC VS STATIC.                                      
263627*---------------------------------------------------------------*         
CICS  * CHANGED 11/09/2022    BY JIM HUNTER      WR: CHG0278008                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
      * CHANGED 02/14/2023    BY Jean Kurk       WR: CHG0281369                 
      * removed copybook for UPC check digit routine                            
      *-----------------------------------------------------------------        
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE  'TMKCS505'.          
           05  PC-CURRENT-MAP-NAME      PIC  X(08)  VALUE  'TM505A  '.          
           05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE  'TMKM505 '.          
           05  PC-APPL-MAP              PIC S9(04)  VALUE  +1                   
                                                    COMP.                       
           05  PC-MAX-RETRIES           PIC S9(04)  VALUE  +3                   
                                                    COMP.                       
           05  PC-MAX-MSG-VAR           PIC S9(04)  VALUE  +1                   
                                                    COMP.                       
           05  PC-TRBL-RSN-PO-NOT-ON-FILE                                       
                                        PIC S9(04)  VALUE  +101                 
                                                    COMP.                       
           05  PC-LINE-TROUBLE-RTN-LENGTH                                       
                                        PIC S9(04)  VALUE  +600                 
                                                    COMP.                       
           05  PC-INTER-APPL-CODES.                                             
               10  PC-INTER-APPL-ADD    PIC  X(01)  VALUE 'A'.                  
               10  PC-INTER-APPL-DELETE PIC  X(01)  VALUE 'D'.                  
               10  PC-INTER-APPL-UPDATE PIC  X(01)  VALUE 'U'.                  
           05  PC-MSG-VAR-SYMBOLIC      PIC  X(04)  VALUE '&VAR'.               
           05  PC-PERIOD                PIC  X(01)  VALUE '.'.                  
           05  PC-TMKCS520              PIC  X(08)  VALUE 'TMKCS520'.           
           05  PC-TMKCS550              PIC  X(08)  VALUE 'TMKCS550'.           
           05  PC-A                     PIC  X(01)  VALUE 'A'.                  
           05  PC-X                     PIC  X(01)  VALUE 'X'.                  
           05  PC-M                     PIC  X(01)  VALUE 'M'.                  
           05  PC-5                     PIC  9(01)  VALUE 5.                    
           05  PC-I                     PIC  X(01)  VALUE 'I'.                  
           05  PC-N                     PIC  X(01)  VALUE 'N'.                  
           05  PC-Y                     PIC  X(01)  VALUE 'Y'.                  
           05  PC-267                   PIC  S9(4)  COMP VALUE +267.            
           05  PC-268                   PIC  S9(4)  COMP VALUE +268.            
           05  PC-269                   PIC  S9(4)  COMP VALUE +269.            
           05  PC-502                   PIC  S9(4)  COMP VALUE +502.            
           05  PC-509                   PIC  S9(4)  COMP VALUE +509.            
           05  PC-511                   PIC  S9(4)  COMP VALUE +511.            
           05  PC-571                   PIC  S9(4)  COMP VALUE +571.            
           05  PC-AUTO-RESOLVE          PIC  X(01)  VALUE 'A'.                  
           05  PC-MANUAL-RESOLVE        PIC  X(01)  VALUE 'M'.                  
           05  PC-AUTO-MANUAL-RESOLVE   PIC  X(01)  VALUE 'B'.                  
           05  PC-COMPANY               PIC  X(02)  VALUE '03'.                 
           05  PC-DPT                   PIC  X(03)  VALUE 'DPT'.                
           05  PC-DB2-DUMMY-DATE        PIC  X(26)  VALUE                       
               '9999-09-09-09.09.09.000000'.                                    
           05  PC-DUMMY-DATE                  PIC  X(10)  VALUE                 
                                              '9999-09-09'.                     
                                                                                
           05  PC-TSYMSG-NUMBERS.                                               
               10  PC-TSYMSG-00004      PIC  9(05)  VALUE  00004.               
               10  PC-TSYMSG-00005      PIC  9(05)  VALUE  00005.               
               10  PC-TSYMSG-00007      PIC  9(05)  VALUE  00007.               
               10  PC-TSYMSG-00008      PIC  9(05)  VALUE  00008.               
               10  PC-TSYMSG-00016      PIC  9(05)  VALUE  00016.               
               10  PC-TSYMSG-00045      PIC  9(05)  VALUE  00045.               
               10  PC-TSYMSG-00046      PIC  9(05)  VALUE  00046.               
               10  PC-TSYMSG-00050      PIC  9(05)  VALUE  00050.               
               10  PC-TSYMSG-00059      PIC  9(05)  VALUE  00059.               
               10  PC-TSYMSG-00069      PIC  9(05)  VALUE  00069.               
               10  PC-TSYMSG-00070      PIC  9(05)  VALUE  00070.               
               10  PC-TSYMSG-00176      PIC  9(05)  VALUE  00176.               
               10  PC-TSYMSG-00308      PIC  9(05)  VALUE  00308.               
               10  PC-TSYMSG-00551      PIC  9(05)  VALUE  00551.               
               10  PC-TSYMSG-00937      PIC  9(05)  VALUE  00937.               
               10  PC-TSYMSG-00938      PIC  9(05)  VALUE  00938.               
               10  PC-TSYMSG-00939      PIC  9(05)  VALUE  00939.               
               10  PC-TSYMSG-00941      PIC  9(05)  VALUE  00941.               
               10  PC-TSYMSG-00973      PIC  9(05)  VALUE  00973.               
               10  PC-TSYMSG-00986      PIC  9(05)  VALUE  00986.               
               10  PC-TSYMSG-03248      PIC  9(05)  VALUE  03248.               
                                                                                
               10  PC-TSYMSG-01024      PIC  9(05)  VALUE  01024.               
      *        TROUBLE MUST BE RESOLVED AUTOMATICALLY                           
                                                                                
               10  PC-TSYMSG-01025      PIC  9(05)  VALUE  01025.               
      *        TROUBLE RESOLUTION NOT REQUIRED                                  
                                                                                
               10  PC-TSYMSG-01026      PIC  9(05)  VALUE  01026.               
      *        DATA ENTERED IS NOT VALID FOR THIS REQUEST                       
                                                                                
               10  PC-TSYMSG-01027      PIC  9(05)  VALUE  01027.               
      *        DEPT NBR CANNOT BE UPDATED FOR THIS TROUBLE REASON'.             
                                                                                
               10  PC-TSYMSG-01041      PIC  9(05)  VALUE  01041.               
      *        DATA ENTERED REQUIRES THE USE OF A RESPOND KEY'                  
                                                                                
               10  PC-TSYMSG-01044      PIC  9(05)  VALUE  01044.               
      *        RESPONSE SUCCESSFUL                                              
                                                                                
               10  PC-TSYMSG-01045      PIC  9(05)  VALUE  01045.               
      *        RESPONSE SUCCESSFUL                                              
                                                                                
               10  PC-TSYMSG-01048      PIC  9(05)  VALUE  01048.               
      *        TROUBLE MAY ALREADY BE RESOLVED                                  
                                                                                
               10  PC-TSYMSG-01061      PIC  9(05)  VALUE  01061.               
      *        USER IS NOT AUTHORIZED                                           
                                                                                
               10  PC-TSYMSG-01066      PIC  9(05)  VALUE  01066.               
               10  PC-TSYMSG-01068      PIC  9(05)  VALUE  01068.               
      *        DO NOT SHIP BEFORE DATE HAS CHANGED; CANNOT RESOLVE              
                                                                                
               10  PC-TSYMSG-01069      PIC  9(05)  VALUE  01069.               
      *        WAITING FOR DECISION FROM BUYING OFFICE; CANNOT RESOLVE          
                                                                                
               10  PC-TSYMSG-02871      PIC  9(05)  VALUE  02871.               
      *        DC COMMENTS CAN NOT BE RESOLVED                                  
                                                                                
               10  PC-TSYMSG-03036      PIC  9(05)  VALUE  03036.               
      *        COC SPECIAL COMMENTS CAN NOT BE RESOLVED                         
               10  PC-TSYMSG-02165      PIC  9(05)  VALUE  02165.               
               10  PC-TSYMSG-02229      PIC  9(05)  VALUE  02229.               
               10  PC-TSYMSG-02243      PIC  9(05)  VALUE  02243.               
               10  PC-TSYMSG-03216      PIC  9(05)  VALUE  03216.               
      *        FALSE AUDITS CAN NOT BE RESOLVED                                 
               10  PC-TSYMSG-03364      PIC  9(05)  VALUE  03364.               
      *        TROUBLE WAS AUTO ACKNOWLEDGED AND RE-DISTRO WAS DONE             
               10  PC-TSYMSG-03365      PIC  9(05)  VALUE  03365.               
      * TROUBLE WAS NOT AUTO ACKNOWLEDGED DUE OTHER PRORATION TROUBLE           
      * FOUND                                                                   
               10  PC-TSYMSG-03366      PIC  9(05)  VALUE  03366.               
      *        TROUBLE WAS AUTOMATICALLY ACKNOWLEDGED BY SYSTEM                 
023000         10  PC-TSYMSG-03384            PIC  9(05)  VALUE 03384.          
023100*  TROUBLE WASN'T AUTO RESOLVED, DISTRO'ED OR ACKNOWLEDGED DUE TO         
023100*  TROUBLE                                                                
023000         10  PC-TSYMSG-03385            PIC  9(05)  VALUE 03385.          
023100*  AUTO ACKNOWLEDGE PENDING UNTIL NEXT BATCH JOB                          
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW              PIC  X(01)  VALUE  'N'.                 
               88  PS-ERROR                         VALUE  'Y'.                 
               88  PS-WARNING                       VALUE  'W'.                 
               88  PS-NO-ERROR                      VALUE  'N'.                 
           05  PS-SOFT-ABORT-SW         PIC  X(01)  VALUE  'N'.                 
               88  PS-SOFT-ABORT                    VALUE  'Y'.                 
           05  PS-VALIDATE-DEPT-SW      PIC  X(01)  VALUE  'N'.                 
               88  PS-VALIDATE-DEPT                 VALUE  'Y'.                 
           05  PS-TRBNTF-CSR-EOF-SW     PIC  X(01)  VALUE  'N'.                 
               88  PS-TRBNTF-CSR-EOF                VALUE  'Y'.                 
               88  PS-NOT-TRBNTF-CSR-EOF            VALUE  'N'.                 
           05  PS-FIRST-TIME-SW         PIC  X(01)  VALUE  'Y'.                 
               88  PS-FIRST-TIME                    VALUE  'Y'.                 
               88  PS-NOT-FIRST-TIME                VALUE  'N'.                 
           05  PS-END-OF-SEL-ITEMS-QUEUE-SW                                     
                                        PIC  X(01)  VALUE  'N'.                 
               88  PS-END-OF-SEL-ITEMS-QUEUE        VALUE  'Y'.                 
               88  PS-NOT-END-OF-SEL-ITEMS-QUEUE    VALUE  'N'.                 
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-HOLD-TM500-FIELDS     PIC X(600).                             
           05  PV-CICS-RESPONSE         PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-CICS-RESP             PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-MSG-VAR-CNT           PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-NULL-IND              PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-PREV-TRBL-RSN-CDE     PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-TRBL-RSLVD-COUNT      PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-DISPLAY-PO.                                                   
      *PS 10/06/2014 STARTS                                                     
               10  PV-PO-NBR            PIC  ZZZZZZZ9.                          
      *PS 10/06/2014 STARTS                                                     
               10  FILLER               PIC  X(01)  VALUE '-'.                  
               10  PV-REC-SEQ-NBR       PIC  9(04).                             
           05  PV-PO-LNE-NBR            PIC  9(04)  VALUE ZERO.                 
           05  PV-PREPK-ID              PIC  9(04)  VALUE ZERO.                 
      *PS 10/06/2014 STARTS                                                     
           05  PV-ERROR-PO-NBR          PIC  9(08)  VALUE ZERO.                 
      *PS 10/06/2014 STARTS                                                     
           05  PV-ERROR-REC-SEQ-NBR     PIC  9(04)  VALUE ZERO.                 
           05  PV-NBR-OF-PIC-AD         PIC S9(04)  COMP.                       
           05  PV-NBR-OF-NEW-TRBLS      PIC S9(04)  COMP.                       
           05  PV-UNPACK-OPER-UNT-ID    PIC  9(04)   VALUE ZERO.                
                                                                                
       01  DK-DB2-KEYS.                                                         
           05  DK-PO-NBR                PIC S9(09)  COMP VALUE +0.              
           05  DK-REC-SEQ-NBR           PIC S9(09)  COMP VALUE +0.              
           05  DK-PO-LNE-NBR            PIC S9(04)  COMP VALUE +0.              
           05  DK-OPER-UNT-ID           PIC S9(04)  COMP VALUE +0.              
           05  DK-FCLTY-ID              PIC S9(04)  COMP VALUE +0.              
           05  DK-PACK-ID               PIC S9(04)  COMP.                       
                                                                                
       01  PV-SEARCH-ARGUMENTS.                                                 
           05  PV-ACTION                PIC X(07) VALUE SPACE.                  
           05  PV-LOCATION              PIC X(02) VALUE SPACE.                  
           05  PV-REASON                PIC 9(03) VALUE ZERO.                   
           05  PV-PRORATE-ALLOC         PIC X(01) VALUE 'X'.                    
           05  PV-TEXT                  PIC X(04) VALUE SPACE.                  
           05  PV-RTV                   PIC X(01) VALUE SPACE.                  
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *  THE LAYOUT FOR THE TEMPORARY STORAGE QUEUE CONTAINING         *        
      *  SELECTED LIST ITEMS TO BE PROCESSED BY THIS PROGRAM.          *        
      *----------------------------------------------------------------*        
           COPY TMWS002.                                                        
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    THE LAYOUT FOR THE COMMAREA USED TO PASS BETWEEN THIS       *        
      *    PROGRAM AND TMKCS520.                                       *        
      *      ALSO PASSED TO AUTO ACKNOWLEDGE PROCESS                   *        
      *----------------------------------------------------------------*        
           COPY TMWS500.                                                        
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      **  SECURITY SUBROUTINE PARAMETERS                                        
      *----------------------------------------------------------------*        
           COPY DPWS008.                                                        
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    MAP LAYOUT                                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY TMKM505.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ATTRIBUTE SETTINGS COPYBOOK.                                *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    FUNCTION KEYS COPYBOOK                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    DATE/TIME COPYBOOK                                          *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS017.                                                        
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *  THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT     *        
      *  SUB-ROUTINE (DPKUT100).                                       *        
      *----------------------------------------------------------------*        
           COPY DPWS010I.                                                       
      /                                                                         
      *----------------------------------------------------------------*        
      *  THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS ARCHITECTURE*        
      *  APPLICATION PROGRAMMING INTERFACE MODULE.                     *        
      *----------------------------------------------------------------*        
           COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
      /                                                                         
      *----------------------------------------------------------------*        
      *  THE LOGISTICS LOCATION DOMAIN PARAMETERS (XLKUT071)           *        
      *----------------------------------------------------------------*        
           COPY XLWS071.                                                        
      /                                                                         
      *----------------------------------------------------------------*        
      *    STANDARD COMMAREA.                                          *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS020.                                                        
           05  TMKCS505-APPL REDEFINES DP020-VARIABLE-COMMAREA.                 
                                                                                
      *----------------------------------------------------------------*        
      *    INTER-APPLICATION COMMAREA.                                 *        
      *----------------------------------------------------------------*        
                                                                                
               10  INTER-APPL-COMM-AREA PIC X(200).                             
           COPY TMWS001.                                                        
           COPY POWS011.                                                        
      *----------------------------------------------------------------*        
      *    SPECIFIC COMMMAREA FOR DPKCS041.                            *        
      *----------------------------------------------------------------*        
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-KEY-NOTIF-NBR               PIC S9(09)               
                                                       COMP.                    
                   15  ASC-KEY-PO-NBR                  PIC S9(09)               
                                                       COMP.                    
                   15  ASC-KEY-PO-LNE-NBR              PIC S9(09)               
                                                       COMP.                    
                   15  ASC-KEY-PREPK-ID                PIC S9(09)               
                                                       COMP.                    
                   15  ASC-KEY-REC-SEQ-NBR             PIC S9(09)               
                                                       COMP.                    
                   15  ASC-KEY-OPER-UNT-ID             PIC S9(04)               
                                                       COMP.                    
                   15  ASC-KEY-FCLTY-ID                PIC S9(04)               
                                                       COMP.                    
                   15  ASC-KEY-FCLTY-AREA-ID           PIC  X(06).              
                                                                                
                   15  ASC-SCREEN-ENTERABLE-FIELDS.                             
                       20  ASC-TEXT1                   PIC X(50).               
                       20  ASC-TEXT2                   PIC X(50).               
                       20  ASC-TEXT3                   PIC X(50).               
                       20  ASC-TEXT4                   PIC X(50).               
                       20  ASC-TEXT5                   PIC X(50).               
                       20  ASC-NEW-PLAN-IND            PIC  X(01).              
                       20  ASC-PRORATED-ALLOC-IND      PIC  X(01).              
                       20  ASC-RTV-IND                 PIC  X(01).              
                   15  ASC-ENT-ID                      PIC S9(09) COMP.         
                   15  ASC-DEPT-NBR                    PIC X(03).               
                   15  ASC-DEPT-NBR-N REDEFINES ASC-DEPT-NBR                    
                                                       PIC 9(03).               
                   15  ASC-FCLTY-NME                   PIC X(25).               
                   15  ASC-FCLTY-AREA-ID               PIC X(06).               
                   15  ASC-VENDOR-NAME                 PIC X(30).               
                   15  ASC-DEPT-NAME                   PIC X(25).               
                   15  ASC-TRBL-RSN-CDE                PIC S9(4)  COMP.         
                   15  ASC-RESOLVED-IND                PIC X(01).               
                       88  ASC-RESOLVED                VALUE 'R'.               
                   15  ASC-MULT-RESP-RSLV-REQUEST-IND  PIC X(01).               
                       88  ASC-MULT-RESP-RSLV-REQUEST  VALUE 'Y'.               
                       88  ASC-NO-MULT-RESP-RSLV-REQUEST                        
                                                       VALUE 'N'.               
                   15  ASC-REQUEST-TYPE-IND            PIC X(01).               
                       88  ASC-MULT-RESPOND-REQUEST    VALUE '1'.               
                       88  ASC-MULT-RESOLVE-REQUEST    VALUE '2'.               
                   15  ASC-DEPT-NBR-CHGD-IND           PIC X(01).               
                       88  ASC-DEPT-NBR-CHGD           VALUE 'Y'.               
                       88  ASC-DEPT-NBR-NOT-CHGD       VALUE 'N'.               
                   15  ASC-INT-APPL-FORMAT-IND         PIC  X(01).              
                       88  ASC-ENTERED-FROM-PO-LIST    VALUE '1'.               
                       88  ASC-ENTERED-FROM-PO-LINE-LIST                        
                                                       VALUE '2'.               
                   15  ASC-USE-SELECTION-QUEUE-IND                              
                                                 PIC  X(01).                    
                       88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.         
                   15  ASC-SEL-QUEUE-NAME        PIC  X(08).                    
                   15  ASC-NUMBER-OF-TS-ITEMS    PIC S9(05)  COMP-3.            
                   15  ASC-NUMBER-OF-SELECTED-ITEMS                             
                                                 PIC S9(09)  COMP-3.            
                   15  ASC-RTV-ACCESS-SW         PIC X(01).                     
                       88 ASC-MERCHANT-ACCESS       VALUE 'Y'.                  
                   15  ASC-PBS-RESOLVE-SW        PIC X(01).                     
                       88 ASC-PBS-RESOLVE-YES       VALUE 'Y'.                  
                   15  ASC-ORDER-CTL-GROUP       PIC X(01).                     
                       88 ASC-MGMT-AUTHORITY        VALUE 'Y'.                  
                   15  ASC-SEL-SUB               PIC S9(04)  COMP.              
                   15  ASC-SEL-ITEM              PIC S9(04)  COMP.              
                   15  ASC-CURRENT-TIMESTAMP.                                   
                       20  ASC-CT-DATE  PIC  X(10).                             
                       20  FILLER       PIC  X(01).                             
                           88 ASC-CT-DASH1          VALUE '-'.                  
                       20  ASC-CT-TIME  PIC  X(08).                             
                       20  FILLER       PIC  X(07).                             
                           88  ASC-CT-ZERO1         VALUE '.000000'.            
                   15  ASC-CURRENT-TMST REDEFINES  ASC-CURRENT-TIMESTAMP        
                                        PIC  X(26).                             
                                                                                
                   15  ASC-HOLD-INTER-APPL       PIC  X(200).                   
               10  FILLER                        PIC  X(200).                   
      /                                                                         
      *----------------------------------------------------------------*        
      *    ABEND PROCESSING COPYBOOK.                                  *        
      *----------------------------------------------------------------*        
           COPY DPWS013.                                                        
      /                                                                         
                                                                                
      *    DB2 AREA FOR THE PURCHASE ORDER HEADER TABLE                         
                                                                                
           EXEC SQL                                                             
                INCLUDE TPURCHS                                                 
           END-EXEC.                                                            
      /                                                                         
      *    DB2 AREA FOR PURCHASE ITEM TABLE                                     
                                                                                
           EXEC SQL                                                             
                INCLUDE TPURITM                                                 
           END-EXEC.                                                            
      /                                                                         
      *    DB2 AREA FOR RECEIVER ITEM TABLE                                     
                                                                                
           EXEC SQL                                                             
                INCLUDE TRECITM                                                 
           END-EXEC.                                                            
      /                                                                         
      *    DB2 AREA FOR ENTITY NAME TABLE                                       
                                                                                
           EXEC SQL                                                             
                INCLUDE TENTNME                                                 
           END-EXEC.                                                            
      /                                                                         
      *    DB2 AREA FOR NOTIFICATION TABLE                                      
                                                                                
           EXEC SQL                                                             
                INCLUDE TTRBNTF                                                 
           END-EXEC.                                                            
      /                                                                         
      *    DB2 AREA FOR TROUBLE REASON CODE TABLE                               
                                                                                
           EXEC SQL                                                             
                INCLUDE TTRBRSN                                                 
           END-EXEC.                                                            
      /                                                                         
      *    DB2 AREA FOR MBS MERCHANDISE AREA TABLE                              
                                                                                
           EXEC SQL                                                             
                INCLUDE TMDSEAR                                                 
           END-EXEC.                                                            
      /                                                                         
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TSHPTPO - SHIPPED PO TABLE                    *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSHPTPO                                                 
           END-EXEC.                                                            
      /                                                                         
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TSHIPMT - SHIPMENT TABLE                      *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSHIPMT                                                 
           END-EXEC.                                                            
      *    DB2 AREA FOR COMMUNICATIONS                                          
      /                                                                         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
      /                                                                         
      *---------------------------------------------------------------*         
      *    TROUBLE NOTIFICATION BY PO LIST CURSOR                     *         
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
                DECLARE TRBNTF-CSR CURSOR                                       
                FOR SELECT                                                      
                      PO_LNE_NBR                                                
                    , PREPK_ID                                                  
                FROM                                                            
                      TTRBNTF                                                   
                WHERE                                                           
                      OPER_UNT_ID         = :TM500-OPER-UNT-ID                  
                  AND FCLTY_ID            = :TM500-FCLTY-ID                     
                  AND PO_NBR              = :TM500-PO-NBR                       
                  AND REC_SEQ_NBR         = :TM500-REC-SEQ-NBR                  
                  AND TRBL_RSN_CDE        = :TM500-TRBL-RSN-CDE                 
           END-EXEC.                                                            
                                                                                
      /                                                                         
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
      /                                                                         
       PROCEDURE DIVISION.                                                      
      **-------------------------------------------------------------**         
      ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
      ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
      ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
      **                                                             **         
      ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
      ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
      **-------------------------------------------------------------**         
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
                                                                                
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
           MOVE LENGTH OF TM505AI        TO DP030-MAP-LENGTH (1).               
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
           PERFORM 1000-CONTROL-PROCESSING                                      
                                                                                
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
       0001-CALL-CICS-ARCH-API.                                                 
                                                                                
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
                                          DFHCOMMAREA                           
                                          DP030-CICS-API-FIELDS                 
                                          DP020-STANDARD-COMMAREA               
                                          TM505AI.                              
                                                                                
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK                                            
                   DP013-XCTL-DISPLAY-RESTART                                   
                   DP013-CICS-ABEND      TO TRUE                                
               MOVE 'BEFORE 0000-MAIN-MODULE'                                   
                                         TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                    PERFORM 1100-FIND-OBJECT-KEYS                               
                    IF  PS-ERROR                                                
                        CONTINUE                                                
                    ELSE                                                        
                        MOVE INTER-APPL-COMM-AREA                               
                                         TO  ASC-HOLD-INTER-APPL                
                        PERFORM 4000-BUILD-INITIAL-PANEL                        
                        PERFORM 3200-RESET-ATTRIBUTES                           
                        PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    
                    END-IF                                                      
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                    PERFORM 2000-PROCESS-PANEL                                  
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                    MOVE ASC-HOLD-INTER-APPL                                    
                                         TO  INTER-APPL-COMM-AREA               
                    PERFORM 3000-EDIT-DATA-IN-COMMAREA                          
                    PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                        
                                                                                
               WHEN OTHER                                                       
                    SET DP013-LOGIC-ABEND TO TRUE                               
                    SET DP013-NO-ROLLBACK TO TRUE                               
                    MOVE '1000-CONTROL-PROCESSING'                              
                                         TO  DP013-PARAGRAPH                    
                    MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'          
                                         TO  DP013-MESSAGE-TEXT(1)              
                    MOVE DP020-NEXT-APPL-ACTIVITY                               
                                         TO  DP013-MESSAGE-TEXT(2)              
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *  DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE    *        
      *  INTER-APPLICATION FORMAT CODE.                                *        
      *----------------------------------------------------------------*        
                                                                                
       1100-FIND-OBJECT-KEYS.                                                   
                                                                                
           INITIALIZE ASC-SPECIFIC-COMMAREA.                                    
           MOVE DP020-INT-APPL-FORMAT-IND                                       
                                         TO  ASC-INT-APPL-FORMAT-IND.           
                                                                                
           PERFORM 1110-RACF-ACCESS.                                            
                                                                                
           IF PS-NO-ERROR                                                       
              SET ASC-NO-MULT-RESP-RSLV-REQUEST                                 
                  ASC-USE-SELECTION-QUEUE TO TRUE                               
              MOVE TM001-NUMBER-OF-SELECTED-ITEMS                               
                                   TO ASC-NUMBER-OF-SELECTED-ITEMS              
              MOVE TM001-NUMBER-OF-TS-ITEMS                                     
                                   TO ASC-NUMBER-OF-TS-ITEMS                    
              MOVE TM001-SEL-QUEUE-NAME                                         
                                   TO ASC-SEL-QUEUE-NAME                        
              PERFORM 1120-PREPARE-LIST-ENTRY                                   
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    DETERMINE IF USER HAS RACF ACCESS TO RESOLVE AN EARLY/LATE           
      *    TROUBLE WITH RTV OF 'N' OR A SHORT OUTSIDE LIMITS TROUBLE            
      *    WITH PRORATE OR ALLOC OF 'Y'.  SET SWITCH FOR MERCHANT VS.           
      *    DC ACCESS.                                                           
      *    DETERMINE IF USER HAS RACF ACCESS TO MANUALLY RESOLVE RETAIL         
      *    PRICE DISCREPANCY TROUBLE.                                           
      *----------------------------------------------------------------*        
       1110-RACF-ACCESS.                                                        
                                                                                
      * IS THE USER AUTHORIZED TO RESOLVE RETAIL PRICE DISCREPANCY              
      * TROUBLE (REASON CODE 133) ON THIS SCREEN?                               
                                                                                
      * ALSO RESTRICT RESOLUTION OF PACK-BY-STORE OVERAGE (532) AND             
      * PACK-BY-STORE SYSTEM ERROR (533) TO GROUP TMRTLPRC.                     
                                                                                
            SET DP008-RSRC-TYPE-FUNC           TO TRUE                          
            MOVE 'TMRTLPRC'                    TO DP008-FUNCTION-NAME           
            SET DP008-ACCESS-TYPE-USE          TO TRUE                          
                                                                                
            EXEC CICS                                                           
               LINK PROGRAM (DP008-SECURITY-SUBROUTINE)                         
                    COMMAREA (DP008-SECURITY-COMMAREA)                          
                    LENGTH (DP008-SECURITY-COMMAREA-LENGTH)                     
            END-EXEC                                                            
                                                                                
            IF DP008-CALL-SUCCESSFUL                                            
               IF DP008-ACCESS-ALLOWED                                          
                   MOVE 'Y'          TO ASC-ORDER-CTL-GROUP                     
               ELSE                                                             
                   MOVE 'N'          TO ASC-ORDER-CTL-GROUP                     
               END-IF                                                           
           ELSE                                                                 
               MOVE '1110-RACF-ACCESS'                                          
                                     TO DP013-PARAGRAPH                         
                MOVE 'UNABLE TO ACCESS CICS'                                    
                                     TO DP013-MESSAGE-TEXT (1)                  
                MOVE SQLCA                 TO DP013-SQLCA                       
                SET DP013-DB2-ABEND                                             
                    DP013-XCTL-DISPLAY-RESTART                                  
                                     TO TRUE                                    
                PERFORM DP013-0000-PROCESS-ABEND                                
            END-IF.                                                             
                                                                                
      *  IS THE USER AUTHORIZED TO KEY THE RTV FLAG ON THIS SCREEN?             
            IF PS-NO-ERROR                                                      
               SET DP008-RSRC-TYPE-FUNC        TO TRUE                          
               MOVE 'TMRTV'                    TO DP008-FUNCTION-NAME           
               SET DP008-ACCESS-TYPE-USE       TO TRUE                          
                                                                                
               EXEC CICS                                                        
                  LINK PROGRAM (DP008-SECURITY-SUBROUTINE)                      
                       COMMAREA (DP008-SECURITY-COMMAREA)                       
                       LENGTH (DP008-SECURITY-COMMAREA-LENGTH)                  
               END-EXEC                                                         
                                                                                
               IF DP008-CALL-SUCCESSFUL                                         
                  IF DP008-ACCESS-ALLOWED                                       
                      MOVE 'Y'                 TO ASC-RTV-ACCESS-SW             
                  ELSE                                                          
                      MOVE 'N'                 TO ASC-RTV-ACCESS-SW             
                  END-IF                                                        
              ELSE                                                              
                  MOVE '1110-RACF-ACCESS'                                       
                                        TO DP013-PARAGRAPH                      
                   MOVE 'UNABLE TO ACCESS CICS'                                 
                                        TO DP013-MESSAGE-TEXT (1)               
                   MOVE SQLCA              TO DP013-SQLCA                       
                   SET DP013-DB2-ABEND                                          
                       DP013-XCTL-DISPLAY-RESTART                               
                                        TO TRUE                                 
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
            END-IF.                                                             
                                                                                
      *  IS THE USER AUTHORIZED TO RESOLVE A PACK-BY-STORE OVERAGE?             
            IF PS-NO-ERROR                                                      
               SET DP008-RSRC-TYPE-FUNC        TO TRUE                          
               MOVE 'TMPBSRSV'                 TO DP008-FUNCTION-NAME           
               SET DP008-ACCESS-TYPE-USE       TO TRUE                          
                                                                                
               EXEC CICS                                                        
                  LINK PROGRAM (DP008-SECURITY-SUBROUTINE)                      
                       COMMAREA (DP008-SECURITY-COMMAREA)                       
                       LENGTH (DP008-SECURITY-COMMAREA-LENGTH)                  
               END-EXEC                                                         
                                                                                
               IF DP008-CALL-SUCCESSFUL                                         
                  IF DP008-ACCESS-ALLOWED                                       
                      MOVE 'Y'                 TO ASC-PBS-RESOLVE-SW            
                  ELSE                                                          
                      MOVE 'N'                 TO ASC-PBS-RESOLVE-SW            
                  END-IF                                                        
              ELSE                                                              
                  MOVE '1110-RACF-ACCESS'                                       
                                        TO DP013-PARAGRAPH                      
                   MOVE 'UNABLE TO ACCESS CICS'                                 
                                        TO DP013-MESSAGE-TEXT (1)               
                   MOVE SQLCA              TO DP013-SQLCA                       
                   SET DP013-DB2-ABEND                                          
                       DP013-XCTL-DISPLAY-RESTART                               
                                        TO TRUE                                 
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
            END-IF.                                                             
                                                                                
      *----------------------------------------------------------------*        
      * ENTRY IS FROM A LIST.  GET THE MESSAGE NUMBER FROM THE TEMP    *        
      * STORAGE QUEUE PASSED FROM THE LIST.                            *        
      *----------------------------------------------------------------*        
                                                                                
       1120-PREPARE-LIST-ENTRY.                                                 
                                                                                
           MOVE TM002-MAX-ITEMS          TO ASC-SEL-SUB.                        
           MOVE ZERO                     TO ASC-SEL-ITEM.                       
           PERFORM 2110-GET-NEXT-LINE.                                          
                                                                                
           IF (TM002-TRBL-LVL-IND (ASC-SEL-SUB) = PC-X OR PC-M)                 
             AND (ASC-ENTERED-FROM-PO-LIST)                                     
               MOVE PC-TSYMSG-00986      TO DP020-MSG-NUMBER                    
               SET TM002-ERR (ASC-SEL-SUB)                                      
                   PS-ERROR                                                     
                   DP020-MSG-FATAL                                              
                   DP020-NEXT-ACT-APPL-ERROR                                    
                                         TO TRUE                                
               PERFORM 9200-REWRITE-SEL-QUEUE                                   
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
      * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
      * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
      * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
      * FUNCTION KEYS CAN GET THIS FAR.                                *        
      *----------------------------------------------------------------*        
                                                                                
       2000-PROCESS-PANEL.                                                      
                                                                                
           IF  TM001-PO-NBR-N NOT GREATER THAN ZERO                             
               MOVE ASC-HOLD-INTER-APPL TO INTER-APPL-COMM-AREA                 
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-FK-LOCAL-GOTO (DP020-SRC-AID)                         
                   PERFORM 2105-PREPARE-INTER-APPL-COMM                         
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                    SET ASC-NO-MULT-RESP-RSLV-REQUEST TO TRUE                   
                    PERFORM 3000-EDIT-DATA-IN-COMMAREA                          
                    PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                        
                                                                                
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
                    SET ASC-NO-MULT-RESP-RSLV-REQUEST                           
                        DP030-DONT-ERASE                                        
                        DP030-SEND-DATAONLY (PC-APPL-MAP)                       
                                         TO TRUE                                
                    MOVE TM002-MAX-ITEMS TO ASC-SEL-SUB                         
                    MOVE ZERO            TO ASC-SEL-ITEM                        
                    PERFORM 2110-GET-NEXT-LINE                                  
                                                                                
                    IF (TM002-TRBL-LVL-IND(ASC-SEL-SUB) = PC-X OR PC-M)         
                      AND (ASC-ENTERED-FROM-PO-LIST)                            
                        MOVE PC-TSYMSG-00986 TO DP020-MSG-NUMBER                
                        SET TM002-ERR (ASC-SEL-SUB)                             
                            PS-ERROR                                            
                            DP020-MSG-FATAL                                     
                            DP020-NEXT-ACT-APPL-ERROR                           
                                         TO TRUE                                
                        PERFORM 9200-REWRITE-SEL-QUEUE                          
                    ELSE                                                        
                        PERFORM 4000-BUILD-INITIAL-PANEL                        
                        PERFORM 3200-RESET-ATTRIBUTES                           
                        PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    
                    END-IF                                                      
                                                                                
               WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
                    SET ASC-NO-MULT-RESP-RSLV-REQUEST TO TRUE                   
                    IF  ASC-USE-SELECTION-QUEUE                                 
                        PERFORM 2110-GET-NEXT-LINE                              
                                                                                
                        IF (TM002-TRBL-LVL-IND (ASC-SEL-SUB) =                  
                                                         PC-X OR PC-M)          
                          AND (ASC-ENTERED-FROM-PO-LIST)                        
                            MOVE PC-TSYMSG-00986 TO DP020-MSG-NUMBER            
                            SET TM002-ERR (ASC-SEL-SUB)                         
                                PS-ERROR                                        
                                DP020-MSG-FATAL                                 
                                DP020-NEXT-ACT-APPL-ERROR                       
                                         TO TRUE                                
                            PERFORM 9200-REWRITE-SEL-QUEUE                      
                        ELSE                                                    
                            PERFORM 4000-BUILD-INITIAL-PANEL                    
                            PERFORM 3200-RESET-ATTRIBUTES                       
                            PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                
                        END-IF                                                  
                    ELSE                                                        
      *                 --- BOTTOM OF LIST ---                                  
                        MOVE PC-TSYMSG-00070                                    
                                         TO DP020-MSG-NUMBER                    
                        SET DP020-MSG-INFORMATIONAL                             
                                         TO TRUE                                
                    END-IF                                                      
                                                                                
               WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                     
                    SET ASC-NO-MULT-RESP-RSLV-REQUEST TO TRUE                   
                    IF  ASC-USE-SELECTION-QUEUE                                 
                        PERFORM 2120-GET-PREV-LINE                              
                        PERFORM 4000-BUILD-INITIAL-PANEL                        
                        PERFORM 3200-RESET-ATTRIBUTES                           
                        PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    
                    ELSE                                                        
      *                 --- TOP OF LIST ---                                     
                        MOVE PC-TSYMSG-00069                                    
                                         TO DP020-MSG-NUMBER                    
                        SET DP020-MSG-INFORMATIONAL                             
                                         TO TRUE                                
                    END-IF                                                      
                                                                                
               WHEN OTHER                                                       
                    PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                        
                    PERFORM 3000-EDIT-DATA-IN-COMMAREA                          
                    IF  PS-NO-ERROR                                             
                        SET DP030-CONTINUE-GO                                   
                                         TO TRUE                                
                        PERFORM 2100-CHECK-FUNCTION-KEY                         
                    ELSE                                                        
                        PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    
                        SET DP030-OVERRIDE-GO                                   
                                         TO TRUE                                
                    END-IF                                                      
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED       *        
      * FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED   *        
      * FROM THE CICS ARCHITECTURE API.                                *        
      *----------------------------------------------------------------*        
                                                                                
       2100-CHECK-FUNCTION-KEY.                                                 
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                    SET ASC-NO-MULT-RESP-RSLV-REQUEST TO TRUE                   
                    PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                        
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
      *             --- RESPOND FUNCTION KEY ---                                
                    SET ASC-NO-MULT-RESP-RSLV-REQUEST                           
                        DP030-CONTINUE-GO TO TRUE                               
                    PERFORM 5000-VALIDATE THRU 5000-EXIT                        
                    IF  PS-NO-ERROR                                             
                        INITIALIZE TM500-FIELDS                                 
                        SET TM500-RESPOND                                       
                            TM500-ADD-DETAILS                                   
                                          TO TRUE                               
                        PERFORM 5050-UPDATE-PROCESS                             
                        IF PS-NO-ERROR                                          
      *                     --- UPDATES SUCCESSFUL ---                          
                            MOVE PC-TSYMSG-01044                                
                                          TO DP020-MSG-NUMBER                   
                            SET DP020-MSG-INFORMATIONAL                         
                                          TO TRUE                               
                            PERFORM 9000-UPDATE-SELECTION-QUEUE                 
                        ELSE                                                    
                            PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                
                        END-IF                                                  
                    ELSE                                                        
                        PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    
                    END-IF                                                      
                                                                                
                                                                                
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)                      
      *             --- RESOLVE FUNCTION KEY ---                                
                    SET ASC-NO-MULT-RESP-RSLV-REQUEST                           
                        DP030-CONTINUE-GO         TO TRUE                       
                    PERFORM 5000-VALIDATE THRU 5000-EXIT                        
                    IF  PS-NO-ERROR                                             
                                                                                
                       INITIALIZE TM500-FIELDS                                  
                       EVALUATE TRUE                                            
                       WHEN ASC-TRBL-RSN-CDE = PC-509 AND                       
                                        ASC-RTV-IND = PC-N                      
                           PERFORM 5010-FETCH-VEND-SHIP-DTE                     
                           MOVE SHIPMT-VEND-SHIP-DTE                            
                                                 TO TM500-VEND-SHIP-DTE         
                           MOVE ASC-RTV-IND      TO TM500-RTV-SW                
      *KBW                 SET TM500-RESOLVE-AUTO                               
                           SET TM500-RESOLVE-MANUAL                             
                               TM500-ADD-DETAILS  TO TRUE                       
                       WHEN ASC-TRBL-RSN-CDE = PC-502                           
                           MOVE ASC-RTV-IND      TO TM500-RTV-SW                
                           SET TM500-RESOLVE-MANUAL                             
                               TM500-ADD-DETAILS TO TRUE                        
                       WHEN OTHER                                               
                           SET TM500-RESOLVE-MANUAL  TO TRUE                    
                           IF  ASC-TEXT1 GREATER THAN SPACES                    
                               SET TM500-ADD-DETAILS TO TRUE                    
                           ELSE                                                 
                               SET TM500-NO-DETAILS  TO TRUE                    
                           END-IF                                               
                       END-EVALUATE                                             
                                                                                
                       PERFORM 5025-GET-TRBL-RSN-RESOLVE-IND                    
                       IF TRBRSN-AUTOACKNL-IND = 'Y'                            
                           PERFORM 5070-CHECK-AUTO-ACKNOW                       
                        ELSE                                                    
                        PERFORM 5050-UPDATE-PROCESS                             
                        END-IF                                                  
                        IF  PS-NO-ERROR                                         
                            MOVE TM500-RESOLVED-IND                             
                                         TO ASC-RESOLVED-IND                    
                            IF DP020-MSG-NUMBER = 02243                         
                                PERFORM 9000-UPDATE-SELECTION-QUEUE             
                            ELSE                                                
      *                         --- UPDATES SUCCESSFUL ---                      
                                MOVE PC-TSYMSG-01045                            
                                         TO DP020-MSG-NUMBER                    
                                SET DP020-MSG-INFORMATIONAL                     
                                         TO TRUE                                
                                PERFORM 9000-UPDATE-SELECTION-QUEUE             
                            END-IF                                              
      *                     PERFORM 5070-CHECK-AUTO-ACKNOW                      
                        ELSE                                                    
                            PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                
                        END-IF                                                  
                    ELSE                                                        
                        PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    
                    END-IF                                                      
                                                                                
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-03 (DP020-SRC-AID)                      
      *             --- UPDATE FUNCTION KEY ---                                 
                    SET ASC-NO-MULT-RESP-RSLV-REQUEST                           
                        DP030-CONTINUE-GO    TO TRUE                            
                    INITIALIZE TM500-FIELDS                                     
                    SET TM500-UPDATE-NOTIF   TO TRUE                            
                    MOVE ASC-DEPT-NBR        TO TM500-DEPT-NBR                  
                    PERFORM 5030-VALIDATE-FOR-UPDATE                            
                    IF  PS-NO-ERROR                                             
                        PERFORM 5050-UPDATE-PROCESS                             
                        IF  PS-NO-ERROR                                         
      *                     --- UPDATES SUCCESSFUL ---                          
                            MOVE PC-TSYMSG-00059                                
                                             TO DP020-MSG-NUMBER                
                            SET DP020-MSG-INFORMATIONAL                         
                                             TO TRUE                            
                            MOVE 1       TO ENTNME-SEQ-NBR                      
                            PERFORM 4130-GET-DEPT-NAME                          
                            MOVE ASC-DEPT-NAME                                  
                                             TO ADEPTDSO                        
                            PERFORM 9000-UPDATE-SELECTION-QUEUE                 
                        ELSE                                                    
                            PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                
                        END-IF                                                  
                    ELSE                                                        
                        PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    
                    END-IF                                                      
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-04 (DP020-SRC-AID)                      
      *             --- MULTIPLE RESPOND FUNCTION KEY ---                       
                    SET ASC-NO-MULT-RESP-RSLV-REQUEST                           
                        DP030-CONTINUE-GO    TO TRUE                            
                    PERFORM 5000-VALIDATE THRU 5000-EXIT                        
      *             MUST CONFIRM BEFORE UPDATE                                  
                    IF  PS-NO-ERROR                                             
                        MOVE PC-TSYMSG-00004 TO DP020-MSG-NUMBER                
                        SET DP020-MSG-INFORMATIONAL                             
                            ASC-MULT-RESPOND-REQUEST                            
                            ASC-MULT-RESP-RSLV-REQUEST                          
                                             TO TRUE                            
                    ELSE                                                        
                        PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    
                    END-IF                                                      
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-05 (DP020-SRC-AID)                      
      *             --- MULTIPLE RESOLVE FUNCTION KEY ---                       
                    SET ASC-NO-MULT-RESP-RSLV-REQUEST                           
                        DP030-CONTINUE-GO    TO TRUE                            
                    PERFORM 5025-GET-TRBL-RSN-RESOLVE-IND                       
                    IF TRBRSN-AUTOACKNL-IND = 'Y'                               
                        PERFORM 5070-CHECK-AUTO-ACKNOW                          
                    ELSE                                                        
                        PERFORM 5000-VALIDATE THRU 5000-EXIT                    
      *             MUST CONFIRM BEFORE UPDATE                                  
                        IF  PS-NO-ERROR                                         
                          MOVE PC-TSYMSG-00004 TO DP020-MSG-NUMBER              
                          SET DP020-MSG-INFORMATIONAL                           
                              ASC-MULT-RESOLVE-REQUEST                          
                              ASC-MULT-RESP-RSLV-REQUEST                        
                                             TO TRUE                            
                          MOVE ASC-RTV-IND     TO TM500-RTV-SW                  
                        ELSE                                                    
                          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                  
                        END-IF                                                  
                    END-IF                                                      
                                                                                
               WHEN DP020-FK-CONFIRM (DP020-SRC-AID)                            
                    IF  ASC-NO-MULT-RESP-RSLV-REQUEST                           
                        MOVE PC-TSYMSG-00016 TO DP020-MSG-NUMBER                
                        SET PS-ERROR                                            
                            DP020-MSG-FATAL  TO TRUE                            
                    ELSE                                                        
                        IF  ASC-MULT-RESPOND-REQUEST                            
      *                     --- MULTIPLE RESPOND REQUEST ---                    
                            SET DP030-CONTINUE-GO                               
                                             TO TRUE                            
                            PERFORM 5000-VALIDATE THRU                          
                                             5000-EXIT                          
                            IF  PS-NO-ERROR                                     
                                INITIALIZE TM500-FIELDS                         
                                SET TM500-RESPOND                               
                                    TM500-ADD-DETAILS                           
                                             TO TRUE                            
                                MOVE TM002-MAX-ITEMS                            
                                             TO ASC-SEL-SUB                     
                                MOVE ZERO    TO ASC-SEL-ITEM                    
                                SET PS-NOT-END-OF-SEL-ITEMS-QUEUE               
                                             TO TRUE                            
                                PERFORM 5060-MULT-UPDATE-PROCESS                
                                    UNTIL PS-END-OF-SEL-ITEMS-QUEUE             
                                       OR PS-ERROR                              
                                IF PS-NO-ERROR                                  
                                  IF DP020-MSG-NUMBER = 02243                   
                                      SET ASC-NO-MULT-RESP-RSLV-REQUEST         
                                             TO TRUE                            
                                  ELSE                                          
                                    IF PV-TRBL-RSLVD-COUNT = ZERO               
      *                             --- NO TROUBLE RESPONDED TO ---             
                                        MOVE PC-TSYMSG-00938                    
                                                  TO DP020-MSG-NUMBER           
                                        MOVE -1    TO  ATEXT1L                  
                                        SET DP020-MSG-INFORMATIONAL             
                                           ASC-NO-MULT-RESP-RSLV-REQUEST        
                                                  TO TRUE                       
                                    ELSE                                        
      *                             --- RESPONSES SUCCESSFUL ---                
                                        MOVE PC-TSYMSG-01044                    
                                             TO DP020-MSG-NUMBER                
                                        SET DP020-MSG-INFORMATIONAL             
                                        ASC-NO-MULT-RESP-RSLV-REQUEST           
                                             TO TRUE                            
                                    END-IF                                      
                                  END-IF                                        
                                ELSE                                            
                                    PERFORM 4400-MOVE-COMMAREA-TO-SCREEN        
                                END-IF                                          
                            ELSE                                                
                                PERFORM 4400-MOVE-COMMAREA-TO-SCREEN            
                            END-IF                                              
                        ELSE                                                    
      *                     --- MULTIPLE RESOLVE REQUEST ---                    
                            SET DP030-CONTINUE-GO TO TRUE                       
                            PERFORM 5000-VALIDATE THRU                          
                                                  5000-EXIT                     
                                                                                
                            IF  PS-NO-ERROR                                     
                                INITIALIZE TM500-FIELDS                         
                                SET TM500-RESOLVE-MANUAL                        
                                             TO TRUE                            
                                                                                
                                IF ASC-TRBL-RSN-CDE = PC-502                    
      *KBW                                                                      
                                                 OR PC-509                      
                                    SET TM500-ADD-DETAILS                       
                                             TO TRUE                            
                                ELSE                                            
                                IF  ASC-TEXT1 GREATER THAN SPACES               
                                    SET TM500-ADD-DETAILS                       
                                             TO TRUE                            
                                ELSE                                            
                                    SET TM500-NO-DETAILS                        
                                             TO TRUE                            
                                END-IF                                          
                                END-IF                                          
                                                                                
                                MOVE TM002-MAX-ITEMS                            
                                             TO ASC-SEL-SUB                     
                                MOVE ZERO    TO ASC-SEL-ITEM                    
                                SET PS-NOT-END-OF-SEL-ITEMS-QUEUE               
                                             TO TRUE                            
                                                                                
                                PERFORM 5060-MULT-UPDATE-PROCESS                
                                  UNTIL PS-END-OF-SEL-ITEMS-QUEUE               
                                     OR PS-ERROR                                
                                                                                
                                IF PS-NO-ERROR                                  
                                  IF DP020-MSG-NUMBER = 02243                   
                                       MOVE TM500-RESOLVED-IND                  
                                                  TO ASC-RESOLVED-IND           
                                       SET ASC-NO-MULT-RESP-RSLV-REQUEST        
                                                  TO TRUE                       
                                  ELSE                                          
                                    IF  PV-TRBL-RSLVD-COUNT = 0                 
      *                             --- NO TROUBLE RESOLVED ---                 
                                       MOVE PC-TSYMSG-00938                     
                                                  TO DP020-MSG-NUMBER           
                                       MOVE -1    TO  ATEXT1L                   
                                       SET DP020-MSG-INFORMATIONAL              
                                           ASC-NO-MULT-RESP-RSLV-REQUEST        
                                                  TO TRUE                       
                                    ELSE                                        
      *                             --- RESOLUTIONS SUCCESSFUL ---              
                                       MOVE TM500-RESOLVED-IND                  
                                                  TO ASC-RESOLVED-IND           
                                       MOVE PC-TSYMSG-01045                     
                                                  TO DP020-MSG-NUMBER           
                                       SET DP020-MSG-INFORMATIONAL              
                                           ASC-NO-MULT-RESP-RSLV-REQUEST        
                                                  TO TRUE                       
                                    END-IF                                      
                                ELSE                                            
                                    PERFORM 4400-MOVE-COMMAREA-TO-SCREEN        
                                END-IF                                          
                            ELSE                                                
                                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN           
                            END-IF                                              
                        END-IF                                                  
                    END-IF                                                      
                                                                                
               WHEN OTHER                                                       
                    SET DP013-NO-ROLLBACK                                       
                        DP013-XCTL-DISPLAY-RESTART                              
                        DP013-CICS-ABEND  TO TRUE                               
                    MOVE '2000-PROCESS-PANEL'                                   
                                         TO DP013-PARAGRAPH                     
                    MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'             
                                         TO DP013-MESSAGE-TEXT (1)              
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
      /                                                                         
       2105-PREPARE-INTER-APPL-COMM.                                            
           INITIALIZE PO011-INTER-APPL-COMMAREA.                                
           MOVE ASC-KEY-PO-NBR         TO PO011-PO-NBR.                         
      /                                                                         
      *----------------------------------------------------------------*        
      *    RETRIEVE THE NEXT KEY FOR PROCESSING.                       *        
      *----------------------------------------------------------------*        
                                                                                
       2110-GET-NEXT-LINE.                                                      
                                                                                
           ADD +1                        TO  ASC-SEL-SUB.                       
           IF  ASC-SEL-SUB > TM002-MAX-ITEMS                                    
               ADD +1                    TO  ASC-SEL-ITEM                       
               MOVE +1                   TO  ASC-SEL-SUB                        
           END-IF.                                                              
                                                                                
           IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
               PERFORM 9100-READ-SEL-QUEUE                                      
               IF  TM002-ITEM (ASC-SEL-SUB) > ZERO                              
                   MOVE TM002-NOTIF-NBR(ASC-SEL-SUB)                            
                                         TO  ASC-KEY-NOTIF-NBR                  
                   SET TM002-INQ (ASC-SEL-SUB)                                  
                                         TO  TRUE                               
                   PERFORM 9200-REWRITE-SEL-QUEUE                               
               ELSE                                                             
                   SUBTRACT +1 FROM ASC-SEL-SUB                                 
      *            --- BOTTOM OF LIST ---                                       
                   MOVE PC-TSYMSG-00070  TO  DP020-MSG-NUMBER                   
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO  TRUE                               
               END-IF                                                           
           ELSE                                                                 
               SUBTRACT +1 FROM ASC-SEL-ITEM                                    
               MOVE TM002-MAX-ITEMS      TO  ASC-SEL-SUB                        
               MOVE PC-TSYMSG-00070      TO  DP020-MSG-NUMBER                   
               SET DP020-MSG-INFORMATIONAL                                      
                                         TO  TRUE                               
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *  GET THE MESSAGE NUMBER (KEY) OF THE PREVIOUS SELECTED         *        
      *  OBJECT FROM THE SELECTED-ITEMS TEMP. STORAGE QUEUE.           *        
      *----------------------------------------------------------------*        
                                                                                
       2120-GET-PREV-LINE.                                                      
                                                                                
           SUBTRACT +1 FROM ASC-SEL-SUB.                                        
           IF  ASC-SEL-SUB < +1                                                 
               SUBTRACT +1 FROM ASC-SEL-ITEM                                    
               MOVE TM002-MAX-ITEMS      TO ASC-SEL-SUB                         
           END-IF.                                                              
                                                                                
           IF  ASC-SEL-ITEM > ZERO                                              
               PERFORM 9100-READ-SEL-QUEUE                                      
               MOVE TM002-NOTIF-NBR(ASC-SEL-SUB)                                
                                         TO  ASC-KEY-NOTIF-NBR                  
               SET TM002-INQ (ASC-SEL-SUB)                                      
                                         TO  TRUE                               
           ELSE                                                                 
               MOVE +1                   TO ASC-SEL-ITEM                        
                                            ASC-SEL-SUB                         
      *        --- TOP OF LIST ---                                              
               MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-INFORMATIONAL                                      
                                         TO TRUE                                
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS   *        
      * IN THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN   *        
      * THE APPLICATION-SPECIFIC COMMAERA, NOT ON THE SCREEN.          *        
      *----------------------------------------------------------------*        
                                                                                
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
                                                                                
           SET ASC-DEPT-NBR-NOT-CHGD     TO  TRUE                               
                                                                                
           IF  ADEPTL > ZERO                                                    
               MOVE ADEPTI               TO  ASC-DEPT-NBR                       
               SET PS-VALIDATE-DEPT                                             
                   ASC-DEPT-NBR-CHGD     TO  TRUE                               
           ELSE                                                                 
               IF  ADEPTF  = DP015-ERASE-EOF                                    
                   MOVE ZERO             TO  ASC-DEPT-NBR                       
                   SET PS-VALIDATE-DEPT                                         
                       ASC-DEPT-NBR-CHGD TO  TRUE                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ATEXT1L > ZERO                                                   
               MOVE ATEXT1I              TO  ASC-TEXT1                          
           ELSE                                                                 
               IF  ATEXT1F  = DP015-ERASE-EOF                                   
                   MOVE SPACE            TO  ASC-TEXT1                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ATEXT2L > ZERO                                                   
               MOVE ATEXT2I              TO  ASC-TEXT2                          
           ELSE                                                                 
               IF  ATEXT2F  = DP015-ERASE-EOF                                   
                   MOVE SPACE            TO  ASC-TEXT2                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ATEXT3L > ZERO                                                   
               MOVE ATEXT3I              TO  ASC-TEXT3                          
           ELSE                                                                 
               IF  ATEXT3F  = DP015-ERASE-EOF                                   
                   MOVE SPACE            TO  ASC-TEXT3                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ATEXT4L > ZERO                                                   
               MOVE ATEXT4I              TO  ASC-TEXT4                          
           ELSE                                                                 
               IF  ATEXT4F  = DP015-ERASE-EOF                                   
                   MOVE SPACE            TO  ASC-TEXT4                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ATEXT5L > ZERO                                                   
               MOVE ATEXT5I              TO  ASC-TEXT5                          
           ELSE                                                                 
               IF  ATEXT5F  = DP015-ERASE-EOF                                   
                   MOVE SPACE            TO  ASC-TEXT5                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ANEWPLNL > ZERO                                                  
               MOVE ANEWPLNI             TO  ASC-NEW-PLAN-IND                   
           ELSE                                                                 
               IF  ANEWPLNF = DP015-ERASE-EOF                                   
                   MOVE SPACE            TO  ASC-NEW-PLAN-IND                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  APROALCL > ZERO                                                  
               MOVE APROALCI             TO  ASC-PRORATED-ALLOC-IND             
           ELSE                                                                 
               IF  APROALCF = DP015-ERASE-EOF                                   
                   MOVE SPACE            TO  ASC-PRORATED-ALLOC-IND             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ARTVINDL > ZERO                                                  
               MOVE ARTVINDI             TO  ASC-RTV-IND                        
           ELSE                                                                 
               IF  ARTVINDF = DP015-ERASE-EOF                                   
                   MOVE SPACE            TO  ASC-RTV-IND                        
               END-IF                                                           
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      * PERFORM EDITS ON EACH FIELD ENTERED BY THE USER.  IF AN ERROR  *        
      * IS FOUND, THE CURSOR IS POSITIONED AT THAT FIELD AND AN        *        
      * APPROPRIATE ERROR MESSAGE IS DISPLAYED.                        *        
      *----------------------------------------------------------------*        
                                                                                
       3000-EDIT-DATA-IN-COMMAREA.                                              
                                                                                
           PERFORM 3200-RESET-ATTRIBUTES.                                       
                                                                                
           IF (ASC-DEPT-NBR > SPACES)                                           
               MOVE ASC-DEPT-NBR         TO DP010I-UNEDITED-FIELD               
               MOVE LENGTH OF ASC-DEPT-NBR                                      
                                         TO DP010I-MAXIMUM-DIGITS               
               MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS             
               SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                          
263627         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263627              USING DP010I-NUMERIC-EDIT-AREA                              
               IF  DP010I-ERROR-DETECTED                                        
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO TRUE                                
                   MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO ADEPTA                              
                   MOVE DP015-RED        TO ADEPTC                              
                   MOVE DP015-REVERSE    TO ADEPTH                              
                   MOVE -1               TO ADEPTL                              
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
               ELSE                                                             
                   MOVE DP010I-NUMERIC-FIELD                                    
                                         TO ASC-DEPT-NBR-N                      
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  PS-VALIDATE-DEPT                                                 
               IF ASC-DEPT-NBR EQUAL ZEROES                                     
                   CONTINUE                                                     
               ELSE                                                             
                   PERFORM 4130-GET-DEPT-NAME                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *  RETURN TO VENDOR INDICATOR                                             
      *    IF ASC-TRBL-RSN-CDE = PC-502 OR PC-509                               
      *        EVALUATE TRUE                                                    
      *        WHEN ASC-RTV-IND = PC-N OR PC-Y                                  
      *            CONTINUE                                                     
      *        WHEN ASC-RTV-IND = SPACE                                         
      *            SET PS-ERROR          TO  TRUE                               
      *            SET DP020-MSG-FATAL   TO  TRUE                               
      *            MOVE PC-TSYMSG-00007  TO  DP020-MSG-NUMBER                   
      *            MOVE DP015-UNP-ALP-BRT-OFF                                   
      *                                      TO ARTVINDA                        
      *            MOVE DP015-RED        TO  ARTVINDC                           
      *            MOVE DP015-REVERSE    TO  ARTVINDH                           
      *            MOVE -1               TO  ARTVINDL                           
      *        WHEN OTHER                                                       
      *            SET PS-ERROR          TO  TRUE                               
      *            SET DP020-MSG-FATAL   TO  TRUE                               
      *            MOVE PC-TSYMSG-00005  TO  DP020-MSG-NUMBER                   
      *            MOVE DP015-UNP-ALP-BRT-OFF                                   
      *                                      TO ARTVINDA                        
      *            MOVE DP015-RED        TO  ARTVINDC                           
      *            MOVE DP015-REVERSE    TO  ARTVINDH                           
      *            MOVE -1               TO  ARTVINDL                           
      *        END-EVALUATE                                                     
      *    END-IF.                                                              
      *                                                                         
           IF  ASC-PRORATED-ALLOC-IND EQUAL SPACE OR PC-Y                       
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-00005      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  APROALCA                           
               MOVE DP015-RED            TO  APROALCC                           
               MOVE DP015-REVERSE        TO  APROALCH                           
               MOVE -1                   TO  APROALCL                           
           END-IF.                                                              
                                                                                
           IF  ASC-NEW-PLAN-IND EQUAL SPACE OR PC-Y                             
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-00005      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ANEWPLNA                           
               MOVE DP015-RED            TO  ANEWPLNC                           
               MOVE DP015-REVERSE        TO  ANEWPLNH                           
               MOVE -1                   TO  ANEWPLNL                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    RESET THE DATA ENTERABLE FIELDS ATTRIBUTE, HIGHLIGHT, AND   *        
      *    COLOR BYTES.                                                *        
      *----------------------------------------------------------------*        
                                                                                
       3200-RESET-ATTRIBUTES.                                                   
                                                                                
           MOVE DP015-UNP-ALP-NOR-OFF    TO ANEWPLNA                            
                                            ADEPTA                              
                                            ATEXT1A                             
                                            ATEXT2A                             
                                            ATEXT3A                             
                                            ATEXT4A                             
                                            APROALCA                            
                                            ARTVINDA.                           
           MOVE DP015-GREEN              TO ANEWPLNC                            
                                            ADEPTC                              
                                            ATEXT1C                             
                                            ATEXT2C                             
                                            ATEXT3C                             
                                            ATEXT4C                             
                                            APROALCC                            
                                            ARTVINDC.                           
           MOVE DP015-UNDERLINE          TO ANEWPLNH                            
                                            ADEPTH                              
                                            ATEXT1H                             
                                            ATEXT2H                             
                                            ATEXT3H                             
                                            ATEXT4H                             
                                            APROALCH                            
                                            ARTVINDH                            
                                                                                
           IF ASC-TRBL-RSN-CDE = PC-502 OR PC-509                               
               CONTINUE                                                         
           ELSE                                                                 
               MOVE DP015-ASK-DRK-OFF    TO ARTVINDA                            
               MOVE DP015-BLUE           TO ARTVINDC                            
               MOVE DP015-HL-OFF         TO ARTVINDH                            
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    VALIDATE THE QUEUE WAS PASSED AND DISPLAY FOR THE FIRST     *        
      *    ENTRY.                                                      *        
      *----------------------------------------------------------------*        
                                                                                
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           INITIALIZE ASC-SCREEN-ENTERABLE-FIELDS.                              
                                                                                
           PERFORM 4120-GET-TROUBLE.                                            
           IF PS-NO-ERROR                                                       
               PERFORM 4100-GET-FACILITY-NAME                                   
               PERFORM 4110-GET-VENDOR-NAME                                     
               PERFORM 4130-GET-DEPT-NAME                                       
               SET DP030-SET-CURSOR-APPL-1   TO  TRUE                           
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    GET THE FACILITY NAME FOR THE SCREEN.                       *        
      *----------------------------------------------------------------*        
                                                                                
       4100-GET-FACILITY-NAME.                                                  
                                                                                
           INITIALIZE XL071-COMMAREA-REC.                                       
                                                                                
           MOVE ASC-KEY-OPER-UNT-ID   TO PV-UNPACK-OPER-UNT-ID.                 
           MOVE PV-UNPACK-OPER-UNT-ID TO XL071-LOCATION-NUMBER.                 
                                                                                
           CALL XL071-LOCATION-MODULE USING DFHEIBLK                            
                                            XL071-COMMAREA                      
                                            XL071-COMMAREA-REC.                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN XL071-NO-ERRORS                                             
                    MOVE XL071-LOC-NM         TO ASC-FCLTY-NME                  
               WHEN XL071-LOC-NOT-FOUND                                         
                    MOVE 'FACILITY NOT FOUND' TO ASC-FCLTY-NME                  
               WHEN XL071-SQLCODE-N = -904                                      
               WHEN XL071-SQLCODE-N = -913                                      
                    PERFORM 9900-SOFT-ABORT                                     
               WHEN XL071-BAD-DB2-RETURN                                        
                    MOVE '4100-GET-FACILITY-NAME'                               
                                         TO DP013-PARAGRAPH                     
                    MOVE 'SELECT THE FACILITY NAME'                             
                                         TO DP013-MESSAGE-TEXT (1)              
                    MOVE SQLCA           TO DP013-SQLCA                         
                    MOVE 'XLT_LOC'       TO DP013-DB2-TABLE-NAME (1)            
                    SET DP013-DB2-ABEND                                         
                       DP013-XCTL-DISPLAY-RESTART                               
                                         TO TRUE                                
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
      /                                                                         
      *----------------------------------------------------------------*        
      *    GET THE VENDOR NAME FOR THE SCREEN.                         *        
      *----------------------------------------------------------------*        
                                                                                
       4110-GET-VENDOR-NAME.                                                    
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                       B.ENT_NAME_DESC                                          
                     , A.DEPT_NBR                                               
               INTO                                                             
                      :ASC-VENDOR-NAME                                          
                     ,:ASC-DEPT-NBR                                             
               FROM                                                             
                       TPURCHS A                                                
                     , TENTNME B                                                
               WHERE                                                            
                       A.PO_NBR           =  :ASC-KEY-PO-NBR                    
                AND    A.VER_STATUS_CDE   =  :PC-A                              
                AND    A.ENT_ID           =  B.ENT_ID                           
                AND    B.TYPE_CDE         = '01'                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLCODE = +100                                              
                    MOVE 'VENDOR NOT FOUND' TO ASC-VENDOR-NAME                  
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE NOT EQUAL ZERO                                      
                    MOVE '4110-GET-VENDOR-NAME'                                 
                                         TO DP013-PARAGRAPH                     
                    MOVE 'SELECT VEND NAME'                                     
                                         TO DP013-MESSAGE-TEXT (1)              
                    MOVE SQLCA           TO DP013-SQLCA                         
                    MOVE 'TPURCHS'       TO DP013-DB2-TABLE-NAME (1)            
                    MOVE 'TENTNME'       TO DP013-DB2-TABLE-NAME (2)            
                    SET DP013-DB2-ABEND                                         
                       DP013-XCTL-DISPLAY-RESTART                               
                                         TO TRUE                                
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
      /                                                                         
      *----------------------------------------------------------------*        
      *    GET THE TROUBLE DETAIL INFORMATION.                         *        
      *----------------------------------------------------------------*        
                                                                                
       4120-GET-TROUBLE.                                                        
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                       OPER_UNT_ID                                              
                     , FCLTY_ID                                                 
                     , FCLTY_AREA_ID                                            
                     , DEPT_NBR                                                 
                     , PO_NBR                                                   
                     , PO_LNE_NBR                                               
                     , PREPK_ID                                                 
                     , REC_SEQ_NBR                                              
                     , TRBL_RSN_CDE                                             
                     , RESOLVED_IND                                             
               INTO                                                             
                      :ASC-KEY-OPER-UNT-ID                                      
                     ,:ASC-KEY-FCLTY-ID                                         
                     ,:ASC-KEY-FCLTY-AREA-ID                                    
                     ,:ASC-DEPT-NBR                                             
                     ,:ASC-KEY-PO-NBR                                           
                     ,:ASC-KEY-PO-LNE-NBR                                       
                     ,:ASC-KEY-PREPK-ID                                         
                     ,:ASC-KEY-REC-SEQ-NBR                                      
                     ,:ASC-TRBL-RSN-CDE                                         
                     ,:ASC-RESOLVED-IND                                         
               FROM                                                             
                       TTRBNTF                                                  
               WHERE                                                            
                       NOTIF_NBR          =  :ASC-KEY-NOTIF-NBR                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLCODE = +100                                              
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP020-NEXT-ACT-APPL-ERROR TO TRUE                        
                   MOVE PC-TSYMSG-00050 TO DP020-MSG-NUMBER                     
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE NOT EQUAL ZERO                                      
                    MOVE '4120-GET-TROUBLE'                                     
                                         TO DP013-PARAGRAPH                     
                    MOVE 'SELECT TROUBLE'                                       
                                         TO DP013-MESSAGE-TEXT (1)              
                    MOVE SQLCA           TO DP013-SQLCA                         
                    MOVE 'TTRBNTF'       TO DP013-DB2-TABLE-NAME (1)            
                    SET DP013-DB2-ABEND                                         
                       DP013-XCTL-DISPLAY-RESTART                               
                                         TO TRUE                                
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
      /                                                                         
      *----------------------------------------------------------------*        
      *    GET THE DEPARTMENT NAME.                                    *        
      *----------------------------------------------------------------*        
                                                                                
       4130-GET-DEPT-NAME.                                                      
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                       MDSEAR_DESC                                              
               INTO                                                             
                      :ASC-DEPT-NAME                                            
               FROM                                                             
                       TMDSEAR                                                  
               WHERE                                                            
                       OPERCO_NBR         =  :PC-COMPANY                        
               AND     DEPT_NBR           =  :ASC-DEPT-NBR                      
               AND     MDSELV_CDE         =  :PC-DPT                            
               AND     CURRENT DATE BETWEEN STRT_DTE                            
                                      AND   END_DTE                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLCODE = +100                                              
                    MOVE 'NO DEPARTMENT' TO  ASC-DEPT-NAME                      
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE NOT EQUAL ZERO                                      
                    MOVE '4130-GET-DEPT-NAME'                                   
                                         TO DP013-PARAGRAPH                     
                    MOVE 'SELECT DEPARTMENT NAME'                               
                                         TO DP013-MESSAGE-TEXT (1)              
                    MOVE SQLCA           TO DP013-SQLCA                         
                    MOVE 'TMDSEAR'       TO DP013-DB2-TABLE-NAME (1)            
                    SET DP013-DB2-ABEND                                         
                       DP013-XCTL-DISPLAY-RESTART                               
                                         TO TRUE                                
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
      /                                                                         
      *----------------------------------------------------------------*        
      *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
      *----------------------------------------------------------------*        
                                                                                
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
                                                                                
           IF  PS-SOFT-ABORT                                                    
               MOVE PC-TSYMSG-00551      TO  DP020-MSG-NUMBER                   
           END-IF.                                                              
                                                                                
           MOVE ASC-FCLTY-NME            TO  AFCLTNMO.                          
           MOVE ASC-VENDOR-NAME          TO  AVENDNMO.                          
           MOVE ASC-DEPT-NBR             TO  ADEPTO.                            
           MOVE ASC-DEPT-NAME            TO  ADEPTDSO.                          
                                                                                
           MOVE ASC-KEY-PO-NBR           TO  PV-PO-NBR.                         
           MOVE ASC-KEY-REC-SEQ-NBR      TO  PV-REC-SEQ-NBR.                    
           MOVE PV-DISPLAY-PO            TO  APORECO.                           
                                                                                
           IF ASC-KEY-PREPK-ID > ZEROES                                         
               MOVE '0000'               TO  APOLINEO                           
               MOVE ASC-KEY-PREPK-ID     TO  PV-PREPK-ID                        
               MOVE PV-PREPK-ID          TO  APOPACKO                           
           ELSE                                                                 
               MOVE ASC-KEY-PO-LNE-NBR   TO  PV-PO-LNE-NBR                      
               MOVE PV-PO-LNE-NBR        TO  APOLINEO                           
               MOVE '0000'               TO  APOPACKO                           
           END-IF.                                                              
                                                                                
           MOVE ASC-TEXT1                TO  ATEXT1O.                           
           MOVE ASC-TEXT2                TO  ATEXT2O.                           
           MOVE ASC-TEXT3                TO  ATEXT3O.                           
           MOVE ASC-TEXT4                TO  ATEXT4O.                           
           MOVE ASC-TEXT5                TO  ATEXT5O.                           
                                                                                
           MOVE ASC-NEW-PLAN-IND         TO  ANEWPLNO.                          
           MOVE ASC-PRORATED-ALLOC-IND   TO  APROALCO.                          
           MOVE ASC-RTV-IND              TO  ARTVINDO.                          
                                                                                
           SET DP030-SET-CURSOR-APPL-1   TO  TRUE.                              
           IF  NOT PS-ERROR                                                     
               MOVE -1                   TO  ATEXT1L                            
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    FOR THE REPSOND KEY, ONLY FREEFORM TEXT MAY BE ENTERED.     *        
      *    AT LEAST ONE LINE MUST BE ENTERED.                          *        
      *----------------------------------------------------------------*        
                                                                                
       5000-VALIDATE.                                                           
                                                                                
           IF DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID) OR                          
                  DP020-FK-LOCAL-FUNC-04(DP020-SRC-AID)                         
                       PERFORM 5001-RESPOND-EDITS                               
           ELSE                                                                 
           IF DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID) OR                          
              DP020-FK-LOCAL-FUNC-05(DP020-SRC-AID)                             
                       PERFORM 5002-RESOLVE-EDITS                               
           END-IF                                                               
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
      /                                                                         
       5001-RESPOND-EDITS.                                                      
           EVALUATE ASC-TRBL-RSN-CDE                                            
               WHEN 502                                                         
                   IF ASC-MERCHANT-ACCESS                                       
                      PERFORM 5000I-TEXT-VS-RTV                                 
                      PERFORM 5000B-PRO-MUST-BE-SPACE                           
                      PERFORM 5000C-ALLOC-MUST-BE-SPACE                         
                   ELSE                                                         
                      PERFORM 5000A-RTV-MUST-BE-SPACE                           
                      PERFORM 5000B-PRO-MUST-BE-SPACE                           
                      PERFORM 5000C-ALLOC-MUST-BE-SPACE                         
                      PERFORM 5000H-TEXT-REQUIRED                               
                   END-IF                                                       
               WHEN 509                                                         
                   IF ASC-MERCHANT-ACCESS                                       
                      PERFORM 5000I-TEXT-VS-RTV                                 
                      PERFORM 5000B-PRO-MUST-BE-SPACE                           
                      PERFORM 5000C-ALLOC-MUST-BE-SPACE                         
                   ELSE                                                         
                      PERFORM 5000A-RTV-MUST-BE-SPACE                           
                      PERFORM 5000B-PRO-MUST-BE-SPACE                           
                      PERFORM 5000C-ALLOC-MUST-BE-SPACE                         
                      PERFORM 5000H-TEXT-REQUIRED                               
                   END-IF                                                       
               WHEN 511                                                         
               WHEN 571                                                         
                      PERFORM 5000B-PRO-MUST-BE-SPACE                           
                      PERFORM 5000C-ALLOC-MUST-BE-SPACE                         
                      PERFORM 5000H-TEXT-REQUIRED                               
               WHEN OTHER                                                       
                      PERFORM 5000A-RTV-MUST-BE-SPACE                           
                      PERFORM 5000B-PRO-MUST-BE-SPACE                           
                      PERFORM 5000C-ALLOC-MUST-BE-SPACE                         
                      PERFORM 5000H-TEXT-REQUIRED                               
           END-EVALUATE.                                                        
                                                                                
       5002-RESOLVE-EDITS.                                                      
           EVALUATE ASC-TRBL-RSN-CDE                                            
               WHEN 502                                                         
                   IF DP020-FK-LOCAL-FUNC-05(DP020-SRC-AID)                     
                       SET PS-ERROR                                             
                           DP020-MSG-FATAL       TO  TRUE                       
                           MOVE -1               TO  ATEXT1L                    
                           MOVE PC-TSYMSG-01066  TO  DP020-MSG-NUMBER           
                   ELSE                                                         
                       IF ASC-MERCHANT-ACCESS                                   
                          PERFORM 5000AAA-RTV-MUST-BE-N                         
                          IF PS-NO-ERROR                                        
                              PERFORM 5000B-PRO-MUST-BE-SPACE                   
                              PERFORM 5000C-ALLOC-MUST-BE-SPACE                 
                              PERFORM 5000D-TEXT-MUST-BE-SPACE                  
                          END-IF                                                
                       ELSE                                                     
                          PERFORM 5000A-RTV-MUST-BE-SPACE                       
                          PERFORM 5000B-PRO-MUST-BE-SPACE                       
                          PERFORM 5000C-ALLOC-MUST-BE-SPACE                     
                       END-IF                                                   
                   END-IF                                                       
               WHEN 509                                                         
                   IF DP020-FK-LOCAL-FUNC-05(DP020-SRC-AID)                     
                       SET PS-ERROR                                             
                           DP020-MSG-FATAL       TO  TRUE                       
                           MOVE -1               TO  ATEXT1L                    
                           MOVE PC-TSYMSG-01066  TO  DP020-MSG-NUMBER           
                   ELSE                                                         
                       IF ASC-MERCHANT-ACCESS                                   
                          PERFORM 5000AAA-RTV-MUST-BE-N                         
                          PERFORM 5000B-PRO-MUST-BE-SPACE                       
                          PERFORM 5000C-ALLOC-MUST-BE-SPACE                     
                          PERFORM 5000D-TEXT-MUST-BE-SPACE                      
                       ELSE                                                     
                          PERFORM 5000A-RTV-MUST-BE-SPACE                       
                          PERFORM 5000B-PRO-MUST-BE-SPACE                       
                          PERFORM 5000C-ALLOC-MUST-BE-SPACE                     
                       END-IF                                                   
                   END-IF                                                       
               WHEN 511                                                         
               WHEN 571                                                         
                      PERFORM 5000A-RTV-MUST-BE-SPACE                           
                      IF ASC-MERCHANT-ACCESS                                    
                         IF ASC-PRORATED-ALLOC-IND EQUAL SPACE                  
                             PERFORM 5000G-VALIDATE-ALLOC                       
                         END-IF                                                 
                         IF PS-NO-ERROR                                         
                             PERFORM 5000J-PRO-VS-ALLOC                         
                         END-IF                                                 
                         PERFORM 5000D-TEXT-MUST-BE-SPACE                       
                      ELSE                                                      
                          PERFORM 5000B-PRO-MUST-BE-SPACE                       
                          PERFORM 5000C-ALLOC-MUST-BE-SPACE                     
                          PERFORM 5000H-TEXT-REQUIRED                           
                      END-IF                                                    
               WHEN 133                                                         
                   IF ASC-MGMT-AUTHORITY                                        
                       CONTINUE                                                 
                   ELSE                                                         
                      SET PS-ERROR                                              
                          DP020-MSG-FATAL   TO  TRUE                            
                      MOVE PC-TSYMSG-01061  TO  DP020-MSG-NUMBER                
                      MOVE -1               TO  ATEXT1L                         
                   END-IF                                                       
               WHEN 532                                                         
               WHEN 533                                                         
                   IF ASC-PBS-RESOLVE-YES                                       
                       CONTINUE                                                 
                   ELSE                                                         
                      SET PS-ERROR                                              
                          DP020-MSG-FATAL   TO  TRUE                            
                      MOVE PC-TSYMSG-01061  TO  DP020-MSG-NUMBER                
                      MOVE -1               TO  ATEXT1L                         
                   END-IF                                                       
               WHEN 509                                                         
                   IF DP020-FK-LOCAL-FUNC-05(DP020-SRC-AID)                     
                       SET PS-ERROR                                             
                           DP020-MSG-FATAL       TO  TRUE                       
                           MOVE -1               TO  ATEXT1L                    
                           MOVE PC-TSYMSG-01066  TO  DP020-MSG-NUMBER           
                   ELSE                                                         
                       IF ASC-MERCHANT-ACCESS                                   
                          PERFORM 5000AAA-RTV-MUST-BE-N                         
                          PERFORM 5000B-PRO-MUST-BE-SPACE                       
                          PERFORM 5000C-ALLOC-MUST-BE-SPACE                     
                          PERFORM 5000D-TEXT-MUST-BE-SPACE                      
                       ELSE                                                     
                          PERFORM 5000A-RTV-MUST-BE-SPACE                       
                          PERFORM 5000B-PRO-MUST-BE-SPACE                       
                          PERFORM 5000C-ALLOC-MUST-BE-SPACE                     
                       END-IF                                                   
                   END-IF                                                       
               WHEN 801                                                         
               WHEN 802                                                         
               WHEN 803                                                         
               WHEN 804                                                         
               WHEN 805                                                         
               WHEN 806                                                         
               WHEN 371                                                         
                   IF DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID) OR                  
                      DP020-FK-LOCAL-FUNC-05(DP020-SRC-AID)                     
                      SET PS-ERROR                                              
                          DP020-MSG-FATAL     TO  TRUE                          
                          MOVE -1             TO  ATEXT1L                       
                      IF ASC-TRBL-RSN-CDE = 803                                 
                         MOVE PC-TSYMSG-03036 TO  DP020-MSG-NUMBER              
                      ELSE                                                      
                         IF (ASC-TRBL-RSN-CDE = 804 OR                          
                             ASC-TRBL-RSN-CDE = 805 OR                          
                             ASC-TRBL-RSN-CDE = 806)                            
                            MOVE PC-TSYMSG-03216 TO  DP020-MSG-NUMBER           
                         ELSE                                                   
                            IF ASC-TRBL-RSN-CDE = 371                           
                               MOVE PC-TSYMSG-03248 TO  DP020-MSG-NUMBER        
                            ELSE                                                
                               MOVE PC-TSYMSG-02871 TO  DP020-MSG-NUMBER        
                            END-IF                                              
                         END-IF                                                 
                      END-IF                                                    
                   ELSE                                                         
                       IF ASC-MERCHANT-ACCESS                                   
                          PERFORM 5000AAA-RTV-MUST-BE-N                         
                          IF PS-NO-ERROR                                        
                              PERFORM 5000B-PRO-MUST-BE-SPACE                   
                              PERFORM 5000C-ALLOC-MUST-BE-SPACE                 
                              PERFORM 5000D-TEXT-MUST-BE-SPACE                  
                          END-IF                                                
                       ELSE                                                     
                          PERFORM 5000A-RTV-MUST-BE-SPACE                       
                          PERFORM 5000B-PRO-MUST-BE-SPACE                       
                          PERFORM 5000C-ALLOC-MUST-BE-SPACE                     
                       END-IF                                                   
                   END-IF                                                       
               WHEN OTHER                                                       
                      PERFORM 5000A-RTV-MUST-BE-SPACE                           
                      PERFORM 5000J-PRO-VS-ALLOC                                
                   IF PS-NO-ERROR                                               
                      PERFORM 5000K-PRO-ALLOC-VS-TEXT                           
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
       5000A-RTV-MUST-BE-SPACE.                                                 
                                                                                
           IF ASC-RTV-IND = SPACE                                               
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL   TO  TRUE                                   
               MOVE PC-TSYMSG-01026  TO  DP020-MSG-NUMBER                       
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                     TO ARTVINDA                                
               MOVE DP015-RED        TO  ARTVINDC                               
               MOVE DP015-REVERSE    TO  ARTVINDH                               
               MOVE -1               TO  ARTVINDL                               
           END-IF.                                                              
                                                                                
       5000AA-RTV-MUST-BE-Y.                                                    
                                                                                
           IF ASC-RTV-IND = PC-Y                                                
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL   TO  TRUE                                   
               MOVE PC-TSYMSG-01026  TO  DP020-MSG-NUMBER                       
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                     TO ARTVINDA                                
               MOVE DP015-RED        TO  ARTVINDC                               
               MOVE DP015-REVERSE    TO  ARTVINDH                               
               MOVE -1               TO  ARTVINDL                               
           END-IF.                                                              
                                                                                
       5000AAA-RTV-MUST-BE-N.                                                   
                                                                                
           EVALUATE TRUE                                                        
           WHEN ASC-RTV-IND = PC-N                                              
               CONTINUE                                                         
           WHEN ASC-RTV-IND = PC-Y                                              
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL   TO  TRUE                                   
               MOVE PC-TSYMSG-01041  TO  DP020-MSG-NUMBER                       
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                     TO ARTVINDA                                
               MOVE DP015-RED        TO  ARTVINDC                               
               MOVE DP015-REVERSE    TO  ARTVINDH                               
               MOVE -1               TO  ARTVINDL                               
           WHEN OTHER                                                           
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL   TO  TRUE                                   
               MOVE PC-TSYMSG-01026  TO  DP020-MSG-NUMBER                       
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                     TO ARTVINDA                                
               MOVE DP015-RED        TO  ARTVINDC                               
               MOVE DP015-REVERSE    TO  ARTVINDH                               
               MOVE -1               TO  ARTVINDL                               
           END-EVALUATE.                                                        
                                                                                
       5000B-PRO-MUST-BE-SPACE.                                                 
           IF  ASC-PRORATED-ALLOC-IND = SPACE                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-01026      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  APROALCA                           
               MOVE DP015-RED            TO  APROALCC                           
               MOVE DP015-REVERSE        TO  APROALCH                           
               MOVE -1                   TO  APROALCL                           
           END-IF.                                                              
                                                                                
       5000C-ALLOC-MUST-BE-SPACE.                                               
           IF  ASC-NEW-PLAN-IND = SPACE                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-01026      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ANEWPLNA                           
               MOVE DP015-RED            TO  ANEWPLNC                           
               MOVE DP015-REVERSE        TO  ANEWPLNH                           
               MOVE -1                   TO  ANEWPLNL                           
           END-IF.                                                              
                                                                                
       5000D-TEXT-MUST-BE-SPACE.                                                
                                                                                
           IF  ASC-TEXT1 GREATER THAN SPACES                                    
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-01026      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ATEXT1A                            
               MOVE DP015-RED            TO  ATEXT1C                            
               MOVE DP015-REVERSE        TO  ATEXT1H                            
               MOVE -1                   TO  ATEXT1L                            
           END-IF.                                                              
                                                                                
           IF  ASC-TEXT2 GREATER THAN SPACES                                    
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-01026      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ATEXT2A                            
               MOVE DP015-RED            TO  ATEXT2C                            
               MOVE DP015-REVERSE        TO  ATEXT2H                            
               MOVE -1                   TO  ATEXT2L                            
           END-IF.                                                              
                                                                                
           IF  ASC-TEXT3 GREATER THAN SPACES                                    
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-01026      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ATEXT3A                            
               MOVE DP015-RED            TO  ATEXT3C                            
               MOVE DP015-REVERSE        TO  ATEXT3H                            
               MOVE -1                   TO  ATEXT3L                            
           END-IF.                                                              
                                                                                
           IF  ASC-TEXT4 GREATER THAN SPACES                                    
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-01026      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ATEXT4A                            
               MOVE DP015-RED            TO  ATEXT4C                            
               MOVE DP015-REVERSE        TO  ATEXT4H                            
               MOVE -1                   TO  ATEXT4L                            
           END-IF.                                                              
                                                                                
           IF  ASC-TEXT5 GREATER THAN SPACES                                    
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-01026      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ATEXT5A                            
               MOVE DP015-RED            TO  ATEXT5C                            
               MOVE DP015-REVERSE        TO  ATEXT5H                            
               MOVE -1                   TO  ATEXT5L                            
           END-IF.                                                              
                                                                                
       5000F-VALIDATE-PRORATE.                                                  
           IF  ASC-PRORATED-ALLOC-IND = PC-Y                                    
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-01026      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  APROALCA                           
               MOVE DP015-RED            TO  APROALCC                           
               MOVE DP015-REVERSE        TO  APROALCH                           
               MOVE -1                   TO  APROALCL                           
           END-IF.                                                              
                                                                                
       5000G-VALIDATE-ALLOC.                                                    
           IF  ASC-NEW-PLAN-IND = PC-Y                                          
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-01026      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ANEWPLNA                           
               MOVE DP015-RED            TO  ANEWPLNC                           
               MOVE DP015-REVERSE        TO  ANEWPLNH                           
               MOVE -1                   TO  ANEWPLNL                           
           END-IF.                                                              
                                                                                
       5000H-TEXT-REQUIRED.                                                     
                                                                                
           IF  ASC-TEXT1 = SPACES                                               
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-00007      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ATEXT1A                            
               MOVE DP015-RED            TO  ATEXT1C                            
               MOVE DP015-REVERSE        TO  ATEXT1H                            
               MOVE -1                   TO  ATEXT1L                            
           END-IF.                                                              
                                                                                
       5000I-TEXT-VS-RTV.                                                       
                                                                                
           IF  ASC-TEXT1 = SPACES                                               
               IF ASC-RTV-IND = PC-Y                                            
                   CONTINUE                                                     
               ELSE                                                             
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO  TRUE                               
                   MOVE PC-TSYMSG-01026  TO  DP020-MSG-NUMBER                   
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO  ATEXT1A                            
                                             ARTVINDA                           
                   MOVE DP015-RED        TO  ATEXT1C                            
                                             ARTVINDC                           
                   MOVE DP015-REVERSE    TO  ATEXT1H                            
                                             ARTVINDH                           
                   MOVE -1               TO  ATEXT1L                            
                                             ARTVINDL                           
               END-IF                                                           
           ELSE                                                                 
               IF ASC-RTV-IND = SPACE                                           
                   CONTINUE                                                     
               ELSE                                                             
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO  TRUE                               
                   MOVE PC-TSYMSG-00973  TO  DP020-MSG-NUMBER                   
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO  ATEXT1A                            
                                             ARTVINDA                           
                   MOVE DP015-RED        TO  ATEXT1C                            
                                             ARTVINDC                           
                   MOVE DP015-REVERSE    TO  ATEXT1H                            
                                             ARTVINDH                           
                   MOVE -1               TO  ATEXT1L                            
                                             ARTVINDL                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
       5000J-PRO-VS-ALLOC.                                                      
           IF  ASC-PRORATED-ALLOC-IND = PC-Y                                    
                                 AND ASC-NEW-PLAN-IND = PC-Y                    
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-00973      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  APROALCA                           
                                             ANEWPLNA                           
               MOVE DP015-RED            TO  APROALCC                           
                                             ANEWPLNC                           
               MOVE DP015-REVERSE        TO  APROALCH                           
                                             ANEWPLNH                           
               MOVE -1                   TO  ANEWPLNL                           
           END-IF.                                                              
                                                                                
       5000K-PRO-ALLOC-VS-TEXT.                                                 
           IF  ASC-PRORATED-ALLOC-IND = PC-Y                                    
                                 OR ASC-NEW-PLAN-IND = PC-Y                     
               IF ASC-TEXT1 > SPACES                                            
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO TRUE                                
                   MOVE PC-TSYMSG-01026  TO DP020-MSG-NUMBER                    
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO ATEXT1A                             
                   MOVE DP015-RED        TO ATEXT1C                             
                   MOVE DP015-REVERSE    TO ATEXT1C                             
                   MOVE -1               TO ATEXT1L                             
               END-IF                                                           
           ELSE                                                                 
               IF ASC-TEXT1 = SPACES                                            
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO  TRUE                               
                   MOVE PC-TSYMSG-00007  TO  DP020-MSG-NUMBER                   
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                             TO ATEXT1A                         
                   MOVE DP015-RED        TO  ATEXT1C                            
                   MOVE DP015-REVERSE    TO  ATEXT1H                            
                   MOVE -1               TO  ATEXT1L                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
      /                                                                         
       5010-FETCH-VEND-SHIP-DTE.                                                
           INITIALIZE DCLTSHIPMT.                                               
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO OR +100))                           
      *                                                                         
               EXEC SQL                                                         
                    SELECT VEND_SHIP_DTE                                        
                      INTO :SHIPMT-VEND-SHIP-DTE                                
                      FROM TSHPTPO A                                            
                          , TSHIPMT B                                           
                     WHERE A.PO_NBR         = :ASC-KEY-PO-NBR                   
                       AND A.REC_SEQ_NBR    = :ASC-KEY-REC-SEQ-NBR              
                       AND A.SHIPT_ID       = B.SHIPT_ID                        
                       AND A.PRIM_SHIPT_IND = :PC-Y                             
                                                                                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                   WHEN SQLCODE = +100                                          
                        MOVE PC-DUMMY-DATE                                      
                                          TO SHIPMT-VEND-SHIP-DTE               
                   WHEN SQLWARN0 NOT = SPACE                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '5010-FETCH-VEND-SHIP-DTE'                         
                                         TO DP013-PARAGRAPH                     
                        MOVE 'FETCH VENDOR SHIP DATE'                           
                                         TO DP013-MESSAGE-TEXT (1)              
                        MOVE 'FOR THE FOLLOWING PO-RCVR SEQ:  '                 
                                         TO DP013-MESSAGE-TEXT (2)              
                        MOVE ASC-KEY-PO-NBR TO PV-ERROR-PO-NBR                  
                        MOVE ASC-KEY-REC-SEQ-NBR                                
                                         TO PV-ERROR-REC-SEQ-NBR                
                        STRING PV-ERROR-PO-NBR      DELIMITED BY SIZE           
                               '-'                  DELIMITED BY SIZE           
                               PV-ERROR-REC-SEQ-NBR DELIMITED BY SIZE           
                                       INTO DP013-MESSAGE-TEXT (3)              
                        MOVE SQLCA   TO DP013-SQLCA                             
                        MOVE 'TSHPTPO ' TO DP013-DB2-TABLE-NAME (1)             
                        MOVE 'TSHIPMT ' TO DP013-DB2-TABLE-NAME (2)             
                        SET DP013-DB2-ABEND                                     
                                         TO TRUE                                
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5010-FETCH-VEND-SHIP-DTE'                                  
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
                    MOVE 'TSHPTPO '  TO DP013-DB2-TABLE-NAME (1)                
                    MOVE 'TSHIPMT '  TO DP013-DB2-TABLE-NAME (2)                
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    GET THE RESOLUTION INDICATOR FOR THE SPECIFIED TROUBLE      *        
      *    REASON.  THIS INDICATOR TELLS US IF THIS TROUBLE CAN BE     *        
      *    RESOLVED MANUALLY OR AUTOMATICALLY.  WE ONLY ALLOW TROUBLE  *        
      *    NOTIFICATIONS TO BE RESOLVED MANUALLY IN THIS PROGRAM.      *        
      *----------------------------------------------------------------*        
                                                                                
       5025-GET-TRBL-RSN-RESOLVE-IND.                                           
                                                                                
           INITIALIZE DCLTTRBRSN.                                               
                                                                                
           EXEC SQL                                                             
                SELECT RESP_RES_IND                                             
                      ,AUTOACKNL_IND                                            
               INTO   :TRBRSN-RESP-RES-IND                                      
                     ,:TRBRSN-AUTOACKNL-IND                                     
               FROM    TTRBRSN                                                  
               WHERE                                                            
                       TRBL_RSN_CDE       =  :ASC-TRBL-RSN-CDE                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE NOT EQUAL ZERO                                      
                    MOVE '5025-GET-TRBL-RSN-RESOLVE-IND'                        
                                         TO DP013-PARAGRAPH                     
                    MOVE 'SELECT THE TROUBLE REASON RESOLVE IND'                
                                         TO DP013-MESSAGE-TEXT (1)              
                    MOVE SQLCA           TO DP013-SQLCA                         
                    MOVE 'TTRBRSN'       TO DP013-DB2-TABLE-NAME (1)            
                    SET DP013-DB2-ABEND                                         
                       DP013-XCTL-DISPLAY-RESTART                               
                                         TO TRUE                                
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
      /                                                                         
      *----------------------------------------------------------------*        
      *    FOR THE UPDATE KEY, ONLY THE DEPARTMENT MAY BE ENTERED.     *        
      *----------------------------------------------------------------*        
                                                                                
       5030-VALIDATE-FOR-UPDATE.                                                
                                                                                
           IF  ASC-PRORATED-ALLOC-IND EQUAL PC-Y                                
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-00176      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  APROALCA                           
               MOVE DP015-RED            TO  APROALCC                           
               MOVE DP015-REVERSE        TO  APROALCH                           
               MOVE -1                   TO  APROALCL                           
           END-IF.                                                              
                                                                                
           IF  ASC-NEW-PLAN-IND EQUAL PC-Y                                      
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-00176      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ANEWPLNA                           
               MOVE DP015-RED            TO  ANEWPLNC                           
               MOVE DP015-REVERSE        TO  ANEWPLNH                           
               MOVE -1                   TO  ANEWPLNL                           
           END-IF.                                                              
                                                                                
           IF  ASC-TEXT1 GREATER THAN SPACES                                    
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-01026      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ATEXT1A                            
               MOVE DP015-RED            TO  ATEXT1C                            
               MOVE DP015-REVERSE        TO  ATEXT1H                            
               MOVE -1                   TO  ATEXT1L                            
           END-IF.                                                              
                                                                                
           IF  ASC-TEXT2 GREATER THAN SPACES                                    
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-00005      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ATEXT2A                            
               MOVE DP015-RED            TO  ATEXT2C                            
               MOVE DP015-REVERSE        TO  ATEXT2H                            
               MOVE -1                   TO  ATEXT2L                            
           END-IF.                                                              
                                                                                
           IF  ASC-TEXT3 GREATER THAN SPACES                                    
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-00005      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ATEXT3A                            
               MOVE DP015-RED            TO  ATEXT3C                            
               MOVE DP015-REVERSE        TO  ATEXT3H                            
               MOVE -1                   TO  ATEXT3L                            
           END-IF.                                                              
                                                                                
           IF  ASC-TEXT4 GREATER THAN SPACES                                    
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-00005      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ATEXT4A                            
               MOVE DP015-RED            TO  ATEXT4C                            
               MOVE DP015-REVERSE        TO  ATEXT4H                            
               MOVE -1                   TO  ATEXT4L                            
           END-IF.                                                              
                                                                                
           IF  ASC-TEXT5 GREATER THAN SPACES                                    
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-00005      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ATEXT5A                            
               MOVE DP015-RED            TO  ATEXT5C                            
               MOVE DP015-REVERSE        TO  ATEXT5H                            
               MOVE -1                   TO  ATEXT5L                            
           END-IF.                                                              
                                                                                
           IF  ASC-DEPT-NBR-CHGD                                                
               IF  ASC-TRBL-RSN-CDE = PC-TRBL-RSN-PO-NOT-ON-FILE                
                   CONTINUE                                                     
               ELSE                                                             
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO  TRUE                               
                   MOVE PC-TSYMSG-01027  TO  DP020-MSG-NUMBER                   
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO  ADEPTA                             
                   MOVE DP015-RED        TO  ADEPTC                             
                   MOVE DP015-REVERSE    TO  ADEPTH                             
                   MOVE -1               TO  ADEPTL                             
               END-IF                                                           
           ELSE                                                                 
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO  TRUE                               
               MOVE PC-TSYMSG-00046      TO  DP020-MSG-NUMBER                   
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO  ADEPTA                             
               MOVE DP015-RED            TO  ADEPTC                             
               MOVE DP015-REVERSE        TO  ADEPTH                             
               MOVE -1                   TO  ADEPTL                             
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    NOW PREPARE THE COMM AREA FOR TMKCS520, THE TROUBLE CENTRAL *        
      *    PROCESSING PROGRAM.                                         *        
      *----------------------------------------------------------------*        
                                                                                
       5050-UPDATE-PROCESS.                                                     
                                                                                
           IF ASC-KEY-PO-LNE-NBR GREATER THAN ZERO                              
               SET TM500-LINE-LEVEL      TO  TRUE                               
               MOVE 0                    TO  TM500-PREPK-ID                     
           ELSE                                                                 
               IF ASC-KEY-PREPK-ID GREATER THAN ZERO                            
                   SET TM500-PACK-LEVEL  TO  TRUE                               
                   MOVE ASC-KEY-PREPK-ID TO  TM500-PREPK-ID                     
               ELSE                                                             
                   SET TM500-PO-LEVEL    TO  TRUE                               
                   MOVE 0                TO  TM500-PREPK-ID                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE ASC-KEY-NOTIF-NBR        TO  TM500-NOTIF-NBR.                   
           MOVE ASC-KEY-OPER-UNT-ID      TO  TM500-OPER-UNT-ID.                 
           MOVE ASC-KEY-FCLTY-ID         TO  TM500-FCLTY-ID.                    
                                                                                
           MOVE ASC-KEY-PO-NBR           TO  TM500-PO-NBR.                      
           MOVE ASC-KEY-PO-LNE-NBR       TO  TM500-PO-LNE-NBR.                  
           MOVE ASC-KEY-REC-SEQ-NBR      TO  TM500-REC-SEQ-NBR.                 
           MOVE ASC-TRBL-RSN-CDE         TO  TM500-TRBL-RSN-CDE.                
                                                                                
           MOVE DP020-USERID             TO  TM500-NOTIF-UID                    
                                             TM500-CRE-UID                      
                                             TM500-RESOLVED-UID                 
                                             TM500-ACKNOWLG-UID.                
                                                                                
           STRING ASC-TEXT1                                                     
                  ASC-TEXT2                                                     
                  ASC-TEXT3                                                     
                  ASC-TEXT4                                                     
                  ASC-TEXT5 DELIMITED BY SIZE                                   
                                       INTO  TM500-FREEFORM-TEXT.               
                                                                                
      **   IF NEW ALLOCATION OR PRORATED ALLOCATION HAS BEEN INDICATED,         
      **   AUTOMATICALLY CREATE DETAILS.                                        
                                                                                
           IF ASC-NEW-PLAN-IND EQUAL PC-Y                                       
               SET TM500-ADD-DETAILS                                            
                   TM500-REDISTRO-YES    TO TRUE                                
               MOVE 'USE NEW ALLOCATION' TO TM500-FREEFORM-TEXT                 
           ELSE                                                                 
              IF ASC-PRORATED-ALLOC-IND EQUAL PC-Y                              
                  SET TM500-ADD-DETAILS  TO TRUE                                
                  MOVE 'USE PRORATED ALLOCATION'                                
                                         TO TM500-FREEFORM-TEXT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE ASC-RTV-IND              TO TM500-RTV-SW                        
           MOVE ASC-PRORATED-ALLOC-IND   TO TM500-PRORATE-IND                   
           MOVE ASC-RTV-ACCESS-SW        TO TM500-RTV-ACCESS-SW                 
                                                                                
           IF (ASC-ENTERED-FROM-PO-LIST) AND                                    
              (TM500-LINE-LEVEL OR TM500-PACK-LEVEL)                            
               SET PS-NOT-TRBNTF-CSR-EOF TO TRUE                                
               PERFORM 5100-OPEN-TRBNTF-CSR                                     
               PERFORM 5110-RESOLVE-TRBL-FOR-REASON                             
                   UNTIL (PS-TRBNTF-CSR-EOF)  OR                                
                         (PS-ERROR)                                             
               IF  PS-NO-ERROR                                                  
                   PERFORM 5130-CLOSE-TRBNTF-CSR                                
               END-IF                                                           
           ELSE                                                                 
               PERFORM 5200-LINK-TO-UPDATE                                      
                                                                                
               EVALUATE TRUE                                                    
                   WHEN TM500-CALL-SUCCESSFUL                                   
                       IF (TM500-RESOLVE-AUTO OR TM500-RESOLVE-MANUAL)          
                         AND (TM500-TRBL-RSN-CDE = PC-267 OR PC-268)            
                           MOVE ASC-KEY-PREPK-ID TO TM500-PREPK-ID              
                           MOVE TM500-FIELDS TO PV-HOLD-TM500-FIELDS            
                           PERFORM 6000-CHECK-FOR-TROUBLE                       
                           MOVE PV-HOLD-TM500-FIELDS TO TM500-FIELDS            
                       END-IF                                                   
                                                                                
                   WHEN TM500-TPURCHS-SELECT-NOT-FND                            
                   WHEN TM500-TTRBNTF-UPDATE-NOT-FND                            
                       CONTINUE                                                 
                   WHEN TM500-DUPLICATE-TROUBLE                                 
                       CONTINUE                                                 
                   WHEN TM500-IN-PROC-UPDATE-NOT-FND                            
                   WHEN TM500-RESPOND-UPDATE-NOT-FND                            
                   WHEN TM500-ACKNWLG-UPDATE-NOT-FND                            
                   WHEN TM500-RESOLVE-UPDATE-NOT-FND                            
                       MOVE PC-TSYMSG-00938 TO DP020-MSG-NUMBER                 
                       MOVE -1                   TO  ATEXT1L                    
                       SET PS-ERROR                                             
                           DP020-MSG-FATAL       TO TRUE                        
                   WHEN TM500-SHIPPING-WINDOW-ERROR                             
                       SET PS-ERROR                                             
                           DP020-MSG-FATAL TO TRUE                              
                       MOVE PC-TSYMSG-01068 TO DP020-MSG-NUMBER                 
                       MOVE -1           TO  ATEXT1L                            
                   WHEN TM500-BUYER-ACTION-PENDING                              
                       SET PS-ERROR                                             
                           DP020-MSG-FATAL TO TRUE                              
                       MOVE PC-TSYMSG-01069 TO DP020-MSG-NUMBER                 
                       MOVE -1           TO  ATEXT1L                            
                   WHEN OTHER                                                   
                       PERFORM 5300-FORMAT-ERROR                                
               END-EVALUATE                                                     
           END-IF.                                                              
      /                                                                         
      ******************************************************************        
      * READ THROUGH THE SELECTED-ITEMS TEMP STORAGE QUEUE TO RETRIEVE *        
      * THE TROUBLE NOTIFICATION RECORDS. FOR EACH NOTIFICATION RECORD *        
      * ON THE QUEUE,  IF IT IS FOR THE SAME TROUBLE REASON, EITHER    *        
      * RESPOND OR RESOLVE THE NOTIFICATION WITH THE INFORMATION       *        
      * ENTERED ON THE SCREEN.  IF IT NOT, SEND OUT AN APPROPRIATE     *        
      * ERROR MESSAGE.  THIS PARAGRAPH IS PERFORMED UNTIL THE END OF   *        
      * THE TEMP STORAGE QUEUE IS REACHED OR AN ERROR IS ENCOUNTERED,  *        
      * WHICHEVER COMES FIRST.                                         *        
      ******************************************************************        
       5060-MULT-UPDATE-PROCESS.                                                
                                                                                
           ADD +1                                TO ASC-SEL-SUB.                
                                                                                
           INITIALIZE TM500-FIELDS                                              
           IF  ASC-MULT-RESPOND-REQUEST                                         
               SET TM500-RESPOND                                                
                   TM500-ADD-DETAILS                                            
                                     TO TRUE                                    
           ELSE                                                                 
               SET TM500-RESOLVE-MANUAL                                         
                                     TO TRUE                                    
                                                                                
               IF ASC-TRBL-RSN-CDE = PC-502 OR PC-509                           
      *KBW     IF ASC-TRBL-RSN-CDE = PC-502                                     
                  SET TM500-ADD-DETAILS                                         
                                     TO TRUE                                    
               ELSE                                                             
                  IF  ASC-TEXT1 GREATER THAN SPACES                             
                        SET TM500-ADD-DETAILS                                   
                                     TO TRUE                                    
                  ELSE                                                          
                        SET TM500-NO-DETAILS                                    
                                     TO TRUE                                    
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ASC-SEL-SUB > TM002-MAX-ITEMS                                    
               ADD +1                            TO ASC-SEL-ITEM                
               MOVE +1                           TO ASC-SEL-SUB                 
               IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                    
                   IF  PS-FIRST-TIME                                            
                       INITIALIZE TM002-SELECTED-ITEM-ARRAY                     
                       PERFORM 9100-READ-SEL-QUEUE                              
                   ELSE                                                         
                       SUBTRACT +1             FROM ASC-SEL-ITEM                
                       PERFORM 9200-REWRITE-SEL-QUEUE                           
                       ADD +1                    TO ASC-SEL-ITEM                
                       INITIALIZE TM002-SELECTED-ITEM-ARRAY                     
                       PERFORM 9100-READ-SEL-QUEUE                              
                   END-IF                                                       
               ELSE                                                             
                   INITIALIZE TM002-SELECTED-ITEM-ARRAY                         
                   SET PS-END-OF-SEL-ITEMS-QUEUE TO TRUE                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  TM002-ITEM (ASC-SEL-SUB) > ZERO                                  
               IF  PS-FIRST-TIME                                                
                   SET PS-NOT-FIRST-TIME TO TRUE                                
                   MOVE TM002-TRBL-RSN-CDE (ASC-SEL-SUB)                        
                        TO PV-PREV-TRBL-RSN-CDE                                 
               END-IF                                                           
                                                                                
               IF TM002-TRBL-RSN-CDE (ASC-SEL-SUB) =                            
                                                PV-PREV-TRBL-RSN-CDE AND        
                  PV-PREV-TRBL-RSN-CDE = ASC-TRBL-RSN-CDE                       
                   MOVE TM002-PREPK-ID (ASC-SEL-SUB) TO TM500-PREPK-ID          
                   IF ASC-ENTERED-FROM-PO-LIST                                  
                       SET TM500-PO-LEVEL       TO TRUE                         
                   ELSE                                                         
                       IF TM500-PREPK-ID > 0                                    
                           SET TM500-PACK-LEVEL TO TRUE                         
                       ELSE                                                     
                           SET TM500-LINE-LEVEL TO TRUE                         
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   MOVE TM002-NOTIF-NBR          (ASC-SEL-SUB)                  
                                            TO  TM500-NOTIF-NBR                 
                   MOVE TM002-OPER-UNT-ID        (ASC-SEL-SUB)                  
                                            TO  TM500-OPER-UNT-ID               
                   MOVE TM002-FCLTY-ID           (ASC-SEL-SUB)                  
                                            TO  TM500-FCLTY-ID                  
                                                                                
                   MOVE TM002-PO-NBR             (ASC-SEL-SUB)                  
                                            TO  TM500-PO-NBR                    
                   MOVE TM002-PO-LNE-NBR         (ASC-SEL-SUB)                  
                                            TO  TM500-PO-LNE-NBR                
                   MOVE TM002-RCVR-SEQ           (ASC-SEL-SUB)                  
                                            TO  TM500-REC-SEQ-NBR               
                   MOVE TM002-TRBL-RSN-CDE       (ASC-SEL-SUB)                  
                                            TO  TM500-TRBL-RSN-CDE              
                                                                                
                                                                                
                   MOVE DP020-USERID        TO  TM500-NOTIF-UID                 
                                                TM500-CRE-UID                   
                                                TM500-RESOLVED-UID              
                                                TM500-ACKNOWLG-UID              
                                                                                
                   STRING ASC-TEXT1                                             
                          ASC-TEXT2                                             
                          ASC-TEXT3                                             
                          ASC-TEXT4                                             
                          ASC-TEXT5 DELIMITED BY SIZE                           
                                          INTO  TM500-FREEFORM-TEXT             
                                                                                
      **   IF NEW ALLOCATION OR PRORATED ALLOCATION HAS BEEN INDICATED,         
      **   AUTOMATICALLY CREATE DETAILS.                                        
                                                                                
                   IF ASC-NEW-PLAN-IND EQUAL PC-Y                               
                       SET TM500-ADD-DETAILS                                    
                           TM500-REDISTRO-YES                                   
                                         TO TRUE                                
                       MOVE 'USE NEW ALLOCATION'                                
                                         TO TM500-FREEFORM-TEXT                 
                   ELSE                                                         
                      IF ASC-PRORATED-ALLOC-IND EQUAL PC-Y                      
                          SET TM500-ADD-DETAILS                                 
                                         TO TRUE                                
                          MOVE 'USE PRORATED ALLOCATION'                        
                                         TO TM500-FREEFORM-TEXT                 
                      END-IF                                                    
                   END-IF                                                       
                                                                                
                   MOVE ASC-RTV-IND      TO TM500-RTV-SW                        
                   MOVE ASC-PRORATED-ALLOC-IND                                  
                                         TO TM500-PRORATE-IND                   
                                                                                
                   IF TM500-TRBL-RSN-CDE = PC-509                               
                     AND TM500-RTV-SW = PC-N                                    
                       IF ASC-MULT-RESOLVE-REQUEST                              
                           SET TM500-RESOLVE-MANUAL                             
                                                  TO TRUE                       
                           PERFORM 5010-FETCH-VEND-SHIP-DTE                     
                           MOVE SHIPMT-VEND-SHIP-DTE                            
                                                 TO TM500-VEND-SHIP-DTE         
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   PERFORM 5200-LINK-TO-UPDATE                                  
                                                                                
                   EVALUATE TRUE                                                
                   WHEN TM500-CALL-SUCCESSFUL                                   
                       IF ASC-MULT-RESOLVE-REQUEST                              
                         AND (PV-PREV-TRBL-RSN-CDE = PC-267 OR PC-268)          
                           MOVE TM500-FIELDS TO PV-HOLD-TM500-FIELDS            
                           PERFORM 6000-CHECK-FOR-TROUBLE                       
                           MOVE PV-HOLD-TM500-FIELDS TO TM500-FIELDS            
                       END-IF                                                   
                       ADD 1                 TO PV-TRBL-RSLVD-COUNT             
                       PERFORM 9000-UPDATE-SELECTION-QUEUE                      
                   WHEN TM500-TPURCHS-SELECT-NOT-FND                            
                   WHEN TM500-DUPLICATE-TROUBLE                                 
                   WHEN TM500-TTRBNTF-UPDATE-NOT-FND                            
                       ADD 1                 TO PV-TRBL-RSLVD-COUNT             
                       PERFORM 9000-UPDATE-SELECTION-QUEUE                      
                   WHEN TM500-BUYER-ACTION-PENDING                              
                       SET PS-ERROR                                             
                               DP020-MSG-FATAL TO TRUE                          
                       MOVE PC-TSYMSG-01069 TO DP020-MSG-NUMBER                 
                       MOVE -1           TO  ATEXT1L                            
                   WHEN TM500-SHIPPING-WINDOW-ERROR                             
                       SET PS-ERROR                                             
                           DP020-MSG-FATAL TO TRUE                              
                       MOVE PC-TSYMSG-01068 TO DP020-MSG-NUMBER                 
                       MOVE -1           TO  ATEXT1L                            
                   WHEN TM500-RESOLVE-UPDATE-NOT-FND                            
                   WHEN TM500-RESPOND-UPDATE-NOT-FND                            
                       SET TM002-INQ (ASC-SEL-SUB)                              
                                             TO TRUE                            
                       PERFORM 9200-REWRITE-SEL-QUEUE                           
                   WHEN OTHER                                                   
                           PERFORM 5300-FORMAT-ERROR                            
                   END-EVALUATE                                                 
               ELSE                                                             
                   MOVE PC-TSYMSG-00939      TO  DP020-MSG-NUMBER               
                   SET TM002-ERR            (ASC-SEL-SUB)                       
                       PS-ERROR                                                 
                       DP020-NEXT-ACT-APPL-ERROR                                
                       DP020-MSG-FATAL       TO  TRUE                           
                   PERFORM 9200-REWRITE-SEL-QUEUE                               
               END-IF                                                           
           ELSE                                                                 
               SET PS-END-OF-SEL-ITEMS-QUEUE TO TRUE                            
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      * CHECK TO SEE IF THE TROUBLE THAT WAS JUST RESOLVED IS                   
      * TO BE AUTO ACKNOWLEDGED IF THE CONDITIONS THAT CAUSED THE               
      * TROUBLE IS TRULY STATISFIED.  THIS MAY INVOLVE ALSO                     
      * AUTOMATICALLY RE-DISTRO'ING THE ENTIRE RECEIVER IF THE                  
      * TROUBLE CALLS FOR THAT.                                                 
      *                                                                         
      *   WILL CALL MODULE TMKCS550 (WHICH IS BOTH BATCH AND CICS               
      *   DEFINED.                                                              
      * AS OF 1/10/05, THIS WOULD BE TROUBLE 222 - NO DISTRO                    
      *----------------------------------------------------------------*        
       5070-CHECK-AUTO-ACKNOW.                                                  
      *    CHECK TO SEE IF THE TROUBLE IS ELIGIABLE FOR AUTO ACKNOWLEDGE        
      *    PROCESS.                                                             
           MOVE ASC-KEY-PO-NBR            TO TM500-PO-NBR.                      
           MOVE ASC-KEY-REC-SEQ-NBR       TO TM500-REC-SEQ-NBR.                 
           MOVE ASC-KEY-OPER-UNT-ID       TO TM500-OPER-UNT-ID.                 
           MOVE ASC-TRBL-RSN-CDE          TO TM500-TRBL-RSN-CDE.                
           MOVE 1                         TO TM500-FCLTY-ID.                    
                                                                                
           MOVE DP020-USERID              TO  TM500-NOTIF-UID                   
                                              TM500-CRE-UID                     
                                              TM500-RESOLVED-UID                
                                              TM500-ACKNOWLG-UID.               
                                                                                
           STRING ASC-TEXT1                                                     
                  ASC-TEXT2                                                     
                  ASC-TEXT3                                                     
                  ASC-TEXT4                                                     
                  ASC-TEXT5 DELIMITED BY SIZE                                   
                                       INTO  TM500-FREEFORM-TEXT.               
                                                                                
      **   IF NEW ALLOCATION OR PRORATED ALLOCATION HAS BEEN INDICATED,         
      **   AUTOMATICALLY CREATE DETAILS.                                        
                                                                                
           IF ASC-NEW-PLAN-IND EQUAL PC-Y                                       
               SET TM500-ADD-DETAILS                                            
                   TM500-REDISTRO-YES    TO TRUE                                
               MOVE 'USE NEW ALLOCATION' TO TM500-FREEFORM-TEXT                 
           ELSE                                                                 
              IF ASC-PRORATED-ALLOC-IND EQUAL PC-Y                              
                  SET TM500-ADD-DETAILS  TO TRUE                                
                  MOVE 'USE PRORATED ALLOCATION'                                
                                         TO TM500-FREEFORM-TEXT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE ASC-RTV-IND              TO TM500-RTV-SW                        
           MOVE ASC-PRORATED-ALLOC-IND   TO TM500-PRORATE-IND                   
           MOVE ASC-RTV-ACCESS-SW        TO TM500-RTV-ACCESS-SW                 
                                                                                
           PERFORM  5400-LINK-TO-AUTO-ACKN                                      
                                                                                
                                                                                
           EVALUATE TRUE                                                        
      *       WHEN TM500-CALL-SUCCESSFUL                                        
      *            PERFORM 5071-DETERMINE-AUTO-ACK-MSG                          
      *       WHEN TM500-DB2-CONTENTION                                         
      *            PERFORM 9900-SOFT-ABORT                                      
              WHEN TM500-DB2-UNRECOVERABLE-ERROR                                
                   MOVE '5070-CHECK-AUTO-ACKNOW'                                
                                         TO DP013-PARAGRAPH                     
                   MOVE 'PERFORM AUTO ACKNOWLEDGE'                              
                                         TO DP013-MESSAGE-TEXT (1)              
                   MOVE SQLCA            TO DP013-SQLCA                         
                   MOVE 'TMKCS550'       TO DP013-DB2-TABLE-NAME (1)            
                   SET DP013-DB2-ABEND                                          
                       DP013-XCTL-DISPLAY-RESTART                               
                                         TO TRUE                                
                   PERFORM DP013-0000-PROCESS-ABEND                             
              WHEN TM500-IN-PROC-UPDATE-NOT-FND                                 
              WHEN TM500-RESPOND-UPDATE-NOT-FND                                 
              WHEN TM500-ACKNWLG-UPDATE-NOT-FND                                 
              WHEN TM500-RESOLVE-UPDATE-NOT-FND                                 
                   MOVE PC-TSYMSG-00938  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO  ATEXT1L                            
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO TRUE                                
              WHEN TM500-SHIPPING-WINDOW-ERROR                                  
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO TRUE                                
                   MOVE PC-TSYMSG-01068  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO  ATEXT1L                            
              WHEN TM500-BUYER-ACTION-PENDING                                   
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO TRUE                                
                   MOVE PC-TSYMSG-01069  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO  ATEXT1L                            
              WHEN OTHER                                                        
                   PERFORM 5071-DETERMINE-AUTO-ACK-MSG                          
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * DETERMINE WHAT MESSAGE TO DISPLAY TO THE USER ON-LINE                   
      *----------------------------------------------------------------*        
       5071-DETERMINE-AUTO-ACK-MSG.                                             
                                                                                
            IF TM500-AUTO-ACKNOW-ALRDY-DONE  OR                                 
               TM500-IN-PROC-UPDATE-NOT-FND  OR                                 
               TM500-RESPOND-UPDATE-NOT-FND  OR                                 
               TM500-ACKNWLG-UPDATE-NOT-FND  OR                                 
               TM500-RESOLVE-UPDATE-NOT-FND                                     
      *MESSAGE 00938 ALREADY RESOLVED                                           
                 MOVE PC-TSYMSG-00938    TO DP020-MSG-NUMBER                    
                 SET TM002-ERR (ASC-SEL-SUB)                                    
                     PS-ERROR                                                   
                     DP020-MSG-FATAL                                            
                     DP020-NEXT-ACT-APPL-ERROR                                  
                                         TO TRUE                                
            ELSE                                                                
            IF TM500-DIDNT-ALLOC-DUE-TO-TRBL OR                                 
               TM500-PREPACK-ERROR  OR                                          
               TM500-DUPLICATE-TROUBLE                                          
      *  TROUBLE WASN'T AUTO RESOLVED, DISTRO'ED OR ACKNOWLEDGED DUE TO         
      *  TROUBLE                                                                
               MOVE PC-TSYMSG-03384      TO  DP020-MSG-NUMBER                   
               SET TM002-ERR (ASC-SEL-SUB)                                      
                   PS-ERROR                                                     
                   DP020-MSG-FATAL                                              
                   DP020-NEXT-ACT-APPL-ERROR                                    
                                         TO TRUE                                
            ELSE                                                                
            IF TM500-AUTO-ACKNOW-DONE                                           
      * TROUBLE WAS AUTOMATICALLY ACKNOWLEDGED                                  
      *         FUTURE USE BECAUSE THIS DOESN'T INCLUDE RE-DISTRO'ING           
               MOVE PC-TSYMSG-03366      TO DP020-MSG-NUMBER                    
               SET TM002-ERR (ASC-SEL-SUB)                                      
                   PS-ERROR                                                     
                   DP020-MSG-FATAL                                              
                   DP020-NEXT-ACT-APPL-ERROR                                    
                                         TO TRUE                                
            ELSE                                                                
              IF TM500-ACK-REDISTRO-DONE                                        
      *MESSAGE 3364 TROUBLE WAS AUTO ACKNOWLEDGED AND REDISTRO WAS              
      * DONE                                                                    
                 MOVE PC-TSYMSG-03364    TO DP020-MSG-NUMBER                    
                 SET TM002-ERR (ASC-SEL-SUB)                                    
                     PS-ERROR                                                   
                     DP020-MSG-FATAL                                            
                     DP020-NEXT-ACT-APPL-ERROR                                  
                                         TO TRUE                                
              ELSE                                                              
                 IF TM500-AUTO-ACKNOW-FAILED OR                                 
                    TM500-AUTO-REDISTRO-FAILED                                  
      *TROUBLE NOT AUTO ACKNOWLEDGED DUE OTHER PRORATION TROUBLE                
                    MOVE PC-TSYMSG-03365    TO DP020-MSG-NUMBER                 
                    SET TM002-ERR (ASC-SEL-SUB)                                 
                        PS-ERROR                                                
                        DP020-MSG-FATAL                                         
                        DP020-NEXT-ACT-APPL-ERROR                               
                                         TO TRUE                                
              ELSE                                                              
                 IF TM500-AUTO-ACKNOW-NOT-FOUND                                 
      *TROUBLE NOT AUTO ACKNOWLEDGED DUE TO NO ALLOCATION                       
                    MOVE PC-TSYMSG-03385    TO DP020-MSG-NUMBER                 
                    SET TM002-ERR (ASC-SEL-SUB)                                 
                        PS-ERROR                                                
                        DP020-MSG-FATAL                                         
                        DP020-NEXT-ACT-APPL-ERROR                               
                                         TO TRUE                                
                 ELSE                                                           
                    PERFORM 5300-FORMAT-ERROR                                   
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * OPEN THE TRBNTF CURSOR                                         *        
      *----------------------------------------------------------------*        
                                                                                
       5100-OPEN-TRBNTF-CSR.                                                    
                                                                                
           EXEC SQL                                                             
                OPEN TRBNTF-CSR                                                 
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE  NOT = +0                                           
                    MOVE '5100-OPEN-TRBNTF-CSR'                                 
                                     TO DP013-PARAGRAPH                         
                    MOVE 'OPEN TRBNTF CURSOR '                                  
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE 'TTRBNTF'   TO DP013-DB2-TABLE-NAME (1)                
                    MOVE SQLCA       TO DP013-SQLCA                             
                    SET DP013-DB2-ABEND                                         
                        DP013-XCTL-DISPLAY-RESTART                              
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *  RESOLVE TROUBLE FOR ALL LINES OF THE PO THAT HAVE THE SAME    *        
      *  SPECIFIED TROUBLE.  THIS WILL ALSO CREATE THE SAME DETAIL     *        
      *  MESSAGE FOR ALL PO LINES INVOLVED.                            *        
      *----------------------------------------------------------------*        
                                                                                
       5110-RESOLVE-TRBL-FOR-REASON.                                            
                                                                                
           PERFORM 5120-FETCH-TRBNTF-ITEM.                                      
                                                                                
           IF  PS-NOT-TRBNTF-CSR-EOF                                            
               MOVE TRBNTF-PO-LNE-NBR TO TM500-PO-LNE-NBR                       
               MOVE TRBNTF-PREPK-ID   TO TM500-PREPK-ID                         
                                                                                
               PERFORM 5200-LINK-TO-UPDATE                                      
               EVALUATE TRUE                                                    
               WHEN TM500-CALL-SUCCESSFUL                                       
                   IF (TM500-RESOLVE-AUTO OR TM500-RESOLVE-MANUAL)              
                     AND (TM500-TRBL-RSN-CDE = PC-267 OR PC-268)                
                       MOVE TM500-FIELDS TO PV-HOLD-TM500-FIELDS                
                       PERFORM 6000-CHECK-FOR-TROUBLE                           
                       MOVE PV-HOLD-TM500-FIELDS TO TM500-FIELDS                
                   END-IF                                                       
                                                                                
               WHEN TM500-TPURCHS-SELECT-NOT-FND                                
               WHEN TM500-DUPLICATE-TROUBLE                                     
               WHEN TM500-TTRBNTF-UPDATE-NOT-FND                                
               WHEN TM500-SHIPPING-WINDOW-ERROR                                 
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO  TRUE                               
                   MOVE PC-TSYMSG-01068  TO  DP020-MSG-NUMBER                   
                   MOVE -1               TO  ATEXT1L                            
               WHEN TM500-BUYER-ACTION-PENDING                                  
                   SET PS-ERROR                                                 
                           DP020-MSG-FATAL TO TRUE                              
                   MOVE PC-TSYMSG-01069 TO DP020-MSG-NUMBER                     
                   MOVE -1           TO  ATEXT1L                                
               WHEN OTHER                                                       
                   PERFORM 5300-FORMAT-ERROR                                    
               END-EVALUATE                                                     
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    FETCH A TTRBNTF ROW                                         *        
      *----------------------------------------------------------------*        
                                                                                
       5120-FETCH-TRBNTF-ITEM.                                                  
                                                                                
           INITIALIZE DCLTTRBNTF.                                               
                                                                                
           EXEC SQL                                                             
                FETCH TRBNTF-CSR                                                
                INTO :TRBNTF-PO-LNE-NBR                                         
                   , :TRBNTF-PREPK-ID                                           
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    CONTINUE                                                    
               WHEN SQLCODE = +100                                              
                    SET PS-TRBNTF-CSR-EOF TO TRUE                               
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE < +0                                                
               WHEN SQLCODE > +0                                                
                    MOVE '5120-FETCH-TRBNTF-ITEM'                               
                                          TO DP013-PARAGRAPH                    
                    MOVE 'FETCH TRBNTF ITEM   '                                 
                                          TO DP013-MESSAGE-TEXT (1)             
                    MOVE 'TTRBNTF'        TO DP013-DB2-TABLE-NAME (1)           
                    MOVE SQLCA            TO DP013-SQLCA                        
                    SET DP013-DB2-ABEND                                         
                        DP013-XCTL-DISPLAY-RESTART                              
                                          TO TRUE                               
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      * CLOSE THE TRBNTF CURSOR                                        *        
      *----------------------------------------------------------------*        
                                                                                
       5130-CLOSE-TRBNTF-CSR.                                                   
                                                                                
           EXEC SQL                                                             
                CLOSE TRBNTF-CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE  NOT = +0                                           
                    MOVE '5130-CLOSE-TRBNTF-CSR'                                
                                     TO DP013-PARAGRAPH                         
                    MOVE 'CLOSE TRBNTF CURSOR '                                 
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE 'TTRBNTF'   TO DP013-DB2-TABLE-NAME (1)                
                    MOVE SQLCA       TO DP013-SQLCA                             
                    SET DP013-DB2-ABEND                                         
                        DP013-XCTL-DISPLAY-RESTART                              
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
      /                                                                         
      *----------------------------------------------------------------*        
      * LINK TO THE TROUBLE CENTRAL PROCESSING ROUTINE (TMKCS520) TO   *        
      * RESPOND, RESOLVE, OR UPDATE THE SELECTED NOTIFICATION RECORDS. *        
      *----------------------------------------------------------------*        
                                                                                
       5200-LINK-TO-UPDATE.                                                     
                                                                                
           SET TM500-FROM-MANUAL-SCREEN TO TRUE.                                
                                                                                
           EXEC CICS LINK                                                       
                PROGRAM (PC-TMKCS520)                                           
                COMMAREA(TM500-FIELDS)                                          
                RESP    (PV-CICS-RESP)                                          
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                           
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-LOGIC-ABEND                                            
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '5200-LINK-TO-UPDATE'                                       
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO LINK TO CENTRAL PROCESSING'                  
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      * FORMAT THE ERROR MESSAGE FROM THE TROUBLE CENTRAL PROCESSING   *        
      * ROUTINE TO DISPLAY AT THE BOTTOM OF THE SCREEN IN OUR STANDARD *        
      * MESSAGE FORMAT.                                                *        
      *----------------------------------------------------------------*        
                                                                                
       5300-FORMAT-ERROR.                                                       
                                                                                
           SET PS-ERROR                                                         
               DP020-MSG-FATAL           TO  TRUE.                              
                                                                                
           EVALUATE TRUE                                                        
           WHEN TM500-RESOLVE-UPDATE-NOT-FND                                    
               MOVE PC-TSYMSG-00938         TO DP020-MSG-NUMBER                 
               MOVE -1                      TO  ATEXT1L                         
           WHEN OTHER                                                           
               STRING 'NNNNN-E-'                                                
                      TM500-RETURN-MSG                                          
                      '-'                                                       
                      TM500-RETURN-CODE                                         
                      ', SQLCODE='                                              
                      TM500-SQLCODE DELIMITED BY SIZE                           
                                       INTO  DP020-MSG-MESSAGE                  
           END-EVALUATE.                                                        
                                                                                
           EXEC CICS                                                            
               SYNCPOINT ROLLBACK                                               
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      * LINK TO THE TROUBLE CENTRAL PROCESSING ROUTINE (TMKCS520) TO   *        
      * RESPOND, RESOLVE, OR UPDATE THE SELECTED NOTIFICATION RECORDS. *        
      *----------------------------------------------------------------*        
                                                                                
       5400-LINK-TO-AUTO-ACKN.                                                  
                                                                                
           SET TM500-FROM-MANUAL-SCREEN TO TRUE.                                
                                                                                
           EXEC CICS LINK                                                       
                PROGRAM (PC-TMKCS550)                                           
                COMMAREA(TM500-FIELDS)                                          
                RESP    (PV-CICS-RESP)                                          
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                           
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-LOGIC-ABEND                                            
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '5400-LINK-TO-AUTO-ACKN'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO LINK TO AUTO ACKNOWLEDGE'                    
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      /                                                                         
      *JPL-------------------------------------------------------------*        
       6000-CHECK-FOR-TROUBLE.                                                  
                                                                                
           SET TM500-PREPACK-EDIT-ONLY                                          
               TM500-NO-DETAILS                                                 
               TM500-REDISTRO-YES                                               
               TM500-PACK-LEVEL         TO TRUE.                                
                                                                                
           MOVE TM500-PO-NBR            TO DK-PO-NBR.                           
           MOVE TM500-REC-SEQ-NBR       TO DK-REC-SEQ-NBR.                      
           MOVE TM500-OPER-UNT-ID       TO DK-OPER-UNT-ID.                      
           MOVE TM500-FCLTY-ID          TO DK-FCLTY-ID.                         
           MOVE TM500-PREPK-ID          TO DK-PACK-ID.                          
           PERFORM 6010-GET-DRIVER-LINE.                                        
           MOVE RECITM-ORD-LNE-NBR      TO TM500-PO-LNE-NBR.                    
                                                                                
           EXEC CICS LINK                                                       
                PROGRAM (PC-TMKCS520)                                           
                COMMAREA(TM500-FIELDS)                                          
                LENGTH  (PC-LINE-TROUBLE-RTN-LENGTH)                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN TM500-CALL-SUCCESSFUL                                         
      ****************************************************************          
      * BELOW IS ONLY A DUMMY RETURN CODE FOR PREPACK EDIT ONLY.                
      * ALTHOUGH TROUBLE IS CALLED AND CREATED WHEN THIS SWITCH IS              
      * ON, PACK EDIT ONLY WOULD NEVER CALL STORE ALLOCATION.                   
      * THUS IN THE CASE OF PACK EDIT ONLY THE FOLLOWING RETURN CODE            
      * WOULD ONLY INDICATE THAT SOME TROUBLE WAS GENERATED.                    
      ****************************************************************          
             WHEN TM500-DIDNT-ALLOC-DUE-TO-TRBL                                 
                 MOVE +0 TO PV-NBR-OF-NEW-TRBLS                                 
                 PERFORM 6030-CHECK-FOR-NEW-TRBL                                
                 IF PV-NBR-OF-NEW-TRBLS > +0                                    
                     MOVE PC-TSYMSG-02243        TO DP020-MSG-NUMBER            
                     SET DP020-MSG-INFORMATIONAL TO TRUE                        
                 END-IF                                                         
                                                                                
             WHEN TM500-DUPLICATE-TROUBLE                                       
             WHEN TM500-TTRBNTF-UPDATE-NOT-FND                                  
                 IF TM500-IN-TROUBLE                                            
                     MOVE PC-TSYMSG-00941        TO DP020-MSG-NUMBER            
                     SET DP020-MSG-INFORMATIONAL TO TRUE                        
                 ELSE                                                           
                     MOVE PC-TSYMSG-02165        TO DP020-MSG-NUMBER            
                     SET DP020-MSG-INFORMATIONAL TO TRUE                        
                 END-IF                                                         
                                                                                
               WHEN OTHER                                                       
                 EXEC CICS                                                      
                      SYNCPOINT ROLLBACK                                        
                 END-EXEC                                                       
                 STRING TM500-PREPACK-PGM-NBR '-'                               
                        TM500-PREPACK-ERROR-NBR                                 
                        DELIMITED BY SIZE                                       
                   INTO DP020-MSG-TEXT                                          
                 MOVE PC-TSYMSG-02229 TO DP020-MSG-NUMBER                       
                 SET DP020-MSG-FATAL  TO TRUE                                   
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
       6010-GET-DRIVER-LINE.                                                    
                                                                                
           EXEC SQL                                                             
                 SELECT DISTINCT                                                
                        D.ORD_NBR                                               
                      , D.ORD_LNE_NBR                                           
                      , D.STATUS_CDE                                            
                      , D.DTL_CK_ITM_QTY                                        
                 INTO                                                           
                       :RECITM-ORD-NBR                                          
                      ,:RECITM-ORD-LNE-NBR                                      
                      ,:RECITM-STATUS-CDE                                       
                      ,:RECITM-DTL-CK-ITM-QTY                                   
                 FROM   TRECITM D                                               
                 WHERE  D.ORD_NBR           = :DK-PO-NBR                        
                   AND  D.REC_SEQ_NBR       = :DK-REC-SEQ-NBR                   
                   AND  D.VER_STATUS_CDE    = 'A'                               
                   AND  D.OPER_UNT_ID       = :DK-OPER-UNT-ID                   
                   AND  D.FCLTY_ID          = :DK-FCLTY-ID                      
                   AND  D.PREPK_ID          = :DK-PACK-ID                       
                   AND  D.PREPK_DRV_LNE_CDE = 'A'                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
                                                                                
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < +0                                                
               WHEN SQLCODE > +0                                                
                    MOVE '6010-RETRIEVE-DRIVER-LINE'                            
                                     TO DP013-PARAGRAPH                         
                    MOVE 'UNSUCCESSFUL DRIVER LINE SELECT'                      
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE SQLCA       TO DP013-SQLCA                             
                    MOVE 'TRECITM '  TO DP013-DB2-TABLE-NAME (1)                
                    SET DP013-DB2-ABEND                                         
                        DP013-XCTL-DISPLAY-RESTART TO TRUE                      
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
       6020-GET-PURITM.                                                         
                                                                                
           EXEC SQL                                                             
                SELECT DISTINCT                                                 
                       COUNT (*)                                                
                  INTO :PV-NBR-OF-PIC-AD                                        
                  FROM TPURCHS A,                                               
                       TPURITM B,                                               
                       TRECITM C                                                
                 WHERE A.PO_NBR         = :DK-PO-NBR                            
                   AND A.VER_STATUS_CDE = 'A'                                   
                   AND A.PO_NBR         = B.PO_NBR                              
                   AND A.PO_NBR         = C.ORD_NBR                             
                   AND B.PO_PREPK_ID    = :DK-PACK-ID                           
                   AND B.PIC_ITM_IND    = 'Y'                                   
                   AND C.ORD_LNE_NBR    = B.APP_PO_LNE_NBR                      
                   AND C.PREPK_ID       = B.PO_PREPK_ID                         
                   AND C.VER_STATUS_CDE = 'A'                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
                                                                                
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < +0                                                
               WHEN SQLCODE > +0                                                
                    MOVE '6020-GET-PURITM' TO DP013-PARAGRAPH                   
                    MOVE 'UNSUCCESSFUL SELECT ON PO ITEM'                       
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE SQLCA       TO DP013-SQLCA                             
                    MOVE 'TPURITM '  TO DP013-DB2-TABLE-NAME (1)                
                    SET DP013-DB2-ABEND                                         
                        DP013-XCTL-DISPLAY-RESTART TO TRUE                      
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
       6030-CHECK-FOR-NEW-TRBL.                                                 
                                                                                
           EXEC SQL                                                             
                SELECT DISTINCT                                                 
                       COUNT (*)                                                
                  INTO :PV-NBR-OF-NEW-TRBLS                                     
                  FROM TTRBNTF A                                                
                 WHERE A.PO_NBR         = :DK-PO-NBR                            
                   AND A.REC_SEQ_NBR    = :DK-REC-SEQ-NBR                       
                   AND A.OPER_UNT_ID    = :DK-OPER-UNT-ID                       
                   AND A.FCLTY_ID       = :DK-FCLTY-ID                          
                   AND A.TRBL_RSN_CDE   = :PC-269                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
                                                                                
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
                                                                                
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < +0                                                
               WHEN SQLCODE > +0                                                
                    MOVE '6030-CHECK-FOR-NEW-TRBL' TO DP013-PARAGRAPH           
                    MOVE 'UNSUCCESSFUL SELECT ON TROUBLE'                       
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE SQLCA       TO DP013-SQLCA                             
                    MOVE 'TTRBNTF '  TO DP013-DB2-TABLE-NAME (1)                
                    SET DP013-DB2-ABEND                                         
                        DP013-XCTL-DISPLAY-RESTART TO TRUE                      
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * RECORD IN THE SELECTION TEMP STORAGE QUEUE THE FACT THAT THIS  *        
      * OBJECT WAS UPDATED.                                            *        
      *----------------------------------------------------------------*        
                                                                                
       9000-UPDATE-SELECTION-QUEUE.                                             
                                                                                
           PERFORM 9100-READ-SEL-QUEUE.                                         
           SET TM002-UPD (ASC-SEL-SUB) TO TRUE.                                 
           PERFORM 9200-REWRITE-SEL-QUEUE.                                      
      /                                                                         
      *----------------------------------------------------------------*        
      * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
                                                                                
       9100-READ-SEL-QUEUE.                                                     
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-SEL-QUEUE-NAME)                                   
                   INTO  (TM002-MESSAGE-SEL-REC)                                
                   ITEM  (ASC-SEL-ITEM)                                         
                   RESP  (PV-CICS-RESPONSE)                                     
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                           
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-LOGIC-ABEND                                            
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9100-READ-SEL-QUEUE'                                       
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
                                                                                
       9200-REWRITE-SEL-QUEUE.                                                  
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE  (ASC-SEL-QUEUE-NAME)                                 
                    FROM   (TM002-MESSAGE-SEL-REC)                              
                    ITEM   (ASC-SEL-ITEM)                                       
                    RESP   (PV-CICS-RESPONSE)                                   
                    REWRITE                                                     
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESPONSE NOT EQUAL DFHRESP(NORMAL)                       
               SET DP013-CICS-ABEND                                             
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9200-REWRITE-SEL-QUEUE'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    THE DB2 RESOURCE IS NOT AVAILABLE, SET THE ERROR MESSAGE    *        
      *    AND THE ERROR SWITCHES.                                     *        
      *----------------------------------------------------------------*        
                                                                                
       9900-SOFT-ABORT.                                                         
                                                                                
           SET PS-ERROR                                                         
               PS-SOFT-ABORT           TO  TRUE.                                
           SET DP020-MSG-FATAL         TO  TRUE.                                
           MOVE PC-TSYMSG-00551        TO  DP020-MSG-NUMBER.                    
      /                                                                         
      *----------------------------------------------------------------*        
      *    DATE/TIME SUBROUTINE.                                       *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD017.                                                        
      /                                                                         
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
