000100******************************************************************      96
000200 IDENTIFICATION DIVISION.                                               90
000300******************************************************************      14
000400                                                                        *2
000500 PROGRAM-ID.    XLKUP048.                                               *2
000600 AUTHOR.        CHERI PARMLEY.                                          *2
000700 INSTALLATION.  KOHLS.                                                  *2
000800 DATE-WRITTEN   JUNE, 2015.                                             *2
000900 DATE-COMPILED.                                                         *2
001000                                                                        *2
001100******************************************************************      *2
001200*  THIS PROGRAM WILL UPDATE ROWS ON THE XLT_VND_STYL TABLE              *2
001220*                                                                         
001230*  THIS PROGRAM WILL TAKE IN AN EXTRACT FILE CREATED IN XLKUT112.         
001240*  THE EXTRACT FILE WILL CONTAIN ALL ACTIVE BRANDS FROM THE               
001250*  TMDARVS, TVSCHVL AND IMT_LBL TABLE TO GET THE STYLE AND BRAND          
001260*  ID.                                                                    
001270*                                                                         
001300******************************************************************      *2
001400                                                                        *2
001500******************************************************************      *2
001600 ENVIRONMENT DIVISION.                                                  *2
001700******************************************************************      *2
001800                                                                        *2
001900 CONFIGURATION SECTION.                                                 *2
002000                                                                        *2
002100 SOURCE-COMPUTER.        IBM-3090.                                      *2
002200 OBJECT-COMPUTER.        IBM-3090.                                      *2
002300                                                                        *2
002400 INPUT-OUTPUT SECTION.                                                  *2
002500                                                                        *2
002600 FILE-CONTROL.                                                          *2
002700                                                                        *2
002800                                                                        *2
002900     SELECT RPT-REQ-UPD-FILE                                            *2
003000         ASSIGN TO INP01.                                               *2
003100                                                                        *2
003200******************************************************************   CL**2
003300 DATA DIVISION.                                                      CL**2
003400******************************************************************   CL**2
003500                                                                        *2
003600 FILE SECTION.                                                          *2
003700                                                                        *2
003800 FD  RPT-REQ-UPD-FILE                                                   *2
003900     RECORDING MODE IS F                                                *2
004000     LABEL RECORDS ARE STANDARD                                         *2
004100     BLOCK CONTAINS 0 RECORDS.                                          *2
004200                                                                        *2
004300 01  RPT-REQ-UPD-REC                   PIC  X(50).                      *2
004500*                                                                       *2
004600******************************************************************      *2
004700 WORKING-STORAGE SECTION.                                               *2
004800******************************************************************      *2
006100*                                                                       *2
006200******************************************************************      *2
006300*  DB2 FORMATTED MESSAGE AREA                                           *2
006400******************************************************************      *2
006500     COPY DPWS004.                                                      *2
006600*                                                                       *2
006610******************************************************************      *2
006620*  LAYOUT FOR THE BRAND EXTRACT FILE                                    *2
006630******************************************************************      *2
       01  XL065-BRAND-RECORD.                                                  
006640     COPY XLRD065.                                                      *2
006650*                                                                       *2
006700*                                                                       *2
006800     COPY SQLCA2.                                                       *2
006900*                                                                       *2
007000******************************************************************      *2
007100*  COMMUNICATIONS AREA FOR DB2                                          *2
007200******************************************************************      *2
007300*                                                                       *2
007400     EXEC SQL                                                           *2
007500          INCLUDE SQLCA                                                 *2
007600     END-EXEC.                                                          *2
007700*                                                                       *2
007800*                                                                       *2
007900******************************************************************      *2
008000*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE XLT_VND_STYL             *2
008100*  TABLE                                                                *2
008200******************************************************************      *2
008300     EXEC SQL                                                           *2
008400          INCLUDE XLT00004                                              *2
008500     END-EXEC.                                                          *2
008600*                                                                       *2
008700 01  ABEND-CODE                      PIC S9(04)    VALUE +0             *2
008800                                                   COMP SYNC.           *2
008900                                                                        *2
009000 01  PS-PROGRAM-SWITCHES.                                               *2
009300     05  PS-EOF-SW                   PIC  X(01)    VALUE 'N'.           *2
009400         88  PS-EOF                                VALUE 'Y'.           *2
009500         88  PS-NOT-EOF                            VALUE 'N'.           *2
009600                                                                        *2
009700 01  PC-PROGRAM-CONSTANTS.                                              *2
010000     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                *2
010100         'XLKUP048'.                                                    *2
010400     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3             *2
010500                                                   COMP SYNC.           *2
010800                                                                        *2
011100     05  PC-COMMIT-HOLD              PIC 9(3)      VALUE 100.           14
011200                                                                        *2
011300 01  PV-PROGRAM-VARIABLES.                                              *2
011500     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.        *2
011600     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.        *2
012500     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                   *2
012600                                                                        *2
013700     05  PV-NOTFND-COUNT             PIC S9(07)  VALUE +0               *2
013800                                                    COMP SYNC.          *2
013900     05  PV-READ-COUNT               PIC S9(07)  VALUE +0               *2
014000                                                    COMP SYNC.          *2
014001     05  PV-INSERT-COUNT             PIC S9(07)  VALUE +0               *2
014002                                                    COMP SYNC.          *2
014001     05  PV-UPDATE-COUNT             PIC S9(07)  VALUE +0               *2
014002                                                    COMP SYNC.          *2
014003     05  PV-UPDATE-COUNTER           PIC S9(07)  VALUE +0               *2
014004                                                    COMP SYNC.          *2
014100     05  PV-COMMIT-COUNT             PIC S9(07)  VALUE +0               *2
014200                                                    COMP SYNC.          *2
014300     05  PV-COMMIT-COUNTER           PIC 9(03)   VALUE ZERO.            *2
014400                                                                        *2
014500 01  PV-COMMIT-INFO.                                                    *2
015800     05  FILLER                      PIC X(22) VALUE                    12
015900        ' # RECS UPDATED     : '.                                       *2
016000     05  PV-RECS-UPDATED-COUNT       PIC 9(7)  VALUE 0.                 *2
015800     05  FILLER                      PIC X(22) VALUE                    12
015900        ' # RECS INSERTED    : '.                                       *2
016000     05  PV-RECS-INSERTD-COUNT       PIC 9(7)  VALUE 0.                 *2
016100*                                                                       *2
016200*                                                                       *2
020600******************************************************************      *2
020700 PROCEDURE DIVISION.                                                    *2
020800******************************************************************      *2
020900                                                                        *2
021000 0000-PROGRAM-MAINLINE.                                                 *2
021100******************************************************************      *2
021200* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE           *2
021300*      PROGRAM.                                                         *2
021400******************************************************************      *2
021500                                                                        *2
021600     PERFORM 1000-INITIALIZE.                                           *2
021700     PERFORM 2000-PROCESS-INPUT                                         *2
021800       UNTIL PS-EOF.                                                    *2
021900     PERFORM 9000-EOJ-ROUTINE.                                          *2
022000                                                                        *2
022100     GOBACK.                                                            *2
022300*                                                                       *2
022400*                                                                       *2
022500 1000-INITIALIZE.                                                       *2
022600******************************************************************      *2
022700*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                          *2
023000******************************************************************      *2
023100                                                                        *2
023700     OPEN INPUT  RPT-REQ-UPD-FILE.                                      *2
023800                                                                        *2
024100     MOVE +0 TO PV-COMMIT-COUNTER                                       *2
024200                                                                        *2
024300     PERFORM 7000-READ-INPUT.                                           *2
024400                                                                        *2
024700*                                                                       *2
026100 2000-PROCESS-INPUT.                                                    *2
026700                                                                        *2
026800     PERFORM 8200-UPDATE-BRAND.                                         *2
026900                                                                        *2
027000     ADD +1 TO PV-COMMIT-COUNTER.                                       *2
027100                                                                        *2
027200     IF PV-COMMIT-COUNTER > PC-COMMIT-HOLD                              *2
027300         EXEC SQL                                                       *2
027400            COMMIT                                                      *2
027500         END-EXEC                                                       *2
027600                                                                        *2
027700         IF SQLCODE NOT = +0                                            *2
027800             DISPLAY '### COMMIT FAILED ! ###'                          *2
027900             PERFORM 9100-SQL-ABEND                                     *2
028000         END-IF                                                         *2
028100                                                                        *2
028200         ADD PV-COMMIT-COUNTER           TO PV-COMMIT-COUNT             *2
028700                                                                        *2
028800         MOVE PV-COMMIT-COUNT    TO PV-RECS-UPDATED-COUNT               *2
028900         DISPLAY 'COMMIT TAKEN: ' PV-COMMIT-INFO                        *2
028910         MOVE PV-UPDATE-COUNT TO PV-DISPLAY-COUNT                        C
028920         DISPLAY 'TOTAL RECORDS UPDATED: '  PV-DISPLAY-COUNT             C
029000                                                                        *2
029100         MOVE +0 TO PV-COMMIT-COUNTER                                   *2
029200     END-IF.                                                            *2
029300                                                                        *2
029400     PERFORM 7000-READ-INPUT.                                           *2
029500                                                                        *2
029700                                                                        *2
029701 7000-READ-INPUT.                                                       *2
029702******************************************************************      *2
029703*  READ   THE INPUT FILE                                                *2
029704******************************************************************      *2
029705     READ RPT-REQ-UPD-FILE                                              *2
029706          INTO XL065-BRAND-RECORD                                         
029707          AT END SET PS-EOF  TO TRUE.                                   *2
029708                                                                        *2
029709     IF PS-NOT-EOF                                                      *2
029710         ADD +1  TO PV-READ-COUNT                                       *2
029711         MOVE XL065-BRND-ID        TO XLT00004-BRND-ID                  *2
029711         MOVE XL065-STYL-ID        TO XLT00004-STYL-ID                  *2
029800     END-IF.                                                            *2
029900*                                                                       *2
043501                                                                        *2
043520                                                                        *2
       8200-UPDATE-BRAND.                                                       
                                                                                
043511         EXEC SQL                                                       *2
043512           UPDATE XLT_VND_STYL                                            
                 SET BRND_ID        = :XLT00004-BRND-ID                         
                    ,LAST_UPD_TMST  = CURRENT TIMESTAMP                         
 43516             WHERE STYL_ID    = :XLT00004-STYL-ID                         
043519         END-EXEC.                                                      *2
                                                                                
043521         EVALUATE TRUE                                                  *2
043522             WHEN SQLCODE = 0                                           *2
043523                 ADD +1           TO PV-UPDATE-COUNT                    *2
043524             WHEN SQLCODE = +100                                        *2
043525                 ADD +1           TO PV-NOTFND-COUNT                    *2
043526             WHEN SQLCODE = -904                                        *2
043527             WHEN SQLCODE = -913                                        *2
043528                 CONTINUE                                               *2
043529             WHEN SQLCODE < 0                                           *2
043530             WHEN SQLCODE > 0                                           *2
043531             WHEN SQLWARN0 = 'W'                                        *2
043532                 MOVE '8200-UPDATE-BRAND'                               *6
043533                             TO PV-PARAGRAPH-NAME                       *2
043534                 MOVE 'UPDATE OF XLT_BRND'                              *2
043535                             TO PV-DB2-OPERATION-ATTEMPTED              *2
043536                 PERFORM 9100-SQL-ABEND                                 *2
043537         END-EVALUATE.                                                  *2
043539                                                                        *2
043550                                                                        *2
043600 9000-EOJ-ROUTINE.                                                      *2
043700******************************************************************      *2
043800*  CLOSE OUTPUT FILE.  DISPLAY RECORD COUNTS                            *2
043900******************************************************************      *2
044000     CLOSE RPT-REQ-UPD-FILE.                                            *2
044100     MOVE PV-READ-COUNT   TO PV-DISPLAY-COUNT.                          *2
044101     DISPLAY 'TOTAL RECORDS READ   : '  PV-DISPLAY-COUNT.               *2
044102     MOVE PV-UPDATE-COUNT TO PV-DISPLAY-COUNT.                          *2
044103     DISPLAY 'TOTAL RECORDS UPDATED: '  PV-DISPLAY-COUNT.               *2
044102     MOVE PV-INSERT-COUNT TO PV-DISPLAY-COUNT.                          *2
044103     DISPLAY 'TOTAL RECORDS INSERTD: '  PV-DISPLAY-COUNT.               *2
044104     MOVE PV-NOTFND-COUNT TO PV-DISPLAY-COUNT.                          *2
044110     DISPLAY 'TOTAL RECORDS NOT FOUND: '  PV-DISPLAY-COUNT.             *2
047700*                                                                       *2
047800*                                                                       *2
047900 9100-SQL-ABEND.                                                        *2
048000******************************************************************      *2
048100*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL          *2
048200*  ERROR OR WARNING CODE OCCURS.                                        *2
048300******************************************************************      *2
048400     DISPLAY '9100-SQL-ABEND'.                                          *2
048500     DISPLAY '** ABEND     **'.                                         *2
048600     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.              *2
048700     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                    *2
048800     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.           *2
048900     DISPLAY '***'                                                      *2
049000     DISPLAY '*** LAST COMMIT TAKEN **' PV-COMMIT-INFO                  *2
049100     DISPLAY '***'                                                      *2
049200     DISPLAY '*** COMMITTED UPDATE COUNTS FOLLOW: ***'                  *2
049300     DISPLAY '***'                                                      *2
049400     EXEC SQL                                                           *2
049500          ROLLBACK                                                      *2
049600     END-EXEC.                                                          *2
049700******************************************************************      *2
049800* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-          *2
049900* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE               *2
050000* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE           *2
050100* FORMAT.                                                               *2
050200******************************************************************      *2
050300     COPY DPPD004.                                                      *2
050400                                                                        *2
050500     MOVE +4013                  TO ABEND-CODE.                         *2
050600     CALL 'ILBOABN0' USING ABEND-CODE.                                  *2
050700                                                                        *2
