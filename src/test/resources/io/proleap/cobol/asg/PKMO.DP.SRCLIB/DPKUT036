00001 ******************************************************************07/24/91
00002  IDENTIFICATION DIVISION.                                         DPKUT036
00003 ******************************************************************   LV006
00004                                                                   DPKUT036
00005  PROGRAM-ID.             DPKUT036.                                DPKUT036
00006  AUTHOR.                 RICK TULLOCH.                            DPKUT036
00007  INSTALLATION.           KOHLS DEPARTMENT STORES.                 DPKUT036
00008  DATE-WRITTEN            7-24-91.                                    CL**6
00009  DATE-COMPILED.                                                   DPKUT036
00010                                                                   DPKUT036
00011 ******************************************************************DPKUT036
00012 *       THIS PROGRAM WILL CREATE AN EXTRACT FILE FROM THE SYSTEM *   CL**6
00013 *  MESSAGE TEXT DB2 TABLE (TSYMSGT) FOR A SUBSEQUENT LOAD INTO   *   CL**6
00014 *  THE CICS ARCHITECTURE VSAM FILE (DPSYMSGT).  A CURSOR IS SET  *   CL**6
00015 *  SET UP TO RETRIEVE ALL ROWS IN SYSTEM MESSAGE NUMBER, MESSAGE *   CL**6
00016 *  TYPE CODE, AND MESSAGE LINE NUMBER SEQUENCE.  EACH ROW IS     *   CL**6
00017 *  FORMATTED INTO AN EXTRACT RECORD, AND IS WRITTEN TO THE       *   CL**6
00018 *  EXTRACT FILE.                                                 *   CL**6
00019 ******************************************************************DPKUT036
00020                                                                   DPKUT036
00021 ******************************************************************DPKUT036
00022  ENVIRONMENT DIVISION.                                            DPKUT036
00023 ******************************************************************DPKUT036
00024                                                                   DPKUT036
00025  CONFIGURATION SECTION.                                           DPKUT036
00026                                                                   DPKUT036
00027  SOURCE-COMPUTER.        IBM-3090.                                DPKUT036
00028  OBJECT-COMPUTER.        IBM-3090.                                DPKUT036
00029                                                                   DPKUT036
00030  INPUT-OUTPUT SECTION.                                            DPKUT036
00031                                                                   DPKUT036
00032  FILE-CONTROL.                                                    DPKUT036
00033                                                                   DPKUT036
00034      SELECT SM-SYSTEM-MESSAGE-TEXT-FILE                           DPKUT036
00035          ASSIGN TO OUT01.                                         DPKUT036
00036                                                                   DPKUT036
00037 ******************************************************************DPKUT036
00038  DATA DIVISION.                                                   DPKUT036
00039 ******************************************************************DPKUT036
00040                                                                   DPKUT036
00041  FILE SECTION.                                                    DPKUT036
00042                                                                   DPKUT036
00043  FD  SM-SYSTEM-MESSAGE-TEXT-FILE                                  DPKUT036
00044      RECORDING MODE IS F                                          DPKUT036
00045      LABEL RECORDS ARE STANDARD                                   DPKUT036
00046      BLOCK CONTAINS 0 RECORDS                                     DPKUT036
00047      RECORD CONTAINS 80 CHARACTERS                                DPKUT036
00048      DATA RECORD IS SM-SYSTEM-MESSAGE-TEXT-RECORD.                DPKUT036
00049                                                                   DPKUT036
00050  01  SM-SYSTEM-MESSAGE-TEXT-RECORD                                DPKUT036
00051                                  PIC X(80).                       DPKUT036
00052                                                                   DPKUT036
00053  WORKING-STORAGE SECTION.                                         DPKUT036
00054                                                                   DPKUT036
00055 *                                                                 DPKUT036
00056 *  SYSTEM MESSAGE TEXT FILE RECORD LAYOUT                         DPKUT036
00057 *                                                                 DPKUT036
00058                                                                   DPKUT036
00059 *    COPY DPRD508O.                                               DPKUT036
00060      COPY DPRD508O.                                                  CL**6
00061                                                                   DPKUT036
00062 *                                                                 DPKUT036
00063 *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        DPKUT036
00064 *                                                                 DPKUT036
00065                                                                   DPKUT036
00066 *    COPY DPWS004.                                                DPKUT036
00067      COPY DPWS004.                                                DPKUT036
00068                                                                   DPKUT036
00069  01  ABEND-CODE                  PIC S9(4)    VALUE ZEROS         DPKUT036
00070                                  COMP SYNC.                       DPKUT036
00071      88  AC-DB2-ERROR-4013                    VALUE +4013.           CL**6
00072                                                                   DPKUT036
00073  01  PC-PROGRAM-CONSTANTS.                                        DPKUT036
00074      05  PC-CURRENT-PROGRAM-NAME PIC X(8)     VALUE 'DPKUT036'.   DPKUT036
00075                                                                   DPKUT036
00076  01  PV-PROGRAM-VARIABLES.                                        DPKUT036
00077      05  PV-LAST-DB2-ACTION      PIC X(48)    VALUE SPACES.       DPKUT036
00078          88  PV-OPEN-SYS-MSG-TEXT-CURSOR      VALUE               DPKUT036
00079              'OPEN THE TSYMSGT SYSTEM MESSAGE TEXT CURSOR     '.  DPKUT036
00080          88  PV-CLOSE-SYS-MSG-TEXT-CURSOR     VALUE               DPKUT036
00081              'CLOSE THE TSYMSGT SYSTEM MESSAGE TEXT CURSOR    '.  DPKUT036
00082          88  PV-FETCH-SYS-MSG-TEXT-ROW        VALUE               DPKUT036
00083              'FETCH A ROW FROM THE TSYMSGT SYS MSG TEXT CURSOR'.  DPKUT036
00084                                                                   DPKUT036
00085 *                                                                 DPKUT036
00086 *  SQL AND COBOL DECLARATIONS FOR THE SYSTEM MESSAGE TEXT TABLE   DPKUT036
00087 *  (TSYMSGT)                                                      DPKUT036
00088 *                                                                 DPKUT036
00089                                                                   DPKUT036
00090      EXEC SQL                                                     DPKUT036
00091          INCLUDE TSYMSGT                                          DPKUT036
00092      END-EXEC.                                                    DPKUT036
00093                                                                   DPKUT036
00094 *                                                                 DPKUT036
00095 *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                DPKUT036
00096 *                                                                 DPKUT036
00097                                                                   DPKUT036
00098      EXEC SQL                                                     DPKUT036
00099          INCLUDE SQLCA                                            DPKUT036
00100      END-EXEC.                                                    DPKUT036
00101                                                                   DPKUT036
00102 *                                                                 DPKUT036
00103 *  CURSOR USED TO RETRIEVE ALL SYSTEM MESSAGE TEXT ROWS, IN SYSTEM   CL**6
00104 *  MESSAGE NUMBER, MESSAGE TYPE CODE, AND MESSAGE LINE NUMBER        CL**6
00105 *  SEQUENCE (REQUIRED FOR THE SUBSEQUENT VSAM LOAD).                 CL**6
00106 *                                                                    CL**6
00107                                                                   DPKUT036
00108      EXEC SQL                                                     DPKUT036
00109          DECLARE SYS_MSG_TXT_CURSOR CURSOR FOR                    DPKUT036
00110              SELECT SYMSG_NBR,                                    DPKUT036
00111                     TYP_CDE,                                      DPKUT036
00112                     LNE_SEQ_NBR,                                  DPKUT036
00113                     APLSYS_CDE,                                   DPKUT036
00114                     LNE_TXT                                       DPKUT036
00115                  FROM TSYMSGT                                     DPKUT036
00116                  ORDER BY SYMSG_NBR,                              DPKUT036
00117                           TYP_CDE,                                   CL**5
00118                           LNE_SEQ_NBR                             DPKUT036
00119      END-EXEC.                                                    DPKUT036
00120                                                                   DPKUT036
00121 ******************************************************************DPKUT036
00122  PROCEDURE DIVISION.                                              DPKUT036
00123 ******************************************************************DPKUT036
00124                                                                   DPKUT036
00125  0000-MAINLINE.                                                   DPKUT036
00126      PERFORM 1000-INITIALIZE.                                     DPKUT036
00127                                                                   DPKUT036
00128      PERFORM 2000-PROCESS-SYSTEM-MESSAGE                          DPKUT036
00129          UNTIL SQLCODE = +100.                                    DPKUT036
00130                                                                   DPKUT036
00131      PERFORM 9000-EOJ-ROUTINE.                                    DPKUT036
00132                                                                   DPKUT036
00133      STOP RUN.                                                    DPKUT036
00134                                                                   DPKUT036
00135 ******************************************************************DPKUT036
00136 *  SET UP FOR PROGRAM PROCESSING BY OPENING THE EXTRACT FILE,    *   CL**6
00137 *  AND THE SYSTEM MESSAGE TEXT CURSOR (ON THE TABLE TSYMSGT).    *   CL**6
00138 ******************************************************************DPKUT036
00139                                                                   DPKUT036
00140  1000-INITIALIZE.                                                 DPKUT036
00141      OPEN OUTPUT SM-SYSTEM-MESSAGE-TEXT-FILE.                     DPKUT036
00142                                                                   DPKUT036
00143      EXEC SQL                                                     DPKUT036
00144          OPEN SYS_MSG_TXT_CURSOR                                  DPKUT036
00145      END-EXEC.                                                    DPKUT036
00146                                                                   DPKUT036
00147      IF SQLCODE = ZERO                                            DPKUT036
00148          PERFORM 7000-FETCH-SYSTEM-MSG-TEXT-ROW                   DPKUT036
00149      ELSE                                                         DPKUT036
00150          SET PV-OPEN-SYS-MSG-TEXT-CURSOR TO TRUE                     CL**3
00151          PERFORM 9100-DB2-ERROR                                   DPKUT036
00152      END-IF.                                                      DPKUT036
00153                                                                   DPKUT036
00154      INITIALIZE DP508O-SYSTEM-MESSAGE-TEXT-RCD.                      CL**3
00155 *1000-EXIT.                                                       DPKUT036
00156                                                                   DPKUT036
00157 ******************************************************************DPKUT036
00158 *  PROCESS A SYSTEM MESSAGE TEXT ROW.  FORMAT AND WRITE A SYSTEM *   CL**6
00159 *  MESSAGE TEXT EXTRACT RECORD.                                  *   CL**6
00160 ******************************************************************DPKUT036
00161                                                                   DPKUT036
00162  2000-PROCESS-SYSTEM-MESSAGE.                                     DPKUT036
00163      MOVE SYMSGT-SYMSG-NBR TO DP508O-SYSTEM-MESSAGE-NUMBER.          CL**5
00164      MOVE SYMSGT-TYP-CDE TO DP508O-MESSAGE-TYPE-CODE.                CL**5
00165      MOVE SYMSGT-LNE-SEQ-NBR TO DP508O-MESSAGE-LINE-SEQ-NUMBER       CL**5
00166      MOVE SYMSGT-APLSYS-CDE TO DP508O-APPL-SYSTEM-CODE.              CL**5
00167      MOVE SYMSGT-LNE-TXT TO DP508O-SYSTEM-MESSAGE-TEXT.              CL**5
00168                                                                      CL**5
00169      PERFORM 8000-WRITE-SYS-MSG-TEXT-RECORD.                         CL**5
00170                                                                      CL**5
00171      PERFORM 7000-FETCH-SYSTEM-MSG-TEXT-ROW.                         CL**5
00172 *2000-EXIT.                                                       DPKUT036
00173                                                                   DPKUT036
00174 ******************************************************************DPKUT036
00175 *  FETCH A ROW FROM THE SYSTEM MESSAGE TEXT CURSOR.              *   CL**6
00176 ******************************************************************DPKUT036
00177                                                                   DPKUT036
00178  7000-FETCH-SYSTEM-MSG-TEXT-ROW.                                  DPKUT036
00179      EXEC SQL                                                     DPKUT036
00180          FETCH SYS_MSG_TXT_CURSOR                                 DPKUT036
00181              INTO :SYMSGT-SYMSG-NBR,                              DPKUT036
00182                   :SYMSGT-TYP-CDE,                                DPKUT036
00183                   :SYMSGT-LNE-SEQ-NBR,                               CL**2
00184                   :SYMSGT-APLSYS-CDE,                             DPKUT036
00185                   :SYMSGT-LNE-TXT                                 DPKUT036
00186      END-EXEC.                                                    DPKUT036
00187                                                                   DPKUT036
00188      IF ((SQLCODE NOT = ZERO)                                     DPKUT036
00189              AND (SQLCODE NOT = +100))                            DPKUT036
00190          SET PV-FETCH-SYS-MSG-TEXT-ROW TO TRUE                       CL**3
00191          PERFORM 9100-DB2-ERROR                                   DPKUT036
00192      END-IF.                                                      DPKUT036
00193 *7000-EXIT.                                                       DPKUT036
00194                                                                   DPKUT036
00195 ******************************************************************DPKUT036
00196 *  WRITE A SYSTEM MESSAGE TEXT EXTRACT RECORD TO THE EXTRACT     *   CL**6
00197 *  FILE.                                                         *   CL**6
00198 ******************************************************************DPKUT036
00199                                                                   DPKUT036
00200  8000-WRITE-SYS-MSG-TEXT-RECORD.                                  DPKUT036
00201      WRITE SM-SYSTEM-MESSAGE-TEXT-RECORD                          DPKUT036
00202          FROM DP508O-SYSTEM-MESSAGE-TEXT-RCD.                     DPKUT036
00203 *8000-EXIT.                                                       DPKUT036
00204                                                                   DPKUT036
00205 ******************************************************************DPKUT036
00206 *  CLOSE ALL OPEN CURSORS AND FILES.                             *DPKUT036
00207 ******************************************************************DPKUT036
00208                                                                   DPKUT036
00209  9000-EOJ-ROUTINE.                                                DPKUT036
00210      EXEC SQL                                                     DPKUT036
00211          CLOSE SYS_MSG_TXT_CURSOR                                 DPKUT036
00212      END-EXEC.                                                    DPKUT036
00213                                                                   DPKUT036
00214      IF SQLCODE NOT = ZERO                                        DPKUT036
00215          SET PV-CLOSE-SYS-MSG-TEXT-CURSOR TO TRUE                    CL**3
00216          PERFORM 9100-DB2-ERROR                                   DPKUT036
00217      END-IF.                                                      DPKUT036
00218                                                                   DPKUT036
00219      CLOSE SM-SYSTEM-MESSAGE-TEXT-FILE.                           DPKUT036
00220 *9000-EXIT.                                                       DPKUT036
00221                                                                   DPKUT036
00222 ******************************************************************DPKUT036
00223 *  FORMAT A DB2 ERROR MESSAGE, AND ABEND THE PROGRAM.            *DPKUT036
00224 ******************************************************************DPKUT036
00225                                                                   DPKUT036
00226  9100-DB2-ERROR.                                                  DPKUT036
00227      DISPLAY '** ABEND **'.                                       DPKUT036
00228      DISPLAY PC-CURRENT-PROGRAM-NAME  ' - SQL ERROR'.             DPKUT036
00229      DISPLAY PC-CURRENT-PROGRAM-NAME  ' - ATTEMPTING TO '         DPKUT036
00230                PV-LAST-DB2-ACTION.                                DPKUT036
00231                                                                   DPKUT036
00232 *                                                                 DPKUT036
00233 *  ROUTINE TO CONVERT THE SQLCA INTO A READABLE FORMAT, USING     DPKUT036
00234 *  THE CALLED MODULE DSNTIAR.                                     DPKUT036
00235 *                                                                 DPKUT036
00236                                                                   DPKUT036
00237      COPY DPPD004.                                                DPKUT036
00238                                                                   DPKUT036
00239      IF SQLCODE NOT = -911                                        DPKUT036
00240          EXEC SQL                                                 DPKUT036
00241              COMMIT                                               DPKUT036
00242          END-EXEC                                                 DPKUT036
00243      END-IF.                                                      DPKUT036
00244                                                                   DPKUT036
00245      SET AC-DB2-ERROR-4013 TO TRUE.                                  CL**6
00246      CALL 'ILBOABN0' USING ABEND-CODE.                            DPKUT036
00247 *9100-EXIT.                                                       DPKUT036
