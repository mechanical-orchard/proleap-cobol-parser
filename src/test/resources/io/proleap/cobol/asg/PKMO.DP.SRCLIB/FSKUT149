      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    FSKUT149.                                                 
       AUTHOR.        SHARON SZPISZAR - STRATAGEM.                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07-2007.                                                  
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * PROGRAM OBJECTIVE:                                                      
      * ------------------                                                      
      * - SEQUENTIALLY READ A FILE OF ALL THE RECORDS FROM THE                  
      *   E-COMMERCE STYLE SUMMARY EXTRACT.                                     
      * - SEQUENTIALLY READ A FILE OF UNLOAD RECORDS FROM TABLES TWEBITM        
      *   JOINED TO TSKXREF FOR ALL ACTIVE WEB-ELIGIBLE ITEMS.                  
      * - MERGE THE RECORDS FROM EACH INPUT FILE INTO A NEW FILE BY ITEM        
      *   NUMBER.                                                               
      * - ALL OF THE RECORDS FROM THE E-COMMERCE EXTRACT WILL BE PUT ON         
      *   THE OUTPUT FILE BUT ONLY SELECTED RECORDS FROM THE UNLOAD FILE        
      *   WILL BE PUT ON THE OUTPUT FILE.                                       
      ******************************************************************        
      * CHANGE HISTORY:                                                         
      * ---------------                                                         
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
      * WR31955 07-05-2007 S SZPISZAR  ORIGINAL IMPLEMENTATION                  
W34688* WR34688 08-06-2009 V MALLANNA  ADD NEW FIELDS WEB EXCLUSIVE IND         
W34688*                      GARI      & EXTENDED SIZE CODE                     
EFC2  * WR38216 03-18-2010 V VIGNESH   COMBINE EFC2 WITH EXISTING EFC           
EFC2FX* INC     06-06-2010 T JOHNSTON  CHG TUPCPLS LOGIC WHICH CAN              
EFC2FX*                                RETURN MULTIPLE ROWS TO A                
EFC2FX*                                WHERE EXISTS CLAUSE                      
W38972* WR38972 10-15-2010 COGNIZANT   MLOF PROJECT CHANGES.                    
W38972*                    (SANDEEP)   REPLACED THE HARD CODED EFC STORE00260000
W38972*                                NUMBER CHECK WITH THE LOCATION   00260000
W38972*                                TYPE CODE AND E-BUSINESS                 
W38972*                                INDICATOR CHECK IN THE PARAGRAPH         
W38972*                                1300-CHECK-FOR-STORE-873.                
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
      ******************************************************************        
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
      ******************************************************************        
       INPUT-OUTPUT SECTION.                                                    
                                                                                
      ******************************************************************        
       FILE-CONTROL.                                                            
                                                                                
           SELECT FS-ECOMM-FILE                                                 
             ASSIGN TO INP01.                                                   
                                                                                
           SELECT FS-TWEB-FILE                                                  
             ASSIGN TO INP02.                                                   
                                                                                
           SELECT FS-NEW-ECOMM-FILE                                             
             ASSIGN TO OUT01.                                                   
                                                                                
      /                                                                         
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  FS-ECOMM-FILE                                                        
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  FS-ECOMM-DTL.                                                        
           05  FILLER                      PIC  X(10).                          
           05  FS-ECOMM-SKU                PIC  X(08).                          
           05  FILLER                      PIC  X(170).                         
           05  FS-ECOMM-ITM-NBR            PIC  S9(15)V    COMP-3.              
           05  FILLER                      PIC  X(104).                         
                                                                                
       FD  FS-TWEB-FILE                                                         
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  FS-TWEB-DTL.                                                         
           05  FS-TWEB-INTR-UPC-ID         PIC  S9(9)      COMP.                
           05  FS-TWEB-ASRT-BEG-DTE        PIC  X(10).                          
           05  FS-TWEB-ASRT-END-DTE        PIC  X(10).                          
           05  FS-TWEB-PRD-GP-NBR          PIC  S9(8)V     COMP-3.              
           05  FS-TWEB-PRD-GP-IND          PIC  X(01).                          
           05  FS-TWEB-SHIP-GP-SEQ-NBR     PIC  S9(4)      COMP.                
W34688     05  FS-TWEB-WEB-EXCLV-IND       PIC  X(1).                           
           05  FS-TWEB-SKU-STAT-CDE        PIC  X(2).                           
           05  FS-TWEB-ITM-NBR             PIC  S9(15)V    COMP-3.              
           05  FS-TWEB-DEPT                PIC  S9(4)V     COMP-3.              
           05  FS-TWEB-CLASS               PIC  S9(4)V     COMP-3.              
           05  FS-TWEB-SUBCLASS            PIC  S9(4)V     COMP-3.              
           05  FS-TWEB-SKU                 PIC  S9(8)V     COMP-3.              
W34688     05  FS-TWEB-KOHLS-SIZE-CDE      PIC  S9(9)V     COMP.                
                                                                                
       FD  FS-NEW-ECOMM-FILE                                                    
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  FS-NEW-ECOMM-DTL                PIC  X(300).                         
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ECOMM-EOF-SW         PIC  X(01)          VALUE 'N'.           
               88  PS-ECOMM-EOF                            VALUE 'Y'.           
           05  PS-TWEB-EOF-SW          PIC  X(01)          VALUE 'N'.           
               88  PS-TWEB-EOF                             VALUE 'Y'.           
                                                                                
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME         PIC  X(08)          VALUE                
               'FSKUT149'.                                                      
           05  PC-MAX-RETRY-CNT        PIC S9(04)          VALUE 3              
                                                           COMP SYNC.           
                                                                                
      ******************************************************************        
       01  PT-PROGRAM-TOTALS.                                                   
           05  PT-ECOMM-RCD-READ       PIC S9(09)          VALUE +0             
                                                           COMP-3.              
           05  PT-TWEB-RCD-READ        PIC S9(09)          VALUE +0             
                                                           COMP-3.              
           05  PT-TWEB-RCD-USED        PIC S9(09)          VALUE +0             
                                                           COMP-3.              
           05  PT-ECOMM-RCD-WRTTN      PIC S9(09)          VALUE +0             
                                                           COMP-3.              
           05  PT-ECOMM-TWEB-MATCH     PIC S9(09)          VALUE +0             
                                                           COMP-3.              
           05  PT-ECOMM-TWEB-MISMATCH  PIC S9(09)          VALUE +0             
                                                           COMP-3.              
                                                                                
      ******************************************************************        
       01  ABEND-CODE                  PIC S9(04)          VALUE +0             
                                                           COMP SYNC.           
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-TUPCPLS-SW           PIC  X(01)           VALUE SPACE.        
           05  PV-HOLD-ECOMM-ITEM-NBR  PIC  S9(15)V COMP-3  VALUE ZEROS.        
           05  PV-HOLD-TWEB-ITEM-NBR   PIC  S9(15)V COMP-3  VALUE ZEROS.        
           05  PV-HOLD-SUB-CL-MDSEAR-ID PIC S9(6)V  COMP-3  VALUE ZEROS.        
           05  PV-HOLD-VS-ID           PIC  S9(9)   COMP.                       
           05  PV-HOLD-KOHLS-SIZE-CDE  PIC  S9(9)   COMP.                       
           05  PV-HOLD-MEITGP-NBR      PIC  S9(9)   COMP.                       
           05  PV-PARAGRAPH-NAME       PIC  X(30)           VALUE SPACE.        
           05  PV-OPER-ATTEMPTED       PIC  X(30)           VALUE SPACE.        
           05  PV-RETRY-CNT            PIC  S9(04)          VALUE +0            
                                                            COMP SYNC.          
                                                                                
      ******************************************************************        
       01  WF-WORK-FIELDS.                                                      
           05  WF-WORK-DEPT-9-4        PIC  9(04).                              
           05  WF-WORK-DEPT-X-4        REDEFINES WF-WORK-DEPT-9-4               
                                       PIC  X(04).                              
           05  WF-WORK-DEPT-9          REDEFINES WF-WORK-DEPT-9-4.              
               10  FILLER              PIC  9(01).                              
               10  WF-WORK-DEPT-9-3    PIC  9(03).                              
               10  WF-WORK-DEPT-X-3    REDEFINES WF-WORK-DEPT-9-3               
                                       PIC  X(03).                              
                                                                                
           05  WF-WORK-CLASS-9-4       PIC  9(04).                              
           05  WF-WORK-CLASS-X-4       REDEFINES WF-WORK-CLASS-9-4              
                                       PIC  X(04).                              
           05  WF-WORK-CLASS-9         REDEFINES WF-WORK-CLASS-9-4.             
               10  FILLER              PIC  9(02).                              
               10  WF-WORK-CLASS-9-2   PIC  9(02).                              
               10  WF-WORK-CLASS-X-2   REDEFINES WF-WORK-CLASS-9-2              
                                       PIC  X(02).                              
                                                                                
           05  WF-WORK-SUBCLASS-9-4    PIC  9(04).                              
           05  WF-WORK-SUBCLASS-X-4    REDEFINES WF-WORK-SUBCLASS-9-4           
                                       PIC  X(04).                              
           05  WF-WORK-SUBCLASS-9      REDEFINES WF-WORK-SUBCLASS-9-4.          
               10  FILLER              PIC  9(02).                              
               10  WF-WORK-SUBCLASS-9-2  PIC  9(02).                            
               10  WF-WORK-SUBCLASS-X-2  REDEFINES WF-WORK-SUBCLASS-9-2         
                                       PIC  X(02).                              
                                                                                
           05  WF-WORK-SKU-9-8         PIC  9(08).                              
           05  WF-WORK-SKU-X-8         REDEFINES WF-WORK-SKU-9-8                
                                       PIC  X(08).                              
                                                                                
           05  WF-WORK-PRD-GP-NBR-9-8  PIC  9(08).                              
           05  WF-WORK-PRD-GP-NBR-X-8  REDEFINES WF-WORK-PRD-GP-NBR-9-8         
                                       PIC  X(08).                              
                                                                                
      ******************************************************************        
      *  FASHION SALES E-COMMERCE WEEKLY SKU EXTRACT - OUTPUT RECORD            
      ******************************************************************        
           COPY FSRD126.                                                        
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF TUPCPLS DB2 TABLE               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TUPCPLS                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF TUPC DB2 TABLE                  
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
                                                                                
W34688*****************************************************************         
W34688*  DCLGEN LAYOUT AND COBOL DECLARATION OF TSKXREF DB2 TABLE               
W34688*****************************************************************         
W34688     EXEC SQL                                                             
W34688         INCLUDE TSKXREF                                                  
W34688     END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF TMDSEAR DB2 TABLE               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF TMDARVS DB2 TABLE               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMDARVS                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF TVNDSTL DB2 TABLE               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TVNDSTL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF TENTNME DB2 TABLE               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TENTNME                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF TSIZE DB2 TABLE                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSIZE                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF TMITGPL DB2 TABLE               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMITGPL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  COMMUNICATIONS AREA FOR DB2                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  COMMUNICATIONS AREA FOR DB2                                            
      ******************************************************************        
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
      *  DB2 FORMATTED ERROR MESSAGE                                            
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
      *  CONTROLS THE MAIN PROCESSING OF THE PROGRAM                            
      ******************************************************************        
       0000-MAIN-PROCESS.                                                       
                                                                                
           PERFORM 8000-OPEN-FILES.                                             
                                                                                
           PERFORM 8100-READ-ECOMM-RCD.                                         
           PERFORM 8200-READ-TWEB-RCD.                                          
                                                                                
           PERFORM 1000-PROCESS-MERGING                                         
               UNTIL PS-ECOMM-EOF                                               
                 AND PS-TWEB-EOF.                                               
                                                                                
           PERFORM 8900-CLOSE-FILES.                                            
                                                                                
           PERFORM 9000-DISPLAY-PGM-TOTALS.                                     
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      * 1000-PROCESS-MERGING.                                                   
      * IF ECOMM ITEM NBR = TWEB ITEM NBR, WRITE NEW ECOMM RECORD.              
      * IF ECOMM ITEM NBR < TWEB ITEM NBR, WRITE NEW ECOMM RECORD.              
      * IF TWEB  ITEM NBR < ECOMM ITEM NBR, CHECK STATUS.                       
      ******************************************************************        
       1000-PROCESS-MERGING.                                                    
                                                                                
           IF PV-HOLD-ECOMM-ITEM-NBR < PV-HOLD-TWEB-ITEM-NBR                    
              ADD +1 TO PT-ECOMM-TWEB-MISMATCH                                  
              INITIALIZE FS-NEW-ECOMM-DTL                                       
              MOVE FS-ECOMM-DTL TO FS126-ECOM-SKU-EXTRACT-RECORD                
              MOVE 'N' TO FS126-DIRECT-SHIP-IND                                 
W34688        MOVE 'N' TO FS126-WEB-EXCLV-IND                                   
W34688        PERFORM 1050-POPULATE-TUPC-SIZE-CDE                               
W34688        MOVE UPC-KOHLS-SIZE-CDE  TO FS126-KOHLS-SIZE-CDE                  
              PERFORM 8300-WRITE-NEW-ECOMM-RCD                                  
              PERFORM 8100-READ-ECOMM-RCD                                       
           END-IF.                                                              
                                                                                
           IF PV-HOLD-ECOMM-ITEM-NBR > PV-HOLD-TWEB-ITEM-NBR                    
              IF FS-TWEB-SKU-STAT-CDE = '10' OR '20' OR '40' OR '90'            
                 MOVE FS-TWEB-SKU-STAT-CDE TO FS126-STATUS-CODE                 
                 MOVE SPACES TO FS126-RCP-STATUS-CODE                           
                 PERFORM 1200-PROCESS-STATUS                                    
              ELSE                                                              
                 PERFORM 1300-CHECK-FOR-STORE-873                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF PV-HOLD-ECOMM-ITEM-NBR = PV-HOLD-TWEB-ITEM-NBR                    
              IF NOT PS-ECOMM-EOF AND                                           
                 NOT PS-TWEB-EOF                                                
                    INITIALIZE FS-NEW-ECOMM-DTL                                 
                    ADD +1 TO PT-ECOMM-TWEB-MATCH                               
                    MOVE FS-ECOMM-DTL TO FS126-ECOM-SKU-EXTRACT-RECORD          
W34688              MOVE FS-TWEB-WEB-EXCLV-IND TO FS126-WEB-EXCLV-IND           
W34688              MOVE FS-TWEB-KOHLS-SIZE-CDE TO FS126-KOHLS-SIZE-CDE         
                    PERFORM 1100-POPULATE-DIRECT-SHIP-FLD                       
                    PERFORM 8300-WRITE-NEW-ECOMM-RCD                            
                    PERFORM 8100-READ-ECOMM-RCD                                 
                    PERFORM 8200-READ-TWEB-RCD                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
W34688******************************************************************        
W34688* POPULATE TPUC KOHLS SIZE CODE FROM TUPC TABLE                  *        
W34688******************************************************************        
W34688 1050-POPULATE-TUPC-SIZE-CDE.                                             
W34688                                                                          
W34688     MOVE '*1050-POPULATE-TUPC-SIZE-CDE *' TO PV-PARAGRAPH-NAME.          
W34688                                                                          
W34688     MOVE ZEROS               TO UPC-KOHLS-SIZE-CDE.                      
W34688     MOVE FS126-ITEM-NMBR     TO SKXREF-ITM-NBR.                          
W34688                                                                          
W34688     PERFORM                                                              
W34688        VARYING PV-RETRY-CNT                                              
W34688        FROM 1 BY 1                                                       
W34688        UNTIL   PV-RETRY-CNT > PC-MAX-RETRY-CNT                           
W34688                                                                          
W34688         EXEC SQL                                                         
W34688            SELECT KOHLS_SIZE_CDE                                         
W34688             INTO :UPC-KOHLS-SIZE-CDE                                     
W34688              FROM TUPC     A,                                            
W34688                   TSKXREF  B                                             
W34688            WHERE B.ITM_NBR         = :SKXREF-ITM-NBR                     
W34688              AND A.UPC_ID          = B.INTR_UPC_ID                       
W34688              AND A.UPC_TYP_CDE     = 'I'                                 
W34688             WITH UR                                                      
W34688         END-EXEC                                                         
W34688           EVALUATE TRUE                                                  
W34688              WHEN SQLCODE = +0                                           
W34688              WHEN SQLCODE = +100                                         
W34688                 MOVE PC-MAX-RETRY-CNT TO PV-RETRY-CNT                    
W34688              WHEN SQLCODE = -911                                         
W34688              WHEN SQLCODE = -913                                         
W34688                 CONTINUE                                                 
W34688              WHEN OTHER                                                  
W34688                 PERFORM 9999-DB2-ABEND                                   
W34688           END-EVALUATE                                                   
W34688     END-PERFORM.                                                         
W34688                                                                          
W34688     IF (SQLWARN0 NOT = SPACES                                            
W34688       OR (SQLCODE > 0 AND SQLCODE NOT = +100)                            
W34688       OR SQLCODE < 0)                                                    
W34688         PERFORM 9999-DB2-ABEND                                           
W34688     END-IF.                                                              
                                                                                
      ******************************************************************        
      * POPULATE DIRECT SHIP FIELD                                     *        
      ******************************************************************        
       1100-POPULATE-DIRECT-SHIP-FLD.                                           
                                                                                
           IF FS-TWEB-SHIP-GP-SEQ-NBR = 13                                      
              MOVE 'Y' TO FS126-DIRECT-SHIP-IND                                 
           ELSE                                                                 
              MOVE 'N' TO FS126-DIRECT-SHIP-IND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * PROCESS NEW E-COMM RECORDS                                     *        
      ******************************************************************        
       1200-PROCESS-STATUS.                                                     
                                                                                
           ADD +1 TO PT-TWEB-RCD-USED.                                          
           PERFORM 1100-POPULATE-DIRECT-SHIP-FLD.                               
           PERFORM 1400-INITIALIZE-FIELDS.                                      
           PERFORM 1500-POPULATE-FROM-UNLOAD-FILE.                              
                                                                                
           PERFORM 1600-POPULATE-FROM-TMDSEAR.                                  
           MOVE MDSEAR-GMA-NBR        TO FS126-MERCH-DIV-CODE.                  
           MOVE MDSEAR-DMA-NBR        TO FS126-DMM-NMBR.                        
           MOVE MDSEAR-BYR-NBR        TO FS126-BUYER-NMBR.                      
                                                                                
           PERFORM 1700-POPULATE-FROM-TUPC-PLS.                                 
           MOVE UPC-SUB-CL-MDSEAR-ID  TO PV-HOLD-SUB-CL-MDSEAR-ID.              
           MOVE UPC-VS-ID             TO PV-HOLD-VS-ID.                         
           MOVE UPC-KOHLS-SIZE-CDE    TO PV-HOLD-KOHLS-SIZE-CDE.                
           MOVE UPCPLS-MEITGP-NBR     TO PV-HOLD-MEITGP-NBR.                    
           MOVE UPC-CSTM-CLOR-DESC    TO FS126-COLOR-DESC.                      
           MOVE UPC-GP-CDE            TO FS126-GROUP-CODE.                      
           MOVE UPC-RPLNMT-IND        TO FS126-RPLNMT-IND.                      
           MOVE UPC-KY-ITM-IND        TO FS126-KY-ITM-IND.                      
           MOVE UPC-SEA-SEQ-NBR       TO FS126-SEA-SEQ-NBR.                     
           MOVE UPC-INTR-UPC-DESC     TO FS126-SKU-DESCRIPTION.                 
           MOVE UPCPLS-UNT-COST-AMT   TO FS126-UNIT-COST-AMT.                   
           MOVE UPCPLS-UNT-RTL-AMT    TO FS126-CUR-UNIT-RTL-AMT.                
                                                                                
           PERFORM 1800-POPULATE-VENDOR-STYLE.                                  
           MOVE MDARVS-STKR-NBR       TO FS126-STICKER-NBR.                     
           MOVE MDARVS-LBL-SEQ-NBR    TO FS126-LBL-SEQ-NBR.                     
           MOVE VNDSTL-VS-NBR         TO FS126-LONG-VENDOR-STYLE.               
           MOVE ENTNME-ENT-NAME-DESC  TO FS126-IN-HSE-VNDR-NAME.                
                                                                                
           PERFORM 1900-POPULATE-FROM-TSIZE.                                    
           MOVE SIZE-KOHLS-DESC       TO FS126-SIZE-DESC.                       
                                                                                
           IF PV-HOLD-MEITGP-NBR > 0                                            
              PERFORM 2000-POPULATE-FROM-TMITGPL                                
              MOVE MITGPL-MITGPL-QTY     TO FS126-GP-QTY                        
              MOVE MITGPL-GP-AMT         TO FS126-GP-RTL-AMT                    
           ELSE                                                                 
              MOVE ZEROS                 TO FS126-GP-QTY,                       
                                            FS126-GP-RTL-AMT                    
           END-IF.                                                              
                                                                                
           PERFORM 8300-WRITE-NEW-ECOMM-RCD.                                    
           PERFORM 8200-READ-TWEB-RCD.                                          
                                                                                
      ******************************************************************        
      * CHECK FOR STORE 873 RECORD ON TUPCPLS TABLE                    *        
      ******************************************************************        
       1300-CHECK-FOR-STORE-873.                                                
                                                                                
           MOVE '*1300-CHECK-FOR-STORE-873    *' TO PV-PARAGRAPH-NAME.          
                                                                                
           MOVE ' ' TO PV-TUPCPLS-SW.                                           
           MOVE FS-TWEB-INTR-UPC-ID TO UPCPLS-UPC-ID.                           
                                                                                
           PERFORM                                                              
              VARYING PV-RETRY-CNT                                              
              FROM 1 BY 1                                                       
              UNTIL   PV-RETRY-CNT > PC-MAX-RETRY-CNT                           
                                                                                
               EXEC SQL                                                         
                  SELECT UPC_STAT_CDE                                           
                   INTO :UPCPLS-UPC-STAT-CDE                                    
W38972              FROM TUPCPLS T,                                             
W38972                   XMT_LOC L                                              
W38972            WHERE T.UPC_ID           = :UPCPLS-UPC-ID                     
W38972*             AND (STR_NBR           = 873                                
EFC2  *              OR  STR_NBR           = 809)                               
W38972              AND T.STR_NBR          = L.LOC_NBR                          
W38972              AND LOC_TYP_CDE        = 'DS'                               
W38972              AND E_BUS_IND          = 'Y'                                
W38972              AND ((T.EFF_BEG_TMST <= CURRENT TIMESTAMP)                  
W38972              AND ((T.EFF_END_TMST >= CURRENT TIMESTAMP)                  
W38972               OR  (T.EFF_END_TMST IS NULL)))                             
                   WITH UR                                                      
               END-EXEC                                                         
                 EVALUATE TRUE                                                  
                    WHEN SQLCODE = +0                                           
EFC2FX              WHEN SQLCODE = -811                                         
                       MOVE PC-MAX-RETRY-CNT TO PV-RETRY-CNT                    
                       MOVE 'Y' TO PV-TUPCPLS-SW                                
                    WHEN SQLCODE = +100                                         
                       MOVE PC-MAX-RETRY-CNT TO PV-RETRY-CNT                    
                       MOVE 'N' TO PV-TUPCPLS-SW                                
                    WHEN SQLCODE = -911                                         
                    WHEN SQLCODE = -913                                         
                       CONTINUE                                                 
                    WHEN OTHER                                                  
                       PERFORM 9999-DB2-ABEND                                   
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF (SQLWARN0 NOT = SPACES                                            
             OR (SQLCODE > 0 AND SQLCODE NOT = +100)                            
EFC2FX       OR (SQLCODE < 0 AND SQLCODE NOT = -811))                           
               PERFORM 9999-DB2-ABEND                                           
           END-IF.                                                              
                                                                                
           IF PV-TUPCPLS-SW = 'N'                                               
              MOVE '20' TO FS126-STATUS-CODE                                    
              MOVE '25' TO FS126-RCP-STATUS-CODE                                
              PERFORM 1200-PROCESS-STATUS                                       
           ELSE                                                                 
              PERFORM 8200-READ-TWEB-RCD                                        
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * INITIALIZE NEW E-COMM FILE FIELDS                              *        
      ******************************************************************        
       1400-INITIALIZE-FIELDS.                                                  
                                                                                
           MOVE ZEROS TO FS126-CUR-ONHAND-QTY,                                  
                         FS126-LAST-RCVD-DATE,                                  
                         FS126-YTD-SALES-UNIT-QTY,                              
                         FS126-CUR-SALES-UNIT-QTY,                              
                         FS126-WK1-SALES-UNIT-QTY,                              
                         FS126-WK2-SALES-UNIT-QTY,                              
                         FS126-WK3-SALES-UNIT-QTY,                              
                         FS126-WK4-SALES-UNIT-QTY,                              
                         FS126-WK5-SALES-UNIT-QTY,                              
                         FS126-ON-ORD-QTY,                                      
                         FS126-FIRST-RCVD-DATE,                                 
                         FS126-FIRST-SALE-DATE,                                 
                         FS126-LAST-SALE-DATE,                                  
                         FS126-HNG-FLD-SEQ-NBR,                                 
                         FS126-STAT-CHNGD-DATE,                                 
                         FS126-NBR-STORES.                                      
                                                                                
           MOVE '00' TO FS126-ORD-BGHT-YY,                                      
                        FS126-ORD-BGHT-MM.                                      
                                                                                
           MOVE 'Y' TO FS126-FS-RPT-IND,                                        
                       FS126-EBUS-LOC-IND.                                      
                                                                                
           MOVE '90' TO FS126-RECORD-INDICATOR.                                 
                                                                                
           MOVE '873' TO FS126-LOC-NBR.                                         
                                                                                
      ******************************************************************        
      * POPULATE DATA FROM UNLOAD FILE                                 *        
      ******************************************************************        
       1500-POPULATE-FROM-UNLOAD-FILE.                                          
                                                                                
           MOVE FS-TWEB-ASRT-BEG-DTE    TO FS126-ASRT-BEG-DTE.                  
           MOVE FS-TWEB-ASRT-END-DTE    TO FS126-ASRT-END-DTE.                  
           MOVE FS-TWEB-PRD-GP-NBR      TO WF-WORK-PRD-GP-NBR-9-8.              
           MOVE WF-WORK-PRD-GP-NBR-X-8  TO FS126-PRD-GP-NBR.                    
           MOVE FS-TWEB-PRD-GP-IND      TO FS126-PRD-GP-IND.                    
           MOVE FS-TWEB-ITM-NBR         TO FS126-ITEM-NMBR.                     
           MOVE FS-TWEB-DEPT            TO WF-WORK-DEPT-9-4.                    
           MOVE WF-WORK-DEPT-X-3        TO FS126-MERCH-DEPT-NMBR.               
           MOVE FS-TWEB-CLASS           TO WF-WORK-CLASS-9-4.                   
           MOVE WF-WORK-CLASS-X-2       TO FS126-MAJOR-CLASS.                   
           MOVE FS-TWEB-SUBCLASS        TO WF-WORK-SUBCLASS-9-4.                
           MOVE WF-WORK-SUBCLASS-X-2    TO FS126-SUBCLASS.                      
           MOVE FS-TWEB-SKU             TO WF-WORK-SKU-9-8.                     
           MOVE WF-WORK-SKU-X-8         TO FS126-SKU.                           
W34688     MOVE FS-TWEB-WEB-EXCLV-IND TO FS126-WEB-EXCLV-IND.                   
W34688     MOVE FS-TWEB-KOHLS-SIZE-CDE TO FS126-KOHLS-SIZE-CDE.                 
                                                                                
      ******************************************************************        
      * POPULATE DATA FROM TABLE TMDSEAR                               *        
      ******************************************************************        
       1600-POPULATE-FROM-TMDSEAR.                                              
                                                                                
           MOVE '*1600-POPULATE-FROM-TMDSEAR  *' TO PV-PARAGRAPH-NAME.          
                                                                                
           MOVE FS126-MERCH-DEPT-NMBR TO MDSEAR-DEPT-NBR.                       
                                                                                
           PERFORM                                                              
              VARYING PV-RETRY-CNT                                              
              FROM 1 BY 1                                                       
              UNTIL   PV-RETRY-CNT > PC-MAX-RETRY-CNT                           
                                                                                
               EXEC SQL                                                         
                  SELECT A.GMA_NBR                                              
                       , B.DMA_NBR                                              
                       , C.BYR_NBR                                              
                   INTO :MDSEAR-GMA-NBR                                         
                       ,:MDSEAR-DMA-NBR                                         
                       ,:MDSEAR-BYR-NBR                                         
                    FROM TMDSEAR A                                              
                       , TMDSEAR B                                              
                       , TMDSEAR C                                              
                 WHERE   A.OPERCO_NBR = '03'                                    
                   AND   A.GMA_NBR    = B.GMA_NBR                               
                   AND   B.DMA_NBR    = C.DMA_NBR                               
                   AND   A.MDSELV_CDE = 'GMA'                                   
                   AND   B.MDSELV_CDE = 'DMA'                                   
                   AND   C.MDSELV_CDE = 'DPT'                                   
                   AND ((A.STRT_DTE  <= CURRENT DATE)                           
                   AND ((A.END_DTE   >= CURRENT DATE)                           
                    OR  (A.END_DTE   IS NULL)))                                 
                   AND ((B.STRT_DTE  <= CURRENT DATE)                           
                   AND ((B.END_DTE   >= CURRENT DATE)                           
                    OR  (B.END_DTE   IS NULL)))                                 
                   AND ((C.STRT_DTE  <= CURRENT DATE)                           
                   AND ((C.END_DTE   >= CURRENT DATE)                           
                    OR  (C.END_DTE   IS NULL)))                                 
                   AND   C.DEPT_NBR   = :MDSEAR-DEPT-NBR                        
                  WITH UR                                                       
               END-EXEC                                                         
                 EVALUATE TRUE                                                  
                    WHEN SQLCODE = +0                                           
                       MOVE PC-MAX-RETRY-CNT TO PV-RETRY-CNT                    
                    WHEN SQLCODE = +100                                         
                       MOVE PC-MAX-RETRY-CNT TO PV-RETRY-CNT                    
                       MOVE '  '  TO MDSEAR-GMA-NBR                             
                       MOVE '  '  TO MDSEAR-DMA-NBR                             
                       MOVE '   ' TO MDSEAR-BYR-NBR                             
                    WHEN SQLCODE = -911                                         
                    WHEN SQLCODE = -913                                         
                       CONTINUE                                                 
                    WHEN OTHER                                                  
                       PERFORM 9999-DB2-ABEND                                   
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF (SQLWARN0 NOT = SPACES                                            
             OR (SQLCODE > 0 AND SQLCODE NOT = +100)                            
             OR SQLCODE < 0)                                                    
               PERFORM 9999-DB2-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * POPULATE DATA FROM TABLES TUPC AND TUPCPLS                     *        
      ******************************************************************        
       1700-POPULATE-FROM-TUPC-PLS.                                             
                                                                                
           MOVE '*1700-POPULATE-FROM-TUPC-PLS *' TO PV-PARAGRAPH-NAME.          
                                                                                
           MOVE FS-TWEB-INTR-UPC-ID TO UPC-UPC-ID.                              
                                                                                
           PERFORM                                                              
              VARYING PV-RETRY-CNT                                              
              FROM 1 BY 1                                                       
              UNTIL   PV-RETRY-CNT > PC-MAX-RETRY-CNT                           
                                                                                
               EXEC SQL                                                         
                  SELECT A.SUB_CL_MDSEAR_ID                                     
                       , A.VS_ID                                                
                       , A.KOHLS_SIZE_CDE                                       
                       , A.CSTM_CLOR_DESC                                       
                       , A.GP_CDE                                               
                       , A.RPLNMT_IND                                           
                       , A.KY_ITM_IND                                           
                       , A.SEA_SEQ_NBR                                          
                       , A.INTR_UPC_DESC                                        
                       , B.UNT_COST_AMT                                         
                       , B.UNT_RTL_AMT                                          
                       , B.MEITGP_NBR                                           
                   INTO :UPC-SUB-CL-MDSEAR-ID                                   
                       ,:UPC-VS-ID                                              
                       ,:UPC-KOHLS-SIZE-CDE                                     
                       ,:UPC-CSTM-CLOR-DESC                                     
                       ,:UPC-GP-CDE                                             
                       ,:UPC-RPLNMT-IND                                         
                       ,:UPC-KY-ITM-IND                                         
                       ,:UPC-SEA-SEQ-NBR                                        
                       ,:UPC-INTR-UPC-DESC                                      
                       ,:UPCPLS-UNT-COST-AMT                                    
                       ,:UPCPLS-UNT-RTL-AMT                                     
                       ,:UPCPLS-MEITGP-NBR                                      
                    FROM TUPC A                                                 
                       , TUPCPLS B                                              
                   WHERE   A.UPC_ID        = :UPC-UPC-ID                        
                     AND   A.UPC_TYP_CDE   = 'I'                                
                     AND   A.UPC_ID        = B.UPC_ID                           
                     AND ((A.EFF_BEG_DTE  <= CURRENT DATE)                      
                     AND ((A.EFF_END_DTE  >= CURRENT DATE)                      
                      OR  (A.EFF_END_DTE  IS NULL)))                            
                     AND   B.STR_NBR = 0                                        
                     AND ((B.EFF_BEG_TMST <= CURRENT TIMESTAMP)                 
                     AND ((B.EFF_END_TMST >= CURRENT TIMESTAMP)                 
                      OR  (B.EFF_END_TMST IS NULL)))                            
                    WITH UR                                                     
               END-EXEC                                                         
                 EVALUATE TRUE                                                  
                    WHEN SQLCODE = +0                                           
                       MOVE PC-MAX-RETRY-CNT TO PV-RETRY-CNT                    
                    WHEN SQLCODE = +100                                         
                       MOVE PC-MAX-RETRY-CNT TO PV-RETRY-CNT                    
                       MOVE ZEROS        TO UPC-SUB-CL-MDSEAR-ID                
                       MOVE ZEROS        TO UPC-VS-ID                           
                       MOVE ZEROS        TO UPC-KOHLS-SIZE-CDE                  
                       MOVE ZEROS        TO UPCPLS-MEITGP-NBR                   
                       MOVE '          ' TO UPC-CSTM-CLOR-DESC                  
                       MOVE '   '        TO UPC-GP-CDE                          
                       MOVE ' '          TO UPC-RPLNMT-IND                      
                       MOVE ' '          TO UPC-KY-ITM-IND                      
                       MOVE ZEROS        TO UPC-SEA-SEQ-NBR                     
                       MOVE '                              '                    
                                         TO UPC-INTR-UPC-DESC                   
                       MOVE ZEROS        TO UPCPLS-UNT-COST-AMT                 
                       MOVE ZEROS        TO UPCPLS-UNT-RTL-AMT                  
                    WHEN SQLCODE = -911                                         
                    WHEN SQLCODE = -913                                         
                       CONTINUE                                                 
                    WHEN OTHER                                                  
                       PERFORM 9999-DB2-ABEND                                   
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF (SQLWARN0 NOT = SPACES                                            
             OR (SQLCODE > 0 AND SQLCODE NOT = +100)                            
             OR SQLCODE < 0)                                                    
               PERFORM 9999-DB2-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * POPULATE DATA FROM TABLES TMDARVS, TVNDSTL AND TENTNME         *        
      ******************************************************************        
       1800-POPULATE-VENDOR-STYLE.                                              
                                                                                
           MOVE '*1800-POPULATE-VENDOR-STYLE  *' TO PV-PARAGRAPH-NAME.          
                                                                                
           MOVE PV-HOLD-SUB-CL-MDSEAR-ID TO MDARVS-SUB-CL-MDSEAR-ID.            
           MOVE PV-HOLD-VS-ID            TO MDARVS-VS-ID.                       
                                                                                
           PERFORM WITH TEST AFTER                                              
              VARYING PV-RETRY-CNT                                              
              FROM 1 BY 1                                                       
              UNTIL (PV-RETRY-CNT > PC-MAX-RETRY-CNT                            
                        OR SQLCODE = 0                                          
                        OR SQLCODE = +100)                                      
                                                                                
               EXEC SQL                                                         
                  SELECT A.STKR_NBR                                             
                       , A.LBL_SEQ_NBR                                          
                       , B.VS_NBR                                               
                       , C.ENT_NAME_DESC                                        
                   INTO :MDARVS-STKR-NBR                                        
                       ,:MDARVS-LBL-SEQ-NBR                                     
                       ,:VNDSTL-VS-NBR                                          
                       ,:ENTNME-ENT-NAME-DESC                                   
                    FROM TMDARVS A                                              
                       , TVNDSTL B                                              
                       , TENTNME C                                              
                 WHERE   A.SUB_CL_MDSEAR_ID = :MDARVS-SUB-CL-MDSEAR-ID          
                   AND   A.VS_ID            = :MDARVS-VS-ID                     
                   AND   A.VS_ID            = B.VS_ID                           
                   AND   B.ENT_ID           = C.ENT_ID                          
                   AND ((B.EFF_BEG_DTE     <= CURRENT DATE)                     
                   AND ((B.EFF_END_DTE     >= CURRENT DATE)                     
                    OR  (B.EFF_END_DTE     IS NULL)))                           
                   AND   C.TYPE_CDE         = '01'                              
                  WITH UR                                                       
               END-EXEC                                                         
                 EVALUATE TRUE                                                  
                    WHEN SQLCODE = +0                                           
                       CONTINUE                                                 
                    WHEN SQLCODE = +100                                         
                       MOVE ZEROS TO MDARVS-STKR-NBR                            
                       MOVE ZEROS TO MDARVS-LBL-SEQ-NBR                         
                       MOVE '                    ' TO VNDSTL-VS-NBR             
                       MOVE '                              '                    
                                             TO ENTNME-ENT-NAME-DESC            
                    WHEN SQLCODE = -911                                         
                    WHEN SQLCODE = -913                                         
                       CONTINUE                                                 
                    WHEN OTHER                                                  
                       PERFORM 9999-DB2-ABEND                                   
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-CNT > PC-MAX-RETRY-CNT                                  
               PERFORM 9999-DB2-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * POPULATE DATA FROM TABLE TSIZE                                 *        
      ******************************************************************        
       1900-POPULATE-FROM-TSIZE.                                                
                                                                                
           MOVE '*1900-POPULATE-FROM-TSIZE    *' TO PV-PARAGRAPH-NAME.          
                                                                                
           MOVE PV-HOLD-KOHLS-SIZE-CDE TO SIZE-KOHLS-SIZE-CDE.                  
                                                                                
           PERFORM                                                              
              VARYING PV-RETRY-CNT                                              
              FROM 1 BY 1                                                       
              UNTIL   PV-RETRY-CNT > PC-MAX-RETRY-CNT                           
                                                                                
               EXEC SQL                                                         
                  SELECT KOHLS_DESC                                             
                    INTO :SIZE-KOHLS-DESC                                       
                    FROM TSIZE                                                  
                   WHERE KOHLS_SIZE_CDE = :SIZE-KOHLS-SIZE-CDE                  
                     AND ((EFF_BEG_DTE  <= CURRENT DATE)                        
                     AND ((EFF_END_DTE  >= CURRENT DATE)                        
                      OR  (EFF_END_DTE  IS NULL)))                              
                    WITH UR                                                     
               END-EXEC                                                         
                 EVALUATE TRUE                                                  
                    WHEN SQLCODE = +0                                           
                       MOVE PC-MAX-RETRY-CNT TO PV-RETRY-CNT                    
                    WHEN SQLCODE = +100                                         
                       MOVE PC-MAX-RETRY-CNT TO PV-RETRY-CNT                    
                       MOVE ZEROS TO SIZE-KOHLS-SIZE-CDE                        
                    WHEN SQLCODE = -911                                         
                    WHEN SQLCODE = -913                                         
                       CONTINUE                                                 
                    WHEN OTHER                                                  
                       PERFORM 9999-DB2-ABEND                                   
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF (SQLWARN0 NOT = SPACES                                            
             OR (SQLCODE > 0 AND SQLCODE NOT = +100)                            
             OR SQLCODE < 0)                                                    
               PERFORM 9999-DB2-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * POPULATE DATA FROM TABLE TMITGPL                               *        
      ******************************************************************        
       2000-POPULATE-FROM-TMITGPL.                                              
                                                                                
           MOVE '*2000-POPULATE-FROM-TMITGPL  *' TO PV-PARAGRAPH-NAME.          
                                                                                
           MOVE PV-HOLD-MEITGP-NBR TO MITGPL-MEITGP-NBR.                        
                                                                                
           PERFORM                                                              
              VARYING PV-RETRY-CNT                                              
              FROM 1 BY 1                                                       
              UNTIL   PV-RETRY-CNT > PC-MAX-RETRY-CNT                           
                                                                                
               EXEC SQL                                                         
                  SELECT MITGPL_QTY                                             
                       , GP_AMT                                                 
                   INTO :MITGPL-MITGPL-QTY                                      
                       ,:MITGPL-GP-AMT                                          
                    FROM TMITGPL                                                
                   WHERE MEITGP_NBR   = :MITGPL-MEITGP-NBR                      
                     AND OPERCO_NBR   = '03'                                    
                     AND ((BEG_DTE     <= CURRENT DATE)                         
                     AND ((END_DTE     >= CURRENT DATE)                         
                      OR  (END_DTE     IS NULL)))                               
                    WITH UR                                                     
               END-EXEC                                                         
                 EVALUATE TRUE                                                  
                    WHEN SQLCODE = +0                                           
                       MOVE PC-MAX-RETRY-CNT TO PV-RETRY-CNT                    
                    WHEN SQLCODE = +100                                         
                       MOVE PC-MAX-RETRY-CNT TO PV-RETRY-CNT                    
                       MOVE ZEROS TO MITGPL-MITGPL-QTY                          
                       MOVE ZEROS TO MITGPL-GP-AMT                              
                    WHEN SQLCODE = -911                                         
                    WHEN SQLCODE = -913                                         
                       CONTINUE                                                 
                    WHEN OTHER                                                  
                       PERFORM 9999-DB2-ABEND                                   
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF (SQLWARN0 NOT = SPACES                                            
             OR (SQLCODE > 0 AND SQLCODE NOT = +100)                            
             OR SQLCODE < 0)                                                    
               PERFORM 9999-DB2-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 8000-OPEN-FILES.                                               *        
      ******************************************************************        
       8000-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  FS-ECOMM-FILE                                            
                       FS-TWEB-FILE                                             
           OPEN OUTPUT FS-NEW-ECOMM-FILE.                                       
                                                                                
      ******************************************************************        
      * 8100-READ-ECOMM-RCD                                            *        
      ******************************************************************        
       8100-READ-ECOMM-RCD.                                                     
                                                                                
           READ FS-ECOMM-FILE                                                   
              AT END                                                            
                 MOVE 999999999999999  TO PV-HOLD-ECOMM-ITEM-NBR                
                 SET PS-ECOMM-EOF TO TRUE.                                      
                                                                                
           IF NOT PS-ECOMM-EOF                                                  
             ADD +1                    TO PT-ECOMM-RCD-READ                     
             MOVE FS-ECOMM-ITM-NBR     TO PV-HOLD-ECOMM-ITEM-NBR                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 8200-READ-TWEB-RCD                                             *        
      ******************************************************************        
       8200-READ-TWEB-RCD.                                                      
                                                                                
           READ FS-TWEB-FILE                                                    
              AT END                                                            
                 MOVE 999999999999999  TO PV-HOLD-TWEB-ITEM-NBR                 
                 SET PS-TWEB-EOF       TO TRUE.                                 
                                                                                
           IF NOT PS-TWEB-EOF                                                   
               ADD +1                  TO PT-TWEB-RCD-READ                      
               MOVE FS-TWEB-ITM-NBR    TO PV-HOLD-TWEB-ITEM-NBR                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 8300-WRITE-NEW-ECOMM-RCD                                       *        
      ******************************************************************        
       8300-WRITE-NEW-ECOMM-RCD.                                                
                                                                                
           WRITE FS-NEW-ECOMM-DTL FROM                                          
              FS126-ECOM-SKU-EXTRACT-RECORD.                                    
                                                                                
           ADD +1                      TO PT-ECOMM-RCD-WRTTN.                   
                                                                                
      ******************************************************************        
      * 8900-CLOSE-FILES.                                              *        
      ******************************************************************        
       8900-CLOSE-FILES.                                                        
                                                                                
           CLOSE FS-ECOMM-FILE                                                  
                 FS-TWEB-FILE                                                   
                 FS-NEW-ECOMM-FILE.                                             
                                                                                
      ******************************************************************        
      * 9000-DISPLAY-PGM-TOTALS.                                       *        
      ******************************************************************        
       9000-DISPLAY-PGM-TOTALS.                                                 
                                                                                
           DISPLAY '** FSKUT149 - PROGRAM TOTALS **'.                           
           DISPLAY '** FSKUT149 - ' PT-ECOMM-RCD-READ                           
               ' NUMBER OF E-COMMERCE RECORDS READ **'.                         
           DISPLAY '** FSKUT149 - ' PT-TWEB-RCD-READ                            
               ' NUMBER OF TWEBITM RECORDS READ **'.                            
           DISPLAY '** FSKUT149 - ' PT-TWEB-RCD-USED                            
               ' NUMBER OF TWEBITM RECORDS PROCESSED **'.                       
           DISPLAY '** FSKUT149 - ' PT-ECOMM-TWEB-MATCH                         
               ' NUMBER OF MATCHED E-COMM AND TWEB RECORDS **'.                 
           DISPLAY '** FSKUT149 - ' PT-ECOMM-TWEB-MISMATCH                      
               ' NUMBER OF MISMATCHED E-COMM NOT = TWEB RECORDS **'.            
           DISPLAY '** FSKUT149 - ' PT-ECOMM-RCD-WRTTN                          
               ' NUMBER OF MERGED RECORDS WRITTEN **'.                          
      /                                                                         
      ******************************************************************        
      * PERFORMS THE ABEND FOR THE PROGRAM IN THE                      *        
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                *        
      ******************************************************************        
       9999-DB2-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = FSKUT149'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-OPER-ATTEMPTED.                      
                                                                                
           PERFORM 9000-DISPLAY-PGM-TOTALS.                                     
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
      * FORMAT.                                                        *        
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
           CALL 'ILBOABN0'             USING ABEND-CODE.                        
                                                                                
