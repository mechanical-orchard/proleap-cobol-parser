       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              IMKUT082.                                       
       AUTHOR.                  BOB FORD.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            09/01/04.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *         NON-REGIONAL CLEARANCE PRICED SKUS                     *        
      *                   SUMMARY EXTRACT                              *        
      *                                                                *        
      *   THIS PROGRAM READS AN INPUT FILE OF NON-RCP SKUS WHICH DRIVES*        
      *   PROCESSING FOR THIS PROGRAM.  THE INPUT FILE IS CREATED BY   *        
      *   SORTING THE ITEM DOMAIN FILE, INCLUDING ONLY THOSE ITEMS     *        
      *   WITH A REGN-PRIC-IND = 'N'.                                  *        
      *                                                                *        
      *   A SECOND INPUT FILE (THE ITEM-LOC DOMAIN FILE) IS READ FOR   *        
      *   THE INPUT ITEM.  IF THERE IS A STORE SPECIFIC RECORD THE     *        
      *   INFORMATION IS USED FOR THAT ITEM/STORE.  IF NOT, THE        *        
      *   CORPORATE-LEVEL INFORMATION IS USED FOR THE ITEM/STORE.      *        
      *                                                                *        
      ******************************************************************        
      *  CHANGE LOG:                                                            
      *  10/04/04 - BOB FORD                                                    
      *             (WR #24794) NEW PROGRAM - CHANGED PROGRAM IMKUT081          
      ******************************************************************        
      *  CHANGE LOG:                                                            
      *  12/22/04 - BOB FORD (WR #24794)                                        
      *             CORRECTD COLOR LOGIC IN 2300-DETERMINE-TICKET.              
      ******************************************************************        
      *  01/07/05 - BOB FORD                                                    
      *          (PITS PM00725410) RECOMPILE TO PICK UP NEW VERSION             
      *                         OF COPYBOOK IMRD090.                            
      ******************************************************************        
      *  09/27/05 - D. THEEL  WR #29434                                         
      *             INCREASE OUT01 LRECL AND ADD LOGIC TO MOVE NEW              
      *             COLUMNS ORIG-CLR-DTE AND MKDN-CDE.                          
      ******************************************************************        
      *  06/03/09 - T. KONTAROVICH WR #36383                                    
      *    THE RECORD'S LENGTH OF A SKU/LOCATION EXTRACT FILE                   
      *    (COPYBOOK IMRD057) IS CHANGED TO 173 BYTES.                          
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT NONRCP-SKU-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT ITEM-LOC-FILE                                                 
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT NONRCP-SUMMARY-FILE-OUT                                       
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  NONRCP-SKU-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  NONRCP-SKU-REC                  PIC  X(80).                          
                                                                                
       FD  ITEM-LOC-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
W36383*    RECORD CONTAINS 250 CHARACTERS                                       
W36383     RECORD CONTAINS 173 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS.                                            
W36383*01  ITEM-LOC-REC                    PIC  X(250).                         
W36383 01  ITEM-LOC-REC                    PIC  X(173).                         
                                                                                
       FD  NONRCP-SUMMARY-FILE-OUT                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 64 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS NONRCP-SUMMARY-REC.                                   
       01  NONRCP-SUMMARY-REC     PIC X(64).                                    
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CD-CURRENT-DATE.                                                     
           05  CD-CURRENT-YEAR         PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-MONTH        PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-DAY          PIC 9(2)     VALUE ZERO.                 
                                                                                
       01  CD-CURRENT-DATE-ISO.                                                 
           05  CD-CURRENT-CC-ISO       PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-YEAR-ISO     PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-MONTH-ISO    PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-DAY-ISO      PIC 9(2)     VALUE ZERO.                 
                                                                                
       01  CD-CURRENT-DATE-TS.                                                  
           05  CD-CURRENT-CC-TS        PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-YEAR-TS      PIC 9(2)     VALUE ZERO.                 
           05  FILLER                  PIC X(1)     VALUE '-'.                  
           05  CD-CURRENT-MONTH-TS     PIC 9(2)     VALUE ZERO.                 
           05  FILLER                  PIC X(1)     VALUE '-'.                  
           05  CD-CURRENT-DAY-TS       PIC 9(2)     VALUE ZERO.                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-RED                  PIC X(01)   VALUE 'R'.                   
           05  PC-YELLOW               PIC X(01)   VALUE 'Y'.                   
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-TIMESTAMP.                                                    
               10 CD-TS-FMT-DATE       PIC X(10)     VALUE SPACE.               
               10 FILLER               PIC X(16)     VALUE                      
                                                '-00.00.00.000000'.             
           05  PV-PROGRAM-NAME         PIC X(8)        VALUE 'IMKUT082'.        
           05  PV-PARAGRAPH-NAME       PIC X(25)       VALUE SPACE.             
           05  PV-ABEND-MESSAGE        PIC X(50)       VALUE SPACE.             
           05  PV-OPER-ATTEMPTED       PIC X(30)       VALUE SPACES.            
           05  PV-ITEM-NUMBER          PIC S9(15)V     VALUE ZERO               
                                                       COMP-3.                  
           05  PV-STATUS-CODE          PIC X(02)      VALUE SPACES.             
           05  PV-SKUS-READ-CNT        PIC 9(7)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-ITEM-LOC-READ-CNT    PIC 9(7)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-RECS-WRITTEN-CNT     PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
      *    05  PV-OUTPUT-MAX           PIC S9(7)      VALUE +100000             
      *                                                COMP.                    
      *    05  PV-OUTPUT-CNT           PIC 9(7)       VALUE ZERO                
      *                                                COMP-3.                  
      *    05  PV-INPUT-CNT-A        PIC 9(7)       VALUE ZERO                  
      *                                                COMP-3.                  
      *    05  PV-LOC-NBR              PIC S9(04)     VALUE +0 COMP.            
      *    05  PV-9-4                  PIC 9(04).                               
      *    05  PV-X-4 REDEFINES PV-9-4 PIC X(04).                               
                                                                                
       01  PS-SWITCHES.                                                         
           05  PS-NONRCP-SKU-EOF-SW        PIC X       VALUE 'N'.               
               88  PS-NONRCP-SKU-EOF                   VALUE 'Y'.               
           05  PS-ITEM-LOC-EOF-SW          PIC X       VALUE 'N'.               
               88  PS-ITEM-LOC-EOF                     VALUE 'Y'.               
                                                                                
       01  ABEND-CODE                          PIC S9(04)  VALUE +4003          
                                                           COMP SYNC.           
      *****************************************************************         
      *     VALID LOCATION TABLE                                      *         
      *****************************************************************         
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE ITEM/LOC DOMAIN FILE                               
      ******************************************************************        
           COPY IMRD057.                                                        
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE IM RCP SKU LIST FILE                               
      ******************************************************************        
           COPY IMRD090.                                                        
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE OUTPUT RCP SKU LIST FILE                           
      ******************************************************************        
           COPY IMRD092.                                                        
                                                                                
           EJECT                                                                
       LINKAGE SECTION.                                                         
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* START OF IMKUT082               '.                        
           DISPLAY '**********************************'.                        
                                                                                
           PERFORM 1000-HOUSEKEEPING.                                           
                                                                                
           PERFORM 2000-READ-NONRCP-SKU-FILE.                                   
           IF PS-NONRCP-SKU-EOF                                                 
              DISPLAY '***********************************************'         
              DISPLAY '*** NO REGIONAL CLEARANCE PRICED SKUS FOUND ***'         
              DISPLAY '***********************************************'         
           ELSE                                                                 
              PERFORM 2100-READ-ITEM-LOC-FILE                                   
              PERFORM 2200-PROCESS-NONRCP-SKUS                                  
                  UNTIL PS-NONRCP-SKU-EOF                                       
           END-IF.                                                              
                                                                                
           PERFORM 1900-EOJ.                                                    
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* IMKUT082 PROGRAM STATISTICS:    '.                        
           DISPLAY '**********************************'.                        
           DISPLAY '* TOTAL SKUS READ: ' PV-SKUS-READ-CNT.                      
           DISPLAY '* TOTAL ITEM-LOC READ: ' PV-ITEM-LOC-READ-CNT.              
           DISPLAY '* TOTAL OUTPUT RECORDS: '                                   
                                         PV-RECS-WRITTEN-CNT.                   
           DISPLAY '**********************************'.                        
           DISPLAY '**********************************'.                        
           DISPLAY '* END OF IMKUT082                 '.                        
           DISPLAY '**********************************'.                        
                                                                                
           STOP RUN.                                                            
      ******************************************************************        
      *    OPEN FILES & DETERMINE CURRENT DATE CCYY-MM-DD.                      
      ******************************************************************        
       1000-HOUSEKEEPING.                                                       
                                                                                
           OPEN INPUT  NONRCP-SKU-FILE                                          
                       ITEM-LOC-FILE                                            
                OUTPUT NONRCP-SUMMARY-FILE-OUT.                                 
                                                                                
           INITIALIZE IM057-ITEM-SKU-LOC-DOMAIN-REC.                            
           ACCEPT CD-CURRENT-DATE FROM DATE.                                    
      **** DETERMINE CENTURY ****                                               
           IF CD-CURRENT-YEAR > '80'                                            
               MOVE '19' TO CD-CURRENT-CC-ISO                                   
           ELSE                                                                 
               MOVE '20' TO CD-CURRENT-CC-ISO                                   
           END-IF.                                                              
           MOVE CD-CURRENT-CC-ISO TO CD-CURRENT-CC-TS                           
           MOVE CD-CURRENT-YEAR   TO CD-CURRENT-YEAR-ISO                        
                                     CD-CURRENT-YEAR-TS.                        
           MOVE CD-CURRENT-MONTH  TO CD-CURRENT-MONTH-ISO                       
                                     CD-CURRENT-MONTH-TS.                       
           MOVE CD-CURRENT-DAY    TO CD-CURRENT-DAY-ISO                         
                                     CD-CURRENT-DAY-TS.                         
           MOVE CD-CURRENT-DATE-TS TO  CD-TS-FMT-DATE.                          
           DISPLAY 'TIMESTAMP = ' PV-TIMESTAMP.                                 
                                                                                
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
       1900-EOJ.                                                                
                                                                                
           CLOSE NONRCP-SKU-FILE                                                
                 ITEM-LOC-FILE                                                  
                 NONRCP-SUMMARY-FILE-OUT.                                       
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE NONRCP-SKU-FILE.                            
      ******************************************************************        
       2000-READ-NONRCP-SKU-FILE.                                               
                                                                                
           READ NONRCP-SKU-FILE                                                 
               INTO IM090-RCP-SKU-LIST-REC                                      
               AT END                                                           
                   SET PS-NONRCP-SKU-EOF TO TRUE                                
               NOT AT END                                                       
                   ADD +1 TO PV-SKUS-READ-CNT.                                  
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE ITEM-LOC-FILE.                              
      ******************************************************************        
       2100-READ-ITEM-LOC-FILE.                                                 
                                                                                
           READ ITEM-LOC-FILE                                                   
               INTO IM057-ITEM-SKU-LOC-DOMAIN-REC                               
               AT END                                                           
                   MOVE ZERO TO IM057-INTR-UPC-ID                               
                   SET PS-ITEM-LOC-EOF TO TRUE                                  
               NOT AT END                                                       
                   ADD +1 TO PV-ITEM-LOC-READ-CNT.                              
      ******************************************************************        
      *    PROCESS ALL NON-REGIONAL CLEARANCE PRICED SKUS.                      
      ******************************************************************        
       2200-PROCESS-NONRCP-SKUS.                                                
                                                                                
           IF IM057-INTR-UPC-ID   = IM090-INTR-UPC-ID                           
               MOVE IM090-SKU              TO IM092-SKU                         
               MOVE IM090-ITM-NBR          TO IM092-ITM-NBR                     
               MOVE IM057-INTR-UPC-ID      TO IM092-INTR-UPC-ID                 
               MOVE IM057-SKU-STAT-CDE     TO IM092-STAT-CDE                    
               MOVE IM057-UNT-RTL-AMT      TO IM092-UNT-RTL-AMT                 
               MOVE IM057-STR-NBR          TO IM092-STR-NBR                     
               MOVE IM057-UNT-RTL-CHG-DTE  TO IM092-UNT-RTL-CHG-DTE             
               MOVE IM057-STAT-CHG-DTE     TO IM092-STAT-CHG-DTE                
               MOVE IM057-ORIG-CLR-DTE     TO IM092-ORIG-CLR-DTE                
               MOVE IM057-MKDN-CDE         TO IM092-MKDN-CDE                    
               PERFORM 2300-DETERMINE-TICKET                                    
               PERFORM 5000-WRITE-OUTPUT-REC                                    
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-ITEM-LOC-FILE.                                     
           IF IM057-INTR-UPC-ID   > IM090-INTR-UPC-ID                           
             OR IM057-INTR-UPC-ID   = ZERO                                      
               PERFORM 2000-READ-NONRCP-SKU-FILE.                               
      ******************************************************************        
      *  USE DATES TO DETERMINE TICKET COLOR                                    
      ******************************************************************        
       2300-DETERMINE-TICKET.                                                   
           IF IM057-UNT-RTL-CHG-DTE > IM057-STAT-CHG-DTE                        
               MOVE PC-RED             TO IM092-TICKET-COLOR                    
           ELSE                                                                 
               MOVE PC-YELLOW          TO IM092-TICKET-COLOR                    
           END-IF.                                                              
      ******************************************************************        
      *    WRITE SUMMARY RECORDS - ONE RECORD FOR ALL STORES                    
      ******************************************************************        
       5000-WRITE-OUTPUT-REC.                                                   
                                                                                
           WRITE NONRCP-SUMMARY-REC FROM IM092-RCP-SKU-REC                      
           ADD +1 TO PV-RECS-WRITTEN-CNT.                                       
      *    ADD +1 TO PV-OUTPUT-CNT.                                             
      *    IF PV-OUTPUT-CNT > PV-OUTPUT-MAX                                     
      *        DISPLAY 'IM092-UPC = ' PV-UPC-INTR-UPC-ID                        
      *        MOVE IM092-STR-NBR TO PV-9-4                                     
      *        DISPLAY 'IM092-LOC = ' PV-X-4                                    
      *        DISPLAY 'OUTPUT RECS = ' PV-RECS-WRITTEN-CNT                     
      *        MOVE ZERO TO PV-OUTPUT-CNT.                                      
      *                                                                         
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPER-ATTEMPTED.                    
           DISPLAY '**********************************'.                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
