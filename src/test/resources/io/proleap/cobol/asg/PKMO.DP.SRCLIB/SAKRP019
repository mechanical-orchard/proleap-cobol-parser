000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.             SAKRP019.                                        
000600 AUTHOR.                 MATTHEW STIFF.                                   
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN            MAY 1999.                                        
000900 DATE-COMPILED.                                                           
001000                                                                          
001100*****************************************************************         
001200* * * *>>>   THIS PROGRAM MUST BE COMPILED COBOL/390   <<<* * * *         
001300*****************************************************************         
001400*                                                                         
001500*  THIS PROGRAM WILL CREATE A DAILY KOHLS CREDIT CORRECTIONS              
001510*  AUDIT REPORT.                                                          
001600*                                                                         
001700*   INPUT: INP01 - CONTAINS POS DATE, WHICH IS USED AS THE                
001800*                  PROCESS DATE TO CREATE THE REPORT.                     
001900*  OUTPUT: RPT01 - REPORT SHOWING KOHLS CREDIT CORRECTIONS                
001910*                  BY SALES DATE.                                         
002000*                                                                         
002100*****************************************************************         
002200*                                                                         
002300*  THIS PROGRAM USES THE TEPDKEY TABLE TO JOIN THE TEPTRN,                
002400*  TEPTND, AND TEPCRD TABLES.  THE TEPDKEY TABLE CONTAINS ALL             
002500*  KEY INFORMATION FOR TRANSACTIONS THAT HAVE BEEN PROCESSED FOR          
002600*  A PROCESSING DAY THAT DO NOT HAVE SALES DATES FOR THAT                 
002700*  PROCESSING DAY.  THE MOST COMMON EXAMPLE OF THIS IS A SALES            
002701*  CORRECTION.                                                            
002702*                                                                         
002703*  THIS PROGRAM ALSO JOINS THE TEPDKEY TABLE WITH THE TEPPAY AND          
002704*  TEPTRN TABLES TO OBTAIN THE KOHLS CREDIT PAYMENT CORRECTIONS.          
002705*                                                                         
002706*  THE REPORT USES DATA FROM THE TEPTND, TEPCRD, TEPPAY, AND              
002707*  TEPTRN TABLES.  A DETAIL LINE IS WRITTEN FOR EACH ROW FETCHED.         
002708*  THE REPORT BREAKS ON STORE NUMBER AND ALSO SALES DATE.                 
002800*  WHEN A NEW SALES DATE IS ENCOUNTERED, THE DATE IS PASSED TO THE        
002900*  CALENDAR ROUTINE TO BE CHANGED INTO GREGORIAN FORMAT.                  
003100*                                                                         
003200*  WHEN THE FETCH IS COMPLETE, A TOTAL LINE IS PRINTED AT THE             
003300*  END OF THE REPORT.                                                     
003600*                                                                         
003700*****************************************************************         
003800*                                                                         
003900*  CHANGED MM/DD/YY BY XXXXXXXXXXX                                        
004000*   WR# XXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX           
004100*                                                                         
P1043 ******************************************************************        
P1043 * 07/10/2015 ACCENTURE                                                    
P1043 *     CHG0118696                                                          
P1043 *    -ADDED A SIX DIGIT BIN NUMBER BEFORE ACCOUNT NUMBER TO               
P1043 *     HANDLE BOTH 12 DIGIT AND 18 DIGIT ACCOUNT NUMBER                    
004200******************************************************************        
P10237*                                                                         
P10237*  CHANGED    :  05/25/15 BY COGNIZANT                                    
P10237*  WR#        :  CHG0104429                                               
P10237*  CHANGE MADE:  MODIFIED THE PROGRAM TO DETOKENIZE CARD-NUMBER           
P10237*                AND WRITE INTO REPORT AS CLEAR TEXT AS PART OF           
P10237*                COSA DATA SECURITY PROJECT.                              
004300*****************************************************************         
COBRND*                                                                         
COBRND*  CHANGED    :  03/28/23 BY BARB SCHULTZ                                 
COBRND*  CHG#       :  CHG00286203                                              
COBRND*  CHANGE MADE:  ADDED THE NEW COBRAND INDICATOR ON TEPPAY IN THE         
COBRND*                KOHLS-PYMT-CURSOR AND CHECKED THE INDICATOR FOR          
COBRND*                'N' SO IT WILL ONLY PULL KOHLS CHARGE ACCOUNTS.          
COBRND*                REMOVED PROTEGRITY AND THE DETOKENIZATION OF THE         
COBRND*                ACCOUNT NUMBERS.  ONLY DISPLAYING THE LAST FOUR          
COBRND*                DIGITS WITH THE REST OF THE FIELD SHOWING                
COBRND*                ASTERISKS ON THE REPORT.                                 
004300*****************************************************************         
004400*****************************************************************         
004500 ENVIRONMENT DIVISION.                                                    
004600*****************************************************************         
004700                                                                          
004800 CONFIGURATION SECTION.                                                   
004900                                                                          
005000 SOURCE-COMPUTER.        IBM-3090.                                        
005100                                                                          
005200 OBJECT-COMPUTER.        IBM-3090.                                        
005300                                                                          
005400 INPUT-OUTPUT SECTION.                                                    
005500                                                                          
005600 FILE-CONTROL.                                                            
005700                                                                          
005800     SELECT POS-DATE-CARD-FILE                                            
005900         ASSIGN TO INP01.                                                 
006000                                                                          
006100     SELECT KOHLS-REPORT-FILE                                             
006200         ASSIGN TO RPT01.                                                 
006300                                                                          
006400 EJECT                                                                    
006500******************************************************************        
006600 DATA DIVISION.                                                           
006700******************************************************************        
006800                                                                          
006900 FILE SECTION.                                                            
007000                                                                          
007100 FD  POS-DATE-CARD-FILE                                                   
007200     RECORDING MODE IS F                                                  
007300     BLOCK CONTAINS 0 RECORDS                                             
007400     LABEL RECORDS ARE STANDARD.                                          
007500                                                                          
007600 01  POS-DATE-CARD-RECORD            PIC X(80).                           
007700                                                                          
007800 FD  KOHLS-REPORT-FILE                                                    
007900     RECORDING MODE IS F                                                  
008000     BLOCK CONTAINS 0  RECORDS.                                           
008100                                                                          
008200 01  KOHLS-REPORT-RECORD             PIC  X(132).                         
008300                                                                          
008400 EJECT                                                                    
008500******************************************************************        
008600 WORKING-STORAGE SECTION.                                                 
008700******************************************************************        
008800 01  FILLER                            PIC X(50)  VALUE                   
008900     ' *** WORKING STORAGE STARTS HERE FOR SAKRP019 *** '.                
009000                                                                          
009100******************************************************************        
009200* RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                            
009300******************************************************************        
009400 01  CC-CONTROL-CARD.                                                     
009500     05  CC-CONTROL-CARD-DISPLAY.                                         
009600         10  CC-CONTROL-NAME     PIC X(14)    VALUE SPACE.                
009700             88  CC-VALID-CONTROL-NAME        VALUE                       
009800                 'P.O.S. DATE = '.                                        
009900         10  CC-POS-DATE-MMDDYY  PIC 9(6)     VALUE ZERO.                 
010500     05  FILLER                  PIC X(60)    VALUE SPACE.                
010510                                                                          
010600******************************************************************        
010700*  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)                     
010800******************************************************************        
010900     COPY DPWS500.                                                        
011000                                                                          
011100 01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO              
011200                                                  COMP SYNC.              
011300     88  AC-CONTROL-CARD-ERROR                    VALUE +4003.            
011400     88  AC-DB2-ERROR                             VALUE +4013.            
011500     88  AC-CALENDAR-ROUTINE-ERROR                VALUE +4019.            
011600                                                                          
P10237 01  ABEND-CODE                        PIC S9(04) VALUE ZERO              
P10237                                                  COMP SYNC.              
011700                                                                          
011800 EJECT                                                                    
011900 01  PS-PROGRAM-SWITCHES.                                                 
012000     05  PS-END-OF-CURSOR-SW           PIC X(01)  VALUE 'N'.              
012100         88  PS-END-OF-CURSOR                     VALUE 'Y'.              
012200     05  PS-END-OF-PYMT-CURSOR-SW      PIC X(01)  VALUE 'N'.              
012300         88  PS-END-OF-PYMT-CURSOR                VALUE 'Y'.              
012600     05  PS-END-OF-CARD-FILE-SW        PIC X(01)  VALUE 'N'.              
012700         88  PS-END-OF-CARD-FILE                  VALUE 'Y'.              
012800     05  PS-USE-CURSOR-SW              PIC X(01)  VALUE SPACE.            
012900         88  PS-USE-REGULAR-CURSOR                VALUE 'R'.              
013000         88  PS-USE-PYMT-CURSOR                   VALUE 'P'.              
P10237     05  PS-TENDER-SW                  PIC X(01)  VALUE 'N'.              
P10237         88  PS-TENDER                            VALUE 'Y'.              
P10237         88  PS-ACCOUNT                           VALUE 'N'.              
013400                                                                          
013500 01  PC-CONSTANTS.                                                        
013600     05  PC-LINE-LIMIT               PIC S9(04)    VALUE +60.             
013700     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
013800                                                   COMP SYNC.             
013900     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
014000                                                   'SAKRP019'.            
014100     05  PC-REPORT-NBR-1             PIC  9(01)    VALUE 1.               
014201     05  PC-KOHLS-TENDER-TYPE        PIC  X(02)    VALUE '04'.            
014230     05  PC-SYSTEM-CORRECTION        PIC  X(01)    VALUE 'S'.             
014240     05  PC-REVERSAL-CORRECTION      PIC  X(01)    VALUE 'R'.             
014250     05  PC-NEGATIVE-ONE             PIC S9(01)    VALUE -1.              
014300                                                                          
014400 EJECT                                                                    
014500 01  PV-MISC-COUNT-FIELDS            COMP-3.                              
014600     05  PV-PAGE-CNT                 PIC S9(05)    VALUE ZERO.            
014700     05  PV-LINE-CNT                 PIC S9(03)    VALUE ZERO.            
014800     05  PV-LINE-CNT-AHEAD           PIC S9(03)    VALUE ZERO.            
014900     05  PV-CURSOR-CNT               PIC S9(07)    VALUE ZERO.            
014910     05  PV-PYMT-CURSOR-CNT          PIC S9(07)    VALUE ZERO.            
014920                                                                          
014930 01  PV-DB2-VARIABLES.                                                    
014931     05  PV-DB2-PARTN-NBR            PIC S9(04)    COMP                   
014932                                                   VALUE ZERO.            
014940     05  PV-DB2-STR-NBR              PIC S9(04)    COMP                   
014950                                                   VALUE ZERO.            
014960     05  PV-DB2-RGST-ID              PIC S9(04)    COMP                   
014970                                                   VALUE ZERO.            
014980     05  PV-DB2-TRN-NBR              PIC S9(04)    COMP                   
014990                                                   VALUE ZERO.            
014991     05  PV-DB2-STRT-TM              PIC  X(08)    VALUE SPACE.           
014992     05  PV-DB2-SLS-CORR-SEQ-NBR     PIC S9(04)    COMP                   
014993                                                   VALUE ZERO.            
014994     05  PV-DB2-TRN-STAT-CDE         PIC  X(02)    VALUE SPACE.           
014995     05  PV-DB2-SLS-DTE              PIC  X(10)    VALUE SPACE.           
014996     05  PV-DB2-ASSOC-ID             PIC S9(09)    COMP                   
014997                                                   VALUE ZERO.            
015000                                                                          
015100******************************************************************        
015200*  ACCUMULATORS TO HOLD STORE AND COMPANY DOLLAR AMOUNTS.                 
015300******************************************************************        
015400 01  PA-PROGRAM-ACCUMULATORS.                                             
015500     05  PA-STORE-SALES-AMOUNT       PIC S9(07)V99  VALUE ZERO            
015510                                         COMP-3.                          
015600     05  PA-STORE-RETURN-AMOUNT      PIC S9(07)V99  VALUE ZERO            
015610                                         COMP-3.                          
015620     05  PA-STORE-PYMT-AMOUNT        PIC S9(07)V99  VALUE ZERO            
015630                                         COMP-3.                          
015700     05  PA-COMPANY-SALES-AMOUNT     PIC S9(07)V99  VALUE ZERO            
015710                                         COMP-3.                          
015800     05  PA-COMPANY-RETURN-AMOUNT    PIC S9(07)V99  VALUE ZERO            
015810                                         COMP-3.                          
015820     05  PA-COMPANY-PYMT-AMOUNT      PIC S9(07)V99  VALUE ZERO            
015830                                         COMP-3.                          
015900                                                                          
016000 01  PV-MISC-FIELDS.                                                      
016100     05  PV-PRINT-RECORD             PIC X(132)    VALUE SPACE.           
016200     05  PV-LINE-SPACING             PIC S9(04)    VALUE ZERO             
016300                                                   COMP SYNC.             
016400     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.           
016500     05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.           
016600     05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.           
016700     05  PV-DB2-OPERATION            PIC  X(30)    VALUE SPACE.           
016900     05  PV-EDIT-MASK                PIC  Z,ZZZ,ZZ9.                      
017200     05  PV-HOLD-STORE               PIC  S9(04)  COMP VALUE ZERO.        
017300     05  PV-HOLD-SALES-DATE          PIC   X(10)  VALUE SPACE.            
017310     05  PV-AMOUNT                   PIC  S9(07)V99 COMP-3                
017320                                          VALUE ZERO.                     
017321     05  PV-AMOUNT-FORMATTED         PIC   ZZ,ZZ9.99-.                    
P10237     05  PV-TNDR-ID                  PIC   X(33)  VALUE SPACE.            
P10237     05  PV-TNDR-SPACES              PIC   9(09)  VALUE ZERO.             
P10237     05  PV-TNDR-LEN                 PIC   9(09)  VALUE ZERO.             
P10237     05  PV-PARTN-NBR                PIC   9(04)  VALUE ZERO.             
P10237     05  PV-STRT-TM                  PIC   X(08)  VALUE SPACE.            
017340     05  PV-CARD-NBR-16.                                                  
017350         10  PV-CARD-NBR-16-FIRST-4  PIC   X(04)  VALUE SPACE.            
017360         10  FILLER                  PIC   X(01)  VALUE '-'.              
017370         10  PV-CARD-NBR-16-SECOND-4 PIC   X(04)  VALUE SPACE.            
017380         10  FILLER                  PIC   X(01)  VALUE '-'.              
017390         10  PV-CARD-NBR-16-THIRD-4  PIC   X(04)  VALUE SPACE.            
017391         10  FILLER                  PIC   X(01)  VALUE '-'.              
017392         10  PV-CARD-NBR-16-LAST-4   PIC   X(04)  VALUE SPACE.            
017393     05  PV-CARD-NBR-13.                                                  
017394         10  PV-CARD-NBR-13-FIRST-4  PIC   X(04)  VALUE SPACE.            
017395         10  FILLER                  PIC   X(01)  VALUE '-'.              
017396         10  PV-CARD-NBR-13-SECOND-3 PIC   X(03)  VALUE SPACE.            
017397         10  FILLER                  PIC   X(01)  VALUE '-'.              
017398         10  PV-CARD-NBR-13-THIRD-2  PIC   X(02)  VALUE SPACE.            
COBRND         10  PV-CARD-NBR-13-THIRD-1  PIC   X(01)  VALUE SPACE.            
017399         10  FILLER                  PIC   X(01)  VALUE '-'.              
017400         10  PV-CARD-NBR-13-LAST-3   PIC   X(03)  VALUE SPACE.            
017410         10  FILLER                  PIC   X(03)  VALUE SPACE.            
COBRND     05  PV-CARD-NBR-12.                                                  
COBRND         10  PV-CARD-NBR-12-FIRST-4  PIC   X(04)  VALUE SPACE.            
COBRND         10  FILLER                  PIC   X(01)  VALUE '-'.              
COBRND         10  PV-CARD-NBR-12-SECOND-3 PIC   X(03)  VALUE SPACE.            
COBRND         10  PV-CARD-NBR-12-THIRD-1  PIC   X(01)  VALUE SPACE.            
COBRND         10  FILLER                  PIC   X(01)  VALUE '-'.              
COBRND         10  PV-CARD-NBR-12-THIRD-2  PIC   X(02)  VALUE SPACE.            
COBRND         10  FILLER                  PIC   X(01)  VALUE ' '.              
COBRND         10  PV-CARD-NBR-12-LAST-2   PIC   X(02)  VALUE SPACE.            
COBRND         10  FILLER                  PIC   X(04)  VALUE SPACE.            
COBRND     05  PV-CARD-NBR-18.                                                  
COBRND         10  PV-CARD-NBR-18-FIRST-3  PIC   X(03)  VALUE SPACE.            
COBRND         10  FILLER                  PIC   X(01)  VALUE '-'.              
COBRND         10  PV-CARD-NBR-18-SECOND-4 PIC   X(04)  VALUE SPACE.            
COBRND         10  FILLER                  PIC   X(01)  VALUE '-'.              
COBRND         10  PV-CARD-NBR-18-THIRD-1  PIC   X(01)  VALUE SPACE.            
COBRND         10  FILLER                  PIC   X(01)  VALUE '-'.              
COBRND         10  PV-CARD-NBR-18-THIRD-2  PIC   X(02)  VALUE SPACE.            
COBRND         10  PV-CARD-NBR-18-LAST-2   PIC   X(02)  VALUE SPACE.            
COBRND         10  FILLER                  PIC   X(04)  VALUE SPACE.            
017800 01  PV-DATE-AND-TIME-FIELDS.                                             
017900     05  PV-POS-CCYY-MM-DD           PIC X(10)     VALUE SPACE.           
018000     05  FILLER REDEFINES PV-POS-CCYY-MM-DD.                              
018100         10  PV-POS-CCYY             PIC X(04).                           
018200         10  FILLER                  PIC X(01).                           
018300         10  PV-POS-MM               PIC X(02).                           
018400         10  FILLER                  PIC X(01).                           
018500         10  PV-POS-DD               PIC X(02).                           
018600     05  PV-CURR-DATE-CCYYMMDD.                                           
018700         10  PV-CURR-DATE-CCYY       PIC  9(04).                          
018800         10  PV-CURR-DATE-MM         PIC  9(02).                          
018900         10  PV-CURR-DATE-DD         PIC  9(02).                          
019000     05  PV-CURR-TIME.                                                    
019100         10  PV-CURR-TIME-HH         PIC  9(02)    VALUE ZERO.            
019200         10  PV-CURR-TIME-MM         PIC  9(02)    VALUE ZERO.            
019300         10  PV-CURR-TIME-SSHH       PIC  9(04)    VALUE ZERO.            
019310     05  PV-POS-DATE-FORMATTED       PIC  X(10)    VALUE SPACE.           
019320     05  PV-SLS-DATE-FORMATTED       PIC  X(10)    VALUE SPACE.           
019400 EJECT                                                                    
019500******************************************************************        
019600*  TOP OF PAGE HEADING LINE FOR PRINTING A 132 CHARACTER REPORT.          
019700******************************************************************        
019800     COPY DPWS132.                                                        
019900                                                                          
020000******************************************************************        
020100*  HEADING LINES FOR THE KOHLS CORRECTIONS REPORT.                        
020200******************************************************************        
020300 01  HL2-HEADING-LINE-2.                                                  
020400     05  FILLER                      PIC  X(35)    VALUE SPACE.           
020500     05  FILLER                      PIC  X(13)    VALUE                  
020600                                          'KOHLS CREDIT '.                
020700     05  FILLER                      PIC  X(12)    VALUE                  
020800                                          'CORRECTIONS '.                 
020900     05  FILLER                      PIC  X(10)    VALUE                  
021000                                          'AUDIT FOR '.                   
021100     05  FILLER                      PIC  X(11)    VALUE                  
021200                                          'PROCESSING '.                  
021300     05  FILLER                      PIC  X(06)    VALUE                  
021400                                          'DATE: '.                       
021500     05  HL2-DATE                    PIC  X(10)    VALUE SPACE.           
022100     05  FILLER                      PIC  X(35)    VALUE SPACE.           
022200                                                                          
022300 01  HL3-HEADING-LINE-3.                                                  
022400     05  FILLER                      PIC  X(59)    VALUE ALL '-'.         
022500     05  FILLER                      PIC  X(09)    VALUE                  
022600                                          ' STORE # '.                    
022700     05  HL3-STORE                   PIC  Z999.                           
023110     05  FILLER                      PIC  X(01)    VALUE SPACE.           
023200     05  FILLER                      PIC  X(59)    VALUE ALL '-'.         
023300                                                                          
023400 01  HL4-HEADING-LINE-4.                                                  
023500     05  FILLER                      PIC  X(42)    VALUE SPACE.           
023600     05  FILLER                      PIC  X(04)    VALUE 'AUTH'.          
023700     05  FILLER                      PIC  X(86)    VALUE SPACE.           
023800                                                                          
023900 01  HL5-HEADING-LINE-5.                                                  
024010     05  FILLER                      PIC  X(10)    VALUE                  
024020                                                   'SALES DATE'.          
024030     05  FILLER                      PIC  X(04)    VALUE SPACE.           
024100     05  FILLER                      PIC  X(05)    VALUE 'REG #'.         
024200     05  FILLER                      PIC  X(02)    VALUE SPACE.           
024300     05  FILLER                      PIC  X(07)    VALUE                  
024310                                                   'TRANS #'.             
024320     05  FILLER                      PIC  X(02)    VALUE SPACE.           
024330     05  FILLER                      PIC  X(08)    VALUE                  
024340                                                   'ASSOC ID'.            
024350     05  FILLER                      PIC  X(02)    VALUE SPACE.           
024360     05  FILLER                      PIC  X(06)    VALUE                  
024370                                                   'NUMBER'.              
024400     05  FILLER                      PIC  X(11)    VALUE SPACE.           
024500     05  FILLER                      PIC  X(06)    VALUE                  
024510                                                   'ACCT #'.              
024600     05  FILLER                      PIC  X(12)    VALUE SPACE.           
024610     05  FILLER                      PIC  X(04)    VALUE                  
024620                                                   'CODE'.                
024630     05  FILLER                      PIC  X(06)    VALUE SPACE.           
024700     05  FILLER                      PIC  X(05)    VALUE                  
024710                                                   'SALES'.               
024800     05  FILLER                      PIC  X(04)    VALUE SPACE.           
024900     05  FILLER                      PIC  X(07)    VALUE                  
024910                                                   'RETURNS'.             
025400     05  FILLER                      PIC  X(04)    VALUE SPACE.           
025500     05  FILLER                      PIC  X(08)    VALUE                  
025501                                                   'PAYMENTS'.            
025502     05  FILLER                      PIC  X(04)    VALUE SPACE.           
025503     05  FILLER                      PIC  X(10)    VALUE                  
025504                                                   'NET AMOUNT'.          
025510     05  FILLER                      PIC  X(05)    VALUE SPACE.           
025691                                                                          
025700 01  HL6-HEADING-LINE-6.                                                  
025800     05  FILLER                      PIC  X(04)    VALUE SPACE.           
025900     05  FILLER                      PIC  X(08)    VALUE                  
026000                                          'STORE # '.                     
026100     05  HL6-STORE                   PIC  Z999.                           
026200     05  FILLER                      PIC  X(11)    VALUE                  
026300                                          ' TOTAL FOR '.                  
026400     05  HL6-DATE                    PIC  X(10)    VALUE SPACE.           
027000     05  FILLER                      PIC  X(44)    VALUE SPACE.           
027110     05  HL6-SALES-TOTAL             PIC  X(10)    VALUE SPACE.           
027200     05  FILLER                      PIC  X(01)    VALUE SPACE.           
027310     05  HL6-RETURN-TOTAL            PIC  X(10)    VALUE SPACE.           
027320     05  FILLER                      PIC  X(02)    VALUE SPACE.           
027330     05  HL6-PYMT-AMOUNT             PIC  X(10)    VALUE SPACE.           
027400     05  FILLER                      PIC  X(04)    VALUE SPACE.           
027810     05  HL6-NET-AMOUNT              PIC  X(10)    VALUE SPACE.           
027900     05  FILLER                      PIC  X(04)    VALUE SPACE.           
028000                                                                          
028100******************************************************************        
028200*  DETAIL LINE PRINTED ON THE REPORT.                                     
028300******************************************************************        
028400 01  DL1-DETAIL-LINE-1.                                                   
028510     05  DL1-SALES-DATE              PIC  X(10)    VALUE SPACE.           
028520     05  FILLER                      PIC  X(04)    VALUE SPACE.           
028600     05  DL1-REGISTER                PIC  9(04)    VALUE ZERO.            
028700     05  FILLER                      PIC  X(04)    VALUE SPACE.           
028800     05  DL1-TRANSACTION             PIC  9(04)    VALUE ZERO.            
028810     05  FILLER                      PIC  X(04)    VALUE SPACE.           
028820     05  DL1-ASSOC-ID                PIC  X(08)    VALUE SPACE.           
028830     05  FILLER                      PIC  X(02)    VALUE SPACE.           
028840     05  DL1-AUTH-NBR                PIC  X(06)    VALUE SPACE.           
028900     05  FILLER                      PIC  X(02)    VALUE SPACE.           
P1043      05  DL1-BIN-NUMBER              PIC  X(06)    VALUE SPACE.           
029100     05  DL1-CARD-NBR                PIC  X(19)    VALUE SPACE.           
029800     05  FILLER                      PIC  X(03)    VALUE SPACE.           
029900     05  DL1-CODE                    PIC  X(01)    VALUE SPACE.           
029901     05  FILLER                      PIC  X(04)    VALUE SPACE.           
029910     05  DL1-SALES-AMOUNT            PIC  X(10)    VALUE SPACE.           
030000     05  FILLER                      PIC  X(01)    VALUE SPACE.           
030110     05  DL1-RETURN-AMOUNT           PIC  X(10)    VALUE SPACE.           
030800     05  FILLER                      PIC  X(02)    VALUE SPACE.           
030810     05  DL1-PYMT-AMOUNT             PIC  X(10)    VALUE SPACE.           
P1043      05  FILLER                      PIC  X(12)    VALUE SPACE.           
030900                                                                          
031000 EJECT                                                                    
031010******************************************************************        
031020*  TOTAL LINE PRINTED AT THE END OF THE REPORT                            
031030******************************************************************        
031040 01 TL1-TOTAL-LINE-1.                                                     
031050    05  FILLER                       PIC X(01) VALUE SPACE.               
031060    05  FILLER                       PIC X(08) VALUE                      
031070                                         'COMPANY '.                      
031080    05  FILLER                       PIC X(10) VALUE                      
031090                                         'TOTAL FOR '.                    
031091    05  FILLER                       PIC X(04) VALUE                      
031092                                         'ALL '.                          
031093    05  FILLER                       PIC X(12) VALUE                      
031094                                         'CORRECTIONS '.                  
031095    05  FILLER                       PIC X(11) VALUE                      
031096                                         'ENTERED ON '.                   
031097    05  TL1-DATE                     PIC X(10) VALUE SPACE.               
031098    05  FILLER                       PIC X(24) VALUE SPACE.               
031099    05  TL1-SALES-AMOUNT             PIC ZZZ,ZZ9.99-.                     
031101    05  TL1-RETURN-AMOUNT            PIC ZZZ,ZZ9.99-.                     
031102    05  FILLER                       PIC X(01) VALUE SPACE.               
031103    05  TL1-PYMT-AMOUNT              PIC ZZZ,ZZ9.99-.                     
031104    05  FILLER                       PIC X(03) VALUE SPACE.               
031105    05  TL1-NET-AMOUNT               PIC ZZZ,ZZ9.99-.                     
031106    05  FILLER                       PIC X(04) VALUE SPACE.               
031108                                                                          
031110******************************************************************        
031200*  WS FIELDS FOR SQLCA MESSAGE ROUTINE                                    
031300******************************************************************        
031400     COPY DPWS004.                                                        
031500 EJECT                                                                    
031600******************************************************************        
031700*  DB2 COMMUNICATION AREA.                                                
031800******************************************************************        
031900     EXEC SQL                                                             
032000          INCLUDE SQLCA                                                   
032100     END-EXEC.                                                            
032200 EJECT                                                                    
032300******************************************************************        
032400*  DB2 TABLE TEPTRN - TRANSACTION TABLE                                   
032500******************************************************************        
032600     EXEC SQL                                                             
032700          INCLUDE TEPTRN                                                  
032800     END-EXEC.                                                            
032900 EJECT                                                                    
032300******************************************************************        
032400*  DB2 TABLE TEPPART - PARTITION TABLE                                    
032500******************************************************************        
032600     EXEC SQL                                                             
032700          INCLUDE TEPPART                                                 
032800     END-EXEC.                                                            
032900 EJECT                                                                    
033000******************************************************************        
033100*  DB2 TABLE TEPTND - TENDER TABLE                                        
033200******************************************************************        
033300     EXEC SQL                                                             
033400          INCLUDE TEPTND                                                  
033500     END-EXEC.                                                            
033600 EJECT                                                                    
033700******************************************************************        
033800*  DB2 TABLE TEPCRD - CREDIT TENDER TABLE                                 
033900******************************************************************        
034000     EXEC SQL                                                             
034100          INCLUDE TEPCRD                                                  
034200     END-EXEC.                                                            
034300 EJECT                                                                    
034301******************************************************************        
034302*  DB2 TABLE TEPDKEY - CURRENT PROCESSING DAY KEYS                        
034303******************************************************************        
034304     EXEC SQL                                                             
034305          INCLUDE TEPDKEY                                                 
034306     END-EXEC.                                                            
034307 EJECT                                                                    
034308******************************************************************        
034309*  DB2 TABLE TEPSAT - SALES AUDIT USER ID TABLE                           
034310******************************************************************        
034320     EXEC SQL                                                             
034330          INCLUDE TEPSAT                                                  
034340     END-EXEC.                                                            
034350 EJECT                                                                    
034360******************************************************************        
034370*  DB2 TABLE TEPPAY - KOHLS PAYMENT TABLE                                 
034380******************************************************************        
034390     EXEC SQL                                                             
034391          INCLUDE TEPPAY                                                  
034392     END-EXEC.                                                            
034393 EJECT                                                                    
036500******************************************************************        
036600*  CURSOR TO RETRIEVE KOHLS CORRECTION DATA                               
036700******************************************************************        
036800     EXEC SQL                                                             
036900       DECLARE KOHLS-CURSOR CURSOR FOR                                    
037010            SELECT  T.PARTN_NBR                                           
037020                   ,T.STR_NBR                                             
037100                   ,T.RGST_ID                                             
037200                   ,T.TRN_NBR                                             
037210                   ,T.STRT_TM                                             
037300                   ,T.SLS_CORR_SEQ_NBR                                    
037310                   ,TRN_STAT_CDE                                          
037400                   ,T.SLS_DTE                                             
037500                   ,ASSOC_ID                                              
037600                   ,TNDR_ID                                               
037700                   ,TNDR_AMT                                              
037710                   ,TNDR_ID_LEN_NBR                                       
037800                   ,AUTH_NBR                                              
037900            FROM    TEPDKEY D                                             
037910                   ,TEPTRN T                                              
038000                   ,TEPTND N                                              
038100                   ,TEPCRD C                                              
038100                   ,TEPPART A                                             
038200            WHERE   D.PRCG_DTE         = :EPDKEY-PRCG-DTE                 
038200              AND   D.SLS_DTE          = A.SLS_DTE                        
038200              AND   A.PARTN_NBR        = T.PARTN_NBR                      
038300              AND   D.STR_NBR          = T.STR_NBR                        
038400              AND   D.RGST_ID          = T.RGST_ID                        
038500              AND   D.TRN_NBR          = T.TRN_NBR                        
038600              AND   D.STRT_TM          = T.STRT_TM                        
038700              AND   D.SLS_CORR_SEQ_NBR = T.SLS_CORR_SEQ_NBR               
038701              AND   T.PARTN_NBR        = N.PARTN_NBR                      
038702              AND   T.STR_NBR          = N.STR_NBR                        
038703              AND   T.RGST_ID          = N.RGST_ID                        
038704              AND   T.TRN_NBR          = N.TRN_NBR                        
038705              AND   T.STRT_TM          = N.STRT_TM                        
038706              AND   T.SLS_CORR_SEQ_NBR = N.SLS_CORR_SEQ_NBR               
038800              AND   N.PARTN_NBR        = C.PARTN_NBR                      
038900              AND   N.STR_NBR          = C.STR_NBR                        
039000              AND   N.RGST_ID          = C.RGST_ID                        
039100              AND   N.TRN_NBR          = C.TRN_NBR                        
039200              AND   N.STRT_TM          = C.STRT_TM                        
039300              AND   N.SLS_CORR_SEQ_NBR = C.SLS_CORR_SEQ_NBR               
039500              AND   N.TNDR_SEQ_NBR     = C.TNDR_SEQ_NBR                   
039801              AND   D.SLS_CORR_SEQ_NBR <> 0                               
039802              AND   VOID_ABRT_CDE      = 'N'                              
039803              AND   TRN_STAT_CDE       IN ('V', 'R', 'L', 'Z')            
039804              AND   TNDR_TYP_CDE       = :PC-KOHLS-TENDER-TYPE            
039806              AND   TNDR_VOID_IND      = 'N'                              
039807              AND   STTLMNT_PRCD_CDE   = 'P'                              
039900           ORDER BY T.STR_NBR                                             
040000                   ,T.SLS_DTE                                             
040100                   ,T.RGST_ID                                             
040200                   ,T.TRN_NBR                                             
040300           WITH UR                                                        
040400     END-EXEC.                                                            
040401                                                                          
040410******************************************************************        
040420*  CURSOR TO RETRIEVE KOHLS PAYMENT CORRECTION DATA                       
040430******************************************************************        
040431                                                                          
040440     EXEC SQL                                                             
040450       DECLARE KOHLS-PYMT-CURSOR CURSOR FOR                               
040470            SELECT  T.PARTN_NBR                                           
040471                   ,T.STR_NBR                                             
040480                   ,T.RGST_ID                                             
040490                   ,T.TRN_NBR                                             
040491                   ,T.STRT_TM                                             
040492                   ,T.SLS_CORR_SEQ_NBR                                    
040493                   ,TRN_STAT_CDE                                          
040494                   ,T.SLS_DTE                                             
040495                   ,ASSOC_ID                                              
040496                   ,KC_ACCT_NBR                                           
040497                   ,PAYT_AMT                                              
040498                   ,AUTH_NBR                                              
040500            FROM    TEPDKEY D                                             
040501                   ,TEPTRN T                                              
040502                   ,TEPPAY P                                              
040502                   ,TEPPART A                                             
040504            WHERE   D.PRCG_DTE         = :EPDKEY-PRCG-DTE                 
040504              AND   D.SLS_DTE          = A.SLS_DTE                        
040504              AND   A.PARTN_NBR        = T.PARTN_NBR                      
040505              AND   D.STR_NBR          = T.STR_NBR                        
040506              AND   D.RGST_ID          = T.RGST_ID                        
040507              AND   D.TRN_NBR          = T.TRN_NBR                        
040508              AND   D.STRT_TM          = T.STRT_TM                        
040509              AND   D.SLS_CORR_SEQ_NBR = T.SLS_CORR_SEQ_NBR               
040510              AND   T.PARTN_NBR        = P.PARTN_NBR                      
040511              AND   T.STR_NBR          = P.STR_NBR                        
040512              AND   T.RGST_ID          = P.RGST_ID                        
040513              AND   T.TRN_NBR          = P.TRN_NBR                        
040514              AND   T.STRT_TM          = P.STRT_TM                        
040515              AND   T.SLS_CORR_SEQ_NBR = P.SLS_CORR_SEQ_NBR               
040523              AND   D.SLS_CORR_SEQ_NBR <> 0                               
040524              AND   VOID_ABRT_CDE      = 'N'                              
040525              AND   TRN_STAT_CDE       IN ('V', 'R', 'L', 'Z')            
COBRND              AND   COBRND_IND         = 'N'                              
040526              AND   STTLMNT_PRCD_CDE   = 'P'                              
040529           ORDER BY T.STR_NBR                                             
040530                   ,T.SLS_DTE                                             
040531                   ,T.RGST_ID                                             
040532                   ,T.TRN_NBR                                             
040533           WITH UR                                                        
040534     END-EXEC.                                                            
040535                                                                          
040600*****************************************************************         
040700* PROCEDURE DIVISION.                                           *         
040800*****************************************************************         
040900 PROCEDURE DIVISION.                                                      
041000                                                                          
041100******************************************************************        
041200* 0000-MAINLINE-PARAGRAPH                                                 
041300*      PRODUCE KOHLS CORRECTIONS REPORT                                   
041400******************************************************************        
041500 0000-MAINLINE-PARAGRAPH.                                                 
041600                                                                          
041700     PERFORM 1000-INITIALIZE-PROGRAM.                                     
041800                                                                          
041900     PERFORM 2000-PRODUCE-REPORT-LINES                                    
042000         UNTIL (PS-END-OF-CURSOR AND PS-END-OF-PYMT-CURSOR).              
044000                                                                          
044100     IF (PV-CURSOR-CNT             GREATER THAN ZERO)                     
044200       OR (PV-PYMT-CURSOR-CNT      GREATER THAN ZERO)                     
075320         PERFORM 5400-PRINT-STORE-TOTAL-LINE                              
075330     END-IF.                                                              
075331                                                                          
075332     PERFORM 4000-PRINT-REPORT-TOTALS.                                    
075333                                                                          
075334     DISPLAY '****************************************'                   
075335     DISPLAY '*************** SAKRP019 ***************'                   
075336     DISPLAY '****************************************'                   
075337     MOVE PV-CURSOR-CNT          TO PV-EDIT-MASK                          
075338     INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'                  
075339     DISPLAY 'CORRECTION ROWS FETCHED........'                            
075340         PV-EDIT-MASK.                                                    
075341     MOVE PV-PYMT-CURSOR-CNT       TO PV-EDIT-MASK.                       
075342     INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'.                 
075343     DISPLAY 'PAYMENT ROWS FETCHED...........'                            
075344         PV-EDIT-MASK.                                                    
075345     DISPLAY '****************************************'                   
075346     DISPLAY '****************************************'                   
075347     DISPLAY '****************************************'                   
075348                                                                          
075349     STOP RUN.                                                            
075350                                                                          
075351 EJECT                                                                    
075352                                                                          
075353******************************************************************        
075354* 1000-INITIALIZE-PROGRAM                                                 
075355*     OPEN FILES, PROCESS CONTROL CARD, FORMAT REPORT HEADINGS,           
075356*     OPEN KOHLS-CUROSR, ATTEMPT FIRST FETCH OF CURSOR                    
075357******************************************************************        
075358 1000-INITIALIZE-PROGRAM.                                                 
075359                                                                          
075360     OPEN INPUT  POS-DATE-CARD-FILE                                       
075361          OUTPUT KOHLS-REPORT-FILE.                                       
075362                                                                          
075363     PERFORM 1100-PROCESS-CONTROL-CARD.                                   
075364                                                                          
075365     PERFORM 1200-FORMAT-HEADINGS.                                        
075366                                                                          
075367     PERFORM 1300-PREPARE-FOR-REPORT.                                     
075368                                                                          
075369******************************************************************        
075370* 1100-PROCESS-CONTROL-CARD                                               
075371*     READ CONTROL CARD TO GET POS DATE IN MMDDYY FORMAT.  CALL           
075372*     DATE ROUTINE TO CONVERT DATE TO A CCYY-MM-DD PROCESS DATE.          
075373******************************************************************        
075374 1100-PROCESS-CONTROL-CARD.                                               
075375                                                                          
075376     PERFORM 1110-READ-POS-DATE-CARD-FILE.                                
075377     CLOSE POS-DATE-CARD-FILE.                                            
075378     IF PS-END-OF-CARD-FILE                                               
075379         MOVE '1100-PROCESS-CONTROL-CARD'  TO PV-PARAGRAPH-NAME           
075380         MOVE 'NO CONTROL CARD TO PROCESS' TO PV-OPERATION-MSG-1          
075381         SET AC-CONTROL-CARD-ERROR TO TRUE                                
075382         PERFORM 9999-ABEND                                               
075383     END-IF.                                                              
075384     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
075385             ' - CONTROL CARD = (' CC-CONTROL-CARD-DISPLAY  ')'.          
075386     PERFORM 1115-CONVERT-INPUT-DATE.                                     
075387 EJECT                                                                    
075388                                                                          
075389******************************************************************        
075390* 1110-READ-POS-DATE-CARD-FILE                                            
075391*     READ DATE CONTROL CARD.                                             
075392******************************************************************        
075393 1110-READ-POS-DATE-CARD-FILE.                                            
075394     READ POS-DATE-CARD-FILE INTO CC-CONTROL-CARD                         
075395        AT END                                                            
075396           SET PS-END-OF-CARD-FILE TO TRUE.                               
075397                                                                          
075398******************************************************************        
075399* 1115-CONVERT-INPUT-DATE                                                 
075400*     CALL KOHLS CALENDAR ROUTINE:                                        
075401*         PASS: CONTROL CARD POS DATE (MMDDYY FORMAT).                    
075402*       RETURN: ACTUAL CALENDAR DB2 ISO DATE (CCYY-MM-DD FORMAT).         
075403******************************************************************        
075404                                                                          
075405 1115-CONVERT-INPUT-DATE.                                                 
075406                                                                          
075407     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
075408                                                                          
075409     SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                           
075410     SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                           
075411     SET  DPG52-LK-DTE-GREG            TO TRUE.                           
075412     MOVE CC-POS-DATE-MMDDYY           TO DPG52-LK-DATE-INPUT.            
075413     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
075414                                             DPG54 DPG55 DPG56.           
075415     EVALUATE TRUE                                                        
075416         WHEN DPG54-SEVERE-ERROR                                          
075417         WHEN DPG54-DATE-INVALID                                          
075418             MOVE '1115-CONVERT-INPUT-DATE'                               
075419                                   TO PV-PARAGRAPH-NAME                   
075420             MOVE 'ERROR CONVERTING INPUT CARD DATE'                      
075421                                   TO PV-OPERATION-MSG-1                  
075422             SET AC-CALENDAR-ROUTINE-ERROR                                
075423                                   TO TRUE                                
075424             MOVE DPG54-ERROR-MESSAGE                                     
075425                                   TO PV-OPERATION-MSG-2                  
075426             PERFORM 9999-ABEND                                           
075427     END-EVALUATE.                                                        
075428                                                                          
075428     MOVE DPG55-DB2-ISO-DATE-FMT   TO PV-POS-CCYY-MM-DD                   
075428                                      EPDKEY-PRCG-DTE.                    
075429     MOVE DPG55-GREG-CC-DATE-FMT   TO PV-POS-DATE-FORMATTED.              
075430 EJECT                                                                    
075431                                                                          
075432******************************************************************        
075433* 1200-FORMAT-HEADINGS                                                    
075434*     GET CURRENT DATE AND TIME FROM SYSTEM.                              
075435*     FORMAT REPORT FIELDS IN THE TOP PAGE HEADING LINE.                  
075436*     FORMAT HEADING LINE 2 WITH CONVERTED POS DATE.                      
075437******************************************************************        
075438 1200-FORMAT-HEADINGS.                                                    
075439                                                                          
075440     MOVE FUNCTION CURRENT-DATE (1:8)                                     
075441                                   TO PV-CURR-DATE-CCYYMMDD.              
075442     ACCEPT PV-CURR-TIME FROM TIME.                                       
075443                                                                          
075444     MOVE PV-CURR-DATE-MM          TO DP132-RUN-MONTH.                    
075445     MOVE PV-CURR-DATE-DD          TO DP132-RUN-DAY.                      
075446     MOVE PV-CURR-DATE-CCYY        TO DP132-RUN-YEAR.                     
075447                                                                          
075448     MOVE PV-CURR-TIME-HH          TO DP132-RUN-HOUR.                     
075449     MOVE PV-CURR-TIME-MM          TO DP132-RUN-MINUTE.                   
075450     MOVE PC-CURRENT-PROGRAM-NAME  TO DP132-PROGRAM-NAME.                 
075451     MOVE PC-REPORT-NBR-1          TO DP132-REPORT-NUMBER.                
075452                                                                          
075453     MOVE PV-POS-DATE-FORMATTED    TO HL2-DATE.                           
075454 EJECT                                                                    
075455                                                                          
075479******************************************************************        
075480* 1300-PREPARE FOR REPORT                                                 
075481*     - OPEN THE CURSOR                                                   
075482*     - FETCH FIRST RECORD                                                
075484******************************************************************        
075485 1300-PREPARE-FOR-REPORT.                                                 
075486                                                                          
075487     PERFORM 8000-PRINT-HEADINGS.                                         
075488     PERFORM 2100-OPEN-KOHLS-CURSOR.                                      
075489     PERFORM 2150-OPEN-KOHLS-PYMT-CURSOR.                                 
075490     PERFORM 2200-FETCH-KOHLS-CURSOR.                                     
075491     PERFORM 2250-FETCH-KOHLS-PYMT-CURSOR.                                
075492     IF PS-END-OF-CURSOR AND PS-END-OF-PYMT-CURSOR                        
075493         DISPLAY '*** NO DATA PRESENT FOR KOHLS CORR ***'                 
075494                 '*** ON SALES DATE '                                     
075495                 PV-POS-DATE-FORMATTED                                    
075496     ELSE                                                                 
075497         PERFORM 2500-PROCESS-FIRST-RECORD                                
075498     END-IF.                                                              
075499                                                                          
075500******************************************************************        
075501* 2000-PRODUCE-REPORT-LINES                                               
075502*  PRODUCE THE ACTUAL REPORT LINES                                        
075503******************************************************************        
075504 2000-PRODUCE-REPORT-LINES.                                               
075505                                                                          
075506     PERFORM 2050-EVALUATE-CURSOR.                                        
075507     IF PS-USE-REGULAR-CURSOR                                             
075508         IF EPTRN-STR-NBR          = PV-HOLD-STORE                        
075509             CONTINUE                                                     
075510         ELSE                                                             
075511             PERFORM 3000-STORE-BREAK                                     
075512         END-IF                                                           
075513                                                                          
075514         IF EPTRN-SLS-DTE          = PV-HOLD-SALES-DATE                   
075515             CONTINUE                                                     
075516         ELSE                                                             
075517             PERFORM 3500-SALES-DATE-BREAK                                
075518         END-IF                                                           
075519     ELSE                                                                 
075520         IF PV-DB2-STR-NBR         = PV-HOLD-STORE                        
075521             CONTINUE                                                     
075522         ELSE                                                             
075523             PERFORM 3000-STORE-BREAK                                     
075524         END-IF                                                           
075525                                                                          
075526         IF PV-DB2-SLS-DTE         = PV-HOLD-SALES-DATE                   
075527             CONTINUE                                                     
075528         ELSE                                                             
075529             PERFORM 3500-SALES-DATE-BREAK                                
075530         END-IF                                                           
075531     END-IF.                                                              
075532                                                                          
075533     PERFORM 5000-PREPARE-DETAIL-LINE.                                    
075534                                                                          
075535     IF PS-USE-REGULAR-CURSOR                                             
075536         PERFORM 2200-FETCH-KOHLS-CURSOR                                  
075537     ELSE                                                                 
075538         PERFORM 2250-FETCH-KOHLS-PYMT-CURSOR                             
075539     END-IF.                                                              
075540                                                                          
075541******************************************************************        
075542* 2050-EVALUATE-CURSOR.                                                   
075543* EVALUATE THE DATA RETRIEVED FROM THE CURSORS TO DETERMINE WHICH         
075544* ONE TO USE SO THE REPORT REMAINS IN THE PROPER ORDER.                   
075545*                                                                         
075546* FIRST, CHECK TO MAKE SURE ONE OF THE CURSORS IS NOT DONE.  THIS         
075547* WILL INDICATE TO USE THE OTHER ONE.  IF BOTH CURSORS STILL HAVE         
075548* DATA, CHECK TO SEE IF THE PAYMENT STORE NUMBER IS LESS THAN THE         
075549* REGULAR STORE.  IF SO, USE THE LINE FROM THE PAYMENT CURSOR.            
075550*                                                                         
075551* IF THE PAYMENT CURSOR STORE IS EQUAL TO THE REGUALR CURSOR STORE        
075552* THE REGISTER NUMBER MUST BE CHECKED.  THIS WILL DETERMINE THE           
075553* ORDER IN THIS SCENARIO.                                                 
075554*                                                                         
075555* FINALLY, IF THE PAYMENT STORE IS GREATER THAN THE REGUALR STORE         
075556* WE KNOW THE REGULAR DETAIL CURSOR INFORMATION SHOULD BE USED.           
075557******************************************************************        
075558 2050-EVALUATE-CURSOR.                                                    
075559                                                                          
075560     EVALUATE TRUE                                                        
075561         WHEN PS-END-OF-PYMT-CURSOR                                       
075562             SET PS-USE-REGULAR-CURSOR                                    
075563                                   TO TRUE                                
075564                                                                          
075565         WHEN PS-END-OF-CURSOR                                            
075566             SET PS-USE-PYMT-CURSOR                                       
075567                                   TO TRUE                                
075568                                                                          
075569         WHEN OTHER                                                       
075570             EVALUATE TRUE                                                
075571                 WHEN PV-DB2-STR-NBR                                      
075572                                   < EPTRN-STR-NBR                        
075573                     SET PS-USE-PYMT-CURSOR                               
075574                                   TO TRUE                                
075575                                                                          
075576                 WHEN PV-DB2-STR-NBR                                      
075577                                   = EPTRN-STR-NBR                        
075579                     EVALUATE TRUE                                        
075580                         WHEN PV-DB2-RGST-ID                              
075581                                   < EPTRN-RGST-ID                        
075582                             SET PS-USE-PYMT-CURSOR                       
075583                                   TO TRUE                                
075584                                                                          
075585                         WHEN PV-DB2-RGST-ID                              
075586                                   = EPTRN-RGST-ID                        
075587                             IF PV-DB2-TRN-NBR                            
075588                                   < EPTRN-TRN-NBR                        
075589                                 SET PS-USE-PYMT-CURSOR                   
075590                                   TO TRUE                                
075591                             ELSE                                         
075592                                 SET PS-USE-REGULAR-CURSOR                
075593                                   TO TRUE                                
075594                             END-IF                                       
075595                                                                          
075596                         WHEN OTHER                                       
075597                             SET PS-USE-REGULAR-CURSOR                    
075598                                   TO TRUE                                
075599                     END-EVALUATE                                         
075609                                                                          
075610                 WHEN OTHER                                               
075611                     SET PS-USE-REGULAR-CURSOR                            
075612                                   TO TRUE                                
075613                                                                          
075614             END-EVALUATE                                                 
075615     END-EVALUATE.                                                        
075616                                                                          
075617******************************************************************        
075618* 2100-OPEN-KOHLS-CURSOR.                                                 
075619*     OPEN KOHLS CURSOR                                                   
075620******************************************************************        
075621 2100-OPEN-KOHLS-CURSOR.                                                  
075622                                                                          
075623     EXEC SQL                                                             
075624          OPEN KOHLS-CURSOR                                               
075625     END-EXEC.                                                            
075626                                                                          
075627     EVALUATE TRUE                                                        
075628         WHEN SQLCODE = ZERO                                              
075629              CONTINUE                                                    
075630         WHEN SQLWARN0 NOT EQUAL SPACE                                    
075631         WHEN SQLCODE NOT EQUAL ZERO                                      
075632             MOVE '2100-OPEN KOHLS CURSOR'                                
075633                                   TO PV-PARAGRAPH-NAME                   
075634             MOVE 'ERROR ON OPEN OF KOHLS-CURSOR'                         
075635                                   TO PV-DB2-OPERATION                    
075636             PERFORM 9100-SQL-ABEND                                       
075637     END-EVALUATE.                                                        
075638 EJECT                                                                    
075639                                                                          
075640******************************************************************        
075641* 2150-OPEN-KOHLS-PYMT-CURSOR.                                            
075642*     OPEN KOHLS PAYEMNT CURSOR                                           
075643******************************************************************        
075644 2150-OPEN-KOHLS-PYMT-CURSOR.                                             
075645                                                                          
075646     EXEC SQL                                                             
075647          OPEN KOHLS-PYMT-CURSOR                                          
075648     END-EXEC.                                                            
075649                                                                          
075650     EVALUATE TRUE                                                        
075651         WHEN SQLCODE = ZERO                                              
075652              CONTINUE                                                    
075653         WHEN SQLWARN0 NOT EQUAL SPACE                                    
075654         WHEN SQLCODE NOT EQUAL ZERO                                      
075655             MOVE '2150-OPEN-KOHLS-PYMT-CURSOR'                           
075656                                   TO PV-PARAGRAPH-NAME                   
075657             MOVE 'ERROR ON OPEN OF KOHLS-PYMT-CURSOR'                    
075658                                   TO PV-DB2-OPERATION                    
075659             PERFORM 9100-SQL-ABEND                                       
075660     END-EVALUATE.                                                        
075661                                                                          
075662******************************************************************        
075663* 2200-FETCH-SUMMARY-CURSOR                                               
075664*   -FETCH ROWS THAT QUALIFY FOR THE REPORT                               
075665******************************************************************        
075666 2200-FETCH-KOHLS-CURSOR.                                                 
075667                                                                          
075668     EXEC SQL                                                             
075669          FETCH KOHLS-CURSOR                                              
075670           INTO :EPTRN-PARTN-NBR                                          
075671               ,:EPTRN-STR-NBR                                            
075672               ,:EPTRN-RGST-ID                                            
075673               ,:EPTRN-TRN-NBR                                            
075674               ,:EPTRN-STRT-TM                                            
075675               ,:EPTRN-SLS-CORR-SEQ-NBR                                   
075676               ,:EPTRN-TRN-STAT-CDE                                       
075677               ,:EPTRN-SLS-DTE                                            
075678               ,:EPTRN-ASSOC-ID                                           
075679               ,:EPTND-TNDR-ID                                            
075680               ,:EPTND-TNDR-AMT                                           
075681               ,:EPTND-TNDR-ID-LEN-NBR                                    
075682               ,:EPCRD-AUTH-NBR                                           
075683     END-EXEC.                                                            
075684                                                                          
075685     EVALUATE TRUE                                                        
075686         WHEN SQLCODE = ZERO                                              
075687             ADD 1                 TO PV-CURSOR-CNT                       
075688         WHEN SQLCODE = PC-ROW-NOT-FOUND                                  
075689             SET PS-END-OF-CURSOR  TO TRUE                                
075690         WHEN SQLWARN0 NOT EQUAL SPACE                                    
075691         WHEN SQLCODE NOT EQUAL ZERO                                      
075692             MOVE '2200-FETCH-KOHLS-CURSOR'                               
075693                                   TO PV-PARAGRAPH-NAME                   
075694             MOVE 'ERROR ON FETCH OF SETTLEMENT-CURSOR'                   
075695                                   TO PV-DB2-OPERATION                    
075696             PERFORM 9100-SQL-ABEND                                       
075697     END-EVALUATE.                                                        
075698 EJECT                                                                    
075699                                                                          
075700******************************************************************        
075701* 2250-FETCH-KOHLS-PYMT-CURSOR                                            
075702*   -FETCH PAYMENT ROWS THAT QUALIFY FOR THE REPORT                       
075703******************************************************************        
075704 2250-FETCH-KOHLS-PYMT-CURSOR.                                            
075705                                                                          
075706     EXEC SQL                                                             
075707          FETCH KOHLS-PYMT-CURSOR                                         
075708           INTO :PV-DB2-PARTN-NBR                                         
075709               ,:PV-DB2-STR-NBR                                           
075710               ,:PV-DB2-RGST-ID                                           
075711               ,:PV-DB2-TRN-NBR                                           
075712               ,:PV-DB2-STRT-TM                                           
075713               ,:PV-DB2-SLS-CORR-SEQ-NBR                                  
075714               ,:PV-DB2-TRN-STAT-CDE                                      
075715               ,:PV-DB2-SLS-DTE                                           
075716               ,:PV-DB2-ASSOC-ID                                          
075717               ,:EPPAY-KC-ACCT-NBR                                        
075718               ,:EPPAY-PAYT-AMT                                           
075719               ,:EPPAY-AUTH-NBR                                           
075720     END-EXEC.                                                            
075721                                                                          
075722     EVALUATE TRUE                                                        
075723         WHEN SQLCODE = ZERO                                              
075724             ADD 1                 TO PV-PYMT-CURSOR-CNT                  
075725         WHEN SQLCODE = PC-ROW-NOT-FOUND                                  
075726             SET PS-END-OF-PYMT-CURSOR                                    
075727                                   TO TRUE                                
075728         WHEN SQLWARN0 NOT EQUAL SPACE                                    
075729         WHEN SQLCODE NOT EQUAL ZERO                                      
075730             MOVE '2250-FETCH-KOHLS-PYMT-CURSOR'                          
075731                                   TO PV-PARAGRAPH-NAME                   
075732             MOVE 'ERROR ON FETCH OF KOHLS-PYMT-CURSOR'                   
075733                                   TO PV-DB2-OPERATION                    
075734             PERFORM 9100-SQL-ABEND                                       
075735     END-EVALUATE.                                                        
075736                                                                          
075737******************************************************************        
075738* 2300-CLOSE-KOHLS-CURSOR                                                 
075739*   -CLOSE THE CURSOR                                                     
075740******************************************************************        
075741 2300-CLOSE-KOHLS-CURSOR.                                                 
075742                                                                          
075743     EXEC SQL                                                             
075744          CLOSE KOHLS-CURSOR                                              
075745     END-EXEC.                                                            
075746                                                                          
075747     EVALUATE TRUE                                                        
075748         WHEN SQLCODE = ZERO                                              
075749              CONTINUE                                                    
075750         WHEN SQLWARN0 NOT EQUAL SPACE                                    
075751         WHEN SQLCODE NOT EQUAL ZERO                                      
075752             MOVE '2300-CLOSE-KOHLS-CURSOR'                               
075753                                   TO PV-PARAGRAPH-NAME                   
075754             MOVE 'ERROR ON CLOSE OF KOHLS-CURSOR'                        
075755                                   TO PV-DB2-OPERATION                    
075756             PERFORM 9100-SQL-ABEND                                       
075757     END-EVALUATE.                                                        
075758 EJECT                                                                    
075759                                                                          
075760******************************************************************        
075761* 2350-CLOSE-KOHLS-PYMT-CURSOR                                            
075762*   -CLOSE THE PAYMENT CURSOR                                             
075763******************************************************************        
075764 2350-CLOSE-KOHLS-PYMT-CURSOR.                                            
075765                                                                          
075766     EXEC SQL                                                             
075767          CLOSE KOHLS-PYMT-CURSOR                                         
075768     END-EXEC.                                                            
075769                                                                          
075770     EVALUATE TRUE                                                        
075771         WHEN SQLCODE = ZERO                                              
075772              CONTINUE                                                    
075773         WHEN SQLWARN0 NOT EQUAL SPACE                                    
075774         WHEN SQLCODE NOT EQUAL ZERO                                      
075775             MOVE '2350-CLOSE-KOHLS-PYMT-CURSOR'                          
075776                                   TO PV-PARAGRAPH-NAME                   
075777             MOVE 'ERROR ON CLOSE OF KOHLS-PYMT-CURSOR'                   
075778                                   TO PV-DB2-OPERATION                    
075779             PERFORM 9100-SQL-ABEND                                       
075780     END-EVALUATE.                                                        
075781                                                                          
075782******************************************************************        
075783*  FIRST RECORD PROCESSING.  WRITE THE HEADER RECORDS AND MOVE            
075784*  THE DATA INTO THE APPROPRIATE FIELDS.                                  
075785******************************************************************        
075786 2500-PROCESS-FIRST-RECORD.                                               
075787                                                                          
075788     PERFORM 2050-EVALUATE-CURSOR.                                        
075789                                                                          
075790     IF PS-USE-REGULAR-CURSOR                                             
075791         MOVE EPTRN-STR-NBR        TO PV-HOLD-STORE                       
075792         MOVE EPTRN-SLS-DTE        TO PV-HOLD-SALES-DATE                  
075793     ELSE                                                                 
075794         MOVE PV-DB2-STR-NBR       TO PV-HOLD-STORE                       
075795         MOVE PV-DB2-SLS-DTE       TO PV-HOLD-SALES-DATE                  
075796     END-IF.                                                              
075797                                                                          
075798     PERFORM 5300-PRINT-STORE-LINE.                                       
075799     PERFORM 3510-FORMAT-SALES-DATE.                                      
075800                                                                          
075801******************************************************************        
075802*  PRINT THE STORE TOTAL WHEN A BREAK ON THE STORE NUMBER OCCURS          
075803******************************************************************        
075804 3000-STORE-BREAK.                                                        
075805                                                                          
075806     PERFORM 5400-PRINT-STORE-TOTAL-LINE.                                 
075807     PERFORM 5300-PRINT-STORE-LINE.                                       
075808                                                                          
075809*** THE SALES DATE SHOULD ALWAYS BE PRINTED UNDER THE NEW STORE           
075810*** INFORMATION.  THIS CHECKS TO SEE IF WE HAVE A DIFFERENT SALES         
075811*** DATE WITH THE NEW STORE.                                              
075812                                                                          
075813     IF (EPTRN-SLS-DTE             = PV-HOLD-SALES-DATE)                  
075814       AND (PV-DB2-SLS-DTE         = PV-HOLD-SALES-DATE)                  
075815         CONTINUE                                                         
075816     ELSE                                                                 
075817         IF PS-USE-REGULAR-CURSOR                                         
075818             MOVE EPTRN-SLS-DTE    TO PV-HOLD-SALES-DATE                  
075819         ELSE                                                             
075820             MOVE PV-DB2-SLS-DTE   TO PV-HOLD-SALES-DATE                  
075821         END-IF                                                           
075822         PERFORM 3510-FORMAT-SALES-DATE                                   
075823     END-IF.                                                              
075824                                                                          
075825     MOVE ZERO TO                  PA-STORE-SALES-AMOUNT                  
075826                                   PA-STORE-RETURN-AMOUNT                 
075827                                   PA-STORE-PYMT-AMOUNT.                  
075828                                                                          
075829     IF PS-USE-REGULAR-CURSOR                                             
075830         MOVE EPTRN-STR-NBR        TO PV-HOLD-STORE                       
075831     ELSE                                                                 
075832         MOVE PV-DB2-STR-NBR       TO PV-HOLD-STORE                       
075833     END-IF.                                                              
075840                                                                          
075843*****************************************************************         
075844*  PRINT THE REQUIRED DATA ON A BREAK IN SALES DATE                       
075845*****************************************************************         
075846 3500-SALES-DATE-BREAK.                                                   
075847                                                                          
075848     PERFORM 5400-PRINT-STORE-TOTAL-LINE.                                 
075849     MOVE ZERO                     TO PA-STORE-SALES-AMOUNT               
075850                                      PA-STORE-RETURN-AMOUNT              
075851                                      PA-STORE-PYMT-AMOUNT.               
075852                                                                          
075853     IF PS-USE-REGULAR-CURSOR                                             
075854         MOVE EPTRN-SLS-DTE        TO PV-HOLD-SALES-DATE                  
075855     ELSE                                                                 
075856         MOVE PV-DB2-SLS-DTE       TO PV-HOLD-SALES-DATE                  
075857     END-IF.                                                              
075858                                                                          
075859     PERFORM 3510-FORMAT-SALES-DATE.                                      
075860                                                                          
075861****************************************************************          
075862*  CALL CALENDAR ROUTINE TO CONVERT THE SALES DATE FROM DB2               
075863*  FORMAT INTO GREGORIAN FORMATTED FORMAT                                 
075864****************************************************************          
075865 3510-FORMAT-SALES-DATE.                                                  
075866                                                                          
075867     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
075868                                                                          
075869     SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                           
075870     SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                           
075871     SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                           
075872                                                                          
075873     IF PS-USE-REGULAR-CURSOR                                             
075874         MOVE EPTRN-SLS-DTE            TO DPG52-LK-DATE-INPUT             
075875     ELSE                                                                 
075876         MOVE PV-DB2-SLS-DTE           TO DPG52-LK-DATE-INPUT             
075877     END-IF.                                                              
075878                                                                          
075879     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
075880                                             DPG54 DPG55 DPG56.           
075881     EVALUATE TRUE                                                        
075882         WHEN DPG54-SEVERE-ERROR                                          
075883         WHEN DPG54-DATE-INVALID                                          
075884             MOVE '3510-FORMAT-SALES-DATEE'                               
075885                                   TO PV-PARAGRAPH-NAME                   
075886             MOVE 'ERROR CONVERTING SALES DATE'                           
075887                                   TO PV-OPERATION-MSG-1                  
075888             SET AC-CALENDAR-ROUTINE-ERROR TO TRUE                        
075889             MOVE DPG54-ERROR-MESSAGE                                     
075890                                   TO PV-OPERATION-MSG-2                  
075891             PERFORM 9999-ABEND                                           
075892     END-EVALUATE.                                                        
075893     MOVE DPG55-GREG-CC-DATE-FMT   TO PV-SLS-DATE-FORMATTED.              
075894                                                                          
075895******************************************************************        
075896* 4000-PRINT-REPORT-TOTALS                                                
075897*   - PRINT REPORT TOTALS                                                 
075898*   - CLOSE REPORT FILE.                                                  
075899******************************************************************        
075900 4000-PRINT-REPORT-TOTALS.                                                
075901                                                                          
075902     PERFORM 2300-CLOSE-KOHLS-CURSOR.                                     
075903     PERFORM 2350-CLOSE-KOHLS-PYMT-CURSOR.                                
075904     MOVE PV-POS-DATE-FORMATTED    TO TL1-DATE.                           
075905     MOVE PA-COMPANY-SALES-AMOUNT                                         
075906                                   TO TL1-SALES-AMOUNT.                   
075907     MOVE PA-COMPANY-RETURN-AMOUNT                                        
075908                                   TO TL1-RETURN-AMOUNT.                  
075909     MOVE PA-COMPANY-PYMT-AMOUNT   TO TL1-PYMT-AMOUNT.                    
075910     COMPUTE PV-AMOUNT             = PA-COMPANY-RETURN-AMOUNT             
075911                                   + PA-COMPANY-SALES-AMOUNT              
075912                                   + PA-COMPANY-PYMT-AMOUNT.              
075913     MOVE PV-AMOUNT                TO TL1-NET-AMOUNT.                     
075914     MOVE +4                       TO PV-LINE-SPACING.                    
075920     MOVE TL1-TOTAL-LINE-1         TO PV-PRINT-RECORD.                    
076000     PERFORM 8200-WRITE-REPORT-LINE.                                      
077400     CLOSE KOHLS-REPORT-FILE.                                             
077500 EJECT                                                                    
077600                                                                          
077700******************************************************************        
077800* 5000-PREPARE-DETAIL-LINE                                                
077900*  EVALUATE CURSOR SWITCH AND PREPARE LINES FOR PRINTING                  
078000******************************************************************        
078100 5000-PREPARE-DETAIL-LINE.                                                
078200                                                                          
078300     IF PV-LINE-CNT                > PC-LINE-LIMIT                        
078400         PERFORM 8000-PRINT-HEADINGS                                      
078500     END-IF.                                                              
078600                                                                          
078700     INITIALIZE                    DL1-SALES-AMOUNT                       
078710                                   DL1-RETURN-AMOUNT                      
078711                                   DL1-PYMT-AMOUNT                        
078720                                   DL1-CODE                               
078721                                   DL1-ASSOC-ID                           
P1043                                    DL1-BIN-NUMBER                         
078730                                   DL1-CARD-NBR.                          
078770     MOVE PV-SLS-DATE-FORMATTED    TO DL1-SALES-DATE.                     
078780                                                                          
P10237     INITIALIZE                    PV-PARTN-NBR                           
P10237                                   PV-STRT-TM                             
P10237                                   PV-TNDR-ID                             
P10237                                   PV-TNDR-LEN                            
P10237                                   PV-TNDR-SPACES.                        
078790     IF PS-USE-REGULAR-CURSOR                                             
078791         PERFORM 5100-PRINT-DETAIL-LINE                                   
078792     ELSE                                                                 
078793         PERFORM 5200-PRINT-DETAIL-PYMT-LINE                              
078794     END-IF.                                                              
078795                                                                          
078800******************************************************************        
078900* 5100-PRINT-DETAIL-LINE                                                  
079000*     FORMAT AND PRINT REPORT DETAIL LINE.                                
079100******************************************************************        
079200 5100-PRINT-DETAIL-LINE.                                                  
079201                                                                          
079614     MOVE EPTRN-RGST-ID            TO DL1-REGISTER.                       
079615     MOVE EPTRN-TRN-NBR            TO DL1-TRANSACTION.                    
079616     MOVE EPCRD-AUTH-NBR           TO DL1-AUTH-NBR.                       
P10237     MOVE EPTRN-PARTN-NBR          TO PV-PARTN-NBR.                       
P10237     MOVE EPTRN-STRT-TM            TO PV-STRT-TM.                         
P10237     MOVE EPTND-TNDR-ID            TO PV-TNDR-ID                          
P10237     MOVE EPTND-TNDR-ID-LEN-NBR    TO PV-TNDR-LEN                         
COBRND     EVALUATE EPTND-TNDR-ID-LEN-NBR                                       
COBRND       WHEN 13                                                            
COBRND         MOVE SPACES               TO DL1-BIN-NUMBER                      
COBRND         MOVE ALL '*'              TO PV-CARD-NBR-13-FIRST-4              
COBRND                                      PV-CARD-NBR-13-SECOND-3             
COBRND                                      PV-CARD-NBR-13-THIRD-2              
COBRND         MOVE EPTND-TNDR-ID (10:1) TO PV-CARD-NBR-13-THIRD-1              
COBRND         MOVE EPTND-TNDR-ID (11:3) TO PV-CARD-NBR-13-LAST-3               
COBRND         MOVE PV-CARD-NBR-13       TO DL1-CARD-NBR                        
COBRND       WHEN 16                                                            
COBRND         MOVE SPACES               TO DL1-BIN-NUMBER                      
COBRND         MOVE ALL '*'              TO PV-CARD-NBR-16-FIRST-4              
COBRND                                      PV-CARD-NBR-16-SECOND-4             
COBRND                                      PV-CARD-NBR-16-THIRD-4              
COBRND         MOVE EPTND-TNDR-ID (13:4) TO PV-CARD-NBR-16-LAST-4               
COBRND         MOVE PV-CARD-NBR-16       TO DL1-CARD-NBR                        
COBRND       WHEN 12                                                            
COBRND         MOVE SPACES               TO DL1-BIN-NUMBER                      
COBRND         MOVE ALL '*'              TO PV-CARD-NBR-12-FIRST-4              
COBRND                                      PV-CARD-NBR-12-SECOND-3             
COBRND                                      PV-CARD-NBR-12-THIRD-1              
COBRND         MOVE EPTND-TNDR-ID (9:2)  TO PV-CARD-NBR-12-THIRD-2              
COBRND         MOVE EPTND-TNDR-ID (11:2) TO PV-CARD-NBR-12-LAST-2               
COBRND         MOVE PV-CARD-NBR-12       TO DL1-CARD-NBR                        
COBRND       WHEN 18                                                            
COBRND         MOVE PV-TNDR-ID (1:6)     TO DL1-BIN-NUMBER                      
COBRND         MOVE ALL '*'              TO PV-CARD-NBR-18-FIRST-3              
COBRND                                      PV-CARD-NBR-18-SECOND-4             
COBRND                                      PV-CARD-NBR-18-THIRD-1              
COBRND         MOVE EPTND-TNDR-ID (15:2) TO PV-CARD-NBR-18-THIRD-2              
COBRND         MOVE EPTND-TNDR-ID (17:2) TO PV-CARD-NBR-18-LAST-2               
COBRND         MOVE PV-CARD-NBR-18       TO DL1-CARD-NBR                        
P10237     END-EVALUATE.                                                        
080809                                                                          
080810     EVALUATE TRUE                                                        
080811         WHEN EPTND-TNDR-AMT       > ZERO                                 
080812             MOVE EPTND-TNDR-AMT   TO PV-AMOUNT-FORMATTED                 
080813             MOVE PV-AMOUNT-FORMATTED                                     
080814                                   TO DL1-SALES-AMOUNT                    
080815             MOVE SPACE            TO DL1-RETURN-AMOUNT                   
080816             ADD EPTND-TNDR-AMT    TO PA-STORE-SALES-AMOUNT               
080817                                                                          
080818         WHEN EPTND-TNDR-AMT       < ZERO                                 
080819             MOVE EPTND-TNDR-AMT   TO PV-AMOUNT-FORMATTED                 
080820             MOVE PV-AMOUNT-FORMATTED                                     
080821                                   TO DL1-RETURN-AMOUNT                   
080822             MOVE SPACE            TO DL1-SALES-AMOUNT                    
080823             ADD EPTND-TNDR-AMT    TO PA-STORE-RETURN-AMOUNT              
080824                                                                          
080825         WHEN OTHER                                                       
080826             MOVE ZERO             TO PV-AMOUNT-FORMATTED                 
080827             MOVE PV-AMOUNT-FORMATTED                                     
080828                                   TO DL1-RETURN-AMOUNT                   
080829                                      DL1-SALES-AMOUNT                    
080830     END-EVALUATE.                                                        
080831                                                                          
080832**** A LATE TRANSACTION, INDICATED BY A -1 SEQUENCE NUMBER, WILL NOT      
080833**** HAVE A SALES AUDIT USER ID.  A REGULAR CORRECTION WILL HVAE A        
080834**** SALES AUDIT USER ID.                                                 
080835                                                                          
080836     IF EPTRN-SLS-CORR-SEQ-NBR     < ZERO                                 
080837         MOVE PC-SYSTEM-CORRECTION TO DL1-CODE                            
080838     ELSE                                                                 
080839         IF EPTRN-TRN-STAT-CDE     = PC-REVERSAL-CORRECTION               
080840             MOVE PC-REVERSAL-CORRECTION                                  
080841                                   TO DL1-CODE                            
080842         END-IF                                                           
080843         PERFORM 7000-SELECT-SLS-AUD-USER-ID                              
080844     END-IF.                                                              
080850                                                                          
080860     MOVE DL1-DETAIL-LINE-1        TO PV-PRINT-RECORD.                    
080861                                                                          
080862**** FOLLOWING CODE CHECKS IF THE LAST LINE PRINTED WAS SINGLE            
080863**** SPACED (LIKE A DETAIL LINE) OR GREATER THAN SINGLE SPACED            
080864**** (LIKE A HEADING LINE).  IF LAST LINE WAS NOT SINGLE SPACED,          
080865**** PRINT THE DETAIL LINE DOUBLE SPACED.                                 
080866                                                                          
080867     IF PV-LINE-SPACING            > +1                                   
080868         MOVE +2                   TO  PV-LINE-SPACING                    
080869     ELSE                                                                 
080870         MOVE +1                   TO  PV-LINE-SPACING                    
080871     END-IF.                                                              
080872     PERFORM 8200-WRITE-REPORT-LINE.                                      
080873     MOVE +1                       TO  PV-LINE-SPACING.                   
081000 EJECT                                                                    
081001                                                                          
081002******************************************************************        
081003* 5200-PRINT-DETAIL-LINE                                                  
081004*     FORMAT AND PRINT REPORT PAYMENT DETAIL LINE                         
081005******************************************************************        
081006 5200-PRINT-DETAIL-PYMT-LINE.                                             
081010                                                                          
081020     MOVE PV-DB2-RGST-ID           TO DL1-REGISTER.                       
081021     MOVE PV-DB2-TRN-NBR           TO DL1-TRANSACTION.                    
081022     MOVE EPPAY-AUTH-NBR           TO DL1-AUTH-NBR.                       
P10237     MOVE PV-DB2-PARTN-NBR         TO PV-PARTN-NBR.                       
P10237     MOVE PV-DB2-STRT-TM           TO PV-STRT-TM.                         
P10237     IF EPPAY-KC-ACCT-NBR = SPACES                                        
P10237        MOVE SPACES                TO PV-TNDR-ID                          
P10237     ELSE                                                                 
P10237        INSPECT FUNCTION REVERSE(EPPAY-KC-ACCT-NBR)                       
P10237                TALLYING PV-TNDR-SPACES FOR LEADING SPACES                
P10237        COMPUTE PV-TNDR-LEN = LENGTH OF EPPAY-KC-ACCT-NBR                 
P10237                              - PV-TNDR-SPACES                            
P10237        MOVE EPPAY-KC-ACCT-NBR  TO PV-TNDR-ID                             
P10237     END-IF.                                                              
COBRND     EVALUATE PV-TNDR-LEN                                                 
COBRND       WHEN 13                                                            
COBRND         MOVE SPACES               TO DL1-BIN-NUMBER                      
COBRND         MOVE ALL '*'              TO PV-CARD-NBR-13-FIRST-4              
COBRND                                      PV-CARD-NBR-13-SECOND-3             
COBRND                                      PV-CARD-NBR-13-THIRD-2              
COBRND         MOVE PV-TNDR-ID (10:1) TO PV-CARD-NBR-13-THIRD-1                 
COBRND         MOVE PV-TNDR-ID (11:3) TO PV-CARD-NBR-13-LAST-3                  
COBRND         MOVE PV-CARD-NBR-13       TO DL1-CARD-NBR                        
COBRND       WHEN 16                                                            
COBRND         MOVE SPACES               TO DL1-BIN-NUMBER                      
COBRND         MOVE ALL '*'              TO PV-CARD-NBR-16-FIRST-4              
COBRND                                      PV-CARD-NBR-16-SECOND-4             
COBRND                                      PV-CARD-NBR-16-THIRD-4              
COBRND         MOVE PV-TNDR-ID (13:4) TO PV-CARD-NBR-16-LAST-4                  
COBRND         MOVE PV-CARD-NBR-16       TO DL1-CARD-NBR                        
COBRND       WHEN 12                                                            
COBRND         MOVE SPACES               TO DL1-BIN-NUMBER                      
COBRND         MOVE ALL '*'              TO PV-CARD-NBR-12-FIRST-4              
COBRND                                      PV-CARD-NBR-12-SECOND-3             
COBRND                                      PV-CARD-NBR-12-THIRD-1              
COBRND         MOVE PV-TNDR-ID (9:2)     TO PV-CARD-NBR-12-THIRD-2              
COBRND         MOVE PV-TNDR-ID (11:2)    TO PV-CARD-NBR-12-LAST-2               
COBRND         MOVE PV-CARD-NBR-12       TO DL1-CARD-NBR                        
COBRND       WHEN 18                                                            
COBRND         MOVE PV-TNDR-ID (1:6)     TO DL1-BIN-NUMBER                      
COBRND         MOVE ALL '*'              TO PV-CARD-NBR-18-FIRST-3              
COBRND                                      PV-CARD-NBR-18-SECOND-4             
COBRND                                      PV-CARD-NBR-18-THIRD-1              
COBRND         MOVE PV-TNDR-ID (15:2) TO PV-CARD-NBR-18-THIRD-2                 
COBRND         MOVE PV-TNDR-ID (17:2) TO PV-CARD-NBR-18-LAST-2                  
COBRND         MOVE PV-CARD-NBR-18       TO DL1-CARD-NBR                        
P10237     END-EVALUATE.                                                        
080809                                                                          
081050     COMPUTE EPPAY-PAYT-AMT        = EPPAY-PAYT-AMT                       
081051                                   * PC-NEGATIVE-ONE.                     
081052     MOVE EPPAY-PAYT-AMT           TO PV-AMOUNT-FORMATTED.                
081053     MOVE PV-AMOUNT-FORMATTED      TO DL1-PYMT-AMOUNT.                    
081054     ADD EPPAY-PAYT-AMT            TO PA-STORE-PYMT-AMOUNT.               
081055                                                                          
081056**** A LATE TRANSACTION, INDICATED BY A -1 SEQUENCE NUMBER, WILL NOT      
081057**** HAVE A SALES AUDIT USER ID.  A REGULAR CORRECTION WILL HVAE A        
081058**** SALES AUDIT USER ID.                                                 
081059                                                                          
081060     IF PV-DB2-SLS-CORR-SEQ-NBR    < ZERO                                 
081061         MOVE PC-SYSTEM-CORRECTION TO DL1-CODE                            
081062     ELSE                                                                 
081063         IF PV-DB2-TRN-STAT-CDE    = PC-REVERSAL-CORRECTION               
081064             MOVE PC-REVERSAL-CORRECTION                                  
081065                                   TO DL1-CODE                            
081066         END-IF                                                           
081067         PERFORM 7000-SELECT-SLS-AUD-USER-ID                              
081068     END-IF.                                                              
081069                                                                          
081070     MOVE DL1-DETAIL-LINE-1        TO PV-PRINT-RECORD.                    
081071                                                                          
081072**** FOLLOWING CODE CHECKS IF THE LAST LINE PRINTED WAS SINGLE            
081073**** SPACED (LIKE A DETAIL LINE) OR GREATER THAN SINGLE SPACED            
081074**** (LIKE A HEADING LINE).  IF LAST LINE WAS NOT SINGLE SPACED,          
081075**** PRINT THE DETAIL LINE DOUBLE SPACED.                                 
081076                                                                          
081077     IF PV-LINE-SPACING            > +1                                   
081078         MOVE +2                   TO  PV-LINE-SPACING                    
081079     ELSE                                                                 
081080         MOVE +1                   TO  PV-LINE-SPACING                    
081081     END-IF.                                                              
081082     PERFORM 8200-WRITE-REPORT-LINE.                                      
081083     MOVE +1                       TO  PV-LINE-SPACING.                   
081084                                                                          
081085*****************************************************************         
081086*  PRINT THE LINE CONTAINING THE STORE NUMBER                             
081087*****************************************************************         
081088 5300-PRINT-STORE-LINE.                                                   
081089                                                                          
081090**** FOLLOWING CODE CHECKS FOR ENOUGH ROOM ON THE PAGE TO                 
081091**** PRINT THE STORE LINE, SALES DATE LINE,                               
081092**** AND AT LEAST ONE DETAIL LINE.                                        
081093                                                                          
081094     MOVE PV-LINE-CNT              TO  PV-LINE-CNT-AHEAD.                 
081095     ADD +8                        TO  PV-LINE-CNT-AHEAD.                 
081096     IF PV-LINE-CNT-AHEAD          > PC-LINE-LIMIT                        
081097         PERFORM 8000-PRINT-HEADINGS                                      
081098     END-IF.                                                              
081099                                                                          
081100     IF PS-USE-REGULAR-CURSOR                                             
081101         MOVE EPTRN-STR-NBR        TO HL3-STORE                           
081102     ELSE                                                                 
081103         MOVE PV-DB2-STR-NBR       TO HL3-STORE                           
081104     END-IF.                                                              
081105                                                                          
081106     MOVE +4                       TO PV-LINE-SPACING.                    
081107     MOVE HL3-HEADING-LINE-3       TO PV-PRINT-RECORD.                    
081108     PERFORM 8200-WRITE-REPORT-LINE.                                      
081110                                                                          
081131******************************************************************        
081132*  PRINT THE STORE TOTAL LINE INFORMATION                                 
081133******************************************************************        
081135 5400-PRINT-STORE-TOTAL-LINE.                                             
081136                                                                          
081137     IF PV-LINE-CNT                > PC-LINE-LIMIT                        
081138         PERFORM 8000-PRINT-HEADINGS                                      
081139     END-IF.                                                              
081140                                                                          
081141     MOVE PV-HOLD-STORE            TO HL6-STORE.                          
081142     MOVE PV-SLS-DATE-FORMATTED    TO HL6-DATE.                           
081144                                                                          
081145     IF PA-STORE-SALES-AMOUNT      > ZERO                                 
081146         MOVE PA-STORE-SALES-AMOUNT                                       
081147                                   TO PV-AMOUNT-FORMATTED                 
081148         MOVE PV-AMOUNT-FORMATTED  TO HL6-SALES-TOTAL                     
081149     ELSE                                                                 
081150         MOVE SPACE                TO HL6-SALES-TOTAL                     
081151     END-IF.                                                              
081152                                                                          
081153     ADD  PA-STORE-SALES-AMOUNT    TO PA-COMPANY-SALES-AMOUNT.            
081155                                                                          
081156     IF PA-STORE-RETURN-AMOUNT     < ZERO                                 
081157         MOVE PA-STORE-RETURN-AMOUNT                                      
081158                                   TO PV-AMOUNT-FORMATTED                 
081159         MOVE PV-AMOUNT-FORMATTED  TO HL6-RETURN-TOTAL                    
081160     ELSE                                                                 
081161         MOVE SPACE                TO HL6-RETURN-TOTAL                    
081162     END-IF.                                                              
081163                                                                          
081164     ADD PA-STORE-RETURN-AMOUNT    TO PA-COMPANY-RETURN-AMOUNT.           
081165                                                                          
081166     IF PA-STORE-PYMT-AMOUNT       NOT EQUAL ZERO                         
081167         MOVE PA-STORE-PYMT-AMOUNT TO PV-AMOUNT-FORMATTED                 
081168         MOVE PV-AMOUNT-FORMATTED  TO HL6-PYMT-AMOUNT                     
081169     ELSE                                                                 
081170         MOVE SPACE                TO HL6-PYMT-AMOUNT                     
081171     END-IF.                                                              
081172                                                                          
081173     ADD PA-STORE-PYMT-AMOUNT      TO PA-COMPANY-PYMT-AMOUNT.             
081174     COMPUTE PV-AMOUNT             = PA-STORE-RETURN-AMOUNT               
081175                                   + PA-STORE-SALES-AMOUNT                
081176                                   + PA-STORE-PYMT-AMOUNT.                
081177     MOVE PV-AMOUNT                TO PV-AMOUNT-FORMATTED.                
081178     MOVE PV-AMOUNT-FORMATTED      TO HL6-NET-AMOUNT.                     
081179     MOVE +2                       TO PV-LINE-SPACING.                    
081180     MOVE HL6-HEADING-LINE-6       TO PV-PRINT-RECORD.                    
081181     PERFORM 8200-WRITE-REPORT-LINE.                                      
081182                                                                          
081183*****************************************************************         
081184* 7000-SELECT-SLS-AUD-USER-ID                                             
081185*   - SELECT THE SALES AUDIT USER ID WHEN THE SALES CORRECTION            
081186*     SEQUNCE NUMBER IS GREATER THAN ZERO                                 
081187*****************************************************************         
081188 7000-SELECT-SLS-AUD-USER-ID.                                             
081189                                                                          
081190     IF PS-USE-REGULAR-CURSOR                                             
081191         EXEC SQL                                                         
081192             SELECT SLS_AUD_USR_ID                                        
081193               INTO :EPSAT-SLS-AUD-USR-ID                                 
081194               FROM TEPSAT                                                
081195              WHERE PARTN_NBR          = :EPTRN-PARTN-NBR                 
081196                AND STR_NBR            = :EPTRN-STR-NBR                   
081197                AND RGST_ID            = :EPTRN-RGST-ID                   
081198                AND TRN_NBR            = :EPTRN-TRN-NBR                   
081199                AND STRT_TM            = :EPTRN-STRT-TM                   
081200                AND SLS_CORR_SEQ_NBR   = :EPTRN-SLS-CORR-SEQ-NBR          
081201             WITH UR                                                      
081202         END-EXEC                                                         
081203     ELSE                                                                 
081204         EXEC SQL                                                         
081205             SELECT SLS_AUD_USR_ID                                        
081206               INTO :EPSAT-SLS-AUD-USR-ID                                 
081207               FROM TEPSAT                                                
081208              WHERE PARTN_NBR          = :PV-DB2-PARTN-NBR                
081209                AND STR_NBR            = :PV-DB2-STR-NBR                  
081210                AND RGST_ID            = :PV-DB2-RGST-ID                  
081211                AND TRN_NBR            = :PV-DB2-TRN-NBR                  
081212                AND STRT_TM            = :PV-DB2-STRT-TM                  
081213                AND SLS_CORR_SEQ_NBR   = :PV-DB2-SLS-CORR-SEQ-NBR         
081214             WITH UR                                                      
081215         END-EXEC                                                         
081216     END-IF.                                                              
081217                                                                          
081218     EVALUATE TRUE                                                        
081219         WHEN SQLCODE = ZERO                                              
081220             MOVE EPSAT-SLS-AUD-USR-ID                                    
081221                                   TO DL1-ASSOC-ID                        
081222         WHEN SQLCODE = PC-ROW-NOT-FOUND                                  
081223             MOVE 'UNKNOWN'        TO DL1-ASSOC-ID                        
081224         WHEN SQLWARN0 NOT EQUAL SPACE                                    
081225         WHEN SQLCODE NOT EQUAL ZERO                                      
081226             MOVE '7000-SELECT-SLS-AUD-USER-ID'                           
081227                                   TO PV-PARAGRAPH-NAME                   
081228             MOVE 'ERROR ON SELECTING SLS AUD USER ID'                    
081229                                   TO PV-DB2-OPERATION                    
081230             PERFORM 9100-SQL-ABEND                                       
081231     END-EVALUATE.                                                        
081232                                                                          
081233******************************************************************        
081240* 8000-PRINT-HEADINGS                                                     
081300*     PRINT THE HEADINGS FOR THE REPORT.                                  
081400******************************************************************        
081500 8000-PRINT-HEADINGS.                                                     
081600                                                                          
081700     ADD +1                        TO PV-PAGE-CNT.                        
081800     MOVE PV-PAGE-CNT TO DP132-PAGE-NUMBER.                               
081900     MOVE DP132-STANDRD-HEADER-132-COLS                                   
082000                                   TO PV-PRINT-RECORD.                    
082100     PERFORM 8100-WRITE-RPT-TOP-OF-PAGE.                                  
082200                                                                          
082300     MOVE HL2-HEADING-LINE-2       TO PV-PRINT-RECORD.                    
082400     MOVE +1                       TO PV-LINE-SPACING.                    
082500     PERFORM 8200-WRITE-REPORT-LINE.                                      
082800                                                                          
082810     MOVE +2                       TO PV-LINE-SPACING.                    
082820     MOVE HL4-HEADING-LINE-4       TO PV-PRINT-RECORD.                    
082830     PERFORM 8200-WRITE-REPORT-LINE.                                      
082831                                                                          
082840     MOVE +1                       TO PV-LINE-SPACING.                    
082850     MOVE HL5-HEADING-LINE-5       TO PV-PRINT-RECORD.                    
082860     PERFORM 8200-WRITE-REPORT-LINE.                                      
082870     MOVE +2                       TO PV-LINE-SPACING.                    
082880                                                                          
082900 EJECT                                                                    
082910                                                                          
083000******************************************************************        
083100* 8100-WRITE-RPT-TOP-OF-PAGE                                              
083200*     WRITE FIRST REPORT LINE AFTER PAGE BREAK.                           
083300******************************************************************        
083400 8100-WRITE-RPT-TOP-OF-PAGE.                                              
083500                                                                          
083600     WRITE KOHLS-REPORT-RECORD                                            
083700       FROM PV-PRINT-RECORD                                               
083800         AFTER ADVANCING PAGE.                                            
083900     MOVE +1                       TO PV-LINE-CNT.                        
084000 EJECT                                                                    
084010                                                                          
084100******************************************************************        
084200* 8200-WRITE-REPORT-LINE                                                  
084300*     WRITE REPORT LINE AFTER ADVANCING LINE SPACING.                     
084400******************************************************************        
084500 8200-WRITE-REPORT-LINE.                                                  
084610                                                                          
084700     WRITE KOHLS-REPORT-RECORD                                            
084800       FROM PV-PRINT-RECORD                                               
084900         AFTER ADVANCING PV-LINE-SPACING.                                 
085000                                                                          
085100     ADD PV-LINE-SPACING TO PV-LINE-CNT.                                  
085200 EJECT                                                                    
085210                                                                          
085300******************************************************************        
085400* 9100-SQL-ABEND                                                          
085500*     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.        
085600******************************************************************        
085700 9100-SQL-ABEND.                                                          
085800     DISPLAY '** ABEND     **'.                                           
085900     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
086000     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
086100     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.                       
086200                                                                          
086300******************************************************************        
086400* COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED             
086500* TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.              
086600******************************************************************        
086700     COPY DPPD004.                                                        
086800                                                                          
086900     SET AC-DB2-ERROR TO TRUE.                                            
087000     CALL 'ILBOABN0' USING AC-ABEND-CODE.                                 
087100                                                                          
087200******************************************************************        
087300* 9999-ABEND                                                              
087400*     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                          
087500******************************************************************        
087600 9999-ABEND.                                                              
087700     DISPLAY '** ABEND           **'.                                     
087800     DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
087900     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
088000     DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.               
088100     DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.               
088200     CALL 'ILBOABN0' USING AC-ABEND-CODE.                                 
P10237                                                                          
