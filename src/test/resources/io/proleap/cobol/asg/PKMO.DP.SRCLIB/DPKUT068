000100*                                                                         
000200 IDENTIFICATION DIVISION.                                                 
000300*                                                                         
000400 PROGRAM-ID.    DPKUT068.                                                 
000500*                                                                         
000600 AUTHOR.        CAL BUSH.                                                 
000700 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000800 DATE-WRITTEN.  07/05/96.                                                 
000900 DATE-COMPILED.                                                           
001000*                                                                         
001100*****************************************************************         
001200* THIS PROGRAM TAKES A RACF EXTRACT OF USERS AND REFORMATS IT             
001210* FOR LOADING INTO ORACLE TABLES.                                         
001220*                                                                         
001300*     DATE     PROGRAMMER   DESCRIPTION OF CHANGE                         
001400*  ----------  ----------   -------------------------------------         
001500*  03/10/2009  TKMA464      SPLIT NAME BY COMMA IF POSSIBLE AND           
001510*                           THEN BY SPACE.  IT WAS SPLITTING BY           
001600*                           EITHER AND THAT CREATED ISSUES.               
001610*                           NAMES LIKE "VAN HALEN,EDDIE" WOULD            
001620*                           BE PARSED AS LASTNAME=VAN,                    
001630*                           FIRSTNAME=HALEN, MID-INIT=E.                  
001700*****************************************************************         
001800     EJECT                                                                
001900 ENVIRONMENT DIVISION.                                                    
002000*                                                                         
002100 CONFIGURATION SECTION.                                                   
002200*                                                                         
002300 SOURCE-COMPUTER.  IBM-3090.                                              
002400 OBJECT-COMPUTER.  IBM-3090.                                              
002500*                                                                         
002600 INPUT-OUTPUT SECTION.                                                    
002700*                                                                         
002800 FILE-CONTROL.                                                            
002900     SELECT INPUT-USERID-FILE                                             
003000                         ASSIGN TO INP01.                                 
003100     SELECT OUTPUT-USERID-FILE                                            
003200                         ASSIGN TO OUT01.                                 
003300     EJECT                                                                
003400 DATA DIVISION.                                                           
003500*                                                                         
003600 FILE SECTION.                                                            
003700*                                                                         
003800 FD  INPUT-USERID-FILE                                                    
003900     RECORD CONTAINS 80 CHARACTERS.                                       
004000 01  INPUT-USERID-RECORD           PIC  X(80).                            
004100*                                                                         
004200 FD  OUTPUT-USERID-FILE                                                   
004300     RECORD CONTAINS 150 CHARACTERS.                                      
004400 01  OUTPUT-USERID-RECORD          PIC  X(150).                           
004500*                                                                         
004600     EJECT                                                                
004700 WORKING-STORAGE SECTION.                                                 
004800*                                                                         
004900 01  PC-PROGRAM-CONSTANTS.                                                
005000     05  PC-COMMA                  PIC  X(01)  VALUE ','.                 
005100*                                                                         
005200 01  PV-PROGRAM-VARIABLES.                                                
005300     05  PV-INPUT-RECS             PIC S9(09)  COMP SYNC                  
005400                                               VALUE ZERO.                
005500     05  PV-OUTPUT-RECS            PIC S9(09)  COMP SYNC                  
005600                                               VALUE ZERO.                
005700     05  PV-PRINT-NUM              PIC  Z,ZZZ,ZZ9                         
005800                                               VALUE ZERO.                
005900     05  PV-UNSTRING-PTR           PIC S9(04)  COMP SYNC                  
006000                                               VALUE ZERO.                
006100     05  PV-UNSTRING-DELIM         PIC  X(01)  VALUE SPACE.               
006200     05  PV-DASH-CNT               PIC  9(02)  VALUE ZERO.                
006300*                                                                         
006400 01  PS-PROGRAM-SWITCHES.                                                 
006500     05  PS-EOF-SW                 PIC  X(01)  VALUE 'N'.                 
006600         88  PS-EOF                            VALUE 'Y'.                 
006700 01  ABEND-CODE                    PIC S9(04)  COMP SYNC                  
006800                                               VALUE ZERO.                
006900     88  AC-BAD-READ                           VALUE +4002.               
007000*                                                                         
007100****************************************************************          
007200** THIS IS THE RECORD LAYOUT FOR THE INPUT RECORD USED BY THIS**          
007300** PROGRAM.                                                   **          
007400****************************************************************          
007500 01  UID-I-INPUT-RECORD.                                                  
007600     05  UID-I-USERID              PIC  X(08)  VALUE SPACE.               
007700     05  FILLER                    PIC  X(01)  VALUE SPACE.               
007800     05  UID-I-NAME                PIC  X(40)  VALUE SPACE.               
007900     05  FILLER                    PIC  X(01)  VALUE SPACE.               
008000     05  UID-I-TYPE-CODE           PIC  X(01)  VALUE SPACE.               
008100         88  UID-I-TYPE-GROUP                  VALUE 'G'.                 
008200         88  UID-I-TYPE-USER                   VALUE 'U'.                 
008300     05  FILLER                    PIC  X(01)  VALUE SPACE.               
008400     05  UID-I-REVOKE-IND          PIC  X(01)  VALUE SPACE.               
008500     05  FILLER                    PIC  X(27)  VALUE SPACE.               
008600****************************************************************          
008700** THIS IS THE RECORD LAYOUT FOR THE OUTPUT RECORD CREATED BY **          
008800** THIS PROGRAM.                                              **          
008900****************************************************************          
009000 01  UID-O-OUTPUT-RECORD.                                                 
009100     05  UID-O-USERID              PIC  X(08)  VALUE SPACE.               
009200     05  FILLER                    PIC  X(01)  VALUE SPACE.               
009300     05  UID-O-NAME                PIC  X(40)  VALUE SPACE.               
009400     05  FILLER                    PIC  X(01)  VALUE SPACE.               
009500     05  UID-O-TYPE-CODE           PIC  X(01)  VALUE SPACE.               
009600     05  FILLER                    PIC  X(01)  VALUE SPACE.               
009700     05  UID-O-REVOKE-IND          PIC  X(01)  VALUE SPACE.               
009800     05  FILLER                    PIC  X(01)  VALUE SPACE.               
009900     05  UID-O-LAST-NAME           PIC  X(40)  VALUE SPACE.               
010000     05  FILLER                    PIC  X(01)  VALUE SPACE.               
010100     05  UID-O-FIRST-NAME          PIC  X(40)  VALUE SPACE.               
010200     05  FILLER                    PIC  X(01)  VALUE SPACE.               
010300     05  UID-O-MID-INIT            PIC  X(01)  VALUE SPACE.               
010400     05  FILLER                    PIC  X(13)  VALUE SPACE.               
010500*                                                                         
010600*                                                                         
010700 EJECT                                                                    
010800 PROCEDURE DIVISION.                                                      
010900*****************************************************************         
011000**  THIS MODULE CONTROLS ALL PROCESSING DONE IN THIS PROGRAM.  **         
011100*****************************************************************         
011200 0000-MAIN-DRIVER.                                                        
011300                                                                          
011400     PERFORM 1000-INITIALIZATION.                                         
011500     PERFORM 2000-PROCESS-INPUT                                           
011600         UNTIL PS-EOF.                                                    
011700     PERFORM 3000-TERMINATION.                                            
011800                                                                          
011900     GOBACK.                                                              
012000****************************************************************          
012100** OPEN FILES AND DO THE PRIMING READ.                        **          
012200****************************************************************          
012300 1000-INITIALIZATION.                                                     
012400     OPEN INPUT  INPUT-USERID-FILE                                        
012500          OUTPUT OUTPUT-USERID-FILE.                                      
012600                                                                          
012700     PERFORM 2400-READ-INPUT-USERID.                                      
012800     IF  PS-EOF                                                           
012900         DISPLAY '*** ABEND ***'                                          
013000         DISPLAY '*** PROGRAM:  DPKUT068'                                 
013100         DISPLAY '*** EMPTY INPUT FILE'                                   
013200         SET AC-BAD-READ TO TRUE                                          
013300         CALL 'ILBOABN0' USING ABEND-CODE                                 
013400     END-IF.                                                              
013500                                                                          
013600****************************************************************          
013700**                                                            **          
013800****************************************************************          
013900 2000-PROCESS-INPUT.                                                      
014000     PERFORM 2100-FORMAT-OUTPUT.                                          
014100     PERFORM 2300-WRITE-OUTPUT-RECORD.                                    
014200     PERFORM 2400-READ-INPUT-USERID.                                      
014300****************************************************************          
014400** FORMAT THE OUTPUT RECORD.                                  **          
014500** IF THE TYPE IS A GROUP, THE GROUP DESCRIPTION IS PUT INTO  **          
014600** THE LAST NAME FIELD, IF NOT, THE NAME IS SPLIT AND THE     **          
014700** PIECES ARE PUT INTO APPROPRIATE NAME FIELDS.               **          
014800****************************************************************          
014900 2100-FORMAT-OUTPUT.                                                      
015000     MOVE SPACE                    TO UID-O-OUTPUT-RECORD.                
015100     MOVE UID-I-USERID             TO UID-O-USERID                        
015200*                                                                         
015300*  RESOLVE A PROBLEM THAT OCCURS WHEN THE NAME IS BLANK                   
015400*                                                                         
015500     IF  UID-I-NAME = SPACE                                               
015600         MOVE 'BLANK NAME'         TO UID-I-NAME                          
015700     END-IF.                                                              
015800     MOVE UID-I-NAME               TO UID-O-NAME                          
015900     MOVE UID-I-TYPE-CODE          TO UID-O-TYPE-CODE                     
016000     MOVE UID-I-REVOKE-IND         TO UID-O-REVOKE-IND                    
016100     IF  UID-I-TYPE-GROUP                                                 
016200         MOVE UID-I-NAME           TO UID-O-LAST-NAME                     
016300     ELSE                                                                 
016400         PERFORM 2200-SPLIT-NAME                                          
016500     END-IF.                                                              
016600****************************************************************          
016700**  THE INPUT NAME FIELD CONTAINS THE COMMA-DELIMTED NAME.    **          
016800**  THIS SPLITS IT INTO THREE FIELDS BASED ON THE DELIMITER   **          
016900****************************************************************          
017000 2200-SPLIT-NAME.                                                         
017100     MOVE 0                        TO PV-DASH-CNT.                        
017200     INSPECT UID-I-NAME TALLYING PV-DASH-CNT FOR ALL '-'.                 
017300                                                                          
017400     MOVE 1                        TO PV-UNSTRING-PTR.                    
017500* DON'T APPLY THE FIX TO THESE CASES MAINLY BECAUSE WE                    
017600* HAVEN'T RESEARCHED WHAT THE IMPACT WILL BE.                             
017700     IF UID-O-USERID(1:4) = 'TKDC'                                        
017800     OR UID-O-USERID(1:3) = 'TKS'                                         
017900     OR UID-O-USERID(1:5) = 'TKTRN'                                       
018000     OR UID-O-USERID(1:4) = 'TKMD'                                        
018100     OR UID-O-USERID(1:5) = 'TKRJE'                                       
018200     OR PV-DASH-CNT > ZERO                                                
018300     OR UID-O-USERID = 'TKDISC1'                                          
018400     OR UID-O-USERID = 'TKFY810'                                          
018500     OR UID-O-USERID = 'TKMA098'                                          
018600     OR UID-O-USERID = 'TKMA911'                                          
018700     OR UID-O-USERID = 'TKMA999'                                          
018800     OR UID-O-USERID = 'TKMOKEY'                                          
018900     OR UID-O-USERID = 'TKMO999'                                          
019000     OR UID-O-USERID = 'TKRTV01'                                          
019100     OR UID-O-USERID(1:6) = 'TKTEST'                                      
019200     OR UID-O-USERID = 'TKUARST'                                          
019300     OR UID-O-USERID = 'TKUPDAT'                                          
019400     OR UID-O-USERID(1:6) = 'TKUSB0'                                      
019500     OR UID-O-USERID = 'TKVRU01'                                          
019600     OR UID-I-NAME(1:11) = 'ADVERTISING'                                  
019700     OR UID-I-NAME(1:7) = 'PROLINE'                                       
019800     OR UID-I-NAME(1:10) = 'INTERSTATE'                                   
019900     OR UID-I-NAME(1:8) = 'KREIKAMP'                                      
020000     OR UID-I-NAME(1:9) = 'KREILKAMP'                                     
020100     OR UID-O-USERID = 'TK53B01'                                          
020200       UNSTRING UID-I-NAME                                                
020300           DELIMITED BY PC-COMMA                                          
020400                 OR ALL SPACES                                            
020500           INTO         UID-O-LAST-NAME                                   
020600           DELIMITER IN PV-UNSTRING-DELIM                                 
020700           POINTER      PV-UNSTRING-PTR                                   
020800       END-UNSTRING                                                       
020900     ELSE                                                                 
021000       UNSTRING UID-I-NAME                                                
021100           DELIMITED BY PC-COMMA                                          
021200           INTO         UID-O-LAST-NAME                                   
021300           DELIMITER IN PV-UNSTRING-DELIM                                 
021400           POINTER      PV-UNSTRING-PTR                                   
021500       END-UNSTRING                                                       
021600     END-IF.                                                              
021700*                                                                         
021800     UNSTRING UID-I-NAME                                                  
021900         DELIMITED BY PC-COMMA                                            
022000               OR ALL SPACES                                              
022100         INTO         UID-O-FIRST-NAME                                    
022200         DELIMITER IN PV-UNSTRING-DELIM                                   
022300         POINTER      PV-UNSTRING-PTR                                     
022400     END-UNSTRING.                                                        
022500*                                                                         
022600     UNSTRING UID-I-NAME                                                  
022700         DELIMITED BY PC-COMMA                                            
022800               OR ALL SPACES                                              
022900         INTO         UID-O-MID-INIT                                      
023000         DELIMITER IN PV-UNSTRING-DELIM                                   
023100         POINTER      PV-UNSTRING-PTR                                     
023200     END-UNSTRING.                                                        
023300****************************************************************          
023400** FORMAT THE OUTPUT RECORD.                                  **          
023500****************************************************************          
023600 2300-WRITE-OUTPUT-RECORD.                                                
023700     WRITE OUTPUT-USERID-RECORD                                           
023800         FROM UID-O-OUTPUT-RECORD.                                        
023900     ADD +1 TO PV-OUTPUT-RECS.                                            
024000****************************************************************          
024100** READ A RECORD FROM THE INPUT FILE.                         **          
024200****************************************************************          
024300 2400-READ-INPUT-USERID.                                                  
024400     READ INPUT-USERID-FILE INTO UID-I-INPUT-RECORD                       
024500         AT END                                                           
024600             SET PS-EOF TO TRUE.                                          
024700     IF  NOT PS-EOF                                                       
024800         ADD +1 TO PV-INPUT-RECS                                          
024900     END-IF.                                                              
025000****************************************************************          
025100** CLOSE FILES                                                **          
025200****************************************************************          
025300 3000-TERMINATION.                                                        
025400     CLOSE INPUT-USERID-FILE                                              
025500           OUTPUT-USERID-FILE.                                            
025600     MOVE PV-INPUT-RECS TO PV-PRINT-NUM.                                  
025700     DISPLAY 'DPKUT068  INPUT RECORDS: ' PV-PRINT-NUM.                    
025800     MOVE PV-OUTPUT-RECS TO PV-PRINT-NUM.                                 
025900     DISPLAY 'DPKUT068 OUTPUT RECORDS: ' PV-PRINT-NUM.                    
