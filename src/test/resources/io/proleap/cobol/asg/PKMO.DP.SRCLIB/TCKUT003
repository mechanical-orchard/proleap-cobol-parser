      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
                                                                        00040000
       PROGRAM-ID.             TCKUT003.                                00050000
       AUTHOR.                 J MYERS.                                 00060000
       INSTALLATION.           KOHLS.                                   00070000
       DATE-WRITTEN            02/2002.                                 00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ***************************************************************** 00110000
      *  PURPOSE:                                                       00120000
      *  CREATE A FLAT FILE FROM THE 5/3RD ADJUSTMENT FILE.  THIS WILL  00130000
      *  BE A FILE OF ADJUSTMENTS AND ADJUSTMENT ADDENDUM RECORDS.      00140000
      ***************************************************************** 00260000
       ENVIRONMENT DIVISION.                                            00290000
      ***************************************************************** 00300000
                                                                        00310000
       CONFIGURATION SECTION.                                           00320000
                                                                        00330000
       SOURCE-COMPUTER.        IBM-3090.                                00340000
                                                                        00350000
       OBJECT-COMPUTER.        IBM-3090.                                00360000
                                                                        00370000
       INPUT-OUTPUT SECTION.                                            00380000
                                                                        00390000
       FILE-CONTROL.                                                    00400000
                                                                        00410000
           SELECT ADJUSTMENT-FILE                                       00420000
               ASSIGN TO INP01.                                         00430000
                                                                        00440000
           SELECT ADJ-EXTRACT-FILE                                      00450000
               ASSIGN TO OUT01.                                         00460000
                                                                        00470000
      ******************************************************************00550000
       DATA DIVISION.                                                   00560000
      ******************************************************************00570000
                                                                        00580000
       FILE SECTION.                                                    00590000
                                                                        00600000
       FD  ADJUSTMENT-FILE                                              00610003
           RECORDING MODE IS F                                          00620000
           BLOCK CONTAINS 0 RECORDS                                     00630000
           LABEL RECORDS ARE STANDARD.                                  00640000
                                                                        00650000
       01  ADJUSTMENT-RECORD           PIC X(320).                      00660000
                                                                        00670000
       FD  ADJ-EXTRACT-FILE                                             00800000
           RECORDING MODE IS F                                          00810000
           LABEL RECORDS ARE STANDARD                                   00820000
           BLOCK CONTAINS 0 RECORDS                                     00830000
           RECORD CONTAINS  258  CHARACTERS.                            00840000
                                                                        00850000
       01  ADJ-EXTRACT-RECORD            PIC X(258).                    00860000
                                                                        00870000
      ******************************************************************00890000
       WORKING-STORAGE SECTION.                                         00900000
      ******************************************************************00910000
       01  FILLER                            PIC X(50)  VALUE           00920000
           ' *** WORKING STORAGE STARTS HERE FOR TCKUT003 *** '.        00930000
                                                                        00940000
      ******************************************************************00950000
      * RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                   *00960000
      ******************************************************************00970000
        01  PS-PROGRAM-SWITCHES.                                        01160000
           05   PS-END-OF-INPUT-FILE-SW      PIC X(01)  VALUE 'N'.      01200010
               88   PS-END-OF-INPUT-FILE                VALUE 'Y'.      01210010
                                                                        01351202
           05   PS-2ND-AD-RECORD-SW          PIC X(01)  VALUE 'N'.      01351302
               88   NOT-2ND-AD-RECORD                   VALUE 'N'.      01351402
               88   2ND-AD-RECORD                       VALUE 'Y'.      01351502
                                                                        01351602
       01  PC-CONSTANTS.                                                01360000
           05  PC-CURRENT-PROGRAM-NAME       PIC  X(08) VALUE           01400000
                                                       'TCKUT003'.      01400100
       01  PV-VARIBLES.                                                 01400221
           05  PV-TRAN-SEQ-NUMBER.                                      01400321
               10  FILLER                    PIC X(02).                 01400421
               10  PV-TRAN-NUMBER            PIC X(04).                 01400521
                                                                        01400621
       01  WORK-AREA.                                                   01401000
           05  WS-ADJ-TERMINAL-NO.                                      01402000
               10  WS-ADJ-STORE-NO           PIC X(04).                 01402200
               10  WS-ADJ-ZEROES             PIC X(04).                 01402314
               10  WS-ADJ-REG-NO             PIC X(04).                 01402400
               10  WS-ADJ-FILLER             PIC X(03).                 01402514
           05  WS-TEMP-ADJUST-AMT            PIC 9(08)V99.              01402615
           05  WS-ADJUST-AMT                 PIC S9(08)V99.             01402715
                                                                        01402815
       01  WORK-SAVE-AREA.                                              01402915
           05  WS-SV-AD-ADJ-NBR              PIC X(12).                 01403015
                                                                        01404015
      ***************************************************************** 03260000
      *  ADJUSTMENT INPUT FILE   LAYOUT                                 03270010
      ******************************************************************03280000
           COPY TCWS004.                                                03290023
                                                                        03300017
      ***************************************************************** 04561000
      *  ADJUSTMENT EXTRACT FILE LAYOUT                                 04562000
      ******************************************************************04563000
           COPY TCWS005.                                                04564023
                                                                        04565017
       PROCEDURE DIVISION.                                              05550000
                                                                        05560017
      ******************************************************************05570000
      * 0000-MAINLINE-PARAGRAPH                                         05580000
      ******************************************************************05600000
       0000-MAINLINE-PARAGRAPH.                                         05610000
                                                                        05620000
           PERFORM 1000-INITIALIZE-PROGRAM.                             05630000
                                                                        05631000
           PERFORM 2000-READ-INPUT-FILE.                                05640010
                                                                        05650000
           PERFORM 3000-PROCESS                                         05700000
               UNTIL  PS-END-OF-INPUT-FILE                              05710010
                                                                        05720000
           CLOSE ADJUSTMENT-FILE                                        05940000
                 ADJ-EXTRACT-FILE.                                      05950000
                                                                        05980000
           GOBACK.                                                      05990000
                                                                        06000000
      ******************************************************************06020000
      * 1000-INITIALIZE-PROGRAM                                         06030000
      *     OPEN FILES, PROCESS CONTROL CARD                            06040000
      ******************************************************************06050000
       1000-INITIALIZE-PROGRAM.                                         06070000
                                                                        06080000
           OPEN INPUT  ADJUSTMENT-FILE                                  06090000
                OUTPUT ADJ-EXTRACT-FILE.                                06100000
                                                                        06130000
      ******************************************************************06141002
      * 2000-READ-INPUT-FILE                                            06142002
      *     READ THE ADJUSTMENT INPUT FILE                              06143002
      ******************************************************************06144002
       2000-READ-INPUT-FILE.                                            06145017
                                                                        06150000
           READ ADJUSTMENT-FILE INTO TC004-ADJUSTMENT-DATA              06160000
               AT END                                                   06170000
                   SET PS-END-OF-INPUT-FILE TO TRUE.                    06180010
                                                                        06181000
      ***************************************************************** 08860200
      * 3000-PROCESS                                                    08860300
      *      PROCESS THE INPUT FIELD LOOKING FOR TD (AD/A2) RECORDS     08860402
      ***************************************************************** 08860500
       3000-PROCESS.                                                    08860617
                                                                        08860700
           INITIALIZE TC005-ADJUSTMENT-FILE.                            08860800
           SET NOT-2ND-AD-RECORD  TO TRUE                               08860909
                                                                        08861402
           IF  TC004-ADJ-RECORD-TYPE  =  'AD'                           08863909
               PERFORM 4000-MOVE-AD-FIELDS                              08864012
               MOVE TC004-TDTL-ADJ-NBR TO  WS-SV-AD-ADJ-NBR             08865009
               PERFORM 3500-READ-FOR-A2-MATCH                           08865109
           ELSE                                                         08865409
               DISPLAY 'A2 RECORD MISMATCH = ' TC004-TDAA-ADJ-NBR       08865509
           END-IF.                                                      08865609
                                                                        08869401
      ***************************************************************** 08869509
      *      2ND AD RECORD WITHOUT A MATCH ON 1ST                       08869709
      *      DO NO READ INPUT FILE                                      08869809
      *      TRY TO PROCESS THE 2ND AD RECORD.                          08869909
      ***************************************************************** 08870009
           IF 2ND-AD-RECORD                                             08870109
               CONTINUE                                                 08870209
           ELSE                                                         08871009
               PERFORM 2000-READ-INPUT-FILE                             08880010
           END-IF.                                                      13886009
                                                                        13886109
      ******************************************************************13886502
      * 3500-READ-FOR-A2-MATCH                                          13886609
      *     READ INPUT FILE TO GET A2 MATCH RECORD                      13886709
      ******************************************************************13886802
       3500-READ-FOR-A2-MATCH.                                          13886917
                                                                        13887002
           PERFORM 2000-READ-INPUT-FILE.                                13887110
                                                                        13887202
           IF  TC004-ADJ-RECORD-TYPE  = 'A2'                            13887309
               IF  WS-SV-AD-ADJ-NBR  NOT =  TC004-TDAA-ADJ-NBR          13887609
                   DISPLAY 'A2 RECORD MISMATCH = ' TC004-TDAA-ADJ-NBR   13887809
                   DISPLAY 'AD RECORD MISMATCH = '  WS-SV-AD-ADJ-NBR    13887909
               ELSE                                                     13888009
                   PERFORM 4100-MOVE-A2-FIELDS                          13888212
                   PERFORM 5000-WRITE-EXTRACT-FILE                      13888309
               END-IF                                                   13888409
           ELSE                                                         13888509
               SET 2ND-AD-RECORD  TO TRUE                               13888609
               DISPLAY 'AD RECORD MISMATCH = '  WS-SV-AD-ADJ-NBR        13888709
           END-IF.                                                      13888809
                                                                        13888909
      ******************************************************************13891202
      * 4000-MOVE-AD-FIELDS                                             13891312
      *     MOVE AD FIELDS TO OUTPUT COPYBOOK TCWS005                   13891412
      ******************************************************************13891502
       4000-MOVE-AD-FIELDS.                                             13891617
                                                                        13891702
           MOVE TC004-TDTL-TERM-SEQ-NBR                                 13891802
                                       TO  PV-TRAN-SEQ-NUMBER.          13891921
           MOVE  PV-TRAN-NUMBER                                         13892121
                                       TO TC005-TRAN-NBR.               13892202
           MOVE TC004-TDTL-ORIG-TRN-DTE                                 13892302
                                       TO TC005-ORIG-TRAN-DTE.          13892402
           MOVE TC004-TDTL-PRI-ACCT-NBR                                 13892522
                                       TO TC005-ORIG-CARD-NBR.          13892613
           MOVE TC004-TDTL-PRI-ACCT-NBR                                 13892702
                                       TO TC005-PRI-CARD-NBR.           13892813
           MOVE TC004-TDTL-ORIG-COMMENTS                                13892902
                                       TO TC005-ORIG-COMMENTS.          13893002
           MOVE TC004-TDTL-RECV-COMMENTS                                13893102
                                       TO TC005-RECV-COMMENTS.          13893202
           MOVE TC004-TDTL-FORCE-STTL-DTE                               13893302
                                       TO TC005-FORCE-STTL-DTE.         13893402
           MOVE TC004-TDTL-ADJ-TMESTMP TO TC005-ADJ-TIMESTAMP.          13893502
           MOVE TC004-TDTL-TRAN-TME    TO TC005-ORIG-TRAN-TM.           13893602
           MOVE TC004-TDTL-ORIG-AMT    TO TC005-ORIG-TRANS-AMT.         13893702
           MOVE TC004-TDTL-ADJ-TYP     TO TC005-ADJUST-TYP.             13893802
           MOVE TC004-TDTL-ADJ-REASON  TO TC005-ADJ-REASON.             13893902
           MOVE TC004-TDTL-ADJ-NBR     TO TC005-ADJ-NBR.                13894002
           MOVE TC004-TDTL-TRAN-DISP-QUAL                               13894120
                                       TO TC005-TRAN-DISP.              13895020
           MOVE TC004-TDTL-ACT-AMT     TO TC005-ADJUST-AMT.             13895716
           MOVE TC004-TDTL-ADJ-TMESTMP TO TC005-STTLMNT-DATE.           13895819
                                                                        13895915
      ******************************************************************13896015
      * 4100-MOVE-A2-FIELDS                                             13896115
      *     MOVE A2 FIELDS TO OUTPUT COPYBOOK TCWS005                   13896215
      ******************************************************************13896315
       4100-MOVE-A2-FIELDS.                                             13896417
                                                                        13897015
           MOVE TC004-TDAA-ADJ-TERM-NBR                                 13898012
                                       TO WS-ADJ-TERMINAL-NO.           13898112
           MOVE WS-ADJ-STORE-NO        TO TC005-STORE-NUMBER.           13898212
           MOVE WS-ADJ-REG-NO          TO TC005-REG-NBR.                13898312
                                                                        13898512
      ******************************************************************13898602
      * 5000-WRITE-EXTRACT-FILE                                         13898702
      *     WRITE THE EXTRACT FILE FROM COPYBOOK TCWS005                13898802
      ******************************************************************13898902
       5000-WRITE-EXTRACT-FILE.                                         13899017
                                                                        13899117
           WRITE ADJ-EXTRACT-RECORD  FROM TC005-ADJUSTMENT-FILE.        13899200
                                                                        13900000
