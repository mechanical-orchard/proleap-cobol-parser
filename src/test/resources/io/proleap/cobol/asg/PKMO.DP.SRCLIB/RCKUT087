000100 IDENTIFICATION DIVISION.                                                 
000200 TITLE 'CREATE PBS REPORT EXTRACT'.                                       
000300                                                                          
000400 PROGRAM-ID.             RCKUT087.                                        
000500 AUTHOR.                 DENNIS STEFFEN.                                  
000600 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000700 DATE-WRITTEN            APRIL 2006.                                      
000800 DATE-COMPILED.                                                           
000900                                                                          
001000 ENVIRONMENT DIVISION.                                                    
001100 CONFIGURATION SECTION.                                                   
001200                                                                          
001300 SOURCE-COMPUTER.        IBM-3090.                                        
001400 OBJECT-COMPUTER.        IBM-3090.                                        
001500 INPUT-OUTPUT SECTION.                                                    
001600 FILE-CONTROL.                                                            
001700                                                                          
001800     SELECT RPT-FILE                                                      
001900         ASSIGN TO OUT01.                                                 
002000                                                                          
002100 DATA DIVISION.                                                           
002200 FILE SECTION.                                                            
002300                                                                          
002400 FD  RPT-FILE                                                             
002500     RECORDING MODE IS F                                                  
002600     LABEL RECORDS ARE STANDARD                                           
002700     BLOCK CONTAINS 0 RECORDS.                                            
002800                                                                          
002900 01  RPT-REC                          PIC X(080).                         
003000                                                                          
003100 WORKING-STORAGE SECTION.                                                 
003200                                                                          
003300 01  FILLER.                                                              
003400     05  FILLER                      PIC  X(36)    VALUE                  
003500         ' ***** WORKING STORAGE PGM=RCKUT087 '.                          
003600     05  PGM-COMPILED                PIC  X(20)    VALUE SPACE.           
003700     05  FILLER                      PIC  X(10)    VALUE                  
003800         ' LOCATION='.                                                    
003900     05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.           
004000     05  FILLER                      PIC  X(06)    VALUE                  
004100         ' *****'.                                                        
004200                                                                          
004300 01  PV-SYSIN-CARD.                                                       
004400     05  PV-SYSIN-DTE                 PIC X(10).                          
004500     05  FILLER                       PIC X(80).                          
004600                                                                          
004700 01  PS-SWITCHES.                                                         
004800     05  PS-END-CSR-SW                PIC X(01) VALUE 'N'.                
004900         88  PS-END-CSR-YES                     VALUE 'Y'.                
005000         88  PS-END-CSR-NO                      VALUE 'N'.                
005100     05  PS-END-LINE-CSR-SW           PIC X(01) VALUE 'N'.                
005200         88  PS-END-LINE-CSR-YES                VALUE 'Y'.                
005300         88  PS-END-LINE-CSR-NO                 VALUE 'N'.                
005400                                                                          
005500     05  PS-ERROR-SW                  PIC X(01) VALUE 'N'.                
005600         88  PS-ERROR-YES                       VALUE 'Y'.                
005700         88  PS-ERROR-NO                        VALUE 'N'.                
005800                                                                          
005900 01  PV-BUCKETS.                                                          
006000     05  PV-AP-VERIFY-ITM-QTY         PIC S9(9) COMP.                     
006100     05  PV-UNT-RTL-PR-AMT            PIC S9(9)V9(2) COMP-3.              
006200     05  PV-CNCLD-UNT-QTY             PIC S9(9) COMP.                     
006300     05  PV-UNT-QTY                   PIC S9(9) COMP.                     
006310     05  PV-PO-INVOICE-QTY            PIC S9(9) COMP.                     
006400                                                                          
006500 01  PC-CONSTANTS.                                                        
006600     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
006700                                                   'RCKUT087'.            
006800     05  PC-ILBOABN0                 PIC  X(08)    VALUE                  
006900                                                   'ILBOABN0'.            
007000     05  PC-MAX-RETRIES              PIC S9(04)    VALUE  +3              
007100                                                   COMP.                  
007200 01  DN-NULL-INDICATORS.                                                  
007300     05  DN-NULL-1                    PIC S9(04) COMP VALUE ZERO.         
007400     05  DN-NULL-2                    PIC S9(04) COMP VALUE ZERO.         
007500     05  DN-NULL-3                    PIC S9(04) COMP VALUE ZERO.         
007600     05  DN-NULL-4                    PIC S9(04) COMP VALUE ZERO.         
007700     05  DN-NULL-5                    PIC S9(04) COMP VALUE ZERO.         
007710     05  DN-NULL-6                    PIC S9(04) COMP VALUE ZERO.         
007800                                                                          
007900 01  PV-MISC-STUFF.                                                       
008000     05  PV-ABEND-PARAGRAPH           PIC  X(30) VALUE SPACES.            
008100     05  PV-DB2-OPERATION-ATTEMPTED   PIC  X(60) VALUE SPACE.             
008200     05  PV-SQL-CODE                  PIC  9(04) VALUE ZERO.              
008300     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
008400                                                   COMP SYNC.             
008500     05  PV-CURR-PO-NBR               PIC S9(09) VALUE ZERO               
008600                                                   COMP SYNC.             
008700     05  PV-PREV-PO-NBR               PIC S9(09) VALUE ZERO               
008800                                                   COMP SYNC.             
008900     05  PV-RETRY-COUNTER             PIC S9(04) VALUE ZERO               
009000                                                 COMP SYNC.               
009100     05  PV-READ-COUNTER              PIC S9(09) COMP-3                   
009200                                              VALUE ZERO.                 
009300     05  PV-WRITE-1-COUNTER           PIC S9(09) COMP-3                   
009400                                              VALUE ZERO.                 
009500     05  PV-SKIPPED-COUNTER           PIC S9(09) COMP-3                   
009600                                              VALUE ZERO.                 
009700     05  PV-NOT-INV-COUNTER           PIC S9(09) COMP-3                   
009800                                              VALUE ZERO.                 
009900     05  PV-BYPASS-COUNT              PIC S9(09) COMP-3                   
010000                                              VALUE ZERO.                 
010100     05  PV-STORE-COUNT               PIC S9(05) COMP-3                   
010200                                              VALUE ZERO.                 
010300     05  PV-PERCENT                   PIC S9(05)V9(4) COMP-3              
010400                                              VALUE ZERO.                 
010500     05  PV-PARM-PERCENT              PIC S9(9) COMP                      
010600                                              VALUE ZERO.                 
010700     05  PV-NEAR-DTE                  PIC X(10) VALUE SPACES.             
010800     05  PV-FAR-DTE                   PIC X(10) VALUE SPACES.             
010900     05  PV-DISP-NBR                  PIC ZZZ,ZZZ,ZZ9.                    
011000     05  PV-ABEND-OPERATION           PIC X(20) VALUE SPACES.             
011100     05  PV-ABEND-STRUCTURE-1         PIC X(20) VALUE SPACES.             
011200                                                                          
011300 01  ABEND-CODE                       PIC S9(04)     VALUE +0             
011400                                                     COMP SYNC.           
011500     88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
011600     88  ABEND-4002-READ-ERROR                       VALUE +4002.         
011700     88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
011800     88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
011900     88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
012000     88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
012100     88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
012200     88  ABEND-4008-DATS-ERROR                       VALUE +4008.         
012300     88  ABEND-4009-KEY-DATS-ERROR                   VALUE +4009.         
012400     88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
012500                                                                          
012600     COPY DPWS004.                                                        
012700                                                                          
012800 01  PV-COUNT-MSG-1.                                                      
012900     05  FILLER                       PIC X(27) VALUE                     
013000         'RECORDS WRITTEN TO OUT01 = '.                                   
013100     05  PV-MSG-1-COUNT               PIC ZZZ,ZZZ,ZZ9.                    
013200                                                                          
013300*    REPORT EXTRACT                                                       
013400     COPY RCRD023.                                                        
013500/                                                                         
013600     EXEC SQL                                                             
013700         INCLUDE TPURCHS                                                  
013800     END-EXEC.                                                            
013900                                                                          
014000     EXEC SQL                                                             
014100         INCLUDE TPURITM                                                  
014200     END-EXEC.                                                            
014300                                                                          
014400     EXEC SQL                                                             
014500         INCLUDE TRECEIV                                                  
014600     END-EXEC.                                                            
014700                                                                          
014800     EXEC SQL                                                             
014900         INCLUDE TRECITM                                                  
015000     END-EXEC.                                                            
015100                                                                          
015200     EXEC SQL                                                             
015300         INCLUDE TRECDIS                                                  
015400     END-EXEC.                                                            
015500                                                                          
015600     EXEC SQL                                                             
015700         INCLUDE TPOAGMT                                                  
015800     END-EXEC.                                                            
015900                                                                          
016000     EXEC SQL                                                             
016100         INCLUDE TKRPRPM                                                  
016200     END-EXEC.                                                            
016300                                                                          
016400*    XLT_LOC                                                              
016500     EXEC SQL                                                             
016600         INCLUDE XLT00010                                                 
016700     END-EXEC.                                                            
016800                                                                          
016900*    XLT_VEND                                                             
017000     EXEC SQL                                                             
017100         INCLUDE XLT00035                                                 
017200     END-EXEC.                                                            
017300                                                                          
017350     EXEC SQL                                                             
017360         INCLUDE TINVOIC                                                  
017370     END-EXEC.                                                            
017371                                                                          
017372     EXEC SQL                                                             
017373         INCLUDE TPREQIT                                                  
017374     END-EXEC.                                                            
017380                                                                          
017400     EXEC SQL                                                             
017500         INCLUDE SQLCA                                                    
017600     END-EXEC.                                                            
017700                                                                          
017800     COPY SQLCA2.                                                         
017900                                                                          
018000     EXEC SQL                                                             
018100         DECLARE PBS-CSR CURSOR FOR                                       
018200      SELECT DISTINCT                                                     
018300             RCV.ORD_NBR                                                  
018400*       ,    RCV.REC_SEQ_NBR                                              
018500                                                                          
018600      FROM TRECEIV RCV                                                    
018700       ,   TPOAGMT AG                                                     
018800       ,   XLT_LOC LOC                                                    
018900       ,   TKRPRPM PM1                                                    
019000       ,   TKRPRPM PM2                                                    
019100                                                                          
019200                                                                          
019300       WHERE RCV.STATUS_CDE     = 'VE'                                    
019400         AND RCV.REC_TYP_CDE    = 'MR'                                    
019500         AND RCV.VER_STATUS_CDE = 'A'                                     
019600                                                                          
019700*        IS IT A DC RECEIVER?                                             
019800         AND RCV.CURR_OPER_UNT_ID = LOC.LOC_NBR                           
019900         AND LOC.LOC_TYP_CDE = 'DC'                                       
020000                                                                          
020100*           IS IT ACTIVE?                                                 
020200                                                                          
020300         AND PM1.LOC_ID           = 90                                    
020400         AND PM1.INTFC_SYS_CDE    = 'KHL'                                 
020500         AND PM1.LOC_ID           = PM2.LOC_ID                            
020600         AND PM1.INTFC_SYS_CDE    = PM2.INTFC_SYS_CDE                     
020700         AND PM1.SYS_PRC_FUNC_CDE = 'PBSBEG'                              
020800         AND PM2.SYS_PRC_FUNC_CDE = 'PBSEND'                              
020900         AND                                                              
021000           (                                                              
021100           DATE(RCV.VERFYD_TMST) BETWEEN                                  
021200                (DATE(:PV-SYSIN-DTE) - PM2.FUNC_QTY DAYS)                 
021300         AND                                                              
021400                 (DATE(:PV-SYSIN-DTE) - PM1.FUNC_QTY DAYS)                
021500           )                                                              
021600                                                                          
021700*                IS IT A PBS ORDER?                                       
021800          AND                                                             
021900          (                                                               
022000          RCV.ORD_NBR = AG.PO_NBR                                         
022100          AND AG.AGRMT_TYP_ID   = '4'                                     
022200          AND AG.VER_STATUS_CDE = 'A'                                     
022300          AND AG.TYP_CDE        = 4                                       
022400         )                                                                
022500                                                                          
022600         WITH UR                                                          
022700     END-EXEC.                                                            
022800/                                                                         
022900     EXEC SQL                                                             
023000         DECLARE LINE-CSR CURSOR FOR                                      
023100      SELECT DISTINCT                                                     
023200          RITM.ORD_LNE_NBR                                                
023300       ,  PITM.UNT_RTL_PR_AMT                                             
023400       ,  PITM.CNCLD_UNT_QTY                                              
023500       ,  PITM.UNT_QTY                                                    
023600                                                                          
023700      FROM TRECITM RITM                                                   
023800       ,   TPURITM PITM                                                   
023900                                                                          
024000                                                                          
024100       WHERE RITM.ORD_NBR        = :RECEIV-ORD-NBR                        
024200         AND RITM.VER_STATUS_CDE = 'A'                                    
024300         AND RITM.ORD_NBR        = PITM.PO_NBR                            
024400         AND RITM.ORD_LNE_NBR    = PITM.APP_PO_LNE_NBR                    
024500         AND PITM.VER_STATUS_CDE = 'A'                                    
024600                                                                          
024700         WITH UR                                                          
024800     END-EXEC.                                                            
024900/                                                                         
025000 PROCEDURE DIVISION.                                                      
025100                                                                          
025200     PERFORM 1000-INITIALIZATION.                                         
025300                                                                          
025400     PERFORM 2000-PROCESS UNTIL PS-END-CSR-YES                            
025500                             OR PS-ERROR-YES                              
025600                                                                          
025700     PERFORM 9000-QUIT.                                                   
025800                                                                          
025900     GOBACK.                                                              
026000/                                                                         
026100 1000-INITIALIZATION.                                                     
026200     MOVE '1000-INITIALIZATION         '  TO PV-ABEND-PARAGRAPH.          
026300     DISPLAY 'RCKUT087 STARTS NOW'                                        
026400                                                                          
026500     ACCEPT PV-SYSIN-CARD FROM SYSIN                                      
026600                                                                          
026700     IF PV-SYSIN-DTE = SPACES OR LOW-VALUES                               
026800         DISPLAY SPACES                                                   
026900         DISPLAY ALL '*'                                                  
027000         DISPLAY '  SYSIN DATE IS MISSING - JOB GO BOOM'                  
027100         DISPLAY ALL '*'                                                  
027200         DISPLAY SPACES                                                   
027300         SET PS-ERROR-YES                                                 
027400             ABEND-4003-CTRL-CARD-ERROR TO TRUE                           
027500         CALL 'ILBOABN0' USING ABEND-CODE                                 
027600     ELSE                                                                 
027700         DISPLAY 'RUN DATE = ' PV-SYSIN-DTE                               
027800     END-IF.                                                              
027900                                                                          
028000     OPEN OUTPUT RPT-FILE.                                                
028100                                                                          
028200                                                                          
028300     PERFORM 7000-OPEN-PBS-CSR.                                           
028400     PERFORM 7010-FETCH-PBS-CSR.                                          
028500                                                                          
028600     IF PS-END-CSR-YES                                                    
028700         MOVE 1                       TO RETURN-CODE                      
028800         DISPLAY SPACES                                                   
028900         DISPLAY ALL '*'                                                  
029000         DISPLAY '  NO OVERAGES FOUND'                                    
029100         DISPLAY ALL '*'                                                  
029200         DISPLAY SPACES                                                   
029300     END-IF.                                                              
029400/                                                                         
029500 2000-PROCESS.                                                            
029600                                                                          
029700     MOVE '2000-PROCESS'                  TO PV-ABEND-PARAGRAPH.          
029800                                                                          
029900     PERFORM 2100-BUILD-RC023                                             
030000                                                                          
030100     EVALUATE TRUE                                                        
030200         WHEN RC023-VARIANCE = ZERO                                       
030300         WHEN RC023-ORIG-ORDER-QTY > RC023-VERIFY-QTY                     
030400             CONTINUE                                                     
030500         WHEN OTHER                                                       
030510             PERFORM 2300-SUM-PO-INVOICE-QTY                              
030520             MOVE PV-PO-INVOICE-QTY  TO RC023-PO-INVOICE-QTY              
030530             COMPUTE                                                      
030540                 RC023-INVCE-VARIANCE =                                   
030550                      RC023-VERIFY-QTY -                                  
030560                         RC023-PO-INVOICE-QTY                             
030570             END-COMPUTE                                                  
030580                                                                          
030600             PERFORM 8000-WRITE-RC023                                     
030700     END-EVALUATE.                                                        
030800                                                                          
030900     PERFORM 7010-FETCH-PBS-CSR.                                          
031000                                                                          
031100 2100-BUILD-RC023.                                                        
031200     INITIALIZE RC023-PBS-REPORT-EXTRACT.                                 
031300                                                                          
031400     MOVE RECEIV-ORD-NBR              TO RC023-ORD-NBR                    
031500                                                                          
031600     PERFORM 7100-SUM-RECDIS.                                             
031700     MOVE PV-AP-VERIFY-ITM-QTY        TO RC023-VERIFY-QTY                 
031800                                                                          
031900     PERFORM 7150-SUM-PURITM.                                             
032000     PERFORM 2200-EXTENDED-RTL.                                           
032110                                                                          
032200     COMPUTE RC023-ORIG-ORDER-QTY =                                       
032300          PV-UNT-QTY + PV-CNCLD-UNT-QTY                                   
032400     END-COMPUTE                                                          
032500                                                                          
032600     COMPUTE                                                              
032700         RC023-VARIANCE =                                                 
032800             RC023-ORIG-ORDER-QTY -                                       
032900                       RC023-VERIFY-QTY                                   
033000     END-COMPUTE.                                                         
033001                                                                          
033100                                                                          
033200     IF PV-CURR-PO-NBR = PV-PREV-PO-NBR                                   
033300         MOVE PURCHS-ENT-ID           TO RC023-ENT-ID                     
033400         MOVE PURCHS-OTB-PER-NBR      TO RC023-OTB-PER-NBR                
033500         MOVE XLT00035-PRIM-VND-NM    TO RC023-VNDR-NAME                  
033600     ELSE                                                                 
033700         PERFORM 7200-TPURCHS                                             
033800         MOVE PURCHS-ENT-ID           TO RC023-ENT-ID                     
033900         MOVE PURCHS-OTB-PER-NBR      TO RC023-OTB-PER-NBR                
034000         MOVE XLT00035-PRIM-VND-NM    TO RC023-VNDR-NAME                  
034100         MOVE PV-CURR-PO-NBR          TO PV-PREV-PO-NBR                   
034200     END-IF.                                                              
034300/                                                                         
034400 2200-EXTENDED-RTL.                                                       
034500                                                                          
034600     PERFORM 7300-OPEN-LINE-CSR.                                          
034700     IF SQLCODE = ZERO                                                    
034800         PERFORM 7310-FETCH-LINE-CSR                                      
034900     END-IF.                                                              
035000                                                                          
035100     PERFORM UNTIL PS-END-LINE-CSR-YES                                    
035200         PERFORM 7400-SELECT-RECDIS-QTYS                                  
035300         COMPUTE RC023-EXTENDED-RETAIL =                                  
035400                 RC023-EXTENDED-RETAIL +                                  
035500          ( ( PURITM-CNCLD-UNT-QTY + PURITM-UNT-QTY)                      
035600              - RECDIS-AP-VERIFY-ITM-QTY )                                
035700              * PURITM-UNT-RTL-PR-AMT                                     
035800         END-COMPUTE                                                      
035900         PERFORM 7310-FETCH-LINE-CSR                                      
036000     END-PERFORM.                                                         
036100                                                                          
036200     PERFORM 7320-CLOSE-LINE-CSR.                                         
036300                                                                          
036400 2300-SUM-PO-INVOICE-QTY.                                                 
036402                                                                          
036403     MOVE '2300-SUM-PO-INVOICE-QTY '  TO PV-ABEND-PARAGRAPH.              
036404                                                                          
036405     INITIALIZE PV-PO-INVOICE-QTY.                                        
036406                                                                          
036407     PERFORM                                                              
036408         WITH TEST AFTER                                                  
036409         VARYING PV-RETRY-COUNTER                                         
036410         FROM 1 BY 1                                                      
036411         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
036412            OR  (SQLCODE = +0)                                            
036413            OR  (SQLCODE = +100))                                         
036414                                                                          
036415     EXEC SQL                                                             
036416         SELECT                                                           
036417             SUM (PAY_REQ_LNE_QTY)                                        
036418         INTO                                                             
036419             :PV-PO-INVOICE-QTY :DN-NULL-6                                
036420         FROM                                                             
036422             TINVOIC B,                                                   
036423             TPREQIT C                                                    
036424         WHERE B.PO_NBR           = :RECEIV-ORD-NBR                       
036427           AND B.PAYT_REQ_SEQ_NBR = C.PAYT_REQ_SEQ_NBR                    
036428           AND B.ENTR_MTHD_CDE    = 'ED'                                  
036429           AND NOT B.STATUS_CDE   = 'DE'                                  
036439         WITH UR                                                          
036440     END-EXEC                                                             
036454                                                                          
036455         EVALUATE TRUE                                                    
036456             WHEN SQLCODE = ZERO                                          
036457             WHEN SQLCODE = +100                                          
036458                IF DN-NULL-6 = -1                                         
036459                   MOVE ZEROES TO PV-PO-INVOICE-QTY                       
036460                END-IF                                                    
036461             WHEN SQLCODE = -904                                          
036462             WHEN SQLCODE = -911                                          
036463             WHEN SQLCODE = -913                                          
036464                  CONTINUE                                                
036465                                                                          
036466             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
036467                 DISPLAY '**************************************'         
036468                 DISPLAY 'WARNING ISSUED '                                
036469                 DISPLAY 'SUM INVOICE QTY                       '         
036470                 DISPLAY '**************************************'         
036471                 PERFORM 9800-SQL-WARNING                                 
036472                                                                          
036473             WHEN SQLCODE < ZERO                                          
036474                 DISPLAY '**************************************'         
036475                 DISPLAY 'ERROR ISSUED ON                       '         
036476                 DISPLAY 'SUM INVOICE QTY                       '         
036477                 DISPLAY '**************************************'         
036478                 PERFORM 9900-SQL-ERROR                                   
036479                                                                          
036480         END-EVALUATE                                                     
036481     END-PERFORM.                                                         
036482                                                                          
036483     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
036484         SQLCODE NOT = ZERO                                               
036485         DISPLAY '**************************************'                 
036486         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
036487         DISPLAY 'SUM QTYS                              '                 
036488         DISPLAY '**************************************'                 
036489         PERFORM 9900-SQL-ERROR                                           
036490     END-IF.                                                              
036491                                                                          
036492 7000-OPEN-PBS-CSR.                                                       
036500                                                                          
036600     MOVE '7000-OPEN-PBS-CSR'       TO PV-ABEND-PARAGRAPH.                
036700                                                                          
036800     PERFORM                                                              
036900         WITH TEST AFTER                                                  
037000         VARYING PV-RETRY-COUNTER                                         
037100         FROM 1 BY 1                                                      
037200         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
037300            OR  (SQLCODE = +0))                                           
037400                                                                          
037500         EXEC SQL                                                         
037600             OPEN PBS-CSR                                                 
037700         END-EXEC                                                         
037800                                                                          
037900         EVALUATE TRUE                                                    
038000             WHEN SQLCODE = ZERO                                          
038100                 CONTINUE                                                 
038200             WHEN SQLCODE = -904                                          
038300             WHEN SQLCODE = -911                                          
038400             WHEN SQLCODE = -913                                          
038500                  CONTINUE                                                
038600                                                                          
038700             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
038800                 DISPLAY '**************************************'         
038900                 DISPLAY 'WARNING ISSUED '                                
039000                 DISPLAY 'OPEN PBS-CSR                          '         
039100                 DISPLAY '**************************************'         
039200                 PERFORM 9800-SQL-WARNING                                 
039300                                                                          
039400             WHEN SQLCODE < ZERO                                          
039500                 DISPLAY '**************************************'         
039600                 DISPLAY 'ERROR ISSUED ON                       '         
039700                 DISPLAY 'OPEN PBS-CSR                          '         
039800                 DISPLAY '**************************************'         
039900                 PERFORM 9900-SQL-ERROR                                   
040000                                                                          
040100         END-EVALUATE                                                     
040200     END-PERFORM.                                                         
040300                                                                          
040400     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
040500         SQLCODE NOT = ZERO                                               
040600         DISPLAY '**************************************'                 
040700         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
040800         DISPLAY 'OPEN PBS-CSR                          '                 
040900         DISPLAY '**************************************'                 
041000         PERFORM 9900-SQL-ERROR                                           
041100     END-IF.                                                              
041200                                                                          
041300 7010-FETCH-PBS-CSR.                                                      
041400                                                                          
041500     MOVE '7010-FETCH-PBS-CSR'        TO PV-ABEND-PARAGRAPH.              
041600                                                                          
041700     PERFORM                                                              
041800         WITH TEST AFTER                                                  
041900         VARYING PV-RETRY-COUNTER                                         
042000         FROM 1 BY 1                                                      
042100         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
042200            OR  (SQLCODE = +0)                                            
042300            OR  (SQLCODE = +100))                                         
042400                                                                          
042500         EXEC SQL                                                         
042600             FETCH PBS-CSR                                                
042700             INTO                                                         
042800             :RECEIV-ORD-NBR                                              
042900         END-EXEC                                                         
043000                                                                          
043100         EVALUATE TRUE                                                    
043200             WHEN SQLCODE = ZERO                                          
043300                 MOVE RECEIV-ORD-NBR  TO PV-CURR-PO-NBR                   
043400             WHEN SQLCODE = +100                                          
043500                 SET PS-END-CSR-YES   TO TRUE                             
043600             WHEN SQLCODE = -904                                          
043700             WHEN SQLCODE = -911                                          
043800             WHEN SQLCODE = -913                                          
043900                  CONTINUE                                                
044000                                                                          
044100             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
044200                 DISPLAY '**************************************'         
044300                 DISPLAY 'WARNING ISSUED '                                
044400                 DISPLAY 'FETCH PBS-CSR                         '         
044500                 DISPLAY '**************************************'         
044600                 PERFORM 9800-SQL-WARNING                                 
044700                                                                          
044800             WHEN SQLCODE < ZERO                                          
044900                 DISPLAY '**************************************'         
045000                 DISPLAY 'ERROR ISSUED ON                       '         
045100                 DISPLAY 'FETCH PBS-CSR                         '         
045200                 DISPLAY '**************************************'         
045300                 PERFORM 9900-SQL-ERROR                                   
045400                                                                          
045500         END-EVALUATE                                                     
045600     END-PERFORM.                                                         
045700                                                                          
045800     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
045900         SQLCODE NOT = ZERO                                               
046000         DISPLAY '**************************************'                 
046100         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
046200         DISPLAY 'FETCH PBS-CSR                         '                 
046300         DISPLAY '**************************************'                 
046400         PERFORM 9900-SQL-ERROR                                           
046500     END-IF.                                                              
046600                                                                          
046700 7020-CLOSE-PBS-CSR.                                                      
046800                                                                          
046900     MOVE '7020-CLOSE-CSR'            TO PV-ABEND-PARAGRAPH.              
047000                                                                          
047100     PERFORM                                                              
047200         WITH TEST AFTER                                                  
047300         VARYING PV-RETRY-COUNTER                                         
047400         FROM 1 BY 1                                                      
047500         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
047600            OR  (SQLCODE = +0))                                           
047700                                                                          
047800         EXEC SQL                                                         
047900             CLOSE PBS-CSR                                                
048000         END-EXEC                                                         
048100                                                                          
048200         EVALUATE TRUE                                                    
048300             WHEN SQLCODE = ZERO                                          
048400                 CONTINUE                                                 
048500             WHEN SQLCODE = -904                                          
048600             WHEN SQLCODE = -911                                          
048700             WHEN SQLCODE = -913                                          
048800                  CONTINUE                                                
048900                                                                          
049000             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
049100                 DISPLAY '**************************************'         
049200                 DISPLAY 'WARNING ISSUED '                                
049300                 DISPLAY 'CLOSE PBS-CSR                         '         
049400                 DISPLAY '**************************************'         
049500                 PERFORM 9800-SQL-WARNING                                 
049600                                                                          
049700             WHEN SQLCODE < ZERO                                          
049800                 DISPLAY '**************************************'         
049900                 DISPLAY 'ERROR ISSUED ON                       '         
050000                 DISPLAY 'CLOSE PBS-CSR                         '         
050100                 DISPLAY '**************************************'         
050200                 PERFORM 9900-SQL-ERROR                                   
050300                                                                          
050400         END-EVALUATE                                                     
050500     END-PERFORM.                                                         
050600                                                                          
050700     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
050800         SQLCODE NOT = ZERO                                               
050900         DISPLAY '**************************************'                 
051000         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
051100         DISPLAY 'CLOSE PBS-CSR                         '                 
051200         DISPLAY '**************************************'                 
051300         PERFORM 9900-SQL-ERROR                                           
051400     END-IF.                                                              
051500/                                                                         
051600 7100-SUM-RECDIS.                                                         
051700                                                                          
051800     MOVE '7100-SUM-RECDIS'           TO PV-ABEND-PARAGRAPH.              
051900                                                                          
052000     INITIALIZE PV-AP-VERIFY-ITM-QTY                                      
052100                                                                          
052200     PERFORM                                                              
052300         WITH TEST AFTER                                                  
052400         VARYING PV-RETRY-COUNTER                                         
052500         FROM 1 BY 1                                                      
052600         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
052700            OR  (SQLCODE = +0)                                            
052800            OR  (SQLCODE = +100))                                         
052900                                                                          
053000     EXEC SQL                                                             
053100         SELECT                                                           
053200             SUM(AP_VERIFY_ITM_QTY) AS VERIFY_QTY                         
053300         INTO                                                             
053400             :PV-AP-VERIFY-ITM-QTY:DN-NULL-1                              
053500         FROM                                                             
053600             TRECDIS                                                      
053700       WHERE                                                              
053800             ORD_NBR            = :RECEIV-ORD-NBR                         
054000                                                                          
054100         WITH UR                                                          
054200     END-EXEC                                                             
054300                                                                          
054400         EVALUATE TRUE                                                    
054500             WHEN SQLCODE = ZERO                                          
054600             WHEN SQLCODE = +100                                          
054700                 CONTINUE                                                 
054800             WHEN SQLCODE = -904                                          
054900             WHEN SQLCODE = -911                                          
055000             WHEN SQLCODE = -913                                          
055100                  CONTINUE                                                
055200                                                                          
055300             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
055400                 DISPLAY '**************************************'         
055500                 DISPLAY 'WARNING ISSUED '                                
055600                 DISPLAY 'SUM QTYS                              '         
055700                 DISPLAY '**************************************'         
055800                 PERFORM 9800-SQL-WARNING                                 
055900                                                                          
056000             WHEN SQLCODE < ZERO                                          
056100                 DISPLAY '**************************************'         
056200                 DISPLAY 'ERROR ISSUED ON                       '         
056300                 DISPLAY 'SUM QTYS                              '         
056400                 DISPLAY '**************************************'         
056500                 PERFORM 9900-SQL-ERROR                                   
056600                                                                          
056700         END-EVALUATE                                                     
056800     END-PERFORM.                                                         
056900                                                                          
057000     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
057100         SQLCODE NOT = ZERO                                               
057200         DISPLAY '**************************************'                 
057300         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
057400         DISPLAY 'SUM QTYS                              '                 
057500         DISPLAY '**************************************'                 
057600         PERFORM 9900-SQL-ERROR                                           
057700     END-IF.                                                              
057800                                                                          
057900 7150-SUM-PURITM.                                                         
058000                                                                          
058100     MOVE '7150-SUM-PURITM'           TO PV-ABEND-PARAGRAPH.              
058200                                                                          
058300     INITIALIZE PV-UNT-QTY                                                
058400                PV-CNCLD-UNT-QTY                                          
058500                                                                          
058600                                                                          
058700     PERFORM                                                              
058800         WITH TEST AFTER                                                  
058900         VARYING PV-RETRY-COUNTER                                         
059000         FROM 1 BY 1                                                      
059100         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
059200            OR  (SQLCODE = +0)                                            
059300            OR  (SQLCODE = +100))                                         
059400                                                                          
059500     EXEC SQL                                                             
059600                                                                          
059700      SELECT                                                              
059800             SUM(UNT_QTY)           AS UNT_QTY                            
059900      ,      SUM(CNCLD_UNT_QTY)     AS CANCELLED                          
060000         INTO                                                             
060100             :PV-UNT-QTY:DN-NULL-2                                        
060200       ,     :PV-CNCLD-UNT-QTY:DN-NULL-3                                  
060300      FROM                                                                
060400           TPURITM                                                        
060500       WHERE                                                              
060600             PO_NBR        = :RECEIV-ORD-NBR                              
060700         AND VER_STATUS_CDE = 'A'                                         
060800         WITH UR                                                          
060900     END-EXEC                                                             
061000                                                                          
061100         EVALUATE TRUE                                                    
061200             WHEN SQLCODE = ZERO                                          
061300             WHEN SQLCODE = +100                                          
061400                 CONTINUE                                                 
061500             WHEN SQLCODE = -904                                          
061600             WHEN SQLCODE = -911                                          
061700             WHEN SQLCODE = -913                                          
061800                  CONTINUE                                                
061900                                                                          
062000             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
062100                 DISPLAY '**************************************'         
062200                 DISPLAY 'WARNING ISSUED '                                
062300                 DISPLAY 'SUM QTYS                              '         
062400                 DISPLAY '**************************************'         
062500                 PERFORM 9800-SQL-WARNING                                 
062600                                                                          
062700             WHEN SQLCODE < ZERO                                          
062800                 DISPLAY '**************************************'         
062900                 DISPLAY 'ERROR ISSUED ON                       '         
063000                 DISPLAY 'SUM QTYS                              '         
063100                 DISPLAY '**************************************'         
063200                 PERFORM 9900-SQL-ERROR                                   
063300                                                                          
063400         END-EVALUATE                                                     
063500     END-PERFORM.                                                         
063600                                                                          
063700     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
063800         SQLCODE NOT = ZERO                                               
063900         DISPLAY '**************************************'                 
064000         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
064100         DISPLAY 'SUM QTYS                              '                 
064200         DISPLAY '**************************************'                 
064300         PERFORM 9900-SQL-ERROR                                           
064400     END-IF.                                                              
064500                                                                          
064600 7200-TPURCHS.                                                            
064700                                                                          
064800     MOVE '7200-TPURCHS'              TO PV-ABEND-PARAGRAPH.              
064900                                                                          
065000     INITIALIZE PV-BUCKETS.                                               
065100                                                                          
065200     PERFORM                                                              
065300         WITH TEST AFTER                                                  
065400         VARYING PV-RETRY-COUNTER                                         
065500         FROM 1 BY 1                                                      
065600         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
065700            OR  (SQLCODE = +0)                                            
065800            OR  (SQLCODE = +100))                                         
065900                                                                          
066000         EXEC SQL                                                         
066100             SELECT                                                       
066200                 P.ENT_ID                                                 
066300             ,   P.OTB_PER_NBR                                            
066400             ,   V.PRIM_VND_NM                                            
066500             INTO :PURCHS-ENT-ID                                          
066600             ,    :PURCHS-OTB-PER-NBR                                     
066700             ,    :XLT00035-PRIM-VND-NM                                   
066800                                                                          
066900      FROM                                                                
067000           TPURCHS    P                                                   
067100         , XLT_VND    V                                                   
067200                                                                          
067300       WHERE                                                              
067400                                                                          
067500         P.PO_NBR                 = :RECEIV-ORD-NBR                       
067600         AND P.ENT_ID             = V.ENT_ID                              
067700         AND P.VER_STATUS_CDE     = 'A'                                   
067800         AND V.STAT_CDE           = 'A'                                   
067900                                                                          
068000         WITH UR                                                          
068100     END-EXEC                                                             
068200                                                                          
068300         EVALUATE TRUE                                                    
068400             WHEN SQLCODE = ZERO                                          
068500             WHEN SQLCODE = +100                                          
068600                 CONTINUE                                                 
068700             WHEN SQLCODE = -904                                          
068800             WHEN SQLCODE = -911                                          
068900             WHEN SQLCODE = -913                                          
069000                  CONTINUE                                                
069100                                                                          
069200             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
069300                 DISPLAY '**************************************'         
069400                 DISPLAY 'WARNING ISSUED '                                
069500                 DISPLAY 'SELECT TPURCHS                        '         
069600                 DISPLAY '**************************************'         
069700                 PERFORM 9800-SQL-WARNING                                 
069800                                                                          
069900             WHEN SQLCODE < ZERO                                          
070000                 DISPLAY '**************************************'         
070100                 DISPLAY 'ERROR ISSUED ON                       '         
070200                 DISPLAY 'SELECT TPURCHS                        '         
070300                 DISPLAY '**************************************'         
070400                 PERFORM 9900-SQL-ERROR                                   
070500                                                                          
070600         END-EVALUATE                                                     
070700     END-PERFORM.                                                         
070800                                                                          
070900     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
071000         SQLCODE NOT = ZERO                                               
071100         DISPLAY '**************************************'                 
071200         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
071300         DISPLAY 'SELECT TPURCHS                        '                 
071400         DISPLAY '**************************************'                 
071500         PERFORM 9900-SQL-ERROR                                           
071600     END-IF.                                                              
071700/                                                                         
071800 7300-OPEN-LINE-CSR.                                                      
071900                                                                          
072000     MOVE '7300-OPEN-LINE-CSR'      TO PV-ABEND-PARAGRAPH.                
072100                                                                          
072200     SET PS-END-LINE-CSR-NO           TO TRUE                             
072300                                                                          
072400     PERFORM                                                              
072500         WITH TEST AFTER                                                  
072600         VARYING PV-RETRY-COUNTER                                         
072700         FROM 1 BY 1                                                      
072800         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
072900            OR  (SQLCODE = +0))                                           
073000                                                                          
073100         EXEC SQL                                                         
073200             OPEN LINE-CSR                                                
073300         END-EXEC                                                         
073400                                                                          
073500         EVALUATE TRUE                                                    
073600             WHEN SQLCODE = ZERO                                          
073700                 CONTINUE                                                 
073800             WHEN SQLCODE = -904                                          
073900             WHEN SQLCODE = -911                                          
074000             WHEN SQLCODE = -913                                          
074100                  CONTINUE                                                
074200                                                                          
074300             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
074400                 DISPLAY '**************************************'         
074500                 DISPLAY 'WARNING ISSUED '                                
074600                 DISPLAY 'OPEN LINE-CSR                         '         
074700                 DISPLAY '**************************************'         
074800                 PERFORM 9800-SQL-WARNING                                 
074900                                                                          
075000             WHEN SQLCODE < ZERO                                          
075100                 DISPLAY '**************************************'         
075200                 DISPLAY 'ERROR ISSUED ON                       '         
075300                 DISPLAY 'OPEN LINE-CSR                         '         
075400                 DISPLAY '**************************************'         
075500                 PERFORM 9900-SQL-ERROR                                   
075600                                                                          
075700         END-EVALUATE                                                     
075800     END-PERFORM.                                                         
075900                                                                          
076000     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
076100         SQLCODE NOT = ZERO                                               
076200         DISPLAY '**************************************'                 
076300         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
076400         DISPLAY 'OPEN LINE-CSR                         '                 
076500         DISPLAY '**************************************'                 
076600         PERFORM 9900-SQL-ERROR                                           
076700     END-IF.                                                              
076800                                                                          
076900 7310-FETCH-LINE-CSR.                                                     
077000                                                                          
077100     MOVE '7310-FETCH-LINE-CSR'       TO PV-ABEND-PARAGRAPH.              
077200                                                                          
077300     PERFORM                                                              
077400         WITH TEST AFTER                                                  
077500         VARYING PV-RETRY-COUNTER                                         
077600         FROM 1 BY 1                                                      
077700         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
077800            OR  (SQLCODE = +0)                                            
077900            OR  (SQLCODE = +100))                                         
078000                                                                          
078100         EXEC SQL                                                         
078200             FETCH LINE-CSR                                               
078300             INTO                                                         
078400             :RECITM-ORD-LNE-NBR                                          
078500       ,     :PURITM-UNT-RTL-PR-AMT                                       
078600       ,     :PURITM-CNCLD-UNT-QTY                                        
078700       ,     :PURITM-UNT-QTY                                              
078800         END-EXEC                                                         
078900                                                                          
079000         EVALUATE TRUE                                                    
079100             WHEN SQLCODE = ZERO                                          
079200                 CONTINUE                                                 
079300             WHEN SQLCODE = +100                                          
079400                 SET PS-END-LINE-CSR-YES   TO TRUE                        
079500             WHEN SQLCODE = -904                                          
079600             WHEN SQLCODE = -911                                          
079700             WHEN SQLCODE = -913                                          
079800                  CONTINUE                                                
079900                                                                          
080000             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
080100                 DISPLAY '**************************************'         
080200                 DISPLAY 'WARNING ISSUED '                                
080300                 DISPLAY 'FETCH LINE-CSR                        '         
080400                 DISPLAY '**************************************'         
080500                 PERFORM 9800-SQL-WARNING                                 
080600                                                                          
080700             WHEN SQLCODE < ZERO                                          
080800                 DISPLAY '**************************************'         
080900                 DISPLAY 'ERROR ISSUED ON                       '         
081000                 DISPLAY 'FETCH LINE-CSR                        '         
081100                 DISPLAY '**************************************'         
081200                 PERFORM 9900-SQL-ERROR                                   
081300                                                                          
081400         END-EVALUATE                                                     
081500     END-PERFORM.                                                         
081600                                                                          
081700     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
081800         SQLCODE NOT = ZERO                                               
081900         DISPLAY '**************************************'                 
082000         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
082100         DISPLAY 'FETCH LINE-CSR                        '                 
082200         DISPLAY '**************************************'                 
082300         PERFORM 9900-SQL-ERROR                                           
082400     END-IF.                                                              
082500                                                                          
082600 7320-CLOSE-LINE-CSR.                                                     
082700                                                                          
082800     MOVE '7320-CLOSE-LINE-CSR'       TO PV-ABEND-PARAGRAPH.              
082900                                                                          
083000     PERFORM                                                              
083100         WITH TEST AFTER                                                  
083200         VARYING PV-RETRY-COUNTER                                         
083300         FROM 1 BY 1                                                      
083400         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
083500            OR  (SQLCODE = +0))                                           
083600                                                                          
083700         EXEC SQL                                                         
083800             CLOSE LINE-CSR                                               
083900         END-EXEC                                                         
084000                                                                          
084100         EVALUATE TRUE                                                    
084200             WHEN SQLCODE = ZERO                                          
084300                 CONTINUE                                                 
084400             WHEN SQLCODE = -904                                          
084500             WHEN SQLCODE = -911                                          
084600             WHEN SQLCODE = -913                                          
084700                  CONTINUE                                                
084800                                                                          
084900             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
085000                 DISPLAY '**************************************'         
085100                 DISPLAY 'WARNING ISSUED '                                
085200                 DISPLAY 'CLOSE LINE-CSR                        '         
085300                 DISPLAY '**************************************'         
085400                 PERFORM 9800-SQL-WARNING                                 
085500                                                                          
085600             WHEN SQLCODE < ZERO                                          
085700                 DISPLAY '**************************************'         
085800                 DISPLAY 'ERROR ISSUED ON                       '         
085900                 DISPLAY 'CLOSE LINE-CSR                        '         
086000                 DISPLAY '**************************************'         
086100                 PERFORM 9900-SQL-ERROR                                   
086200                                                                          
086300         END-EVALUATE                                                     
086400     END-PERFORM.                                                         
086500                                                                          
086600     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
086700         SQLCODE NOT = ZERO                                               
086800         DISPLAY '**************************************'                 
086900         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
087000         DISPLAY 'CLOSE LINE-CSR                        '                 
087100         DISPLAY '**************************************'                 
087200         PERFORM 9900-SQL-ERROR                                           
087300     END-IF.                                                              
087400                                                                          
087500 7400-SELECT-RECDIS-QTYS.                                                 
087600                                                                          
087700     MOVE '7400-SELECT-RECDIS-QTYS'   TO PV-ABEND-PARAGRAPH.              
087800                                                                          
087900     INITIALIZE RECDIS-AP-VERIFY-ITM-QTY                                  
088000                                                                          
088100     PERFORM                                                              
088200         WITH TEST AFTER                                                  
088300         VARYING PV-RETRY-COUNTER                                         
088400         FROM 1 BY 1                                                      
088500         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
088600            OR  (SQLCODE = +0)                                            
088700            OR  (SQLCODE = +100))                                         
088800                                                                          
088900         EXEC SQL                                                         
089000             SELECT                                                       
089100                 SUM(AP_VERIFY_ITM_QTY)                                   
089200             INTO :RECDIS-AP-VERIFY-ITM-QTY:DN-NULL-5                     
089300                                                                          
089400          FROM                                                            
089500             TRECDIS                                                      
089600                                                                          
089700          WHERE                                                           
089800                                                                          
089900           ORD_NBR                = :RECEIV-ORD-NBR                       
090000         AND ORD_LNE_NBR          = :RECITM-ORD-LNE-NBR                   
090200                                                                          
090300         WITH UR                                                          
090400     END-EXEC                                                             
090500                                                                          
090600         EVALUATE TRUE                                                    
090700             WHEN SQLCODE = ZERO                                          
090800             WHEN SQLCODE = +100                                          
090900                 CONTINUE                                                 
091000             WHEN SQLCODE = -904                                          
091100             WHEN SQLCODE = -911                                          
091200             WHEN SQLCODE = -913                                          
091300                  CONTINUE                                                
091400                                                                          
091500             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
091600                 DISPLAY '**************************************'         
091700                 DISPLAY 'WARNING ISSUED '                                
091800                 DISPLAY 'SELECT TRECDIS                        '         
091900                 DISPLAY '**************************************'         
092000                 PERFORM 9800-SQL-WARNING                                 
092100                                                                          
092200             WHEN SQLCODE < ZERO                                          
092300                 DISPLAY '**************************************'         
092400                 DISPLAY 'ERROR ISSUED ON                       '         
092500                 DISPLAY 'SELECT TRECDIS                        '         
092600                 DISPLAY '**************************************'         
092700                 PERFORM 9900-SQL-ERROR                                   
092800                                                                          
092900         END-EVALUATE                                                     
093000     END-PERFORM.                                                         
093100                                                                          
093200     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
093300         SQLCODE NOT = ZERO                                               
093400         DISPLAY '**************************************'                 
093500         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
093600         DISPLAY 'SELECT TRECDIS                        '                 
093700         DISPLAY '**************************************'                 
093800         PERFORM 9900-SQL-ERROR                                           
093900     END-IF.                                                              
094000/                                                                         
094100 8000-WRITE-RC023.                                                        
094200                                                                          
094300     MOVE '8000-WRITE-RC023    '  TO PV-ABEND-PARAGRAPH.                  
094400     WRITE                                                                
094500         RPT-REC                                                          
094600         FROM RC023-PBS-REPORT-EXTRACT                                    
094700     END-WRITE.                                                           
094800     ADD 1                            TO PV-WRITE-1-COUNTER.              
094900                                                                          
095000/                                                                         
095100 9000-QUIT.                                                               
095200                                                                          
095300     CLOSE RPT-FILE                                                       
095400                                                                          
095500     MOVE PV-WRITE-1-COUNTER          TO PV-MSG-1-COUNT                   
095600     DISPLAY PV-COUNT-MSG-1.                                              
095700/                                                                         
095800 9100-SQL-ABEND.                                                          
095900******************************************************************        
096000*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
096100*  ERROR OR WARNING CODE OCCURS.                                          
096200******************************************************************        
096300     DISPLAY '9100-SQL-ABEND'.                                            
096400     DISPLAY '** ABEND     **'.                                           
096500     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
096600     DISPLAY '** PARAGRAPH ** = ' PV-ABEND-PARAGRAPH.                     
096700     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
096800     DISPLAY '**              ** = '                                      
096900     MOVE SQLCODE TO PV-SQL-CODE.                                         
097000     DISPLAY '** SQL CODE  ** = ' PV-SQL-CODE.                            
097100     MOVE +4013 TO PV-ABEND-CODE.                                         
097200     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
097300                                                                          
097400******************************************************************        
097500*  THIS MODULE PERFORMS THE SQL WARNING.                         *        
097600******************************************************************        
097700                                                                          
097800 9800-SQL-WARNING.                                                        
097900                                                                          
098000     MOVE SQLCODE               TO SQLCODE2.                              
098100     MOVE SQLERRD(3)            TO SQLERRD3.                              
098200     DISPLAY '** ABEND **       ** = SQLWARNING'.                         
098300     DISPLAY '** PROGRAM        ** = RCKUT087'.                           
098400     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
098500     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
098600     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
098700                                                                          
098800     COPY DPPD004.                                                        
098900     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
099000     CALL 'ILBOABN0' USING ABEND-CODE.                                    
099100/                                                                         
099200******************************************************************        
099300* THIS MODULE PERFORMS THE SQL ERROR.                            *        
099400******************************************************************        
099500                                                                          
099600 9900-SQL-ERROR.                                                          
099700                                                                          
099800     MOVE SQLCODE               TO SQLCODE2.                              
099900     MOVE SQLERRD(3)            TO SQLERRD3.                              
100000     DISPLAY '** ABEND **       ** = SQLERROR'.                           
100100     DISPLAY '** PROGRAM        ** = RCKUT087'.                           
100200     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
100300     DISPLAY '** OPERATION      ** = ' PV-ABEND-OPERATION                 
100400     DISPLAY '** TABLE          ** = ' PV-ABEND-STRUCTURE-1               
100500     DISPLAY SPACES                                                       
100600                                                                          
100700     COPY DPPD004.                                                        
100800     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
100900     CALL 'ILBOABN0' USING ABEND-CODE.                                    
