       IDENTIFICATION DIVISION.                                         00010010
       PROGRAM-ID.        PXKCS706.                                     00020010
       AUTHOR.            KOHLS DEPARTMENT STORES.                      00030010
       INSTALLATION.      KOHLS DEPARTMENT STORES.                      00040010
       DATE-WRITTEN.      JAN, 2007.                                    00050010
       DATE-COMPILED.                                                   00060010
       ENVIRONMENT DIVISION.                                            00070010
       DATA DIVISION.                                                   00080010
      ******************************************************************00090010
      **T706: MQ LISTENER                                               00100010
      **    PROGRAM THAT READS THE XS.STORE.RF.INTERMEDIATE.Q QUEUE AND 00110010
      **    WRITES TO THE XS.STORE.RF.Q.                                00120010
      ******************************************************************00130010
      **DATE:02/01/2007 CHANGED BY:RICH KELLEN INITIAL PROGRAM RELEASE  00140010
      ******************************************************************00150010
       WORKING-STORAGE SECTION.                                         00160010
      ******************************************************************00170010
       01  RF-SVR-READ-Q             PIC X(48)                          00180010
                                     VALUE 'XS.STORE.RF.INTERMEDIATE.Q'.00190010
       01  RF-SVR-WRITE-Q            PIC X(48)                          00200010
           VALUE 'XS.STORE.RF.Q                                  '.     00210010
      * FOLLOWING QUE IS USED FOR TESTING SO LYNN MCCANN CAN SEE DATA.  00220010
      * WHEN USED, COMMENT OUT ABOVE QUE AND UNCOMMENT QUE BELOW.       00230010
      *    VALUE 'IS.TEST.CLUSTER.Q                              '.     00240010
       01  WS-MQCONN               PIC X(8)  VALUE 'CSQCCONN'.          00250010
       01  WS-MQOPEN               PIC X(8)  VALUE 'CSQCOPEN'.          00260010
       01  WS-MQINQ                PIC X(8)  VALUE 'CSQCINQ'.           00270010
       01  WS-MQGET                PIC X(8)  VALUE 'CSQCGET'.           00280010
       01  WS-MQPUT1               PIC X(8)  VALUE 'CSQCPUT1'.          00290010
       01  WS-MQPUT                PIC X(8)  VALUE 'CSQCPUT'.           00300010
       01  WS-MQBACK               PIC X(8)  VALUE 'CSQCBACK'.          00310010
       01  WS-MQCMIT               PIC X(8)  VALUE 'CSQCCOMM'.          00320010
       01  WS-MQCLOSE              PIC X(8)  VALUE 'CSQCCLOS'.          00330010
       01  WS-MQDISC               PIC X(8)  VALUE 'CSQCDISC'.          00340010
                                                                        00350010
      *    API CONTROL BLOCKS                                           00360010
       01  MQM-OBJECT-DESCRIPTOR.                                       00370010
           COPY CMQODV.                                                 00380010
       01  MQM-MESSAGE-DESCRIPTOR.                                      00390010
           COPY CMQMDV.                                                 00400010
       01  MQM-GET-MESSAGE-OPTIONS.                                     00410010
           COPY CMQGMOV.                                                00420010
       01  MQM-PUT-MESSAGE-OPTIONS.                                     00430010
           COPY CMQPMOV.                                                00440010
      *    MQV CONTAINS CONSTANTS (FOR FILLING IN THE CONTROL BLOCKS)   00450010
      *    AND RETURN CODES (FOR TESTING THE RESULT OF A CALL)          00460010
       01  MQM-CONSTANTS.                                               00470010
           COPY CMQV.                                                   00480010
      **---------------------------------------------------------------*00490010
      **  VARIABLES REQUIRED FOR THE USE OF THE LOG MESSAGE ROUTINE    *00500010
      **  (EPPD000)                                                    *00510010
      **---------------------------------------------------------------*00520010
           COPY EPWS000.                                                00530010
      *----------------------------------------------------------------*00540010
      *    PROGRAM CONSTANTS                                            00550010
      *----------------------------------------------------------------*00560010
       01  PC-PROGRAM-CONSTANTS.                                        00570010
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08) VALUE             00580010
               'PXKCS706'.                                              00590010
           05  PC-YES                      PIC  X(01) VALUE 'Y'.        00600010
           05  PC-MQPUT-RETRY-COUNTER      PIC  9(3)  VALUE ZERO.       00610010
           05  PC-DELAY-INTERVAL.                                       00620010
               10  FILLER                  PIC X(2)   VALUE SPACES.     00630010
               10  PC-DELAY-TIME           PIC 9(8)   VALUE 1500.       00640010
           05  PC-MAX-RETRIES              PIC S9(3)  VALUE +3          00650010
                                                      COMP SYNC.        00660010
      *----------------------------------------------------------------*00670010
      *    PROGRAM SWITCHES                                             00680010
      *----------------------------------------------------------------*00690010
       01  PS-PROGRAM-SWITCHES.                                         00700010
           05  PS-MQGET-SUCCESSFUL-SW    PIC X(01) VALUE 'Y'.           00710010
               88  PS-MQGET-SUCCESSFUL             VALUE 'Y'.           00720010
               88  PS-MQGET-NOT-SUCCESSFUL         VALUE 'N'.           00730010
           05  PS-MQPUT-SUCCESSFUL-SW    PIC X(01) VALUE 'Y'.           00740010
               88  PS-MQPUT-SUCCESSFUL             VALUE 'Y'.           00750010
               88  PS-MQPUT-NOT-SUCCESSFUL         VALUE 'N'.           00760010
           05  PS-START-TRANID-INDICATOR PIC X(01) VALUE 'Y'.           00770010
               88  PS-START-TRANID-YES             VALUE 'Y'.           00780010
               88  PS-START-TRANID-NO              VALUE 'N'.           00790010
      *----------------------------------------------------------------*00800010
      *    PROGRAM VARIABLES                                            00810010
      *----------------------------------------------------------------*00820010
       01  PV-PROGRAM-VARIABLES.                                        00830010
           05  PV-QMGR-NAME               PIC X(04)   VALUE SPACES.     00840010
               88  PV-PROD-QMGR-NAME                  VALUE 'QKSP'.     00850010
               88  PV-TEST-QMGR-NAME                  VALUE 'QKST'.     00860010
               88  PV-QA-QMGR-NAME                    VALUE 'QKSQ'.     00870010
           05  PV-RECV-MSG-BUFFER.                                      00880010
               10  PV-RECV-TRANID         PIC X(04)   VALUE SPACES.     00890010
               10  PV-RECV-MSG.                                         00900010
                   15 FILLER              PIC X(12)   VALUE SPACES.     00910010
                   15 PV-RECV-BUFFER      PIC X(499988) VALUE SPACES.   00920016
           05  PV-SEND-MSG-BUFFER.                                      00930010
               10  PV-SEND-BUFFER         PIC X(500000) VALUE ZEROES.   00940016
               10  PV-SEND-MQ-INDICATOR   PIC X(01)   VALUE SPACES.     00950010
               10  PV-SEND-REPLYTOQ       PIC X(48)   VALUE SPACES.     00960010
               10  PV-SEND-REPLYTOQMGR    PIC X(48)   VALUE SPACES.     00970010
               10  PV-SEND-MSGID          PIC X(24)   VALUE SPACES.     00980010
           05  PV-START-LENGTH            PIC 9(4)   BINARY VALUE 0.    00990010
           05  PV-RC                      PIC 9(4)   BINARY VALUE 0.    01000010
           05  PV-ERROR-MESSAGE           PIC X(48)  VALUE SPACES.      01010010
           05  PV-QM-NAME                 PIC X(48).                    01020010
           05  PV-MSGBUFFER               PIC X(1000000) VALUE SPACES.  01030010
           05  PV-MSGLENGTH            PIC S9(9) BINARY VALUE +1000000. 01040010
           05  PV-DATA-LENGTH          PIC S9(9) BINARY.                01050010
           05  PV-HCONN                PIC S9(9) BINARY VALUE 0.        01060010
           05  PV-GQ-HANDLE            PIC S9(9) BINARY VALUE 0.        01070010
           05  PV-OPEN-OPTIONS         PIC S9(9) BINARY VALUE 0.        01080010
           05  PV-COMPLETION-CODE      PIC S9(9) BINARY VALUE 0.        01090010
           05  PV-REASON               PIC S9(9) BINARY VALUE 0.        01100010
           05  PV-CON-REASON           PIC S9(9) BINARY VALUE 0.        01110010
           05  PV-PQ-HANDLE            PIC S9(9) BINARY VALUE 0.        01120010
           05  PV-CICS-RESPONSE                PIC S9(08)  VALUE ZEROS  01130010
                                                           COMP SYNC.   01140010
           05 PV-MQPUT-RETRY-COUNTER     PIC S9(04)      BINARY         01150010
                                                          VALUE 0.      01160010
           05 PV-COMMIT-CTR              PIC 9(5) VALUE 0.              01170010
                                                                        01180010
       01  PH-PROGRAM-HOLD-VARIABLES.                                   01190010
           05  PH-COMPLETION-CODE          PIC  S9(09) BINARY.          01200010
           05  PH-REASON                   PIC  S9(09) BINARY.          01210010
                                                                        01220010
      *  LAYOUT FOR THE ABEND MODULE COMMUNICATIONS AREA                01230010
           COPY DPWS013.                                                01240010
      *  RECORD LAYOUT FOR SYSTEM MESSAGES FILE USED FOR CICS MESSAGES  01250010
           COPY DPRD507I.                                               01260010
      *  FORMATS CICS SYSTEM MESSAGES FOR NON-ARCHITECTURE PROGRAMS     01270010
           COPY DPWS011.                                                01280010
                                                                        01290010
       PROCEDURE DIVISION.                                              01300010
      ******************************************************************01310010
       0000-PROGRAM-MAINLINE.                                           01320010
                                                                        01330010
           PERFORM 1500-MQ-SETUP.                                       01340010
           INITIALIZE PS-PROGRAM-SWITCHES.                              01350010
           PERFORM 2100-MQGET-MSG.                                      01360010
           PERFORM 2000-PROCESS-MESSAGES                                01370010
                   UNTIL PS-MQGET-NOT-SUCCESSFUL.                       01380010
           PERFORM 1600-CLOSE-GET-Q.                                    01390010
           PERFORM 1650-CLOSE-PUT-Q.                                    01400010
           PERFORM 1700-DISCONNECT-QMGR.                                01410010
           GOBACK.                                                      01420010
                                                                        01430010
       1500-MQ-SETUP.                                                   01440010
                                                                        01450010
      * DETERMINE THE SYSTEM                                            01460010
           PERFORM EP000-0000-INITIALIZE.                               01470010
           IF EP000-PV-KOHLS-PROD-SYSTEM                                01480010
               SET PV-PROD-QMGR-NAME     TO TRUE                        01490010
           ELSE                                                         01500010
                IF EP000-PV-KOHLS-QA-SYSTEM                             01510010
                    SET PV-QA-QMGR-NAME  TO TRUE                        01520010
                ELSE                                                    01530010
                    SET PV-TEST-QMGR-NAME                               01540010
                                         TO TRUE                        01550010
                END-IF                                                  01560010
           END-IF.                                                      01570010
      ***************************************************************   01580010
      *    CONNECT TO THE QUEUE MANAGER                                 01590010
      ***************************************************************   01600010
           MOVE PV-QMGR-NAME TO PV-QM-NAME.                             01610010
           CALL WS-MQCONN USING PV-QM-NAME                              01620010
                               PV-HCONN                                 01630010
                               PV-COMPLETION-CODE                       01640010
                               PV-REASON.                               01650010
      ***************************************************************   01660010
      *    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE              01670010
      *    AND EXIT                                                     01680010
      ***************************************************************   01690010
           MOVE PV-REASON TO PV-CON-REASON.                             01700010
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   01710010
               MOVE 'MQCONN ERROR'        TO PV-ERROR-MESSAGE           01720010
               MOVE '1500-MQ-SETUP'       TO DP013-PARAGRAPH            01730010
               MOVE  PV-ERROR-MESSAGE     TO DP013-MESSAGE-TEXT (1)     01740010
               MOVE  PV-COMPLETION-CODE   TO DP013-MESSAGE-TEXT (2)     01750010
               MOVE  PV-REASON            TO DP013-MESSAGE-TEXT (3)     01760010
               SET DP013-CICS-ABEND                                     01770010
                   DP013-NO-ROLLBACK          TO TRUE                   01780010
               PERFORM 9999-WRITE-CICS-LOG-MSG                          01790010
               PERFORM 9100-EXEC-CICS-RETURN                            01800010
           END-IF.                                                      01810010
      ***************************************************************   01820010
      *    OPEN THE XS.STORE.RF.INTERMEDIATE.Q QUEUE FOR INPUT          01830010
      *    THIS QUEUE RESIDES ON THE SERVER                             01840010
      ***************************************************************   01850010
           COMPUTE PV-OPEN-OPTIONS = MQOO-INPUT-SHARED +                01860010
                                     MQOO-FAIL-IF-QUIESCING.            01870010
           MOVE MQOT-Q                   TO MQOD-OBJECTTYPE.            01880010
           MOVE RF-SVR-READ-Q            TO MQOD-OBJECTNAME.            01890010
           MOVE PV-QM-NAME               TO MQOD-OBJECTQMGRNAME.        01900010
                                                                        01910010
           CALL WS-MQOPEN USING PV-HCONN                                01920010
                               MQM-OBJECT-DESCRIPTOR                    01930010
                               PV-OPEN-OPTIONS                          01940010
                               PV-GQ-HANDLE                             01950010
                               PV-COMPLETION-CODE                       01960010
                               PV-REASON.                               01970010
      ***************************************************************   01980010
      *    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE              01990010
      *    AND EXIT                                                     02000010
      ***************************************************************   02010010
           MOVE PV-REASON TO PV-CON-REASON.                             02020010
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   02030010
               MOVE 'MQCONN ERROR2'       TO PV-ERROR-MESSAGE           02040010
               MOVE '1500-MQ-SETUP'       TO DP013-PARAGRAPH            02050010
               MOVE  PV-ERROR-MESSAGE     TO DP013-MESSAGE-TEXT (1)     02060010
               MOVE  PV-COMPLETION-CODE   TO DP013-MESSAGE-TEXT (2)     02070010
               MOVE  PV-REASON            TO DP013-MESSAGE-TEXT (3)     02080010
               SET DP013-CICS-ABEND                                     02090010
                   DP013-NO-ROLLBACK       TO TRUE                      02100010
               PERFORM 9999-WRITE-CICS-LOG-MSG                          02110010
               PERFORM 9100-EXEC-CICS-RETURN                            02120010
           END-IF.                                                      02130010
      **********************************************************        02140010
      *    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                    02150010
      *    AND EXIT.                                                    02160010
      **********************************************************        02170010
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   02180010
               PERFORM 1700-DISCONNECT-QMGR                             02190010
               MOVE 'MQOPEN READ Q'       TO PV-ERROR-MESSAGE           02200010
               MOVE '1500-MQ-SETUP'       TO DP013-PARAGRAPH            02210010
               MOVE  PV-ERROR-MESSAGE     TO DP013-MESSAGE-TEXT (1)     02220010
               MOVE  PV-COMPLETION-CODE   TO DP013-MESSAGE-TEXT (2)     02230010
               MOVE  PV-REASON            TO DP013-MESSAGE-TEXT (3)     02240010
               SET DP013-CICS-ABEND                                     02250010
                   DP013-NO-ROLLBACK       TO TRUE                      02260010
               PERFORM 9999-WRITE-CICS-LOG-MSG                          02270010
               PERFORM 9100-EXEC-CICS-RETURN                            02280010
           END-IF.                                                      02290010
      ***************************************************************   02300010
      *    OPEN THE XS.STORE.RF.Q QUEUE FOR OUTPUT                      02310010
      *    THIS QUEUE RESIDES ON THE SERVER                             02320010
      ***************************************************************   02330010
      *    INITIALIZE MQM-OBJECT-DESCRIPTOR.                            02340010
           COMPUTE PV-OPEN-OPTIONS = MQOO-OUTPUT +                      02350010
                                     MQOO-FAIL-IF-QUIESCING.            02360010
           MOVE MQOT-Q                   TO MQOD-OBJECTTYPE.            02370010
      *    MOVE 1                        TO MQOD-OBJECTTYPE.            02380010
           MOVE RF-SVR-WRITE-Q           TO MQOD-OBJECTNAME.            02390010
           IF PV-TEST-QMGR-NAME                                         02391014
             MOVE 'ISWMQ20T'             TO MQOD-OBJECTQMGRNAME         02392015
           ELSE                                                         02393015
             MOVE SPACE                  TO MQOD-OBJECTQMGRNAME.        02410015
                                                                        02420010
           CALL WS-MQOPEN USING PV-HCONN                                02430010
                               MQM-OBJECT-DESCRIPTOR                    02440010
                               PV-OPEN-OPTIONS                          02450010
                               PV-PQ-HANDLE                             02460010
                               PV-COMPLETION-CODE                       02470010
                               PV-REASON.                               02480010
      ***************************************************************   02490010
      *    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE              02500010
      *    AND EXIT                                                     02510010
      ***************************************************************   02520010
           MOVE PV-REASON TO PV-CON-REASON.                             02530010
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   02540010
               MOVE 'MQCONN ERROR3'       TO PV-ERROR-MESSAGE           02550010
               MOVE '1500-MQ-SETUP'       TO DP013-PARAGRAPH            02560010
               MOVE  PV-ERROR-MESSAGE     TO DP013-MESSAGE-TEXT (1)     02570010
               MOVE  PV-COMPLETION-CODE   TO DP013-MESSAGE-TEXT (2)     02580010
               MOVE  PV-REASON            TO DP013-MESSAGE-TEXT (3)     02590010
      *        DISPLAY 'PXKCS706 - OBJECT NAME= ' MQOD-OBJECTQMGRNAME   02591015
               STRING  MQOD-OBJECTNAME DELIMITED BY SIZE                02600010
                      , ' ' DELIMITED BY SIZE                           02610010
                      , MQOD-OBJECTQMGRNAME DELIMITED BY SIZE           02620010
                      , 'HERE1 ' DELIMITED BY SIZE                      02630010
                      INTO DP013-MESSAGE-TEXT (4)                       02640010
               SET DP013-CICS-ABEND                                     02650010
                   DP013-NO-ROLLBACK       TO TRUE                      02660010
               PERFORM 9999-WRITE-CICS-LOG-MSG                          02670010
               PERFORM 9100-EXEC-CICS-RETURN                            02680010
           END-IF.                                                      02690010
      **********************************************************        02700010
      *    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                    02710010
      *    AND EXIT.                                                    02720010
      **********************************************************        02730010
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   02740010
               PERFORM 1700-DISCONNECT-QMGR                             02750010
               MOVE 'MQOPEN WRITE Q'      TO PV-ERROR-MESSAGE           02760010
               MOVE '1500-MQ-SETUP'       TO DP013-PARAGRAPH            02770010
               MOVE  PV-ERROR-MESSAGE     TO DP013-MESSAGE-TEXT (1)     02780010
               MOVE  PV-COMPLETION-CODE   TO DP013-MESSAGE-TEXT (2)     02790010
               MOVE  PV-REASON            TO DP013-MESSAGE-TEXT (3)     02800010
               SET DP013-CICS-ABEND                                     02810010
                   DP013-NO-ROLLBACK       TO TRUE                      02820010
               PERFORM 9999-WRITE-CICS-LOG-MSG                          02830010
               PERFORM 9100-EXEC-CICS-RETURN                            02840010
           END-IF.                                                      02850010
                                                                        02860010
       1600-CLOSE-GET-Q.                                                02870010
                                                                        02880010
           CALL WS-MQCLOSE USING PV-HCONN                               02890010
                                 PV-GQ-HANDLE                           02900010
                                 MQCO-NONE                              02910010
                                 PV-COMPLETION-CODE                     02920010
                                 PV-REASON.                             02930010
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   02940010
               MOVE 'MQCLOSE GQ HANDLE'   TO PV-ERROR-MESSAGE           02950010
               MOVE '1600-CLOSE-GET-Q'    TO DP013-PARAGRAPH            02960010
               MOVE  PV-ERROR-MESSAGE     TO DP013-MESSAGE-TEXT (1)     02970010
               MOVE  PV-COMPLETION-CODE   TO DP013-MESSAGE-TEXT (2)     02980010
               MOVE  PV-REASON            TO DP013-MESSAGE-TEXT (3)     02990010
               SET DP013-CICS-ABEND                                     03000010
                   DP013-NO-ROLLBACK       TO TRUE                      03010010
               PERFORM 9999-WRITE-CICS-LOG-MSG                          03020010
               PERFORM 9100-EXEC-CICS-RETURN                            03030010
           END-IF.                                                      03040010
                                                                        03050010
       1650-CLOSE-PUT-Q.                                                03060010
                                                                        03070010
           CALL WS-MQCLOSE USING PV-HCONN                               03080010
                                 PV-PQ-HANDLE                           03090010
                                 MQCO-NONE                              03100010
                                 PV-COMPLETION-CODE                     03110010
                                 PV-REASON.                             03120010
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   03130010
               MOVE 'MQCLOSE PQ HANDLE'   TO PV-ERROR-MESSAGE           03140010
               MOVE '1650-CLOSE-PUT-Q'    TO DP013-PARAGRAPH            03150010
               MOVE  PV-ERROR-MESSAGE     TO DP013-MESSAGE-TEXT (1)     03160010
               MOVE  PV-COMPLETION-CODE   TO DP013-MESSAGE-TEXT (2)     03170010
               MOVE  PV-REASON            TO DP013-MESSAGE-TEXT (3)     03180010
               SET DP013-CICS-ABEND                                     03190010
                   DP013-NO-ROLLBACK       TO TRUE                      03200010
               PERFORM 9999-WRITE-CICS-LOG-MSG                          03210010
               PERFORM 9100-EXEC-CICS-RETURN                            03220010
           END-IF.                                                      03230010
                                                                        03240010
       1700-DISCONNECT-QMGR.                                            03250010
                                                                        03260010
           IF PV-CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED      03270010
              CALL WS-MQDISC USING PV-HCONN                             03280010
                                  PV-COMPLETION-CODE                    03290010
                                  PV-REASON                             03300010
              IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                03310010
                  MOVE 'MQDISC'           TO PV-ERROR-MESSAGE           03320010
                  MOVE '1700-DISCONNECT'  TO DP013-PARAGRAPH            03330010
                  MOVE PV-ERROR-MESSAGE   TO DP013-MESSAGE-TEXT (1)     03340010
                  MOVE PV-COMPLETION-CODE TO DP013-MESSAGE-TEXT (2)     03350010
                  MOVE PV-REASON          TO DP013-MESSAGE-TEXT (3)     03360010
                  SET DP013-CICS-ABEND                                  03370010
                      DP013-NO-ROLLBACK    TO TRUE                      03380010
                  PERFORM 9999-WRITE-CICS-LOG-MSG                       03390010
                  PERFORM 9100-EXEC-CICS-RETURN                         03400010
              END-IF                                                    03410010
           END-IF.                                                      03420010
                                                                        03430010
      ******************************************************************03440010
      **MAIN PROGRAM PROCESSOR                                          03450010
      ******************************************************************03460010
       2000-PROCESS-MESSAGES.                                           03470010
                                                                        03480010
      *     IF  PS-START-TRANID-YES                                     03490010
      *         PERFORM 2200-START-TRANID                               03500010
      *     END-IF.                                                     03510010
            ADD 1 TO PV-COMMIT-CTR.                                     03520010
            IF PV-COMMIT-CTR > 1000                                     03530010
              PERFORM 2300-CICS-MQ-COMMIT                               03540010
              MOVE 0 TO PV-COMMIT-CTR                                   03550010
            END-IF.                                                     03551010
                                                                        03560010
            PERFORM 2100-MQGET-MSG.                                     03570010
                                                                        03580010
      ******************************************************************03590010
      **GET MESSAGE FROM QUEUE FOR PROCESSING                           03600010
      ******************************************************************03610010
       2100-MQGET-MSG.                                                  03620010
                                                                        03630010
           INITIALIZE PV-MSGBUFFER.                                     03640010
           MOVE 1000000 TO     PV-MSGLENGTH.                            03650010
           MOVE MQMI-NONE                   TO MQMD-MSGID.              03660010
           MOVE MQCI-NONE                   TO MQMD-CORRELID.           03670010
           MOVE RF-SVR-READ-Q               TO MQOD-OBJECTNAME.         03680010
           MOVE 60000                       TO MQGMO-WAITINTERVAL.      03690010
                                                                        03700010
           COMPUTE MQGMO-OPTIONS  = MQGMO-SYNCPOINT +                   03710010
                                    MQGMO-WAIT.                         03720010
                                                                        03730010
           CALL WS-MQGET USING PV-HCONN                                 03740010
                               PV-GQ-HANDLE                             03750010
                               MQM-MESSAGE-DESCRIPTOR                   03760010
                               MQM-GET-MESSAGE-OPTIONS                  03770010
                               PV-MSGLENGTH                             03780010
                               PV-MSGBUFFER                             03790010
                               PV-DATA-LENGTH                           03800010
                               PV-COMPLETION-CODE                       03810010
                               PV-REASON.                               03820010
                                                                        03830010
           IF  PV-COMPLETION-CODE = MQCC-OK                             03840010
               SET PS-MQGET-SUCCESSFUL      TO TRUE                     03850010
               SET PS-START-TRANID-YES      TO TRUE                     03860010
               MOVE PC-YES                  TO PV-SEND-MQ-INDICATOR     03870010
               MOVE MQMD-REPLYTOQ           TO PV-SEND-REPLYTOQ         03880010
               MOVE MQMD-REPLYTOQMGR        TO PV-SEND-REPLYTOQMGR      03890010
               MOVE MQMD-MSGID              TO PV-SEND-MSGID            03900010
               MOVE PV-MSGBUFFER            TO PV-RECV-MSG-BUFFER       03910010
               MOVE PV-RECV-MSG             TO PV-SEND-BUFFER           03920010
               MOVE 1 TO PV-MQPUT-RETRY-COUNTER                         03930010
               SET PS-MQPUT-NOT-SUCCESSFUL TO TRUE                      03940010
               PERFORM 2150-MQPUT-MSG                                   03950010
                 UNTIL PS-MQPUT-SUCCESSFUL                              03960010
                    OR PV-MQPUT-RETRY-COUNTER > PC-MAX-RETRIES          03970010
           ELSE                                                         03980010
               IF  PV-REASON NOT EQUAL MQRC-NO-MSG-AVAILABLE            03990010
                   MOVE PV-COMPLETION-CODE                              04000010
                                            TO PH-COMPLETION-CODE       04010010
                   MOVE PV-REASON           TO PH-REASON                04020010
                   PERFORM 1600-CLOSE-GET-Q                             04030010
                   PERFORM 1700-DISCONNECT-QMGR                         04040010
                   DISPLAY 'PXKCS706 MQGET ERROR - ' PV-MSGBUFFER ' - ' 04050010
                                                     PV-MSGLENGTH       04060010
                   MOVE 'MQGET'             TO PV-ERROR-MESSAGE         04070010
                   MOVE '2100-MQGET-MSG'    TO DP013-PARAGRAPH          04080010
                   MOVE 'MQGET ERR'         TO DP013-MESSAGE-TEXT (1)   04090010
                   MOVE PH-COMPLETION-CODE                              04100010
                                            TO DP013-MESSAGE-TEXT (2)   04110010
                   MOVE PH-REASON           TO DP013-MESSAGE-TEXT (3)   04120010
                   SET DP013-CICS-ABEND                                 04130010
                       DP013-NO-ROLLBACK       TO TRUE                  04140010
                   PERFORM 9999-WRITE-CICS-LOG-MSG                      04150010
                   PERFORM 9100-EXEC-CICS-RETURN                        04160010
               END-IF                                                   04170010
               SET PS-START-TRANID-NO       TO TRUE                     04180010
               SET PS-MQGET-NOT-SUCCESSFUL  TO TRUE                     04190010
           END-IF.                                                      04200010
                                                                        04210010
      ******************************************************************04220010
      **PUT MESSAGE TO QUEUE FOR PROCESSING                             04230010
      ******************************************************************04240010
       2150-MQPUT-MSG.                                                  04250010
                                                                        04260010
           INITIALIZE PV-MSGBUFFER.                                     04270010
           MOVE PV-RECV-MSG-BUFFER          TO PV-MSGBUFFER.            04280010
           MOVE MQMI-NONE                   TO MQMD-MSGID.              04290010
           MOVE RF-SVR-WRITE-Q              TO MQOD-OBJECTNAME.         04300010
      * THIS STMT CONVERTS EBCDIC (MAINFRAME) FORMAT TO ASCII(SERVER).  04310010
      *    MOVE MQFMT-STRING                TO MQMD-FORMAT.             04320010
                                                                        04330010
           COMPUTE MQPMO-OPTIONS = MQPMO-SYNCPOINT.                     04340010
           MOVE PV-DATA-LENGTH TO PV-MSGLENGTH.                         04350010
           CALL WS-MQPUT USING PV-HCONN                                 04360010
                               PV-PQ-HANDLE                             04370010
                               MQM-MESSAGE-DESCRIPTOR                   04380010
                               MQM-PUT-MESSAGE-OPTIONS                  04390010
                               PV-MSGLENGTH                             04400010
                               PV-MSGBUFFER                             04410010
                               PV-COMPLETION-CODE                       04420010
                               PV-REASON.                               04430010
                                                                        04440010
           IF  PV-COMPLETION-CODE = MQCC-OK                             04450010
               SET PS-MQPUT-SUCCESSFUL      TO TRUE                     04460010
           ELSE                                                         04470010
               IF  PV-REASON NOT EQUAL MQRC-PAGESET-FULL OR             04480010
                      MQRC-Q-FULL                                       04490010
                   ADD 1 TO PV-MQPUT-RETRY-COUNTER                      04500010
               ELSE                                                     04510010
                   MOVE PV-COMPLETION-CODE                              04520010
                                            TO PH-COMPLETION-CODE       04530010
                   MOVE PV-REASON           TO PH-REASON                04540010
                   PERFORM 1600-CLOSE-GET-Q                             04550010
                   PERFORM 1700-DISCONNECT-QMGR                         04560010
                   MOVE 'MQPUT'             TO PV-ERROR-MESSAGE         04570010
                   MOVE '2150-MQPUT-MSG'    TO DP013-PARAGRAPH          04580010
                   MOVE 'MQPUT ERR'         TO DP013-MESSAGE-TEXT (1)   04590010
                   MOVE PH-COMPLETION-CODE                              04600010
                                            TO DP013-MESSAGE-TEXT (2)   04610010
                   MOVE PH-REASON           TO DP013-MESSAGE-TEXT (3)   04620010
                   SET DP013-CICS-ABEND                                 04630010
                       DP013-NO-ROLLBACK    TO TRUE                     04640010
                   PERFORM 9999-WRITE-CICS-LOG-MSG                      04650010
                   PERFORM 9100-EXEC-CICS-RETURN                        04660010
               END-IF                                                   04670010
           END-IF.                                                      04680010
                                                                        04690010
      ******************************************************************04700010
      **START TRANSACTION BASED ON HEADER INFORMATION ON TRANSACTION    04710010
      ******************************************************************04720010
       2200-START-TRANID.                                               04730010
                                                                        04740010
           MOVE LENGTH OF PV-SEND-MSG-BUFFER TO PV-START-LENGTH.        04750010
                                                                        04760010
           EXEC CICS START TRANSID (PV-RECV-TRANID)                     04770010
                           FROM    (PV-SEND-MSG-BUFFER)                 04780010
                           LENGTH  (PV-START-LENGTH)                    04790010
                           RESP    (PV-RC)                              04800010
           END-EXEC.                                                    04810010
                                                                        04820010
           IF PV-RC NOT = 0                                             04830010
               PERFORM 1600-CLOSE-GET-Q                                 04840010
               PERFORM 1700-DISCONNECT-QMGR                             04850010
               MOVE 'EXEC CICS START'     TO PV-ERROR-MESSAGE           04860010
               MOVE '2200-START-TRANID'   TO DP013-PARAGRAPH            04870010
               MOVE PV-ERROR-MESSAGE      TO DP013-MESSAGE-TEXT (1)     04880010
               SET DP013-CICS-ABEND                                     04890010
                   DP013-NO-ROLLBACK       TO TRUE                      04900010
               PERFORM 9999-WRITE-CICS-LOG-MSG                          04910010
               PERFORM 9100-EXEC-CICS-RETURN                            04920010
           END-IF.                                                      04930010
                                                                        04940010
      *--------------------------------------------------------------*  04950010
      *  COMMIT THE MQ PUT BY USING A CICS SYNCPOINT COMMAND.           04960010
      *  (MQ COMMIT DOES NOT WORK IN CICS, THE SYNCPOINT MUST BE     *  04970010
      *  USED)                                                          04980010
      *--------------------------------------------------------------*  04990010
       2300-CICS-MQ-COMMIT.                                             05000010
                                                                        05010010
      *       DISPLAY 'PXKCS706 SYNCPOINT'                              05011010
           EXEC CICS                                                    05020010
               SYNCPOINT                                                05030010
           END-EXEC.                                                    05040010
      ***-----------------------------------------------------------*** 05050010
      *      PARAGRAPH WILL ISSUE A CICS RETURN                       * 05060010
      ***-----------------------------------------------------------*** 05070010
       9100-EXEC-CICS-RETURN.                                           05080010
           EXEC CICS                                                    05090010
               RETURN                                                   05100010
           END-EXEC.                                                    05110010
                                                                        05120010
      ***-----------------------------------------------------------*** 05130010
      *      PARAGRAPH WILL WRITE THE CIC LOG MESSAGE WHEN ERROR      * 05140010
      *      WHEN ERROR SITUATION IS ENCOUNTERED.                     * 05150010
      ***-----------------------------------------------------------*** 05160010
       9999-WRITE-CICS-LOG-MSG.                                         05170010
                                                                        05180010
           MOVE PC-CURRENT-PROGRAM-NAME                                 05190010
                                       TO DP013-PROGRAM.                05200010
           MOVE DFHEIBLK               TO DP013-DFHEIBLK.               05210010
           INITIALIZE DP013-SRC-TASK-NUMBER                             05220010
                      DP013-STACK.                                      05230010
           SET DP013-LINK-RETURN       TO TRUE.                         05240010
                                                                        05250010
           EXEC CICS                                                    05260010
               LINK                                                     05270010
                   PROGRAM   (DP013-NAME)                               05280010
                   COMMAREA  (DP013-PARAMETERS)                         05290010
                   LENGTH    (DP013-LENGTH)                             05300010
           END-EXEC.                                                    05310010
                                                                        05320010
      *   NON-CICS ARCHITECTURE SYSTEM MESSAGE FORMAT MODULE            05330010
           COPY DPPD011.                                                05340010
      *   NON-CICS ARCHITECTURE ABEND ROUTINE CALL                      05350010
           COPY DPPD014.                                                05360010
           COPY EPPD000.                                                05370010
