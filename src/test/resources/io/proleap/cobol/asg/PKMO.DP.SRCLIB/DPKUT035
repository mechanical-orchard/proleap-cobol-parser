00001 ******************************************************************04/17/89
00002  IDENTIFICATION DIVISION.                                         DPKUT035
00003 ******************************************************************   LV010
00004  PROGRAM-ID.      DPKUT035.                                          CL**2
00005  AUTHOR.          COLLEEN KOBE (STRATAGEM).                       DPKUT035
00006  INSTALLATION.    KOHLS DEPARTMENT STORES.                        DPKUT035
00007  DATE-WRITTEN.    APRIL 12, 1989.                                    CL**2
00008  DATE-COMPILED.                                                   DPKUT035
00009                                                                   DPKUT035
00010 ******************************************************************   CL**2
00011 * CHANGES MADE:                                                      CL**2
00012 ******************************************************************   CL**2
00013  ENVIRONMENT DIVISION.                                            DPKUT035
00014  CONFIGURATION SECTION.                                           DPKUT035
00015  SOURCE-COMPUTER. IBM-3090.                                       DPKUT035
00016  OBJECT-COMPUTER. IBM-3090.                                       DPKUT035
00017                                                                   DPKUT035
00018  INPUT-OUTPUT SECTION.                                            DPKUT035
00019  FILE-CONTROL.                                                    DPKUT035
00020      SELECT FILE-TYPE-IN-FILE          ASSIGN TO INP01.              CL**8
00021      SELECT FILE-TYPE-OUT-FILE         ASSIGN TO OUT01.              CL**8
00022                                                                   DPKUT035
00023      EJECT                                                        DPKUT035
00024 ******************************************************************DPKUT035
00025  DATA DIVISION.                                                   DPKUT035
00026 ******************************************************************DPKUT035
00027  FILE SECTION.                                                    DPKUT035
00028                                                                   DPKUT035
00029  FD  FILE-TYPE-IN-FILE                                               CL**7
00030      BLOCK CONTAINS 0 RECORDS                                     DPKUT035
00031      RECORD CONTAINS 83 CHARACTERS                                DPKUT035
00032      LABEL RECORDS ARE STANDARD                                   DPKUT035
00033      RECORDING MODE F                                             DPKUT035
00034      DATA RECORD IS FILE-TYPE-IN-RECORD.                             CL**7
00035                                                                   DPKUT035
00036  01  FILE-TYPE-IN-RECORD     PIC X(83).                              CL**7
00037                                                                   DPKUT035
00038  FD  FILE-TYPE-OUT-FILE                                              CL**7
00039      BLOCK CONTAINS 0 RECORDS                                        CL**2
00040      RECORD CONTAINS 83 CHARACTERS                                   CL**2
00041      LABEL RECORDS ARE STANDARD                                      CL**2
00042      RECORDING MODE F                                                CL**2
00043      DATA RECORD IS FILE-TYPE-OUT-RECORD.                            CL**7
00044                                                                      CL**2
00045  01  FILE-TYPE-OUT-RECORD    PIC X(83).                              CL**7
00046                                                                      CL**2
00047      EJECT                                                        DPKUT035
00048 ******************************************************************DPKUT035
00049  WORKING-STORAGE SECTION.                                         DPKUT035
00050 ******************************************************************DPKUT035
00051                                                                   DPKUT035
00052 **   FILE TYPE FILE LAYOUT (INPUT)    **                             CL**2
00053                                                                   DPKUT035
00054      COPY DPRD007I.                                                  CL*10
00055                                                                      CL**5
00056 **   FILE TYPE FILE LAYOUT (OUTPUT)   **                             CL**7
00057                                                                      CL**7
00058      COPY DPRD007O.                                                  CL*10
00059                                                                   DPKUT035
00060      EJECT                                                        DPKUT035
00061 ******************************************************************DPKUT035
00062  01  PROGRAM-SWITCHES.                                            DPKUT035
00063      05  PS-EOF-INPUT-FILE-SW     PIC X         VALUE 'N'.           CL**2
00064          88  EOF-INPUT-FILE                     VALUE 'Y'.           CL**2
00065                                                                   DPKUT035
00066  01  WORK-AREA.                                                   DPKUT035
00067      05  WS-HOLD-FILE-NAME       PIC X(44)      VALUE SPACES.        CL**2
00068      EJECT                                                        DPKUT035
00069                                                                      CL**6
00070 ******************************************************************DPKUT035
00071  PROCEDURE DIVISION.                                              DPKUT035
00072 ******************************************************************DPKUT035
00073                                                                   DPKUT035
00074      OPEN INPUT     FILE-TYPE-IN-FILE                                CL**9
00075           OUTPUT    FILE-TYPE-OUT-FILE.                              CL**9
00076                                                                   DPKUT035
00077      PERFORM A100-READ-INPUT-FILE.                                   CL**2
00078                                                                   DPKUT035
00079      PERFORM A200-ELIMINATE-DUPLICATES                               CL**7
00080              UNTIL EOF-INPUT-FILE.                                   CL**7
00081                                                                   DPKUT035
00082      CLOSE          FILE-TYPE-IN-FILE                                CL**9
00083                     FILE-TYPE-OUT-FILE.                              CL**9
00084                                                                   DPKUT035
00085      STOP RUN.                                                    DPKUT035
00086                                                                   DPKUT035
00087      EJECT                                                        DPKUT035
00088 ***************************************************************** DPKUT035
00089 * A100 READ INPUT FILE AND SET END-OF-FILE SWITCH.              *    CL**2
00090 ***************************************************************** DPKUT035
00091  A100-READ-INPUT-FILE.                                               CL**2
00092                                                                   DPKUT035
00093      READ FILE-TYPE-IN-FILE INTO DP007I-RECORD                       CL**9
00094          AT END                                                   DPKUT035
00095              SET EOF-INPUT-FILE TO TRUE.                             CL**2
00096                                                                   DPKUT035
00097 ******************************************************************DPKUT035
00098 * A200 THIS PARAGRAPH PROCESSES THE INPUT FILE. IF THE RECORD    *   CL**2
00099 *      JUST READ IS THE SAME AS THE FILE NAME FROM THE LAST      *   CL**2
00100 *      RECORD, IT IS BYPASSED. IF IT IS A DIFFERENT FILE NAME,   *   CL**2
00101 *      AN OUTPUT RECORD IS WRITTEN.                              *   CL**2
00102 ******************************************************************DPKUT035
00103  A200-ELIMINATE-DUPLICATES.                                          CL**2
00104                                                                   DPKUT035
00105      IF DP007I-FILE-NAME NOT = WS-HOLD-FILE-NAME                     CL**2
00106          MOVE DP007I-FILE-NAME TO WS-HOLD-FILE-NAME                  CL**2
00107          PERFORM B100-WRITE-OUTPUT-RECORD                            CL**2
00108      END-IF.                                                         CL**2
00109                                                                      CL**2
00110      PERFORM A100-READ-INPUT-FILE.                                   CL**2
00111                                                                   DPKUT035
00112 ******************************************************************DPKUT035
00113 * B100 THIS PARAGRAPH WRITES OUT AN OUTPUT RECORD, ONE PER       *   CL**2
00114 *      FILE NAME.                                                *   CL**2
00115 ******************************************************************DPKUT035
00116  B100-WRITE-OUTPUT-RECORD.                                           CL**2
00117                                                                   DPKUT035
00118      MOVE DP007I-RECORD TO DP007O-RECORD.                            CL**2
00119      WRITE FILE-TYPE-OUT-RECORD FROM DP007O-RECORD.                  CL**9
