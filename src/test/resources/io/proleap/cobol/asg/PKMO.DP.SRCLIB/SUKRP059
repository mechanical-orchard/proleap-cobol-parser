      *                                                                 00010000
       IDENTIFICATION DIVISION.                                         00020000
      *                                                                 00030000
      *                                                                 00040000
       PROGRAM-ID.             SUKRP059.                                00050000
       AUTHOR.                 V.SUDHAN.                                00060000
       INSTALLATION.           KOHLS.                                   00070000
       DATE-WRITTEN            SEPTEMBER, 2009.                         00080000
       DATE-COMPILED.                                                   00090000
      *                                                                 00100000
      ******************************************************************00110000
      *  THIS PROGRAM WILL PRINT THE DAILY PRINT AND APPLY SERVICE     *00120000
      *  LEVEL REPORT.                                                 *00130000
      ******************************************************************00140000
      *                                                                 00150000
       ENVIRONMENT DIVISION.                                            00160000
      *                                                                 00170000
       CONFIGURATION SECTION.                                           00180000
      *                                                                 00190000
       SOURCE-COMPUTER.        IBM-3090.                                00200000
       OBJECT-COMPUTER.        IBM-3090.                                00210000
      *                                                                 00220000
       INPUT-OUTPUT SECTION.                                            00230000
      *                                                                 00240000
       FILE-CONTROL.                                                    00250000
      *                                                                 00260000
           SELECT SU-PNA-SLA-EXTRACT-FILE                               00270000
               ASSIGN TO INP01.                                         00280000
      *                                                                 00290000
           SELECT SU-PNA-SLA-REPORT-FILE                                00300000
               ASSIGN TO RPT01.                                         00310000
      *                                                                 00320000
       DATA DIVISION.                                                   00330000
      *                                                                 00340000
       FILE SECTION.                                                    00350000
      *                                                                 00360000
       FD  SU-PNA-SLA-EXTRACT-FILE                                      00370000
           RECORDING MODE IS F                                          00380000
           LABEL RECORDS ARE STANDARD                                   00390000
           BLOCK CONTAINS 0 RECORDS.                                    00400000
      *                                                                 00410000
       01  SU-PNA-SLA-EXTRACT-RECORD       PIC  X(1050).                00420000
      *                                                                 00430000
       FD  SU-PNA-SLA-REPORT-FILE                                       00440000
           RECORDING MODE IS F                                          00450000
           LABEL RECORDS ARE STANDARD                                   00460000
           BLOCK CONTAINS 0 RECORDS.                                    00470000
      *                                                                 00480000
       01  SU-PNA-SLA-REPORT-LINE          PIC  X(200).                 00490000
      *                                                                 00500000
       WORKING-STORAGE SECTION.                                         00510000
      *                                                                 00520000
      ***************************************************************** 00530000
      *  STANDARD REPORT HEADER FOR A 132 COLUMN REPORT.              * 00540000
      ***************************************************************** 00550000
      *                                                                 00560000
           COPY DPWS132O.                                               00570000
      *                                                                 00580000
      ***************************************************************** 00590000
      *  RECORD LAYOUT FOR THE PRINT AND APPLY EXTRACT FILE.          * 00600000
      ***************************************************************** 00610000
      *                                                                 00620000
           COPY SUWS103.                                                00630000
      *                                                                 00640000
      ***************************************************************** 00650000
      *  RECORD LAYOUT FOR THE KOHL'S DATE ROUTINE.                   * 00660000
      ***************************************************************** 00670000
      *                                                                 00680000
           COPY DPWS500.                                                00690000
      *                                                                 00700000
      ***************************************************************** 00710000
      *  COPYBOOK MEMBER OF PRINTER LINE COMMANDS TO BE USED WITH     * 00720000
      *  PRINTEK PRINTERS                                             * 00730000
      ***************************************************************** 00740000
      *                                                                 00750000
           COPY DSWS004.                                                00760000
      *                                                                 00770000
      ***************************************************************** 00780000
      *  WORK AREA FOR SQL ERROR ROUTINE 'DSNTIAR'                    * 00790000
      ***************************************************************** 00800000
      *                                                                 00810000
           COPY DPWS004.                                                00820000
      *                                                                 00830000
       01  ABEND-CODE                      PIC S9(04) VALUE +0          00840000
                                                      COMP SYNC.        00850000
      *                                                                 00860000
      ******************************************************************00870000
      *     PAGE HEADINGS        PAGE HEADINGS         PAGE HEADING    *00880000
      ******************************************************************00890000
      *                                                                 00900000
       01  PH2-PAGE-HEADING-1.                                          00910000
           05  FILLER                      PIC  X(45)  VALUE SPACE.     00920000
           05  FILLER                      PIC  X(42)  VALUE            00930000
               'DAILY PRINT AND APPLY SERVICE LEVEL REPORT'.            00940000
           05  FILLER                      PIC  X(45)  VALUE SPACE.     00950000
      *                                                                 00960000
       01  PH2-PAGE-HEADING-2.                                          00970000
           05  FILLER                      PIC  X(61)  VALUE SPACE.     00980000
           05  PV-PAGE-HEADER-3-DATE       PIC  X(10)  VALUE SPACES.    00990000
           05  FILLER                      PIC  X(61)  VALUE SPACE.     01000000
      *                                                                 01010000
       01  PH2-PAGE-HEADING-3.                                          01020000
           05  FILLER                      PIC  X(11)  VALUE SPACE.     01030000
           05  FILLER                      PIC  X(16)  VALUE            01040000
           '--------------- '.                                          01050000
           05  PH2-PROCESS-MM-DD          PIC   X(05)  VALUE  SPACES.   01060000
           05 FILLER                      PIC   X(44)  VALUE            01070000
           ' --------------   --------------- WTD ------'.              01080000
           05  FILLER                      PIC  X(52) VALUE             01090000
           '----------   --------------- MTD ----------------   '.      01100000
           05  FILLER                      PIC  X(36)  VALUE            01110000
           '--------------- YTD ----------------'.                      01120000
      *                                                                 01130000
       01  PH2-PAGE-HEADING-4.                                          01140000
           05  FILLER                      PIC  X(16)  VALUE SPACE.     01150000
           05  FILLER                      PIC  X(58)  VALUE            01160000
           '  # OF        # >SLA                     # OF        # >SL'.01170000
           05  FILLER                      PIC  X(57)  VALUE            01180000
           'A                     # OF        # >SLA                 '. 01190000
           05  FILLER                      PIC  X(22)  VALUE            01200000
           '    # OF        # >SLA'.                                    01210000
      *                                                                 01220000
       01  PH2-PAGE-HEADING-5.                                          01230000
           05  FILLER                      PIC  X(06)  VALUE SPACE.     01240000
           05  FILLER                      PIC  X(58)  VALUE            01250000
           'DC         SCANS         (1.9)        SLA         SCANS   '.01260000
           05  FILLER                      PIC  X(58)  VALUE            01270000
           '      (1.9)        SLA         SCANS         (1.9)        '.01280000
           05  FILLER                      PIC  X(42)  VALUE            01290000
           'SLA         SCANS         (1.9)        SLA'.                01300000
      *                                                                 01310000
      ******************************************************************01320000
      *     DETAIL HEADINGS      DETAIL HEADINGS       DETAIL HEADINGS *01330000
      ******************************************************************01340000
      *                                                                 01350000
       01  PNA-SLA-RPT-DETAIL-LINE.                                     01360000
           05  FILLER                      PIC  X(05)  VALUE SPACES.    01370000
           05  PNA-STORE-NBR               PIC  9(03).                  01380000
           05  PNA-STORE-NBR-X REDEFINES PNA-STORE-NBR                  01390000
                                           PIC  ZZZ.                    01400000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01410000
           05  DLY-NO-OF-SCANS             PIC  ZZZ,ZZZ,ZZ9.            01420000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01430000
           05  DLY-GT-NO-OF-SCANS          PIC  ZZZ,ZZZ,ZZ9.            01440000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01450000
           05  DLY-NO-OF-SLAS              PIC  ZZ9.9999.               01460000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01470000
           05  WTD-NO-OF-SCANS             PIC  ZZZ,ZZZ,ZZ9.            01480000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01490000
           05  WTD-GT-NO-OF-SCANS          PIC  ZZZ,ZZZ,ZZ9.            01500000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01510000
           05  WTD-NO-OF-SLAS              PIC  ZZ9.9999.               01520000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01530000
           05  MTD-NO-OF-SCANS             PIC  ZZZ,ZZZ,ZZ9.            01540000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01550000
           05  MTD-GT-NO-OF-SCANS          PIC  ZZZ,ZZZ,ZZ9.            01560000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01570000
           05  MTD-NO-OF-SLAS              PIC  ZZ9.9999.               01580000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01590000
           05  YTD-NO-OF-SCANS             PIC  ZZZ,ZZZ,ZZ9.            01600000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01610000
           05  YTD-GT-NO-OF-SCANS          PIC  ZZZ,ZZZ,ZZ9.            01620000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01630000
           05  YTD-NO-OF-SLAS              PIC  ZZ9.9999.               01640000
      ******************************************************************01650000
       01  PNA-SLA-RPT-TOTAL-LINE-1.                                    01660000
           05  FILLER                      PIC  X(05)  VALUE SPACES.    01670000
           05  FILLER                      PIC  X(03)  VALUE 'ALL'.     01680000
           05  FILLER                      PIC  X(133) VALUE SPACES.    01690000
       01  PNA-SLA-RPT-TOTAL-LINE-2.                                    01700000
           05  FILLER                      PIC  X(04)  VALUE SPACES.    01710000
           05  FILLER                      PIC  X(04)  VALUE "DC'S".    01720000
           05  FILLER                      PIC  X(01)  VALUE SPACES.    01730000
           05  TOT-DLY-NO-OF-SCANS         PIC  Z,ZZZ,ZZZ,ZZ9.          01740000
           05  FILLER                      PIC  X(01)  VALUE SPACES.    01750000
           05  TOT-DLY-GT-NO-OF-SCANS      PIC  Z,ZZZ,ZZZ,ZZ9.          01760000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01770000
           05  TOT-DLY-NO-OF-SLAS          PIC  ZZ9.9999.               01780000
           05  FILLER                      PIC  X(01)  VALUE SPACES.    01790000
           05  TOT-WTD-NO-OF-SCANS         PIC  Z,ZZZ,ZZZ,ZZ9.          01800000
           05  FILLER                      PIC  X(01)  VALUE SPACES.    01810000
           05  TOT-WTD-GT-NO-OF-SCANS      PIC  Z,ZZZ,ZZZ,ZZ9.          01820000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01830000
           05  TOT-WTD-NO-OF-SLAS          PIC  ZZ9.9999.               01840000
           05  FILLER                      PIC  X(01)  VALUE SPACES.    01850000
           05  TOT-MTD-NO-OF-SCANS         PIC  Z,ZZZ,ZZZ,ZZ9.          01860000
           05  FILLER                      PIC  X(01)  VALUE SPACES.    01870000
           05  TOT-MTD-GT-NO-OF-SCANS      PIC  Z,ZZZ,ZZZ,ZZ9.          01880000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01890000
           05  TOT-MTD-NO-OF-SLAS          PIC  ZZ9.9999.               01900000
           05  FILLER                      PIC  X(01)  VALUE SPACES.    01910000
           05  TOT-YTD-NO-OF-SCANS         PIC  Z,ZZZ,ZZZ,ZZ9.          01920000
           05  FILLER                      PIC  X(01)  VALUE SPACES.    01930000
           05  TOT-YTD-GT-NO-OF-SCANS      PIC  Z,ZZZ,ZZZ,ZZ9.          01940000
           05  FILLER                      PIC  X(03)  VALUE SPACES.    01950000
           05  TOT-YTD-NO-OF-SLAS          PIC  ZZ9.9999.               01960000
      *                                                                 01970000
       01  BLANK-LINE.                                                  01980000
           05  FILLER                      PIC  X(132) VALUE SPACE.     01990000
      *                                                                 02000000
       01  END-OF-REPORT-LINE.                                          02010000
           05  FILLER                      PIC  X(54)  VALUE SPACE.     02020000
           05  FILLER                      PIC  X(25)  VALUE            02030000
               '***** END OF REPORT *****'.                             02040000
           05  FILLER                      PIC  X(54)  VALUE SPACE.     02050000
      *                                                                 02060000
       01  NO-DATA-LINE.                                                02070000
           05  FILLER                      PIC  X(49)  VALUE SPACE.     02080000
           05  FILLER                      PIC  X(35)  VALUE            02090000
               '********* NO DATA TO REPORT *******'.                   02100000
           05  FILLER                      PIC  X(49)  VALUE SPACE.     02110000
      *                                                                 02120000
       01  PC-CONSTANTS.                                                02130000
           05  PC-MAX-REPORT-LINES         PIC S9(04)  VALUE +59        02140000
                                                           COMP SYNC.   02150000
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)  VALUE            02160000
               'SUKRP059'.                                              02170000
           05  PC-DAYS-TO-DECREMENT        PIC S9(03)  VALUE -1.        02180000
      *                                                                 02190000
       01  PS-PROGRAM-SWITCHES.                                         02200000
           05  PS-EOF-PNA-SLA-EXTRACT-IND  PIC  X(01)  VALUE 'N'.       02210000
               88  PS-EOF-PNA-SLA-EXTRACT              VALUE 'Y'.       02220000
               88  PS-NOT-EOF-PNA-SLA-EXTRACT          VALUE 'N'.       02230000
      *                                                                 02240000
       01  PV-PROGRAM-VARIABLES.                                        02250000
           05  PV-PNA-SLA-RPT-CLAYOUT      PIC  X(200).                 02260000
           05  PV-PNA-EXT-REC-CNT          PIC  9(09)  VALUE 0.         02270000
           05  PV-PNA-RPT-REC-WRITTEN      PIC  9(09)  VALUE 0.         02280000
           05  PV-DLY-NO-OF-SLAS           PIC  9(3).9(4) VALUE ZEROES. 02290000
           05  PV-WTD-NO-OF-SLAS           PIC  9(3).9(4) VALUE ZEROES. 02300000
           05  PV-MTD-NO-OF-SLAS           PIC  9(3).9(4) VALUE ZEROES. 02310000
           05  PV-YTD-NO-OF-SLAS           PIC  9(3).9(4) VALUE ZEROES. 02320000
           05  PV-T-DLY-NO-OF-SLAS         PIC  9(3)V9(6) VALUE ZEROES. 02330000
           05  PV-T-WTD-NO-OF-SLAS         PIC  9(3)V9(6) VALUE ZEROES. 02340000
           05  PV-T-MTD-NO-OF-SLAS         PIC  9(3)V9(6) VALUE ZEROES. 02350000
           05  PV-T-YTD-NO-OF-SLAS         PIC  9(3)V9(6) VALUE ZEROES. 02360000
           05  PV-DLY-NO-OF-SCANS          PIC S9(09)  VALUE 0.         02370000
           05  PV-WTD-NO-OF-SCANS          PIC S9(09)  VALUE 0.         02380000
           05  PV-MTD-NO-OF-SCANS          PIC S9(09)  VALUE 0.         02390000
           05  PV-YTD-NO-OF-SCANS          PIC S9(09)  VALUE 0.         02400000
           05  PV-DLY-GT-NO-OF-SCANS       PIC S9(09)  VALUE 0.         02410000
           05  PV-WTD-GT-NO-OF-SCANS       PIC S9(09)  VALUE 0.         02420000
           05  PV-MTD-GT-NO-OF-SCANS       PIC S9(09)  VALUE 0.         02430000
           05  PV-YTD-GT-NO-OF-SCANS       PIC S9(09)  VALUE 0.         02440000
           05  PV-TOT-DLY-NO-OF-SLAS       PIC 9(3).9(4) VALUE ZEROES.  02450000
           05  PV-TOT-WTD-NO-OF-SLAS       PIC 9(3).9(4) VALUE ZEROES.  02460000
           05  PV-TOT-MTD-NO-OF-SLAS       PIC 9(3).9(4) VALUE ZEROES.  02470000
           05  PV-TOT-YTD-NO-OF-SLAS       PIC 9(3).9(4) VALUE ZEROES.  02480000
           05  PV-T-TOT-DLY-NO-OF-SLAS     PIC 9(3)V9(6) VALUE ZEROES.  02490000
           05  PV-T-TOT-WTD-NO-OF-SLAS     PIC 9(3)V9(6) VALUE ZEROES.  02500000
           05  PV-T-TOT-MTD-NO-OF-SLAS     PIC 9(3)V9(6) VALUE ZEROES.  02510000
           05  PV-T-TOT-YTD-NO-OF-SLAS     PIC 9(3)V9(6) VALUE ZEROES.  02520000
           05  PV-TOT-DLY-NO-OF-SCANS      PIC S9(09)  VALUE 0.         02530000
           05  PV-TOT-WTD-NO-OF-SCANS      PIC S9(09)  VALUE 0.         02540000
           05  PV-TOT-MTD-NO-OF-SCANS      PIC S9(09)  VALUE 0.         02550000
           05  PV-TOT-YTD-NO-OF-SCANS      PIC S9(09)  VALUE 0.         02560000
           05  PV-TOT-DLY-GT-NO-OF-SCANS   PIC S9(09)  VALUE 0.         02570000
           05  PV-TOT-WTD-GT-NO-OF-SCANS   PIC S9(09)  VALUE 0.         02580000
           05  PV-TOT-MTD-GT-NO-OF-SCANS   PIC S9(09)  VALUE 0.         02590000
           05  PV-TOT-YTD-GT-NO-OF-SCANS   PIC S9(09)  VALUE 0.         02600000
           05  PV-PROCESS-DATE             PIC  X(10).                  02610000
           05  PV-PROC-DATE.                                            02620000
               10 PV-PROC-DATE-MM          PIC  X(02).                  02630000
               10 FILLER                   PIC  X(01)  VALUE '/'.       02640000
               10 PV-PROC-DATE-DD          PIC  X(02).                  02650000
               10 FILLER                   PIC  X(01)  VALUE '/'.       02660000
               10 PV-PROC-DATE-CCYY        PIC  X(04).                  02670000
           05  PV-DATE.                                                 02680000
               10 PV-DATE-CCYY             PIC  9(04).                  02690000
               10 PV-DATE-MM               PIC  9(02).                  02700000
               10 PV-DATE-DD               PIC  9(02).                  02710000
           05  PV-FISCAL-YEAR              PIC  X(04).                  02720000
           05  PV-FISCAL-MONTH             PIC  X(02).                  02730000
           05  PV-FISCAL-WEEK              PIC  X(02).                  02740000
           05  PV-OPER-UNT-ID              PIC  9(04).                  02750000
           05  PV-DETAIL-PAGE-COUNT        PIC 9(04)  VALUE 0           02760000
                                                           COMP SYNC.   02770000
           05  PV-DETAIL-LINE-COUNT        PIC 9(09)  VALUE 0           02780000
                                                           COMP SYNC.   02790000
           05  PV-LINE-COUNT               PIC 9(04)  VALUE 0           02800000
                                                           COMP SYNC.   02810000
           05  PV-PARAGRAPH-NAME           PIC  X(30)  VALUE SPACE.     02820000
           05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)  VALUE SPACE.     02830000
           05  PV-CURR-DATE.                                            02840000
               15  PV-CURR-YY              PIC  X(02).                  02850000
               15  PV-CURR-MM              PIC  X(02).                  02860000
               15  PV-CURR-DD              PIC  X(02).                  02870000
           05  PV-CURR-TIME.                                            02880000
               15  PV-CURR-HH              PIC  X(02).                  02890000
               15  PV-CURR-MIN             PIC  X(02).                  02900000
               15  PV-CURR-SS              PIC  X(02).                  02910000
      ******************************************************************02920000
      *  DB2 COMMUNICATION AREA.                                       *02930000
      ******************************************************************02940000
           EXEC SQL                                                     02950000
                INCLUDE SQLCA                                           02960000
           END-EXEC.                                                    02970000
      *                                                                 02980000
      ******************************************************************02990000
      *  DB2 TABLE COBOL WORKING STORAGE AREA FOR THE COMPANY CONTROL  *03000000
      *  TABLE (TCOCNTL)                                               *03010000
      ******************************************************************03020000
      *                                                                 03030000
           EXEC SQL                                                     03040000
                INCLUDE TCOCNTL                                         03050000
           END-EXEC.                                                    03060000
      *                                                                 03070000
      ******************************************************************03080000
      *  DB2 TABLE COBOL WORKING STORAGE AREA FOR THE ATTRIBUTE TABLE  *03090000
      *  (TDTATTR)                                                     *03100000
      ******************************************************************03110000
      *                                                                 03120000
           EXEC SQL                                                     03130000
                INCLUDE TDTATTR                                         03140000
           END-EXEC.                                                    03150000
      *                                                                 03160000
      ******************************************************************03170000
      *   DB2 COMMUNICATIONS AREA2                                     *03180000
      ******************************************************************03190000
           COPY SQLCA2.                                                 03200000
      *                                                                 03210000
       PROCEDURE DIVISION.                                              03220000
      *                                                                 03230000
      ******************************************************************03240000
      *  THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM.   *03250000
      ******************************************************************03260000
      *                                                                 03270000
       0000-PROGRAM-MAINLINE.                                           03280000
      *                                                                 03290000
           PERFORM 1000-INITIALIZE.                                     03300000
      *                                                                 03310000
           PERFORM 2000-CREATE-REPORT                                   03320000
               UNTIL PS-EOF-PNA-SLA-EXTRACT.                            03330000
      *                                                                 03340000
           PERFORM 3000-EOJ-ROUTINE.                                    03350000
      *                                                                 03360000
           STOP RUN.                                                    03370000
      *                                                                 03380000
      ******************************************************************03390000
      *  THIS PARAGRAPH                                                *03400000
      *  1. OPEN ALL INPUT/OUTPUT FILES                                *03410000
      *  2. GET ALL HEADER INFORMATION AND PRINTS                      *03420000
      *  3. IF THE INPUT FILE IS EMPTY, THEN PRINTS THE NO DATA MSG    *03430000
      ******************************************************************03440000
      *                                                                 03450000
       1000-INITIALIZE.                                                 03460000
      *                                                                 03470000
           DISPLAY '** PROGRAM SUKRP059 EXECUTION STARTED **'.          03480000
           OPEN INPUT  SU-PNA-SLA-EXTRACT-FILE                          03490000
                OUTPUT SU-PNA-SLA-REPORT-FILE.                          03500000
      *                                                                 03510000
           PERFORM 1100-SET-UP-HEADER.                                  03520000
           PERFORM 8200-PRINT-PAGE-HEADING.                             03530000
      *                                                                 03540000
           SET PS-NOT-EOF-PNA-SLA-EXTRACT     TO TRUE.                  03550000
           PERFORM 8000-READ-EXTRACT-FILE.                              03560000
                                                                        03570000
      *    IF THERE IS NO DATA, PRINT NO DATA MESSAGE TO THE REPORT.    03580000
           IF PS-EOF-PNA-SLA-EXTRACT                                    03590000
              DISPLAY '**                                         **'   03600000
              DISPLAY '*******  NO DATA TO REPORT IN SUKRP059 ******'   03610000
              DISPLAY '**                                         **'   03620000
              MOVE BLANK-LINE           TO PV-PNA-SLA-RPT-CLAYOUT       03630000
              PERFORM 8100-WRITE-PNA-SLA-RPT                            03640000
                                                                        03650000
              MOVE NO-DATA-LINE         TO PV-PNA-SLA-RPT-CLAYOUT       03660000
              PERFORM 8100-WRITE-PNA-SLA-RPT                            03670000
                                                                        03680000
              MOVE BLANK-LINE           TO PV-PNA-SLA-RPT-CLAYOUT       03690000
              PERFORM 8100-WRITE-PNA-SLA-RPT                            03700000
                                                                        03710000
              MOVE END-OF-REPORT-LINE   TO PV-PNA-SLA-RPT-CLAYOUT       03720000
              PERFORM 8100-WRITE-PNA-SLA-RPT                            03730000
           ELSE                                                         03740000
              MOVE SU103-OPER-UNT-ID-X  TO PV-OPER-UNT-ID               03750000
           END-IF.                                                      03760000
      *                                                                 03770000
      ******************************************************************03780000
      *  THIS PARAGRAPH MOVES ALL INFORMATION TO THE HEADER FIELDS AND *03790000
      *  PRINTS THE PNA SLA REPORT HEADERS.                            *03800000
      ******************************************************************03810000
      *                                                                 03820000
       1100-SET-UP-HEADER.                                              03830000
      *                                                                 03840000
           MOVE PC-CURRENT-PROGRAM-NAME                                 03850000
                                     TO DP132O-PROGRAM-NAME.            03860000
      *                                                                 03870000
           ACCEPT PV-CURR-DATE       FROM DATE.                         03880000
           MOVE PV-CURR-YY           TO DP132O-RUN-YEAR.                03890000
           MOVE PV-CURR-MM           TO DP132O-RUN-MONTH.               03900000
           MOVE PV-CURR-DD           TO DP132O-RUN-DAY.                 03910000
      *                                                                 03920000
           ACCEPT PV-CURR-TIME       FROM TIME.                         03930000
           MOVE PV-CURR-HH           TO DP132O-RUN-HOUR.                03940000
           MOVE PV-CURR-MIN          TO DP132O-RUN-MINUTE.              03950000
      *                                                                 03960000
           PERFORM 1110-GET-DATE-PARMS.                                 03970000
           DISPLAY '** PROCESSING DATE : ' PV-PROCESS-DATE.             03980000
           MOVE PV-PROCESS-DATE(1:4) TO PV-DATE-CCYY                    03990000
                                        PV-PROC-DATE-CCYY               04000000
           MOVE PV-PROCESS-DATE(6:2) TO PV-DATE-MM                      04010000
                                        PV-PROC-DATE-MM                 04020000
           MOVE PV-PROCESS-DATE(9:2) TO PV-DATE-DD                      04030000
                                        PV-PROC-DATE-DD                 04040000
      *                                                                 04050000
           MOVE PV-PROC-DATE         TO PV-PAGE-HEADER-3-DATE.          04060000
           MOVE PV-PROC-DATE(1:5)    TO PH2-PROCESS-MM-DD.              04070000
      *                                                                 04080000
      ******************************************************************04090000
      **  THIS PARAGRAPH FETCHES THE PROCESSING DATE FROM THE TCOCNTL **04100000
      **  TDTATTR DB2 TABLES. THIS WILL RETURN ONE ROW FROM THE QUERY.**04110000
      ******************************************************************04120000
      *                                                                 04130000
       1110-GET-DATE-PARMS.                                             04140000
      *                                                                 04150000
            EXEC SQL                                                    04160000
                 SELECT   A.BTCH_PRC_DTE                                04170000
                         ,B.KY_DTE                                      04180000
                         ,B.FSCL_YR_NBR                                 04190000
                         ,B.FSCL_MN_NBR                                 04200000
                         ,B.FSCL_WK_NBR                                 04210000
                    INTO  :COCNTL-BTCH-PRC-DTE                          04220000
                         ,:DTATTR-KY-DTE                                04230000
                         ,:DTATTR-FSCL-YR-NBR                           04240000
                         ,:DTATTR-FSCL-MN-NBR                           04250000
                         ,:DTATTR-FSCL-WK-NBR                           04260000
                  FROM    TCOCNTL A                                     04270000
                         ,TDTATTR B                                     04280000
                  WHERE A.OPER_UNT_ID = 90                              04290000
                    AND A.BTCH_PRC_DTE = B.KY_DTE                       04300000
            END-EXEC.                                                   04310000
      *                                                                 04320000
           EVALUATE TRUE                                                04330000
               WHEN SQLCODE = ZEROS                                     04340000
                    MOVE COCNTL-BTCH-PRC-DTE  TO  PV-PROCESS-DATE       04350000
                    MOVE DTATTR-FSCL-YR-NBR   TO  PV-FISCAL-YEAR        04360000
                    MOVE DTATTR-FSCL-MN-NBR   TO  PV-FISCAL-MONTH       04370000
                    MOVE DTATTR-FSCL-WK-NBR   TO  PV-FISCAL-WEEK        04380000
               WHEN SQLCODE = +100                                      04390000
               WHEN SQLWARN0 NOT = SPACES                               04400000
               WHEN SQLCODE < ZERO                                      04410000
               WHEN SQLCODE > ZERO                                      04420000
                   MOVE '1110-GET-DATE-PARMS'                           04430000
                                   TO PV-PARAGRAPH-NAME                 04440000
                   MOVE 'GET DATE FROM TCOCNTL & TDTATTR'               04450000
                                   TO PV-DB2-OPERATION-ATTEMPTED        04460000
                   PERFORM 9000-SQL-ABEND                               04470000
           END-EVALUATE.                                                04480000
      *                                                                 04490000
      ******************************************************************04500000
      * THIS PARAGRAPH COLLECTS THE DETAIL HEADER INFORMATION AND PRINT*04510000
      * IT IN THE DETAIL LAYOUT.                                       *04520000
      ******************************************************************04530000
      *                                                                 04540000
       2000-CREATE-REPORT.                                              04550000
      *                                                                 04560000
             IF (SU103-OPER-UNT-ID-X NOT EQUAL PV-OPER-UNT-ID)          04570000
                PERFORM 8300-PRINT-DEATIL-LINE                          04580000
                MOVE SU103-OPER-UNT-ID-X   TO PV-OPER-UNT-ID            04590000
             ELSE                                                       04600000
                IF (SU103-OPER-UNT-ID-X  EQUAL PV-OPER-UNT-ID) AND      04610000
                   (SU103-PROCESS-DATE   EQUAL PV-DATE)                 04620000
                   ADD SU103-TOTL-CARTON-SCANS TO                       04630000
                                            PV-DLY-NO-OF-SCANS          04640000
                                            PV-TOT-DLY-NO-OF-SCANS      04650000
                                            PV-WTD-NO-OF-SCANS          04660000
                                            PV-TOT-WTD-NO-OF-SCANS      04670000
                                            PV-MTD-NO-OF-SCANS          04680000
                                            PV-TOT-MTD-NO-OF-SCANS      04690000
                                            PV-YTD-NO-OF-SCANS          04700000
                                            PV-TOT-YTD-NO-OF-SCANS      04710000
                   ADD SU103-TOTL-GT-SLA       TO                       04720000
                                            PV-DLY-GT-NO-OF-SCANS       04730000
                                            PV-TOT-DLY-GT-NO-OF-SCANS   04740000
                                            PV-WTD-GT-NO-OF-SCANS       04750000
                                            PV-TOT-WTD-GT-NO-OF-SCANS   04760000
                                            PV-MTD-GT-NO-OF-SCANS       04770000
                                            PV-TOT-MTD-GT-NO-OF-SCANS   04780000
                                            PV-YTD-GT-NO-OF-SCANS       04790000
                                            PV-TOT-YTD-GT-NO-OF-SCANS   04800000
                ELSE                                                    04810000
      *                                                                 04820000
                IF ((SU103-OPER-UNT-ID-X  EQUAL PV-OPER-UNT-ID) AND     04830000
                   (SU103-PROCESS-DATE NOT EQUAL PV-DATE) AND           04840000
                   (SU103-FISCAL-YR EQUAL PV-FISCAL-YEAR) AND           04850000
                   (SU103-FISCAL-WK EQUAL PV-FISCAL-WEEK))              04860000
                   ADD SU103-TOTL-CARTON-SCANS TO                       04870000
                                            PV-WTD-NO-OF-SCANS          04880000
                                            PV-TOT-WTD-NO-OF-SCANS      04890000
                                            PV-MTD-NO-OF-SCANS          04900000
                                            PV-TOT-MTD-NO-OF-SCANS      04910000
                                            PV-YTD-NO-OF-SCANS          04920000
                                            PV-TOT-YTD-NO-OF-SCANS      04930000
                   ADD SU103-TOTL-GT-SLA       TO                       04940000
                                            PV-WTD-GT-NO-OF-SCANS       04950000
                                            PV-TOT-WTD-GT-NO-OF-SCANS   04960000
                                            PV-MTD-GT-NO-OF-SCANS       04970000
                                            PV-TOT-MTD-GT-NO-OF-SCANS   04980000
                                            PV-YTD-GT-NO-OF-SCANS       04990000
                                            PV-TOT-YTD-GT-NO-OF-SCANS   05000000
                ELSE                                                    05010000
      *                                                                 05020000
                IF ((SU103-OPER-UNT-ID-X  EQUAL PV-OPER-UNT-ID) AND     05030000
                   (SU103-PROCESS-DATE NOT EQUAL PV-DATE) AND           05040000
                   (SU103-FISCAL-YR EQUAL PV-FISCAL-YEAR) AND           05050000
                   (SU103-FISCAL-MO EQUAL PV-FISCAL-MONTH))             05060000
                   ADD SU103-TOTL-CARTON-SCANS TO                       05070000
                                            PV-MTD-NO-OF-SCANS          05080000
                                            PV-TOT-MTD-NO-OF-SCANS      05090000
                                            PV-YTD-NO-OF-SCANS          05100000
                                            PV-TOT-YTD-NO-OF-SCANS      05110000
                   ADD SU103-TOTL-GT-SLA       TO                       05120000
                                            PV-MTD-GT-NO-OF-SCANS       05130000
                                            PV-TOT-MTD-GT-NO-OF-SCANS   05140000
                                            PV-YTD-GT-NO-OF-SCANS       05150000
                                            PV-TOT-YTD-GT-NO-OF-SCANS   05160000
                ELSE                                                    05170000
      *                                                                 05180000
                IF ((SU103-OPER-UNT-ID-X  EQUAL PV-OPER-UNT-ID) AND     05190000
                   (SU103-PROCESS-DATE NOT EQUAL PV-DATE) AND           05200000
                   (SU103-FISCAL-YR EQUAL PV-FISCAL-YEAR))              05210000
                   ADD SU103-TOTL-CARTON-SCANS TO                       05220000
                                            PV-YTD-NO-OF-SCANS          05230000
                                            PV-TOT-YTD-NO-OF-SCANS      05240000
                   ADD SU103-TOTL-GT-SLA       TO                       05250000
                                            PV-YTD-GT-NO-OF-SCANS       05260000
                                            PV-TOT-YTD-GT-NO-OF-SCANS   05270000
                END-IF                                                  05280000
                END-IF                                                  05290000
                END-IF                                                  05300000
                END-IF                                                  05310000
                PERFORM 8000-READ-EXTRACT-FILE                          05320000
             END-IF.                                                    05330000
      *                                                                 05340000
      *                                                                 05350000
      ******************************************************************05360000
      * THIS PARAGRAPH PRINTS THE GRAN TOTAL, DISPALYS THE RECORDS READ*05370000
      * & WRITTEN, CLOSES THE FILES.                                   *05380000
      ******************************************************************05390000
      *                                                                 05400000
       3000-EOJ-ROUTINE.                                                05410000
      *                                                                 05420000
           IF PV-PNA-EXT-REC-CNT EQUAL ZEROES                           05430000
              CONTINUE                                                  05440000
           ELSE                                                         05450000
              PERFORM 8300-PRINT-DEATIL-LINE                            05460000
           END-IF                                                       05470000
      *                                                                 05480000
           MOVE PNA-SLA-RPT-TOTAL-LINE-1                                05490000
                                      TO PV-PNA-SLA-RPT-CLAYOUT         05500000
           PERFORM 8100-WRITE-PNA-SLA-RPT                               05510000
      *                                                                 05520000
            IF PV-TOT-DLY-GT-NO-OF-SCANS EQUAL ZEROES                   05530000
              MOVE 100.00 TO PV-TOT-DLY-NO-OF-SLAS                      05540000
            END-IF                                                      05550000
            IF (PV-TOT-DLY-GT-NO-OF-SCANS EQUAL ZEROES) AND             05560000
               (PV-TOT-DLY-NO-OF-SCANS    EQUAL ZEROES)                 05570000
              MOVE 000.00 TO PV-TOT-DLY-NO-OF-SLAS                      05580000
            END-IF                                                      05590000
            IF (PV-TOT-DLY-GT-NO-OF-SCANS > ZEROES) AND                 05600000
               (PV-TOT-DLY-NO-OF-SCANS    > ZEROES)                     05610000
              COMPUTE PV-T-TOT-DLY-NO-OF-SLAS =                         05620000
              (PV-TOT-DLY-GT-NO-OF-SCANS / PV-TOT-DLY-NO-OF-SCANS) * 10005630000
              END-COMPUTE                                               05640000
              COMPUTE PV-TOT-DLY-NO-OF-SLAS   =                         05650000
                (100.00 - PV-T-TOT-DLY-NO-OF-SLAS)                      05660000
              END-COMPUTE                                               05670000
            END-IF                                                      05680000
      *                                                                 05690000
            IF PV-TOT-WTD-GT-NO-OF-SCANS EQUAL ZEROES                   05700000
              MOVE 100.00 TO PV-TOT-WTD-NO-OF-SLAS                      05710000
            END-IF                                                      05720000
            IF (PV-TOT-WTD-GT-NO-OF-SCANS EQUAL ZEROES) AND             05730000
               (PV-TOT-WTD-NO-OF-SCANS    EQUAL ZEROES)                 05740000
              MOVE 000.00 TO PV-TOT-WTD-NO-OF-SLAS                      05750000
            END-IF                                                      05760000
            IF (PV-TOT-WTD-GT-NO-OF-SCANS > ZEROES) AND                 05770000
               (PV-TOT-WTD-NO-OF-SCANS    > ZEROES)                     05780000
              COMPUTE PV-T-TOT-WTD-NO-OF-SLAS =                         05790000
              (PV-TOT-WTD-GT-NO-OF-SCANS / PV-TOT-WTD-NO-OF-SCANS) * 10005800000
              END-COMPUTE                                               05810000
              COMPUTE PV-TOT-WTD-NO-OF-SLAS   =                         05820000
                (100.00 - PV-T-TOT-WTD-NO-OF-SLAS)                      05830000
              END-COMPUTE                                               05840000
            END-IF                                                      05850000
      *                                                                 05860000
            IF PV-TOT-MTD-GT-NO-OF-SCANS EQUAL ZEROES                   05870000
              MOVE 100.00 TO PV-TOT-MTD-NO-OF-SLAS                      05880000
            END-IF                                                      05890000
            IF (PV-TOT-MTD-GT-NO-OF-SCANS EQUAL ZEROES) AND             05900000
               (PV-TOT-MTD-NO-OF-SCANS    EQUAL ZEROES)                 05910000
              MOVE 000.00 TO PV-TOT-MTD-NO-OF-SLAS                      05920000
            END-IF                                                      05930000
            IF (PV-TOT-MTD-GT-NO-OF-SCANS > ZEROES) AND                 05940000
               (PV-TOT-MTD-NO-OF-SCANS    > ZEROES)                     05950000
              COMPUTE PV-T-TOT-MTD-NO-OF-SLAS =                         05960000
              (PV-TOT-MTD-GT-NO-OF-SCANS / PV-TOT-MTD-NO-OF-SCANS) * 10005970000
              END-COMPUTE                                               05980000
              COMPUTE PV-TOT-MTD-NO-OF-SLAS   =                         05990000
                (100.00 - PV-T-TOT-MTD-NO-OF-SLAS)                      06000000
              END-COMPUTE                                               06010000
            END-IF                                                      06020000
      *                                                                 06030000
            IF PV-TOT-YTD-GT-NO-OF-SCANS EQUAL ZEROES                   06040000
              MOVE 100.00 TO PV-TOT-YTD-NO-OF-SLAS                      06050000
            END-IF                                                      06060000
            IF (PV-TOT-YTD-GT-NO-OF-SCANS EQUAL ZEROES) AND             06070000
               (PV-TOT-YTD-NO-OF-SCANS    EQUAL ZEROES)                 06080000
              MOVE 000.00 TO PV-TOT-YTD-NO-OF-SLAS                      06090000
            END-IF                                                      06100000
            IF (PV-TOT-YTD-GT-NO-OF-SCANS > ZEROES) AND                 06110000
               (PV-TOT-YTD-NO-OF-SCANS    > ZEROES)                     06120000
              COMPUTE PV-T-TOT-YTD-NO-OF-SLAS =                         06130000
              (PV-TOT-YTD-GT-NO-OF-SCANS / PV-TOT-YTD-NO-OF-SCANS) * 10006140000
              END-COMPUTE                                               06150000
              COMPUTE PV-TOT-YTD-NO-OF-SLAS   =                         06160000
                (100.00 - PV-T-TOT-YTD-NO-OF-SLAS)                      06170000
              END-COMPUTE                                               06180000
            END-IF                                                      06190000
      *                                                                 06200000
           MOVE PV-TOT-DLY-NO-OF-SCANS    TO TOT-DLY-NO-OF-SCANS        06210000
           MOVE PV-TOT-DLY-GT-NO-OF-SCANS TO TOT-DLY-GT-NO-OF-SCANS     06220000
           MOVE PV-TOT-DLY-NO-OF-SLAS     TO TOT-DLY-NO-OF-SLAS         06230000
           MOVE PV-TOT-WTD-NO-OF-SCANS    TO TOT-WTD-NO-OF-SCANS        06240000
           MOVE PV-TOT-WTD-GT-NO-OF-SCANS TO TOT-WTD-GT-NO-OF-SCANS     06250000
           MOVE PV-TOT-WTD-NO-OF-SLAS     TO TOT-WTD-NO-OF-SLAS         06260000
           MOVE PV-TOT-MTD-NO-OF-SCANS    TO TOT-MTD-NO-OF-SCANS        06270000
           MOVE PV-TOT-MTD-GT-NO-OF-SCANS TO TOT-MTD-GT-NO-OF-SCANS     06280000
           MOVE PV-TOT-MTD-NO-OF-SLAS     TO TOT-MTD-NO-OF-SLAS         06290000
           MOVE PV-TOT-YTD-NO-OF-SCANS    TO TOT-YTD-NO-OF-SCANS        06300000
           MOVE PV-TOT-YTD-GT-NO-OF-SCANS TO TOT-YTD-GT-NO-OF-SCANS     06310000
           MOVE PV-TOT-YTD-NO-OF-SLAS     TO TOT-YTD-NO-OF-SLAS         06320000
      *                                                                 06330000
           MOVE PNA-SLA-RPT-TOTAL-LINE-2                                06340000
                                      TO PV-PNA-SLA-RPT-CLAYOUT         06350000
           PERFORM 8100-WRITE-PNA-SLA-RPT                               06360000
      *                                                                 06370000
           DISPLAY '                                                   '06380000
           DISPLAY '** SUKRP059-TOTAL PNA SLA EXTRACT RECORDS READ   : '06390000
                   PV-PNA-EXT-REC-CNT.                                  06400000
           DISPLAY '                                                   '06410000
           DISPLAY '** SUKRP059-TOTAL PNA SLA DETAIL RECORDS WRITTEN : '06420000
                   PV-DETAIL-LINE-COUNT.                                06430000
           DISPLAY '                                                   '06440000
           DISPLAY '** SUKRP059-TOTAL PNA SLA REPORT RECORDS WRITTEN : '06450000
                   PV-PNA-RPT-REC-WRITTEN.                              06460000
           DISPLAY '                                                   '06470000
           DISPLAY '** PROGRAM SUKRP059 EXECUTION ENDED   **'.          06480000
      *                                                                 06490000
           CLOSE  SU-PNA-SLA-EXTRACT-FILE                               06500000
                  SU-PNA-SLA-REPORT-FILE.                               06510000
      *                                                                 06520000
      ******************************************************************06530000
      * THIS PARAGRAPH READS THE INPUT IN SUWS103 LAYOUT               *06540000
      ******************************************************************06550000
      *                                                                 06560000
       8000-READ-EXTRACT-FILE.                                          06570000
      *                                                                 06580000
           READ SU-PNA-SLA-EXTRACT-FILE  INTO SU103-PNA-SLA-INFO        06590000
               AT END                                                   06600000
                  SET PS-EOF-PNA-SLA-EXTRACT TO TRUE                    06610000
               NOT AT END                                               06620000
                  ADD +1 TO PV-PNA-EXT-REC-CNT                          06630000
           END-READ.                                                    06640000
      *                                                                 06650000
      ******************************************************************06660000
      * THIS PARAGRAPH WRITES THE PNA SLA REPORT IN REPORT LAYOUT.     *06670000
      ******************************************************************06680000
      *                                                                 06690000
       8100-WRITE-PNA-SLA-RPT.                                          06700000
      *                                                                 06710000
             WRITE SU-PNA-SLA-REPORT-LINE                               06720000
              FROM PV-PNA-SLA-RPT-CLAYOUT                               06730000
               ADD +1 TO PV-PNA-RPT-REC-WRITTEN                         06740000
                         PV-LINE-COUNT.                                 06750000
      *                                                                 06760000
      ******************************************************************06770000
      *  THIS PARAGRAPH CREATES THE PAGE HEADING FOR THE PNA SLA REPORT*06780000
      *  WHEN EVER 60 LINES PER PAGE IS PRINTED.                       *06790000
      ******************************************************************06800000
      *                                                                 06810000
       8200-PRINT-PAGE-HEADING.                                         06820000
      *                                                                 06830000
           MOVE ZEROES               TO PV-LINE-COUNT.                  06840000
           ADD 1                     TO PV-DETAIL-PAGE-COUNT.           06850000
           MOVE 1                    TO DP132O-REPORT-NUMBER.           06860000
           MOVE PV-DETAIL-PAGE-COUNT TO DP132O-PAGE-NUMBER.             06870000
      *                                                                 06880000
           MOVE DP132O-STANDRD-HEADER-132-COLS                          06890000
                                     TO PV-PNA-SLA-RPT-CLAYOUT.         06900000
           PERFORM 8100-WRITE-PNA-SLA-RPT.                              06910000
      *                                                                 06920000
           MOVE BLANK-LINE           TO PV-PNA-SLA-RPT-CLAYOUT.         06930000
           PERFORM 8100-WRITE-PNA-SLA-RPT.                              06940000
      *                                                                 06950000
           MOVE PH2-PAGE-HEADING-1   TO PV-PNA-SLA-RPT-CLAYOUT.         06960000
           PERFORM 8100-WRITE-PNA-SLA-RPT.                              06970000
      *                                                                 06980000
           MOVE PH2-PAGE-HEADING-2   TO PV-PNA-SLA-RPT-CLAYOUT.         06990000
           PERFORM 8100-WRITE-PNA-SLA-RPT.                              07000000
      *                                                                 07010000
           MOVE BLANK-LINE           TO PV-PNA-SLA-RPT-CLAYOUT.         07020000
           PERFORM 8100-WRITE-PNA-SLA-RPT.                              07030000
      *                                                                 07040000
           MOVE PH2-PAGE-HEADING-3   TO PV-PNA-SLA-RPT-CLAYOUT.         07050000
           PERFORM 8100-WRITE-PNA-SLA-RPT.                              07060000
      *                                                                 07070000
           MOVE PH2-PAGE-HEADING-4   TO PV-PNA-SLA-RPT-CLAYOUT.         07080000
           PERFORM 8100-WRITE-PNA-SLA-RPT.                              07090000
      *                                                                 07100000
           MOVE PH2-PAGE-HEADING-5   TO PV-PNA-SLA-RPT-CLAYOUT.         07110000
           PERFORM 8100-WRITE-PNA-SLA-RPT.                              07120000
      *                                                                 07130000
           MOVE BLANK-LINE           TO PV-PNA-SLA-RPT-CLAYOUT.         07140000
           PERFORM 8100-WRITE-PNA-SLA-RPT.                              07150000
      *                                                                 07160000
      ******************************************************************07170000
      *  THIS PARAGRAPH CREATES THE DETAIL LINE FOR THE PNA SLA REPORT *07180000
      *  WHEN EVER DC SEQUENCE CHANGES.                                *07190000
      ******************************************************************07200000
      *                                                                 07210000
       8300-PRINT-DEATIL-LINE.                                          07220000
      *                                                                 07230000
      *    IF THE NUMBER OF LINES PRINTED REACHES THE MAXIMUM NUMBER OF 07240000
      *    LINES CAN PRINTED PER PAGE THEN GOTO THE NEXT PAGE           07250000
      *                                                                 07260000
            IF PV-LINE-COUNT > PC-MAX-REPORT-LINES                      07270000
               PERFORM 8200-PRINT-PAGE-HEADING                          07280000
            END-IF                                                      07290000
      *                                                                 07300000
            IF PV-DLY-GT-NO-OF-SCANS EQUAL ZEROES                       07310000
              MOVE 100.00 TO PV-DLY-NO-OF-SLAS                          07320000
            END-IF                                                      07330000
            IF (PV-DLY-GT-NO-OF-SCANS EQUAL ZEROES) AND                 07340000
               (PV-DLY-NO-OF-SCANS    EQUAL ZEROES)                     07350000
              MOVE 000.00 TO PV-DLY-NO-OF-SLAS                          07360000
            END-IF                                                      07370000
            IF (PV-DLY-GT-NO-OF-SCANS > ZEROES) AND                     07380000
               (PV-DLY-NO-OF-SCANS    > ZEROES)                         07390000
              COMPUTE PV-T-DLY-NO-OF-SLAS =                             07400000
              (PV-DLY-GT-NO-OF-SCANS / PV-DLY-NO-OF-SCANS) * 100        07410000
              END-COMPUTE                                               07420000
              COMPUTE PV-DLY-NO-OF-SLAS   =                             07430000
                (100.00 - PV-T-DLY-NO-OF-SLAS)                          07440000
              END-COMPUTE                                               07450000
            END-IF                                                      07460000
      *                                                                 07470000
            IF PV-WTD-GT-NO-OF-SCANS EQUAL ZEROES                       07480000
              MOVE 100.00 TO PV-WTD-NO-OF-SLAS                          07490000
            END-IF                                                      07500000
            IF (PV-WTD-GT-NO-OF-SCANS EQUAL ZEROES) AND                 07510000
               (PV-WTD-NO-OF-SCANS    EQUAL ZEROES)                     07520000
              MOVE 000.00 TO PV-WTD-NO-OF-SLAS                          07530000
            END-IF                                                      07540000
            IF (PV-WTD-GT-NO-OF-SCANS > ZEROES) AND                     07550000
               (PV-WTD-NO-OF-SCANS    > ZEROES)                         07560000
              COMPUTE PV-T-WTD-NO-OF-SLAS =                             07570000
              (PV-WTD-GT-NO-OF-SCANS / PV-WTD-NO-OF-SCANS) * 100        07580000
              END-COMPUTE                                               07590000
              COMPUTE PV-WTD-NO-OF-SLAS   =                             07600000
                (100.00 - PV-T-WTD-NO-OF-SLAS)                          07610000
              END-COMPUTE                                               07620000
            END-IF                                                      07630000
      *                                                                 07640000
            IF PV-MTD-GT-NO-OF-SCANS EQUAL ZEROES                       07650000
              MOVE 100.00 TO PV-MTD-NO-OF-SLAS                          07660000
            END-IF                                                      07670000
            IF (PV-MTD-GT-NO-OF-SCANS EQUAL ZEROES) AND                 07680000
               (PV-MTD-NO-OF-SCANS    EQUAL ZEROES)                     07690000
              MOVE 000.00 TO PV-MTD-NO-OF-SLAS                          07700000
            END-IF                                                      07710000
            IF (PV-MTD-GT-NO-OF-SCANS > ZEROES) AND                     07720000
               (PV-MTD-NO-OF-SCANS    > ZEROES)                         07730000
              COMPUTE PV-T-MTD-NO-OF-SLAS =                             07740000
              (PV-MTD-GT-NO-OF-SCANS / PV-MTD-NO-OF-SCANS) * 100        07750000
              END-COMPUTE                                               07760000
              COMPUTE PV-MTD-NO-OF-SLAS   =                             07770000
                (100.00 - PV-T-MTD-NO-OF-SLAS)                          07780000
              END-COMPUTE                                               07790000
            END-IF                                                      07800000
      *                                                                 07810000
            IF PV-YTD-GT-NO-OF-SCANS EQUAL ZEROES                       07820000
              MOVE 100.00 TO PV-YTD-NO-OF-SLAS                          07830000
            END-IF                                                      07840000
            IF (PV-YTD-GT-NO-OF-SCANS EQUAL ZEROES) AND                 07850000
               (PV-YTD-NO-OF-SCANS    EQUAL ZEROES)                     07860000
              MOVE 000.00 TO PV-YTD-NO-OF-SLAS                          07870000
            END-IF                                                      07880000
            IF (PV-YTD-GT-NO-OF-SCANS > ZEROES) AND                     07890000
               (PV-YTD-NO-OF-SCANS    > ZEROES)                         07900000
              COMPUTE PV-T-YTD-NO-OF-SLAS =                             07910000
              (PV-YTD-GT-NO-OF-SCANS / PV-YTD-NO-OF-SCANS) * 100        07920000
              END-COMPUTE                                               07930000
              COMPUTE PV-YTD-NO-OF-SLAS   =                             07940000
                (100.00 - PV-T-YTD-NO-OF-SLAS)                          07950000
              END-COMPUTE                                               07960000
            END-IF                                                      07970000
      *                                                                 07980000
            MOVE PV-OPER-UNT-ID        TO PNA-STORE-NBR-X               07990000
            MOVE PV-DLY-NO-OF-SCANS    TO DLY-NO-OF-SCANS               08000000
            MOVE PV-DLY-GT-NO-OF-SCANS TO DLY-GT-NO-OF-SCANS            08010000
            MOVE PV-DLY-NO-OF-SLAS     TO DLY-NO-OF-SLAS                08020000
            MOVE PV-WTD-NO-OF-SCANS    TO WTD-NO-OF-SCANS               08030000
            MOVE PV-WTD-GT-NO-OF-SCANS TO WTD-GT-NO-OF-SCANS            08040000
            MOVE PV-WTD-NO-OF-SLAS     TO WTD-NO-OF-SLAS                08050000
            MOVE PV-MTD-NO-OF-SCANS    TO MTD-NO-OF-SCANS               08060000
            MOVE PV-MTD-GT-NO-OF-SCANS TO MTD-GT-NO-OF-SCANS            08070000
            MOVE PV-MTD-NO-OF-SLAS     TO MTD-NO-OF-SLAS                08080000
            MOVE PV-YTD-NO-OF-SCANS    TO YTD-NO-OF-SCANS               08090000
            MOVE PV-YTD-GT-NO-OF-SCANS TO YTD-GT-NO-OF-SCANS            08100000
            MOVE PV-YTD-NO-OF-SLAS     TO YTD-NO-OF-SLAS                08110000
            MOVE PNA-SLA-RPT-DETAIL-LINE                                08120000
                                       TO PV-PNA-SLA-RPT-CLAYOUT        08130000
            PERFORM 8100-WRITE-PNA-SLA-RPT                              08140000
            ADD +1 TO PV-DETAIL-LINE-COUNT                              08150000
            MOVE ZEROES                TO PV-OPER-UNT-ID                08160000
                                          PV-DLY-NO-OF-SCANS            08170000
                                          PV-DLY-GT-NO-OF-SCANS         08180000
                                          PV-DLY-NO-OF-SLAS             08190000
                                          PV-WTD-NO-OF-SCANS            08200000
                                          PV-WTD-GT-NO-OF-SCANS         08210000
                                          PV-WTD-NO-OF-SLAS             08220000
                                          PV-MTD-NO-OF-SCANS            08230000
                                          PV-MTD-GT-NO-OF-SCANS         08240000
                                          PV-MTD-NO-OF-SLAS             08250000
                                          PV-YTD-NO-OF-SCANS            08260000
                                          PV-YTD-GT-NO-OF-SCANS         08270000
                                          PV-YTD-NO-OF-SLAS             08280000
                                          PNA-STORE-NBR                 08290000
                                          DLY-NO-OF-SCANS               08300000
                                          DLY-GT-NO-OF-SCANS            08310000
                                          DLY-NO-OF-SLAS                08320000
                                          WTD-NO-OF-SCANS               08330000
                                          WTD-GT-NO-OF-SCANS            08340000
                                          WTD-NO-OF-SLAS                08350000
                                          MTD-NO-OF-SCANS               08360000
                                          MTD-GT-NO-OF-SCANS            08370000
                                          MTD-NO-OF-SLAS                08380000
                                          YTD-NO-OF-SCANS               08390000
                                          YTD-GT-NO-OF-SCANS            08400000
                                          YTD-NO-OF-SLAS.               08410000
      *                                                                 08420000
      ******************************************************************08430000
      * 9000-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE     *08440000
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                *08450000
      ******************************************************************08460000
      *                                                                 08470000
       9000-SQL-ABEND.                                                  08480000
      *                                                                 08490000
           IF SQLCODE EQUAL -904 OR -911 OR -913                        08500000
              MOVE +4050 TO RETURN-CODE                                 08510000
           ELSE                                                         08520000
              PERFORM 9100-ABEND-PROGRAM                                08530000
           END-IF.                                                      08540000
      *                                                                 08550000
       9100-ABEND-PROGRAM.                                              08560000
      *                                                                 08570000
              DISPLAY '** ABEND      **'.                               08580000
              DISPLAY '** PROGRAM    ** = SUKRP059'.                    08590000
              DISPLAY '** PARAGRAPH  ** = ' PV-PARAGRAPH-NAME.          08600000
              DISPLAY '** OPERATION  ** = ' PV-DB2-OPERATION-ATTEMPTED. 08610000
              COPY DPPD004.                                             08620000
              MOVE +4013 TO ABEND-CODE.                                 08630000
              CALL 'ILBOABN0' USING ABEND-CODE.                         08640000
