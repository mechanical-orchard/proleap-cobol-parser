       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUP011                                                  
       AUTHOR.        DORSEY JACKSON                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07-01-2002.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * INKUP011 - CALCULATES THE SHRINK PERCENTAGE                             
      ******************************************************************        
      *     THIS PROGRAM CALCULATES THE SHRINK PERCENTAGE FOR EACH      00290000
      *  RECORD READ, AND WRITES A RECORD TO THE OUTPUT FILE.           00330000
      ******************************************************************        
      ******************************************************************        
      ******************************************************************        
      *                        CHANGE LOG                              *        
      *  CHANGE ID    CHANGE DATE            REASON FOR CHANGE         *        
W26600*  ---------    -----------            -----------------         *        
W26600*  W26600       04-  -2004             STORE EXPANSION           *        
W26600*                                      M JOHNSON                 *        
W26600*                                                                *        
W33304*  W33304       12-16-2008    SATHISH/INFOSYS                    *        
W33304*                             HOLDING INVENTORY LEDGER OPEN(HILO)*        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT INP-DEPT-RANKING-FILE                                         
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUT-DEPT-PERCENT-FILE                                         
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INP-DEPT-RANKING-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS   0 RECORDS                                          
W26600*    RECORD CONTAINS 94 CHARACTERS                                        
           DATA RECORD IS INP-DEPT-RANKING-RECORD.                              
                                                                                
W33304 01  INP-DEPT-RANKING-RECORD       PIC X(126).                            
                                                                                
       FD  OUT-DEPT-PERCENT-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS   0 RECORDS                                          
W26600*    RECORD CONTAINS 97 CHARACTERS                                        
           DATA RECORD IS OUT-DEPT-PERCENT-RECORD.                              
                                                                                
W26600 01  OUT-DEPT-PERCENT-RECORD        PIC X(120).                           
                                                                                
       EJECT                                                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      *****************************************************************         
      ***************************************************************** 00120000
      *  STORE SHORTAGE RECORD LAYOUT - INPUT.                          00130000
      ***************************************************************** 00140000
           COPY INRD003I.                                               00000400
                                                                                
      *****************************************************************         
      ***************************************************************** 00120000
      *  STORE SHORTAGE RECORD LAYOUT - OUTPUT.                         00130000
      ***************************************************************** 00140000
           COPY INRD002O.                                               00000400
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME            PIC X(08) VALUE 'INKUP011'.           
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-INPUT-EOF-SW            PIC X(1)  VALUE 'N'.                  
               88  PS-INPUT-EOF                     VALUE 'Y'.                  
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PERCENT                 PIC S9(3)V99 VALUE ZEROES             
                                                       COMP-3.                  
           05  PV-PERCENTX                PIC S9(6)V9999 VALUE ZEROES      CL**1
                                                       COMP-3.                  
           05  PV-MAXPCT                  PIC S9(3)V99 VALUE 999.99.            
                                                                                
       EJECT                                                                    
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-INKUP011.                                                           
                                                                                
      ****************************************************************          
      *    OPEN ALL FILES.                                                      
      *    PROCESS AND WRITE OUTPUT RECORDS                                     
      *    CLOSE ALL FILES.                                                     
      ****************************************************************          
           OPEN INPUT  INP-DEPT-RANKING-FILE                                    
                OUTPUT OUT-DEPT-PERCENT-FILE.                                   
                                                                                
           PERFORM 1000-INITIALIZE-HEADER.                                      
                                                                                
           PERFORM 2000-PROCESS                                                 
               UNTIL PS-INPUT-EOF.                                              
                                                                                
           CLOSE INP-DEPT-RANKING-FILE                                          
                 OUT-DEPT-PERCENT-FILE.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
       EJECT                                                                    
                                                                                
       1000-INITIALIZE-HEADER.                                                  
                                                                                
      ****************************************************************          
      *    READ FIRST STORE RECORD.                                             
      *                                                                         
      *    PERFORMED BY 0000-INKUP011.                                          
      ****************************************************************          
           PERFORM 4000-READ-RANKING-RECORD.                                    
                                                                                
       EJECT                                                                    
                                                                                
       2000-PROCESS.                                                            
                                                                                
      ****************************************************************          
      *    PRINT DETAIL FOR THE CURRENT STORE RECORD AND                        
      *    READ THE NEXT INPUT RECORD.                                          
      *                                                                         
      *    PERFORMED BY 0000-INKUP011.                                          
      ****************************************************************          
                                                                                
           MOVE IN003I-STORE-SHORTAGE-REC TO IN002O-STORE-SHORTAGE-REC          
           IF IN003I-TOTAL-SALES = 0                                            
               MOVE PV-MAXPCT   TO PV-PERCENTX                                  
               MOVE PV-PERCENTX TO PV-PERCENT                                   
               MOVE PV-PERCENT  TO IN002O-SHRINK-PER                            
           ELSE                                                                 
               COMPUTE PV-PERCENTX =                                            
                ((IN003I-SHORT-RETAIL / IN003I-TOTAL-SALES) * 100)          0000
                 ON SIZE ERROR                                                  
                   MOVE 999.99  TO PV-PERCENTX                                  
               END-COMPUTE                                                      
               MOVE PV-PERCENTX TO PV-PERCENT                                   
               MOVE PV-PERCENT  TO IN002O-SHRINK-PER                            
           END-IF                                                           0000
                                                                            0000
           IF PV-PERCENTX > 999.99                                          0000
               MOVE PV-MAXPCT  TO PV-PERCENT                                    
               MOVE PV-PERCENT TO IN002O-SHRINK-PER                             
           ELSE                                                             0000
               IF PV-PERCENTX < -999.99                                     0000
                   COMPUTE PV-PERCENT = PV-MAXPCT * -1                      0000
                   END-COMPUTE                                              0000
                   MOVE PV-PERCENT  TO IN002O-SHRINK-PER                        
               ELSE                                                         0000
                   MOVE PV-PERCENTX TO PV-PERCENT                           0000
                   MOVE PV-PERCENT  TO IN002O-SHRINK-PER                        
               END-IF                                                   00002600
           END-IF                                                       00002600
                                                                            0000
           IF IN003I-SHORT-RETAIL > 0                                   00002600
              IF PV-PERCENT < 0                                         00002600
                  COMPUTE PV-PERCENT = (PV-PERCENT * -1)                        
                  END-COMPUTE                                                   
                  MOVE PV-PERCENT TO IN002O-SHRINK-PER                          
              END-IF                                                    00002600
           ELSE                                                         00002600
              IF IN003I-SHORT-RETAIL < 0                                00002600
                  IF PV-PERCENT > 0                                     00002600
                      COMPUTE PV-PERCENT = (PV-PERCENT * -1)                    
                      END-COMPUTE                                               
                      MOVE PV-PERCENT TO IN002O-SHRINK-PER                      
                  END-IF                                                00002600
              END-IF                                                    00002600
           END-IF.                                                      00002600
           MOVE ZEROES TO PV-PERCENT                                            
                          PV-PERCENTX.                                          
           PERFORM Z200-WRITE-OUTPUT-FILE                                       
           PERFORM 4000-READ-RANKING-RECORD.                                    
                                                                                
       4000-READ-RANKING-RECORD.                                                
      ****************************************************************          
      *    READ THE STORE SHORTAGE FILE                                         
      *                                                                         
      *    PERFORMED BY 1000-INITIALIZE-HEADER.                                 
      *    PERFORMED BY 2000-PROCESS.                                           
      ****************************************************************          
           READ INP-DEPT-RANKING-FILE INTO IN003I-STORE-SHORTAGE-REC            
               AT END                                                           
                   SET PS-INPUT-EOF TO TRUE.                                    
                                                                                
       Z200-WRITE-OUTPUT-FILE.                                                  
      ****************************************************************          
      *    WRITE STORE SHORTAGE OUTPUT - PERCENT-FILE                           
      *                                                                         
      *    PERFORMED BY 2000-PROCESS.                                           
      ****************************************************************          
           WRITE OUT-DEPT-PERCENT-RECORD FROM                                   
                 IN002O-STORE-SHORTAGE-REC.                                     
           EJECT                                                                
                                                                                
