       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKRP120.                                            
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       05/02/2001.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:                                                                 
      *    THE PROGRAM WILL CREATE A UNIT BOOKING BYPASSED AUDIT TRAIL          
      * REPORT FROM THE BOOKING ADJUSTMENTS BYPASSED FILE. THE DATASET          
      * IS CREATED IN THE INK365. THE REPORT(S) REPRESENT SKUS WHERE A          
      * ZERO ADJUSTMENT WOULD HAVE BEEN APPLIED TO FASHION SALES. THE           
      * REPORT TOTALS BYPASSED RECORDS CREATED IN INKUT118.                     
RB0201******************************************************************        
RB0201* CHANGE LOG                                                              
RB0201* DATE       WHO     CHANGE DESC                                          
RB0201* ---------- ------- -----------------------------------------            
RB0201* 10-17-2001 R.BUTZ  INCREASE LRECL OF INP01 FROM 60 TO 70                
RB0201*                    AND INCLUDE THE IN-TRANSIT QUANTITY                  
RB0201*                    IN THE REPORT                                        
W21732******************************************************************        
W21732* CHANGE LOG                                                              
W21732* DATE       WHO           CHANGE DESC                                    
W21732* ---------- -------       ---------------------------------------        
W21732* 05-12-2002 ASHOK,IGSI    NON-INTELLIGENT SKU CONVERSION.                
W21732*                          INWS016 WAS INCREASED IN SIZE TO               
W21732*                          TO 80 BYTES.                                   
W26600* W26600   08-01-2004  ROD JANSSEN INVENTORY STORE EXPANSION              
W26006* 02-24-2005 ERIN SEARL    REMOVE REFERENCES TO OBSOLETE                  
W26006*                          STORE TABLE.                                   
R26006* W26006   08-24-2005  R. MCDONALD - CORRECT STORE EXPECTED               
R26006*                          TOTALS ON THE LAST PAGE                        
W27808* W27808   08-31-2005  SCOTT JACKSON - ADD CONTROL CARD AND LOGIC         
W27808*                          TO PROCESS BYPASSED OR EXCLUDED RECORDS        
W34286* W34286  02-20-2008  T KONTAROVICH - UPDATE LOGIC TO CREATE THE          
W34286*                         REPORT FOR VARINCE BYPASSED RECORDS.            
PK1383* PKE1383 07-30-2009  PRITHIVIRAJ S -CORRECTED THE CODE TO DISPLAY        
PK1383*                       THE FOUR DIGIT STORE NUMBER IN THE REPORT.        
P40463*PRB40463 22-03-2013  INCLUDED 'WITH UR' IN THE QUERIES                   
P40463******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT IN-BYPASSED-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
W27808     SELECT CONTROL-FILE                                                  
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT OUT-SUMMARY-RPT                                               
               ASSIGN TO RPT01.                                                 
                                                                                
           SELECT OUT-DETAIL-RPT                                                
               ASSIGN TO RPT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  IN-BYPASSED-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           DATA RECORD IS IN016-PHYSICAL-COUNT-RECORD.                          
W21732 01  IN-BYPASSED-RECORD       PIC X(80).                                  
                                                                                
W27808 FD  CONTROL-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  CONTROL-REC              PIC X(80).                                  
                                                                                
       FD  OUT-SUMMARY-RPT                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS OMITTED                                                
           BLOCK CONTAINS  0 RECORDS                                            
           DATA RECORD IS OUT-SUMMARY-RECORD.                                   
       01  OUT-SUMMARY-RECORD            PIC X(132).                            
                                                                                
       FD  OUT-DETAIL-RPT                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS OMITTED                                                
           BLOCK CONTAINS  0 RECORDS                                            
           DATA RECORD IS OUT-DETAIL-RECORD.                                    
       01  OUT-DETAIL-RECORD             PIC X(132).                            
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
W26600     COPY INWS016.                                                        
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PC-PRINT-CONTROL.                                                    
           05  PC-PAGE-COUNT             PIC S9(05) VALUE ZERO.                 
           05  PC-LINE-COUNT             PIC S9(03) VALUE ZERO COMP-3.          
           05  PC-LINE-SPACE             PIC S9(05) VALUE ZERO.                 
           05  PC-PAGE-COUNT2            PIC S9(05) VALUE ZERO.                 
           05  PC-LINE-COUNT2            PIC S9(03) VALUE ZERO COMP-3.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARA                   PIC X(30)  VALUE SPACES.               
           05  PV-DB2-OPER-ATTEMPTED     PIC X(30)  VALUE SPACES.               
W21732     05  PV-TAB-MAX                PIC 9(04)  VALUE 999.                  
W26600     05  MAX-STORES                PIC 9(05) COMP VALUE 10000.            
W26600     05  PV-STR-NDX                PIC S9(04) COMP VALUE ZEROS.           
W21732     05  PV-DEPT-SUB               PIC 9(05)  VALUE ZEROES.               
           05  PV-COMPUTED-FIELDS.                                              
               10  PV-ADJUSTMENT-QTY     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-OLD-ONHAND-QTY     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-QTY                PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-AFTER-RCPT         PIC S9(15) COMP-3 VALUE ZEROS.         
RB0201         10  PV-CUTOFF-INTRANS     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-CUTOFF-STR-EXPTED  PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-STR-EXPTED         PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-STR-EXPECTED-CUTOFF                                       
                                         PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TOT-ADJUSTMENT-QTY PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TOT-OLD-ONHAND-QTY PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TOT-QTY            PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TOT-AFTER-RCPT     PIC S9(15) COMP-3 VALUE ZEROS.         
RB0201         10  PV-TOT-CUTOFF-INTRANS PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TOT-CUTOFF-STR-EXPTED                                     
                                         PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-WRK-ADJUSTMENT-QTY PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-DTL-ADJUSTMENT-QTY PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-DTL-OLD-ONHAND-QTY PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-DTL-QTY            PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-DTL-AFTER-RCPT     PIC S9(15) COMP-3 VALUE ZEROS.         
RB0201         10  PV-DTL-CUTOFF-INTRANS PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-DTL-STR-EXPTED     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-DTL-CUTOFF-STR-EXPTED                                     
                                         PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TB-STR-EXPTED      PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TB-STR-CUTOFF-STR-EXPTED                                  
                                         PIC S9(15) COMP-3 VALUE ZEROS.         
W21732     05  PV-DEPT-NMBR              PIC X(04).                             
W21732     05  PV-DEPT-NMBR-NUM REDEFINES                                       
W21732         PV-DEPT-NMBR              PIC 9(04).                             
W26600     05  PV-STORE-NMBR             PIC X(04).                             
           05  PV-STORE-NMBR-NUM REDEFINES                                      
W26600         PV-STORE-NMBR             PIC 9(04).                             
W26600     05  PV-STORE-NMBR-SAVE        PIC X(04).                             
W26600     05  PV-STORE-NMBR-SV  REDEFINES                                      
W26600         PV-STORE-NMBR-SAVE        PIC 9(04).                             
W26600     05  PV-LOC-NBR                PIC 9(04).                             
W26600     05  PV-LOC-NBR-X REDEFINES                                           
W26600         PV-LOC-NBR                PIC X(04).                             
                                                                                
       01  SWITCHES.                                                            
           05  WS-BYPASS-DONE-SW         PIC X(01) VALUE 'N'.                   
               88  BYPASS-PROCESSING-COMPLETE      VALUE 'Y'.                   
           05  WS-PRINT-SW               PIC X(01) VALUE 'N'.                   
               88  PRINT-INFO                      VALUE 'Y'.                   
           05  WS-FIRST-TIME-SW          PIC X(01) VALUE 'N'.                   
               88  FIRST-TIME-THRU                 VALUE 'N'.                   
               88  CONTINUED-PROCESSING            VALUE 'Y'.                   
                                                                                
       01  TB-DEPT-TABLE.                                                       
W21732     05 TB-DEPT-TAB                OCCURS 10000 TIMES.                    
              10  TB-ADJUSTMENT-QTY      PIC S9(15) COMP-3 VALUE ZEROS.         
              10  TB-QTY                 PIC S9(15) COMP-3 VALUE ZEROS.         
              10  TB-OLD-ONHAND-QTY      PIC S9(15) COMP-3 VALUE ZEROS.         
              10  TB-AFTER-RCPT          PIC S9(15) COMP-3 VALUE ZEROS.         
              10  TB-PROCESS-SW          PIC X(01)         VALUE SPACE.         
RB0201        10  TB-CUTOFF-INTRANS      PIC S9(15) COMP-3 VALUE ZEROS.         
              10  TB-CUTOFF-STR-EXPTED   PIC S9(15) COMP-3 VALUE ZEROS.         
                                                                                
       01  TB-STORE-ACCUM-TABLE.                                                
W26600     05  TB-STR-ELEMENTS           OCCURS 10000 TIMES.                    
               10  TB-STR-ADJUSTMENT-QTY PIC S9(15) COMP-3 VALUE ZEROS.         
               10  TB-STR-QTY            PIC S9(15) COMP-3 VALUE ZEROS.         
               10  TB-STR-OLD-ONHAND     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  TB-STR-AFTER-RCPT     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  TB-STR-PROCESS-SW     PIC X(01)         VALUE SPACE.         
RB0201         10  TB-STR-CUTOFF-INTRANS PIC S9(15) COMP-3 VALUE ZEROS.         
               10  TB-STR-CUTOFF-STR-EXPTED                                     
                                         PIC S9(15) COMP-3 VALUE ZEROS.         
                                                                                
       01  CC-CONTROL-RECORD.                                                   
           05  CC-REPORT-CONTROL         PIC X(08).                             
               88  CC-BYPASSED-RUN                  VALUE 'BYPASSED'.           
               88  CC-EXCLUDED-RUN                  VALUE 'EXCLUDED'.           
           05  FILLER                    PIC X(72)  VALUE SPACES.               
                                                                                
       01  CPU-DATE.                                                            
           05  CPU-YEAR                  PIC X(02)  VALUE SPACES.               
           05  CPU-MONTH                 PIC X(02)  VALUE SPACES.               
           05  CPU-DAY                   PIC X(02)  VALUE SPACES.               
                                                                                
       01  CPU-TIME.                                                            
           05  CPU-HOUR                  PIC X(02)  VALUE SPACES.               
           05  CPU-MINUTE                PIC X(02)  VALUE SPACES.               
           05  FILLER                    PIC X(02)  VALUE SPACES.               
                                                                                
       01  H1-HEADING.                                                          
           05  FILLER              PIC X(40)   VALUE SPACES.                    
W27808     05  FILLER              PIC X(20)   VALUE                            
W27808        '    UNIT BOOKINGS - '.                                           
W27808     05  H1-REPORT-TYPE      PIC X(08)   VALUE SPACES.                    
W27808     05  FILLER              PIC X(24)   VALUE                            
W27808        ' COUNTS AUDIT REPORT    '.                                       
                                                                                
       01  H1A-HEADING.                                                         
           05  FILLER              PIC X(40)   VALUE SPACES.                    
W27808     05  FILLER              PIC X(16)   VALUE                            
W27808        'UNIT BOOKINGS - '.                                               
W27808     05  H1A-REPORT-TYPE     PIC X(08)   VALUE SPACES.                    
W27808     05  FILLER              PIC X(28)   VALUE                            
W27808        ' COUNTS DETAIL AUDIT REPORT '.                                   
                                                                                
       01  H1B-HEADING.                                                         
           05  FILLER              PIC X(56)   VALUE SPACES.                    
           05  FILLER              PIC X(06)   VALUE 'STORE '.                  
           05  FILLER              PIC X(01)   VALUE SPACES.                    
W26600     05  H1B-STORE           PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  H1B-STORE-NAME      PIC X(13)   VALUE SPACES.                    
                                                                                
W34286 01  H1-VAR-HEADING.                                                      
W34286     05  FILLER              PIC X(44)   VALUE SPACES.                    
W34286     05  FILLER              PIC X(11)   VALUE                            
W34286        'VARIANCE - '.                                                    
W34286     05  H1-VAR-REPORT-TYPE  PIC X(08)   VALUE SPACES.                    
W34286     05  FILLER              PIC X(28)   VALUE                            
W34286        ' COUNTS AUDIT REPORT    '.                                       
W34286                                                                          
W34286 01  H1A-VAR-HEADING.                                                     
W34286     05  FILLER              PIC X(44)   VALUE SPACES.                    
W34286     05  FILLER              PIC X(11)   VALUE                            
W34286        'VARIANCE - '.                                                    
W34286     05  H1A-VAR-REPORT-TYPE PIC X(08)   VALUE SPACES.                    
W34286     05  FILLER              PIC X(28)   VALUE                            
W34286        ' COUNTS DETAIL AUDIT REPORT '.                                   
W34286                                                                          
       01  H2-HEADING.                                                          
RB0201     05  FILLER               PIC X(04)     VALUE 'DEPT'.                 
RB0201     05  FILLER               PIC X(14)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(08)     VALUE 'PHYSICAL'.             
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(09)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE 'CUTOFF'.               
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(05)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(10)     VALUE 'POST AFTER'.           
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(05)     VALUE SPACES.                 
W27808     05  FILLER               PIC X(10)     VALUE 'STR-EXPECT'.           
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(02)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(13)     VALUE 'FASHION SALES'.        
RB0201     05  FILLER               PIC X(11)     VALUE SPACES.                 
                                                                                
       01  H2B-HEADING.                                                         
RB0201     05  FILLER               PIC X(05)     VALUE 'STORE'.                
RB0201     05  FILLER               PIC X(13)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(08)     VALUE 'PHYSICAL'.             
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(09)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE 'CUTOFF'.               
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(05)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(10)     VALUE 'POST AFTER'.           
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(05)     VALUE SPACES.                 
W27808     05  FILLER               PIC X(10)     VALUE 'STR-EXPECT'.           
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(02)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(13)     VALUE 'FASHION SALES'.        
RB0201     05  FILLER               PIC X(10)     VALUE SPACES.                 
                                                                                
       01  H3-HEADING.                                                          
RB0201     05  FILLER               PIC X(04)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(14)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(08)     VALUE 'QUANTITY'.             
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(09)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE 'ONHAND'.               
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(05)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(10)     VALUE 'FLUSH RCPT'.           
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(09)     VALUE 'QUANTITY'.             
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(04)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(11)     VALUE 'ADJUSTMENT'.           
RB0201     05  FILLER               PIC X(10)     VALUE SPACES.                 
                                                                                
                                                                                
       01  H4-HEADING.                                                          
RB0201     05  FILLER               PIC X(05)     VALUE 'STORE'.                
W21732     05  FILLER               PIC X(08)     VALUE SPACES.                 
W21732     05  FILLER               PIC X(03)     VALUE 'SKU'.                  
W21732     05  FILLER               PIC X(14)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(08)     VALUE 'PHYSICAL'.             
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(09)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE 'CUTOFF'.               
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(05)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(10)     VALUE 'POST AFTER'.           
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(05)     VALUE SPACES.                 
W27808     05  FILLER               PIC X(10)     VALUE 'STR EXPECT'.           
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(02)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(13)     VALUE 'FASHION SALES'.        
RB0201     05  FILLER               PIC X(04)     VALUE SPACES.                 
                                                                                
       01  H5-HEADING.                                                          
RB0201     05  FILLER               PIC X(05)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(02)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(04)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(02)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(05)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(02)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(03)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(07)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(08)     VALUE 'QUANTITY'.             
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(09)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(06)     VALUE 'ONHAND'.               
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(05)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(10)     VALUE 'FLUSH RCPT'.           
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(05)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(10)     VALUE 'QUANTITY'.             
RB0201     05  FILLER               PIC X(06)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(02)     VALUE SPACES.                 
RB0201     05  FILLER               PIC X(13)     VALUE 'ADJUSTMENT'.           
RB0201     05  FILLER               PIC X(04)     VALUE SPACES.                 
                                                                                
       01  C1-COMMENT.                                                          
W26600     05  C1-STORE-NBR        PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE                            
               'STR TTL **'.                                                    
                                                                                
       01  D1-DETAIL-LINE.                                                      
W21732     05  D1-DEPT-NBR          PIC 9999.                                   
W21732     05  D1-DEPT-NBR-1 REDEFINES D1-DEPT-NBR.                             
W21732         10 D1-DEPT-1         PIC X(01).                                  
W21732         10 D1-DEPT-2         PIC X(03).                                  
           05  FILLER               PIC X(01)        VALUE SPACES.              
           05  D1-COMMENT           PIC X(14)        VALUE SPACES.              
W21732     05  FILLER               PIC X(05)        VALUE SPACES.              
           05  D1-QTY               PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
           05  FILLER               PIC X(06)        VALUE SPACES.              
           05  D1-ONHAND-QTY        PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
           05  FILLER               PIC X(06)        VALUE SPACES.              
           05  D1-AFTER-RCPT        PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
           05  FILLER               PIC X(06)        VALUE SPACES.              
RB0201     05  D1-CUTOFF-STR-EXPTED PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
RB0201     05  FILLER               PIC X(06)        VALUE SPACES.              
           05  D1-ADJUSTMENT-QTY    PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
                                                                                
       01  D2-DETAIL-LINE.                                                      
W26600     05  D2-STORE-NBR         PIC Z999.                                   
W26600     05  FILLER               PIC X(03)        VALUE SPACES.              
W21732     05  D2-SKU-NBR.                                                      
W21732         10  FILLER     PIC XXX VALUE SPACES.                             
W21732         10  D2-SKU     PIC X(08).                                        
W21732         10  FILLER     PIC X(06) VALUE SPACES.                           
W21732     05  D2-COMMENT REDEFINES D2-SKU-NBR  PIC X(17).                      
RB0201     05  FILLER               PIC X(06)        VALUE SPACES.              
           05  D2-QTY               PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
           05  FILLER               PIC X(06)        VALUE SPACES.              
           05  D2-ONHAND-QTY        PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
           05  FILLER               PIC X(06)        VALUE SPACES.              
           05  D2-AFTER-RCPT        PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
           05  FILLER               PIC X(06)        VALUE SPACES.              
RB0201     05  D2-CUTOFF-STR-EXPTED PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
RB0201     05  FILLER               PIC X(06)        VALUE SPACES.              
           05  D2-ADJUSTMENT-QTY    PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
                                                                                
      /****************************************************************         
      * COPY FOR STANDARD HEADING                                               
      *****************************************************************         
           COPY DPWS133O.                                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * DB2 STORE TABLE DCLGEN.                                                 
      *****************************************************************         
           EXEC SQL                                                             
W26600         INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 MERCHANDISE DEPARTMENT                                              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      *FLOW:    SEQUENTIALLY READ THE UNIT BOOKINGS BYPASSED FILE.              
      *      ACCUMULATE TOTALS FOR PHYSICAL INVENTORY, CUTOFF ONHAND,           
      *      POST AFTER FLUSH RECEIPTS, AND FASHION SALES ADJUSTMENTS.          
      *      FASHION SALES ADJUSTMENTS ARE CALCULATED FROM THE LATER.           
      *      NUMBERS ARE ACCUMULATED BY DEPARTMENT FOR A GIVEN STORE.           
      *                                                                         
      *         WHEN A STORE BREAK OCCURS, PRINT A DETAIL LINE FOR EACH         
      *      DEPARTMENT BY DUMPING THE CONTENTS OF THE DEPARTMENT TABLE         
      *      FOR THE STORE BEING PROCESSED. TOTAL UP FOR THE STORE.             
      *                                                                         
      *         THE MODULE GENERATES TWO REPORTS. A SUMMARIZED AUDIT            
      *      REPORT AND A DETAIL AUDIT REPORT. AS WE ACCUMULATE FOR THE         
      *      SUMMARY, WRITE A DETAIL LINE FOR EACH SKU BEING PROCESSED.         
      *                                                                         
      *         AT END OF FILE, PROVIDE COMPANY TOTALS; AS WELL, AS A           
      *      STORE LEVEL SUMMARY REPORT FOR REPORT 1.                           
      *                                                                         
      *         TABLES USED IN THE REPORT.                                      
W26600*      XMT_LOC - LOCATION TABLE.                                          
      *                                                                         
      *****************************************************************         
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM B200-PRINT-REPORT                                            
               UNTIL BYPASS-PROCESSING-COMPLETE.                                
                                                                                
           PERFORM B600-EOJ.                                                    
                                                                                
       A999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /****************************************************************         
      * B100-HOUSEKEEPING.......                                                
      *****************************************************************         
       B100-HOUSEKEEPING.                                                       
           MOVE '*B100-HOUSEKEEPING.              *' TO PV-PARA                 
                                                                                
           OPEN  INPUT IN-BYPASSED-FILE                                         
W27808                 CONTROL-FILE                                             
                OUTPUT OUT-SUMMARY-RPT                                          
                OUTPUT OUT-DETAIL-RPT.                                          
                                                                                
           ACCEPT CPU-DATE FROM DATE.                                           
           MOVE CPU-YEAR   TO DP133O-RUN-YEAR.                                  
           MOVE CPU-MONTH  TO DP133O-RUN-MONTH.                                 
           MOVE CPU-DAY    TO DP133O-RUN-DAY.                                   
                                                                                
           ACCEPT CPU-TIME FROM TIME.                                           
           MOVE CPU-HOUR   TO DP133O-RUN-HOUR.                                  
           MOVE CPU-MINUTE TO DP133O-RUN-MINUTE.                                
                                                                                
           MOVE 'INKRP120' TO DP133O-PROGRAM-NAME.                              
           MOVE ZEROS      TO PC-PAGE-COUNT.                                    
           MOVE ZEROS      TO PC-PAGE-COUNT2.                                   
                                                                                
           MOVE 'N'        TO WS-BYPASS-DONE-SW.                                
           MOVE 'N'        TO WS-FIRST-TIME-SW.                                 
                                                                                
           INITIALIZE TB-DEPT-TABLE.                                            
           INITIALIZE TB-STORE-ACCUM-TABLE.                                     
           INITIALIZE IN016-PHYSICAL-COUNT-RECORD.                              
                                                                                
W27808     PERFORM Z100-READ-CONTROL-FILE.                                      
           PERFORM Z200-READ-BYPASSED-FILE.                                     
                                                                                
           PERFORM Z810-PRINT-HEADINGS.                                         
                                                                                
      /****************************************************************         
      * B200-PRINT-REPORT.                                                      
      *     READ THE UNIT BOOKINGS BYPASSED FILE UNTIL END OF FILE.             
      * GENERATE THE DETAIL PHYSICAL INVENTORY AUDIT REPORT. ACCUMULATE         
      * NUMBER BY DEPARTMENT FOR THE STORE BEING PROCESSED. WHEN A STORE        
      * BREAK OCCURS, PRINT THE STORE'S SUMMARIZED PHYSICAL INVENTORY           
      * AUDIT REPORT BY DEPARTMENT. PROVIDE TOTALS FOR THE STORE. INIT          
      * ACCUMULATORS AND RESET SWITCHES FOR THE NEXT STORE.                     
      *****************************************************************         
       B200-PRINT-REPORT.                                                       
           MOVE '*B200-PRINT-REPORT.              *' TO PV-PARA                 
                                                                                
           IF IN016-STORE NOT EQUAL PV-STORE-NMBR-SAVE                          
             IF PRINT-INFO                                                      
               PERFORM Z800-PRINT-HEADINGS                                      
               PERFORM B400-PRINT-DETAIL-LINE                                   
                   VARYING PV-DEPT-SUB FROM 1 BY 1                              
                   UNTIL PV-DEPT-SUB GREATER PV-TAB-MAX                         
               PERFORM B410-PRINT-STORE-TOTAL                                   
               MOVE 'N'       TO WS-PRINT-SW                                    
             END-IF                                                             
             MOVE IN016-STORE TO PV-STORE-NMBR-SAVE                             
             INITIALIZE TB-DEPT-TABLE                                           
             MOVE ZEROS       TO PV-ADJUSTMENT-QTY                              
             MOVE ZEROS       TO PV-OLD-ONHAND-QTY                              
             MOVE ZEROS       TO PV-QTY                                         
             MOVE ZEROS       TO PV-AFTER-RCPT                                  
RB0201       MOVE ZEROS       TO PV-CUTOFF-INTRANS                              
             MOVE ZEROS       TO PV-CUTOFF-STR-EXPTED                           
           END-IF.                                                              
                                                                                
           PERFORM C100-ACCUM-DEPT-TOTALS.                                      
                                                                                
           PERFORM B420-PRINT-DETAIL-LINE-2.                                    
                                                                                
           PERFORM Z200-READ-BYPASSED-FILE.                                     
                                                                                
      /****************************************************************         
      * B400-PRINT-DETAIL-LINE.                                                 
      *     PRINT THE SUMMARIZED INVENTORY AUDIT REPORT FOR THE STORE           
      * BEING PROCESSED. THE SUBSCRIPT REPRESENTS THE ACTUAL DEPARTMENT.        
      * IF MORE THAN 999 DEPARTMENTS EXIST, WE WILL NEED TO EXPAND THE          
      * TABLES.                                                                 
      *****************************************************************         
       B400-PRINT-DETAIL-LINE.                                                  
           MOVE '*B400-PRINT-DETAIL-LINE.         *' TO PV-PARA                 
                                                                                
           IF TB-PROCESS-SW (PV-DEPT-SUB) GREATER SPACE                         
               MOVE SPACES                          TO D1-DETAIL-LINE           
               MOVE PV-DEPT-SUB                     TO D1-DEPT-NBR              
               MOVE TB-ADJUSTMENT-QTY (PV-DEPT-SUB) TO D1-ADJUSTMENT-QTY        
               MOVE TB-QTY (PV-DEPT-SUB)            TO D1-QTY                   
               MOVE TB-OLD-ONHAND-QTY (PV-DEPT-SUB) TO D1-ONHAND-QTY            
               MOVE TB-AFTER-RCPT (PV-DEPT-SUB)     TO D1-AFTER-RCPT            
21591          MOVE TB-CUTOFF-STR-EXPTED(PV-DEPT-SUB)                           
                                                TO D1-CUTOFF-STR-EXPTED         
                                                                                
               IF PC-LINE-COUNT > +59                                           
                   PERFORM Z800-PRINT-HEADINGS                                  
               END-IF                                                           
                                                                                
               MOVE D1-DETAIL-LINE  TO OUT-SUMMARY-RECORD                       
               MOVE 1               TO PC-LINE-SPACE                            
               PERFORM Z700-WRITE-OUT-SUMMARY-RECORD                            
               ADD 1                TO PC-LINE-COUNT                            
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * B410-PRINT-STORE-TOTAL.                                                 
      *****************************************************************         
       B410-PRINT-STORE-TOTAL.                                                  
           MOVE '*B410-PRINT-STORE-TOTAL.         *' TO PV-PARA                 
                                                                                
           MOVE SPACES                   TO D1-DETAIL-LINE.                     
W26600     MOVE XMT00001-LOC-NBR         TO C1-STORE-NBR.                       
           MOVE C1-COMMENT               TO D1-COMMENT.                         
           MOVE PV-ADJUSTMENT-QTY        TO D1-ADJUSTMENT-QTY.                  
           MOVE PV-OLD-ONHAND-QTY        TO D1-ONHAND-QTY.                      
           MOVE PV-QTY                   TO D1-QTY.                             
           MOVE PV-AFTER-RCPT            TO D1-AFTER-RCPT.                      
           MOVE PV-CUTOFF-STR-EXPTED     TO D1-CUTOFF-STR-EXPTED.               
                                                                                
           IF PC-LINE-COUNT > +59                                               
               PERFORM Z800-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           ADD PV-ADJUSTMENT-QTY         TO PV-TOT-ADJUSTMENT-QTY.              
           ADD PV-OLD-ONHAND-QTY         TO PV-TOT-OLD-ONHAND-QTY.              
           ADD PV-QTY                    TO PV-TOT-QTY.                         
           ADD PV-AFTER-RCPT             TO PV-TOT-AFTER-RCPT.                  
           ADD PV-CUTOFF-INTRANS         TO PV-TOT-CUTOFF-INTRANS.              
           ADD PV-CUTOFF-STR-EXPTED      TO PV-TOT-CUTOFF-STR-EXPTED.           
                                                                                
           MOVE D1-DETAIL-LINE           TO OUT-SUMMARY-RECORD.                 
           MOVE 2                        TO PC-LINE-SPACE.                      
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 2                         TO PC-LINE-COUNT.                      
                                                                                
      /****************************************************************         
      * B420-PRINT-DETAIL-LINE-2.                                               
      *     PRINT THE DETAILS INVENTORY AUDIT REPORT. THE REPORT DEVELOP        
      * REPORT TWO. IT WILL CONTAIN A DETAIL LINE FOR EACH STORE/SKU            
      * BEING PROCESSED.                                                        
      *****************************************************************         
       B420-PRINT-DETAIL-LINE-2.                                                
           MOVE '*B420-PRINT-DETAIL-LINE-2.       *' TO PV-PARA.                
                                                                                
           MOVE SPACES                TO D2-DETAIL-LINE.                        
           MOVE IN016-STORE           TO D2-STORE-NBR.                          
W21732     MOVE IN016-SKU             TO D2-SKU.                                
                                                                                
           MOVE PV-WRK-ADJUSTMENT-QTY TO D2-ADJUSTMENT-QTY.                     
           MOVE IN016-QTY             TO D2-QTY.                                
           MOVE IN016-OLD-ONHAND-QTY  TO D2-ONHAND-QTY.                         
           MOVE IN016-AFTER-RECEIPTS  TO D2-AFTER-RCPT.                         
21591 *    MOVE IN016-CUTOFF-INTRANSIT                                          
21591 *                               TO D2-CUTOFF-INTRANS.                     
           COMPUTE PV-STR-EXPECTED-CUTOFF =                                     
                 (IN016-STR-EXP-OH-QTY - IN016-CUTOFF-INTRANSIT)                
           MOVE PV-STR-EXPECTED-CUTOFF TO D2-CUTOFF-STR-EXPTED.                 
                                                                                
           ADD PV-WRK-ADJUSTMENT-QTY  TO PV-DTL-ADJUSTMENT-QTY.                 
           ADD IN016-QTY              TO PV-DTL-QTY.                            
           ADD IN016-OLD-ONHAND-QTY   TO PV-DTL-OLD-ONHAND-QTY.                 
           ADD IN016-AFTER-RECEIPTS   TO PV-DTL-AFTER-RCPT.                     
21591 *    ADD IN016-CUTOFF-INTRANSIT TO PV-DTL-CUTOFF-INTRANS.                 
           COMPUTE PV-DTL-STR-EXPTED =                                          
                 (IN016-STR-EXP-OH-QTY - IN016-CUTOFF-INTRANSIT)                
           ADD PV-DTL-STR-EXPTED TO PV-DTL-CUTOFF-STR-EXPTED                    
                                                                                
           IF PC-LINE-COUNT2 > +59                                              
               PERFORM Z810-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           MOVE D2-DETAIL-LINE        TO OUT-DETAIL-RECORD.                     
           MOVE 1                     TO PC-LINE-SPACE.                         
           PERFORM Z710-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1                      TO PC-LINE-COUNT2.                        
                                                                                
      /****************************************************************         
      * B600-EOJ.                                                               
      *     PRINT THE SUMMARIZED INVENTORY AUDIT TRAIL REPORT, BY DEPT,         
      * FOR THE LAST STORE PROCESSED.                                           
      *     GENERATE A TOTAL PAGE FOR ALL STORES.                               
      *     GENERATE A SUMMARY PAGE, BY STORE NUMBER, OF THE INFORMATION        
      * CREATED BY DEPARTMENT. TOTAL UP FOR ALL STORES.                         
      *     TOTAL UP FOR THE DETAILS INVENTORY AUDIT TRAIL REPORT.              
      *     CLOSE THE FILES.                                                    
      *****************************************************************         
       B600-EOJ.                                                                
      *** FINAL TOTAL UP......                                                  
                                                                                
           IF PRINT-INFO                                                        
             PERFORM Z800-PRINT-HEADINGS                                        
             PERFORM B400-PRINT-DETAIL-LINE                                     
                 VARYING PV-DEPT-SUB FROM 1 BY 1                                
                 UNTIL PV-DEPT-SUB GREATER PV-TAB-MAX                           
             PERFORM B410-PRINT-STORE-TOTAL                                     
           END-IF.                                                              
                                                                                
      *** NORMAL END OF JOB. PRINT STORE/DEPT TOTALS....                        
                                                                                
           MOVE '*B600-EOJ.                        ' TO PV-PARA                 
                                                                                
W26600     MOVE 'TTL'  TO PV-LOC-NBR-X.                                         
W26600     MOVE SPACES TO XMT00001-LOC-10-ABBR-NM                               
           PERFORM Z800-PRINT-HEADINGS.                                         
                                                                                
           MOVE SPACES                   TO D1-DETAIL-LINE.                     
           MOVE 'TOTAL'                  TO D1-COMMENT.                         
           MOVE PV-TOT-ADJUSTMENT-QTY    TO D1-ADJUSTMENT-QTY.                  
           MOVE PV-TOT-AFTER-RCPT        TO D1-AFTER-RCPT.                      
R26006*    MOVE PV-TOT-CUTOFF-INTRANS    TO D1-CUTOFF-STR-EXPTED.               
R26006     MOVE PV-TOT-CUTOFF-STR-EXPTED TO D1-CUTOFF-STR-EXPTED.               
           MOVE PV-TOT-QTY               TO D1-QTY.                             
           MOVE PV-TOT-OLD-ONHAND-QTY    TO D1-ONHAND-QTY.                      
                                                                                
           MOVE D1-DETAIL-LINE           TO OUT-SUMMARY-RECORD.                 
           MOVE 2                        TO PC-LINE-SPACE.                      
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 2                         TO PC-LINE-COUNT.                      
                                                                                
      *** PRINT A STORE TOTALS SUMMARY PAGE AFTER THE DETAIL TOTALS.            
                                                                                
           MOVE ZEROS          TO PV-ADJUSTMENT-QTY.                            
           MOVE ZEROS          TO PV-OLD-ONHAND-QTY.                            
           MOVE ZEROS          TO PV-QTY.                                       
           MOVE ZEROS          TO PV-AFTER-RCPT.                                
21591      MOVE ZEROS          TO PV-CUTOFF-INTRANS.                            
21591      MOVE ZEROS          TO PV-CUTOFF-STR-EXPTED.                         
           MOVE ZEROS          TO PV-TOT-ADJUSTMENT-QTY.                        
           MOVE ZEROS          TO PV-TOT-AFTER-RCPT.                            
21591      MOVE ZEROS          TO PV-TOT-CUTOFF-INTRANS.                        
21591      MOVE ZEROS          TO PV-TOT-CUTOFF-STR-EXPTED.                     
           MOVE ZEROS          TO PV-TOT-QTY.                                   
           MOVE ZEROS          TO PV-TOT-OLD-ONHAND-QTY.                        
                                                                                
W26600     MOVE 'TTL'          TO PV-LOC-NBR-X.                                 
W26600     MOVE 'SUMMARY'      TO XMT00001-LOC-10-ABBR-NM.                      
           PERFORM Z805-PRINT-HEADINGS.                                         
                                                                                
           MOVE ZEROS          TO PV-STR-NDX.                                   
           PERFORM C200-PRINT-STORE-TOTALS                                      
             VARYING PV-STR-NDX FROM 1 BY 1                                     
               UNTIL PV-STR-NDX IS GREATER THAN MAX-STORES.                     
                                                                                
      *** PRINT A STORE TOTALS SUMMARY PAGE TOTAL AFTER UNLOADING TBL.          
                                                                                
           MOVE SPACES                        TO D1-DETAIL-LINE.                
           MOVE 'TOTAL'                       TO D1-COMMENT.                    
           MOVE PV-TOT-ADJUSTMENT-QTY         TO D1-ADJUSTMENT-QTY.             
           MOVE PV-TOT-AFTER-RCPT             TO D1-AFTER-RCPT.                 
R26006*    MOVE PV-TOT-CUTOFF-INTRANS         TO D1-CUTOFF-STR-EXPTED.          
R26006     MOVE PV-TOT-CUTOFF-STR-EXPTED      TO D1-CUTOFF-STR-EXPTED.          
           MOVE PV-TOT-QTY                    TO D1-QTY.                        
           MOVE PV-TOT-OLD-ONHAND-QTY         TO D1-ONHAND-QTY.                 
                                                                                
           IF PC-LINE-COUNT > +59                                               
              PERFORM Z805-PRINT-HEADINGS                                       
           END-IF.                                                              
                                                                                
           MOVE D1-DETAIL-LINE           TO OUT-SUMMARY-RECORD.                 
           MOVE 2                        TO PC-LINE-SPACE.                      
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 2                         TO PC-LINE-COUNT.                      
                                                                                
      *** PRINT A TOTALS SUMMARY PAGE FOR THE DETAIL REPORT....                 
                                                                                
           MOVE SPACES                        TO D2-DETAIL-LINE.                
           MOVE 'TOTAL'                       TO D2-COMMENT.                    
           MOVE PV-DTL-ADJUSTMENT-QTY         TO D2-ADJUSTMENT-QTY.             
           MOVE PV-DTL-AFTER-RCPT             TO D2-AFTER-RCPT.                 
21591      MOVE PV-DTL-CUTOFF-STR-EXPTED      TO D2-CUTOFF-STR-EXPTED.          
           MOVE PV-DTL-QTY                    TO D2-QTY.                        
           MOVE PV-DTL-OLD-ONHAND-QTY         TO D2-ONHAND-QTY.                 
                                                                                
           IF PC-LINE-COUNT2 > +59                                              
              PERFORM Z810-PRINT-HEADINGS                                       
           END-IF.                                                              
                                                                                
           MOVE D2-DETAIL-LINE           TO OUT-DETAIL-RECORD.                  
           MOVE 2                        TO PC-LINE-SPACE.                      
           PERFORM Z710-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 2                         TO PC-LINE-COUNT2.                     
                                                                                
      *** CLOSE THE FILES.....                                                  
                                                                                
           CLOSE IN-BYPASSED-FILE                                               
W27808           CONTROL-FILE                                                   
                 OUT-SUMMARY-RPT                                                
                 OUT-DETAIL-RPT.                                                
                                                                                
      /****************************************************************         
      * C100-ACCUM-DEPT-TOTALS.                                                 
      *     REFORMAT THE STORE NUMBER AND THE DEPARTMENT NUMBER TO A            
      * NUMERIC VALUE TO BE USED TO SUBSCRIPT THE DEPARTMENT AND STORE          
      * TABLES. IF EITHER VALUE EXCEEDS 999, THE MODULE WILL ABEND AND          
      * AN THE JOB WILL STOP.                                                   
      *     CALCULATE THE FASHION SALES ADJUSTMENTS IN A SIMILAR                
      * FASHION AS INKUT117 DOES.                                               
      *     ACCUMUALTE THE VALUES IN THE DEPARTMENT TABLE AND THE STORE         
      * TABLE.                                                                  
      *****************************************************************         
       C100-ACCUM-DEPT-TOTALS.                                                  
           MOVE '*C100-ACCUM-DEPT-TOTALS.          ' TO PV-PARA                 
                                                                                
      *** ACCUMULATE TO THE DEPARTMENT TABLE.....                               
                                                                                
           MOVE IN016-DEPT       TO PV-DEPT-NMBR.                               
           INSPECT PV-DEPT-NMBR REPLACING ALL ' ' BY '0'.                       
                                                                                
           MOVE IN016-STORE      TO PV-STORE-NMBR.                              
           INSPECT PV-STORE-NMBR REPLACING ALL ' ' BY '0'.                      
                                                                                
           MOVE PV-DEPT-NMBR-NUM  TO PV-DEPT-SUB.                               
           IF PV-DEPT-SUB IS GREATER THAN PV-TAB-MAX                            
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY 'C100-ACCUM-DEPT-TOTALS.....   '                         
               DISPLAY 'MORE THAN 999 DEPARTMENTS EXIST'                        
               DISPLAY 'MUST EXPAND TABLE             '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY 'A B E N D                   * '                         
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           COMPUTE PV-WRK-ADJUSTMENT-QTY = IN016-QTY -                          
                                         (IN016-OLD-ONHAND-QTY                  
W27808                                   - (IN016-STR-EXP-OH-QTY                
W27808                                   - IN016-CUTOFF-INTRANSIT)              
                                         - IN016-AFTER-RECEIPTS).               
                                                                                
           ADD PV-WRK-ADJUSTMENT-QTY TO TB-ADJUSTMENT-QTY (PV-DEPT-SUB).        
           ADD IN016-QTY             TO TB-QTY (PV-DEPT-SUB).                   
           ADD IN016-OLD-ONHAND-QTY  TO TB-OLD-ONHAND-QTY (PV-DEPT-SUB).        
           ADD IN016-AFTER-RECEIPTS  TO TB-AFTER-RCPT (PV-DEPT-SUB).            
21591 *    ADD IN016-CUTOFF-INTRANSIT                                           
21591 *                              TO TB-CUTOFF-INTRANS (PV-DEPT-SUB).        
           COMPUTE PV-TB-STR-EXPTED =                                           
                 (IN016-STR-EXP-OH-QTY - IN016-CUTOFF-INTRANSIT)                
           ADD PV-TB-STR-EXPTED TO TB-CUTOFF-STR-EXPTED (PV-DEPT-SUB).          
           MOVE 'Y'                  TO TB-PROCESS-SW (PV-DEPT-SUB).            
                                                                                
           ADD PV-WRK-ADJUSTMENT-QTY TO PV-ADJUSTMENT-QTY.                      
           ADD IN016-QTY             TO PV-QTY.                                 
           ADD IN016-OLD-ONHAND-QTY  TO PV-OLD-ONHAND-QTY.                      
           ADD IN016-AFTER-RECEIPTS  TO PV-AFTER-RCPT.                          
21591 *    ADD IN016-CUTOFF-INTRANSIT                                           
21591 *                              TO PV-CUTOFF-INTRANS.                      
           COMPUTE PV-STR-EXPTED =                                              
                 (IN016-STR-EXP-OH-QTY - IN016-CUTOFF-INTRANSIT).               
           ADD PV-STR-EXPTED TO PV-CUTOFF-STR-EXPTED.                           
           MOVE 'Y'                  TO WS-PRINT-SW.                            
                                                                                
      *** ACCUMULATE TO THE STORE TABLE.....                                    
                                                                                
           MOVE PV-STORE-NMBR-NUM TO PV-STR-NDX.                                
           IF PV-STR-NDX IS GREATER THAN MAX-STORES                             
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY 'C100-ACCUM-DEPT-TOTALS.....   '                         
W26600         DISPLAY 'MORE THAN 9999 STORES EXIST    '                        
               DISPLAY 'MUST EXPAND TABLE             '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY 'A B E N D                   * '                         
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           ADD PV-WRK-ADJUSTMENT-QTY                                            
                         TO TB-STR-ADJUSTMENT-QTY (PV-STR-NDX).                 
           ADD IN016-QTY TO TB-STR-QTY (PV-STR-NDX).                            
           ADD IN016-OLD-ONHAND-QTY TO TB-STR-OLD-ONHAND (PV-STR-NDX).          
           ADD IN016-AFTER-RECEIPTS TO TB-STR-AFTER-RCPT (PV-STR-NDX).          
21591 *    ADD IN016-CUTOFF-INTRANSIT                                           
21591 *                            TO TB-STR-CUTOFF-INTRANS(PV-STR-NDX).        
           COMPUTE PV-TB-STR-CUTOFF-STR-EXPTED =                                
                 (IN016-STR-EXP-OH-QTY - IN016-CUTOFF-INTRANSIT).               
           ADD PV-TB-STR-CUTOFF-STR-EXPTED                                      
                               TO TB-STR-CUTOFF-STR-EXPTED (PV-STR-NDX).        
           MOVE 'Y'                 TO TB-STR-PROCESS-SW (PV-STR-NDX).          
                                                                                
      /****************************************************************         
      * C200-PRINT-STORE-TOTALS.                                                
      *     PRINT THE SUMMARIZED INVENTORY REPORT STORE SUMMARY PAGE(S).        
      * THE NUMBERS ARE ACCUMULATED AS WE DEVELOP THE SUMMARY AND DETAIL        
      * REPORTS. THE PAGE IS CREATED AT END OF FILE TIME.                       
      *****************************************************************         
       C200-PRINT-STORE-TOTALS.                                                 
           MOVE '*C200-PRINT-STORE-TOTALS.         ' TO PV-PARA                 
                                                                                
           IF TB-STR-PROCESS-SW (PV-STR-NDX) GREATER SPACE                      
                                                                                
               IF PC-LINE-COUNT > +59                                           
                   PERFORM Z805-PRINT-HEADINGS                                  
               END-IF                                                           
                                                                                
               MOVE PV-STR-NDX        TO D1-DEPT-NBR                            
PK1383*        MOVE SPACES            TO D1-DEPT-1                              
               MOVE SPACES            TO D1-COMMENT                             
               MOVE TB-STR-ADJUSTMENT-QTY (PV-STR-NDX)                          
                                      TO D1-ADJUSTMENT-QTY                      
               MOVE TB-STR-AFTER-RCPT (PV-STR-NDX)                              
                                      TO D1-AFTER-RCPT                          
21591          MOVE TB-STR-CUTOFF-STR-EXPTED (PV-STR-NDX)                       
21591                                 TO D1-CUTOFF-STR-EXPTED                   
               MOVE TB-STR-OLD-ONHAND (PV-STR-NDX)                              
                                      TO D1-ONHAND-QTY                          
               MOVE TB-STR-QTY (PV-STR-NDX)                                     
                                      TO D1-QTY                                 
                                                                                
               MOVE D1-DETAIL-LINE    TO OUT-SUMMARY-RECORD                     
               MOVE 1                 TO PC-LINE-SPACE                          
               PERFORM Z700-WRITE-OUT-SUMMARY-RECORD                            
               ADD 1                  TO PC-LINE-COUNT                          
                                                                                
               ADD TB-STR-ADJUSTMENT-QTY (PV-STR-NDX)                           
                                      TO PV-TOT-ADJUSTMENT-QTY                  
               ADD TB-STR-AFTER-RCPT (PV-STR-NDX)                               
                                      TO PV-TOT-AFTER-RCPT                      
21591 *        ADD TB-STR-CUTOFF-INTRANS (PV-STR-NDX)                           
21591 *                               TO PV-TOT-CUTOFF-INTRANS                  
21591          ADD TB-STR-CUTOFF-STR-EXPTED (PV-STR-NDX)                        
21591                                 TO PV-TOT-CUTOFF-STR-EXPTED               
               ADD TB-STR-QTY (PV-STR-NDX)                                      
                                      TO PV-TOT-QTY                             
               ADD TB-STR-OLD-ONHAND (PV-STR-NDX)                               
                                      TO PV-TOT-OLD-ONHAND-QTY                  
                                                                                
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * Z100-READ-CONTROL-FILE....                                              
      *****************************************************************         
W27808 Z100-READ-CONTROL-FILE.                                                  
           MOVE '*Z100-READ-CONTROL-FILE     *' TO PV-PARA                      
                                                                                
           READ CONTROL-FILE                                                    
               INTO CC-CONTROL-RECORD.                                          
                                                                                
W34286     IF CC-REPORT-CONTROL = 'VARIANCE'                                    
W34286        MOVE 'BYPASSED'         TO H1-VAR-REPORT-TYPE                     
W34286                                   H1A-VAR-REPORT-TYPE                    
W34286     ELSE                                                                 
              MOVE CC-REPORT-CONTROL  TO H1-REPORT-TYPE                         
                                         H1A-REPORT-TYPE.                       
                                                                                
      /****************************************************************         
      * Z200-READ-BYPASSED-FILE....                                             
      *****************************************************************         
       Z200-READ-BYPASSED-FILE.                                                 
           MOVE '*Z200-READ-BYPASSED-FILE     *' TO PV-PARA                     
                                                                                
           READ IN-BYPASSED-FILE NEXT RECORD                                    
             INTO IN016-PHYSICAL-COUNT-RECORD                                   
               AT END  MOVE 'Y'  TO WS-BYPASS-DONE-SW.                          
                                                                                
           IF FIRST-TIME-THRU                                                   
               MOVE IN016-STORE  TO PV-STORE-NMBR-SAVE                          
               MOVE 'Y'          TO WS-FIRST-TIME-SW.                           
                                                                                
      /****************************************************************         
      * Z700-WRITE-OUT-SUMMARY-RECORD.                                          
      *****************************************************************         
       Z700-WRITE-OUT-SUMMARY-RECORD.                                           
                                                                                
           IF PC-LINE-SPACE IS EQUAL TO 999                                     
               WRITE OUT-SUMMARY-RECORD AFTER ADVANCING PAGE                    
           ELSE                                                                 
               WRITE OUT-SUMMARY-RECORD                                         
                            AFTER ADVANCING PC-LINE-SPACE LINES                 
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * Z710-WRITE-OUT-SUMMARY-RECORD.                                          
      *****************************************************************         
       Z710-WRITE-OUT-SUMMARY-RECORD.                                           
                                                                                
           IF PC-LINE-SPACE IS EQUAL TO 999                                     
               WRITE OUT-DETAIL-RECORD AFTER ADVANCING PAGE                     
           ELSE                                                                 
               WRITE OUT-DETAIL-RECORD                                          
                                 AFTER ADVANCING PC-LINE-SPACE LINES            
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * Z800-PRINT-HEADINGS.                                                    
      *     USED TO PRINT HEADINGS FOR THE SUMMARIZED INVENTORY AUDIT           
      * REPORT. THE ROUTINE GRABS INFORMATION OFF THE STORE TABLE               
      * XMT_LOC.                                                                
      *****************************************************************         
       Z800-PRINT-HEADINGS.                                                     
                                                                                
W26600     MOVE PV-STORE-NMBR-SV   TO XMT00001-LOC-NBR.                         
W26600                                                                          
           EXEC SQL                                                             
W26600         SELECT LOC_NBR,                                                  
W26600                LOC_10_ABBR_NM                                            
W26600         INTO :XMT00001-LOC-NBR,                                          
W26600              :XMT00001-LOC-10-ABBR-NM                                    
W26600         FROM XMT_LOC                                                     
W26600         WHERE LOC_NBR = :XMT00001-LOC-NBR                                
P40463         WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE IS EQUAL TO ZERO                                    
W26600           MOVE XMT00001-LOC-NBR TO PV-LOC-NBR                            
               WHEN SQLCODE IS EQUAL TO +100                                    
W26600           MOVE ZEROS TO PV-LOC-NBR                                       
W26600           MOVE '***' TO XMT00001-LOC-10-ABBR-NM                          
               WHEN SQLWARN IS NOT EQUAL TO SPACES                              
               WHEN SQLCODE IS NOT EQUAL TO ZERO                                
                 DISPLAY SPACES                                                 
                 DISPLAY '** ABEND IN PROGRAM INKRP120'                         
                 DISPLAY '** ERROR PROCESSING DB2 STORE TABLE.'                 
W26600           DISPLAY 'STORE NUMBER: ', IN016-STORE-9                        
                 PERFORM Z900-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
           ADD 1              TO PC-PAGE-COUNT.                                 
                                                                                
           MOVE PC-PAGE-COUNT TO DP133O-PAGE-NUMBER.                            
           MOVE 1             TO DP133O-REPORT-NUMBER.                          
           MOVE DP133O-STANDRD-HEADER-133-COLS TO OUT-SUMMARY-RECORD.           
           MOVE 999           TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           MOVE 1             TO PC-LINE-COUNT.                                 
                                                                                
W34286     IF CC-REPORT-CONTROL = 'VARIANCE'                                    
W34286        MOVE H1-VAR-HEADING TO OUT-SUMMARY-RECORD                         
W34286     ELSE                                                                 
              MOVE H1-HEADING TO OUT-SUMMARY-RECORD.                            
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
W26600     MOVE PV-LOC-NBR-X             TO H1B-STORE.                          
W26600     MOVE XMT00001-LOC-10-ABBR-NM  TO H1B-STORE-NAME.                     
           MOVE H1B-HEADING TO OUT-SUMMARY-RECORD.                              
           MOVE 1           TO PC-LINE-SPACE.                                   
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE H2-HEADING TO OUT-SUMMARY-RECORD.                               
           MOVE 1          TO PC-LINE-SPACE.                                    
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE H3-HEADING TO OUT-SUMMARY-RECORD.                               
           MOVE 1          TO PC-LINE-SPACE.                                    
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE SPACES     TO OUT-SUMMARY-RECORD.                               
           MOVE 1          TO PC-LINE-SPACE.                                    
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
      /****************************************************************         
      * Z805-PRINT-HEADINGS.                                                    
      *     USED TO PRINT HEADINGS FOR THE SUMMARIZED INVENTORY AUDIT           
      * REPORT - STORE TOTALS PAGE.                                             
      *****************************************************************         
       Z805-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1              TO PC-PAGE-COUNT.                                 
                                                                                
           MOVE PC-PAGE-COUNT TO DP133O-PAGE-NUMBER.                            
           MOVE 1             TO DP133O-REPORT-NUMBER.                          
           MOVE DP133O-STANDRD-HEADER-133-COLS TO OUT-SUMMARY-RECORD.           
           MOVE 999           TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           MOVE 1             TO PC-LINE-COUNT.                                 
                                                                                
W34286     IF CC-REPORT-CONTROL = 'VARIANCE'                                    
W34286        MOVE H1-VAR-HEADING TO OUT-SUMMARY-RECORD                         
W34286     ELSE                                                                 
              MOVE H1-HEADING TO OUT-SUMMARY-RECORD.                            
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
W26600     MOVE PV-LOC-NBR-X             TO H1B-STORE.                          
W26600     MOVE XMT00001-LOC-10-ABBR-NM  TO H1B-STORE-NAME.                     
           MOVE H1B-HEADING TO OUT-SUMMARY-RECORD.                              
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE H2B-HEADING TO OUT-SUMMARY-RECORD.                              
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE H3-HEADING TO OUT-SUMMARY-RECORD.                               
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE SPACES     TO OUT-SUMMARY-RECORD.                               
           MOVE 1          TO PC-LINE-SPACE.                                    
           PERFORM Z700-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
      /****************************************************************         
      * Z810-PRINT-HEADINGS.                                                    
      *     USED TO PRINT HEADINGS FOR THE DETAILEDINVENTORY AUDIT              
      * REPORT.                                                                 
      *****************************************************************         
       Z810-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1              TO PC-PAGE-COUNT2.                                
                                                                                
           MOVE PC-PAGE-COUNT2 TO DP133O-PAGE-NUMBER.                           
           MOVE 2             TO DP133O-REPORT-NUMBER.                          
           MOVE DP133O-STANDRD-HEADER-133-COLS                                  
                              TO OUT-DETAIL-RECORD.                             
           MOVE 999           TO PC-LINE-SPACE.                                 
           PERFORM Z710-WRITE-OUT-SUMMARY-RECORD.                               
           MOVE 1             TO PC-LINE-COUNT2.                                
                                                                                
W34286     IF CC-REPORT-CONTROL = 'VARIANCE'                                    
W34286        MOVE H1A-VAR-HEADING TO OUT-DETAIL-RECORD                         
W34286     ELSE                                                                 
              MOVE H1A-HEADING   TO OUT-DETAIL-RECORD.                          
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z710-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT2.                                
                                                                                
           MOVE H4-HEADING    TO OUT-DETAIL-RECORD.                             
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z710-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE H5-HEADING    TO OUT-DETAIL-RECORD.                             
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z710-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE SPACES        TO OUT-DETAIL-RECORD.                             
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z710-WRITE-OUT-SUMMARY-RECORD.                               
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
      /****************************************************************         
      * Z900-SQL-ABEND.                                                         
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKRP120'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARA.                              
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
