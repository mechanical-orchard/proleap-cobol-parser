00001  IDENTIFICATION DIVISION.                                         02/05/92
00002                                                                   MBKCS133
00003  PROGRAM-ID.    MBKCS133.                                            LV016
00004  AUTHOR.        M. FURLICK.                                          CL**1
00005  INSTALLATION.  KOHLS DEPARTMENT STORES.                             CL**1
00006  DATE-WRITTEN.  11-04-91.                                            CL**1
00007  DATE-COMPILED.                                                      CL**1
00008                                                                      CL**1
00009 *****************************************************************    CL**1
00010 *    R133 - WORKER RESPONSIBLE SELECTION                             CL**1
00011 *                                                                    CL**1
00012 *    THE WORKER USER ID IS THE ONLY REQUIRED ENTRY ON THIS SCREEN.   CL**1
00013 *    THE HISTORY INDICATOR (INCLUDE = Y, DON'T INCLUDE = N) IS THE   CL**1
00014 *    ONLY OTHER ENTERABLE FIELD, AND IT IS OPTIONAL.                 CL**1
00015 *    AFTER VALIDATION, MBKCS134 (WORKER RESPONSIBLE LIST) MAY BE     CL**1
00016 *    INVOKED.                                                        CL**1
00017 *****************************************************************    CL**1
00018 *                                                                    CL**1
00019 *                                                                    CL**1
00020 *                                                                    CL**1
00021 *                                                                    CL**1
00022  ENVIRONMENT DIVISION.                                               CL**1
00023 *                                                                    CL**1
00024 *                                                                    CL**1
00025 *                                                                    CL**1
00026  DATA DIVISION.                                                      CL**1
00027 *                                                                    CL**1
00028 *                                                                    CL**1
00029  WORKING-STORAGE SECTION.                                            CL**1
00030                                                                      CL**1
00031  01  PC-PROGRAM-CONSTANTS.                                           CL**1
00032      05  PC-NAVIGATOR-TRAN-ID           PIC X(04)    VALUE 'Z008'.   CL**1
00033      05  PC-MAXIMUM-COMMAREA-LENGTH     PIC S9(04)   VALUE +4072     CL**1
00034                                                      COMP SYNC.      CL**1
00035      05  PC-CURRENT-MAP-NAME            PIC X(08)    VALUE           CL**1
00036      'MB133A  '.                                                     CL**1
00037      05  PC-CURRENT-MAPSET-NAME         PIC X(08)    VALUE           CL**1
00038      'MBKM133 '.                                                     CL**1
00039      05  PC-CURRENT-PROGRAM-NAME        PIC X(08)    VALUE           CL**1
00040      'MBKCS133'.                                                     CL**1
00041      05  PC-HISTORY-DEFAULT             PIC X(01)    VALUE 'N'.      CL**1
00042      05  PC-MAX-RETRIES                 PIC 9(02)    VALUE  3.       CL**1
00043      05  PC-ERASE-EOF-COMP              PIC S9(04)   VALUE +128      CL**1
00044                                                      COMP SYNC.      CL**1
00045      05  FILLER REDEFINES PC-ERASE-EOF-COMP.                         CL**1
00046          10  FILLER                     PIC X(01).                   CL**1
00047          10  PC-ERASE-EOF               PIC X(01).                   CL**1
00048      05  PC-ZERO-VALUE                  PIC X(01)    VALUE '0'.      CL**1
00049      05  PC-COMPANY                     PIC X(02)    VALUE '03'.     CL**1
00050      05  PC-TSYMSG-NUMBERS.                                          CL**1
00051          10 PC-TSYMSG-00005             PIC 9(05)    VALUE 00005.    CL**1
00052          10 PC-TSYMSG-00007             PIC 9(05)    VALUE 00007.    CL**1
00053          10 PC-TSYMSG-00045             PIC 9(05)    VALUE 00045.    CL**3
00054          10 PC-TSYMSG-00134             PIC 9(05)    VALUE 00134.    CL**6
00055          10 PC-TSYMSG-00148             PIC 9(05)    VALUE 00148.    CL**8
00056                                                                      CL**1
00057  01  PM-PROGRAM-MESSAGES.                                            CL**1
00058      05  PM-PROGRAM-MESSAGE-1           PIC X(78)    VALUE           CL**1
00059      'INVALID NEXT APPLICATION ACTIVITY RECEIVED'.                   CL**1
00060                                                                      CL**1
00061  01  PS-PROGRAM-SWITCHES.                                            CL**1
00062      05  PS-MAP-SEND-TYPE               PIC X(01)    VALUE '1'.      CL**1
00063          88  PS-SEND-MAP-N-DATA-FRSET                VALUE '1'.      CL**1
00064          88  PS-SEND-MAP-N-DATA-FRSET-CPOSN          VALUE '2'.      CL**1
00065          88  PS-SEND-DATA-ONLY-FRSET                 VALUE '3'.      CL**1
00066          88  PS-SEND-DATA-ONLY                       VALUE '4'.      CL**1
00067          88  PS-SEND-DATA-ONLY-ERASEAUP              VALUE '5'.      CL**1
00068          88  PS-SEND-MAP-ONLY-FRSET                  VALUE '6'.      CL**1
00069      05  PS-ERROR-SW                    PIC X(01)    VALUE 'N'.      CL**1
00070          88  PS-ERROR                                VALUE 'Y'.      CL**1
00071          88  PS-NO-ERROR                             VALUE 'N'.      CL**1
00072      05  PS-SPECIF-TRAN-SW              PIC X(01)    VALUE 'N'.      CL**1
00073          88  PS-SPECIF-TRAN                          VALUE 'Y'.      CL**1
00074          88  PS-NO-SPECIF-TRAN                       VALUE 'N'.      CL**1
00075      05  PS-SPECIF-GMA-SW               PIC X(01)    VALUE 'N'.      CL**1
00076          88  PS-SPECIF-GMA                           VALUE 'Y'.      CL**1
00077          88  PS-NO-SPECIF-GMA                        VALUE 'N'.      CL**1
00078      05  PS-SPECIF-DMA-SW               PIC X(01)    VALUE 'N'.      CL**1
00079          88  PS-SPECIF-DMA                           VALUE 'Y'.      CL**1
00080          88  PS-NO-SPECIF-DMA                        VALUE 'N'.      CL**1
00081      05  PS-SPECIF-DPT-SW               PIC X(01)    VALUE 'N'.      CL**1
00082          88  PS-SPECIF-DPT                           VALUE 'Y'.      CL**1
00083          88  PS-NO-SPECIF-DPT                        VALUE 'N'.      CL**1
00084      05  PS-ROW-FOUND-SW                PIC X(01)    VALUE 'N'.      CL**1
00085          88  PS-ROW-FOUND                            VALUE 'Y'.      CL**1
00086          88  PS-ROW-NOT-FOUND                        VALUE 'N'.      CL**1
00087                                                                      CL**1
00088  01  PV-PROGRAM-VARIABLES.                                           CL**1
00089      05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0        CL**1
00090                                                      COMP SYNC.      CL**1
00091      05  PV-NAV-MNEMONIC                PIC X(08)    VALUE SPACES.   CL**1
00092      05  PV-MESSAGE                     PIC X(78)    VALUE SPACES.   CL**1
00093      05  PV-RETRY-COUNTER               PIC S9(3)    VALUE ZERO      CL**1
00094                                                      COMP-3.         CL**1
00095      05  PV-SQL-SUB                     PIC S9(3)    VALUE ZERO      CL**3
00096                                                      COMP-3.         CL**3
00097      05  PV-DB2-COUNT                   PIC S9(9)    VALUE ZERO      CL**1
00098                                                      COMP-3.         CL**1
00099                                                                      CL**1
00100  01  PV-DB2-DATE                        PIC X(10).                   CL*13
00101  01  PV-DB2-HISTORY-DATE                PIC X(10)    VALUE           CL*13
00102                                                      '1900-01-01'.   CL*12
00103  01  PV-DB2-WORK-DATE.                                               CL*13
00104      05  PV-DW-CCYY                     PIC X(4).                    CL*13
00105      05  FILLER                         PIC X.                       CL*13
00106          88  PV-DW-DASH1                             VALUE '-'.      CL*13
00107      05  PV-DW-MM                       PIC XX.                      CL*13
00108      05  FILLER                         PIC X.                       CL*13
00109          88  PV-DW-DASH2                             VALUE '-'.      CL*13
00110      05  PV-DW-DD                       PIC XX.                      CL*13
00111  EJECT                                                               CL**1
00112 *****************************************************************    CL**1
00113 *    STANDARD COMMAREA.                                              CL**1
00114 *****************************************************************    CL**1
00115                                                                      CL**1
00116      COPY DPWS020.                                                   CL*16
00117      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                   CL**1
00118                                                                      CL**1
00119 *****************************************************************    CL**1
00120 *    INTER-APPLICATION COMMAREA.                                     CL**1
00121 *****************************************************************    CL**1
00122                                                                      CL**1
00123          10  INTER-APPL-COMM-AREA       PIC X(200).                  CL**1
00124                                                                      CL**1
00125      COPY MBWS005.                                                   CL*16
00126                                                                      CL**1
00127 *****************************************************************    CL**1
00128 *    APPLICATION SPECIFIC COMMAREA                                   CL**1
00129 *****************************************************************    CL**1
00130                                                                      CL**1
00131          10  MB133-SPECIFIC-COMMAREA.                                CL**1
00132              15  FILLER                      PIC X(02).              CL**1
00133              15  MB133-WORKER                PIC X(08).              CL**1
00134              15  MB133-HISTORY-FLAG          PIC X.                  CL**1
00135                  88  MB133-VALID-HIST-VALUES VALUE 'Y', 'N'.         CL**1
00136                  88  MB133-INCLUDE-HISTORY   VALUE 'Y'.              CL*14
00137              15  MB133-SAVE-INTER-APPL-COMM  PIC X(200).             CL**1
00138              15  FILLER                      PIC X(2654).            CL**1
00139 *****************************************************************    CL**1
00140 *    ABEND PROCESSING COPYBOOK.                                      CL**1
00141 *****************************************************************    CL**1
00142                                                                      CL**1
00143      COPY DPWS013.                                                   CL*16
00144                                                                      CL**1
00145 *****************************************************************    CL**1
00146 *    NUMERIC FIELD EDIT ROUTINE WORKING STORAGE                      CL**1
00147 *****************************************************************    CL**1
00148                                                                      CL**1
00149      COPY DPWS010I.                                                  CL**1
00150                                                                      CL**1
00151 *****************************************************************    CL**1
00152 *    ATTRIBUTE SETTINGS COPYBOOK.                                    CL**1
00153 *****************************************************************    CL**1
00154                                                                      CL**1
00155      COPY DPWS015.                                                   CL**1
00156                                                                      CL**1
00157 *****************************************************************    CL**1
00158 *    CURRENT DATE AND TIME COPYBOOK.                                 CL**1
00159 *****************************************************************    CL**1
00160                                                                      CL**1
00161      COPY DPWS017.                                                   CL**1
00162                                                                      CL**1
00163 *****************************************************************    CL**1
00164 *    TOGGLE FUNCTION KEY LINES COPYBOOK.                             CL**1
00165 *****************************************************************    CL**1
00166                                                                      CL**1
00167      COPY DPWS009.                                                   CL**1
00168                                                                      CL**1
00169 *****************************************************************    CL**1
00170 *    FUNCTION KEYS COPYBOOK.                                         CL**1
00171 *****************************************************************    CL**1
00172                                                                      CL**1
00173      COPY DPWS016.                                                   CL**1
00174                                                                      CL**1
00175 *****************************************************************    CL**1
00176 *    DPKUT045 COPYBOOK.                                              CL**1
00177 *****************************************************************    CL**1
00178                                                                      CL**1
00179      COPY DPWS045.                                                   CL**1
00180                                                                      CL**1
00179      COPY DPWS006.                                                   CL**1
00180                                                                      CL**1
00181      COPY DPRD507I.                                                  CL**1
00182  EJECT                                                               CL**1
00183                                                                      CL**1
00184 *****************************************************************    CL**1
00185 *    STANDARD ATTENTION IDENTIFIER LIST                              CL**1
00186 *****************************************************************    CL**1
00187                                                                      CL**1
00188      COPY DFHAID.                                                    CL**1
00189                                                                      CL**1
00190 *****************************************************************    CL**1
00191 *    STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST           CL**1
00192 *****************************************************************    CL**1
00193                                                                      CL**1
00194      COPY DFHBMSCA.                                                  CL**1
00195                                                                      CL**1
00196 *****************************************************************    CL**1
00197 *    DB2 COMMUNICATION AREA.                                         CL**1
00198 *****************************************************************    CL**1
00199                                                                      CL**1
00200      EXEC SQL                                                        CL**1
00201           INCLUDE SQLCA                                              CL**1
00202      END-EXEC.                                                       CL**1
00203                                                                      CL**1
00204 *****************************************************************    CL**1
00205 *    WORKER DCLGEN                                                   CL**1
00206 *****************************************************************    CL**1
00207                                                                      CL**1
00208      EXEC SQL                                                        CL**1
00209           INCLUDE TWORKER                                            CL**1
00210      END-EXEC.                                                       CL**1
00211                                                                      CL**1
00212                                                                      CL**1
00213 *****************************************************************    CL**1
00214 *    RESPONSIBLE AREA DCLGEN                                         CL**1
00215 *****************************************************************    CL**1
00216                                                                      CL**1
00217      EXEC SQL                                                        CL**1
00218           INCLUDE TRSPBAR                                            CL**1
00219      END-EXEC.                                                       CL**1
00220                                                                      CL**1
00221 *****************************************************************    CL**1
00222 *    MAP LAYOUT                                                      CL**1
00223 *****************************************************************    CL**1
00224                                                                      CL**1
00225      COPY MBKM133.                                                   CL**1
00226                                                                      CL**1
00227  01  MD-MAP-DATA-AREA REDEFINES MB133AO.                             CL**1
00228      05  FILLER                         PIC X(426).                  CL**1
00229  EJECT                                                               CL**1
00230  LINKAGE SECTION.                                                    CL**1
00231                                                                      CL**1
00232  01  DFHCOMMAREA.                                                    CL**1
00233      05  FILLER                         OCCURS  1 TO 4072 TIMES      CL**1
00234                                         DEPENDING ON EIBCALEN.       CL**1
00235          10  FILLER                     PIC X.                       CL**1
00236                                                                      CL**1
00237 *                                                                    CL**1
00238 *                                                                    CL**1
00239 *                                                                    CL**1
00240  PROCEDURE DIVISION.                                                 CL**1
00241                                                                      CL**1
00242  0000-MAIN-MODULE.                                                   CL**1
00243                                                                      CL**1
00244 ******************************************************************   CL**1
00245 *    PROCESS THIS TRANSACTION IF THE COMMAREA IS VALID.              CL**1
00246 ******************************************************************   CL**1
00247                                                                      CL**1
00248      PERFORM DP015-0000-CHECK-COMMAREA.                              CL**1
00249                                                                      CL**1
00250 ******************************************************************   CL**1
00251 *    DP020-SRC-RETURN-W-TRAN-ID WILL BE TRUE IF THIS PROGRAM WAS     CL**1
00252 *    SENT A STANDARD COMMAREA. IF NOT SET TRUE (MEANING OTHER        CL**1
00253 *    THAN A STANDARD COMMAREA WAS SENT TO THIS PROGRAM, THIS         CL**1
00254 *    PROGRAM WILL RETURN TO CISS.                                    CL**1
00255 ******************************************************************   CL**1
00256      IF DP020-SRC-RETURN-W-TRAN-ID                                   CL**1
00257          PERFORM 1000-CONTROL-PROCESSING                             CL**1
00258      END-IF.                                                         CL**1
00259                                                                      CL**1
00260      PERFORM DP003-0000-RETURN-TO-CICS.                              CL**1
00261  EJECT                                                               CL**1
00262  1000-CONTROL-PROCESSING.                                            CL**1
00263                                                                      CL**1
00264 ******************************************************************   CL**1
00265 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT       CL**1
00266 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                       CL**1
00267 *                                                                    CL**1
00268 *    PERFORMED BY 0000-MAIN-MODULE.                                  CL**1
00269 ******************************************************************   CL**1
00270                                                                      CL**1
00271      EVALUATE TRUE                                                   CL**1
00272                                                                      CL**1
00273          WHEN DP020-NEXT-ACT-INITIAL                                 CL**1
00274              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                    CL**1
00275              INITIALIZE MB133-SPECIFIC-COMMAREA                      CL**1
00276              PERFORM 2000-BUILD-INITIAL-PANEL                        CL**1
00277                                                                      CL**1
00278          WHEN DP020-NEXT-ACT-READ-MAP                                CL**1
00279              PERFORM 3000-PROCESS-PANEL                              CL**1
00280                                                                      CL**1
00281          WHEN DP020-NEXT-ACT-RETURN                                  CL**1
00282              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                    CL**1
00283              MOVE MB133-SAVE-INTER-APPL-COMM                         CL**1
00284                                  TO  MB005-INTER-APPL-COMMAREA       CL**1
00285              PERFORM 2200-DISPLAY-PANEL                              CL**1
00286                                                                      CL**1
00287          WHEN DP020-NEXT-ACT-ERROR                                   CL**1
00288              MOVE DP020-MSG-MESSAGE       TO PV-MESSAGE              CL**1
00289                                                                      CL**1
00290              IF DP020-NAV-MNEMONIC = SPACES                          CL**1
00291                  MOVE SPACES              TO ANAVCMDO                CL**1
00292                  MOVE DP015-GREEN         TO ANAVCMDC                CL**1
00293                  MOVE DP015-UNDERLINE     TO ANAVCMDH                CL**1
00294              ELSE                                                    CL**1
00295                  MOVE DP015-RED           TO ANAVCMDC                CL**1
00296                  MOVE DP015-REVERSE       TO ANAVCMDH                CL**1
00297              END-IF                                                  CL**1
00298                                                                      CL**1
00299              SET PS-ERROR                 TO TRUE                    CL**1
00300              SET PS-SEND-DATA-ONLY-FRSET  TO TRUE                    CL**1
00301                                                                      CL**1
00302              MOVE PV-MESSAGE              TO DP020-MSG-MESSAGE       CL**1
00303              MOVE -1                      TO ANAVCMDL                CL**1
00304              PERFORM 3300-EDIT-DATA-IN-COMMAREA                      CL**1
00305              PERFORM 2200-DISPLAY-PANEL                              CL**1
00306                                                                      CL**1
00307          WHEN OTHER                                                  CL**1
00308              SET DP013-LOGIC-ABEND        TO TRUE                    CL**1
00309              SET DP013-NO-ROLLBACK        TO TRUE                    CL**1
00310                                                                      CL**1
00311              MOVE 'B100-CONTROL-PROCESSING' TO                       CL**1
00312                   DP013-PARAGRAPH                                    CL**1
00313                                                                      CL**1
00314              MOVE PM-PROGRAM-MESSAGE-1      TO                       CL**1
00315                   DP013-MESSAGE-TEXT(1)                              CL**1
00316                                                                      CL**1
00317              PERFORM DP013-0000-PROCESS-ABEND                        CL**1
00318                                                                      CL**1
00319      END-EVALUATE.                                                   CL**1
00320  EJECT                                                               CL**1
00321  2000-BUILD-INITIAL-PANEL.                                           CL**1
00322                                                                      CL**1
00323 ******************************************************************   CL**1
00324 *    PERFORMED ON THE FIRST PASS IF THIS PROGRAM                     CL**1
00325 *                                                                    CL**1
00326 *    PERFORMED BY B100-CONTROL-PROCESSING.                           CL**1
00327 ******************************************************************   CL**1
00328                                                                      CL**1
00329                                                                      CL**1
00330      MOVE PC-HISTORY-DEFAULT     TO MB133-HISTORY-FLAG.              CL**1
00331                                                                      CL**1
00332      PERFORM 2200-DISPLAY-PANEL.                                     CL**1
00333      EJECT                                                           CL**1
00334  2200-DISPLAY-PANEL.                                                 CL**1
00335                                                                      CL**1
00336 ******************************************************************   CL**1
00337 *    FILL THE MAP WITH THE NECESSARY DATA AND WRITE IT OUT.          CL**1
00338 *                                                                    CL**1
00339 *    PERFORMED BY 1000-CONTROL-PROCESSING.                           CL**1
00340 *    PERFORMED BY 3000-PROCESS-PANEL.                                CL**1
00341 *    PERFORMED BY 3100-CHECK-LOCAL-FKEYS.                            CL**1
00342 ******************************************************************   CL**1
00343                                                                      CL**1
00344      PERFORM DP017-0000-GET-CURR-DATE-TIME.                          CL**1
00345      MOVE DP017-EDITED-TIME-HMS  TO  ATIMEO.                         CL**1
00346                                                                      CL**1
00347      IF  PS-SEND-MAP-N-DATA-FRSET                                    CL**1
00348          PERFORM DP002-0000-FORMAT-TITLE-LINES                       CL**1
00349          PERFORM 3400-MOVE-COMMAREA-TO-SCREEN                        CL**1
00350      END-IF.                                                         CL**1
00351                                                                      CL**1
00352      PERFORM DP005-0000-FORMAT-MSG-LINES.                            CL**1
00353                                                                      CL**1
00354      IF (DP020-SRC-AID = DP016-PA2) OR                               CL**1
00355         (PS-SEND-MAP-N-DATA-FRSET)                                   CL**1
00356          PERFORM DP009-0000-LOAD-FK-LINES                            CL**1
00357      END-IF.                                                         CL**1
00358                                                                      CL**1
00359      IF  PS-NO-ERROR                                                 CL**1
00360          MOVE -1                 TO  AWORKERL                        CL**1
00361      END-IF.                                                         CL**4
00362                                                                      CL**1
00363      PERFORM DP008-0000-WRITE-PANEL.                                 CL**1
00364                                                                      CL**1
00365  EJECT                                                               CL**1
00366  3000-PROCESS-PANEL.                                                 CL**1
00367                                                                      CL**1
00368 ******************************************************************   CL**1
00369 *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED     CL**1
00370 *    ON WHAT FUNCTION KEY WAS SELECTED.                              CL**1
00371 *                                                                    CL**1
00372 *    PERFORMED BY B100-CONTROL-PROCESSING.                           CL**1
00373 ******************************************************************   CL**1
00374                                                                      CL**1
00375      SET PS-SEND-DATA-ONLY-FRSET TO TRUE.                            CL**1
00376                                                                      CL**1
00377      PERFORM DP007-0000-GET-PANEL.                                   CL**1
00378                                                                      CL**1
00379      IF (DP020-SRC-AID NOT = DP016-PF5)  AND                         CL**1
00380         (DP020-SRC-AID NOT = DP016-CLEAR)                            CL**1
00381          PERFORM 3200-MOVE-SCREEN-TO-COMMAREA                        CL**1
00382      END-IF.                                                         CL**1
00383                                                                      CL**1
00384      PERFORM 3100-CHECK-LOCAL-FKEYS.                                 CL**1
00385  EJECT                                                               CL**1
00386                                                                      CL**1
00387  3100-CHECK-LOCAL-FKEYS.                                             CL**1
00388                                                                      CL**1
00389 ******************************************************************   CL**1
00390 *    PERFORM THE APPROPRIATE PARAGRAPH BASED ON WHICH FUNCTION       CL**1
00391 *    KEY THE USER HIT.                                               CL**1
00392 *                                                                    CL**1
00393 *    PERFORMED BY 3000-PROCESS-PANEL.                                CL**1
00394 ******************************************************************   CL**1
00395                                                                      CL**1
00396      EVALUATE TRUE                                                   CL**1
00397                                                                      CL**1
00398          WHEN DP020-FK-GO-TO-TRAN(DP020-SRC-AID) OR                  CL**1
00399               DP020-FK-RETURN(DP020-SRC-AID)                         CL**1
00400               PERFORM 4000-NAVIGATE                                  CL**1
00401                                                                      CL**1
00402          WHEN  DP020-FK-EDIT-GOTO(DP020-SRC-AID)                     CL**1
00403                PERFORM 3300-EDIT-DATA-IN-COMMAREA                    CL**1
00404                IF  PS-NO-ERROR                                       CL**1
00405                    PERFORM 4000-NAVIGATE                             CL**1
00406                ELSE                                                  CL**1
00407                    PERFORM 2200-DISPLAY-PANEL                        CL**1
00408                END-IF                                                CL**1
00409                                                                      CL**1
00410          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                        CL**1
00411              MOVE SPACES       TO  ANAVCMDO                          CL*15
00412                                    ANAVKEYO                          CL*15
00413              SET PS-SEND-MAP-N-DATA-FRSET                            CL**1
00414                                  TO  TRUE                            CL**1
00415              INITIALIZE MB133-SPECIFIC-COMMAREA                      CL**3
00416              PERFORM 2000-BUILD-INITIAL-PANEL                        CL**1
00417                                                                      CL**1
00418          WHEN DP020-SRC-AID = DP016-ENTER                            CL**1
00419              IF (DP020-NAV-MNEMONIC = SPACES)     OR                 CL**1
00420                 (DP020-NAV-MNEMONIC = LOW-VALUES)                    CL**1
00421                  PERFORM 3300-EDIT-DATA-IN-COMMAREA                  CL**1
00422                  PERFORM 2200-DISPLAY-PANEL                          CL**1
00423              ELSE                                                    CL**1
00424                  PERFORM 4000-NAVIGATE                               CL**1
00425              END-IF                                                  CL**1
00426                                                                      CL**1
00427          WHEN DP020-SRC-AID = DP016-PA2                              CL**1
00428              SET PS-SEND-DATA-ONLY TO TRUE                           CL**1
00429              MOVE -1               TO ANAVCMDL                       CL**1
00430              PERFORM DP017-0000-GET-CURR-DATE-TIME                   CL**1
00431              PERFORM DP009-0000-LOAD-FK-LINES                        CL**1
00432              PERFORM DP008-0000-WRITE-PANEL                          CL**1
00433                                                                      CL**1
00434          WHEN DP020-SRC-AID = DP016-CLEAR                            CL**1
00435              SET PS-SEND-MAP-N-DATA-FRSET                            CL**1
00436                                  TO  TRUE                            CL**1
00437              PERFORM 3300-EDIT-DATA-IN-COMMAREA                      CL**1
00438              PERFORM 2200-DISPLAY-PANEL                              CL**1
00439                                                                      CL**1
00440          WHEN OTHER                                                  CL**1
00441              PERFORM 3300-EDIT-DATA-IN-COMMAREA                      CL*11
00442              MOVE PC-TSYMSG-00045                                    CL**1
00443                                  TO  DP020-MSG-NUMBER                CL**1
00444              SET DP020-MSG-FATAL TO  TRUE                            CL**1
00445              MOVE -1             TO  ANAVCMDL                        CL**1
00446              PERFORM 2200-DISPLAY-PANEL                              CL**1
00447                                                                      CL**1
00448      END-EVALUATE.                                                   CL**1
00449  EJECT                                                               CL**1
00450                                                                      CL**1
00451  3200-MOVE-SCREEN-TO-COMMAREA.                                       CL**1
00452                                                                      CL**1
00453 ******************************************************************   CL**1
00454 *    MOVE THE FIELDS FROM THE MAP TO THE COMMAREA IF ANYTHING        CL**1
00455 *    WAS ENTERED IN THAT FIELD.                                      CL**1
00456 *                                                                    CL**1
00457 *    PERFORMED BY 3000-PROCESS-PANEL.                                CL**1
00458 ******************************************************************   CL**1
00459                                                                      CL**1
00460      IF (AWORKERL GREATER THAN ZERO                                  CL**1
00461      OR  AWORKERA EQUAL PC-ERASE-EOF)                                CL**1
00462          MOVE AWORKERI           TO MB133-WORKER                     CL**3
00463      END-IF.                                                         CL**1
00464                                                                      CL**1
00465      IF (AHISTL GREATER THAN ZERO                                    CL**1
00466      OR  AHISTA EQUAL PC-ERASE-EOF)                                  CL**1
00467         MOVE AHISTI              TO MB133-HISTORY-FLAG               CL**1
00468      END-IF.                                                         CL**1
00469                                                                      CL**1
00470                                                                      CL**1
00471  EJECT                                                               CL**1
00472  3300-EDIT-DATA-IN-COMMAREA.                                         CL**1
00473                                                                      CL**1
00474 ******************************************************************   CL**1
00475 *    VALIDATE THE WORKER NUMBER AND THE HISTORY FLAG.                CL**1
00476 *                                                                    CL**1
00477 *    PERFORMED BY 1000-CONTROL-PROCESSING.                           CL**1
00478 *    PERFORMED BY 3000-PROCESS-PANEL.                                CL**1
00479 *    PERFORMED BY 3100-CHECK-LOCAL-FKEYS.                            CL**1
00480 ******************************************************************   CL**1
00481                                                                      CL**1
00482      SET PS-NO-ERROR             TO  TRUE.                           CL**1
00483                                                                      CL**1
00484      IF (DP020-NAV-MNEMONIC = SPACES OR                              CL**1
00485          DP020-NAV-MNEMONIC = LOW-VALUES) AND                        CL**1
00486         (DP020-NAV-KEY-DATA > SPACES)                                CL**1
00487          MOVE DP015-RED          TO  ANAVKEYC                        CL**1
00488          MOVE DP015-REVERSE      TO  ANAVKEYH                        CL**1
00489          MOVE -1                 TO  ANAVKEYL                        CL**1
00490          MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                CL**1
00491          SET DP020-MSG-FATAL     TO  TRUE                            CL**1
00492          SET PS-ERROR            TO  TRUE                            CL**1
00493      END-IF.                                                         CL**1
00494                                                                      CL**1
00495      IF  MB133-VALID-HIST-VALUES                                     CL**1
00496          MOVE DP015-GREEN        TO  AHISTC                          CL**1
00497          MOVE DP015-UNDERLINE    TO  AHISTH                          CL**1
00498      ELSE                                                            CL**1
00499          MOVE DP015-RED          TO  AHISTC                          CL**1
00500          MOVE DP015-REVERSE      TO  AHISTH                          CL**1
00501          MOVE -1                 TO  AHISTL                          CL**1
00502          MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                CL**1
00503          SET DP020-MSG-FATAL     TO  TRUE                            CL**1
00504          SET PS-ERROR            TO  TRUE                            CL**1
00505      END-IF.                                                         CL**1
00506                                                                      CL**1
00507      IF  MB133-WORKER GREATER THAN SPACES                            CL**1
00508          MOVE DP015-GREEN        TO  AWORKERC                        CL**1
00509          MOVE DP015-UNDERLINE    TO  AWORKERH                        CL**1
00510          SET PS-ROW-NOT-FOUND    TO  TRUE                            CL**7
00511          PERFORM 3310-VALIDATE-WORKER                                CL**1
00512      ELSE                                                            CL**4
00513          MOVE DP015-RED          TO  AWORKERC                        CL**4
00514          MOVE DP015-REVERSE      TO  AWORKERH                        CL**4
00515          MOVE -1                 TO  AWORKERL                        CL**4
00516          MOVE PC-TSYMSG-00007    TO  DP020-MSG-NUMBER                CL**4
00517          SET DP020-MSG-FATAL     TO  TRUE                            CL**4
00518          SET PS-ERROR            TO  TRUE                            CL**4
00519      END-IF.                                                         CL**1
00520      EJECT                                                           CL**1
00521                                                                      CL**1
00522  3310-VALIDATE-WORKER.                                               CL**1
00523 ******************************************************************   CL**1
00524 *    ENSURE THE WORKER NUMBER ENTERED ON THE SCREEN IS ON THE        CL**1
00525 *    DATABASE.                                                       CL**1
00526 *                                                                    CL**1
00527 *    PERFORMED FROM 3300                                             CL**1
00528 ******************************************************************   CL**1
00529                                                                      CL**1
00530      PERFORM WITH TEST AFTER                                         CL**1
00531          VARYING PV-RETRY-COUNTER                                    CL**1
00532          FROM 1 BY 1                                                 CL**1
00533          UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)                 CL**1
00534                  OR (SQLCODE = 0)                                    CL**1
00535                  OR (SQLCODE = +100))                                CL**1
00536                                                                      CL**1
00537          EXEC SQL                                                    CL**1
00538              SELECT                                                  CL**1
00539                     UID_NBR                                          CL**1
00540              INTO                                                    CL**1
00541                     :WORKER-UID-NBR                                  CL**1
00542              FROM                                                    CL**1
00543                     TWORKER                                          CL**1
00544              WHERE                                                   CL**1
00545                     OPERCO_NBR    =  :PC-COMPANY                     CL**1
00546                AND  UID_NBR       =  :MB133-WORKER                   CL**2
00547          END-EXEC                                                    CL**1
00548                                                                      CL**1
00549          EVALUATE TRUE                                               CL**1
00550              WHEN SQLCODE = ZERO                                     CL**1
00551                  SET PS-ROW-FOUND TO TRUE                            CL**7
00552              WHEN SQLCODE = -904                                     CL**1
00553              WHEN SQLCODE = -913                                     CL**1
00554                   CONTINUE                                           CL**1
00555              WHEN SQLCODE = +100                                     CL**1
00556                  SET PS-ERROR    TO  TRUE                            CL**1
00557              WHEN SQLWARN0 NOT = SPACES                              CL**1
00558              WHEN SQLCODE < 0                                        CL**1
00559              WHEN SQLCODE > 0                                        CL**1
00560                  MOVE '3310-VALIDATE-WORKER'                         CL**1
00561                                  TO  DP013-PARAGRAPH                 CL**1
00562                  MOVE 'VALIDATE WORKER NUMBER'                       CL**1
00563                                  TO  DP013-MESSAGE-TEXT(1)           CL**1
00564                  MOVE SQLCA      TO  DP013-SQLCA                     CL**1
00565                  MOVE 'TWORKER'  TO  DP013-DB2-TABLE-NAME(1)         CL**1
00566                  SET DP013-DB2-ABEND                                 CL**1
00567                                  TO  TRUE                            CL**1
00568                  SET DP013-NO-ROLLBACK                               CL**1
00569                                  TO  TRUE                            CL**1
00570                  SET DP013-XCTL-DISPLAY-RESTART                      CL**1
00571                                  TO  TRUE                            CL**1
00572                  PERFORM DP013-0000-PROCESS-ABEND                    CL**1
00573          END-EVALUATE                                                CL**1
00574      END-PERFORM.                                                    CL**1
00575                                                                      CL**1
00576      IF PV-RETRY-COUNTER > PC-MAX-RETRIES                            CL**1
00577          MOVE '3310-VALIDATE-WORKER'                                 CL**1
00578                                  TO  DP013-PARAGRAPH                 CL**1
00579          MOVE 'SELECT WORKER INFORMATION'                            CL**1
00580                                  TO  DP013-MESSAGE-TEXT(1)           CL**1
00581          MOVE 'MAX NUMBER OF RETRIES EXCEEDED'                       CL**1
00582                                  TO  DP013-MESSAGE-TEXT(2)           CL**1
00583          MOVE SQLCA              TO  DP013-SQLCA                     CL**1
00584          MOVE 'TWORKER'          TO  DP013-DB2-TABLE-NAME(1)         CL**1
00585          SET DP013-DB2-ABEND     TO  TRUE                            CL**1
00586          SET DP013-NO-ROLLBACK   TO  TRUE                            CL**1
00587          SET DP013-XCTL-DISPLAY-RESTART                              CL**1
00588                                  TO  TRUE                            CL**1
00589          PERFORM DP013-0000-PROCESS-ABEND                            CL**1
00590      END-IF.                                                         CL**1
00591                                                                      CL**1
00592      IF  PS-ROW-FOUND                                                CL**7
00593          PERFORM 3320-VALIDATE-RSPBAR                                CL**1
00594      ELSE                                                            CL**5
00595          MOVE DP015-RED          TO  AWORKERC                        CL**5
00596          MOVE DP015-REVERSE      TO  AWORKERH                        CL**5
00597          MOVE -1                 TO  AWORKERL                        CL**5
00598          MOVE PC-TSYMSG-00148    TO  DP020-MSG-NUMBER                CL**8
00599          SET DP020-MSG-FATAL     TO  TRUE                            CL**5
00600          SET PS-ERROR            TO  TRUE                            CL**5
00601      END-IF.                                                         CL**5
00602  EJECT                                                               CL**1
00603                                                                      CL**1
00604  3320-VALIDATE-RSPBAR.                                               CL**1
00605 ******************************************************************   CL**1
00606 *    ENSURE THE WORKER NUMBER ENTERED ON THE SCREEN IS ON THE        CL**1
00607 *    DATABASE.                                                       CL**1
00608 *                                                                    CL**1
00609 *    PERFORMED FROM 3310                                             CL**1
00610 ******************************************************************   CL**1
00611 *                                                                    CL*12
00612      IF  MB133-INCLUDE-HISTORY                                       CL*14
00613          MOVE PV-DB2-HISTORY-DATE                                    CL*12
00614                                TO  PV-DB2-DATE                       CL*12
00615      ELSE                                                            CL*12
00616          PERFORM DP017-0000-GET-CURR-DATE-TIME                       CL*12
00617          MOVE DP017-EDITED-DATE-YYYYMMDD                             CL*12
00618                                TO  PV-DB2-WORK-DATE                  CL*13
00619          SET PV-DW-DASH1       TO  TRUE                              CL*12
00620          SET PV-DW-DASH2       TO  TRUE                              CL*12
00621          MOVE PV-DB2-WORK-DATE TO  PV-DB2-DATE                       CL*12
00622      END-IF.                                                         CL*12
00623 *                                                                    CL*12
00624      PERFORM                                                         CL**1
00625          WITH TEST AFTER                                             CL**1
00626          VARYING PV-SQL-SUB                                          CL**1
00627          FROM 1 BY 1                                                 CL**1
00628          UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                CL**1
00629             OR SQLCODE = 0                                           CL**1
00630             OR SQLCODE = +100                                        CL**1
00631                                                                      CL**1
00632          EXEC SQL                                                    CL**1
00633            SELECT COUNT(DISTINCT A.MDSEAR_DKEY)                      CL**1
00634            INTO  :PV-DB2-COUNT                                       CL**1
00635            FROM TRSPBAR A,                                           CL**1
00636                 TWORKER W                                            CL**1
00637            WHERE A.OPERCO_NBR      =  :PC-COMPANY                    CL**1
00638              AND A.WORKER_NBR      =  W.WORKER_NBR                   CL**1
00639              AND A.WORKER_END_DTE  >  :PV-DB2-DATE                   CL*12
00640              AND W.UID_NBR         =  :WORKER-UID-NBR                CL**1
00641          END-EXEC                                                    CL**1
00642 *                                                                    CL**1
00643          EVALUATE TRUE                                               CL**1
00644              WHEN SQLCODE = -904                                     CL**1
00645              WHEN SQLCODE = -913                                     CL**1
00646              WHEN SQLCODE = ZERO                                     CL**1
00647              WHEN SQLCODE = +100                                     CL**1
00648                   CONTINUE                                           CL**1
00649              WHEN SQLWARN0 NOT EQUAL SPACE                           CL**1
00650              WHEN SQLCODE NOT EQUAL ZERO                             CL**1
00651                   MOVE '2150-DEPARTMENT-CHECK'                       CL**1
00652                                TO  DP013-PARAGRAPH                   CL**1
00653                   MOVE 'COUNT FUNCTION - DEPARTMENTS'                CL**1
00654                                TO  DP013-MESSAGE-TEXT (1)            CL**1
00655                   MOVE SQLCA   TO  DP013-SQLCA                       CL**1
00656                   SET DP013-DB2-ABEND                                CL**1
00657                                TO  TRUE                              CL**1
00658                   PERFORM DP013-0000-PROCESS-ABEND                   CL**1
00659          END-EVALUATE                                                CL**1
00660      END-PERFORM.                                                    CL**1
00661 *                                                                    CL**1
00662      IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                      CL**1
00663          MOVE '2150-DEPARTMENT-CHECK'                                CL**1
00664                                TO  DP013-PARAGRAPH                   CL**1
00665          MOVE 'MAX RETRIES EXCEEDED FOR COUNT - DEPARTMENTS'         CL**1
00666                                TO  DP013-MESSAGE-TEXT (1)            CL**1
00667          MOVE SQLCA            TO  DP013-SQLCA                       CL**1
00668          SET DP013-DB2-ABEND   TO  TRUE                              CL**1
00669          PERFORM DP013-0000-PROCESS-ABEND                            CL**1
00670      END-IF.                                                         CL**1
00671 *                                                                    CL**1
00672      IF  PV-DB2-COUNT EQUAL ZERO                                     CL**1
00673          MOVE DP015-RED          TO  AWORKERC                        CL**5
00674          MOVE DP015-REVERSE      TO  AWORKERH                        CL**5
00675          MOVE -1                 TO  AWORKERL                        CL**5
00676          MOVE PC-TSYMSG-00134    TO  DP020-MSG-NUMBER                CL**6
00677          SET DP020-MSG-FATAL     TO  TRUE                            CL**8
00678          SET PS-ERROR            TO  TRUE                            CL**5
00679      END-IF.                                                         CL**1
00680  EJECT                                                               CL**1
00681                                                                      CL**1
00682  3400-MOVE-COMMAREA-TO-SCREEN.                                       CL**1
00683 ******************************************************************   CL**1
00684 *    MOVE THE FIELDS FROM THE COMMAREA TO THE SCREEN.                CL**1
00685 *                                                                    CL**1
00686 *    PERFORMED BY 2200-DISPLAY-PANEL.                                CL**1
00687 ******************************************************************   CL**1
00688                                                                      CL**1
00689      MOVE PC-COMPANY             TO  AOPERCOO.                       CL**3
00690      MOVE MB133-WORKER           TO  AWORKERO.                       CL**1
00691      MOVE MB133-HISTORY-FLAG     TO  AHISTO.                         CL**1
00692                                                                      CL**1
00693  EJECT                                                               CL**1
00694                                                                      CL**1
00695  EJECT                                                               CL**1
00696  4000-NAVIGATE.                                                      CL**1
00697 ******************************************************************   CL**1
00698 *    SETUP THE INTER APPLICATION COMMAREA WITH DATA FROM THE         CL**1
00699 *    SPECIFIC COMMAREA AND SET THE INTER APPLICATION FORMAT CODE.    CL**1
00700 *                                                                    CL**1
00701 *    PERFORMED BY 3100-CHECK-LOCAL-FKEYS                             CL**1
00702 ******************************************************************   CL**1
00703                                                                      CL**1
00704      MOVE MB133-WORKER           TO  MB005-USERID.                   CL**1
00705      MOVE MB133-HISTORY-FLAG     TO  MB005-HISTORY-INDICATOR.        CL**1
00706      MOVE PC-COMPANY             TO  MB005-CO-NUMBER.                CL*10
00707      MOVE MB005-INTER-APPL-COMMAREA                                  CL**1
00708                                  TO  MB133-SAVE-INTER-APPL-COMM.     CL**1
00709      SET DP020-MB-WORKER-RSPBL-SELECT                                CL**9
00710                                  TO  TRUE.                           CL**9
00711      PERFORM DP015-1000-START-NAVIGATOR.                             CL**1
00712  EJECT                                                               CL**1
00713 ******************************************************************   CL**1
00714 *    FILL IN SCREEN HEADER MODULE                                    CL**1
00715 ******************************************************************   CL**1
00716                                                                      CL**1
00717      COPY DPPD002.                                                   CL**1
00718  EJECT                                                               CL**1
00719 ******************************************************************   CL**1
00720 *    RETURN TO CICS MODULE                                           CL**1
00721 ******************************************************************   CL**1
00722                                                                      CL**1
00723      COPY DPPD003.                                                   CL**1
00724  EJECT                                                               CL**1
00725 ******************************************************************   CL**1
00726 *    FORMAT MESSAGE LINES MODULE                                     CL**1
00727 ******************************************************************   CL**1
00728                                                                      CL**1
00729      COPY DPPD005.                                                   CL**1
00730  EJECT                                                               CL**1
00731 ******************************************************************   CL**1
00732 *    GET PANEL MODULE                                                CL**1
00733 ******************************************************************   CL**1
00734                                                                      CL**1
00735      COPY DPPD007.                                                   CL**1
00736  EJECT                                                               CL**1
00737 ******************************************************************   CL**1
00738 *    WRITE PANEL MODULE                                              CL**1
00739 ******************************************************************   CL**1
00740                                                                      CL**1
00741      COPY DPPD008.                                                   CL**1
00742  EJECT                                                               CL**1
00743 ******************************************************************   CL**1
00744 *    TOGGLE THE FUNCTION KEY LINE MODULE                             CL**1
00745 ******************************************************************   CL**1
00746                                                                      CL**1
00747      COPY DPPD009.                                                   CL**1
00748  EJECT                                                               CL**1
00749 ******************************************************************   CL**1
00750 *    ABEND PROCESSOR MODULE                                          CL**1
00751 ******************************************************************   CL**1
00752                                                                      CL**1
00753      COPY DPPD013.                                                   CL**1
00754  EJECT                                                               CL**1
00755 ******************************************************************   CL**1
00756 *    CHECK COMMUNICATION AREA MODULE                                 CL**1
00757 ******************************************************************   CL**1
00758                                                                      CL**1
00759      COPY DPPD015.                                                   CL**1
00760  EJECT                                                               CL**1
00761 ******************************************************************   CL**1
00762 *    UPDATE TIME AND DATE MODULE                                     CL**1
00763 ******************************************************************   CL**1
00764                                                                      CL**1
00765      COPY DPPD017.                                                   CL**1
00766  EJECT                                                               CL**1
