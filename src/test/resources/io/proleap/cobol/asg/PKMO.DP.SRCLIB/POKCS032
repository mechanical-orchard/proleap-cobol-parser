00001 *-----------------------*                                         02/25/94
00002  IDENTIFICATION DIVISION.                                         POKCS032
00003 *-----------------------*                                            LV001
00004  PROGRAM-ID.    POKCS032.                                         POKCS032
00005  AUTHOR.        DOUG NOLTNER - STRATAGEM.                         POKCS032
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          POKCS032
00007  DATE-WRITTEN.  09/93.                                            POKCS032
00008  DATE-COMPILED.                                                   POKCS032
00009 *----------------------------------------------------------------*POKCS032
00010 *    W032 - SHIPMENT INFOR INQUIRY                               *POKCS032
00011 *                                                                *POKCS032
00012 *    THIS PROGRAM ALLOWS THE USER TO VIEW SHIPMENT INFORMATION   *POKCS032
00013 *    FOR A PURCHASE ORDER.                                       *POKCS032
00014 *----------------------------------------------------------------*POKCS032
      * DATE     WHO      DESCRIPTION                                 *         
      * 063099   TKMA490  PITS/WR 10594    - PROJECT DESC             *         
      *                   ADD SEASONAL SET YEAR AND SHORT NAME TO     *         
      *                   SCREEN.                                     *         
      *---------------------------------------------------------------*         
      * DATE  11/08/00                                                 *        
      *   WR21649 REMOVE OBSELETE VMS ATTRIBUTES FROM PURCHASING       *        
00015 *----------------------------------------------------------------*POKCS032
C98147* DATE  11/07/14  JOHN SELWITSCHKA              - CHG0098147              
C98147*       RECOMPILED PROGRAM TO BRING IN NEW MAP WITH 8-DIGIT PO            
C98147*----------------------------------------------------------------*        
CICS  *  CHANGED 11/11/2022    BY BARB SCHULTZ    WR: CHG0278485       *POKCS032
CICS  *                                                                *POKCS032
CICS  *  ADDED COPYBOOK DPWS930 AND MADE THE CICS ARCHITECTURE CALL    *POKCS032
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.     *POKCS032
C98147*----------------------------------------------------------------*        
00016                                                                   POKCS032
00017  ENVIRONMENT DIVISION.                                            POKCS032
00018                                                                   POKCS032
00019                                                                   POKCS032
00020  DATA DIVISION.                                                   POKCS032
00021                                                                   POKCS032
00022  WORKING-STORAGE SECTION.                                         POKCS032
00023                                                                   POKCS032
00024  01  PC-PROGRAM-CONSTANTS.                                        POKCS032
00025      05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE  'POKCS032'.  POKCS032
00026      05  PC-CURRENT-MAP-NAME      PIC  X(08)  VALUE  'PO032A  '.  POKCS032
00027      05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE  'POKM032 '.  POKCS032
00028      05  PC-APPL-MAP              PIC S9(04)  VALUE  +1           POKCS032
00029                                               COMP.               POKCS032
00030      05  PC-MAX-RETRIES           PIC S9(04)  VALUE  +3           POKCS032
00031                                               COMP.               POKCS032
00032      05  PC-INIT-DATE             PIC  X(10)  VALUE '0001-01-01'. POKCS032
00033      05  PC-TSYMSG-NUMBERS.                                       POKCS032
00034          10  PC-TSYMSG-00083      PIC  9(05)  VALUE  00083.       POKCS032
00035 *                                                                 POKCS032
00036  01  PV-PROGRAM-VARIABLES.                                        POKCS032
00037      05  PV-CICS-RESPONSE         PIC S9(04)  VALUE +0            POKCS032
00038                                               COMP SYNC.          POKCS032
00039      05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0            POKCS032
00040                                               COMP SYNC.          POKCS032
           05  PV-SEASET-DATA.                                                  
               10  FILLER               PIC  X(05)  VALUE 'SET: '.              
               10  PV-SEASET-SHRT-NM    PIC  X(05)  VALUE SPACES.               
               10  FILLER               PIC  X(01)  VALUE SPACES.               
               10  PV-SEASET-YR         PIC  X(04)  VALUE SPACES.               
00041      EJECT                                                        POKCS032
00042 *----------------------------------------------------------------*POKCS032
00043 *    MAP LAYOUT                                                  *POKCS032
00044 *----------------------------------------------------------------*POKCS032
00045                                                                   POKCS032
00046      COPY POKM032.                                                POKCS032
00047                                                                   POKCS032
00048  EJECT                                                            POKCS032
00049 *----------------------------------------------------------------*POKCS032
00050 *    ATTRIBUTE SETTINGS COPYBOOK.                                *POKCS032
00051 *----------------------------------------------------------------*POKCS032
00052                                                                   POKCS032
00053      COPY DPWS015.                                                POKCS032
00054                                                                   POKCS032
00055 *----------------------------------------------------------------*POKCS032
00056 *    FUNCTION KEYS COPYBOOK                                      *POKCS032
00057 *----------------------------------------------------------------*POKCS032
00058                                                                   POKCS032
00059      COPY DPWS016.                                                POKCS032
00060                                                                   POKCS032
00061  EJECT                                                            POKCS032
00062 **--------------------------------------------------------------**POKCS032
00063 ** NUMERIC EDIT LAYOUT (DPKUT100)                               **POKCS032
00064 **--------------------------------------------------------------**POKCS032
00065      COPY DPWS010I.                                               POKCS032
00066  EJECT                                                            POKCS032
00067 **--------------------------------------------------------------**POKCS032
CICS  ** PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.            **POKCS032
00069 **--------------------------------------------------------------**POKCS032
00070      COPY DPWS030.                                                POKCS032
CICS       COPY DPWS930.                                                POKCS032
00071  EJECT                                                            POKCS032
00072 **--------------------------------------------------------------**POKCS032
00073 *    STANDARD COMMAREA.                                           POKCS032
00074 **--------------------------------------------------------------**POKCS032
00075                                                                   POKCS032
00076      COPY DPWS020.                                                POKCS032
00077      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                POKCS032
00078                                                                   POKCS032
00079 ***************************************************************** POKCS032
00080 *    INTER-APPLICATION COMMAREA.                                  POKCS032
00081 ***************************************************************** POKCS032
00082                                                                   POKCS032
00083          10  INTER-APPL-COMM-AREA PIC X(200).                     POKCS032
010500     COPY POWS011.                                                        
00084      COPY POWS017.                                                POKCS032
00085 ***************************************************************** POKCS032
00086 *    SPECIFIC COMMMAREA FOR POKCS032.                             POKCS032
00087 ***************************************************************** POKCS032
00088          10  ASC-SPECIFIC-COMMAREA.                               POKCS032
00089              15  ASC-PO-NBR                PIC S9(09)  COMP.      POKCS032
00090              15  ASC-DPT-NBR               PIC  X(03).            POKCS032
00091              15  ASC-ENT-NAME-DESC         PIC  X(30).            POKCS032
00092              15  ASC-SHIPT-POINT-TXT       PIC  X(20).            POKCS032
00093              15  ASC-FOB-POINT-TXT         PIC  X(20).            POKCS032
00097              15  ASC-BCKORDS-PERMTD-IND    PIC  X(01).            POKCS032
00098              15  ASC-FRGT-PAYT-CDE         PIC  X(01).            POKCS032
00099              15  ASC-VEND-FRGT-PCT         PIC S9(02)V99 COMP-3.  POKCS032
00100              15  ASC-KOHLS-FRGT-PCT        PIC S9(02)V99 COMP-3.  POKCS032
00101              15  ASC-SHIPT-DEST-NBR        PIC  9(03).            POKCS032
00102              15  ASC-STORE-NAME            PIC  X(40).            POKCS032
                   15  ASC-SEASET-DATA           PIC  X(15).                    
00103      EJECT                                                        POKCS032
00104 *----------------------------------------------------------------*POKCS032
00105 *    ABEND PROCESSING COPYBOOK.                                  *POKCS032
00106 *----------------------------------------------------------------*POKCS032
00107                                                                   POKCS032
00108      COPY DPWS013.                                                POKCS032
00109                                                                   POKCS032
00110  EJECT                                                            POKCS032
00111 *----------------------------------------------------------------*POKCS032
00112 *    DB2 AREA FOR PURCHASE ORDER                                 *POKCS032
00113 *----------------------------------------------------------------*POKCS032
00114      EXEC SQL                                                     POKCS032
00115           INCLUDE TPURCHS                                         POKCS032
00116      END-EXEC.                                                    POKCS032
00117 *----------------------------------------------------------------*POKCS032
00118 *    DB2 AREA FOR ENTERPRISE NAME                                *POKCS032
00119 *----------------------------------------------------------------*POKCS032
00120      EXEC SQL                                                     POKCS032
00121           INCLUDE TENTNME                                         POKCS032
00122      END-EXEC.                                                    POKCS032
00123 *----------------------------------------------------------------*POKCS032
00124 *    DB2 AREA FOR PLANNED SHIPMENTS                              *POKCS032
00125 *----------------------------------------------------------------*POKCS032
00126      EXEC SQL                                                     POKCS032
00127           INCLUDE TPLNSHP                                         POKCS032
00128      END-EXEC.                                                    POKCS032
00129 *----------------------------------------------------------------*POKCS032
00130 *    DB2 AREA FOR LOCATIONS                                      *POKCS032
00131 *----------------------------------------------------------------*POKCS032
00132      EXEC SQL                                                     POKCS032
00133           INCLUDE XMT00001                                        POKCS032
00134      END-EXEC.                                                    POKCS032
00135 *----------------------------------------------------------------*POKCS032
00136 *    DB2 AREA FOR COMMUNICATIONS                                 *POKCS032
00137 *----------------------------------------------------------------*POKCS032
00138      EXEC SQL                                                     POKCS032
00139           INCLUDE SQLCA                                           POKCS032
00140      END-EXEC.                                                    POKCS032
00141                                                                   POKCS032
00142  EJECT                                                            POKCS032
00143  LINKAGE SECTION.                                                 POKCS032
00144                                                                   POKCS032
00145  01  DFHCOMMAREA.                                                 POKCS032
00146      05  FILLER                         OCCURS  1 TO 4072 TIMES   POKCS032
00147                                         DEPENDING ON EIBCALEN.    POKCS032
00148          10  FILLER                     PIC X.                    POKCS032
00149                                                                   POKCS032
00150                                                                   POKCS032
00151  PROCEDURE DIVISION.                                              POKCS032
00152 ***************************************************************** POKCS032
00153 ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. ** POKCS032
00154 ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  ** POKCS032
00155 ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            ** POKCS032
00156 **                                                             ** POKCS032
00157 ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  ** POKCS032
00158 ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       ** POKCS032
00159 ***************************************************************** POKCS032
00160  0000-MAIN-MODULE.                                                POKCS032
00161      INITIALIZE DP030-CICS-API-FIELDS.                            POKCS032
00162      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       POKCS032
00163      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          POKCS032
00164      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         POKCS032
00165      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       POKCS032
00166      MOVE LENGTH OF PO032AI        TO DP030-MAP-LENGTH (1).       POKCS032
00167      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     POKCS032
00168      PERFORM 0001-CALL-CICS-ARCH-API.                             POKCS032
00169 *                                                                 POKCS032
00170      PERFORM 1000-CONTROL-PROCESSING                              POKCS032
00171 *                                                                 POKCS032
00172      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     POKCS032
00173      PERFORM 0001-CALL-CICS-ARCH-API.                             POKCS032
00174                                                                   POKCS032
00175  0001-CALL-CICS-ARCH-API.                                         POKCS032
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                      POKCS032
00177                                     DFHCOMMAREA                   POKCS032
00178                                     DP030-CICS-API-FIELDS         POKCS032
00179                                     DP020-STANDARD-COMMAREA       POKCS032
00180                                     PO032AI.                      POKCS032
00181 *                                                                 POKCS032
00182      IF  DP030-RC-CALL-SUCCESSFUL                                 POKCS032
00183          CONTINUE                                                 POKCS032
00184      ELSE                                                         POKCS032
00185          SET DP013-NO-ROLLBACK                                    POKCS032
00186              DP013-XCTL-DISPLAY-RESTART                           POKCS032
00187              DP013-CICS-ABEND      TO TRUE                        POKCS032
CICS           MOVE '0001-CALL-CICS-ARCH-API'                           POKCS032
00189                                    TO DP013-PARAGRAPH             POKCS032
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE OPOKCS032
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)      POKCS032
00192          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)      POKCS032
00193          PERFORM DP013-0000-PROCESS-ABEND                         POKCS032
00194      END-IF.                                                      POKCS032
00195  EJECT                                                            POKCS032
00196 *----------------------------------------------------------------*POKCS032
00197 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *POKCS032
00198 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *POKCS032
00199 *----------------------------------------------------------------*POKCS032
00200                                                                   POKCS032
00201  1000-CONTROL-PROCESSING.                                         POKCS032
00202                                                                   POKCS032
00203      EVALUATE TRUE                                                POKCS032
00204          WHEN DP020-NEXT-ACT-INITIAL                              POKCS032
00205              PERFORM 1100-FIND-OBJECT-KEYS                        POKCS032
00206              PERFORM 4000-BUILD-INITIAL-PANEL                     POKCS032
00207                                                                   POKCS032
00208          WHEN DP020-NEXT-ACT-READ-MAP                             POKCS032
00209              PERFORM 2000-PROCESS-PANEL                           POKCS032
026000                                                                          
031700         WHEN DP020-NEXT-ACT-RETURN                               02540000
031800             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 02550000
00210                                                                   POKCS032
00211          WHEN OTHER                                               POKCS032
00212              SET DP013-LOGIC-ABEND TO TRUE                        POKCS032
00213              SET DP013-NO-ROLLBACK TO TRUE                        POKCS032
00214              MOVE '1000-CONTROL-PROCESSING'                       POKCS032
00215                                    TO DP013-PARAGRAPH             POKCS032
00216              MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   POKCS032
00217                                    TO DP013-MESSAGE-TEXT(1)       POKCS032
00218              MOVE DP020-NEXT-APPL-ACTIVITY                        POKCS032
00219                                    TO DP013-MESSAGE-TEXT(2)       POKCS032
00220              PERFORM DP013-0000-PROCESS-ABEND                     POKCS032
00221      END-EVALUATE.                                                POKCS032
00222 ******************************************************************POKCS032
00223 ** DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE   **POKCS032
00224 ** INTER-APPLICATION FORMAT CODE.                               **POKCS032
00225 ******************************************************************POKCS032
00226  1100-FIND-OBJECT-KEYS.                                           POKCS032
00227      INITIALIZE ASC-SPECIFIC-COMMAREA.                            POKCS032
00228      IF  DP020-NAV-KEY-DATA > SPACE                               POKCS032
00229          MOVE DP020-NAV-KEY-DATA   TO ASC-PO-NBR                  POKCS032
00230      ELSE                                                         POKCS032
00231          MOVE PO017-PO-NBR         TO ASC-PO-NBR.                 POKCS032
00232  EJECT                                                            POKCS032
00233 *----------------------------------------------------------------*POKCS032
00234 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *POKCS032
00235 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *POKCS032
00236 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *POKCS032
00237 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *POKCS032
00238 * FUNCTION KEYS CAN GET THIS FAR.                                *POKCS032
00239 *----------------------------------------------------------------*POKCS032
00240                                                                   POKCS032
00241  2000-PROCESS-PANEL.                                              POKCS032
00242                                                                   POKCS032
00243      EVALUATE TRUE                                                POKCS032
00244                                                                   POKCS032
00245          WHEN DP020-SRC-AID = DP016-CLEAR                         POKCS032
00246              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 POKCS032
00247                                                                   POKCS032
00248          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     POKCS032
00249              SET DP030-DONT-ERASE                                 POKCS032
00250                  DP030-SEND-DATAONLY (PC-APPL-MAP)                POKCS032
00251                                    TO TRUE                        POKCS032
00252              PERFORM 4000-BUILD-INITIAL-PANEL                     POKCS032
00253                                                                   POKCS032
00254          WHEN OTHER                                               POKCS032
00255              PERFORM 2100-CHECK-FUNCTION-KEY                      POKCS032
00256              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 POKCS032
00257      END-EVALUATE.                                                POKCS032
00258 *----------------------------------------------------------------*POKCS032
00259 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED       *POKCS032
00260 * FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED   *POKCS032
00261 * FROM THE CICS ARCHITECTURE API.                                *POKCS032
00262 *----------------------------------------------------------------*POKCS032
00263                                                                   POKCS032
00264  2100-CHECK-FUNCTION-KEY.                                         POKCS032
00265      EVALUATE TRUE                                                POKCS032
00266          WHEN DP020-SRC-AID = DP016-ENTER                         POKCS032
00267              CONTINUE                                             POKCS032
               WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
                   MOVE ASC-PO-NBR       TO PO011-PO-NBR                        
                   MOVE ASC-DPT-NBR      TO PO011-DEPT-NBR                      
                   MOVE ASC-ENT-NAME-DESC                                       
                                         TO PO011-VENDOR-NAME                   
                   SET DP030-CONTINUE-GO TO TRUE                                
00268          WHEN OTHER                                               POKCS032
00269              SET DP013-NO-ROLLBACK                                POKCS032
00270                  DP013-XCTL-DISPLAY-RESTART                       POKCS032
00271                  DP013-CICS-ABEND  TO TRUE                        POKCS032
00272              MOVE '2100-CHECK-FUNCTION-KEY'                       POKCS032
00273                                    TO DP013-PARAGRAPH             POKCS032
00274              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      POKCS032
00275                                    TO DP013-MESSAGE-TEXT (1)      POKCS032
00276              PERFORM DP013-0000-PROCESS-ABEND                     POKCS032
00277      END-EVALUATE.                                                POKCS032
00278                                                                   POKCS032
00279 *----------------------------------------------------------------*POKCS032
00280 *    VALIDATE THE QUEUE WAS PASSED AND DISPLAY FOR THE FIRST     *POKCS032
00281 *    ENTRY.                                                      *POKCS032
00282 *----------------------------------------------------------------*POKCS032
00283                                                                   POKCS032
00284  4000-BUILD-INITIAL-PANEL.                                        POKCS032
00285      PERFORM 4100-GET-SHIP.                                       POKCS032
00286      IF SQLCODE = +100                                            POKCS032
00287          SET DP020-NEXT-ACT-APPL-ERROR                            POKCS032
00288                                    TO TRUE                        POKCS032
00289          MOVE PC-TSYMSG-00083      TO DP020-MSG-NUMBER            POKCS032
00290          SET DP020-MSG-INFORMATIONAL                              POKCS032
00291                                    TO TRUE                        POKCS032
00292      ELSE                                                         POKCS032
00293          MOVE PURCHS-DEPT-NBR      TO ASC-DPT-NBR                 POKCS032
               IF PURCHS-SEASET-YR > SPACES                                     
                   MOVE PURCHS-SEASET-YR TO PV-SEASET-YR                        
                   MOVE PURCHS-SEASET-SHRT-NM                                   
                                         TO PV-SEASET-SHRT-NM                   
                   MOVE PV-SEASET-DATA   TO ASC-SEASET-DATA                     
               ELSE                                                             
                   MOVE SPACES           TO ASC-SEASET-DATA                     
               END-IF                                                           
00294          MOVE ENTNME-ENT-NAME-DESC TO ASC-ENT-NAME-DESC           POKCS032
00295          MOVE PLNSHP-SHIPT-POINT-TXT                              POKCS032
00296                                    TO ASC-SHIPT-POINT-TXT         POKCS032
00297          MOVE PLNSHP-FOB-POINT-TXT                                POKCS032
00298                                    TO ASC-FOB-POINT-TXT           POKCS032
00305          MOVE PLNSHP-BCKORDS-PERMTD-IND                           POKCS032
00306                                    TO ASC-BCKORDS-PERMTD-IND      POKCS032
00307          MOVE PLNSHP-FRGT-PAYT-CDE TO ASC-FRGT-PAYT-CDE           POKCS032
00308          COMPUTE ASC-VEND-FRGT-PCT =                              POKCS032
00309               PLNSHP-VEND-FRGT-PCT * 100                          POKCS032
00310          COMPUTE ASC-KOHLS-FRGT-PCT =                             POKCS032
00311               PLNSHP-KOHLS-FRGT-PCT * 100                         POKCS032
00312 *        MOVE PLNSHP-SHIPT-DEST-NBR                               POKCS032
00313 *                                  TO ASC-SHIPT-DEST-NBR          POKCS032
00314 *        IF PLNSHP-SHIPT-DEST-NBR = ZERO                          POKCS032
00315 *            MOVE 'DIRECT TO STORE'                               POKCS032
00316 *                                  TO ASC-STORE-NAME              POKCS032
00317 *        ELSE                                                     POKCS032
00318 *            PERFORM 4200-GET-STORE                               POKCS032
00319 *            IF SQLCODE = +100                                    POKCS032
00320 *                MOVE SPACES       TO ASC-STORE-NAME              POKCS032
00321 *            ELSE                                                 POKCS032
00322 *                MOVE TSTR000-STORE-NAME                          POKCS032
00323 *                                  TO ASC-STORE-NAME              POKCS032
00324 *            END-IF                                               POKCS032
00325 *        END-IF                                                   POKCS032
00326          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     POKCS032
00327      END-IF.                                                      POKCS032
00328                                                                   POKCS032
00329 *----------------------------------------------------------------*POKCS032
00330 * GET SHIPMENT SCREEN FIELDS                                     *POKCS032
00331 *----------------------------------------------------------------*POKCS032
00332                                                                   POKCS032
00333  4100-GET-SHIP.                                                   POKCS032
00334      PERFORM                                                      POKCS032
00335          WITH TEST AFTER                                          POKCS032
00336          VARYING PV-RETRY-COUNTER                                 POKCS032
00337          FROM 1 BY 1                                              POKCS032
00338          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               POKCS032
00339              OR (SQLCODE = ZERO)                                  POKCS032
00340              OR (SQLCODE = +100))                                 POKCS032
00341              EXEC SQL                                             POKCS032
00342                  SELECT A.DEPT_NBR,                               POKCS032
                              A.SEASET_YR,                                      
                              A.SEASET_SHRT_NM,                                 
00343                         B.ENT_NAME_DESC,                          POKCS032
00344                         C.SHIPT_POINT_TXT,                        POKCS032
00345                         C.FOB_POINT_TXT,                          POKCS032
00346                         C.LATE_SHIPT_IND,                         POKCS032
00347                         C.EARLY_SHIPT_IND,                        POKCS032
00348                         C.PARTIAL_SHIPT_IND,                      POKCS032
00349                         C.BCKORDS_PERMTD_IND,                     POKCS032
00350                         C.FRGT_PAYT_CDE,                          POKCS032
00351                         C.VEND_FRGT_PCT,                          POKCS032
00352                         C.KOHLS_FRGT_PCT,                         POKCS032
00353                         C.SHIPT_DEST_NBR                          POKCS032
00354                  INTO :PURCHS-DEPT-NBR,                           POKCS032
                            :PURCHS-SEASET-YR,                                  
                            :PURCHS-SEASET-SHRT-NM,                             
00355                       :ENTNME-ENT-NAME-DESC,                      POKCS032
00356                       :PLNSHP-SHIPT-POINT-TXT,                    POKCS032
00357                       :PLNSHP-FOB-POINT-TXT,                      POKCS032
00358                       :PLNSHP-LATE-SHIPT-IND,                     POKCS032
00359                       :PLNSHP-EARLY-SHIPT-IND,                    POKCS032
00360                       :PLNSHP-PARTIAL-SHIPT-IND,                  POKCS032
00361                       :PLNSHP-BCKORDS-PERMTD-IND,                 POKCS032
00362                       :PLNSHP-FRGT-PAYT-CDE,                      POKCS032
00363                       :PLNSHP-VEND-FRGT-PCT,                      POKCS032
00364                       :PLNSHP-KOHLS-FRGT-PCT,                     POKCS032
00365                       :PLNSHP-SHIPT-DEST-NBR                      POKCS032
00366                  FROM TPURCHS A,                                  POKCS032
00367                       TENTNME B,                                  POKCS032
00368                       TPLNSHP C                                   POKCS032
00369                  WHERE A.PO_NBR = :ASC-PO-NBR                     POKCS032
00370                    AND A.VER_STATUS_CDE = 'A'                     POKCS032
00371                    AND B.ENT_ID = A.ENT_ID                        POKCS032
00372                    AND B.TYPE_CDE = '01'                          POKCS032
00373                    AND C.PO_NBR = A.PO_NBR                        POKCS032
00374                    AND C.VER_STATUS_CDE = A.VER_STATUS_CDE        POKCS032
00375              END-EXEC                                             POKCS032
00376          EVALUATE TRUE                                            POKCS032
00377              WHEN SQLCODE = ZERO                                  POKCS032
00378              WHEN SQLCODE = +100                                  POKCS032
00379              WHEN SQLCODE = -904                                  POKCS032
00380              WHEN SQLCODE = -913                                  POKCS032
00381                  CONTINUE                                         POKCS032
00382              WHEN SQLWARN0 NOT = SPACES                           POKCS032
00383              WHEN SQLCODE < ZERO                                  POKCS032
00384              WHEN SQLCODE > ZERO                                  POKCS032
00385                  MOVE '4100-GET-SHIP  '                           POKCS032
00386                                    TO DP013-PARAGRAPH             POKCS032
00387                  MOVE 'SQL ERROR SELECTING PO'                    POKCS032
00388                                    TO DP013-MESSAGE-TEXT (1)      POKCS032
00389                  MOVE SQLCA        TO DP013-SQLCA                 POKCS032
00390                  MOVE 'TPURCHS '   TO DP013-DB2-TABLE-NAME (1)    POKCS032
00391                  SET DP013-DB2-ABEND                              POKCS032
00392                      DP013-XCTL-DISPLAY-RESTART TO TRUE           POKCS032
00393                  PERFORM DP013-0000-PROCESS-ABEND                 POKCS032
00394          END-EVALUATE                                             POKCS032
00395      END-PERFORM.                                                 POKCS032
00396 *                                                                 POKCS032
00397      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        POKCS032
00398          MOVE '4100-GET-SHIP  '    TO DP013-PARAGRAPH             POKCS032
00399          MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT PO'  POKCS032
00400                                    TO DP013-MESSAGE-TEXT (1)      POKCS032
00401          MOVE SQLCA                TO DP013-SQLCA                 POKCS032
00402          MOVE 'TPURCHS '           TO DP013-DB2-TABLE-NAME (1)    POKCS032
00403          SET DP013-DB2-ABEND                                      POKCS032
00404              DP013-XCTL-DISPLAY-RESTART                           POKCS032
00405                                    TO TRUE                        POKCS032
00406          PERFORM DP013-0000-PROCESS-ABEND                         POKCS032
00407      END-IF.                                                      POKCS032
00408                                                                   POKCS032
00409 *----------------------------------------------------------------*POKCS032
00410 * GET STORE NAME                                                 *POKCS032
00411 *----------------------------------------------------------------*POKCS032
00412                                                                   POKCS032
00413  4200-GET-STORE.                                                  POKCS032
00414      PERFORM                                                      POKCS032
00415          WITH TEST AFTER                                          POKCS032
00416          VARYING PV-RETRY-COUNTER                                 POKCS032
00417          FROM 1 BY 1                                              POKCS032
00418          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               POKCS032
00419              OR (SQLCODE = ZERO)                                  POKCS032
00420              OR (SQLCODE = +100))                                 POKCS032
00421              EXEC SQL                                             POKCS032
00422                  SELECT LOC_NM                                    POKCS032
00423                  INTO :XMT00001-LOC-NM                            POKCS032
00424                  FROM XMT_LOC                                     POKCS032
00425                  WHERE LOC_NBR =  :PLNSHP-SHIPT-DEST-NBR          POKCS032
00428              END-EXEC                                             POKCS032
00429          EVALUATE TRUE                                            POKCS032
00430              WHEN SQLCODE = ZERO                                  POKCS032
00431              WHEN SQLCODE = +100                                  POKCS032
00432              WHEN SQLCODE = -904                                  POKCS032
00433              WHEN SQLCODE = -913                                  POKCS032
00434                  CONTINUE                                         POKCS032
00435              WHEN SQLWARN0 NOT = SPACES                           POKCS032
00436              WHEN SQLCODE < ZERO                                  POKCS032
00437              WHEN SQLCODE > ZERO                                  POKCS032
00438                  MOVE '4200-GET-STORE '                           POKCS032
00439                                    TO DP013-PARAGRAPH             POKCS032
00440                  MOVE 'SQL ERROR SELECTING STORE'                 POKCS032
00441                                    TO DP013-MESSAGE-TEXT (1)      POKCS032
00442                  MOVE SQLCA        TO DP013-SQLCA                 POKCS032
00443                  MOVE 'XMT_LOC '   TO DP013-DB2-TABLE-NAME (1)    POKCS032
00444                  SET DP013-DB2-ABEND                              POKCS032
00445                      DP013-XCTL-DISPLAY-RESTART TO TRUE           POKCS032
00446                  PERFORM DP013-0000-PROCESS-ABEND                 POKCS032
00447          END-EVALUATE                                             POKCS032
00448      END-PERFORM.                                                 POKCS032
00449 *                                                                 POKCS032
00450      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        POKCS032
00451          MOVE '4200-GET-STORE '    TO DP013-PARAGRAPH             POKCS032
00452          MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT PO'  POKCS032
00453                                    TO DP013-MESSAGE-TEXT (1)      POKCS032
00454          MOVE SQLCA                TO DP013-SQLCA                 POKCS032
00455          MOVE 'XMT_LOC '           TO DP013-DB2-TABLE-NAME (1)    POKCS032
00456          SET DP013-DB2-ABEND                                      POKCS032
00457              DP013-XCTL-DISPLAY-RESTART                           POKCS032
00458                                    TO TRUE                        POKCS032
00459          PERFORM DP013-0000-PROCESS-ABEND                         POKCS032
00460      END-IF.                                                      POKCS032
00461                                                                   POKCS032
00462                                                                   POKCS032
00463 *----------------------------------------------------------------*POKCS032
00464 *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *POKCS032
00465 *----------------------------------------------------------------*POKCS032
00466                                                                   POKCS032
00467  4400-MOVE-COMMAREA-TO-SCREEN.                                    POKCS032
00468      MOVE ASC-PO-NBR               TO APONUMO.                    POKCS032
00469      MOVE ASC-DPT-NBR              TO ADEPTO.                     POKCS032
           MOVE ASC-SEASET-DATA          TO ASEASETO.                           
00470      MOVE ASC-ENT-NAME-DESC        TO AVENDNMO.                   POKCS032
00471      MOVE ASC-SHIPT-POINT-TXT      TO ASHPOPTO.                   POKCS032
00472      MOVE ASC-FOB-POINT-TXT        TO AFOBPTO.                    POKCS032
00488      IF ASC-BCKORDS-PERMTD-IND = 'Y'                              POKCS032
00489          MOVE 'YES'                TO ABACKPRO                    POKCS032
00490      ELSE                                                         POKCS032
00491          MOVE 'NO '                TO ABACKPRO                    POKCS032
00492      END-IF.                                                      POKCS032
00493      IF ASC-FRGT-PAYT-CDE = 'K'                                   POKCS032
00494          MOVE 'KOHLS'              TO AFRTPAYO                    POKCS032
00495      ELSE                                                         POKCS032
00496          IF ASC-FRGT-PAYT-CDE = 'V'                               POKCS032
00497              MOVE 'VENDOR'         TO AFRTPAYO                    POKCS032
00498          ELSE                                                     POKCS032
00499              MOVE 'BOTH'           TO AFRTPAYO                    POKCS032
00500          END-IF                                                   POKCS032
00501      END-IF.                                                      POKCS032
00502      MOVE ASC-VEND-FRGT-PCT        TO AVNDFPTO.                   POKCS032
00503      MOVE ASC-KOHLS-FRGT-PCT       TO AKHLFPTO.                   POKCS032
00504                                                                   POKCS032
00505  EJECT                                                            POKCS032
00506 *----------------------------------------------------------------*POKCS032
00507 *    ABEND PROCESSOR MODULE                                      *POKCS032
00508 *----------------------------------------------------------------*POKCS032
00509                                                                   POKCS032
00510      COPY DPPD013.                                                POKCS032
