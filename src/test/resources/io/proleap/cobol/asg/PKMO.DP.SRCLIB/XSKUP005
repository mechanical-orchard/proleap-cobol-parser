       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP005.                                         00020000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/29/2002.                                       00050040
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **       VSM ITEM MESSAGE - NUMBER 005 - STYLE ADD                00080040
      ******************************************************************00090024
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO ADD A NEW STYLE ID TO  00100040
      ***     TABLE XST_VND_STYL.                                       00110040
      ******************************************************************00120000
      **09/29/2002 CHANGED BY:JULIE SMITH  PROGRAM WRITE                00130057
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00140057
092704**09/27/2004 CHANGED BY:JULIE TEWES  ADDING COLUMN VS_DESC        00150060
      ******************************************************************00160000
       ENVIRONMENT DIVISION.                                            00170000
       CONFIGURATION SECTION.                                           00180000
       SOURCE-COMPUTER.  IBM-3090.                                      00190000
       OBJECT-COMPUTER.  IBM-3090.                                      00200000
       DATA DIVISION.                                                   00210000
                                                                        00220017
       WORKING-STORAGE SECTION.                                         00230017
      ******************************************************************00240000
      **PROGRAM SWITCHES                                                00250000
      ******************************************************************00260000
       01  PS-PROGRAM-SWITCHES.                                         00270000
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00280020
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00290018
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00300018
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00310046
               88  PS-ACTION-REVERSE                  VALUE 01.         00320047
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00330045
               88  PS-ACTION-NOTHING                  VALUE 02.         00340046
      ******************************************************************00350000
      **PROGRAM COUNTERS                                                00360018
      ******************************************************************00370000
       01  PC-PROGRAM-COUNTERS.                                         00380018
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00390018
           05  PC-RETRY-COUNTER-2          PIC 9(01)  VALUE ZEROS.      00400019
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00410024
      ******************************************************************00420018
      **PROGRAM VARIABLES                                               00430018
      ******************************************************************00440018
       01  PV-PROGRAM-VARIABLES.                                        00450018
           05  PV-PARAGRAPH-NAME           PIC X(20)  VALUE SPACES.     00460043
           05  PV-TIME-EVALUATE-INSERT.                                 00470019
               10  PV-TIME-HOUR-IN         PIC 9(02) VALUE ZEROS.       00480040
               10  PV-TIME-MINUTE-IN       PIC 9(02) VALUE ZEROS.       00490040
               10  PV-TIME-SECOND-IN       PIC 9(02) VALUE ZEROS.       00500040
               10  PV-TIME-MICROSEC-IN     PIC 9(06) VALUE ZEROS.       00510040
           05  PV-TIME-EVALUATE-UPDATE.                                 00520019
               10  PV-TIME-HOUR-UP         PIC 9(02) VALUE ZEROS.       00530040
               10  PV-TIME-MINUTE-UP       PIC 9(02) VALUE ZEROS.       00540040
               10  PV-TIME-SECOND-UP       PIC 9(02) VALUE ZEROS.       00550040
               10  PV-TIME-MICROSEC-UP     PIC 9(06) VALUE ZEROS.       00560040
      ******************************************************************00570018
      **TIMESTAMP VARIABLES                                             00580018
      ******************************************************************00590018
       01  SD-TIMESTAMP                    PIC X(26)  VALUE SPACES.     00600040
       01  FILLER REDEFINES SD-TIMESTAMP.                               00610018
           05  SD-CURR-DTE                 PIC X(10).                   00620041
           05  FILLER                      PIC X(01).                   00630041
           05  SD-HOUR                     PIC X(02).                   00640041
           05  FILLER                      PIC X(01).                   00650041
           05  SD-MINUTE                   PIC X(02).                   00660041
           05  FILLER                      PIC X(01).                   00670041
           05  SD-SECOND                   PIC X(02).                   00680041
           05  FILLER                      PIC X(01).                   00690041
           05  SD-MICROSEC                 PIC X(06).                   00700041
      ******************************************************************00710000
      **COPYBOOKS                                                       00720000
      ******************************************************************00730000
      ***DB2 MESSAGE AREA                                               00740000
           COPY SQLCA2.                                                 00750060
      ***MESSAGE COPYBOOK                                               00760018
           COPY IMRD008.                                                00770060
      ***DATE ROUTINE COPYBOOK                                          00780019
           COPY DPWS500.                                                00790060
      ******************************************************************00800000
      **TABLE INCLUSIONS                                                00810000
      ******************************************************************00820000
      ***DB2 COMMUNICATIONS AREA                                        00830000
           EXEC SQL                                                     00840000
                INCLUDE SQLCA                                           00850000
           END-EXEC.                                                    00860000
      ***XST_VND_STYL TABLE                                             00870000
           EXEC SQL                                                     00880000
                INCLUDE XST00013                                        00890017
           END-EXEC.                                                    00900000
                                                                        00910000
       LINKAGE SECTION.                                                 00920000
      ******************************************************************00930000
      **LINKAGE VARIABLES                                               00940000
      ******************************************************************00950000
       01  LV-IMRD008-RECORD               PIC X(450).                  00960018
       01  LV-RETURN-CODE                  PIC S9(04).                  00970017
       01  LV-ACTION-IND                   PIC 9(02).                   00980049
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00990052
                                                                        01000024
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      01010019
                                LV-RETURN-CODE,                         01020044
                                LV-ACTION-IND,                          01030052
                                LV-CURRENT-TIMESTAMP.                   01040052
      ******************************************************************01050000
       0000-MAINLINE.                                                   01060000
                                                                        01070000
            PERFORM 1000-INITIALIZATION.                                01080000
            PERFORM 2000-PROCESS-MESSAGE                                01090040
                    UNTIL PS-PROCESS-COMPLETE.                          01100018
            PERFORM 9999-WRAPUP.                                        01110000
                                                                        01120000
       EJECT.                                                           01130018
      ***************************************************************** 01140000
      **INITIALIZATION AREA - ALL DCLGEN MOVES AND FIELD INITIALIZATION 01150059
      ***************************************************************** 01160000
       1000-INITIALIZATION.                                             01170000
                                                                        01180000
           MOVE '1000-INITIALIZATION'      TO PV-PARAGRAPH-NAME.        01190018
                                                                        01200018
           SET PS-PROCESS-CONTINUE         TO TRUE.                     01210020
           INITIALIZE PC-RETRY-COUNTER                                  01220044
                      PS-ACTION-IND.                                    01230045
                                                                        01240023
           MOVE LV-IMRD008-RECORD          TO IMRD008-RECORD.           01250023
                                                                        01260019
      ***SET UP TIMESTAMP IN CASE DATE COMPARISON IS NEEDED             01270019
           MOVE IM008-CRE-TMST             TO SD-TIMESTAMP.             01280019
           MOVE SD-HOUR                    TO PV-TIME-HOUR-IN.          01290023
           MOVE SD-MINUTE                  TO PV-TIME-MINUTE-IN.        01300023
           MOVE SD-SECOND                  TO PV-TIME-SECOND-IN.        01310023
           MOVE SD-MICROSEC                TO PV-TIME-MICROSEC-IN.      01320023
           INITIALIZE SD-TIMESTAMP.                                     01330019
                                                                        01340018
           MOVE IM008-ITEM-KEY-RMS-STYL-ID TO XST00013-STYL-ID.         01350015
           MOVE IM008-ITEM-KEY-DEPT        TO XST00013-DEPT-NBR.        01360015
           MOVE IM008-ITEM-KEY-CLASS       TO XST00013-MAJ-CL-NBR.      01370015
           MOVE IM008-ITEM-KEY-SUBCL       TO XST00013-SUB-CL-NBR.      01380015
           MOVE IM008-ITEM-KEY-VS-ID       TO XST00013-VS-ID.           01390015
           MOVE IM008-ITEM-KEY-VS-NBR      TO XST00013-VS-NBR.          01400015
           MOVE IM008-ITEM-KEY-ENT-ID      TO XST00013-ENT-ID.          01410015
           MOVE IM008-STYLE-ADD-STKR-NBR   TO XST00013-STKR-NBR.        01420015
           MOVE IM008-STYLE-ADD-RTL-CLSN-DESC TO XST00013-RTL-CLSN-DESC.01430042
           MOVE IM008-STYLE-ADD-LBL-DESC   TO XST00013-LBL-NM.          01440042
           MOVE IM008-STYLE-ADD-ESL-DESC   TO XST00013-ESL-IND.         01450042
           MOVE IM008-STYLE-ADD-HNG-FOLD-DESC TO XST00013-HNG-FOLD-DESC.01460042
092704     MOVE IM008-STYLE-ADD-VS-DESC    TO XST00013-VS-DESC.         01470060
121402     MOVE LV-CURRENT-TIMESTAMP       TO XST00013-LAST-UPD-TMST.   01480057
121402     MOVE IM008-CRE-TMST            TO XST00013-SOR-LAST-MSG-TMST.01490057
                                                                        01500000
       EJECT.                                                           01510018
      ***************************************************************** 01520001
      **INSERT NEW STYLE ID INTO TABLE XST_VND_STYL                     01530059
      ***************************************************************** 01540001
       2000-PROCESS-MESSAGE.                                            01550040
                                                                        01560000
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01570040
                                                                        01580017
           EXEC SQL                                                     01590018
                INSERT INTO XST_VND_STYL ( STYL_ID                      01600018
                                         , DEPT_NBR                     01610018
                                         , MAJ_CL_NBR                   01620018
                                         , SUB_CL_NBR                   01630018
                                         , VS_ID                        01640018
                                         , VS_NBR                       01650018
                                         , ENT_ID                       01660018
                                         , STKR_NBR                     01670018
                                         , RTL_CLSN_DESC                01680018
                                         , LBL_NM                       01690018
                                         , ESL_IND                      01700018
                                         , HNG_FOLD_DESC                01710018
                                         , LAST_UPD_TMST                01720057
121402                                   , SOR_LAST_MSG_TMST            01730060
092704                                   , VS_DESC)                     01740060
                VALUES ( :XST00013-STYL-ID                              01750018
                       , :XST00013-DEPT-NBR                             01760018
                       , :XST00013-MAJ-CL-NBR                           01770018
                       , :XST00013-SUB-CL-NBR                           01780018
                       , :XST00013-VS-ID                                01790018
                       , :XST00013-VS-NBR                               01800018
                       , :XST00013-ENT-ID                               01810018
                       , :XST00013-STKR-NBR                             01820018
                       , :XST00013-RTL-CLSN-DESC                        01830018
                       , :XST00013-LBL-NM                               01840018
                       , :XST00013-ESL-IND                              01850018
                       , :XST00013-HNG-FOLD-DESC                        01860018
                       , :XST00013-LAST-UPD-TMST                        01870057
121402                 , :XST00013-SOR-LAST-MSG-TMST                    01880060
092704                 , :XST00013-VS-DESC)                             01890066
           END-EXEC.                                                    01900018
                                                                        01910004
           EVALUATE TRUE                                                01920020
      ***SUCCESSFUL PROCESSING                                          01930059
              WHEN SQLCODE = ZEROS                                      01940020
                   SET PS-PROCESS-COMPLETE   TO TRUE                    01950018
      ***REFERENTIAL INTEGRITY ISSUE                                    01960059
              WHEN SQLCODE = -530                                       01970020
              WHEN SQLCODE = -532                                       01980059
                   SET PS-PROCESS-COMPLETE   TO TRUE                    01990018
      ***ROW ALREADY EXISTS ON TABLE                                    02000059
              WHEN SQLCODE = -803                                       02010020
                   PERFORM 2100-SELECT-DATA                             02020018
      ***ROW/TABLE LOCK                                                 02030059
              WHEN SQLCODE = -913                                       02040028
              WHEN SQLCODE = -911                                       02050028
                   ADD +1                      TO PC-RETRY-COUNTER      02060027
                   IF  PC-RETRY-COUNTER >= PC-RETRY-MAX                 02070031
                       SET PS-PROCESS-COMPLETE TO TRUE                  02080018
                   ELSE                                                 02090018
                       ADD +1                  TO PC-RETRY-COUNTER      02100027
                   END-IF                                               02110018
              WHEN OTHER                                                02120018
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02130018
           END-EVALUATE.                                                02140018
                                                                        02150001
       EJECT.                                                           02160018
      ******************************************************************02170018
      *SELECT TIMESTAMP FOR EXISTING RECORD FROM TIME COMPARISON        02180059
      ******************************************************************02190018
       2100-SELECT-DATA.                                                02200018
                                                                        02210018
           MOVE '2100-SELECT-DATA' TO PV-PARAGRAPH-NAME                 02220018
                                                                        02230018
           EXEC SQL                                                     02240018
                SELECT LAST_UPD_TMST                                    02250018
                INTO  :SD-TIMESTAMP                                     02260018
                FROM   XST_VND_STYL                                     02270018
                WHERE  STYL_ID = :XST00013-STYL-ID                      02280018
           END-EXEC.                                                    02290018
                                                                        02300018
           EVALUATE TRUE                                                02310020
              WHEN SQLCODE = ZEROS                                      02320020
                   PERFORM 2150-DATE-COMPARE                            02330019
              WHEN OTHER                                                02340018
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02350018
           END-EVALUATE.                                                02360018
                                                                        02370018
       EJECT.                                                           02380018
      ******************************************************************02390018
      **COMPARE DATE OF EXISTING ROW TO MESSAGE AND UPDATE IF MESSAGE   02400024
      **        HAS A NEWER TIMESTAMP.                                  02410024
      ******************************************************************02420018
       2150-DATE-COMPARE.                                               02430019
                                                                        02440018
           MOVE '2150-DATE-COMPARE' TO PV-PARAGRAPH-NAME                02450019
                                                                        02460018
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              02470018
                                                                        02480018
           SET DPG53-LK-CMPR-DTE-ISO-FMT   TO TRUE.                     02490058
           SET DPG52-LK-DTE-ISO-FMT        TO TRUE.                     02500058
           SET DPG51-ACTUAL-CALENDAR-ONLY  TO TRUE.                     02510058
121402     MOVE XST00013-SOR-LAST-MSG-TMST TO DPG52-LK-DATE-INPUT.      02520058
           MOVE SD-TIMESTAMP               TO DPG53-LK-CMPR-DATE-INPUT. 02530058
                                                                        02540018
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                02550018
                                                   DPG52                02560018
                                                   DPG53                02570018
                                                   DPG54                02580018
                                                   DPG55                02590018
                                                   DPG56.               02600018
                                                                        02610018
           EVALUATE TRUE                                                02620019
              WHEN DPG55-DAYS-DIFFERENCE = 0                            02630019
                   MOVE SD-HOUR                TO PV-TIME-HOUR-UP       02640023
                   MOVE SD-MINUTE              TO PV-TIME-MINUTE-UP     02650023
                   MOVE SD-SECOND              TO PV-TIME-SECOND-UP     02660023
                   MOVE SD-MICROSEC            TO PV-TIME-MICROSEC-UP   02670023
                   EVALUATE TRUE                                        02680024
                      WHEN PV-TIME-EVALUATE-INSERT >                    02690024
                              PV-TIME-EVALUATE-UPDATE                   02700024
                           PERFORM 2200-UPDATE-DATA                     02710024
                                   UNTIL PS-PROCESS-COMPLETE            02720024
                      WHEN OTHER                                        02730024
                           SET PS-ACTION-NOTHING   TO TRUE              02740045
                           SET PS-PROCESS-COMPLETE TO TRUE              02750024
                   END-EVALUATE                                         02760024
      ***DATA ALREADY ON TABLE IS CURRENT                               02770059
              WHEN DPG55-DAYS-DIFFERENCE < ZERO                         02780019
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02790019
      ***MESSAGE TIMESTAMP IS CURRENT                                   02800059
              WHEN DPG55-DAYS-DIFFERENCE > ZERO                         02810019
                   PERFORM 2200-UPDATE-DATA                             02820019
                           UNTIL PS-PROCESS-COMPLETE                    02830024
           END-EVALUATE.                                                02840019
                                                                        02850018
       EJECT.                                                           02860018
      ******************************************************************02870019
      **MESSAGE TIMESTAMP IS NEWER SO UPDATE ROW ON TABLE               02880059
      ******************************************************************02890019
       2200-UPDATE-DATA.                                                02900019
                                                                        02910019
           MOVE '2200-UPDATE-DATA' TO PV-PARAGRAPH-NAME                 02920019
                                                                        02930019
           EXEC SQL                                                     02940019
              UPDATE XST_VND_STYL                                       02950019
                  SET   DEPT_NBR          = :XST00013-DEPT-NBR          02960053
                      , MAJ_CL_NBR        = :XST00013-MAJ-CL-NBR        02970053
                      , SUB_CL_NBR        = :XST00013-SUB-CL-NBR        02980053
                      , VS_ID             = :XST00013-VS-ID             02990053
                      , VS_NBR            = :XST00013-VS-NBR            03000053
                      , ENT_ID            = :XST00013-ENT-ID            03010053
                      , STKR_NBR          = :XST00013-STKR-NBR          03020053
                      , RTL_CLSN_DESC     = :XST00013-RTL-CLSN-DESC     03030053
                      , LBL_NM            = :XST00013-LBL-NM            03040053
                      , ESL_IND           = :XST00013-ESL-IND           03050053
                      , HNG_FOLD_DESC     = :XST00013-HNG-FOLD-DESC     03060053
                      , LAST_UPD_TMST     = :XST00013-LAST-UPD-TMST     03070053
121402                , SOR_LAST_MSG_TMST = :XST00013-SOR-LAST-MSG-TMST 03080057
092704                , VS_DESC           = :XST00013-VS-DESC           03090060
                  WHERE STYL_ID       = :XST00013-STYL-ID               03100019
           END-EXEC.                                                    03110019
                                                                        03120019
           EVALUATE TRUE                                                03130020
      ***SUCCESSFUL PROCESSING                                          03140059
              WHEN SQLCODE = ZEROS                                      03150020
                   SET PS-ACTION-REVERSE       TO TRUE                  03160047
                   SET PS-PROCESS-COMPLETE     TO TRUE                  03170039
      ***ROW/TABLE LOCK                                                 03180059
              WHEN SQLCODE = -911                                       03190020
              WHEN SQLCODE = -913                                       03200059
                   IF  PC-RETRY-COUNTER-2 >= PC-RETRY-MAX               03210035
                       SET PS-PROCESS-COMPLETE TO TRUE                  03220019
                   ELSE                                                 03230019
                       ADD +1                  TO PC-RETRY-COUNTER-2    03240024
                   END-IF                                               03250019
              WHEN OTHER                                                03260019
                   SET PS-PROCESS-COMPLETE     TO TRUE                  03270036
           END-EVALUATE.                                                03280019
                                                                        03290019
       EJECT.                                                           03300019
      ******************************************************************03310000
      **RETURN VARIABLES TO CALLING PROGRAM                             03320059
      ******************************************************************03330000
       9999-WRAPUP.                                                     03340000
                                                                        03350000
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME              03360044
                                                                        03370000
           MOVE SQLCODE               TO LV-RETURN-CODE.                03380044
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 03390045
                                                                        03400044
           EXIT PROGRAM.                                                03410000
                                                                        03420000
       EJECT.                                                           03430018
