       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.             XSKRP020.                                00020000
       AUTHOR.                 KOHLS DEPARTMENT STORES.                 00030000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00040000
       DATE-WRITTEN.           02/03/2003.                              00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      ***XS DOMAIN - XST_VND_STYL REPORT                                00080000
      ******************************************************************00090000
      **THIS PROGRAM FORMATS XST_VND_STYL ERROR DATA INTO A REPORT      00100000
      ******************************************************************00110000
      *DATE:02/03/2003   MADE BY:CHRIS ALBRIGHT INITIAL WRITE           00120000
      ******************************************************************00130000
100704*DATE:10/07/2004   MADE BY:JULIE TEWES ADD COLUMN VS-DESC TO TABLE00140020
      ******************************************************************00150020
       ENVIRONMENT DIVISION.                                            00160000
       CONFIGURATION SECTION.                                           00170000
       SOURCE-COMPUTER.                        IBM-3090.                00180000
       OBJECT-COMPUTER.                        IBM-3090.                00190000
       INPUT-OUTPUT SECTION.                                            00200000
       FILE-CONTROL.                                                    00210000
                                                                        00220000
           SELECT INPUT-FILE                                            00230000
                  ASSIGN TO INP01.                                      00240000
           SELECT REPORT-FILE                                           00250000
                  ASSIGN TO RPT01.                                      00260000
                                                                        00270000
       DATA DIVISION.                                                   00280000
       FILE SECTION.                                                    00290000
                                                                        00300000
       FD  INPUT-FILE                                                   00310000
           RECORDING MODE IS F                                          00320000
           LABEL RECORDS ARE STANDARD                                   00330000
           BLOCK CONTAINS 0 CHARACTERS.                                 00340000
       01  INPUT-RECORD                    PIC X(400).                  00350021
                                                                        00360000
       FD  REPORT-FILE                                                  00370000
           RECORDING MODE IS F                                          00380000
           LABEL RECORDS ARE STANDARD                                   00390000
           BLOCK CONTAINS 0 CHARACTERS.                                 00400000
       01  REPORT-RECORD                   PIC X(133).                  00410000
                                                                        00420000
       WORKING-STORAGE SECTION.                                         00430000
      ******************************************************************00440000
      *SWITCHES                                                         00450000
      ******************************************************************00460000
       01  PS-PROGRAM-SWITCHES.                                         00470000
           05  PS-END-OF-INPUT-FILE-SW    PIC X(01)   VALUE 'N'.        00480000
               88  PS-EOF-INPUT-FILE                  VALUE 'Y'.        00490000
               88  PS-NOT-EOF-INPUT-FILE              VALUE 'N'.        00500000
      ******************************************************************00510000
      **TIME FIELDS                                                     00520000
      ******************************************************************00530000
       01  DF-DATETIME-FIELDS.                                          00540000
           05  DF-ACCEPT-TIME.                                          00550000
               10  DF-ACCEPT-HR             PIC 9(02) VALUE ZEROS.      00560000
               10  DF-ACCEPT-MIN            PIC 9(02) VALUE ZEROS.      00570000
               10  DF-ACCEPT-SEC            PIC 9(02) VALUE ZEROS.      00580000
               10  DF-ACCEPT-HSEC           PIC 9(02) VALUE ZEROS.      00590000
           05  DF-ACCEPT-DATE.                                          00600000
               10  DF-ACCEPT-YR             PIC 9(02) VALUE ZEROS.      00610000
               10  DF-ACCEPT-MO             PIC 9(02) VALUE ZEROS.      00620000
               10  DF-ACCEPT-DY             PIC 9(02) VALUE ZEROS.      00630000
      ******************************************************************00640000
      *PROGRAM CONSTANTS                                                00650000
      ******************************************************************00660000
       01  PC-PROGRAM-CONSTANTS.                                        00670000
           05  PC-PROGRAM-NAME            PIC X(08) VALUE 'XSKRP020'.   00680000
      ******************************************************************00690000
      **PROGRAM COUNTS                                                  00700000
      ******************************************************************00710000
       01  PC-PROGRAM-COUNTERS.                                         00720000
           05  PC-LINE-COUNT              PIC 9(03) VALUE ZEROS.        00730000
           05  PC-PAGE-COUNT              PIC 9(03) VALUE ZEROS.        00740000
           05  PC-RECORDS-READ            PIC 9(06) VALUE ZEROS.        00750000
      ***************************************************************** 00760000
      **ABEND CODE FIELDS                                               00770000
      ***************************************************************** 00780000
       01  ABEND-CODE                     PIC S9(4)                     00790000
                                                 VALUE ZEROS COMP SYNC. 00800000
       01  PV-MISC-ABEND-FIELDS.                                        00810000
           05  PV-PARAGRAPH-NAME          PIC  X(30)    VALUE SPACE.    00820000
           05  PV-OPERATION-MSG-1         PIC  X(40)    VALUE SPACE.    00830000
           05  PV-OPERATION-MSG-2         PIC  X(40)    VALUE SPACE.    00840000
           05  PV-DB2-OPERATION           PIC  X(30)    VALUE SPACES.   00850000
      ***************************************************************** 00860000
      **REPORT HEADINGS                                                 00870000
      ***************************************************************** 00880000
       01  REPORT-HEADING-1.                                            00890000
           05  FILLER                     PIC  X(56)  VALUE SPACES.     00900000
           05  FILLER                     PIC  X(22)                    00910000
                                 VALUE 'XS DOMAIN ERROR REPORT'.        00920000
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00930000
       01  REPORT-HEADING-2.                                            00940000
           05  FILLER                     PIC  X(52)  VALUE SPACES.     00950000
           05  FILLER                     PIC  X(29)                    00960000
                                 VALUE 'XST_VND_STYL INTEGRITY REPORT'. 00970000
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00980000
       01  REPORT-HEADING-3.                                            00990000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01000000
           05  FILLER                     PIC  X(07)                    01010000
                                 VALUE 'STYL_ID'.                       01020000
           05  FILLER                     PIC  X(03)  VALUE SPACES.     01030000
           05  FILLER                     PIC  X(08)                    01040000
                                 VALUE 'DEPT_NBR'.                      01050000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01060000
           05  FILLER                     PIC  X(10)                    01070000
                                 VALUE 'MAJ_CL_NBR'.                    01080000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01090000
           05  FILLER                     PIC  X(10)                    01100000
                                 VALUE 'SUB_CL_NBR'.                    01110000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01120000
           05  FILLER                     PIC  X(05)                    01130000
                                 VALUE 'VS_ID'.                         01140000
           05  FILLER                     PIC  X(07)  VALUE SPACES.     01150000
           05  FILLER                     PIC  X(06)                    01160000
                                 VALUE 'VS_NBR'.                        01170000
           05  FILLER                     PIC  X(16)  VALUE SPACES.     01180000
           05  FILLER                     PIC  X(06)                    01190000
                                 VALUE 'ENT_ID'.                        01200000
100704     05  FILLER                     PIC  X(15)  VALUE SPACES.     01210023
100704     05  FILLER                     PIC  X(07)                    01220020
100704                           VALUE 'VS_DESC'.                       01230020
       01  REPORT-HEADING-4.                                            01240020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01250020
           05  FILLER                     PIC  X(08)  VALUE ALL '-'.    01260020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01270020
           05  FILLER                     PIC  X(08)  VALUE ALL '-'.    01280020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01290020
           05  FILLER                     PIC  X(10)  VALUE ALL '-'.    01300020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01310020
           05  FILLER                     PIC  X(10)  VALUE ALL '-'.    01320020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01330020
           05  FILLER                     PIC  X(10)  VALUE ALL '-'.    01340020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01350020
           05  FILLER                     PIC  X(20)  VALUE ALL '-'.    01360020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01370020
           05  FILLER                     PIC  X(10)  VALUE ALL '-'.    01380020
100704     05  FILLER                     PIC  X(11)  VALUE SPACES.     01390023
100704     05  FILLER                     PIC  X(30)  VALUE ALL '-'.    01400020
       01  REPORT-HEADING-5.                                            01410020
           05  FILLER                     PIC  X(04)  VALUE SPACES.     01420020
           05  FILLER                     PIC  X(08)                    01430020
                                 VALUE 'STKR_NBR'.                      01440020
           05  FILLER                     PIC  X(04)  VALUE SPACES.     01450020
           05  FILLER                     PIC  X(13)                    01460020
                                 VALUE 'RTL_CLSN_DESC'.                 01470020
           05  FILLER                     PIC  X(09)  VALUE SPACES.     01480020
           05  FILLER                     PIC  X(06)                    01490020
                                 VALUE 'LBL_NM'.                        01500020
           05  FILLER                     PIC  X(16)  VALUE SPACES.     01510020
           05  FILLER                     PIC  X(13)                    01520020
                                 VALUE 'HNG_FOLD_DESC'.                 01530020
           05  FILLER                     PIC  X(09)  VALUE SPACES.     01540020
           05  FILLER                     PIC  X(07)                    01550020
                                 VALUE 'ESL_IND'.                       01560020
           05  FILLER                     PIC  X(15)  VALUE SPACES.     01570020
       01  REPORT-HEADING-6.                                            01580020
           05  FILLER                     PIC  X(04)  VALUE SPACES.     01590020
           05  FILLER                     PIC  X(10)  VALUE ALL '-'.    01600020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01610020
           05  FILLER                     PIC  X(20)  VALUE ALL '-'.    01620020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01630000
           05  FILLER                     PIC  X(20)  VALUE ALL '-'.    01640000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01650000
           05  FILLER                     PIC  X(20)  VALUE ALL '-'.    01660000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01670000
           05  FILLER                     PIC  X(20)  VALUE ALL '-'.    01680000
      ***************************************************************** 01690000
      **REPORT DETAIL                                                   01700000
      ***************************************************************** 01710000
       01  REPORT-DETAIL-1.                                             01720000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01730000
           05  RD-XS-STYL-ID              PIC  ZZZZZZZ9.                01740000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01750000
           05  RD-XS-DEPT-NBR             PIC  ZZZ9.                    01760000
           05  FILLER                     PIC  X(06)  VALUE SPACES.     01770000
           05  RD-XS-MAJ-CL-NBR           PIC  ZZZ9.                    01780000
           05  FILLER                     PIC  X(08)  VALUE SPACES.     01790000
           05  RD-XS-SUB-CL               PIC  ZZZ9.                    01800000
           05  FILLER                     PIC  X(08)  VALUE SPACES.     01810000
           05  RD-XS-VS-ID                PIC  ZZZZZZZZZ9.              01820000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01830000
           05  RD-XS-VS-NBR               PIC  X(20)  VALUE SPACES.     01840000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01850000
           05  RD-XS-ENT-ID               PIC  ZZZZZZZZZ9.              01860000
100704     05  FILLER                     PIC  X(11)  VALUE SPACES.     01870023
100704     05  RD-XS-VS-DESC              PIC  X(30)  VALUE SPACES.     01880020
       01  REPORT-DETAIL-2.                                             01890000
           05  FILLER                     PIC  X(04)  VALUE SPACES.     01900000
           05  RD-XS-STKR-NBR             PIC  ZZZZZZZZZ9.              01910000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01920000
           05  RD-XS-RTL-CLSN-DESC        PIC  X(20).                   01930000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01940000
           05  RD-XS-LBL-NM               PIC  X(20).                   01950000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01960000
           05  RD-XS-HNG-FOLD-DESC        PIC  X(20).                   01970000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01980000
           05  RD-XS-ESL-IND              PIC  X(20).                   01990000
       01  REPORT-DETAIL-3.                                             02000000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     02010000
           05  RD-VSM-STYL-ID             PIC  ZZZZZZZ9.                02020000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     02030000
           05  RD-VSM-DEPT-NBR            PIC  ZZZ9.                    02040000
           05  FILLER                     PIC  X(06)  VALUE SPACES.     02050000
           05  RD-VSM-MAJ-CL-NBR          PIC  ZZZ9.                    02060000
           05  FILLER                     PIC  X(08)  VALUE SPACES.     02070000
           05  RD-VSM-SUB-CL              PIC  ZZZ9.                    02080000
           05  FILLER                     PIC  X(08)  VALUE SPACES.     02090000
           05  RD-VSM-VS-ID               PIC  ZZZZZZZZZ9.              02100000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     02110000
           05  RD-VSM-VS-NBR              PIC  X(20)  VALUE SPACES.     02120000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     02130000
           05  RD-VSM-ENT-ID              PIC  ZZZZZZZZZ9.              02140000
100704     05  FILLER                     PIC  X(11)  VALUE SPACES.     02150023
100704     05  RD-VSM-VS-DESC             PIC  X(30)  VALUE SPACES.     02160020
       01  REPORT-DETAIL-4.                                             02170020
           05  FILLER                     PIC  X(04)  VALUE SPACES.     02180020
           05  RD-VSM-STKR-NBR            PIC  ZZZZZZZZZ9.              02190020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     02200020
           05  RD-VSM-RTL-CLSN-DESC       PIC  X(20).                   02210020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     02220020
           05  RD-VSM-LBL-NM              PIC  X(20).                   02230020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     02240020
           05  RD-VSM-HNG-FOLD-DESC       PIC  X(20).                   02250020
           05  FILLER                     PIC  X(02)  VALUE SPACES.     02260020
           05  RD-VSM-ESL-IND             PIC  X(20).                   02270020
       01  RF-END-OF-REPORT.                                            02280020
           05  FILLER                     PIC X(57)  VALUE SPACES.      02290020
           05  FILLER                     PIC X(21)                     02300020
                                 VALUE '***END OF REPORT***'.           02310020
           05  FILLER                     PIC X(57)  VALUE SPACES.      02320020
      ***************************************************************** 02330020
      **COPYBOOKS                                                       02340020
      ***************************************************************** 02350020
      ****ABEND ROUTINE INFORMATION                                     02360020
           COPY DPWS900.                                                02370020
      ****ABEND ROUTINE INFORMATION                                     02380020
           COPY DPWS004.                                                02390020
      ***STANDARD REPORT HEADER FOR A 132 COLUMN REPORT.                02400000
           COPY DPWS132O.                                               02410000
      ***INPUT RECORD                                                   02420000
           COPY XSRD010.                                                02430000
                                                                        02440000
       PROCEDURE DIVISION.                                              02450000
      ******************************************************************02460000
       0000-MAINLINE.                                                   02470000
                                                                        02480000
           PERFORM 1000-INITIALIZATION.                                 02490000
           PERFORM 2000-PROCESS-FILES                                   02500000
                   UNTIL PS-EOF-INPUT-FILE.                             02510000
           PERFORM 9999-WRAPUP.                                         02520000
           GOBACK.                                                      02530000
                                                                        02540000
       EJECT.                                                           02550000
      ******************************************************************02560000
      *INITIALIZATION ROUTINE                                           02570000
      ******************************************************************02580000
       1000-INITIALIZATION.                                             02590000
                                                                        02600000
           OPEN INPUT   INPUT-FILE                                      02610000
                OUTPUT  REPORT-FILE.                                    02620000
                                                                        02630000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             02640000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             02650000
                                                                        02660000
           PERFORM 1100-SETUP-HEADINGS.                                 02670000
                                                                        02680000
           PERFORM 9999-READ-INPUT-FILE.                                02690000
                                                                        02700000
       EJECT.                                                           02710000
      ******************************************************************02720000
      *SETS UP ALL HEADING STATIC VARIABLES FOR THE REPORT DURATION     02730000
      ******************************************************************02740000
       1100-SETUP-HEADINGS.                                             02750000
                                                                        02760000
           MOVE '1200-SETUP-HEADINGS' TO PV-PARAGRAPH-NAME.             02770000
                                                                        02780000
           MOVE DF-ACCEPT-YR          TO DP132O-RUN-YEAR.               02790000
           MOVE DF-ACCEPT-MO          TO DP132O-RUN-MONTH.              02800000
           MOVE DF-ACCEPT-DY          TO DP132O-RUN-DAY.                02810000
           MOVE DF-ACCEPT-HR          TO DP132O-RUN-HOUR.               02820000
           MOVE DF-ACCEPT-MIN         TO DP132O-RUN-MINUTE.             02830000
                                                                        02840000
           MOVE PC-PROGRAM-NAME  TO DP132O-PROGRAM-NAME.                02850000
           MOVE 1                TO DP132O-REPORT-NUMBER.               02860000
                                                                        02870000
           PERFORM 4000-PRINT-HEADINGS.                                 02880000
                                                                        02890000
       EJECT.                                                           02900000
      ******************************************************************02910000
      *PROCESS BOTH FILES                                               02920000
      ******************************************************************02930000
       2000-PROCESS-FILES.                                              02940000
                                                                        02950000
           MOVE '2000-PROCESS-FILES' TO PV-PARAGRAPH-NAME.              02960000
                                                                        02970000
           PERFORM 4100-PRINT-DETAIL.                                   02980000
                                                                        02990000
           IF  PC-LINE-COUNT > 60                                       03000000
               PERFORM 4000-PRINT-HEADINGS                              03010000
           END-IF.                                                      03020000
                                                                        03030000
           PERFORM 9999-READ-INPUT-FILE.                                03040000
                                                                        03050000
       EJECT.                                                           03060000
      ******************************************************************03070000
      *PRINTS REPORT HEADINGS AT TOP OF PAGE                            03080000
      ******************************************************************03090000
       4000-PRINT-HEADINGS.                                             03100000
                                                                        03110000
           MOVE '4000-PRINT-HEADINGS' TO PV-PARAGRAPH-NAME.             03120000
                                                                        03130000
           INITIALIZE PC-LINE-COUNT.                                    03140000
           ADD +1                     TO PC-PAGE-COUNT.                 03150000
           MOVE PC-PAGE-COUNT         TO DP132O-PAGE-NUMBER.            03160000
                                                                        03170000
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      03180000
                                                  AFTER ADVANCING PAGE. 03190000
           WRITE REPORT-RECORD FROM REPORT-HEADING-1                    03200000
                                               AFTER ADVANCING 2 LINES. 03210000
           WRITE REPORT-RECORD FROM REPORT-HEADING-2                    03220000
                                               AFTER ADVANCING 1 LINES. 03230000
           WRITE REPORT-RECORD FROM REPORT-HEADING-3                    03240000
                                               AFTER ADVANCING 3 LINES. 03250000
           WRITE REPORT-RECORD FROM REPORT-HEADING-4                    03260000
                                               AFTER ADVANCING 1 LINES. 03270000
           WRITE REPORT-RECORD FROM REPORT-HEADING-5                    03280000
                                               AFTER ADVANCING 1 LINES. 03290000
           WRITE REPORT-RECORD FROM REPORT-HEADING-6                    03300000
                                               AFTER ADVANCING 1 LINES. 03310000
                                                                        03320000
           ADD +9                     TO PC-LINE-COUNT.                 03330000
                                                                        03340000
       EJECT.                                                           03350000
      ******************************************************************03360000
      *PRINTS DETAIL REPORT                                             03370000
      ******************************************************************03380000
       4100-PRINT-DETAIL.                                               03390000
                                                                        03400000
           MOVE '4100-PRINT-DETAIL' TO PV-PARAGRAPH-NAME.               03410000
                                                                        03420020
           MOVE XSRD010-XS-STYL-ID TO RD-XS-STYL-ID.                    03430000
           MOVE XSRD010-XS-DEPT-NBR TO RD-XS-DEPT-NBR.                  03440000
           MOVE XSRD010-XS-MAJ-CL-NBR TO RD-XS-MAJ-CL-NBR.              03450000
           MOVE XSRD010-XS-SUB-CL TO RD-XS-SUB-CL.                      03460000
           MOVE XSRD010-XS-VS-ID TO RD-XS-VS-ID.                        03470000
           MOVE XSRD010-XS-VS-NBR TO RD-XS-VS-NBR.                      03480000
           MOVE XSRD010-XS-ENT-ID TO RD-XS-ENT-ID.                      03490000
           MOVE XSRD010-XS-STKR-NBR TO RD-XS-STKR-NBR.                  03500000
           MOVE XSRD010-XS-RTL-CLSN-DESC TO RD-XS-RTL-CLSN-DESC.        03510000
           MOVE XSRD010-XS-LBL-NM TO RD-XS-LBL-NM.                      03520000
           MOVE XSRD010-XS-HNG-FOLD-DESC TO RD-XS-HNG-FOLD-DESC.        03530000
           MOVE XSRD010-XS-ESL-IND TO RD-XS-ESL-IND.                    03540000
100704     MOVE XSRD010-XS-VS-DESC TO RD-XS-VS-DESC.                    03550020
                                                                        03560000
           MOVE XSRD010-VSM-STYL-ID TO RD-VSM-STYL-ID.                  03570000
           MOVE XSRD010-VSM-DEPT-NBR TO RD-VSM-DEPT-NBR.                03580000
           MOVE XSRD010-VSM-MAJ-CL-NBR TO RD-VSM-MAJ-CL-NBR.            03590000
           MOVE XSRD010-VSM-SUB-CL TO RD-VSM-SUB-CL.                    03600000
           MOVE XSRD010-VSM-VS-ID TO RD-VSM-VS-ID.                      03610000
           MOVE XSRD010-VSM-VS-NBR TO RD-VSM-VS-NBR.                    03620000
           MOVE XSRD010-VSM-ENT-ID TO RD-VSM-ENT-ID.                    03630000
           MOVE XSRD010-VSM-STKR-NBR TO RD-VSM-STKR-NBR.                03640000
           MOVE XSRD010-VSM-RTL-CLSN-DESC TO RD-VSM-RTL-CLSN-DESC.      03650000
           MOVE XSRD010-VSM-LBL-NM TO RD-VSM-LBL-NM.                    03660000
           MOVE XSRD010-VSM-HNG-FOLD-DESC TO RD-VSM-HNG-FOLD-DESC.      03670000
           MOVE XSRD010-VSM-ESL-IND TO RD-VSM-ESL-IND.                  03680000
100704     MOVE XSRD010-VSM-VS-DESC TO RD-VSM-VS-DESC.                  03690020
                                                                        03700020
           WRITE REPORT-RECORD FROM REPORT-DETAIL-1                     03710020
                                            AFTER ADVANCING 2 LINES.    03720020
           WRITE REPORT-RECORD FROM REPORT-DETAIL-2                     03730020
                                            AFTER ADVANCING 1 LINES.    03740020
           WRITE REPORT-RECORD FROM REPORT-DETAIL-3                     03750020
                                            AFTER ADVANCING 1 LINES.    03760000
           WRITE REPORT-RECORD FROM REPORT-DETAIL-4                     03770000
                                            AFTER ADVANCING 1 LINES.    03780000
                                                                        03790000
           ADD +5                   TO PC-LINE-COUNT.                   03800000
                                                                        03810000
       EJECT.                                                           03820000
      ******************************************************************03830000
      *READS INPUT FILE                                                 03840000
      ******************************************************************03850000
       9999-READ-INPUT-FILE.                                            03860000
                                                                        03870000
           READ INPUT-FILE INTO XSRD010-VNDSTYL-ERR-RECORD              03880000
               AT END SET PS-EOF-INPUT-FILE TO TRUE                     03890000
           END-READ.                                                    03900000
                                                                        03910000
           ADD +1  TO PC-RECORDS-READ.                                  03920000
                                                                        03930000
       EJECT.                                                           03940000
      ******************************************************************03950000
      *PROGRAM END PROCESSING                                           03960000
      ******************************************************************03970000
       9999-WRAPUP.                                                     03980000
                                                                        03990000
           WRITE REPORT-RECORD FROM RF-END-OF-REPORT                    04000000
                                    AFTER ADVANCING 2 LINES.            04010000
                                                                        04020000
           CLOSE INPUT-FILE                                             04030000
                 REPORT-FILE.                                           04040000
                                                                        04050000
           DISPLAY '*************************************************'. 04060000
           DISPLAY '* PROGRAM..................: ' PC-PROGRAM-NAME.     04070000
           DISPLAY '* RUN DATE.................: ' DF-ACCEPT-DATE.      04080000
           DISPLAY '* RUN TIME.................: ' DF-ACCEPT-TIME.      04090000
           DISPLAY '*--------- COUNTS FOR THIS RUN ---------'.          04100000
           DISPLAY '* RECORDS READ........: ' PC-RECORDS-READ.          04110000
           DISPLAY '*************************************************'. 04120000
                                                                        04130000
       EJECT.                                                           04140000
                                                                        04150000
       EJECT.                                                           04160000
