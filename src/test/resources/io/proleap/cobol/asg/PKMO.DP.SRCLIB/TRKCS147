000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    TRKCS147.                                                 
000500 AUTHOR.        LARRY SCHLEICHER (STRATAGEM).                             
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  06-10-97.                                                 
000800 DATE-COMPILED.                                                           
000900*----------------------------------------------------------------*        
000920*----------------------------------------------------------------*        
001000*    Y147 - COUNTRY CODE CRITERIA SPECIFICATION                  *        
001100*                                                                *        
001110*                                                                *        
001200*    THIS PROGRAM ALLOWS THE USER ENTER DATA TO BE USED BY       *        
001300*    SUBSEQUENT PROGRAMS TO CREATE A LIST OR TO BRING UP A       *        
001400*    DETAIL SCREEN.  THE COUNTRY CODE, IF ENTERED, IS EDITTED    *        
001500*    TO BE A VALID CODE.  ALL OTHER EDITS ARE DONE IN THE        *        
001600*    DESTINATION PROGRAMS WHEN THE 'GO-TO' TYPE FUNCTION IS USED *        
001700*    (LOCAL-FUNC-01 THRU LOCAL-FUNC-12, WITH DISPOSITION 'G').   *        
001800*----------------------------------------------------------------*        
P5207 *DATE: 11/27/2013 CHANGE MADE BY: COGNIZANT(SHEBENA)             *        
P5207 *CHANGE MADE: RECOMPILED THE PROGRAM AS THE TRWS019 COPYBOOK HAS *        
P5207 *             NEW FIELDS INCOTERM CODE AND DESCRIPTION ADDED TO  *        
P5207 *             IT.  CHANGE TAG : P5207  CR # : CHG0081355         *        
P5207 *----------------------------------------------------------------*        
CICS  * CHANGED 11/21/2022    BY JIM HUNTER      WR: CHG0278495                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
001900                                                                          
002000                                                                          
002100                                                                          
002200 ENVIRONMENT DIVISION.                                                    
002300                                                                          
002400                                                                          
002500                                                                          
002600 DATA DIVISION.                                                           
002700                                                                          
002800                                                                          
002900 WORKING-STORAGE SECTION.                                                 
003000*                                                                         
003100 01  DK-DB2-CONSTANTS.                                                    
003200     05  DK-CNTRY-CDE              PIC  X(03).                            
003300                                                                          
003400 01  PV-PROGRAM-VARIABLES.                                                
003500     05  PV-ASTRK-CNT                   PIC S9(04)  VALUE +0              
003600                                                    COMP SYNC.            
003700 01  PC-PROGRAM-CONSTANTS.                                                
003800     05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'TR147A  '.         
003900     05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'TRKM147 '.         
004000     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'TRKCS147'.         
004100     05  PC-ASTRK                  PIC  X(01)  VALUE '*'.                 
004200     05  PC-A                      PIC  X(01)  VALUE 'A'.                 
004210     05  PC-I                      PIC  X(01)  VALUE 'I'.                 
004300     05  PC-TSYMSG-NUMBERS.                                               
004400         10  PC-TSYMSG-00007       PIC  9(05)  VALUE  00007.              
004500         10  PC-TSYMSG-00008       PIC  9(05)  VALUE  00008.              
004600         10  PC-TSYMSG-00095       PIC  9(05)  VALUE  00095.              
004700         10  PC-TSYMSG-00224       PIC  9(05)  VALUE  00224.              
004800         10  PC-TSYMSG-01450       PIC  9(05)  VALUE  01450.              
004810         10  PC-TSYMSG-01644       PIC  9(05)  VALUE  01644.              
004900*                                                                         
005000 01  PS-PROGRAM-SWITCHES.                                                 
005100     05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.                
005200         88  PS-ERROR                          VALUE  'Y'.                
005300         88  PS-NO-ERROR                       VALUE  'N'.                
005400                                                                          
005500 01  PM-PROGRAM-MESSAGES.                                                 
005600     05  PM-ABEND-INFO-A.                                                 
005700         10  FILLER                PIC X(15)   VALUE                      
005800                                   'DP020-SRC-AID: '.                     
005900         10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.              
006000         10  FILLER                PIC X(19)   VALUE                      
006100                                   '. DP020-FK-ACTION: '.                 
006200         10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.              
006300         10  FILLER                PIC X(22)   VALUE                      
006400                                   '. DP020-FK-DISPN-IND: '.              
006500         10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.              
006600         10  FILLER                PIC X(01)   VALUE '.'.                 
006700         10  FILLER                PIC X(19)   VALUE SPACE.               
006800                                                                          
006900                                                                          
007000*----------------------------------------------------------------*        
007100*    MAP LAYOUT                                                  *        
007200*----------------------------------------------------------------*        
007300                                                                          
007400     COPY TRKM147.                                                        
007500     EJECT                                                                
007600*----------------------------------------------------------------*        
007700*    ATTRIBUTE SETTINGS COPYBOOK.                                *        
007800*----------------------------------------------------------------*        
007900                                                                          
008000     COPY DPWS015.                                                        
008100                                                                          
008200*----------------------------------------------------------------*        
008300*    FUNCTION KEYS COPYBOOK                                      *        
008400*----------------------------------------------------------------*        
008500                                                                          
008600     COPY DPWS016.                                                        
008700 EJECT                                                                    
008800******************************************************************        
008900** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **        
009000** SUB-ROUTINE (DPKUT100).                                      **        
009100******************************************************************        
009200     COPY DPWS010I.                                                       
009300 EJECT                                                                    
009400******************************************************************        
009500** THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS            **        
009600** ARCHITECTURE API (BLACK BOX) SUB-ROUTINE.                    **        
009700******************************************************************        
009800     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
009900 EJECT                                                                    
010000*****************************************************************         
010100*    STANDARD COMMAREA.                                                   
010200*****************************************************************         
010300                                                                          
010400     COPY DPWS020.                                                        
010500     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
010600                                                                          
010700*****************************************************************         
010800*    INTER-APPLICATION COMMAREA.                                          
010900*****************************************************************         
011000                                                                          
011100         10  INTER-APPL-COMM-AREA PIC X(200).                             
011200     COPY TRWS019.                                                        
011300*****************************************************************         
011400*    SPECIFIC COMMMAREA FOR TRKCS147.                                     
011500*****************************************************************         
011600         10  ASC-SPECIFIC-COMMAREA.                                       
011700             15  ASC-COUNTRY-CODE               PIC  X(03).               
011800             15  ASC-COUNTRY-CODE-DESC          PIC  X(40).               
011900             15  ASC-COUNTRY-CODE-IND           PIC  X(01).               
012000                 88  ASC-VALID-COUNTRY-CODE    VALUE 'Y'.                 
012100                 88  ASC-INVALID-COUNTRY-CODE  VALUE 'N'.                 
012110             15  ASC-COUNTRY-STAT-CDE           PIC  X(01).               
012200     EJECT                                                                
012300*----------------------------------------------------------------*        
012400*    ABEND PROCESSING COPYBOOK.                                  *        
012500*----------------------------------------------------------------*        
012600                                                                          
012700     COPY DPWS013.                                                        
012800                                                                          
012900*---------------------------------------------------------------*         
013000*    DB2 AREA FOR COUNTRY CODE TABLE                            *         
013100*---------------------------------------------------------------*         
013200     EXEC SQL                                                             
013300          INCLUDE TCNTRY                                                  
013400     END-EXEC.                                                            
013500                                                                          
013600*---------------------------------------------------------------*         
013700*    DB2 AREA FOR COMMUNICATIONS                                *         
013800*---------------------------------------------------------------*         
013900     EXEC SQL                                                             
014000          INCLUDE SQLCA                                                   
014100     END-EXEC.                                                            
014200                                                                          
014300 EJECT                                                                    
014400 LINKAGE SECTION.                                                         
014500                                                                          
014600 01  DFHCOMMAREA.                                                         
014700     05  FILLER                         OCCURS  1 TO 4072 TIMES           
014800                                        DEPENDING ON EIBCALEN.            
014900         10  FILLER                     PIC X.                            
015000                                                                          
015100                                                                          
015200 PROCEDURE DIVISION.                                                      
015300*****************************************************************         
015400** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
015500** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
015600** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
015700**                                                             **         
015800** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
015900** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
016000*****************************************************************         
016100 0000-MAIN-MODULE.                                                        
016200     INITIALIZE DP030-CICS-API-FIELDS.                                    
016300     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
016400     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
016500     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
016600     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
016700     MOVE LENGTH OF TR147AI        TO DP030-MAP-LENGTH (1).               
016800     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
016900     PERFORM 0001-CALL-CICS-ARCH-API.                                     
017000*                                                                         
017100     PERFORM 1000-CONTROL-PROCESSING                                      
017200*                                                                         
017300     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
017400     PERFORM 0001-CALL-CICS-ARCH-API.                                     
017500                                                                          
017600 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
017800                                    DFHCOMMAREA                           
017900                                    DP030-CICS-API-FIELDS                 
018000                                    DP020-STANDARD-COMMAREA               
018100                                    TR147AI.                              
018200*                                                                         
018300     IF  DP030-RC-CALL-SUCCESSFUL                                         
018400         CONTINUE                                                         
018500     ELSE                                                                 
018600         SET DP013-NO-ROLLBACK                                            
018700             DP013-XCTL-DISPLAY-RESTART                                   
018800             DP013-CICS-ABEND      TO TRUE                                
018900         MOVE 'BEFORE 0000-MAIN-MODULE'                                   
019000                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
019300         MOVE DP030-RETURN-CODE                                           
019400                                   TO DP013-MESSAGE-TEXT (3)              
019500         PERFORM DP013-0000-PROCESS-ABEND                                 
019600     END-IF.                                                              
019700 EJECT                                                                    
019800*----------------------------------------------------------------*        
019900*    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
020000*    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
020100*----------------------------------------------------------------*        
020200                                                                          
020300 1000-CONTROL-PROCESSING.                                                 
020400                                                                          
020500     EVALUATE TRUE                                                        
020600         WHEN DP020-NEXT-ACT-INITIAL                                      
020700             INITIALIZE ASC-SPECIFIC-COMMAREA                             
020800             PERFORM 4000-BUILD-INITIAL-PANEL                             
020900                                                                          
021000         WHEN DP020-NEXT-ACT-READ-MAP                                     
021100             PERFORM 2000-FKEY-PROCESSING                                 
021200                                                                          
021300         WHEN DP020-NEXT-ACT-RETURN                                       
021301             MOVE SPACES  TO ASC-COUNTRY-CODE                             
021302             MOVE SPACES  TO ASC-COUNTRY-CODE-DESC                        
021303             MOVE SPACES  TO ASC-COUNTRY-STAT-CDE                         
021400             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
021410             MOVE -1               TO ACNTCODL                            
021420             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
021500*            PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
021600                                                                          
021700         WHEN OTHER                                                       
021800             SET DP013-LOGIC-ABEND                                        
021900                 DP013-NO-ROLLBACK TO TRUE                                
022000             MOVE '1000-CONTROL-PROCESSING'                               
022100                                   TO  DP013-PARAGRAPH                    
022200             MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
022300                                   TO  DP013-MESSAGE-TEXT(1)              
022400             MOVE DP020-NEXT-APPL-ACTIVITY                                
022500                                   TO DP013-MESSAGE-TEXT(2)               
022600             PERFORM DP013-0000-PROCESS-ABEND                             
022700     END-EVALUATE.                                                        
022800                                                                          
022900*----------------------------------------------------------------*        
023000* FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
023100* SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
023200* EDITS.                                                         *        
023300*----------------------------------------------------------------*        
023400                                                                          
023500 2000-FKEY-PROCESSING.                                                    
023600                                                                          
023700     EVALUATE TRUE                                                        
023800                                                                          
023900         WHEN DP020-SRC-AID = DP016-CLEAR                                 
024000             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
024100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
024200                                                                          
024300         WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
024400             INITIALIZE ASC-SPECIFIC-COMMAREA                             
024500             PERFORM 4000-BUILD-INITIAL-PANEL                             
024600             PERFORM 3200-RESET-ATTRIBUTES                                
024700                                                                          
024800         WHEN OTHER                                                       
024900             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
025000             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
025100             IF PS-NO-ERROR OR                                            
025200                DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                    
025300*               DP020-FK-LOCAL-FUNC-04 (DP020-SRC-AID)                    
025310                 MOVE SPACE            TO DP020-MSG-NUMBER-X              
025400                 SET DP030-CONTINUE-GO TO TRUE                            
025500                 PERFORM 2100-CHECK-FUNCTION-KEY                          
025600             ELSE                                                         
025700                 SET DP030-OVERRIDE-GO TO TRUE                            
025800             END-IF                                                       
025900     END-EVALUATE.                                                        
026000******************************************************************        
026100** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **        
026200** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **        
026300** FROM THE CICS ARCHITECTURE API.                              **        
026400******************************************************************        
026500 2100-CHECK-FUNCTION-KEY.                                                 
026600     EVALUATE TRUE                                                        
026700         WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)                      
026800         WHEN DP020-FK-LOCAL-FUNC-03 (DP020-SRC-AID)                      
026900             IF ASC-VALID-COUNTRY-CODE  AND ASC-COUNTRY-CODE              
026910                      > SPACE                                             
027000                 PERFORM 2110-PREPARE-INTER-APPL-COMM                     
027100             ELSE                                                         
027200                 SET DP030-OVERRIDE-GO                                    
027300                                   TO TRUE                                
027400                 MOVE PC-TSYMSG-01450                                     
027500                                   TO DP020-MSG-NUMBER                    
027600                 SET DP020-MSG-FATAL                                      
027700                                    TO TRUE                               
027800             END-IF                                                       
027900         WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
027901             MOVE 'I' TO ASC-COUNTRY-STAT-CDE                             
027902             MOVE SPACE TO ASC-COUNTRY-CODE                               
027910             PERFORM 2110-PREPARE-INTER-APPL-COMM                         
028000         WHEN DP020-FK-LOCAL-FUNC-04 (DP020-SRC-AID)                      
028001             IF ASC-COUNTRY-STAT-CDE = PC-I                               
028002                 SET DP030-OVERRIDE-GO                                    
028003                                   TO TRUE                                
028004                 MOVE PC-TSYMSG-01644                                     
028005                                   TO DP020-MSG-NUMBER                    
028006                 SET DP020-MSG-FATAL                                      
028007                                    TO TRUE                               
028008             ELSE                                                         
028009                 MOVE 'A' TO ASC-COUNTRY-STAT-CDE                         
028010                 PERFORM 2110-PREPARE-INTER-APPL-COMM                     
028020             END-IF                                                       
028200                                                                          
028300         WHEN DP020-SRC-AID = DP016-ENTER                                 
028400             CONTINUE                                                     
028500                                                                          
028600         WHEN OTHER                                                       
028700             SET DP013-NO-ROLLBACK                                        
028800                 DP013-XCTL-DISPLAY-RESTART                               
028900                 DP013-CICS-ABEND  TO TRUE                                
029000             MOVE '2100-CHECK-FUNCTION-KEY'                               
029100                                   TO DP013-PARAGRAPH                     
029200             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
029300                                   TO DP013-MESSAGE-TEXT (1)              
029400             MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:'         
029500                                   TO DP013-MESSAGE-TEXT (2)              
029600             MOVE DP020-SRC-AID    TO PM-A-SRC-AID                        
029700             MOVE DP020-FK-ACTION (DP020-SRC-AID)                         
029800                                   TO PM-A-FK-ACTION                      
029900             MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)                      
030000                                   TO PM-A-FK-DISPN                       
030100             MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)              
030200             MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'                
030300                                   TO DP013-MESSAGE-TEXT (4)              
030400             PERFORM DP013-0000-PROCESS-ABEND                             
030500     END-EVALUATE.                                                        
030600******************************************************************        
030700** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **        
030800** INTER-APPLICATION COMMAREA.                                  **        
030900******************************************************************        
031000 2110-PREPARE-INTER-APPL-COMM.                                            
031100     INITIALIZE TR019-INTER-APPL-COMMAREA.                                
031200     MOVE ASC-COUNTRY-CODE         TO TR019-COUNTRY-CODE.                 
031300*    MOVE ASC-COUNTRY-CODE-DESC    TO TR019-COUNTRY-CODE-DESC.            
031310     MOVE ASC-COUNTRY-STAT-CDE     TO TR019-STAT-CDE.                     
031400 EJECT                                                                    
031500*----------------------------------------------------------------*        
031600* MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
031700* THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *        
031800* APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *        
031900*----------------------------------------------------------------*        
032000                                                                          
032100 2200-MOVE-SCREEN-TO-COMMAREA.                                            
032200     IF  ACNTCODL > ZERO                                                  
032300         MOVE ACNTCODI             TO ASC-COUNTRY-CODE                    
032400     ELSE                                                                 
032500         IF  ACNTCODF = DP015-ERASE-EOF                                   
032600             MOVE SPACE            TO ASC-COUNTRY-CODE                    
032700                                      ASC-COUNTRY-CODE-DESC               
032800         END-IF                                                           
032900     END-IF.                                                              
033000                                                                          
033100*----------------------------------------------------------------*        
033200* ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *        
033300* CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *        
033400* A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*        
033500* IN THE DESTINATION PROGRAM, NOT HERE.                          *        
033600*----------------------------------------------------------------*        
033700                                                                          
033800 3000-EDIT-DATA-IN-COMMAREA.                                              
033900     PERFORM 3200-RESET-ATTRIBUTES.                                       
034820         IF  ASC-COUNTRY-CODE > SPACE                                     
034830             MOVE ASC-COUNTRY-CODE     TO DK-CNTRY-CDE                    
034831             PERFORM 3100-SELECT-COUNTRY                                  
034840             INSPECT DK-CNTRY-CDE TALLYING PV-ASTRK-CNT FOR ALL           
034850                                               PC-ASTRK                   
034860             IF PV-ASTRK-CNT > 0                                          
034870                 MOVE SPACES          TO ASC-COUNTRY-CODE-DESC            
034880                                              ACNTDESO                    
034890                 MOVE PC-A            TO ASC-COUNTRY-STAT-CDE             
034891             ELSE                                                         
034900                 IF ASC-INVALID-COUNTRY-CODE                              
035000                     MOVE SPACES        TO ASC-COUNTRY-CODE-DESC          
035100                                          ACNTDESO                        
035200                     SET PS-ERROR                                         
035300                         DP020-MSG-FATAL   TO TRUE                        
035400                     MOVE PC-TSYMSG-01450  TO DP020-MSG-NUMBER            
035500                     MOVE DP015-UNP-ALP-BRT-OFF                           
035600                                           TO ACNTCODA                    
035700                     MOVE DP015-RED        TO ACNTCODC                    
035800                     MOVE DP015-REVERSE    TO ACNTCODH                    
035900                     MOVE -1               TO ACNTCODL                    
036000                     SET DP030-SET-CURSOR-APPL-1 TO TRUE                  
036010                 ELSE                                                     
036020                     IF ASC-COUNTRY-STAT-CDE = 'I'                        
036030                         MOVE SPACES    TO ASC-COUNTRY-CODE-DESC          
036040                                              ACNTDESO                    
036050                         SET PS-ERROR                                     
036060                             DP020-MSG-FATAL   TO TRUE                    
036070                         MOVE PC-TSYMSG-01644 TO DP020-MSG-NUMBER         
036080                         MOVE DP015-UNP-ALP-BRT-OFF                       
036090                                               TO ACNTCODA                
036091                         MOVE DP015-RED        TO ACNTCODC                
036092                         MOVE DP015-REVERSE    TO ACNTCODH                
036093                         MOVE -1               TO ACNTCODL                
036094                         SET DP030-SET-CURSOR-APPL-1 TO TRUE              
036100                      ELSE                                                
036200                          MOVE CNTRY-CNTRY-NM                             
036210                             TO ASC-COUNTRY-CODE-DESC                     
036300                                              ACNTDESO                    
036400                      END-IF                                              
036500                  END-IF                                                  
036510             END-IF                                                       
036600     END-IF.                                                              
036700                                                                          
036800*                                                                         
036891                                                                          
036900     IF  PS-NO-ERROR                                                      
037000         MOVE -1                   TO ACNTCODL                            
037100         SET DP030-SET-CURSOR-APPL-1                                      
037200                                   TO TRUE                                
037300     END-IF.                                                              
037400                                                                          
037500 3100-SELECT-COUNTRY.                                                     
037600                                                                          
037700     EXEC SQL                                                             
037800         SELECT A.CNTRY_NM                                                
037810              , A.STAT_CDE                                                
037900            INTO :CNTRY-CNTRY-NM                                          
037910                ,:CNTRY-STAT-CDE                                          
038000             FROM TCNTRY A                                                
038100             WHERE A.CNTRY_CDE = :DK-CNTRY-CDE                            
038200               AND A.STAT_CDE  IN (:PC-A, :PC-I)                          
038300     END-EXEC                                                             
038400                                                                          
038500         EVALUATE TRUE                                                    
038600             WHEN SQLCODE = ZERO                                          
038700                 SET ASC-VALID-COUNTRY-CODE TO TRUE                       
038710*                IF CNTRY-STAT-CDE = 'I'                                  
038711                    MOVE CNTRY-STAT-CDE                                   
038720                     TO ASC-COUNTRY-STAT-CDE                              
038730*                END-IF                                                   
038800             WHEN SQLCODE = +100                                          
038900                 SET ASC-INVALID-COUNTRY-CODE TO TRUE                     
039000                 MOVE SPACES TO CNTRY-CNTRY-NM                            
039100             WHEN OTHER                                                   
039200                 MOVE '3100-SELECT-COUNTRY'                               
039300                                   TO DP013-PARAGRAPH                     
039400                 MOVE 'SQL ERROR SELECTING COUNTRY NAME'                  
039500                                   TO DP013-MESSAGE-TEXT (1)              
039600                 MOVE SQLCA        TO DP013-SQLCA                         
039700                 MOVE 'TCNTRY  '   TO DP013-DB2-TABLE-NAME (1)            
039800                 SET DP013-DB2-ABEND                                      
039900                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
040000                 PERFORM DP013-0000-PROCESS-ABEND                         
040100         END-EVALUATE.                                                    
040200                                                                          
040300******************************************************************        
040400** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **        
040500** MAP.                                                         **        
040600******************************************************************        
040700 3200-RESET-ATTRIBUTES.                                                   
040800     MOVE DP015-UNP-ALP-NOR-OFF    TO ACNTCODA.                           
040900     MOVE DP015-GREEN              TO ACNTCODC.                           
041000     MOVE DP015-UNDERLINE          TO ACNTCODH.                           
041100*                                                                         
041200*----------------------------------------------------------------*        
041300*  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *        
041400*  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *        
041500*  IT HERE.                                                      *        
041600*----------------------------------------------------------------*        
041700 4000-BUILD-INITIAL-PANEL.                                                
041800     SET DP030-SET-CURSOR-APPL-1   TO TRUE                                
041900     MOVE -1                       TO ACNTCODL.                           
042000*                                                                         
042100*----------------------------------------------------------------*        
042200*    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
042300*----------------------------------------------------------------*        
042400 4400-MOVE-COMMAREA-TO-SCREEN.                                            
042500     MOVE ASC-COUNTRY-CODE         TO ACNTCODO.                           
042600     MOVE ASC-COUNTRY-CODE-DESC    TO ACNTDESO.                           
042700 EJECT                                                                    
042800*----------------------------------------------------------------*        
042900*    ABEND PROCESSOR MODULE                                      *        
043000*----------------------------------------------------------------*        
043100                                                                          
043200     COPY DPPD013.                                                        
