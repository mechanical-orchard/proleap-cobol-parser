       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      XSKUT019.                                       00020000
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    10/02/2002.                                     00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **XS DOMAIN TABLE INTEGRITY PROCESS - FOR TABLE XST_VND_STYL      00080000
      ******************************************************************00090000
      **THIS PROGRAM VALIDATES THE DATA IN THE XS DOMAIN IS THE SAME    00100000
      **AS THE DATA IN THE SOR - VSM.                                   00110000
      ******************************************************************00120000
      *DATE:10/02/2002   MADE BY:JULIE SMITH INITIAL PROGRAM WRITE      00130000
011403*DATE:01/14/2003   MADE BY:JULIE SMITH FIXES                      00140000
      ******************************************************************00150000
022504*DATE:02/25/2004   MADE BY:RAY GRAF                               00160000
022504*USE NEW EXTRACT FILE PKMO.IM.ITEM.STYL.DOMAIN.V2.EXTRACT ALONG   00170000
022504*WITH COPYBOOK IMRD058 AS THE NEW FILE LAYOUT                     00180000
      ******************************************************************00190000
100704*DATE:10/07/2004   MADE BY:JULIE TEWES ADD VS_DESC TO TABLES      00200000
      ******************************************************************00210000
      ******************************************************************00241001
P46039* DATE:17/02/2014   MADE BY:COGNIZANT    PRB0046039               00242001
      ******************************************************************00246001
       ENVIRONMENT DIVISION.                                            00250000
       CONFIGURATION SECTION.                                           00260000
       SOURCE-COMPUTER.        IBM-3090.                                00270000
       OBJECT-COMPUTER.        IBM-3090.                                00280000
       INPUT-OUTPUT SECTION.                                            00290000
       FILE-CONTROL.                                                    00300000
                                                                        00310000
           SELECT CONTROL-CARD                                          00320000
                  ASSIGN TO INP00.                                      00330000
           SELECT VSM-EXTRACT                                           00340000
                  ASSIGN TO INP01.                                      00350000
           SELECT XS-EXTRACT                                            00360000
                  ASSIGN TO INP02.                                      00370000
           SELECT DIFFERENCES-FILE                                      00380000
                  ASSIGN TO OUT01.                                      00390000
                                                                        00400000
       DATA DIVISION.                                                   00410000
       FILE SECTION.                                                    00420000
                                                                        00430000
       FD  CONTROL-CARD                                                 00440000
           RECORDING MODE F                                             00450000
           BLOCK CONTAINS 0 RECORDS                                     00460000
           LABEL RECORDS ARE OMITTED                                    00470000
           DATA RECORD IS CONTROL-CARD-RECORD.                          00480000
       01  CONTROL-CARD-RECORD             PIC X(080).                  00490000
                                                                        00500000
       FD  VSM-EXTRACT                                                  00510000
           RECORDING MODE F                                             00520000
           BLOCK CONTAINS 0 RECORDS                                     00530000
           LABEL RECORDS ARE OMITTED                                    00540000
           DATA RECORD IS VSM-RECORD.                                   00550000
022504 01  VSM-RECORD                      PIC X(510).                  00560000
                                                                        00570000
       FD  XS-EXTRACT                                                   00580000
           RECORDING MODE F                                             00590000
           BLOCK CONTAINS 0 RECORDS                                     00600000
           LABEL RECORDS ARE OMITTED                                    00610000
           DATA RECORD IS XS-RECORD.                                    00620000
       01  XS-RECORD                       PIC X(156).                  00630000
                                                                        00640000
       FD  DIFFERENCES-FILE                                             00650000
           RECORDING MODE F                                             00660000
           BLOCK CONTAINS 0 RECORDS                                     00670000
           LABEL RECORDS ARE OMITTED                                    00680000
           DATA RECORD IS DIFFERENCES-RECORD.                           00690000
100704 01  DIFFERENCES-RECORD              PIC X(400).                  00700000
                                                                        00710000
       WORKING-STORAGE SECTION.                                         00720000
      ******************************************************************00730000
      **SWITCHES                                                        00740000
      ******************************************************************00750000
       01  PS-PROGRAM-SWITCHES.                                         00760000
           05  PS-END-OF-VSM-EXTRACT-SW        PIC X(01) VALUE 'N'.     00770000
               88  PS-END-OF-VSM-EXTRACT                 VALUE 'Y'.     00780000
               88  PS-NOT-END-OF-VSM-EXTRACT             VALUE 'N'.     00790000
           05  PS-END-OF-XS-EXTRACT-SW         PIC X(01) VALUE 'N'.     00800000
               88  PS-END-OF-XS-EXTRACT                  VALUE 'Y'.     00810000
               88  PS-NOT-END-OF-XS-EXTRACT              VALUE 'N'.     00820000
           05  PS-ERROR-FOUND-SW               PIC X(01) VALUE 'N'.     00830000
               88  PS-ERROR-FOUND                        VALUE 'Y'.     00840000
               88  PS-ERROR-NOT-FOUND                    VALUE 'N'.     00850000
      ******************************************************************00860000
      **CONTROL CARD LAYOUT                                             00870000
      ******************************************************************00880000
       01  CONTROL-CARD-LAYOUT.                                         00890000
           05  CC-START-POINT                  PIC 9(05) VALUE ZEROS.   00900000
           05  FILLER                          PIC X(01) VALUE SPACES.  00910000
           05  CC-READ-FREQUENCY               PIC 9(05) VALUE ZEROS.   00920000
           05  FILLER                          PIC X(69) VALUE SPACES.  00930000
      ******************************************************************00940000
      **TIMESTAMP VARIABLES                                             00950000
      ******************************************************************00960000
       01  SD-TIMESTAMP                        PIC X(26) VALUE SPACES.  00970000
       01  FILLER REDEFINES SD-TIMESTAMP.                               00980000
           05  SD-CURR-DTE                     PIC X(10).               00990000
           05  FILLER                          PIC X(01).               01000000
           05  SD-HOUR                         PIC X(02).               01010000
           05  FILLER                          PIC X(01).               01020000
           05  SD-MINUTE                       PIC X(02).               01030000
           05  FILLER                          PIC X(01).               01040000
           05  SD-SECOND                       PIC X(02).               01050000
           05  FILLER                          PIC X(01).               01060000
           05  SD-MICROSEC                     PIC X(06).               01070000
      ******************************************************************01080000
      **PROGRAM CONSTANTS                                               01090000
      ******************************************************************01100000
       01  PC-PROGRAM-CONSTANTS.                                        01110000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                01120000
                                                       VALUE 'XSKUT019'.01130000
      ******************************************************************01140000
      **PROGRAM COUNTERS                                                01150000
      ******************************************************************01160000
       01  PC-PROGRAM-COUNTER.                                          01170000
           05  PC-RECORD-COUNT                 PIC S9(09) VALUE ZEROS   01180000
                                                          COMP-3.       01190000
           05  PC-VSM-EXTRACT-RECS-READ        PIC 9(09) VALUE ZEROS.   01200000
           05  PC-XS-EXTRACT-RECS-READ         PIC 9(09) VALUE ZEROS.   01210000
           05  PC-ERROR-RECORD-WRITTEN         PIC 9(09) VALUE ZEROS.   01220000
      ******************************************************************01230000
      **ERROR HANDLING                                                  01240000
      ******************************************************************01250000
       01  PV-MISC-ABEND-FIELDS.                                        01260000
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      01270000
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      01280000
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      01290000
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      01300000
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 01310000
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           01320000
      ******************************************************************01330000
      **COPYBOOKS                                                       01340000
      ******************************************************************01350000
      ***VSM EXTRACT FILE LAYOUT                                        01360000
022504     COPY IMRD058.                                                01370000
      ***XS EXTRACT FILE LAYOUT                                         01380000
           COPY XSRD009.                                                01390000
      ***XS ERROR FILE LAYOUT                                           01400000
           COPY XSRD010.                                                01410000
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01420000
           COPY DPWS004.                                                01430000
      ***DB2 ABEND COPYBOOK                                             01440000
           COPY DPWS900.                                                01450000
      ***DB2 MESSAGE AREA                                               01460000
           COPY SQLCA2.                                                 01470000
      ******************************************************************01480000
      **TABLE INCLUSIONS                                                01490000
      ******************************************************************01500000
      ***DB2 COMMUNICATIONS AREA                                        01510000
           EXEC SQL                                                     01520000
                INCLUDE SQLCA                                           01530000
           END-EXEC.                                                    01540000
      ***TABLE XST_VND_STYL                                             01550000
           EXEC SQL                                                     01560000
                INCLUDE XST00013                                        01570000
           END-EXEC.                                                    01580000
                                                                        01590000
       PROCEDURE DIVISION.                                              01600000
      ***************************************************************** 01610000
       000-MAINLINE.                                                    01620000
                                                                        01630000
           PERFORM 1000-INITIALIZATION.                                 01640000
           PERFORM 2000-PROCESS-RECORDS                                 01650000
                   UNTIL PS-END-OF-VSM-EXTRACT                          01660000
                      OR PS-END-OF-XS-EXTRACT.                          01670000
           PERFORM 3000-PROCESS-EXTRA-RECORDS                           01680000
                   UNTIL PS-END-OF-VSM-EXTRACT                          01690000
                     AND PS-END-OF-XS-EXTRACT.                          01700000
           PERFORM 9999-WRAPUP.                                         01710000
           GOBACK.                                                      01720000
                                                                        01730000
       EJECT.                                                           01740000
      ******************************************************************01750000
      **INITIALIZATION AREA                                             01760000
      ******************************************************************01770000
       1000-INITIALIZATION.                                             01780000
                                                                        01790000
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01800000
                                                                        01810000
           INITIALIZE PC-PROGRAM-COUNTER.                               01820000
                                                                        01830000
           OPEN INPUT  CONTROL-CARD                                     01840000
                       VSM-EXTRACT                                      01850000
                       XS-EXTRACT                                       01860000
                OUTPUT DIFFERENCES-FILE.                                01870000
                                                                        01880000
           PERFORM 1100-SELECT-DB2-TIME.                                01890000
           PERFORM 1200-SELECT-RECORD-COUNT.                            01900000
                                                                        01910000
           PERFORM 9999-READ-CONTROL-CARD.                              01920000
           PERFORM 9999-READ-VSM-EXTRACT CC-START-POINT TIMES.          01930000
           PERFORM 9999-READ-XS-EXTRACT  CC-START-POINT TIMES.          01940000
                                                                        01950000
       EJECT.                                                           01960000
      ******************************************************************01970000
      **RETRIEVE TIME FROM DB2                                          01980000
      ******************************************************************01990000
       1100-SELECT-DB2-TIME.                                            02000000
                                                                        02010000
           MOVE '1100-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME.            02020000
                                                                        02030000
           EXEC SQL                                                     02040000
                SELECT CURRENT TIMESTAMP                                02050000
                INTO  :SD-TIMESTAMP                                     02060000
                FROM   SYSIBM.SYSDUMMY1                                 02070000
           END-EXEC.                                                    02080000
                                                                        02090000
           EVALUATE TRUE                                                02100000
              WHEN SQLCODE = ZERO                                       02110000
                   CONTINUE                                             02120000
              WHEN OTHER                                                02130000
               PERFORM 9999-ABEND-ROUTINE                               02140000
           END-EVALUATE.                                                02150000
                                                                        02160000
       EJECT.                                                           02170000
      ******************************************************************02180000
      **RETRIEVE TIME FROM DB2                                          02190000
      ******************************************************************02200000
       1200-SELECT-RECORD-COUNT.                                        02210000
                                                                        02220000
           MOVE '1200-SELECT-RECORD-COUNT' TO PV-PARAGRAPH-NAME.        02230000
                                                                        02240000
           EXEC SQL                                                     02250000
                SELECT COUNT(*)                                         02260000
                INTO  :PC-RECORD-COUNT                                  02270000
                FROM   XST_VND_STYL                                     02280000
           END-EXEC.                                                    02290000
                                                                        02300000
           EVALUATE TRUE                                                02310000
              WHEN SQLCODE = ZERO                                       02320000
                   CONTINUE                                             02330000
              WHEN OTHER                                                02340000
                   PERFORM 9999-ABEND-ROUTINE                           02350000
           END-EVALUATE.                                                02360000
                                                                        02370000
       EJECT.                                                           02380000
      ******************************************************************02390000
      **PROCESSES INPUT FILE                                            02400000
      ******************************************************************02410000
       2000-PROCESS-RECORDS.                                            02420000
                                                                        02430000
           MOVE '2000-PROCESS-RECORDS' TO PV-PARAGRAPH-NAME.            02440000
                                                                        02450000
           INITIALIZE XSRD010-VNDSTYL-ERR-RECORD.                       02460000
                                                                        02470000
           EVALUATE TRUE                                                02480000
              WHEN IM058-RMS-STYL-ID = XSRD009-STYL-ID                  02490000
                   PERFORM 2100-COMPARE-FIELDS                          02500000
                   PERFORM 9999-READ-XS-EXTRACT  CC-READ-FREQUENCY TIMES02510000
                   PERFORM 9999-READ-VSM-EXTRACT CC-READ-FREQUENCY TIMES02520000
              WHEN IM058-RMS-STYL-ID > XSRD009-STYL-ID                  02530000
                   PERFORM 9999-MOVE-XS-VALUES                          02540000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02550000
                   PERFORM 9999-READ-XS-EXTRACT                         02560000
              WHEN IM058-RMS-STYL-ID < XSRD009-STYL-ID                  02570000
                   PERFORM 9999-MOVE-VSM-VALUES                         02580000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02590000
                   PERFORM 9999-READ-VSM-EXTRACT                        02600000
           END-EVALUATE.                                                02610000
                                                                        02620000
       EJECT.                                                           02630000
      ******************************************************************02640000
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   02650000
      ******************************************************************02660000
       2100-COMPARE-FIELDS.                                             02670000
                                                                        02680000
           MOVE '2100-COMPARE-FIELDS' TO PV-PARAGRAPH-NAME.             02690000
                                                                        02700000
           INITIALIZE PS-ERROR-FOUND-SW.                                02710000
                                                                        02720000
           IF  XSRD009-DEPT-NBR     NOT EQUAL IM058-DEPT-NBR            02730000
               SET PS-ERROR-FOUND     TO TRUE                           02740000
           END-IF.                                                      02750000
                                                                        02760000
           IF  XSRD009-MAJ-CL-NBR   NOT EQUAL IM058-MAJ-CL-NBR          02770000
               SET PS-ERROR-FOUND     TO TRUE                           02780000
           END-IF.                                                      02790000
                                                                        02800000
           IF  XSRD009-SUB-CL-NBR   NOT EQUAL IM058-SUB-CL-NBR          02810000
               SET PS-ERROR-FOUND     TO TRUE                           02820000
           END-IF.                                                      02830000
                                                                        02840000
           IF  XSRD009-VS-ID        NOT EQUAL IM058-VS-ID               02850000
               SET PS-ERROR-FOUND     TO TRUE                           02860000
           END-IF.                                                      02870000
                                                                        02880000
           IF  XSRD009-VS-NBR       NOT EQUAL IM058-VS-NBR              02890000
               SET PS-ERROR-FOUND     TO TRUE                           02900000
           END-IF.                                                      02910000
                                                                        02920000
           IF  XSRD009-ENT-ID       NOT EQUAL IM058-ENT-ID              02930000
               SET PS-ERROR-FOUND     TO TRUE                           02940000
           END-IF.                                                      02950000
                                                                        02960000
           IF  XSRD009-STKR-NBR     NOT EQUAL IM058-STKR-NBR            02970000
               SET PS-ERROR-FOUND     TO TRUE                           02980000
           END-IF.                                                      02990000
                                                                        03000000
           IF  XSRD009-RTL-CLSN-DESC NOT EQUAL IM058-RTL-CLSN-DESC      03010000
               SET PS-ERROR-FOUND     TO TRUE                           03020000
           END-IF.                                                      03030000
                                                                        03040000
           IF  XSRD009-LBL-NM       NOT EQUAL IM058-LBL-DESC            03050000
               SET PS-ERROR-FOUND     TO TRUE                           03060000
           END-IF.                                                      03070000
                                                                        03080000
           IF  XSRD009-ESL-IND       NOT EQUAL IM058-ESL-DESC           03090000
P46039         IF  (IM058-ESL-DESC  = SPACES                            03100000
P46039              AND XSRD009-ESL-IND = 'NO')                         03110002
P46039         OR  (IM058-ESL-DESC  = 'NO'                              03120002
P46039             AND XSRD009-ESL-IND = SPACES)                        03130002
P46039             CONTINUE                                             03140002
P46039         ELSE                                                     03150000
P46039             SET PS-ERROR-FOUND     TO TRUE                       03160002
               END-IF                                                   03170000
           END-IF.                                                      03180000
                                                                        03190000
           IF  XSRD009-HNG-FOLD-DESC NOT EQUAL IM058-HNG-FOLD-DESC      03200000
               SET PS-ERROR-FOUND     TO TRUE                           03210000
           END-IF.                                                      03220000
                                                                        03230000
100704     IF  XSRD009-VS-DESC       NOT EQUAL IM058-VS-DESC            03240000
100704         SET PS-ERROR-FOUND     TO TRUE                           03250000
100704     END-IF.                                                      03260000
                                                                        03270000
           IF  PS-ERROR-FOUND                                           03280000
               PERFORM 9999-MOVE-XS-VALUES                              03290000
               PERFORM 9999-MOVE-VSM-VALUES                             03300000
               PERFORM 9999-WRITE-ERROR-RECORD                          03310000
           END-IF.                                                      03320000
                                                                        03330000
       EJECT.                                                           03340000
      ******************************************************************03350000
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   03360000
      ******************************************************************03370000
       3000-PROCESS-EXTRA-RECORDS.                                      03380000
                                                                        03390000
           MOVE '3000-PROCESS-EXTRA-RECORDS' TO PV-PARAGRAPH-NAME.      03400000
                                                                        03410000
           INITIALIZE DIFFERENCES-RECORD.                               03420000
                                                                        03430000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03440000
               PERFORM 9999-MOVE-VSM-VALUES                             03450000
               PERFORM 9999-WRITE-ERROR-RECORD                          03460000
               PERFORM 9999-READ-VSM-EXTRACT                            03470000
           END-IF.                                                      03480000
                                                                        03490000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03500000
               PERFORM 9999-MOVE-XS-VALUES                              03510000
               PERFORM 9999-WRITE-ERROR-RECORD                          03520000
               PERFORM 9999-READ-XS-EXTRACT                             03530000
           END-IF.                                                      03540000
                                                                        03550000
       EJECT.                                                           03560000
      ***************************************************************** 03570000
      **READ CONTROL CARD FOR FREQUENCY OF COMPARE                      03580000
      ***************************************************************** 03590000
       9999-READ-CONTROL-CARD.                                          03600000
                                                                        03610000
           READ CONTROL-CARD INTO CONTROL-CARD-LAYOUT.                  03620000
                                                                        03630000
       EJECT.                                                           03640000
      ***************************************************************** 03650000
      **READ VSM EXTRACT FILE                                           03660000
      ***************************************************************** 03670000
       9999-READ-VSM-EXTRACT.                                           03680000
                                                                        03690000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03700000
               READ VSM-EXTRACT INTO IM058-ITEM-STYLE-DOMAIN-REC        03710000
                    AT END                                              03720000
                       SET PS-END-OF-VSM-EXTRACT TO TRUE                03730000
               END-READ                                                 03740000
           END-IF.                                                      03750000
                                                                        03760000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03770000
               ADD +1                       TO PC-VSM-EXTRACT-RECS-READ 03780000
           END-IF.                                                      03790000
                                                                        03800000
       EJECT.                                                           03810000
      ***************************************************************** 03820000
      **READ XS EXTRACT FILE                                            03830000
      ***************************************************************** 03840000
       9999-READ-XS-EXTRACT.                                            03850000
                                                                        03860000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03870000
               READ XS-EXTRACT INTO XSRD009-VNDSTYL-UNLOAD-FILE         03880000
                    AT END                                              03890000
                       SET PS-END-OF-XS-EXTRACT TO TRUE                 03900000
               END-READ                                                 03910000
           END-IF.                                                      03920000
                                                                        03930000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03940000
               ADD +1                       TO PC-XS-EXTRACT-RECS-READ  03950000
           END-IF.                                                      03960000
                                                                        03970000
       EJECT.                                                           03980000
      ***************************************************************** 03990000
      **MOVES XS EXTRACT VALUES TO ERROR RECORD                         04000000
      ***************************************************************** 04010000
       9999-MOVE-XS-VALUES.                                             04020000
                                                                        04030000
           MOVE XSRD009-STYL-ID       TO XSRD010-XS-STYL-ID.            04040000
           MOVE XSRD009-DEPT-NBR      TO XSRD010-XS-DEPT-NBR.           04050000
           MOVE XSRD009-MAJ-CL-NBR    TO XSRD010-XS-MAJ-CL-NBR.         04060000
           MOVE XSRD009-SUB-CL-NBR    TO XSRD010-XS-SUB-CL.             04070000
           MOVE XSRD009-VS-ID         TO XSRD010-XS-VS-ID.              04080000
           MOVE XSRD009-VS-NBR        TO XSRD010-XS-VS-NBR.             04090000
           MOVE XSRD009-ENT-ID        TO XSRD010-XS-ENT-ID.             04100000
           MOVE XSRD009-STKR-NBR      TO XSRD010-XS-STKR-NBR.           04110000
           MOVE XSRD009-RTL-CLSN-DESC TO XSRD010-XS-RTL-CLSN-DESC.      04120000
           MOVE XSRD009-LBL-NM        TO XSRD010-XS-LBL-NM.             04130000
           MOVE XSRD009-ESL-IND       TO XSRD010-XS-ESL-IND.            04140000
           MOVE XSRD009-HNG-FOLD-DESC TO XSRD010-XS-HNG-FOLD-DESC.      04150000
100704     MOVE XSRD009-VS-DESC       TO XSRD010-XS-VS-DESC.            04160000
                                                                        04170000
       EJECT.                                                           04180000
      ***************************************************************** 04190000
      **MOVES VSM EXTRACT VALUES TO ERROR RECORD                        04200000
      ***************************************************************** 04210000
       9999-MOVE-VSM-VALUES.                                            04220000
                                                                        04230000
011403     MOVE IM058-RMS-STYL-ID      TO XSRD010-VSM-STYL-ID.          04240000
011403     MOVE IM058-DEPT-NBR         TO XSRD010-VSM-DEPT-NBR.         04250000
011403     MOVE IM058-MAJ-CL-NBR       TO XSRD010-VSM-MAJ-CL-NBR.       04260000
011403     MOVE IM058-SUB-CL-NBR       TO XSRD010-VSM-SUB-CL.           04270000
011403     MOVE IM058-VS-ID            TO XSRD010-VSM-VS-ID.            04280000
011403     MOVE IM058-VS-NBR           TO XSRD010-VSM-VS-NBR.           04290000
011403     MOVE IM058-ENT-ID           TO XSRD010-VSM-ENT-ID.           04300000
011403     MOVE IM058-STKR-NBR         TO XSRD010-VSM-STKR-NBR.         04310000
011403     MOVE IM058-RTL-CLSN-DESC    TO XSRD010-VSM-RTL-CLSN-DESC.    04320000
011403     MOVE IM058-LBL-DESC         TO XSRD010-VSM-LBL-NM.           04330000
011403     MOVE IM058-ESL-DESC         TO XSRD010-VSM-ESL-IND.          04340000
011403     MOVE IM058-HNG-FOLD-DESC    TO XSRD010-VSM-HNG-FOLD-DESC.    04350000
100704     MOVE IM058-VS-DESC          TO XSRD010-VSM-VS-DESC.          04360000
                                                                        04370000
       EJECT.                                                           04380000
      ***************************************************************** 04390000
      **ERROR FILE                                                      04400000
      ***************************************************************** 04410000
       9999-WRITE-ERROR-RECORD.                                         04420000
                                                                        04430000
           WRITE DIFFERENCES-RECORD FROM XSRD010-VNDSTYL-ERR-RECORD.    04440000
                                                                        04450000
           ADD +1                       TO PC-ERROR-RECORD-WRITTEN.     04460000
                                                                        04470000
       EJECT.                                                           04480000
      ****************************************************************  04490000
      *END OF PROGRAM ROUTINE                                           04500000
      ****************************************************************  04510000
       9999-WRAPUP.                                                     04520000
                                                                        04530000
           CLOSE CONTROL-CARD                                           04540000
                 VSM-EXTRACT                                            04550000
                 XS-EXTRACT                                             04560000
                 DIFFERENCES-FILE.                                      04570000
                                                                        04580000
           PERFORM 9999-DISPLAY-STATISTICS.                             04590000
                                                                        04600000
           IF  PC-ERROR-RECORD-WRITTEN > 0                              04610000
               MOVE 04               TO RETURN-CODE                     04620000
           END-IF.                                                      04630000
                                                                        04640000
       EJECT.                                                           04650000
      ****************************************************************  04660000
      *DISPLAY PROGRAM STATISTICS                                       04670000
      ****************************************************************  04680000
       9999-DISPLAY-STATISTICS.                                         04690000
                                                                        04700000
           DISPLAY '*************************************************'. 04710000
           DISPLAY '* PROGRAM..............: ' PC-CURRENT-PROGRAM-NAME. 04720000
           DISPLAY '* DATE/TIMESTAMP.......: ' SD-TIMESTAMP.            04730000
           DISPLAY '* RECORDS IN TABLE.....: ' PC-RECORD-COUNT.         04740000
           DISPLAY '* VSM EXTRACT RECORDS..: ' PC-VSM-EXTRACT-RECS-READ.04750000
           DISPLAY '* XS EXTRACT RECORDS...: ' PC-XS-EXTRACT-RECS-READ. 04760000
           DISPLAY '* ERROR RECORDS WRITTEN: ' PC-ERROR-RECORD-WRITTEN. 04770000
           DISPLAY '*************************************************'. 04780000
                                                                        04790000
       EJECT.                                                           04800000
      ******************************************************************04810000
      **SQL ABEND ROUTINE                                               04820000
      ******************************************************************04830000
       9999-ABEND-ROUTINE.                                              04840000
                                                                        04850000
           DISPLAY '** ABEND     **'.                                   04860000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        04870000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04880000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               04890000
      ******************************************************************04900000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     04910000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      04920000
      ******************************************************************04930000
           COPY DPPD004.                                                04940000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04950000
           CALL 'ILBOABN0' USING ABEND-CODE.                            04960000
                                                                        04970000
       EJECT                                                            04980000
