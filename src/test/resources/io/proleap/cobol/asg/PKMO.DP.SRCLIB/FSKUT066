      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.             FSKUT066.                                        
       AUTHOR.                 JOHN PRICE.                                      
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            JULY 2000.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *                                                                *        
      *  THIS PROGRAM WILL COMPUTE CONTROL TOTALS,  QUANTITY AND       *        
      *  AMOUNT FROM THE DAILY SALES TRANSACTIONS (EXTRACT FILE)       *        
      *  PRIOR TO RUNNING DAILY SALES DB/2 UPDATES                     *        
      *  THESE TOTALS WILL BE CHECKED PRIOR TO RUNNING FSK030.         *        
      *                                                                *        
      ******************************************************************        
      * 07/22/2002 MARIA KLAMIK - RECOMPLIE PROGRAM WITH CHANGES TO    *        
      *       KDU1806 FOR NEW SKU.                                     *        
      *                                           (WR# 21568)          *        
      ******************************************************************        
207210* 08/24/2018 JIM HUNTER  - REPLACE COPYBOOK KDU1806 WITH PSWS010 *        
207210*                                           (CHG0207210)         *        
207210******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT EXTRACT-FILE                                                  
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT VERIFICATION-FILE                                             
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  EXTRACT-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 65 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS EXTRACT-RECORD.                                       
                                                                                
       01  EXTRACT-RECORD                   PIC X(65).                          
                                                                                
       FD  VERIFICATION-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 65 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS VERIFICATION-RECORD.                                  
                                                                                
       01  VERIFICATION-RECORD              PIC X(65).                          
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'FSKUT066'.        
           05  PC-PROC-NAME                 PIC X(08)  VALUE 'FSK029  '.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EXTRACT-FILE-SW           PIC X(01)  VALUE 'N'.               
               88  PS-END-OF-EXTRACT-FILE              VALUE 'Y'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DATE.                                                         
               10  PV-DATE-YY               PIC 9(02)  VALUE ZERO.              
               10  PV-DATE-MM               PIC 9(02)  VALUE ZERO.              
               10  PV-DATE-DD               PIC 9(02)  VALUE ZERO.              
                                                                                
           05  PV-TIME.                                                         
               10  PV-TIME-HH               PIC 9(02)  VALUE ZERO.              
               10  PV-TIME-MM               PIC 9(02)  VALUE ZERO.              
               10  PV-TIME-SS               PIC 9(02)  VALUE ZERO.              
               10  PV-TIME-HS               PIC 9(02)  VALUE ZERO.              
                                                                                
           05  PV-DISPLAY-QTY               PIC ZZZ,ZZZ,ZZ9.                    
           05  PV-DISPLAY-AMT               PIC ZZZ,ZZZ,ZZ9.99.                 
           05  PV-DISPLAY-RCDS-READ         PIC Z,ZZZ,ZZ9.                      
                                                                                
       01 PA-PROGRAM-ACCUMULATORE.                                              
          05  PA-EXTRACT-RCDS-READ          PIC 9(07) COMP-3 VALUE ZERO.        
          05  PA-TOTAL-QTY                  PIC S9(09)       COMP-3             
                                                             VALUE ZERO.        
          05  PA-TOTAL-AMT                  PIC S9(09)V9(02) COMP-3             
                                                             VALUE ZERO.        
                                                                                
      ******************************************************************        
      *    THIS IS THE COPYBOOK FOR BOTH THE INPUT AND OUT FILES       *        
      ******************************************************************        
                                                                                
207210     COPY PSWS010.                                                        
                                                                                
       01  OUT01-RECORD.                                                        
           05  OUT01-ID.                                                        
               10  OUT01-PROC-NAME      PIC X(08)     VALUE 'FSK029  '.         
               10  FILLER               PIC X(02).                              
               10  OUT01-RUN-DATE.                                              
                   15  OUT01-RUN-MONTH  PIC 9(02)     VALUE ZERO.               
                   15  FILLER           PIC X(01)     VALUE '/'.                
                   15  OUT01-RUN-DAY    PIC 9(02)     VALUE ZERO.               
                   15  FILLER           PIC X(01)     VALUE '/'.                
                   15  OUT01-RUN-YEAR   PIC 9(02)     VALUE ZERO.               
               10  FILLER               PIC X(02).                              
               10  OUT01-RUN-TIME.                                              
                   15  OUT01-RUN-HOUR   PIC 9(02)     VALUE ZERO.               
                   15  FILLER           PIC X(01)     VALUE ':'.                
                   15  OUT01-RUN-MINUTE PIC 9(02)     VALUE ZERO.               
               10  FILLER               PIC X(02).                              
               10  OUT01-QTY-TOTAL      PIC S9(09)       COMP-3                 
                                                         VALUE +0.              
               10  OUT01-AMT-TOTAL      PIC S9(09)V9(02) COMP-3                 
                                                         VALUE +0.              
               10  FILLER               PIC X(18)     VALUE SPACES.             
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL PS-END-OF-EXTRACT-FILE.                                    
                                                                                
           PERFORM 2200-BUILD-VERIFICATION-FILE.                                
                                                                                
           PERFORM 8200-WRITE-VERIFICATION-FILE.                                
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WILL:                                                   
      *      1.  OPEN I/O FILES                                                 
      *      2.  READ FIRST EXTRACT INPUT RECORD                                
      ******************************************************************        
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  EXTRACT-FILE                                             
                OUTPUT VERIFICATION-FILE.                                       
                                                                                
           PERFORM 8100-READ-EXTRACT-FILE.                                      
                                                                                
           IF  PS-END-OF-EXTRACT-FILE                                           
               DISPLAY SPACE                                                    
               DISPLAY '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@'          
               DISPLAY SPACE                                                    
               DISPLAY 'PROGRAM      = ' PC-PROGRAM-NAME                        
               DISPLAY '** ABEND **  = EXTRACT INPUT FILE IS EMPTY'             
               DISPLAY SPACE                                                    
               DISPLAY '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@'          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WILL:                                                   
      *      1.  PERFORM VARIOUS ROUTINES TO PROCESS THE INPUT RECORDS          
      *          AND WRITE THE MODIFIED RECORDS TO THE OUTPUT FILE              
      ******************************************************************        
                                                                                
       2000-MAIN-ROUTINE.                                                       
                                                                                
           COMPUTE PA-TOTAL-QTY = PA-TOTAL-QTY + D040-SALE-UNITS.               
           COMPUTE PA-TOTAL-AMT = PA-TOTAL-AMT + D040-SALE-AMT.                 
                                                                                
           PERFORM 8100-READ-EXTRACT-FILE.                                      
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WILL:                                                   
      *      1.  GET THE SYSTEM DATE AND TIME FOR THE OUTPUT RECORD             
      ******************************************************************        
                                                                                
       2200-BUILD-VERIFICATION-FILE.                                            
                                                                                
           ACCEPT PV-DATE                     FROM DATE.                        
           ACCEPT PV-TIME                     FROM TIME.                        
                                                                                
           MOVE PV-DATE-YY                    TO OUT01-RUN-YEAR.                
           MOVE PV-DATE-MM                    TO OUT01-RUN-MONTH.               
           MOVE PV-DATE-DD                    TO OUT01-RUN-DAY.                 
                                                                                
           MOVE PV-TIME-HH                    TO OUT01-RUN-HOUR.                
           MOVE PV-TIME-MM                    TO OUT01-RUN-MINUTE.              
                                                                                
           MOVE PA-TOTAL-QTY                  TO OUT01-QTY-TOTAL.               
           MOVE PA-TOTAL-AMT                  TO OUT01-AMT-TOTAL.               
                                                                                
           MOVE PC-PROC-NAME                  TO OUT01-PROC-NAME.               
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WILL:                                          *        
      *      1. READ THE INPUT DAILY-SALES FILE                        *        
      ******************************************************************        
       8100-READ-EXTRACT-FILE.                                                  
                                                                                
           READ EXTRACT-FILE INTO D040-RECORD                                   
               AT END                                                           
                   SET PS-END-OF-EXTRACT-FILE      TO  TRUE.                    
                                                                                
           IF  PS-END-OF-EXTRACT-FILE                                           
               NEXT SENTENCE                                                    
           ELSE                                                                 
               ADD +1                    TO PA-EXTRACT-RCDS-READ                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WILL:                                          *        
      *      1. WRITE THE OUTPUT VERIFICATION-FILE                     *        
      ******************************************************************        
                                                                                
       8200-WRITE-VERIFICATION-FILE.                                            
                                                                                
           WRITE VERIFICATION-RECORD                                            
               FROM OUT01-RECORD.                                               
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WILL:                                          *        
      *      1. CLOSE INPUT & OUTPUT FILES                             *        
      *      2. DISPLAY VARIOUS TOTALS                                 *        
      ******************************************************************        
                                                                                
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE EXTRACT-FILE                                                   
                 VERIFICATION-FILE.                                             
                                                                                
           MOVE PA-TOTAL-QTY                  TO PV-DISPLAY-QTY.                
           MOVE PA-TOTAL-AMT                  TO PV-DISPLAY-AMT.                
           MOVE PA-EXTRACT-RCDS-READ          TO PV-DISPLAY-RCDS-READ.          
                                                                                
           DISPLAY SPACE                                                        
           DISPLAY '@@@@@@@@@@@@@ PROGRAM FSKUT066 TOTALS @@@@@@@@@@@@@'        
           DISPLAY SPACE                                                        
           DISPLAY '   TOTAL ACCUMULATED QUANTITY     = '                       
                                              PV-DISPLAY-QTY                    
           DISPLAY '   TOTAL ACCUMULATED AMOUNT       = '                       
                                              PV-DISPLAY-AMT                    
           DISPLAY SPACE                                                        
           DISPLAY '   TOTAL INPUT RECORDS READ       = '                       
                                              PV-DISPLAY-RCDS-READ              
           DISPLAY SPACE                                                        
           DISPLAY '@@@@@@@@@@@@@ PROGRAM FSKUT066 TOTALS @@@@@@@@@@@@@'        
           DISPLAY SPACE.                                                       
