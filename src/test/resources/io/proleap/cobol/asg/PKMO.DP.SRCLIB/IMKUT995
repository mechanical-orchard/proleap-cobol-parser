       ID  DIVISION.                                                    05/31/95
       PROGRAM-ID.         IMKUT995.                                    MLCC621 
       AUTHOR.             ACCENTURE.                                      LV091
       INSTALLATION.       KOHLS DEPARTMENT STORES.                        CL**1
       DATE-WRITTEN.       SEP 2013.                                       CL*48
       DATE-COMPILED.                                                      CL**1
                                                                           CL**1
      ******************************************************************        
      *   IMKUT995 - UNIQUE KOHLS STYLE                                         
      ******************************************************************        
      *THIS PROGRAM SHALL RETRIEVE THE ATTRIBUTE IM020-RMS-STYL-ID,             
      *IM020-VS-ID AND IM020-SUB-CL-MSDEAR-ID FROM OUTPUT FILE FROM             
      *PREVIOUS STEP. USING THESE ATTRIBUTES FROM THE FILE RUN A DB2            
      *QUERY TO VERIFY WHETHER THE SAME IS EXISTING IN TMDARVS TABLE            
      *AND EXTRACT ALL THE UNIQUE VALUES OF 'RMS-STYLE-ID-S AND                 
      *WRITE INTO AN OUTPUT FILE.                                               
      ******************************************************************        
      * PROJECT HISTORY:                                                        
      * PROJ      DATE       NAME         DESCRIPTION                           
      * W3502S    04/05/2013 ROHAN DUA    INITIAL CREATION                      
      ******************************************************************        
      *                                                                    CL**1
       ENVIRONMENT DIVISION.                                               CL**1
      *                                                                    CL**1
       CONFIGURATION SECTION.                                              CL**1
      *                                                                    CL**1
       SOURCE-COMPUTER.        IBM-3090.                                   CL**1
       OBJECT-COMPUTER.        IBM-3090.                                   CL**1
                                                                           CL**1
       INPUT-OUTPUT SECTION.                                               CL**1
       FILE-CONTROL.                                                       CL**1
           SELECT SKU-DELETE-INPUT-FILE                                         
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT IM-DEL-RMS-STYLE-FILE                                         
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                      CL**1
       FILE SECTION.                                                       CL**1
       FD  SKU-DELETE-INPUT-FILE                                                
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-DELETE-INPUT-RECORD         PIC X(100).                          
                                                                                
                                                                                
       FD  IM-DEL-RMS-STYLE-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DELETED-RMS-STYLE-ID            PIC Z(08).                           
                                                                                
       WORKING-STORAGE SECTION.                                            CL**1
                                                                           CL**1
      *****************************************************************    CL**1
      *PROGRAM VARIABLES                                                        
      *****************************************************************    CL**1
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME           PIC  X(30) VALUE SPACES.        CL*40
           05  PV-DB2-OPER-ATTEMPTED       PIC  X(30) VALUE SPACES.        CL*40
           05  PV-SAVE-DTL-KEY.                                                 
               10 PV-SAVE-SUB-CL-MDSEAR-ID PIC S9(06) COMP-3                    
                                                      VALUE 0.                  
               10 PV-SAVE-VS-ID            PIC S9(09) COMP                      
                                                      VALUE 0.                  
               10 PV-SAVE-RMS-STYL-ID      PIC S9(08) COMP                      
                                                      VALUE 0.                  
           05 PV-HOLD-RMS-STYLE-ID         PIC S9(08) COMP                      
                                                      VALUE 0.                  
       01  INPUT-FILE-STATUS               PIC X(02)  VALUE '00'.               
           88  INPUT-FILE-SUCCESS                     VALUE '00'.               
           88  INPUT-FILE-END                         VALUE '10'.               
           88  INPUT-FILE-ERROR                       VALUE '42'.               
       01  OUTPUT-FILE-STATUS              PIC X(02)  VALUE '00'.               
           88  OUTPUT-FILE-SUCCESS                    VALUE '00'.               
           88  OUTPUT-FILE-END                        VALUE '10'.               
           88  OUTPUT-FILE-ERROR                      VALUE '42'.               
      *****************************************************************    CL**1
      *SWITCHES                                                                 
      *****************************************************************    CL**1
       01  PS-PROGRAM-SWITCHES.                                            CL*15
           05  WS-ABEND-CODE               PIC S9(04) VALUE ZERO                
                                                      COMP SYNC.                
               88  ABEND-4005-OPEN-ERROR              VALUE +4005.              
               88  ABEND-4006-CLOSE-ERROR             VALUE +4006.              
      *****************************************************************    CL**1
      *ACCUMULATORS                                                             
      *****************************************************************    CL**1
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-READ-COUNT          PIC S9(7)  VALUE 0                   
                                                      COMP-3.                   
           05  PA-RECS-WRITE-COUNT         PIC S9(7)  VALUE 0                   
                                                      COMP-3.                   
       01  PW-PROGRAM-WORKAREA.                                                 
           05  PW-EOF-CODE                 PIC X(1)   VALUE 'N'.                
               88  PW-EOF                             VALUE 'Y'.                
      *                                                                    CL**1
      ******************************************************************00940000
      *    COPYBOOK FOR INPUT FILE.                                     00950000
      ******************************************************************00960000
           COPY IMRD020.                                                00970000
      *****************************************************************    CL**1
      *****  DB2 TABLE DCLGEN                                              CL*47
      *****************************************************************    CL**1
      *                                                                    CL**1
           EXEC SQL                                                        CL**1
               INCLUDE TMDARVS                                             CL*47
           END-EXEC.                                                       CL**1
      *                                                                    CL*47
      *---------------------------------------------------------------*         
                                                                                
       01  ABEND-CODE               PIC S9(04)  VALUE ZERO                 CL**1
                                                COMP SYNC.                 CL**1
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
      *****************************************************************         
       LINKAGE SECTION.                                                    CL**1
      *                                                                    CL**1
       PROCEDURE DIVISION.                                                 CL**1
      *                                                                    CL**1
      ******************************************************************   CL**1
      * MAIN-LINE LOGIC.                                                   CL**1
      ******************************************************************   CL**1
       0000-MAIN-MODULE.                                                   CL**1
      *                                                                    CL**1
                                                                           CL*63
           PERFORM 1000-INITIALIZATION.                                    CL**1
                                                                                
           PERFORM 2000-READ-SKU-DELETE-FILE                                    
             UNTIL PW-EOF.                                                      
                                                                                
           PERFORM 5000-END-OF-JOB.                                             
                                                                                
           STOP RUN.                                                            
      *----------------------------------------------------------------*        
       1000-INITIALIZATION.                                                     
           PERFORM 1200-OPEN-READ-FILE                                          
           PERFORM 1400-OPEN-WRITE-FILE                                         
           PERFORM 2000-READ-SKU-DELETE-FILE.                                   
      *                                                                    CL*15
      ******************************************************************        
      * 1200-OPEN-READ-FILE.                                           *        
      * THIS PARAGRAPH DOES CHECK FOR SUCCESFUL FILE OPEN              *        
      ******************************************************************        
       1200-OPEN-READ-FILE.                                                     
            OPEN INPUT SKU-DELETE-INPUT-FILE.                                   
                IF INPUT-FILE-SUCCESS THEN                                      
                  CONTINUE                                                      
                ELSE                                                            
                  DISPLAY 'INPUT FILE OPEN ERROR =>' INPUT-FILE-STATUS          
                  SET ABEND-4005-OPEN-ERROR TO TRUE                             
                  PERFORM 9000-SQL-ABEND                                        
                END-IF.                                                         
      ******************************************************************        
      * 1400-OPEN-WRITE-FILE.                                                   
      * THIS PARAGRAPH DOES CHECK FOR SUCCESFUL FILE OPEN                       
      *****************************************************************         
       1400-OPEN-WRITE-FILE.                                                    
           OPEN OUTPUT IM-DEL-RMS-STYLE-FILE.                                   
           IF OUTPUT-FILE-SUCCESS THEN                                          
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'FILE OPEN ERROR =>' OUTPUT-FILE-STATUS                   
              SET ABEND-4005-OPEN-ERROR TO TRUE                                 
              PERFORM 9000-SQL-ABEND                                            
           END-IF.                                                              
      ******************************************************************        
      * 1600-CLOSE-WRITE-FILE.                                         *        
      *  THIS PARAGRAPH DOES CHECK FOR SUCCESFUL FILE CLOSE            *        
      ******************************************************************        
       1600-CLOSE-READ-FILE.                                                    
           CLOSE SKU-DELETE-INPUT-FILE.                                         
           IF OUTPUT-FILE-SUCCESS THEN                                          
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'FILE CLOSE ERROR =>'                                     
                                        OUTPUT-FILE-STATUS                      
              SET ABEND-4006-CLOSE-ERROR TO TRUE                                
              PERFORM 9000-SQL-ABEND                                            
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 1800-CLOSE-WRITE-FILE.                                         *        
      *  THIS PARAGRAPH DOES CHECK FOR SUCCESFUL FILE CLOSE            *        
      ******************************************************************        
       1800-CLOSE-WRITE-FILE.                                                   
           CLOSE IM-DEL-RMS-STYLE-FILE.                                         
           IF OUTPUT-FILE-SUCCESS THEN                                          
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'FILE CLOSE ERROR =>'                                     
                                        OUTPUT-FILE-STATUS                      
              SET ABEND-4006-CLOSE-ERROR TO TRUE                                
              PERFORM 9000-SQL-ABEND                                            
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2000-READ-SKU-DELETE-FILE.                                     *        
      *  THIS PARAGRAPH READS ALL ATTRIBUTES FROM THE FILE             *        
      *  EXTRACTS SUB-CL-MDSEAR-ID & VS-ID                             *        
      ******************************************************************        
       2000-READ-SKU-DELETE-FILE.                                               
                                                                                
           MOVE '*2000-READ-SKU-DELETE-FILE  *' TO PV-PARAGRAPH-NAME.           
                                                                                
           READ SKU-DELETE-INPUT-FILE                                           
               INTO IM020-STYLE-DELETE-REC                                      
                   AT END  SET PW-EOF TO TRUE.                                  
                                                                                
           IF PW-EOF                                                            
               CONTINUE                                                         
           ELSE                                                                 
               ADD +1                      TO PA-RECS-READ-COUNT                
               MOVE IM020-SUB-CL-MDSEAR-ID TO PV-SAVE-SUB-CL-MDSEAR-ID          
               MOVE IM020-VS-ID            TO PV-SAVE-VS-ID                     
               MOVE IM020-RMS-STYL-ID      TO PV-SAVE-RMS-STYL-ID               
               PERFORM 3000-FETCH-RMS-STYLE-ID                                  
           END-IF.                                                              
                                                                                
      ******************************************************************   CL*15
      * 3000-FETCH-RMS-STYLE-ID                                            CL*15
      * FETCH RMS-STYLE-ID FROM VS_ID AND SUB_CL_MDSEAR_ID FROM TMDARVS    CL*15
      ******************************************************************   CL*15
       3000-FETCH-RMS-STYLE-ID.                                                 
                                                                                
                                                                                
           MOVE '3000-FETCH-RMS-STYLE-ID   ' TO PV-PARAGRAPH-NAME.              
                                                                                
           EXEC SQL                                                             
             SELECT  RMS_STYL_ID                                                
               INTO  :MDARVS-RMS-STYL-ID                                        
               FROM  TMDARVS                                                    
                 WHERE SUB_CL_MDSEAR_ID = :PV-SAVE-SUB-CL-MDSEAR-ID             
                 AND VS_ID              = :PV-SAVE-VS-ID                        
           END-EXEC.                                                            
            EVALUATE TRUE                                                       
               WHEN SQLCODE = +0                                                
                  IF PV-HOLD-RMS-STYLE-ID NOT EQUAL PV-SAVE-RMS-STYL-ID         
                    MOVE  MDARVS-RMS-STYL-ID TO PV-HOLD-RMS-STYLE-ID            
                    MOVE  MDARVS-RMS-STYL-ID TO DELETED-RMS-STYLE-ID            
                    PERFORM 4000-WRITE-RMS-STYLE-FILE                           
                  END-IF                                                        
               WHEN OTHER                                                       
                  PERFORM 9000-SQL-ABEND                                        
            END-EVALUATE.                                                       
      ******************************************************************   CL*15
      * 4000-WRITE-OUTPUT-FILE                                             CL*15
      * WRITE UNIQUE RMS-STYLE-ID'S INTO THE FILE                          CL*15
      ******************************************************************   CL*15
       4000-WRITE-RMS-STYLE-FILE.                                          CL*30
      *                                                                    CL*15
           ADD 1 TO PA-RECS-WRITE-COUNT.                                        
                                                                                
           WRITE DELETED-RMS-STYLE-ID.                                     CL*15
      ******************************************************************   CL*15
      * 5000-END-OF-JOB.                                                   CL*15
      ******************************************************************   CL*15
       5000-END-OF-JOB.                                                         
           DISPLAY PA-RECS-READ-COUNT.                                          
           DISPLAY PA-RECS-WRITE-COUNT.                                         
           PERFORM 1600-CLOSE-READ-FILE                                         
           PERFORM 1800-CLOSE-WRITE-FILE.                                       
                                                                                
      ******************************************************************   CL*15
      *  STANDARD DB2 ERROR ABEND ROUTINE                                  CL**1
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                CL**1
      ******************************************************************   CL**1
       9000-SQL-ABEND.                                                     CL*30
                                                                           CL**1
           PERFORM 5000-END-OF-JOB                                              
           MOVE +4013                 TO ABEND-CODE.                       CL**1
           DISPLAY '**  ABEND     **'.                                     CL**1
           DISPLAY '**  PROGRAM   **  = IMKUT995 '.                        CL*23
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.               CL**1
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.           CL**1
                                                                           CL**1
           CALL 'ILBOABN0'  USING ABEND-CODE.                              CL**1
                                                                                
