       IDENTIFICATION DIVISION.                                         08/16/04
      ******************************************************************INKUP012
                                                                           LV020
       PROGRAM-ID.             INKUP012.                                   CL**1
       AUTHOR.                 PAUL KEBER.                                 CL**1
       INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL**1
       DATE-WRITTEN.           08/16/2004.                                 CL**1
       DATE-COMPILED.                                                      CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      *  INKUP012 - ITEM CUTOFF INDICATOR UPDATE                           CL**1
      ******************************************************************   CL**1
      * THIS PROGRAM WILL UPDATE THE TINVPAR ITEM CUTOFF INDICATOR     *   CL**1
      * FROM 'N' TO 'Y' FOR ALL STORES WHERE THE ITEM CUTOFF IS BEING  *   CL**1
      * TAKEN FOR THE FIRST TIME DURING THIS INVENTORY PERIOD.         *   CL**1
      *                                                                *        
      * THE INPUT TO THIS PROGRAM WILL CONTAIN A CONTROL CARD FOR THE  *        
      * PROCESSING DATE.                                               *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * WR/PITS#    DATE    DESCRIPTION                                 00200031
      * --------  --------  ------------------------------------------- 00210031
      * WR26600   08/16/04  PAUL KEBER - INITIAL INSTALLATION.          00220031
W28545* W28545    08/10/2006  ADD LOGIC FOR DEPARTMENT LEVEL                    
W28545*                      INVENTORY PROCESS.                                 
W28545*                      -B. GRAHAM - STRATAGEM                             
W33304* W33304    04/09/2008  HOLD INVENTORY LEDGER OPEN ACROSS PERIODS         
W33304*                       CHUCK EBERT - STRATAGEM                           
      ******************************************************************   CL**1
                                                                           CL**1
      ******************************************************************   CL**1
       ENVIRONMENT DIVISION.                                               CL**1
      ******************************************************************   CL**1
                                                                           CL**1
       CONFIGURATION SECTION.                                              CL**1
                                                                           CL**1
       SOURCE-COMPUTER.                        IBM-3090.                   CL**1
       OBJECT-COMPUTER.                        IBM-3090.                   CL**1
                                                                           CL**1
       INPUT-OUTPUT SECTION.                                               CL**1
                                                                           CL**1
       FILE-CONTROL.                                                       CL**1
                                                                           CL**1
           SELECT                CC-DATE-CNTL-FILE                         CL**1
                                 ASSIGN TO INP01.                          CL**1
                                                                           CL**1
           SELECT                INV-CUTOFF-STORES-FILE                    CL**1
                                 ASSIGN TO INP02.                          CL**1
                                                                           CL**1
      ******************************************************************   CL**1
       DATA DIVISION.                                                      CL**1
      ******************************************************************   CL**1
                                                                           CL**1
       FILE SECTION.                                                       CL**1
                                                                           CL**1
       FD  CC-DATE-CNTL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CC-DATE-CNTL-REC                    PIC X(80).                       
                                                                                
       FD  INV-CUTOFF-STORES-FILE                                          CL**1
           RECORDING MODE IS F                                             CL**1
           LABEL RECORDS ARE STANDARD                                      CL**1
           BLOCK CONTAINS 0 RECORDS.                                       CL**1
                                                                           CL**1
       01  INV-CUTOFF-STORES-RECORD            PIC X(2).                   CL**1
                                                                           CL**1
       WORKING-STORAGE SECTION.                                            CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      * PROGRAM CONSTANTS                                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL**1
       01  PC-PROGRAM-CONSTANTS.                                           CL**1
           05  PC-PROGRAM-NAME                 PIC  X(08)  VALUE           CL**1
               'INKUP012'.                                                 CL*17
           05  PC-MAXIMUM-RETRY-COUNT          PIC S9(04)  VALUE +2             
                                                           COMP SYNC.           
                                                                           CL**1
       01  ABEND-CODE                          PIC S9(04)                  CL**1
                                               COMP SYNC   VALUE 0.        CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      * PROGRAM VARIABLES                                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL*17
       01  PV-PROGRAM-VARIABLES.                                           CL**1
           05  PV-RETRY-COUNT                  PIC S9(04)  VALUE ZERO           
                                                           COMP SYNC.           
           05  PV-DB2-OPERATION-ATTEMPTED      PIC X(30)   VALUE SPACE.    CL**1
           05  PV-PARAGRAPH-NAME               PIC X(30)   VALUE SPACE.    CL**1
           05  PV-TINVPAR-NOT-FOUND-MSG.                                   CL**1
               10  FILLER                      PIC X(36)   VALUE           CL**1
                   'TINVPAR ROW NOT FOUND FOR LOCATION: '.                      
               10  PV-TINVPAR-NFM-LOC-NBR      PIC Z(04)   VALUE ZERO.     CL**1
                                                                           CL**1
       01  PV-PROCESS-DATE-AREA.                                                
           05  PV-PROCESS-DATE             PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES PV-PROCESS-DATE.                                
               10  PV-PROCESS-CCYY         PIC X(04).                           
               10  PV-PROCESS-HYPHEN1      PIC X(01).                           
               10  PV-PROCESS-MM           PIC X(02).                           
               10  PV-PROCESS-HYPHEN2      PIC X(01).                           
               10  PV-PROCESS-DD           PIC X(02).                           
           05  FILLER                      PIC X(70)       VALUE SPACES.        
                                                                                
      ******************************************************************   CL**1
      * PROGRAM SWITCHES                                               *   CL**1
      ******************************************************************   CL**1
                                                                           CL*17
       01  PS-PROGRAM-SWITCHES.                                            CL**1
           05  PS-END-OF-INPUT-SW              PIC X(01)   VALUE 'N'.      CL**1
               88  PS-END-OF-INPUT                         VALUE 'Y'.      CL**1
                                                                           CL**1
      ******************************************************************   CL*15
      * PROGRAM ACCUMULATORS                                           *   CL*15
      ******************************************************************   CL*15
                                                                           CL*17
       01  PA-PROGRAM-ACCUMULATORS.                                        CL*15
           05  PA-INPUT-RECS-READ              PIC S9(09)  COMP-3          CL*15
                                                           VALUE ZERO.     CL*15
           05  PA-TINVPAR-ROWS-UPDATED         PIC S9(09)  COMP-3          CL*15
                                                           VALUE ZERO.     CL*15
                                                                           CL*15
      ******************************************************************        
      * INVENTORY CUTOFF STORES INPUT FILE RECORD LAYOUT.                       
      ******************************************************************        
       01  IC-INV-CUTOFF-STORES-REC.                                            
           05  IC-STORE                        PIC S9(04)  VALUE ZERO           
                                                           COMP SYNC.           
                                                                           CL**1
      ******************************************************************01140000
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                   CL**1
      ******************************************************************01140000
           EXEC SQL                                                        CL**1
               INCLUDE SQLCA                                               CL**1
           END-EXEC.                                                       CL**1
                                                                           CL**1
           COPY SQLCA2.                                                    CL**1
                                                                           CL**1
      ******************************************************************01140000
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004           CL**1
      ******************************************************************01140000
           COPY DPWS004.                                                   CL**1
                                                                           CL**1
28545                                                                           
28545 *****************************************************************         
28545 * TINCNTL DCLGEN                                                          
28545 *****************************************************************         
28545      EXEC SQL                                                             
28545          INCLUDE TINCNTL                                                  
28545      END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TINVPAR TABLE DCLGEN                                                   
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                           CL**1
W33304******************************************************************        
W33304*  SQL DB2 TINVPAR CURSOR DECLARATION                                     
W33304******************************************************************        
W33304*    EXEC SQL                                                             
W33304*        DECLARE TINVPAR_CSR CURSOR FOR                                   
W33304*            SELECT ACTL_INV_DTE                                      0291
W33304*                       ,A.INV_ID                                     0291
W33304*            FROM TINVPAR A                                               
W33304*                ,TINCNTL B                                               
W33304*            WHERE LOC_NBR           = :INVPAR-LOC-NBR                0312
W33304*              AND ACTL_FIN_BK_DTE   = '9999-09-09'                   0313
W33304*              AND PLND_INV_DTE     >= :PV-PROCESS-DATE               0314
W33304*              AND SCHD_PHY_CNT_IND IN ('Y','C')                      0315
W33304*              AND A.INV_ID = B.INV_ID                                    
W33304*              AND B.ACTV_IND = 'Y'                                       
W33304*            ORDER BY PLND_INV_DTE                                        
W33304*    END-EXEC.                                                            
                                                                                
       LINKAGE SECTION.                                                    CL**1
                                                                           CL**1
       PROCEDURE DIVISION.                                                 CL**1
                                                                           CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      *                 MAINLINE.                                      *   CL**6
      ******************************************************************   CL**1
       0000-MAINLINE.                                                      CL**1
                                                                           CL**1
           PERFORM 1000-INITIALIZE.                                        CL*12
                                                                           CL**1
           PERFORM 2000-PROCESS-INPUT                                      CL*12
               UNTIL PS-END-OF-INPUT.                                      CL**1
                                                                           CL**1
           PERFORM 9000-EOJ-ROUTINE.                                       CL*12
                                                                           CL**1
           GOBACK.                                                         CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      *  OPEN ALL FILES AND READ THE INPUT FILES.                      *   CL**6
      ******************************************************************   CL**1
                                                                           CL**1
       1000-INITIALIZE.                                                    CL**1
                                                                           CL**1
           OPEN INPUT CC-DATE-CNTL-FILE                                    CL**1
                      INV-CUTOFF-STORES-FILE.                              CL**1
                                                                           CL**1
           PERFORM 1100-READ-PROCESS-DATE.                                      
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' PROCESSING DATE: ' PV-PROCESS-DATE.                        
                                                                                
           IF PV-PROCESS-CCYY NOT NUMERIC                                       
             OR PV-PROCESS-MM NOT NUMERIC                                       
             OR PV-PROCESS-DD NOT NUMERIC                                       
             OR PV-PROCESS-HYPHEN1 NOT = '-'                                    
             OR PV-PROCESS-HYPHEN2 NOT = '-'                                    
               MOVE +4002                 TO ABEND-CODE                         
               DISPLAY '**  ABEND     **'                                       
               DISPLAY '**  PROGRAM   **  =  INKUP012'                          
               DISPLAY '**  INVALID PROCESSING DATE '                           
               CALL 'ILBOABN0'  USING ABEND-CODE                                
           END-IF.                                                              
                                                                                
           PERFORM 1200-READ-CUTOFF-STORES-FILE.                           CL*12
                                                                           CL**1
      ******************************************************************        
      *  READ THE PROCESS DATE CONTROL CARD FILE                                
      ******************************************************************        
       1100-READ-PROCESS-DATE.                                                  
                                                                                
           READ CC-DATE-CNTL-FILE INTO PV-PROCESS-DATE-AREA                     
               AT END                                                           
                   MOVE +4001                 TO ABEND-CODE                     
                   DISPLAY '**  ABEND     **'                                   
                   DISPLAY '**  PROGRAM   **  =  INKUP012'                      
                   DISPLAY '**  EMPTY DATE CONTROL FILE'                        
                   CALL 'ILBOABN0'  USING ABEND-CODE                            
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *  READ THE INVENTORY CUTOFF STORES FILE.                                 
      ******************************************************************        
       1200-READ-CUTOFF-STORES-FILE.                                            
                                                                                
           READ INV-CUTOFF-STORES-FILE                                     CL**3
               INTO IC-INV-CUTOFF-STORES-REC                               CL**3
               AT END                                                      CL**3
                   SET PS-END-OF-INPUT TO TRUE                             CL**3
               NOT AT END                                                  CL**3
                   ADD +1 TO PA-INPUT-RECS-READ.                                
                                                                                
                                                                                
      ******************************************************************        
      *  FOR EACH INVENTORY CUTOFF STORES RECORD READ, FIND THE CURRENT         
      *  ROW TO UPDATE AND UPDATE THE ITEM CUTOFF INDICATOR FROM N TO Y         
      *  ON TINVPAR.                                                            
      ******************************************************************   CL**1
                                                                           CL**1
       2000-PROCESS-INPUT.                                                 CL**4
                                                                           CL**4
           MOVE IC-STORE TO INVPAR-LOC-NBR.                             FSKUP045
                                                                                
W33304*    PERFORM 2100-OPEN-TINVPAR-CSR.                                       
W33304*                                                                         
W33304*    PERFORM 2200-FETCH-TINVPAR-CSR.                                      
W33304*                                                                         
W33304*    PERFORM 2300-CLOSE-TINVPAR-CSR.                                      
W33304     PERFORM 2200-SELECT-TINVPAR.                                         
                                                                                
           PERFORM 3000-UPDATE-TINVPAR.                                    CL**4
                                                                                
           PERFORM 1200-READ-CUTOFF-STORES-FILE.                           CL*12
                                                                           CL**1
W33304******************************************************************        
W33304*  SELECT INVENTORY PARAMETERS FOR STORE                                  
W33304******************************************************************        
W33304 2200-SELECT-TINVPAR.                                                     
W33304                                                                          
W33304     INITIALIZE PV-RETRY-COUNT.                                           
W33304                                                                          
W33304     PERFORM  WITH TEST AFTER                                             
W33304         UNTIL SQLCODE = +0                                               
W33304            OR SQLCODE = +100                                             
W33304            OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                    
W33304         EXEC SQL                                                         
W33304             SELECT A.ACTL_INV_DTE                                    0291
W33304                  , A.INV_ID                                          0291
W33304               INTO :INVPAR-ACTL-INV-DTE,                                 
W33304                    :INVPAR-INV-ID                                        
W33304               FROM TINVPAR A                                             
W33304                  , TINCNTL B                                             
W33304               WHERE LOC_NBR             = :INVPAR-LOC-NBR            0312
W33304                 AND A.LOC_INV_STAT_CDE  = 'IN'                           
W33304                 AND A.ACTL_FIN_BK_DTE   = '9999-09-09'               0313
W33304                 AND A.PLND_INV_DTE     >= :PV-PROCESS-DATE           0314
W33304                 AND A.SCHD_PHY_CNT_CDE IN ('PI','NS','CL')           0315
W33304                 AND A.INV_ID            = B.INV_ID                       
W33304                 AND B.ACTV_IND          = 'Y'                            
W33304         END-EXEC                                                         
W33304                                                                          
W33304         EVALUATE TRUE                                                    
W33304             WHEN SQLCODE = +0                                            
W33304                  CONTINUE                                           CL*15
W33304             WHEN SQLCODE = +100                                          
W33304                  MOVE INVPAR-LOC-NBR TO PV-TINVPAR-NFM-LOC-NBR      CL*15
W33304                  DISPLAY PV-TINVPAR-NOT-FOUND-MSG                        
W33304                  MOVE '2200-SELECT-TINVPAR'                             C
W33304                                 TO PV-PARAGRAPH-NAME                CL**1
W33304                  MOVE 'SELECT TINVPAR'                              CL*11
W33304                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
W33304                  PERFORM 9100-SQL-ABEND                             CL**1
W33304             WHEN SQLCODE = -904                                          
W33304             WHEN SQLCODE = -913                                          
W33304                  ADD 1          TO PV-RETRY-COUNT                        
W33304             WHEN OTHER                                                   
W33304                  MOVE '2200-SELECT-TINVPAR'                             C
W33304                                 TO PV-PARAGRAPH-NAME                CL**1
W33304                  MOVE 'SELECT TINVPAR'                              CL*11
W33304                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
W33304                  PERFORM 9100-SQL-ABEND                             CL**1
W33304         END-EVALUATE                                                     
W33304     END-PERFORM.                                                         
W33304                                                                          
W33304     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
W33304         MOVE '2200-SELECT-TINVPAR'                                      C
W33304                                 TO PV-PARAGRAPH-NAME                CL**1
W33304         MOVE 'SELECT TINVPAR'   TO PV-DB2-OPERATION-ATTEMPTED       CL**1
W33304         PERFORM 9100-SQL-ABEND                                      CL**1
W33304     END-IF.                                                              
                                                                                
      ******************************************************************FSKUP045
      *  UPDATE SPECIAL STORE SPECIFIC EXPIRED ROWS ON TINVPAR          FSKUP045
      ******************************************************************FSKUP045
       3000-UPDATE-TINVPAR.                                             FSKUP045
      *                                                                 FSKUP045
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                        FSKUP045
           PERFORM  WITH TEST AFTER                                             
               UNTIL SQLCODE = +0                                               
                  OR SQLCODE = +100                                             
                  OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                    
               EXEC SQL                                                         
                    UPDATE TINVPAR                                              
                        SET ITM_CTOFF_IND = 'Y'                                 
                        WHERE LOC_NBR      = :INVPAR-LOC-NBR                    
                          AND ACTL_INV_DTE = :INVPAR-ACTL-INV-DTE               
28545                     AND INV_ID       = :INVPAR-INV-ID                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                        ADD SQLERRD(3) TO PA-TINVPAR-ROWS-UPDATED          CL*15
                   WHEN SQLCODE = +100                                          
                        MOVE INVPAR-LOC-NBR TO PV-TINVPAR-NFM-LOC-NBR      CL*15
                        DISPLAY PV-TINVPAR-NOT-FOUND-MSG                        
                        MOVE '3000-UPDATE-TINVPAR'                             C
                                       TO PV-PARAGRAPH-NAME                CL**1
                        MOVE 'UPDATE TINVPAR TABLE '                       CL*11
                                       TO PV-DB2-OPERATION-ATTEMPTED       CL**1
                        PERFORM 9100-SQL-ABEND                             CL**1
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                        ADD 1          TO PV-RETRY-COUNT                        
                   WHEN OTHER                                                   
                        MOVE '3000-UPDATE-TINVPAR'                             C
                                       TO PV-PARAGRAPH-NAME                CL**1
                        MOVE 'UPDATE TINVPAR TABLE '                       CL*11
                                       TO PV-DB2-OPERATION-ATTEMPTED       CL**1
                        PERFORM 9100-SQL-ABEND                             CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '3000-UPDATE-TINVPAR'                                      C
                                       TO PV-PARAGRAPH-NAME                CL**1
               MOVE 'UPDATE TINVPAR TABLE '                                CL*11
                                       TO PV-DB2-OPERATION-ATTEMPTED       CL**1
               PERFORM 9100-SQL-ABEND                                      CL**1
           END-IF.                                                              
                                                                                
      *                                                                         
      ******************************************************************   CL**1
      *      PERFORMS WRAP UP PROCESSING                               *   CL*12
      ******************************************************************   CL**1
                                                                           CL**1
       9000-EOJ-ROUTINE.                                                   CL**1
                                                                           CL**1
           CLOSE CC-DATE-CNTL-FILE                                         CL*12
                 INV-CUTOFF-STORES-FILE.                                   CL*12
                                                                           CL**1
           EXEC SQL                                                        CL**5
               COMMIT WORK                                                 CL**5
           END-EXEC.                                                       CL**5
                                                                           CL*12
           PERFORM 9005-DISPLAY-TOTALS.                                    CL*16
                                                                           CL*16
      ******************************************************************   CL*16
      *      DISPLAYS PROGRAM TOTALS                                   *   CL*16
      ******************************************************************   CL*16
                                                                           CL*16
       9005-DISPLAY-TOTALS.                                                CL*16
                                                                           CL*16
           DISPLAY '************************************************'.     CL*16
           DISPLAY '**'.                                                   CL*16
           DISPLAY '** INKUP012 TOTALS **************'.                    CL*16
           DISPLAY '** INPUT RECS READ       : ' PA-INPUT-RECS-READ.       CL*16
           DISPLAY '** TINVPAR ROWS UPDATED  : '                           CL*16
                                              PA-TINVPAR-ROWS-UPDATED.     CL*16
           DISPLAY '** END OF INKUP012 TOTALS *******'.                    CL*16
                                                                           CL*16
      ******************************************************************   CL**1
      *      PERFORMS ABEND PROCESSING FOR SQL ERRORS.                 *   CL*13
      ******************************************************************   CL**1
                                                                           CL**1
       9100-SQL-ABEND.                                                     CL**1
                                                                           CL*13
           PERFORM 9005-DISPLAY-TOTALS.                                    CL*16
           DISPLAY ' '.                                                    CL*16
           DISPLAY '9100-SQL-ABEND'                                        CL**1
           DISPLAY '** ABEND     **'.                                      CL**1
           DISPLAY '** PROGRAM   ** = INKUP012'.                           CL*17
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                 CL**1
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.        CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**1
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE            CL**1
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE        CL**1
      * FORMAT.                                                            CL**1
      ******************************************************************   CL**1
           COPY DPPD004.                                                   CL**1
                                                                           CL**1
           MOVE +4013 TO ABEND-CODE.                                       CL**1
           CALL 'ILBOABN0' USING ABEND-CODE.                               CL**1
                                                                           CL*13
