      *                                                                         
       IDENTIFICATION DIVISION.                                                 
      *                                                                         
       PROGRAM-ID.    DPKUT011.                                                 
      *                                                                         
       AUTHOR.        CALVIN BUSH.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07/11/90.                                                 
       DATE-COMPILED.                                                           
      *                                                                         
      *****************************************************************         
      **  THIS PROGRAM CREATES THE SEQUENTIAL FILE USED TO LOAD THE             
      **  'MNEMONIC' ONLINE VSAM FILE WHICH IS USED BY THE MENU AND             
      **  NAVIGATOR PROGRAMS IN THE CICS ARCHITECTURE.                          
      **                                                                        
      **   DATE  04/08/96                                                       
      **       CHANGE MADE: ADD LOGIC FOR CICS24X7 PROCESSING;                  
      **       ARCHITECTURE CHANGES TO ALLOW CICS TO BE UP                      
      **       24HRS/DAY, 7DAYS/WEEK.                                           
      **       ALSO ADDED LOGIC FOR THE FUNCTION KEY PROJECT.                   
      **       1) INCREASE LRECL OF TMNEMONIC KSDS VSAM FILE FROM 760           
      **          TO 850.                                                       
      **       2) WHEN UPDATING THE TMNEMONIC KSDS VSAM FILE FROM THE           
      **          TCICSROUTE DB2 TABLE, UPDATE THE NEW FIELD                    
      **          FUNC_KY_DISPN_IND.                                            
      **       MADE BY: CHERI MASTEL               WR #: 3767                   
      **                                                                        
      **   DATE  09/09/99                                                       
      **       ADD ACCURATE LOCATION REQUIRED INDICATOR.                        
      **       MADE BY: JEFF ROUBIK                WR #:10808                   
      *****************************************************************         
           EJECT                                                                
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
           SELECT OUTPUT-MNEMONIC-FILE      ASSIGN TO OUT01.                    
           EJECT                                                                
       DATA DIVISION.                                                           
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  OUTPUT-MNEMONIC-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 850 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS.                                            
       01  OUTPUT-MNEMONIC-RECORD           PIC  X(850).                        
           EJECT                                                                
       WORKING-STORAGE SECTION.                                                 
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CHARACTER-MAX             PIC S9(04)  VALUE +30               
                                                        COMP.                   
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACE.            
           05  PV-DB2-OPERATION             PIC  X(70)  VALUE SPACE.            
           05  PV-STRING-PTR                PIC S9(04)  VALUE +0                
                                                        COMP.                   
           05  PV-CHARACTER-COUNT           PIC S9(04)  VALUE +0                
                                                        COMP.                   
           05  PV-AID-SUB                   PIC  9(02)  VALUE ZERO.             
       01  TC-TITLE-CHARACTER-TABLE.                                            
           05  TC-TITLE-CHARACTER           PIC  X(01)                          
                                            OCCURS 30 TIMES                     
                                            INDEXED BY TC-CHAR-IDX.             
      *                                                                         
       01  ABEND-CODE                    PIC S9(04)  VALUE +0                   
                                                     COMP                       
                                                     SYNC.                      
      *                                                                         
      **   PARAMETER LIST USED TO CALL DB2 ABEND ROUTINE DSNTIAR                
      *                                                                         
           COPY DPWS004.                                                        
      *                                                                         
      **   OUTPUT RECORD LAYOUT FOR MNEMONIC FILE                               
      *                                                                         
           COPY DPRD503.                                                        
      *                                                                         
      **   DCLGEN FOR TABLE TCICSROUTE                                          
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TCICSRTE                                                 
           END-EXEC.                                                            
      *                                                                         
      **   DCLGEN FOR TABLE TMNEMONIC                                           
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TMNMNC                                                   
           END-EXEC.                                                            
      *                                                                         
      **   THE DB2 COMMUNICATIONS AREA                                          
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
               DECLARE MNEMONIC_CSR CURSOR FOR                                  
               SELECT MNEMONIC_CODE,                                            
                      TRANS_ID_CODE,                                            
                      AVAIL_INDCT,                                              
                      CMAREA_FRMT_CODE,                                         
                      SAVE_CMAREA_INDCT,                                        
                      CMAREA_LNGTH_MSRMT,                                       
                      PANEL_TITLE_NAME,                                         
                      GLBL_ACCS_IND,                                            
                      DB2_PKG_CDE,                                              
                      ACRT_LOC_REQR_IND                                         
               FROM TMNEMONIC                                                   
               ORDER BY MNEMONIC_CODE                                           
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
               DECLARE FUNCTION_KEY_CSR CURSOR FOR                              
               SELECT AID_SEQUENCE_NMBR,                                        
                      DEST_MNEMONIC_CODE,                                       
                      ACTION_CODE,                                              
                      ROUTE_STACK_INDCT,                                        
                      FUNC_KEY_LBL_NAME,                                        
                      IN_APLCTN_COMM_CDE,                                       
                      FUNC_KY_DISPN_IND                                         
               FROM TCICSROUTE                                                  
               WHERE MNEMONIC_CODE = :TMNEMONIC-MNEMONIC-CODE                   
           END-EXEC.                                                            
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      **  THIS MODULE CONTROLS ALL PROCESSING DONE IN THIS PROGRAM.             
      *****************************************************************         
       A000-MAIN-DRIVER.                                                        
           PERFORM B100-INITIALIZATION.                                         
           PERFORM B200-BUILD-MNEMONIC-RECORDS                                  
               UNTIL SQLCODE = +100.                                            
           PERFORM B300-TERMINATION.                                            
           GOBACK.                                                              
      *****************************************************************         
      **  THIS MODULE OPENS THE OUTPUT FILE AND THE CURSOR FOR                  
      **  DB2 TABLE TMNEMONIC. IT ALSO DOES THE INITIAL FETCH ON THE            
      **  TMNEMONIC TABLE CURSOR.                                               
      *****************************************************************         
       B100-INITIALIZATION.                                                     
           OPEN OUTPUT OUTPUT-MNEMONIC-FILE.                                    
           INITIALIZE DCLTCICSROUTE                                             
                      DCLTMNEMONIC.                                             
           EXEC SQL                                                             
               OPEN MNEMONIC_CSR                                                
           END-EXEC.                                                            
           IF  SQLCODE = 0                                                      
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'B100-INITIALIZATION'  TO PV-PARAGRAPH-NAME                 
               MOVE 'OPEN MNEMONIC CURSOR' TO PV-DB2-OPERATION                  
               PERFORM U900-SQL-ABEND                                           
           END-IF.                                                              
           PERFORM C100-FETCH-MNEMONIC-CURSOR.                                  
      ****************************************************************          
      **  THIS PARAGRAPH FILLS THE OUTPUT RECORD LAYOUT, WRITES THE             
      **  RECORD AND THEN FETCHES THE NEXT ROW FROM THE MNEMONIC                
      **  CURSOR.                                                               
      ****************************************************************          
       B200-BUILD-MNEMONIC-RECORDS.                                             
           INITIALIZE DP503-MNEMONIC-RECORD.                                    
           MOVE TMNEMONIC-MNEMONIC-CODE  TO DP503-MNEMONIC.                     
           MOVE TMNEMONIC-GLBL-ACCS-IND  TO DP503-GLOBAL-ACCESS-IND.            
           MOVE TMNEMONIC-AVAIL-INDCT    TO DP503-TRAN-AVAILABLE-SW.            
           MOVE TMNEMONIC-CMAREA-FRMT-CODE                                      
                                         TO DP503-COMMAREA-FORMAT.              
           MOVE TMNEMONIC-SAVE-CMAREA-INDCT                                     
                                         TO DP503-SAVE-COMMAREA-SW.             
           MOVE TMNEMONIC-TRANS-ID-CODE  TO DP503-TRAN-ID.                      
           MOVE TMNEMONIC-CMAREA-LNGTH-MSRMT                                    
                                         TO DP503-APPL-COMMAREA-LENGTH.         
           MOVE TMNEMONIC-DB2-PKG-CDE    TO DP503-DB2-PKG-IND.                  
           MOVE TMNEMONIC-ACRT-LOC-REQR-IND                                     
                                         TO DP503-ACRT-LOC-REQR-IND.            
           PERFORM C200-CENTER-TITLE.                                           
           PERFORM D200-OPEN-FUNCTION-KEY-CSR.                                  
           PERFORM D100-FETCH-FUNCTION-KEY-CURSOR.                              
           PERFORM C300-FILL-FK-ARRAY                                           
               VARYING PV-AID-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL SQLCODE = +100.                                            
           PERFORM D300-CLOSE-FUNCTION-KEY-CSR.                                 
           PERFORM U800-WRITE-MNEMONIC-RECORD.                                  
           PERFORM C100-FETCH-MNEMONIC-CURSOR.                                  
      *                                                                         
      *                                                                         
       B300-TERMINATION.                                                        
           CLOSE OUTPUT-MNEMONIC-FILE.                                          
           EXEC SQL                                                             
              CLOSE MNEMONIC_CSR                                                
           END-EXEC.                                                            
      *                                                                         
           IF  SQLCODE = 0                                                      
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'B300-TERMINATION'      TO PV-PARAGRAPH-NAME                
               MOVE 'CLOSE MNEMONIC CURSOR' TO PV-DB2-OPERATION                 
               PERFORM U900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
       C100-FETCH-MNEMONIC-CURSOR.                                              
           EXEC SQL                                                             
               FETCH MNEMONIC_CSR                                               
               INTO  :TMNEMONIC-MNEMONIC-CODE,                                  
                     :TMNEMONIC-TRANS-ID-CODE,                                  
                     :TMNEMONIC-AVAIL-INDCT,                                    
                     :TMNEMONIC-CMAREA-FRMT-CODE,                               
                     :TMNEMONIC-SAVE-CMAREA-INDCT,                              
                     :TMNEMONIC-CMAREA-LNGTH-MSRMT,                             
                     :TMNEMONIC-PANEL-TITLE-NAME,                               
                     :TMNEMONIC-GLBL-ACCS-IND,                                  
                     :TMNEMONIC-DB2-PKG-CDE,                                    
                     :TMNEMONIC-ACRT-LOC-REQR-IND                               
           END-EXEC.                                                            
           IF  (SQLCODE = 0                                                     
                       OR +100)                                                 
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'C100-FETCH-MNEMONIC-CURSOR' TO PV-PARAGRAPH-NAME           
               MOVE 'FETCH MNEMONIC CURSOR'      TO PV-DB2-OPERATION            
               PERFORM U900-SQL-ABEND                                           
           END-IF.                                                              
      ****************************************************************          
      **  THIS PARAGRAPH WILL CENTER THE PANEL TITLE INTO THE PANEL             
      **  TITLE FIELD.  THE TITLE IS PUT INTO A TABLE AND THEN A                
      **  PERFORM IS DONE TO FIND THE NUMBER OF CHARACTERS IN THE               
      **  TITLE.  THE STARTING POSITION IS THEN CALCULATED INTO THE             
      **  STRING POINTER AND THE TITLE IS MOVED INTO THE OUTPUT FIELD           
      **  USING A STRING.                                                       
      ****************************************************************          
       C200-CENTER-TITLE.                                                       
           MOVE TMNEMONIC-PANEL-TITLE-NAME TO TC-TITLE-CHARACTER-TABLE.         
      *                                                                         
           PERFORM                                                              
               VARYING TC-CHAR-IDX                                              
               FROM PC-CHARACTER-MAX BY -1                                      
               UNTIL (TC-TITLE-CHARACTER (TC-CHAR-IDX) NOT = SPACE              
                  OR TC-CHAR-IDX = ZERO)                                        
           END-PERFORM.                                                         
      *                                                                         
           SET PV-CHARACTER-COUNT       TO TC-CHAR-IDX.                         
      *                                                                         
           COMPUTE PV-STRING-PTR =                                              
               ((LENGTH OF DP503-PANEL-TITLE - PV-CHARACTER-COUNT)              
               / 2) + 1.                                                        
      *                                                                         
           INITIALIZE DP503-PANEL-TITLE.                                        
      *                                                                         
           STRING TMNEMONIC-PANEL-TITLE-NAME DELIMITED BY SIZE                  
               INTO DP503-PANEL-TITLE                                           
               POINTER PV-STRING-PTR                                            
           END-STRING.                                                          
      ****************************************************************          
      **  THIS PARAGRAPH WILL FILL THE AID-KEY TABLE IN THE OUTPUT              
      **  RECORD FROM THE CICSROUTE TABLE.  THE DESTINATION MNEMONIC            
      **  IS USED AS THE PFKEY LABEL IN THE ONLINE ARCHITECTURE                 
      **  PROGRAMS.  THERE WILL NOT BE A ROW IN THE MNEMONIC TABLE              
      **  FOR CICSROUTE DESTINATION MNEMONICS THAT DEFINE 'LOCAL' PFKEYS        
      **  SUCH AS PAGE FORWARD OR PAGE BACK.                                    
      ****************************************************************          
       C300-FILL-FK-ARRAY.                                                      
           MOVE TCICSROUTE-AID-SEQUENCE-NMBR TO PV-AID-SUB.                     
           MOVE TCICSROUTE-ACTION-CODE                                          
                  TO DP503-FK-ACTION (PV-AID-SUB).                              
           MOVE TCICSROUTE-ROUTE-STACK-INDCT                                    
                  TO DP503-FK-STACK-IND (PV-AID-SUB).                           
           MOVE TCICSROUTE-DEST-MNEMONIC-CODE                                   
                  TO DP503-FK-MNEMONIC (PV-AID-SUB)                             
                     TMNEMONIC-MNEMONIC-CODE.                                   
           MOVE TCICSROUTE-FUNC-KEY-LBL-NAME                                    
                  TO DP503-FK-LABEL (PV-AID-SUB).                               
      *                                                                         
           IF  (DP503-FK-LABEL (PV-AID-SUB) = SPACE)                            
             AND (DP503-FK-MNEMONIC (PV-AID-SUB) NOT = SPACE)                   
               MOVE DP503-FK-MNEMONIC (PV-AID-SUB)                              
                  TO DP503-FK-LABEL (PV-AID-SUB)                                
           END-IF.                                                              
      *                                                                         
           MOVE TCICSROUTE-IN-APLCTN-COMM-CDE                                   
                  TO DP503-FK-INT-APPL-FORMAT-IND (PV-AID-SUB).                 
      *                                                                         
           MOVE TCICSROUTE-FUNC-KY-DISPN-IND                                    
                  TO DP503-FK-DISPN-IND (PV-AID-SUB).                           
      *                                                                         
           IF  DP503-FK-LABEL (PV-AID-SUB) =                                    
                                DP503-FK-MNEMONIC (PV-AID-SUB)                  
               SET DP503-FK-NOT-ALT-LABEL (PV-AID-SUB) TO TRUE                  
           ELSE                                                                 
               SET DP503-FK-ALT-LABEL (PV-AID-SUB) TO TRUE                      
           END-IF.                                                              
      *                                                                         
           PERFORM D400-SELECT-MNEMONIC-TRANSID.                                
           IF  SQLCODE = +0                                                     
               MOVE TMNEMONIC-TRANS-ID-CODE                                     
                  TO DP503-FK-TRAN-ID (PV-AID-SUB)                              
           ELSE                                                                 
               MOVE SPACE                                                       
                  TO DP503-FK-TRAN-ID (PV-AID-SUB)                              
           END-IF.                                                              
      *                                                                         
           PERFORM D100-FETCH-FUNCTION-KEY-CURSOR.                              
      *                                                                         
      *                                                                         
       D100-FETCH-FUNCTION-KEY-CURSOR.                                          
           EXEC SQL                                                             
               FETCH FUNCTION_KEY_CSR                                           
               INTO  :TCICSROUTE-AID-SEQUENCE-NMBR,                             
                     :TCICSROUTE-DEST-MNEMONIC-CODE,                            
                     :TCICSROUTE-ACTION-CODE,                                   
                     :TCICSROUTE-ROUTE-STACK-INDCT,                             
                     :TCICSROUTE-FUNC-KEY-LBL-NAME,                             
                     :TCICSROUTE-IN-APLCTN-COMM-CDE,                            
                     :TCICSROUTE-FUNC-KY-DISPN-IND                              
           END-EXEC.                                                            
           IF  (SQLCODE = 0                                                     
                       OR +100)                                                 
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'D100-FETCH-FUNCTION-KEY-CURSOR'                            
                                                 TO PV-PARAGRAPH-NAME           
               STRING 'FETCH TCICSROUTE TABLE CURSOR FOR MNEMONIC ('            
                                              DELIMITED BY SIZE                 
                      TMNEMONIC-MNEMONIC-CODE DELIMITED BY SIZE                 
                      ')'                     DELIMITED BY SIZE                 
                   INTO PV-DB2-OPERATION                                        
               PERFORM U900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
       D200-OPEN-FUNCTION-KEY-CSR.                                              
           EXEC SQL                                                             
               OPEN FUNCTION_KEY_CSR                                            
           END-EXEC.                                                            
      *                                                                         
           IF  SQLCODE = 0                                                      
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'D200-OPEN-FUNCTION-KEY-CSR'                                
                                           TO PV-PARAGRAPH-NAME                 
               MOVE 'OPEN FUNCTION_KEY_CSR, TABLE IS TCICSROUTE'                
                                           TO PV-DB2-OPERATION                  
               PERFORM U900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
       D300-CLOSE-FUNCTION-KEY-CSR.                                             
           EXEC SQL                                                             
               CLOSE FUNCTION_KEY_CSR                                           
           END-EXEC.                                                            
      *                                                                         
           IF  SQLCODE = 0                                                      
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'D300-CLOSE-FUNCTION-KEY-CSR'                               
                                           TO PV-PARAGRAPH-NAME                 
               MOVE 'CLOSE FUNCTION_KEY_CSR, TABLE IS TCICSROUTE'               
                                           TO PV-DB2-OPERATION                  
               PERFORM U900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
       D400-SELECT-MNEMONIC-TRANSID.                                            
           EXEC SQL                                                             
               SELECT TRANS_ID_CODE                                             
               INTO  :TMNEMONIC-TRANS-ID-CODE                                   
               FROM   TMNEMONIC                                                 
               WHERE  MNEMONIC_CODE = :TMNEMONIC-MNEMONIC-CODE                  
           END-EXEC.                                                            
      *                                                                         
           IF  (SQLCODE = 0                                                     
                       OR +100)                                                 
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'D400-SELECT-MNEMONIC-TRANSID' TO PV-PARAGRAPH-NAME         
               STRING 'SELECT TMNEMONIC.TRANS_ID_CODE FOR MNEMONIC ('           
                                                  DELIMITED BY SIZE             
                      TMNEMONIC-MNEMONIC-CODE     DELIMITED BY SIZE             
                      ')'                         DELIMITED BY SIZE             
                   INTO PV-DB2-OPERATION                                        
               PERFORM U900-SQL-ABEND                                           
           END-IF.                                                              
      ****************************************************************          
      **  THIS PARAGRAPH WRITES THE OUTPUT CONVERSION RECORDS.                  
      ****************************************************************          
       U800-WRITE-MNEMONIC-RECORD.                                              
           WRITE OUTPUT-MNEMONIC-RECORD                                         
               FROM DP503-MNEMONIC-RECORD.                                      
      ******************************************************************        
      * U900-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE              
      * EVENT THAT AN SQL ERROR CODE OCCURS.                                    
      ******************************************************************        
       U900-SQL-ABEND.                                                          
           DISPLAY '** ABEND **         **'.                                    
           DISPLAY '** PROGRAM          ** = DPKUT011'.                         
           DISPLAY '** PARAGRAPH NAME   ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** DB2 OPERATION    ** = ' PV-DB2-OPERATION.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
