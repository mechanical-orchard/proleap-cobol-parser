       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.           PXKUT121.                                  00020000
       AUTHOR.               DAVE WELLER.                               00030000
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00040000
       DATE-WRITTEN          06/2012.                                   00050000
       DATE-COMPILED.                                                   00060000
                                                                        00070000
      ******************************************************************00080000
      *   PXKUT121 - ADJUST STR-OH-QTY FOR EACH ITEM ON THE INP01 FILE  00090001
      *              WITH THE FSHN-ONHAND-QTY FROM THE ONHAND FILE      00100001
      *              BY MATCHING THE FILES.                             00110001
      *              THE OUT01 FILE WILL CONTAIN ALL OF THE INP01 ROWS  00120001
      *              WITH THE OH-QTY ADJUSTED OR LEFT AT ZERO.          00130001
      *                                                                 00180000
      ******************** HISTORY OF CHANGES **************************00190000
      * CHG ID/                                                        *00200000
      * WR/PROJ          DATE        DESCRIPTION OF CHANGES            *00210000
      * ---------------  ----------  --------------------------------  *00220000
      * WR39700          06/15/2012  INITIAL IMPLEMENTATION             00230001
      *                                                                 00240000
      ******************************************************************00250000
                                                                        00260000
       ENVIRONMENT DIVISION.                                            00270000
                                                                        00280000
       CONFIGURATION SECTION.                                           00290000
                                                                        00300000
       SOURCE-COMPUTER. IBM-3090.                                       00310000
       OBJECT-COMPUTER. IBM-3090.                                       00320000
                                                                        00330000
       INPUT-OUTPUT SECTION.                                            00340000
       FILE-CONTROL.                                                    00350000
           SELECT EVENT-EXTR-FILE                                       00360001
               ASSIGN TO INP01.                                         00370000
                                                                        00380000
           SELECT ONHAND-ADJ-FILE                                       00390001
               ASSIGN TO INP02.                                         00400000
                                                                        00410000
           SELECT EVENT-OH-FILE                                         00420001
               ASSIGN TO OUT01.                                         00430000
                                                                        00440000
       DATA DIVISION.                                                   00450000
                                                                        00460000
       FILE SECTION.                                                    00470000
       FD  EVENT-EXTR-FILE                                              00480001
           LABEL RECORDS ARE STANDARD                                   00490000
           RECORDING MODE IS F                                          00500000
           BLOCK CONTAINS 0 RECORDS.                                    00510000
       01  EVENT-EXTR-REC                  PIC  X(250).                 00520001
                                                                        00530000
       FD  ONHAND-ADJ-FILE                                              00540001
           LABEL RECORDS ARE STANDARD                                   00550000
           RECORDING MODE IS F                                          00560000
           BLOCK CONTAINS 0 RECORDS.                                    00570000
       01  ONHAND-ADJ-REC                  PIC  X(080).                 00580001
                                                                        00590000
       FD  EVENT-OH-FILE                                                00600001
           LABEL RECORDS ARE STANDARD                                   00610000
           RECORDING MODE IS F                                          00620000
           BLOCK CONTAINS 0 RECORDS.                                    00630000
       01  EVENT-OH-REC                    PIC X(250).                  00640001
                                                                        00650000
       WORKING-STORAGE SECTION.                                         00660000
                                                                        00670000
      ***************************************************************** 00700000
      *  PROGRAM'S ACCUMULATORS                                       * 00710000
      ***************************************************************** 00720000
       01  PA-PROGRAM-ACCUMULATORS.                                     00730000
           05 PA-RECORDS-READ            PIC 9(10)       VALUE ZEROES.  00740000
           05 PA-RECORDS-UPDATED         PIC 9(10)       VALUE ZEROES.  00750000
           05 PA-RECORDS-WRITTEN         PIC 9(10)       VALUE ZEROES.  00760000
           05 PA-RECORDS-REJECTED        PIC 9(10)       VALUE ZEROES.  00770000
                                                                        00780000
      ***************************************************************** 00790000
      *  PROGRAM'S CONSTANTS                                          * 00800000
      ***************************************************************** 00810000
       01  PC-PROGRAM-CONSTANTS.                                        00820000
           05  PC-PROGRAM-NAME             PIC X(08)       VALUE        00830000
               'PXKUT121'.                                              00840000
                                                                        00850000
      ***************************************************************** 00860000
      *  PROGRAM'S SWITCHES                                           * 00870000
      ***************************************************************** 00880000
       01  PS-PROGRAM-SWITCHES.                                         00890000
           05  PS-END-OF-EXTR-INP-FILE-SW PIC X(01)        VALUE 'N'.   00900000
               88  PS-END-OF-EXTR-INP-FILE                 VALUE 'Y'.   00910000
               88  PS-NOT-END-OF-EXTR-INP-FILE             VALUE 'N'.   00920000
           05  PS-END-OF-ADJ-INP-FILE-SW PIC X(01)         VALUE 'N'.   00930000
               88  PS-END-OF-ADJ-INP-FILE                  VALUE 'Y'.   00940000
               88  PS-NOT-END-OF-ADJ-INP-FILE              VALUE 'N'.   00950000
                                                                        00960000
      ***************************************************************** 00970000
      *  PROGRAM'S VARIABLES                                          * 00980000
      ***************************************************************** 00990000
       01  PV-PROGRAM-VARIABLES.                                        01000000
           05  PV-ADJ-QTY                  PIC  9(9)   VALUE ZEROES.    01020004
           05  PV-STR-NBR                  PIC  9(4)   VALUE ZEROES.    01030005
           05  PV-INTR-UPC-ID              PIC  9(11)  VALUE ZEROES.    01040008
                                                                        01050000
      ***************************************************************** 01060000
      *  ABEND CODE AREA                                              * 01070000
      ***************************************************************** 01080000
       01  ABEND-CODE                      PIC S9(04)  VALUE +0  COMP.  01090000
           88  ABEND-4013-DB-ERROR                     VALUE +4013.     01100000
                                                                        01110000
                                                                        01120000
      ******************************************************************01130000
      **COPYBOOKS                                                       01140000
      ******************************************************************01150000
      **** EXTRACT INPUT RECORD LAYOUT                                  01160001
           COPY PXRD141.                                                01170001
                                                                        01180000
      ****ON HAND QTY ADJUSTMENT INPUT RECORD LAYOUT                    01190000
           COPY PXRD003.                                                01200001
                                                                        01210000
                                                                        01450000
       PROCEDURE DIVISION.                                              01460000
                                                                        01470000
      ******************************************************************01480000
      *    MAINLINE LOGIC                                              *01490000
      ******************************************************************01500000
                                                                        01510000
       0000-MAINLINE.                                                   01520000
                                                                        01530000
           PERFORM 1000-INITIALIZATION.                                 01540000
                                                                        01550000
           PERFORM 2000-PROCESS                                         01560002
               UNTIL PS-END-OF-EXTR-INP-FILE.                           01570000
                                                                        01580000
           PERFORM 9900-EOJ-LOGIC.                                      01590000
                                                                        01600000
           GOBACK.                                                      01610000
                                                                        01620000
      ******************************************************************01630000
      *    OPEN FILES AND READ FIRST RECORDS FROM INPUT FILES.         *01640000
      ******************************************************************01650000
       1000-INITIALIZATION.                                             01660000
                                                                        01670000
           OPEN INPUT  EVENT-EXTR-FILE                                  01680001
                       ONHAND-ADJ-FILE                                  01690001
               OUTPUT  EVENT-OH-FILE.                                   01700001
                                                                        01710000
           PERFORM 6100-READ-EXTR-INP-FL.                               01720001
           PERFORM 6200-READ-ONHAND-ADJ-FILE.                           01730001
           IF PS-END-OF-EXTR-INP-FILE                                   01740000
               DISPLAY ' *** NOTHING TO PROCESS ***'                    01750000
               DISPLAY ' '                                              01760000
           END-IF.                                                      01770000
                                                                        01780000
      ******************************************************************01790000
      *    2000-PROCESS                                                *01800002
      *    THE INPUT FILES ARE IN SKU, STORE SEQUENCE.                 *01810000
      ******************************************************************01820000
       2000-PROCESS.                                                    01830002
                                                                        01840000
           IF PXRD003-STR-NBR = PXRD141-STR-NBR                         01850001
             IF PXRD003-INTR-UPC-ID = PXRD141-INTR-UPC-ID               01880001
      *        COMPUTE PV-INTR-UPC-ID = PXRD003-INTR-UPC-ID * 1         01881010
      *        MOVE PXRD003-STR-NBR     TO PV-STR-NBR                   01900010
      *        DISPLAY 'MATCH - STR: ' PV-STR-NBR                       01901010
      *                    ' UPC-ID: ' PV-INTR-UPC-ID                   01902010
               COMPUTE PV-ADJ-QTY =                                     01910001
                   PXRD003-FSHN-ONHAND-QTY                              01911001
               COMPUTE PXRD141-STR-OH-QTY = PV-ADJ-QTY                  01930005
               IF PV-ADJ-QTY NOT EQUAL 0 THEN                           01940012
                   ADD 1 TO PA-RECORDS-UPDATED                          01950002
               END-IF                                                   01960012
               PERFORM 6300-WRITE-OUTP-FL                               01970001
               PERFORM 6200-READ-ONHAND-ADJ-FILE                        01980001
               PERFORM 6100-READ-EXTR-INP-FL                            01990001
             ELSE                                                       01990101
               IF PXRD003-INTR-UPC-ID < PXRD141-INTR-UPC-ID             01991001
                 ADD 1 TO PA-RECORDS-REJECTED                           01992001
                 PERFORM 6200-READ-ONHAND-ADJ-FILE                      01996001
               ELSE                                                     01996101
                 PERFORM 6300-WRITE-OUTP-FL                             01996201
                 PERFORM 6100-READ-EXTR-INP-FL                          01996301
               END-IF                                                   01997001
             END-IF                                                     01998001
           ELSE                                                         02000001
             IF PXRD003-STR-NBR < PXRD141-STR-NBR                       02010001
               ADD 1 TO PA-RECORDS-REJECTED                             02020001
               PERFORM 6200-READ-ONHAND-ADJ-FILE                        02070001
             ELSE                                                       02080001
               PERFORM 6300-WRITE-OUTP-FL                               02130001
               PERFORM 6100-READ-EXTR-INP-FL                            02150001
             END-IF                                                     02151001
           END-IF.                                                      02160001
                                                                        02310000
      ******************************************************************02710000
      *  READ EXTRACT INPUT FILE                                        02720000
      ******************************************************************02730000
       6100-READ-EXTR-INP-FL.                                           02740001
                                                                        02750000
           READ EVENT-EXTR-FILE INTO PXRD141-WHITE-TICKET-REC           02760001
               AT END SET PS-END-OF-EXTR-INP-FILE TO TRUE.              02770000
                                                                        02780000
           IF PS-NOT-END-OF-EXTR-INP-FILE                               02790000
               ADD 1 TO PA-RECORDS-READ                                 02800000
           END-IF.                                                      02810000
                                                                        02820000
      ******************************************************************02830000
      *  READ ADJUSTMENT INPUT FILE                                     02840000
      ******************************************************************02850000
       6200-READ-ONHAND-ADJ-FILE.                                       02860001
                                                                        02870000
           READ ONHAND-ADJ-FILE INTO PXRD003-PX-COMMON-FORMAT           02880001
               AT END SET PS-END-OF-ADJ-INP-FILE TO TRUE.               02890000
                                                                        02900000
           IF PS-END-OF-ADJ-INP-FILE                                    02910000
               MOVE ALL '9' TO PXRD003-INTR-UPC-ID                      02920001
                               PXRD003-STR-NBR                          02930001
           END-IF.                                                      02940000
                                                                        02950000
      ******************************************************************02960000
      *  WRITE OH QTY ADJ FILE                                          02970000
      ******************************************************************02980000
       6300-WRITE-OUTP-FL.                                              02990001
           ADD 1 TO PA-RECORDS-WRITTEN.                                 03000000
           WRITE EVENT-OH-REC                                           03010001
             FROM PXRD141-WHITE-TICKET-REC.                             03020001
                                                                        03350000
      ******************************************************************03360000
      *    9900-EOJ-LOGIC.                                              03370000
      *      THIS IS PERFORMED AT END OF INPUT FILE.                    03380000
      *      PURPOSE: CLOSE INPUT FILE,                                 03390000
      *      DISPLAY RECORD COUNTS.                                     03400000
      ******************************************************************03410000
       9900-EOJ-LOGIC.                                                  03420000
                                                                        03430000
           CLOSE EVENT-EXTR-FILE                                        03440001
                 ONHAND-ADJ-FILE                                        03450001
                 EVENT-OH-FILE.                                         03460001
                                                                        03470000
           DISPLAY ' '.                                                 03480000
           DISPLAY 'RECORDS READ         ' PA-RECORDS-READ.             03490000
           DISPLAY 'RECORDS UPDATED     ' PA-RECORDS-UPDATED.           03500002
           DISPLAY 'ADJ RECS - NO MATCH ' PA-RECORDS-REJECTED.          03510002
           DISPLAY 'RECORDS WRITTEN     ' PA-RECORDS-WRITTEN.           03520002
           DISPLAY '**------ END ' PC-PROGRAM-NAME ' ------**'.         03530000
                                                                        03540000
      ******************************************************************03550000
      *    END OF SOURCE CODE FOR PXKUT121                             *03560000
      ******************************************************************03570000
