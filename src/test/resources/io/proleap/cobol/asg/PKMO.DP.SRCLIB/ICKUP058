       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ICKUP058.                                                 
       AUTHOR.        R. KELLEN.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  05/01/2008.                                               
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      * ICKUP058 - PURGE IC COUNTS                                              
      *****************************************************************         
      *                                                                         
      *  TABLES UPDATED ARE:                                                    
      *      ICT_SKU_LOC_CNT                                                    
      *      ICT_LOC_CNT_STAT                                                   
      *      ICT_LOC_CNT_SUM                                                    
      *  FILES READ ARE:                                                        
      *      IMRD037 SORTED IN CNT_STUP_ID, LOCATION, CNT_SEQ_NBR AND           
      *      SKU_NBR SEQUENCE.                                                  
      *****************************************************************         
      * 05/01/08 - R KELLEN         -  WR NO. WR30388                           
      *    - NEW PROGRAM.  THIS PROGRAM READS A FILE OF COUNT ID'S TO BE        
      *                    PURGED FROM THE ICT_SKU_LOC_CNT,                     
      *                    ICT_LOC_CNT_STAT AND ICT_LOC_CNT_SUM TABLES.         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT IC-PURGE-FILE            ASSIGN TO INP01.                     
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
       FILE SECTION.                                                            
                                                                                
       FD  IC-PURGE-FILE                                                        
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS MOS-UPDATE-RECORD.                                    
       01  IC-PURGE-RECORD            PIC  X(39).                               
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME                PIC X(08)    VALUE                
                                                 'ICKUP058'.                    
           05  PC-MAXIMUM-RETRY-COUNT         PIC S9(04)   VALUE +2             
                                                           COMP SYNC.           
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-WORK-VARIABLES.                                                   
           05  PV-RETRY-COUNT                 PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.        
           05  PV-RECS-READ-CNTR              PIC  9(09)   VALUE ZEROES.00830000
           05  PV-SQLCODE-DISPLAY             PIC +(8)9    VALUE ZEROES.00830000
           05  PV-PREV-INTR-UPC-ID            PIC S9(9)    VALUE +0             
                                                           COMP.                
           05  PV-COMMITS-TAKEN            PIC S9(09)  COMP-3              CL*15
                                                         VALUE ZERO.       CL*15
           05  PV-COMMIT-INTERVAL          PIC S9(09)  COMP-3              CL**4
                                                       VALUE +15000.       CL**4
           05  PV-COMMIT-COUNT             PIC S9(09)  COMP-3              CL**4
                                                         VALUE ZERO.       CL**4
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW              PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-FILE                          VALUE 'Y'.           
                                                                                
       01  ABEND-CODE                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
                                                                                
       01  DISPLAY-EDITS.                                                       
           05  DI-RECS-READ-CNTR            PIC  9(09)  VALUE ZEROS.            
           05  DI-SKU-LOC-DEL-CNTR          PIC  9(09)  VALUE ZEROS.            
           05  DI-LOC-STAT-DEL-CNTR         PIC  9(09)  VALUE ZEROS.            
           05  DI-LOC-SUM-DEL-CNTR          PIC  9(09)  VALUE ZEROS.            
           05  DI-COMMIT-CNTR               PIC  9(09)  VALUE ZEROS.            
                                                                                
      *                                                                         
      *****************************************************************         
      *    INPUT COPYBOOK                                                       
      *****************************************************************         
           COPY ICRD037.                                                        
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      *  ICT_SKU_LOC_CNT - SKU LOC COUNT TABLE                                  
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE ICT00011                                                
           END-EXEC.                                                            
      *****************************************************************         
      *  ICT_LOC_CNT_STAT -LOCATION COUNT STATUS TABLE                          
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE ICT00020                                                
           END-EXEC.                                                            
      *****************************************************************         
      *  ICT_LOC_CNT_SUM - LOCATION COUNT SUM TABLE                             
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE ICT00012                                                
           END-EXEC.                                                            
                                                                                
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * 0000-MAIN-MODULE.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-PURGE                                           
             UNTIL PS-END-OF-FILE.                                              
                                                                                
           PERFORM 3000-EOJ-PROCESSING.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                           *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           OPEN INPUT IC-PURGE-FILE.                                            
                                                                                
           PERFORM 4000-READ-RECORD.                                            
                                                                                
           IF PS-END-OF-FILE                                                    
               DISPLAY ' '                                                      
               DISPLAY '***********************************'                    
               DISPLAY ' NO ROWS TO PURGE'                                      
               DISPLAY '***********************************'                    
               DISPLAY ' '                                                      
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2000-PROCESS-PURGE.                                            *        
      *  ==> PROCESSES:                                                *        
      *    - PURGE ROWS ON INPUT FILE                                  *        
      ******************************************************************        
       2000-PROCESS-PURGE.                                                      
           MOVE ICRD037-CNT-STUP-ID TO ICT00011-CNT-STUP-ID                     
                                       ICT00012-CNT-STUP-ID.                    
           MOVE ICRD037-LOC-NBR     TO ICT00011-LOC-NBR                         
                                       ICT00012-LOC-NBR.                        
           MOVE ICRD037-CNT-SEQ-NBR TO ICT00011-CNT-SEQ-NBR                     
                                       ICT00012-CNT-SEQ-NBR.                    
           MOVE ICRD037-SKU-NBR     TO ICT00011-SKU-NBR.                        
           PERFORM 6100-DELETE-SKU-LOC.                                         
           PERFORM 6300-DELETE-LOC-SUM.                                         
           IF ICRD037-CNT-STUP-ID NOT = ICT00020-CNT-STUP-ID                    
             OR ICRD037-LOC-NBR NOT = ICT00020-LOC-NBR                          
             OR ICRD037-CNT-SEQ-NBR NOT = ICT00020-CNT-SEQ-NBR                  
             MOVE ICRD037-CNT-STUP-ID TO ICT00020-CNT-STUP-ID                   
             MOVE ICRD037-LOC-NBR     TO ICT00020-LOC-NBR                       
             MOVE ICRD037-CNT-SEQ-NBR TO ICT00020-CNT-SEQ-NBR                   
             PERFORM 6200-DELETE-LOC-STAT                                       
           END-IF.                                                              
           IF PV-COMMIT-COUNT >= PV-COMMIT-INTERVAL                             
               MOVE ZERO TO PV-COMMIT-COUNT                                     
               DISPLAY 'COMMIT TAKEN - ' ICT00011-CNT-STUP-ID                   
                    ' - ' ICT00011-CNT-SEQ-NBR                                  
                    ' - ' ICT00011-LOC-NBR                                      
               EXEC SQL                                                         
                   COMMIT WORK                                                  
               END-EXEC                                                         
               ADD +1 TO DI-COMMIT-CNTR                                         
           END-IF.                                                              
           ADD +1 TO PV-COMMIT-COUNT.                                           
           PERFORM 4000-READ-RECORD.                                            
                                                                                
      ******************************************************************        
      * 3000-EOJ-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - CLOSE FILES                                               *        
      *    - DISPLAYS PROCESSING TOTALS                                *        
      *    - TAKES A FINAL COMMIT                                      *        
      ******************************************************************        
       3000-EOJ-PROCESSING.                                                     
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.                     
                                                                                
           CLOSE IC-PURGE-FILE.                                                 
                                                                                
           EXEC SQL                                                        CL**5
               COMMIT WORK                                                 CL**5
           END-EXEC.                                                       CL**5
           ADD +1 TO DI-COMMIT-CNTR.                                       CL*16
                                                                                
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
      ******************************************************************        
      * 4000-READ-RECORD.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - READS THE NEXT RECORD IN THE INPUT FILE.                  *        
      ******************************************************************        
       4000-READ-RECORD.                                                        
                                                                                
           READ IC-PURGE-FILE                                                   
               INTO ICRD037-IC-COUNTS-FILE                                      
                   AT END                                                       
                       MOVE 'Y'     TO PS-END-OF-FILE-SW                        
                   NOT AT END                                                   
                       ADD +1                  TO PV-RECS-READ-CNTR             
           END-READ.                                                            
                                                                                
                                                                                
                                                                                
      ******************************************************************        
      * 6100-DELETE-SKU-LOC.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - DELETE ALL ROWS WITH SPECIFIED CNT-STUP-ID, LOC-NBR,      *        
      *      CNT-SEQ-NBR AND SKU-NBR.                                  *        
      ******************************************************************        
       6100-DELETE-SKU-LOC.                                                     
           MOVE '6100-DELETE-SKU-LOC' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR  SQLCODE = +100                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                  DELETE                                                        
                  FROM ICT_SKU_LOC_CNT                                          
                  WHERE CNT_STUP_ID      = :ICT00011-CNT-STUP-ID                
                    AND LOC_NBR          = :ICT00011-LOC-NBR                    
                    AND CNT_SEQ_NBR      = :ICT00011-CNT-SEQ-NBR                
                    AND SKU_NBR          = :ICT00011-SKU-NBR                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                     DISPLAY 'NOT FOUND - ' ICT00011-CNT-STUP-ID                
                                      ' - ' ICT00011-LOC-NBR                    
                                      ' - ' ICT00011-CNT-SEQ-NBR                
                                      ' - ' ICT00011-SKU-NBR                    
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'DELETE ICT_SKU_LOC'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'DELETE ICT_SKU_LOC_CNT'                                    
                   TO PV-DB2-OPERATION-ATTEMPTED                                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 6200-DELETE-LOC-STAT                                           *        
      *  ==> PROCESSES:                                                *        
      *    - DELETE ALL ROWS WITH SPECIFIED CNT-STUP-ID, LOC-NBR AND   *        
      *      CNT-SEQ-NBR.                                              *        
      ******************************************************************        
       6200-DELETE-LOC-STAT.                                                    
           MOVE '6200-DELETE-LOC-STAT' TO PV-PARAGRAPH-NAME.                    
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR  SQLCODE = +100                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                  DELETE                                                        
                  FROM ICT_LOC_CNT_STAT                                         
                  WHERE CNT_STUP_ID      = :ICT00020-CNT-STUP-ID                
                    AND LOC_NBR          = :ICT00020-LOC-NBR                    
                    AND CNT_SEQ_NBR      = :ICT00020-CNT-SEQ-NBR                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                     DISPLAY 'NOT FOUND2- ' ICT00020-CNT-STUP-ID                
                                      ' - ' ICT00020-LOC-NBR                    
                                      ' - ' ICT00020-CNT-SEQ-NBR                
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'DELETE ICT_LOC_CNT_STAT'                           
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'DELETE ICT_LOC_CNT_STAT'                                   
                   TO PV-DB2-OPERATION-ATTEMPTED                                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 6300-DELETE-LOC-SUM                                            *        
      *  ==> PROCESSES:                                                *        
      *    - DELETE ALL ROWS WITH SPECIFIED CNT-STUP-ID, LOC-NBR AND   *        
      *      CNT-SEQ-NBR.                                              *        
      ******************************************************************        
       6300-DELETE-LOC-SUM.                                                     
           MOVE '6300-DELETE-LOC-SUM' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR  SQLCODE = +100                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                  DELETE                                                        
                  FROM ICT_LOC_CNT_SUM                                          
                  WHERE CNT_STUP_ID      = :ICT00012-CNT-STUP-ID                
                    AND LOC_NBR          = :ICT00012-LOC-NBR                    
                    AND CNT_SEQ_NBR      = :ICT00012-CNT-SEQ-NBR                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'DELETE ICT_LOC_CNT_SUM'                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'DELETE ICT_LOC_CNT_SUM'                                    
                   TO PV-DB2-OPERATION-ATTEMPTED                                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * 9990-DISPLAY-CONTROL-INFO.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
      ******************************************************************        
       9990-DISPLAY-CONTROL-INFO.                                               
                                                                                
           DISPLAY '********************************************'.              
           DISPLAY '**** RUN STATISTICS FOR PROGRAM ICKUP058 ***'.              
           DISPLAY '****'.                                                      
           DISPLAY 'INPUT RECORDS READ:            ' PV-RECS-READ-CNTR.         
           DISPLAY 'COMMITS TAKEN:                 ' DI-COMMIT-CNTR.            
           DISPLAY '****'.                                                      
           DISPLAY '**** RUN STATS END FOR PROGRAM ICKUP058  ***'.              
           DISPLAY '********************************************'.              
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND.                                                *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *        
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
                                                                                
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'ICKUP058 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                           
           DISPLAY 'INPUT KEY = ' ICRD037-CNT-STUP-ID ' - '                     
                                  ICRD037-LOC-NBR     ' - '                     
                                  ICRD037-CNT-SEQ-NBR ' - '                     
                                  ICRD037-SKU-NBR.                              
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
