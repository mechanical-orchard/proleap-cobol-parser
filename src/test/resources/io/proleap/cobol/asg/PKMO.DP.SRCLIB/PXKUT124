       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.           PXKUT124.                                  00020000
       AUTHOR.               DAVE WELLER.                               00030000
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00040000
       DATE-WRITTEN          06/2012.                                   00050000
       DATE-COMPILED.                                                   00060000
                                                                        00070000
      ******************************************************************00080000
      *   PXKUT124 - ADJUST STR-OH-QTY FOR EACH ITEM ON THE INP01 FILE  00090000
      *              WITH THE FSHN-ONHAND-QTY FROM THE ONHAND FILE      00100000
      *              BY MATCHING THE FILES.                             00110000
      *              THE OUT01 FILE WILL CONTAIN ALL OF THE INP01 ROWS  00120000
      *              WITH THE OH-QTY ADJUSTED OR LEFT AT ZERO.          00130000
      *                                                                 00140000
      ******************** HISTORY OF CHANGES **************************00150000
      * CHG ID/                                                        *00160000
      * WR/PROJ          DATE        DESCRIPTION OF CHANGES            *00170000
      * ---------------  ----------  --------------------------------  *00180000
      * WR39700          06/15/2012  INITIAL IMPLEMENTATION             00190000
      *                                                                 00200000
      ******************************************************************00210000
                                                                        00220000
       ENVIRONMENT DIVISION.                                            00230000
                                                                        00240000
       CONFIGURATION SECTION.                                           00250000
                                                                        00260000
       SOURCE-COMPUTER. IBM-3090.                                       00270000
       OBJECT-COMPUTER. IBM-3090.                                       00280000
                                                                        00290000
       INPUT-OUTPUT SECTION.                                            00300000
       FILE-CONTROL.                                                    00310000
           SELECT EVENT-EXTR-FILE                                       00320000
               ASSIGN TO INP01.                                         00330000
                                                                        00340000
           SELECT ONHAND-ADJ-FILE                                       00350000
               ASSIGN TO INP02.                                         00360000
                                                                        00370000
           SELECT EVENT-OH-FILE                                         00380000
               ASSIGN TO OUT01.                                         00390000
                                                                        00400000
       DATA DIVISION.                                                   00410000
                                                                        00420000
       FILE SECTION.                                                    00430000
       FD  EVENT-EXTR-FILE                                              00440000
           LABEL RECORDS ARE STANDARD                                   00450000
           RECORDING MODE IS F                                          00460000
           BLOCK CONTAINS 0 RECORDS.                                    00470000
       01  EVENT-EXTR-REC                  PIC  X(250).                 00480000
                                                                        00490000
       FD  ONHAND-ADJ-FILE                                              00500000
           LABEL RECORDS ARE STANDARD                                   00510000
           RECORDING MODE IS F                                          00520000
           BLOCK CONTAINS 0 RECORDS.                                    00530000
       01  ONHAND-ADJ-REC                  PIC  X(080).                 00540000
                                                                        00550000
       FD  EVENT-OH-FILE                                                00560000
           LABEL RECORDS ARE STANDARD                                   00570000
           RECORDING MODE IS F                                          00580000
           BLOCK CONTAINS 0 RECORDS.                                    00590000
       01  EVENT-OH-REC                    PIC X(250).                  00600000
                                                                        00610000
       WORKING-STORAGE SECTION.                                         00620000
                                                                        00630000
      ***************************************************************** 00640000
      *  PROGRAM'S ACCUMULATORS                                       * 00650000
      ***************************************************************** 00660000
       01  PA-PROGRAM-ACCUMULATORS.                                     00670000
           05 PA-RECORDS-READ            PIC 9(10)       VALUE ZEROES.  00680000
           05 PA-RECORDS-UPDATED         PIC 9(10)       VALUE ZEROES.  00690000
           05 PA-RECORDS-WRITTEN         PIC 9(10)       VALUE ZEROES.  00700000
           05 PA-RECORDS-REJECTED        PIC 9(10)       VALUE ZEROES.  00710000
                                                                        00720000
      ***************************************************************** 00730000
      *  PROGRAM'S CONSTANTS                                          * 00740000
      ***************************************************************** 00750000
       01  PC-PROGRAM-CONSTANTS.                                        00760000
           05  PC-PROGRAM-NAME             PIC X(08)       VALUE        00770000
               'PXKUT124'.                                              00780000
                                                                        00790000
      ***************************************************************** 00800000
      *  PROGRAM'S SWITCHES                                           * 00810000
      ***************************************************************** 00820000
       01  PS-PROGRAM-SWITCHES.                                         00830000
           05  PS-END-OF-EXTR-INP-FILE-SW PIC X(01)        VALUE 'N'.   00840000
               88  PS-END-OF-EXTR-INP-FILE                 VALUE 'Y'.   00850000
               88  PS-NOT-END-OF-EXTR-INP-FILE             VALUE 'N'.   00860000
           05  PS-END-OF-ADJ-INP-FILE-SW PIC X(01)         VALUE 'N'.   00870000
               88  PS-END-OF-ADJ-INP-FILE                  VALUE 'Y'.   00880000
               88  PS-NOT-END-OF-ADJ-INP-FILE              VALUE 'N'.   00890000
                                                                        00900000
      ***************************************************************** 00910000
      *  PROGRAM'S VARIABLES                                          * 00920000
      ***************************************************************** 00930000
       01  PV-PROGRAM-VARIABLES.                                        00940000
           05  PV-ADJ-QTY                  PIC  9(9)   VALUE ZEROES.    00950000
           05  PV-STR-NBR                  PIC  9(4)   VALUE ZEROES.    00960000
           05  PV-INTR-UPC-ID              PIC  9(11)  VALUE ZEROES.    00970000
                                                                        00980000
      ***************************************************************** 00990000
      *  ABEND CODE AREA                                              * 01000000
      ***************************************************************** 01010000
       01  ABEND-CODE                      PIC S9(04)  VALUE +0  COMP.  01020000
           88  ABEND-4013-DB-ERROR                     VALUE +4013.     01030000
                                                                        01040000
                                                                        01050000
      ******************************************************************01060000
      **COPYBOOKS                                                       01070000
      ******************************************************************01080000
      **** EXTRACT INPUT RECORD LAYOUT                                  01090000
           COPY PXRD142.                                                01100000
                                                                        01110000
      ****ON HAND QTY ADJUSTMENT INPUT RECORD LAYOUT                    01120000
           COPY PXRD003.                                                01130000
                                                                        01140000
                                                                        01150000
       PROCEDURE DIVISION.                                              01160000
                                                                        01170000
      ******************************************************************01180000
      *    MAINLINE LOGIC                                              *01190000
      ******************************************************************01200000
                                                                        01210000
       0000-MAINLINE.                                                   01220000
                                                                        01230000
           PERFORM 1000-INITIALIZATION.                                 01240000
                                                                        01250000
           PERFORM 2000-PROCESS                                         01260000
               UNTIL PS-END-OF-EXTR-INP-FILE.                           01270000
                                                                        01280000
           PERFORM 9900-EOJ-LOGIC.                                      01290000
                                                                        01300000
           GOBACK.                                                      01310000
                                                                        01320000
      ******************************************************************01330000
      *    OPEN FILES AND READ FIRST RECORDS FROM INPUT FILES.         *01340000
      ******************************************************************01350000
       1000-INITIALIZATION.                                             01360000
                                                                        01370000
           OPEN INPUT  EVENT-EXTR-FILE                                  01380000
                       ONHAND-ADJ-FILE                                  01390000
               OUTPUT  EVENT-OH-FILE.                                   01400000
                                                                        01410000
           PERFORM 6100-READ-EXTR-INP-FL.                               01420000
           PERFORM 6200-READ-ONHAND-ADJ-FILE.                           01430000
           IF PS-END-OF-EXTR-INP-FILE                                   01440000
               DISPLAY ' *** NOTHING TO PROCESS ***'                    01450000
               DISPLAY ' '                                              01460000
           END-IF.                                                      01470000
                                                                        01480000
      ******************************************************************01490000
      *    2000-PROCESS                                                *01500000
      *    THE INPUT FILES ARE IN SKU, STORE SEQUENCE.                 *01510000
      ******************************************************************01520000
       2000-PROCESS.                                                    01530000
                                                                        01540000
           IF PXRD003-STR-NBR = PXRD142-STR-NBR                         01550000
             IF PXRD003-INTR-UPC-ID = PXRD142-INTR-UPC-ID               01560000
      *        COMPUTE PV-INTR-UPC-ID = PXRD003-INTR-UPC-ID * 1         01570000
      *        MOVE PXRD003-STR-NBR     TO PV-STR-NBR                   01580000
      *        DISPLAY 'MATCH - STR: ' PV-STR-NBR                       01590000
      *                    ' UPC-ID: ' PV-INTR-UPC-ID                   01600000
               COMPUTE PV-ADJ-QTY =                                     01610000
                   PXRD003-FSHN-ONHAND-QTY                              01620000
               COMPUTE PXRD142-STR-OH-QTY = PV-ADJ-QTY                  01630000
               IF PV-ADJ-QTY NOT EQUAL 0 THEN                           01640001
                   ADD 1 TO PA-RECORDS-UPDATED                          01650000
               END-IF                                                   01660001
               PERFORM 6300-WRITE-OUTP-FL                               01670000
               PERFORM 6200-READ-ONHAND-ADJ-FILE                        01680000
               PERFORM 6100-READ-EXTR-INP-FL                            01690000
             ELSE                                                       01700000
               IF PXRD003-INTR-UPC-ID < PXRD142-INTR-UPC-ID             01710000
                 ADD 1 TO PA-RECORDS-REJECTED                           01720000
                 PERFORM 6200-READ-ONHAND-ADJ-FILE                      01730000
               ELSE                                                     01740000
                 PERFORM 6300-WRITE-OUTP-FL                             01750000
                 PERFORM 6100-READ-EXTR-INP-FL                          01760000
               END-IF                                                   01770000
             END-IF                                                     01780000
           ELSE                                                         01790000
             IF PXRD003-STR-NBR < PXRD142-STR-NBR                       01800000
               ADD 1 TO PA-RECORDS-REJECTED                             01810000
               PERFORM 6200-READ-ONHAND-ADJ-FILE                        01820000
             ELSE                                                       01830000
               PERFORM 6300-WRITE-OUTP-FL                               01840000
               PERFORM 6100-READ-EXTR-INP-FL                            01850000
             END-IF                                                     01860000
           END-IF.                                                      01870000
                                                                        01880000
      ******************************************************************01890000
      *  READ EXTRACT INPUT FILE                                        01900000
      ******************************************************************01910000
       6100-READ-EXTR-INP-FL.                                           01920000
                                                                        01930000
           READ EVENT-EXTR-FILE INTO PXRD142-YELLOW-TICKET-REC          01940000
               AT END SET PS-END-OF-EXTR-INP-FILE TO TRUE.              01950000
                                                                        01960000
           IF PS-NOT-END-OF-EXTR-INP-FILE                               01970000
               ADD 1 TO PA-RECORDS-READ                                 01980000
           END-IF.                                                      01990000
                                                                        02000000
      ******************************************************************02010000
      *  READ ADJUSTMENT INPUT FILE                                     02020000
      ******************************************************************02030000
       6200-READ-ONHAND-ADJ-FILE.                                       02040000
                                                                        02050000
           READ ONHAND-ADJ-FILE INTO PXRD003-PX-COMMON-FORMAT           02060000
               AT END SET PS-END-OF-ADJ-INP-FILE TO TRUE.               02070000
                                                                        02080000
           IF PS-END-OF-ADJ-INP-FILE                                    02090000
               MOVE ALL '9' TO PXRD003-INTR-UPC-ID                      02100000
                               PXRD003-STR-NBR                          02110000
           END-IF.                                                      02120000
                                                                        02130000
      ******************************************************************02140000
      *  WRITE OH QTY ADJ FILE                                          02150000
      ******************************************************************02160000
       6300-WRITE-OUTP-FL.                                              02170000
           ADD 1 TO PA-RECORDS-WRITTEN.                                 02180000
           WRITE EVENT-OH-REC                                           02190000
             FROM PXRD142-YELLOW-TICKET-REC.                            02200000
                                                                        02210000
      ******************************************************************02220000
      *    9900-EOJ-LOGIC.                                              02230000
      *      THIS IS PERFORMED AT END OF INPUT FILE.                    02240000
      *      PURPOSE: CLOSE INPUT FILE,                                 02250000
      *      DISPLAY RECORD COUNTS.                                     02260000
      ******************************************************************02270000
       9900-EOJ-LOGIC.                                                  02280000
                                                                        02290000
           CLOSE EVENT-EXTR-FILE                                        02300000
                 ONHAND-ADJ-FILE                                        02310000
                 EVENT-OH-FILE.                                         02320000
                                                                        02330000
           DISPLAY ' '.                                                 02340000
           DISPLAY 'RECORDS READ         ' PA-RECORDS-READ.             02350000
           DISPLAY 'RECORDS UPDATED     ' PA-RECORDS-UPDATED.           02360000
           DISPLAY 'ADJ RECS - NO MATCH ' PA-RECORDS-REJECTED.          02370000
           DISPLAY 'RECORDS WRITTEN     ' PA-RECORDS-WRITTEN.           02380000
           DISPLAY '**------ END ' PC-PROGRAM-NAME ' ------**'.         02390000
                                                                        02400000
      ******************************************************************02410000
      *    END OF SOURCE CODE FOR PXKUT124                             *02420000
      ******************************************************************02430000
