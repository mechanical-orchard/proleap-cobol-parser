      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.      XMKUT009.                                               
       AUTHOR.          LINDA FRIESS.                                           
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    12/08/05.                                               
       DATE-COMPILED.                                                           
      *****************************************************************         
      * THIS PROGRAM WILL CREATE THE INITIAL ITEM/STORE LIST FOR THE            
      * SERVICE LEVEL DATA CAPTURE PROCESS.                                     
      *                                                                         
      *****************************************************************         
      *  CHANGE MADE:                                                 *         
      *                                                               *         
      *  CHANGED 00/00/00 BY                            WR#           *         
      *                                                                         
      *****************************************************************         
      * DATE:         07/30/2018                                      *         
      * WR/IR#:       CHG0204126                                      *         
      * PROGRAMMER:   COGNIZANT                                       *         
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE           *         
      *****************************************************************         
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *                                                                         
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SVC-LEVEL-EXTRACT-FILE                                        
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT SVC-LEVEL-ITEM-STORE-LIST                                     
               ASSIGN                       TO OUT01.                           
                                                                                
           SELECT ORACLE-LOGON-FILE                                             
               ASSIGN                       TO ORALOGON.                        
           EJECT                                                                
      *                                                                         
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  SVC-LEVEL-EXTRACT-FILE                                               
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SVC-LEVEL-EXTRACT-RECORD        PIC  X(40).                          
      *                                                                         
       FD  SVC-LEVEL-ITEM-STORE-LIST                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SVC-LVL-ITEM-STR-RECORD         PIC  X(40).                          
                                                                                
       FD  ORACLE-LOGON-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
       01  OL-ORACLE-LOGON-RECORD      PIC X(80).                               
           EJECT                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-----------------------------------------------------------------        
      *    COPYBOOK FOR SERVICE LEVEL EXTRACT INPUT FILE                        
      *-----------------------------------------------------------------        
           COPY MHRD007.                                                        
      *                                                                         
      *-----------------------------------------------------------------        
      *    COPYBOOK FOR SERVICE LEVEL ITEM STORE OUTPUT FILE                    
      *-----------------------------------------------------------------        
           COPY MHRD008.                                                        
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-ORACLE-USERID-LEN         PIC S9(04)  VALUE ZEROS             
                                                        COMP SYNC.              
           05  PV-ORACLE-PASSWORD-LEN       PIC S9(04)  VALUE ZEROS             
                                                        COMP SYNC.              
           05  PV-LOC-SUB                   PIC S9(04)  VALUE +0                
                                                        COMP SYNC.              
           05  PV-PREV-ITEM                 PIC S9(15)V COMP-3                  
                                                        VALUE ZERO.             
           05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
           05  PV-OPERATION                 PIC X(30)   VALUE SPACES.           
           05  PV-RECORDS-WRITTEN           PIC S9(09)  COMP-3                  
                                                        VALUE +0.               
           05  PV-RECORDS-BYPASSED          PIC S9(09)  COMP-3                  
                                                        VALUE +0.               
           05  PV-GOT-SKU-ID                PIC S9(09)  COMP-3                  
                                                        VALUE +0.               
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-AUTO-LOGON               PIC  X(01)   VALUE '/'.              
           05  PC-PROGRAM-NAME              PIC X(08)   VALUE                   
                                                        'XMKUT009'.             
           05  PC-1                         PIC S9(3)   VALUE +1                
                                                        COMP-3.                 
           05  PC-DISPLAY-MAX               PIC S9(09)  COMP-3                  
                                                        VALUE +50000.           
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
006510     05  PS-END-OF-LOGON-FILE-SW      PIC X(01)      VALUE 'N'.           
006530         88  PS-END-OF-LOGON-FILE                    VALUE 'Y'.           
           05  PS-ORA-SRC-CTL-DONE-SW       PIC X(01)      VALUE 'N'.           
               88  PS-ORA-SRC-EMPTY                        VALUE 'Y'.           
           05  PS-SVC-LVL-SW                PIC X(01)      VALUE 'N'.           
               88  END-OF-SVC-LVL-FILE                     VALUE 'Y'.           
               88  NOT-END-SVC-LVL-FILE                    VALUE 'N'.           
           05  PS-END-OF-LOC-CSR-SW         PIC X(01)      VALUE 'N'.           
               88  END-OF-LOCATION-CSR                     VALUE 'Y'.           
               88  NOT-END-OF-LOC-CSR                      VALUE 'N'.           
                                                                                
       01  CC-ORACLE-CONNECTION.                                                
           05  CC-ORACLE-USERID           PIC X(40).                            
               88 CC-AUTO-LOGON                       VALUE '/'.                
           05  CC-ORACLE-PASSWORD         PIC X(40).                            
                                                                                
                                                                                
      *                                                                         
      ******************************************************************        
      * WORKING-STORAGE FOR CALENDAR ROUTINE                                    
      ******************************************************************        
           COPY DPWS500.                                                        
      *                                                                         
      ******************************************************************        
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                
      ******************************************************************        
           COPY DPWS004.                                                        
      *                                                                         
      ****************************************************************          
      **ORACLE DECLARE SECTION                                                  
      ****************************************************************          
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
       01  USERNAME                    PIC  X(10)  VARYING.                     
       01  PASSWD                      PIC  X(10)  VARYING.                     
       01  PV-ORACLE-ID                PIC  X(01)  VALUE '/'.                   
                                                                                
      ******************************************************************        
      * COBOL DECLARATION FOR TABLE XMT_LOC_DIM                                 
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE XMT00009                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * COBOL DECLARATION FOR TABLE XMT_SKU_MDSE_DIM                            
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE XMT00010                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ******************************************************************        
      *  ERROR PROCESSING VARIABLES                                             
      ******************************************************************        
                                                                                
       01  ABEND-CODE                          PIC S9(04)  VALUE +0             
                                                           COMP SYNC.           
                                                                                
      ******************************************************************        
      *  LOC ID CROSS-REFERENCE TABLE                                           
      ******************************************************************        
       01  LOCATION-TABLE.                                                      
           05  LOC-ENTRY              OCCURS 9999 TIMES.                        
               10  LOC-ID                PIC S9(11) COMP-3.                     
      *                                                                         
      *                                                                         
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-FILE                                            
              UNTIL END-OF-SVC-LVL-FILE.                                        
                                                                                
           PERFORM 8000-END-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ****************************************************************          
      * LOGS INTO THE ORACLE DATABASE, CREATES THE LOCATION TABLE,              
      * AND OPENS ALL FILES.                                                    
      ****************************************************************          
       1000-INITIALIZATION.                                                     
      *                                                                         
           PERFORM 9000-ORACLE-SETUP.                                           
      *                                                                         
           PERFORM 6200-DECLARE-LOC-CURSOR.                                     
           PERFORM 6400-OPEN-LOC-CURSOR.                                        
      *                                                                         
      *    --- CREATE TABLE FOR CW_LOC_ID CROSS-REFERENCE ---                   
           INITIALIZE LOCATION-TABLE.                                           
           PERFORM 1200-POPULATE-LOC-TABLE                                      
              UNTIL END-OF-LOCATION-CSR.                                        
      *                                                                         
           PERFORM 6600-CLOSE-LOC-CURSOR.                                       
      *                                                                         
           OPEN INPUT  SVC-LEVEL-EXTRACT-FILE                                   
                OUTPUT SVC-LEVEL-ITEM-STORE-LIST.                               
      *                                                                         
           PERFORM 6000-GET-SVC-LVL-RECORD.                                     
                                                                                
      *                                                                         
      ****************************************************************          
      * THIS PARAGRAPH WILL POPULATE THE LOCATION TABLE.                        
      ****************************************************************          
       1200-POPULATE-LOC-TABLE.                                                 
                                                                                
           PERFORM 7000-FETCH-LOC-CURSOR.                                       
                                                                                
           IF NOT-END-OF-LOC-CSR                                                
              MOVE LOC-NBR   IN XMT00009 TO PV-LOC-SUB                          
              MOVE CW-LOC-ID IN XMT00009 TO LOC-ID(PV-LOC-SUB)                  
           END-IF.                                                              
                                                                                
                                                                                
      ****************************************************************          
      * THIS PARAGRAPH WILL PROCESS THE INPUT FILE AND WRITE TO THE             
      * OUTPUT FILE.  STORE TYPES OTHER THAN 'DS' WILL BE BYPASSED.             
      ****************************************************************          
       2000-PROCESS-FILE.                                                       
      *                                                                         
           IF NOT-END-SVC-LVL-FILE                                              
                                                                                
      *       ---- ON ITEM NBR BREAK, GET SKU ID ----                           
              IF MH007-ITEM-NBR NOT EQUAL PV-PREV-ITEM                          
                 PERFORM 7200-GET-SKU-ID                                        
                 ADD +1 TO PV-GOT-SKU-ID                                        
                                                                                
                 MOVE MH007-ITEM-NBR TO PV-PREV-ITEM                            
              END-IF                                                            
                                                                                
      *       ---- GET LOCATION ID FROM INTERNAL TABLE ----                     
              MOVE MH007-LOC-NBR      TO PV-LOC-SUB                             
              MOVE LOC-ID(PV-LOC-SUB) TO MH008-CW-LOC-ID                        
                                                                                
      *       --- IF HAVE VALID SKU & LOC ID'S, WRITE RECORD ---                
              IF (MH008-CW-SKU-ID > 0) AND (MH008-CW-LOC-ID > 0)                
                 PERFORM 2100-CHECK-OUT-OF-STOCK                                
                 PERFORM 7500-WRITE-ITEM-STR-RECORD                             
                                                                                
              ELSE                                                              
                 ADD +1 TO PV-RECORDS-BYPASSED                                  
                                                                                
      **         IF MH008-CW-LOC-ID EQUAL ZERO                                  
      **            DISPLAY '** LOCATION ' MH007-LOC-NBR                        
      **                    ' NOT FOUND ON XMT_LOC_DIM TABLE'                   
      **         END-IF                                                         
      **         --- ABEND IF TOO MANY "NOT FOUNDS" DISPLAYED ---               
      **         IF PV-RECORDS-BYPASSED > PC-DISPLAY-MAX                        
      **            DISPLAY '** ABEND ** IN XMKUT009'                           
      **            DISPLAY '** PARAGRAPH: 2000-PROCESS-FILE'                   
      **            DISPLAY '** MAX ERROR MESSAGES DISPLAYED'                   
      **            DISPLAY '** --NOT ABLE TO RETRIEVE CW IDS'                  
      **            MOVE +4003 TO ABEND-CODE                                    
      **            CALL 'ILBOABN0' USING ABEND-CODE                            
      **         END-IF                                                         
              END-IF                                                            
                                                                                
              PERFORM 6000-GET-SVC-LVL-RECORD                                   
           END-IF.                                                              
                                                                                
                                                                                
      ****************************************************************          
      *    CHECK FOR OUT OF STOCK & BELOW MIN PRES.                             
      *    --DIFFERENT CRITERIA FOR REPLENISHMENT & NON-REPL.                   
      ****************************************************************          
       2100-CHECK-OUT-OF-STOCK.                                                 
      *                                                                         
           MOVE ZERO TO MH008-DAYS-OUT-STOCK                                    
                        MH008-DAYS-BELOW-MIN.                                   
      *                                                                         
           IF MH007-NOT-REPLENISHMENT-ITEM                                      
              IF MH007-ONHAND-QTY < PC-1                                        
                 MOVE PC-1 TO MH008-DAYS-OUT-STOCK                              
                              MH008-DAYS-BELOW-MIN                              
              END-IF                                                            
                                                                                
           ELSE                                                                 
      *       ----- REPLENISHMENT ITEM -----                                    
              IF MH007-ONHAND-QTY < PC-1                                        
                 MOVE PC-1 TO MH008-DAYS-OUT-STOCK                              
              END-IF                                                            
                                                                                
              IF MH007-FT-CDE = 'O'                                             
                 IF MH007-ONHAND-QTY < MH007-CSTOCK-QTY OR                      
                    MH007-ONHAND-QTY < PC-1                                     
                    MOVE PC-1 TO MH008-DAYS-BELOW-MIN                           
                 END-IF                                                         
                                                                                
              ELSE                                                              
      *          ----- FT CODE = U -----                                        
                 IF MH007-ONHAND-QTY < MH007-USERMIN-QTY OR                     
                    MH007-ONHAND-QTY < PC-1                                     
                    MOVE PC-1 TO MH008-DAYS-BELOW-MIN                           
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
      ****************************************************************          
      *     READ SERVICE LEVEL EXTRACT FILE                                     
      ****************************************************************          
       6000-GET-SVC-LVL-RECORD.                                                 
                                                                                
           READ SVC-LEVEL-EXTRACT-FILE                                          
              INTO MH007-SVC-LVL-FINAL-RECORD                                   
                AT END SET END-OF-SVC-LVL-FILE TO TRUE.                         
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * DECLARE CURSOR FOR LOCATION TABLE                                       
      *----------------------------------------------------------------*        
       6200-DECLARE-LOC-CURSOR.                                                 
      *                                                                         
           EXEC SQL                                                             
             DECLARE LOCATION_CSR CURSOR FOR                                    
               SELECT                                                           
                     LOC_NBR                                                    
                    ,CW_LOC_ID                                                  
               FROM XMT_LOC_DIM                                                 
               WHERE LOC_TYP_CDE = 'DS'                                         
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      * OPEN THE LOCATION_CSR                                                   
      *----------------------------------------------------------------*        
       6400-OPEN-LOC-CURSOR.                                                    
                                                                                
           EXEC SQL                                                             
                 OPEN LOCATION_CSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = 1403                                              
                   SET END-OF-LOCATION-CSR TO TRUE                              
               WHEN OTHER                                                       
                   MOVE '6400-OPEN-LOC-CURSOR' TO PV-PARAGRAPH-NAME             
                   MOVE 'OPEN CURSOR'          TO PV-OPERATION                  
                   PERFORM 9900-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * CLOSE THE LOCATION CURSOR                                               
      *----------------------------------------------------------------*        
       6600-CLOSE-LOC-CURSOR.                                                   
                                                                                
           EXEC SQL                                                             
                CLOSE LOCATION_CSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '6600-CLOSE-LOC-CURSOR' TO PV-PARAGRAPH-NAME            
                   MOVE 'CLOSE CURSOR'          TO PV-OPERATION                 
                   PERFORM 9900-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * FETCH THE LOCATION CURSOR                                               
      ******************************************************************        
       7000-FETCH-LOC-CURSOR.                                                   
                                                                                
           EXEC SQL                                                             
                FETCH LOCATION_CSR                                              
                INTO :XMT00009.LOC-NBR                                          
                    ,:XMT00009.CW-LOC-ID                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = 1403                                              
                   SET END-OF-LOCATION-CSR TO TRUE                              
               WHEN OTHER                                                       
                   MOVE '7000-FETCH-LOC-CURSOR' TO PV-PARAGRAPH-NAME            
                   MOVE 'FETCH CURSOR'          TO PV-OPERATION                 
                   PERFORM 9900-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    READ ORACLE TABLE TO GET SKU ID                                      
      ******************************************************************        
       7200-GET-SKU-ID.                                                         
      *                                                                         
           EXEC SQL                                                             
                 SELECT CW_SKU_ID                                               
                   INTO :XMT00010.CW-SKU-ID                                     
                   FROM XMT_SKU_MDSE_DIM                                        
                  WHERE ITM_NBR = :MH007-ITEM-NBR                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE CW-SKU-ID TO MH008-CW-SKU-ID                            
               WHEN SQLCODE = 1403                                              
                   MOVE ZERO TO MH008-CW-SKU-ID                                 
                   DISPLAY '** ITEM ' MH007-ITEM-NBR                            
                           ' NOT FOUND ON XMT_SKU_MDSE_DIM TABLE'               
               WHEN SQLCODE = -2112                                             
                   MOVE ZERO TO MH008-CW-SKU-ID                                 
                   DISPLAY '** ITEM ' MH007-ITEM-NBR                            
                           ' RETURNED MULTIPLE ROWS'                            
               WHEN OTHER                                                       
                   MOVE '7200-GET-SKU-ID'   TO PV-PARAGRAPH-NAME                
                   MOVE 'SELECT FOR SKU ID' TO PV-OPERATION                     
                   PERFORM 9900-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    WRITE THE SERVICE LEVEL DATA TO A FILE                               
      ******************************************************************        
       7500-WRITE-ITEM-STR-RECORD.                                              
      *                                                                         
           MOVE MH007-ITEM-NBR     TO MH008-ITEM-NBR.                           
           MOVE MH007-LOC-NBR      TO MH008-LOC-NBR.                            
           MOVE MH007-UPC-STAT-CDE TO MH008-STATUS-CODE.                        
           MOVE MH007-MR-IND       TO MH008-REPLNMT-IND.                        
      *                                                                         
           WRITE SVC-LVL-ITEM-STR-RECORD                                        
              FROM MH008-SVC-LEVEL-DATA-RECORD.                                 
                                                                                
           ADD +1 TO PV-RECORDS-WRITTEN.                                        
                                                                                
                                                                                
      ****************************************************************          
      *     CLOSE FILES & DISPLAY RECORD COUNTS                                 
      ****************************************************************          
       8000-END-ROUTINE.                                                        
                                                                                
           CLOSE SVC-LEVEL-EXTRACT-FILE                                         
                 SVC-LEVEL-ITEM-STORE-LIST.                                     
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '** NUMBER OF RECORDS WRITTEN:  '                            
                                               PV-RECORDS-WRITTEN.              
           DISPLAY '** NUMBER OF RECORDS BYPASSED: '                            
                                               PV-RECORDS-BYPASSED.             
                                                                                
           DISPLAY '** NUMBER OF SKU IDS READ:     '                            
                                               PV-GOT-SKU-ID.                   
                                                                                
      ****************************************************************          
      * CONNECT TO ORACLE.                                                      
      ****************************************************************          
       9000-ORACLE-SETUP.                                                       
                                                                                
           OPEN INPUT ORACLE-LOGON-FILE.                                        
                                                                                
           PERFORM 9100-READ-ORACLE-LOGON.                                      
                                                                                
           CLOSE ORACLE-LOGON-FILE.                                             
                                                                                
           IF ((PS-END-OF-LOGON-FILE)                                           
                   OR (CC-ORACLE-CONNECTION = SPACES))                          
               DISPLAY '** ABEND **'                                            
               DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
                         'NOT PROVIDED'                                         
               SET PS-ORA-SRC-EMPTY TO TRUE                                     
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           IF CC-AUTO-LOGON                                                     
               DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
               EXEC SQL                                                         
                    CONNECT :USERNAME                                           
               END-EXEC                                                         
           ELSE                                                                 
               INITIALIZE PV-ORACLE-USERID-LEN                                  
                          PV-ORACLE-PASSWORD-LEN                                
               INSPECT CC-ORACLE-USERID                                         
                   TALLYING PV-ORACLE-USERID-LEN                                
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            INSPECT CC-ORACLE-PASSWORD                                          
                TALLYING PV-ORACLE-PASSWORD-LEN                                 
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
                      USERNAME-ARR                                              
            MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
            MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
                      PASSWD-ARR                                                
            MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
            DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
                      USERNAME-ARR                                              
            EXEC SQL                                                            
              CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
            END-EXEC                                                            
            INITIALIZE CC-ORACLE-PASSWORD                                       
                       PASSWD                                                   
            END-IF.                                                             
                                                                                
020500     IF (SQLCODE NOT = 0)                                                 
020900         DISPLAY PV-PARAGRAPH-NAME                                        
020910         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
               PERFORM 9900-SQL-ERROR                                           
021200     END-IF.                                                              
                                                                                
                                                                                
           EXEC SQL                                                             
            ALTER SESSION                                                       
                SET NLS_DATE_FORMAT = 'YYYY-MM-DD-HH24.MI.SS'                   
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *  9100-READ-ORACLE-LOGON.                                      *         
      *****************************************************************         
       9100-READ-ORACLE-LOGON.                                                  
                                                                                
034510     READ ORACLE-LOGON-FILE                                               
034520         INTO CC-ORACLE-CONNECTION                                        
034530         AT END                                                           
034540             SET PS-END-OF-LOGON-FILE TO TRUE                             
034550     END-READ.                                                            
                                                                                
      ******************************************************************        
      *    ABEND ON AN SQL ERROR.                                               
      ******************************************************************        
       9900-SQL-ERROR.                                                          
                                                                                
           DISPLAY '*** ABEND ***'.                                             
           DISPLAY '** XMKUT009 - SQL ERROR - ORACLE'.                          
           DISPLAY '** PROGRAM          ** = ' PC-PROGRAM-NAME.                 
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-OPERATION.                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES MODULE DSNTIAR TO FORMAT             
      *    THE INFO IN SQLCA INTO UNDERSTANDABLE VERBAGE.                       
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       EJECT                                                                    
