000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.         PEKRP085.                                            
000300 AUTHOR.             JIM ARENDT.                                          
000400 INSTALLATION.       KOHLS DEPARTMENT STORES.                             
000500 DATE-WRITTEN.       JANUARY 1998.                                        
000600 DATE-COMPILED.                                                           
000700                                                                          
000800******************************************************************        
000900* THIS PROGRAM PRODUCES TWO MARKDOWN/MARKUP STORE AUDIT REPORTS. *        
001000* THE FIRST REPORT LISTS THE STORES UNDER EACH TYPE CODE.        *        
001010* THE SECOND REPORT LISTS EACH STORES NET MARKDOWN/UP AMOUNT.    *        
001020*                                                                *        
001030* THE PROGRAM HAS THREE INPUT FILES:                             *        
001040* 1) CONTROL FILE THAT CONTAINS THE ENDING DATE THAT THE PROGRAM *        
001050*    IS BEING RUN FOR.                                           *        
001060* 2) INPUT FILE2 CONTAINS ALL THE RECORDS.  THIS FILE HAS BEEN   *        
001070*    SORTED BY CATEGORY, TYPE CODE AND STORE NUMBER.             *        
001080* 2) INPUT FILE3 CONTAINS ALL THE RECORDS.  THIS FILE HAS BEEN   *        
001090*    SORTED BY STORE NUMBER.                                     *        
001100*                                                                *        
001110* THE PROGRAM HAS TWO OUTPUT FILES:                              *        
001111* 1) TOTAL FILE CONTAINS THE TOTAL AMOUNT OF MARKDOWN/MARKUP FOR *        
001112*    EACH CATEGORY/TYPE CODE.  THIS FILE WILL BE USED IN PROGRAM *        
001113*    PEKRP090.                                                   *        
001114* 2) REPORT FILE CONTAINS THE PRINTED OUTPUT REPORTS.            *        
001115*                                                                *        
001116* THIS REPORT RUNS ONCE EVERY WEEK AND AT MONTH END.             *        
004200******************************************************************        
003900* 01/10/00 - JIM ARENDT WR NO. PM00336970                        *        
004000*    - CHANGED ILB0ABNO TO ILBOABN0.                             *        
004000*    - CHANGED PROGRAM TO NO LONGER ABEND WHEN INPUT FILE2 OR    *        
004000*      INPUT FILE3 IS EMPTY.  PROGRAM WILL DISPLAY THE HEADINGS  *        
A04000*      AND AN ERROR MESSAGE.                                     *        
004200******************************************************************        
003900* 10/28/03 - MOYER    WR NO. WR0024472                           *        
004000*    - RECOMPILE FOR MLRD023 CHANGE OVER A YEAR AGO              *        
001117******************************************************************        
      * 05/14/04 - TODD C. MICHALEK -  WR NO. WR26602                  *        
      *    - EXPAND PV-HOLD-STORE-NBR AND RPT-DTL-STORE-NO TO 5 BYTES  *        
      *      TO HANDLE THE NEW 5 BYTE ML023-PC-STORE-NBR FIELD         *        
001117******************************************************************        
W26606* 01/24/06 - JOHN SELWITSCHKA -  WR NO. WR28582                  *        
W26606*    - PRICE CHANGE COPYBOOKS EXPANSION.                         *        
W26606*      MODIFIED INPUT-FILE2 AND INPUT-FILE3 LRECL.               *        
W26606******************************************************************        
001118 ENVIRONMENT DIVISION.                                                    
001119******************************************************************        
001120 CONFIGURATION SECTION.                                                   
001130                                                                          
001140 SOURCE-COMPUTER.    IBM-3090.                                            
001150 OBJECT-COMPUTER.    IBM-3090.                                            
001160                                                                          
001170 INPUT-OUTPUT SECTION.                                                    
001180                                                                          
001190 FILE-CONTROL.                                                            
001200                                                                          
001300     SELECT CONTROL-FILE              ASSIGN TO INP01.                    
001400     SELECT INPUT-FILE2               ASSIGN TO INP02.                    
001500     SELECT INPUT-FILE3               ASSIGN TO INP03.                    
001600     SELECT REPORT-FILE               ASSIGN TO RPT01.                    
001700     SELECT TOTAL-FILE                ASSIGN TO TOT01.                    
001800                                                                          
001900******************************************************************        
002000 DATA DIVISION.                                                           
002100******************************************************************        
002200                                                                          
002300 FILE SECTION.                                                            
002400                                                                          
002500 FD  CONTROL-FILE                                                         
002600     RECORDING MODE IS F                                                  
002700     BLOCK CONTAINS 0 RECORDS                                             
002800     LABEL RECORDS ARE OMITTED.                                           
002900                                                                          
003000 01  CONTROL-RECORD                   PIC  X(80).                         
003100                                                                          
003200 FD  INPUT-FILE2                                                          
003300     RECORDING MODE IS F                                                  
003400     BLOCK CONTAINS 0 RECORDS                                             
003500     LABEL RECORDS ARE OMITTED.                                           
003600                                                                          
W26606 01  INPUT-RECORD2                    PIC  X(130).                        
003800                                                                          
003900 FD  INPUT-FILE3                                                          
004000     RECORDING MODE IS F                                                  
004010     BLOCK CONTAINS 0 RECORDS                                             
004020     LABEL RECORDS ARE OMITTED.                                           
004030                                                                          
W26606 01  INPUT-RECORD3                    PIC  X(130).                        
004050                                                                          
004060 FD  REPORT-FILE                                                          
004070     RECORDING MODE IS F                                                  
004080     BLOCK CONTAINS 0 RECORDS                                             
004090     LABEL RECORDS ARE OMITTED.                                           
004100                                                                          
004200 01  REPORT-RECORD                    PIC  X(132).                        
004300                                                                          
004400 FD  TOTAL-FILE                                                           
004500     RECORDING MODE IS F                                                  
004600     BLOCK CONTAINS 0 RECORDS                                             
004700     LABEL RECORDS ARE OMITTED.                                           
004710                                                                          
004720 01  TOTAL-RECORD                     PIC  X(680).                        
004730                                                                          
004740 WORKING-STORAGE SECTION.                                                 
004750                                                                          
004760******************************************************************        
004770*    CR-CONTROL RECORD                                           *        
004780******************************************************************        
004790 01  CR-CONTROL-RECORD.                                                   
004800     05  FILLER                       PIC  X(03)    VALUE SPACE.          
004900     05  CR-PERIOD-END-DATE.                                              
005000         10  CR-PERIOD-END-DTE-CC     PIC  X(02)    VALUE SPACE.          
005100         10  CR-PERIOD-END-DTE-YY     PIC  X(02)    VALUE SPACE.          
005200         10  FILLER                   PIC  X(01)    VALUE SPACE.          
005300         10  CR-PERIOD-END-DTE-MM     PIC  X(02)    VALUE SPACE.          
005400         10  FILLER                   PIC  X(01)    VALUE SPACE.          
005500         10  CR-PERIOD-END-DTE-DD     PIC  X(02)    VALUE SPACE.          
005600     05  FILLER                       PIC  X(01)    VALUE SPACE.          
005700     05  CR-MONTH-WEEK-VALUE          PIC  X(01)    VALUE SPACE.          
005800     05  FILLER                       PIC  X(65)    VALUE SPACE.          
005900                                                                          
006000******************************************************************        
006100*    PC-PROGRAM CONSTANTS                                        *        
006200******************************************************************        
006300 01  PC-PROGRAM-CONSTANTS.                                                
006400     05  PC-PROGRAM-NAME             PIC  X(08)     VALUE                 
006500                                                    'PEKRP085'.           
006600     05  PC-ONE                      PIC  9(01)     VALUE 1.              
006700     05  PC-TWO                      PIC  9(01)     VALUE 2.              
006800     05  PC-WEEK-VALUE               PIC  X(17)     VALUE                 
006900         ' FOR WEEK ENDING '.                                             
007000     05  PC-MONTH-VALUE              PIC  X(17)     VALUE                 
007100         'FOR MONTH ENDING '.                                             
007200     05  PC-MAX-LINES-PER-PAGE       PIC S9(03)     VALUE +66             
007300                                                    COMP.                 
007400                                                                          
007500******************************************************************        
007600*    PROGRAM VARIABLES                                           *        
007700******************************************************************        
007800 01  PV-PROGRAM-VARIABLES.                                                
007900     05  PV-ADVANCE                   PIC  9(01)    VALUE ZERO.           
008000     05  PV-SUB1                      PIC S9(04)    VALUE +0              
008100                                                    COMP SYNC.            
008200     05  PV-SUB2                      PIC S9(04)    VALUE +0              
008300                                                    COMP SYNC.            
008400     05  PV-HOLD-CATEGORY             PIC  X(01)    VALUE SPACE.          
008500     05  PV-NUM-CATEGORY    REDEFINES   PV-HOLD-CATEGORY                  
008600                                      PIC  9(01).                         
008700     05  PV-HOLD-TYP-CDE              PIC  X(01)    VALUE SPACE.          
008800     05  PV-NUM-TYP-CDE     REDEFINES   PV-HOLD-TYP-CDE                   
008900                                      PIC  9(01).                         
009000     05  PV-HOLD-STORE-NBR            PIC  X(05)    VALUE SPACE.          
009100     05  PV-WORK-PERCENT              PIC S9(03)V9(8)                     
009200                                                    VALUE ZERO.           
009300     05  PV-CATEGORY-TOTAL            PIC S9(10)V9(2)                     
009400                                                    VALUE ZERO.           
009500     05  PV-STORE-TOTAL               PIC S9(10)V9(2)                     
009600                                                    VALUE ZERO.           
009700     05  PV-TYPE-TOTAL                PIC S9(10)V9(2)                     
009800                                                    VALUE ZERO.           
009900     05  PV-GRAND-TOTAL               PIC S9(11)V9(2)                     
010000                                                    VALUE ZERO.           
010100     05  PV-ROUNDED-PERCENT           PIC S9(03)V9(1)                     
010200                                                    VALUE ZERO.           
010300     05  PV-REVERSE-STORE             PIC S9(10)V9(2)                     
010400                                                    VALUE ZERO.           
010500     05  PV-REVERSE-TOTAL             PIC S9(11)V9(2)                     
010600                                                    VALUE ZERO.           
010700     05  PV-CURRENT-DATE.                                                 
010800         10  PV-CURRENT-YEAR          PIC  9(02)    VALUE ZERO.           
010900         10  PV-CURRENT-MONTH         PIC  9(02)    VALUE ZERO.           
011000         10  PV-CURRENT-DAY           PIC  9(02)    VALUE ZERO.           
011100     05  PV-CURRENT-TIME.                                                 
011200         10  PV-CURRENT-HOUR          PIC  9(02)    VALUE ZERO.           
011300         10  PV-CURRENT-MINUTE        PIC  9(02)    VALUE ZERO.           
011400         10  FILLER                   PIC  9(04)    VALUE ZERO.           
011500                                                                          
011600 01  ABEND-CODE                       PIC S9(04)    VALUE +0 COMP.        
011700                                                                          
011800******************************************************************        
011900*    PROGRAM TABLE                                               *        
012000******************************************************************        
012100 01  REPORT-TABLE.                                                        
012200     05  RPT-CATEGORY                 OCCURS 5 TIMES.                     
012300         10  RPT-TYPE-CODE            OCCURS 4 TIMES.                     
012400             15  RT-VALUE             PIC  X(27).                         
012500             15  RT-TOTAL             PIC S9(10)V9(2) COMP-3.             
012600                                                                          
012700******************************************************************        
012800*    PS-PROGRAM SWITCHES                                         *        
012900******************************************************************        
013000 01  PS-PROGRAM-SWITCHES.                                                 
013100     05  PS-END-OF-CONTROL-FILE-SW      PIC  X(01)  VALUE 'N'.            
013200         88  PS-END-OF-CONTROL-FILE                 VALUE 'Y'.            
013300     05  PS-END-OF-FILE2-SW             PIC  X(01)  VALUE 'N'.            
013400         88  PS-END-OF-FILE2                        VALUE 'Y'.            
013500     05  PS-END-OF-FILE3-SW             PIC  X(01)  VALUE 'N'.            
013600         88  PS-END-OF-FILE3                        VALUE 'Y'.            
013700     05  PS-WRITE-HEADING-SW            PIC  X(01)  VALUE 'N'.            
013800         88  PS-WRITE-TYPE-HEADING                  VALUE 'A'.            
013900         88  PS-DID-PAGE-BREAK                      VALUE 'B'.            
014000                                                                          
014100******************************************************************        
014200*    PA-PROGRAM ACCUMULATORS                                     *        
014300******************************************************************        
014400 01  PA-PROGRAM-ACCUMULATORS.                                             
014500     05  PA-LINE-COUNTER                PIC S9(11)  COMP-3                
014600                                                    VALUE ZERO.           
014700     05  PA-PAGE-COUNTER                PIC S9(11)  COMP-3                
014800                                                    VALUE ZERO.           
014900                                                                          
015000******************************************************************        
015100*    STANDARD HEADER FOR 132 COLUMN REPORT                       *        
015200******************************************************************        
015300     COPY DPWS132O.                                                       
015400                                                                          
015500******************************************************************        
015600*    INPUT COPYBOOK                                              *        
015700******************************************************************        
W26606     COPY MLRD023.                                                        
015900                                                                          
016000 01  REPORT-TITLE-LINE1.                                                  
016100     05  FILLER                         PIC  X(47)  VALUE SPACES.         
016200     05  FILLER                         PIC  X(36)  VALUE                 
016300         'MARKDOWN / MARKUP STORE AUDIT REPORT'.                          
016400     05  FILLER                         PIC  X(49)  VALUE SPACES.         
016500                                                                          
016600 01  REPORT-TITLE-LINE2.                                                  
016700     05  FILLER                         PIC  X(51)  VALUE SPACES.         
016800     05  RPT-TL2-TIME-PERIOD            PIC  X(17)  VALUE SPACES.         
016900     05  RPT-TL2-WEEKLY-MM              PIC  X(02)  VALUE SPACES.         
017000     05  FILLER                         PIC  X(01)  VALUE '/'.            
017100     05  RPT-TL2-WEEKLY-DD              PIC  X(02)  VALUE SPACES.         
017200     05  FILLER                         PIC  X(01)  VALUE '/'.            
017300     05  RPT-TL2-WEEKLY-CC              PIC  X(02)  VALUE SPACES.         
017400     05  RPT-TL2-WEEKLY-YY              PIC  X(02)  VALUE SPACES.         
017500     05  FILLER                         PIC  X(54)  VALUE SPACES.         
017600                                                                          
017700 01  REPORT-STORE-HEADING.                                                
017800     05  FILLER                         PIC  X(62)  VALUE SPACES.         
017900     05  FILLER                         PIC  X(05)  VALUE                 
018000         'STORE'.                                                         
018100     05  FILLER                         PIC  X(65)  VALUE SPACES.         
018200                                                                          
018300 01  REPORT-NET-TOTAL-HEADING.                                            
018400     05  FILLER                         PIC  X(32)  VALUE SPACES.         
018500     05  FILLER                         PIC  X(18)  VALUE                 
018600         'NET TOTAL BY STORE'.                                            
018700     05  FILLER                         PIC  X(82)  VALUE SPACES.         
018800                                                                          
018900 01  REPORT-TYPE-CODE-HEADING.                                            
019000     05  FILLER                         PIC  X(32)  VALUE SPACES.         
019100     05  REPORT-TYPE-CODE               PIC  X(27)  VALUE SPACES.         
019200     05  FILLER                         PIC  X(73)  VALUE SPACES.         
019300                                                                          
019400 01  REPORT-DETAIL-LINE.                                                  
019500     05  FILLER                         PIC  X(62)  VALUE SPACES.         
019600     05  RPT-DTL-STORE-NO               PIC  X(05).                       
019700     05  FILLER                         PIC  X(15)  VALUE SPACES.         
019800     05  RPT-DTL-EXT-RTL-AMT            PIC  ZZ,ZZZ,ZZZ.99-.              
019900     05  FILLER                         PIC  X(01)  VALUE SPACES.         
020000     05  RPT-DTL-PERCENT                PIC  ZZZ.9.                       
020100     05  FILLER                         PIC  X(01)  VALUE '%'.            
020200     05  RPT-DTL-NEGATIVE               PIC  X(01).                       
020300     05  FILLER                         PIC  X(28)  VALUE SPACES.         
020400                                                                          
020500 01  REPORT-SUB-TOTAL-LINE.                                               
020600     05  FILLER                         PIC  X(38)  VALUE SPACES.         
020700     05  FILLER                         PIC  X(05)  VALUE                 
020800         'TOTAL'.                                                         
020900     05  FILLER                         PIC  X(38)  VALUE SPACES.         
021000     05  RPT-SUBT-RTL-AMT               PIC  ZZZ,ZZZ,ZZZ.99-.             
021100     05  FILLER                         PIC  X(36)  VALUE SPACES.         
021200                                                                          
021300 01  REPORT-TYP-CODE-TOTAL-LINE.                                          
021400     05  FILLER                         PIC  X(32)  VALUE SPACE.          
021500     05  FILLER                         PIC  X(06)  VALUE                 
021600         'TOTAL '.                                                        
021700     05  RPT-TYP-CODE-VALUE             PIC  X(12)  VALUE SPACE.          
021800     05  FILLER                         PIC  X(31)  VALUE SPACES.         
021900     05  RPT-TYP-CODE-TOTAL             PIC  ZZZ,ZZZ,ZZZ.99-.             
022000     05  FILLER                         PIC  X(36)  VALUE SPACES.         
022100                                                                          
022200 01  REPORT-GRAND-TOTAL-LINE.                                             
022300     05  FILLER                         PIC  X(32)  VALUE SPACE.          
022400     05  FILLER                         PIC  X(12)  VALUE                 
022500         'GRAND TOTAL:'.                                                  
022600     05  FILLER                         PIC  X(34)  VALUE SPACES.         
022700     05  RPT-GRAND-TOTAL                PIC  ZZ,ZZZ,ZZZ,ZZZ.99-.          
022800     05  FILLER                         PIC  X(36)  VALUE SPACES.         
022900                                                                          
023000 01  REPORT-BLANK-LINE                  PIC  X(132) VALUE SPACES.         
023100                                                                          
023200 01  REPORT-ENDING-LINE.                                                  
023300     05  FILLER                         PIC  X(90)  VALUE SPACES.         
023400     05  FILLER                         PIC  X(19)  VALUE                 
023500         '***END OF REPORT***'.                                           
023600     05  FILLER                         PIC  X(23)  VALUE SPACES.         
023100                                                                          
023200 01  REPORT-NO-DATA-LINE.                                                 
023300     05  FILLER                         PIC  X(48)  VALUE SPACES.         
023400     05  FILLER                         PIC  X(32)  VALUE                 
023500         '*** NO INPUT DATA TO PROCESS ***'.                              
023600     05  FILLER                         PIC  X(52)  VALUE SPACES.         
023700                                                                          
023800******************************************************************        
023900 PROCEDURE DIVISION.                                                      
024000                                                                          
024100 A100-MAINLINE.                                                           
024200                                                                          
024300     PERFORM A150-INITIALIZE-RTN.                                         
024400                                                                          
024500     PERFORM A120-PROCESS-REPORT1.                                        
024600                                                                          
024700     PERFORM I120-PROCESS-REPORT2.                                        
024800                                                                          
024900     PERFORM Z999-CLOSE-FILES.                                            
025000                                                                          
025100     GOBACK.                                                              
025200                                                                          
025300******************************************************************        
025400 A120-PROCESS-REPORT1.                                                    
025500                                                                          
025600     PERFORM B100-CALC-TOTAL-RTN.                                         
025700                                                                          
025800     PERFORM B200-PRINT-DATA-RTN.                                         
025900                                                                          
026000     PERFORM Z998-EOJ-ROUTINE.                                            
026100                                                                          
026200******************************************************************        
026300*     - INITIALIZES THE INTERNAL TABLE.                          *        
026400*     - OPENS THE INPUT/OUTPUT FILES.                            *        
026500*     - READS THE CONTROL RECORD.                                *        
026600*     - ACCEPTS THE CURRENT DATE AND TIME FOR THE REPORT HEADING *        
026700******************************************************************        
026800 A150-INITIALIZE-RTN.                                                     
026900                                                                          
027000     PERFORM A175-INITIALIZE-TABLE                                        
027100       VARYING PV-SUB1 FROM 1 BY 1                                        
027200         UNTIL PV-SUB1 > 5.                                               
027300                                                                          
027400     OPEN INPUT  INPUT-FILE2                                              
027500                 INPUT-FILE3                                              
027600                 CONTROL-FILE.                                            
027700     OPEN OUTPUT REPORT-FILE                                              
027800                 TOTAL-FILE.                                              
027900                                                                          
028000     READ CONTROL-FILE INTO CR-CONTROL-RECORD                             
028100       AT END                                                             
028200         SET PS-END-OF-CONTROL-FILE TO TRUE.                              
028300                                                                          
028400     IF PS-END-OF-CONTROL-FILE                                            
028500         DISPLAY ' '                                                      
028600         DISPLAY '** ABEND     **'                                        
028700         DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME                    
028800         DISPLAY '** PARAGRAPH **  = A150-INITIALIZE-RTN'                 
028900         DISPLAY '** CONTROL FILE IS EMPTY  '                             
029000         DISPLAY ' '                                                      
029100         MOVE +4002                TO ABEND-CODE                          
029200         CALL 'ILBOABN0' USING ABEND-CODE                                 
029300     END-IF.                                                              
029400                                                                          
029500     MOVE CR-PERIOD-END-DTE-MM     TO RPT-TL2-WEEKLY-MM.                  
029600     MOVE CR-PERIOD-END-DTE-DD     TO RPT-TL2-WEEKLY-DD.                  
029700     MOVE CR-PERIOD-END-DTE-CC     TO RPT-TL2-WEEKLY-CC.                  
029800     MOVE CR-PERIOD-END-DTE-YY     TO RPT-TL2-WEEKLY-YY.                  
029900                                                                          
030000     IF CR-MONTH-WEEK-VALUE = 'W'                                         
030100         MOVE PC-WEEK-VALUE        TO RPT-TL2-TIME-PERIOD                 
030200     ELSE                                                                 
030300         MOVE PC-MONTH-VALUE       TO RPT-TL2-TIME-PERIOD                 
030400     END-IF.                                                              
030500                                                                          
030600     ACCEPT PV-CURRENT-DATE      FROM DATE.                               
030700     MOVE PV-CURRENT-MONTH         TO DP132O-RUN-MONTH.                   
030800     MOVE PV-CURRENT-DAY           TO DP132O-RUN-DAY.                     
030900     MOVE PV-CURRENT-YEAR          TO DP132O-RUN-YEAR.                    
031000                                                                          
031100     ACCEPT PV-CURRENT-TIME      FROM TIME.                               
031200     MOVE PV-CURRENT-HOUR          TO DP132O-RUN-HOUR.                    
031300     MOVE PV-CURRENT-MINUTE        TO DP132O-RUN-MINUTE.                  
031400                                                                          
031500     MOVE PC-PROGRAM-NAME          TO DP132O-PROGRAM-NAME.                
031600                                                                          
031700******************************************************************        
031800 A175-INITIALIZE-TABLE.                                                   
031900                                                                          
032000     PERFORM                                                              
032100       VARYING PV-SUB2 FROM 1 BY 1                                        
032200         UNTIL PV-SUB2 > 4                                                
032300             MOVE SPACES           TO RT-VALUE (PV-SUB1, PV-SUB2)         
032400             MOVE ZEROES           TO RT-TOTAL (PV-SUB1, PV-SUB2)         
032500     END-PERFORM.                                                         
032600                                                                          
032700******************************************************************        
032800*     - READ EACH RECORD FROM INPUT FILE2.                       *        
032800*     - IF THERE ARE NO RECORDS IN INPUT FILE2, WE WANT TO       *        
032800*       CONTINUE PROCESSING.                                     *        
032900*     - FOR EACH SEPERATE CATEGORY/TYPE CODE INCREMENT THE       *        
033000*       CORRESPONDING TABLE ENTRY.  THESE TOTALS WILL BE USED    *        
033100*       WHEN PRINTING THE DETAIL LINES TO DETERMINE THE PERCENT  *        
033200*       OF THE TOTAL EACH STORE HAS.                             *        
033300*     - AFTER READING THE LAST RECORD FROM INPUT FILE2, WRITE    *        
033400*       THESE TOTALS TO THE TOTAL FILE.                          *        
033500******************************************************************        
033600 B100-CALC-TOTAL-RTN.                                                     
033700                                                                          
033800     READ INPUT-FILE2 INTO ML023-PC-RECORD                                
033900       AT END                                                             
034000         SET PS-END-OF-FILE2       TO TRUE.                               
034100                                                                          
034200     IF PS-END-OF-FILE2                                                   
               CONTINUE                                                         
           ELSE                                                                 
035300         PERFORM B120-CALC-LOOP                                           
035400            UNTIL PS-END-OF-FILE2                                         
035100     END-IF.                                                              
035500                                                                          
035600     CLOSE INPUT-FILE2.                                                   
035700     WRITE TOTAL-RECORD FROM REPORT-TABLE.                                
035800                                                                          
035900******************************************************************        
036000*     - FOR EACH RECORD READ, ACCUMULATE TOTALS FOR EACH         *        
036100*       CATEGORY AND TYPE CODE.                                  *        
036200*     - STORE THESE TOTALS IN AN INTERNAL TABLE.                 *        
036300******************************************************************        
036400 B120-CALC-LOOP.                                                          
036500                                                                          
036600     EVALUATE ML023-PC-CATEGORY                                           
036700         WHEN '1'                                                         
036800             PERFORM B121-BUYER-TOTAL                                     
036900         WHEN '2'                                                         
037000             PERFORM B122-STORE-TOTAL                                     
037100         WHEN '3'                                                         
037200             PERFORM B123-PROMO-TOTAL                                     
037300         WHEN '4'                                                         
037400             PERFORM B124-REGIS-TOTAL                                     
037500         WHEN '5'                                                         
037600             PERFORM B125-UNMATCHED-TOTAL                                 
037700     END-EVALUATE.                                                        
037800                                                                          
037900     READ INPUT-FILE2 INTO ML023-PC-RECORD                                
038000       AT END                                                             
038100         SET PS-END-OF-FILE2           TO TRUE.                           
038200                                                                          
038300******************************************************************        
038400*     - CATEGORY = '1'(BUYER).  EVALUATE PC-TYP-CDE TO           *        
038500*       DETERMINE WHICH VARIABLE TO INCREMENT.                   *        
038600******************************************************************        
038700 B121-BUYER-TOTAL.                                                        
038800                                                                          
038900     EVALUATE ML023-PC-TYP-CDE                                            
039000         WHEN '1'                                                         
039100             MOVE 'BUYER MARKDOWN             '                           
039200                                       TO RT-VALUE (1, 1)                 
039300             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (1, 1)                 
039400         WHEN '2'                                                         
039500             MOVE 'BUYER MARKDOWN/CANCEL      '                           
039600                                       TO RT-VALUE (1, 2)                 
039700             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (1, 2)                 
039800         WHEN '3'                                                         
039900             MOVE 'BUYER MARKUP               '                           
040000                                       TO RT-VALUE (1, 3)                 
040100             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (1, 3)                 
040200         WHEN '4'                                                         
040300             MOVE 'BUYER MARKUP/CANCEL        '                           
040400                                       TO RT-VALUE (1, 4)                 
040500             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (1, 4)                 
040600     END-EVALUATE.                                                        
040700                                                                          
040800******************************************************************        
040900*     - CATEGORY = '2'(STORE).  EVALUATE PC-TYP-CDE TO           *        
041000*       DETERMINE WHICH VARIABLE TO INCREMENT.                   *        
041100******************************************************************        
041200 B122-STORE-TOTAL.                                                        
041300                                                                          
041400     EVALUATE ML023-PC-TYP-CDE                                            
041500         WHEN '1'                                                         
041600             MOVE 'STORE MARKDOWN             '                           
041700                                       TO RT-VALUE (2, 1)                 
041800             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (2, 1)                 
041900         WHEN '2'                                                         
042000             MOVE 'STORE MARKDOWN/CANCEL      '                           
042100                                       TO RT-VALUE (2, 2)                 
042200             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (2, 2)                 
042300         WHEN '3'                                                         
042400             MOVE 'STORE MARKUP               '                           
042500                                       TO RT-VALUE (2, 3)                 
042600             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (2, 3)                 
042700         WHEN '4'                                                         
042800             MOVE 'STORE MARKUP/CANCEL        '                           
042900                                       TO RT-VALUE (2, 4)                 
043000             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (2, 4)                 
043100     END-EVALUATE.                                                        
043200                                                                          
043300******************************************************************        
043400*     - CATEGORY = '3'(PROMOTION).  EVALUATE PC-TYP-CDE TO       *        
043500*       DETERMINE WHICH VARIABLE TO INCREMENT.                   *        
043600******************************************************************        
043700 B123-PROMO-TOTAL.                                                        
043800                                                                          
043900     EVALUATE ML023-PC-TYP-CDE                                            
044000         WHEN '1'                                                         
044100             MOVE 'PROMOTIONAL MARKDOWN       '                           
044200                                       TO RT-VALUE (3, 1)                 
044300             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (3, 1)                 
044400         WHEN '2'                                                         
044500             MOVE 'PROMOTIONAL MARKDOWN/CANCEL'                           
044600                                       TO RT-VALUE (3, 2)                 
044700             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (3, 2)                 
044800         WHEN '3'                                                         
044900             MOVE 'PROMOTIONAL MARKUP         '                           
045000                                       TO RT-VALUE (3, 3)                 
045100             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (3, 3)                 
045200         WHEN '4'                                                         
045300             MOVE 'PROMOTIONAL MARKUP/CANCEL  '                           
045400                                       TO RT-VALUE (3, 4)                 
045500             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (3, 4)                 
045600     END-EVALUATE.                                                        
045700                                                                          
045800******************************************************************        
045900*     - CATEGORY = '4'(REGISTER).  EVALUATE PC-TYP-CDE TO        *        
046000*       DETERMINE WHICH VARIABLE TO INCREMENT.                   *        
046100******************************************************************        
046200 B124-REGIS-TOTAL.                                                        
046300                                                                          
046400     EVALUATE ML023-PC-TYP-CDE                                            
046500         WHEN '1'                                                         
046600             MOVE 'REGISTER MARKDOWN          '                           
046700                                       TO RT-VALUE (4, 1)                 
046800             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (4, 1)                 
046900         WHEN '2'                                                         
047000             MOVE 'REGISTER MARKDOWN/CANCEL   '                           
047100                                       TO RT-VALUE (4, 2)                 
047200             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (4, 2)                 
047300         WHEN '3'                                                         
047400             MOVE 'REGISTER MARKUP            '                           
047500                                       TO RT-VALUE (4, 3)                 
047600             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (4, 3)                 
047700         WHEN '4'                                                         
047800             MOVE 'REGISTER MARKUP/CANCEL     '                           
047900                                       TO RT-VALUE (4, 4)                 
048000             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (4, 4)                 
048100     END-EVALUATE.                                                        
048200                                                                          
048300******************************************************************        
048400*     - CATEGORY = '5'(UNMATCHED).  EVALUATE PC-TYP-CDE TO       *        
048500*       DETERMINE WHICH VARIABLE TO INCREMENT.                   *        
048600******************************************************************        
048700 B125-UNMATCHED-TOTAL.                                                    
048800                                                                          
048900     EVALUATE ML023-PC-TYP-CDE                                            
049000         WHEN '1'                                                         
049100             MOVE 'UNMATCHED MARKDOWN         '                           
049200                                       TO RT-VALUE (5, 1)                 
049300             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (5, 1)                 
049400         WHEN '2'                                                         
049500             MOVE 'UNMATCHED MARKDOWN/CANCEL  '                           
049600                                       TO RT-VALUE (5, 2)                 
049700             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (5, 2)                 
049800         WHEN '3'                                                         
049900             MOVE 'UNMATCHED MARKUP           '                           
050000                                       TO RT-VALUE (5, 3)                 
050100             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (5, 3)                 
050200         WHEN '4'                                                         
050300             MOVE 'UNMATCHED MARKUP/CANCEL    '                           
050400                                       TO RT-VALUE (5, 4)                 
050500             ADD ML023-PC-EXTD-RTL-AMT TO RT-TOTAL (5, 4)                 
050600     END-EVALUATE.                                                        
050700                                                                          
050800******************************************************************        
050900 B200-PRINT-DATA-RTN.                                                     
051000                                                                          
051100     PERFORM C200-INITIAL-READ.                                           
051200                                                                          
           IF PS-END-OF-FILE2                                                   
              CONTINUE                                                          
           ELSE                                                                 
051300        PERFORM D200-PRINT-LOOP                                           
051400            UNTIL PS-END-OF-FILE2                                         
051500                                                                          
051600        PERFORM E100-WRITE-STORE-LINE                                     
051700                                                                          
051800        PERFORM F100-WRITE-TOTAL-TYPE-CODE                                
051900                                                                          
052000        PERFORM G100-WRITE-TOTAL-CATEGORY                                 
052100                                                                          
052200        PERFORM Z200-GRAND-TOTAL-RTN                                      
052300                                                                          
052400        PERFORM Z300-PRINT-LINE                                           
           END-IF.                                                              
052500                                                                          
052600******************************************************************        
052700*     - READ THE FIRST RECORD FROM INPUT-FILE2                   *        
052800*     - MOVE CATEGORY, TYPE CODE AND STORE NUMBER TO HOLD AREA.  *        
052900******************************************************************        
053000 C200-INITIAL-READ.                                                       
053100                                                                          
053200     MOVE 'N'                      TO PS-END-OF-FILE2-SW.                 
053300     OPEN INPUT INPUT-FILE2.                                              
053400                                                                          
053500     READ INPUT-FILE2 INTO ML023-PC-RECORD                                
053600       AT END                                                             
053700         SET PS-END-OF-FILE2       TO TRUE.                               
053800                                                                          
053900     IF PS-END-OF-FILE2                                                   
055000         MOVE PC-ONE               TO DP132O-REPORT-NUMBER                
054000         PERFORM Z550-PRINT-MAIN-HDG                                      
 54100         WRITE REPORT-RECORD                                              
 54100            FROM REPORT-NO-DATA-LINE                                      
054200               AFTER ADVANCING 3 LINES                                    
054900     ELSE                                                                 
055000         MOVE PC-ONE               TO DP132O-REPORT-NUMBER                
055100         MOVE ML023-PC-CATEGORY    TO PV-HOLD-CATEGORY                    
055200         MOVE ML023-PC-TYP-CDE     TO PV-HOLD-TYP-CDE                     
055300         MOVE ML023-PC-STORE-NBR   TO PV-HOLD-STORE-NBR                   
055400                                                                          
055500         PERFORM Z500-PROCESS-PAGE-BREAK                                  
054800     END-IF.                                                              
055600                                                                          
055700******************************************************************        
055800*     EVALUATE EACH RECORD READ:                                 *        
055900*       IF CATEGORY, TYPE CODE, AND STORE NUMBER = HOLD AREAS    *        
056000*           INCREMENT THE ACCUMULATOR                            *        
056100*     - IF CATEGORY DOES NOT = HOLD CATEGORY                     *        
056200*           WRITE THE STORE TOTAL LINE, THE TYPE CODE TOTAL LINE *        
056300*           AND THE CATEGORY TOTAL LINE.                         *        
056400*     - IF TYPE CODE DOES NOT = HOLD TYPE CODE                   *        
056500*           WRITE THE STORE TOTAL LINE AND THE TYPE CODE TOTAL   *        
056600*           LINE.                                                *        
056700*     - IF STORE NUMBER DOES NOT = HOLD STORE NUMBER             *        
056800*           WRITE THE STORE LINE                                 *        
056900******************************************************************        
057000 D200-PRINT-LOOP.                                                         
057100                                                                          
057200     IF ML023-PC-CATEGORY = PV-HOLD-CATEGORY                              
057300         IF ML023-PC-TYP-CDE = PV-HOLD-TYP-CDE                            
057400             IF ML023-PC-STORE-NBR = PV-HOLD-STORE-NBR                    
057500                 ADD ML023-PC-EXTD-RTL-AMT                                
057600                                   TO PV-STORE-TOTAL                      
057700             ELSE                                                         
057800                 PERFORM E100-WRITE-STORE-LINE                            
057900             END-IF                                                       
058000         ELSE                                                             
058100             PERFORM E100-WRITE-STORE-LINE                                
058200             PERFORM F100-WRITE-TOTAL-TYPE-CODE                           
058300             PERFORM Z100-TYPE-CODE-HEADING                               
058400         END-IF                                                           
058500     ELSE                                                                 
058600         PERFORM E100-WRITE-STORE-LINE                                    
058700         PERFORM F100-WRITE-TOTAL-TYPE-CODE                               
058800         PERFORM G100-WRITE-TOTAL-CATEGORY                                
058900         PERFORM Z100-TYPE-CODE-HEADING                                   
059000     END-IF.                                                              
059100                                                                          
059200     READ INPUT-FILE2 INTO ML023-PC-RECORD                                
059300       AT END                                                             
059400         SET PS-END-OF-FILE2       TO TRUE.                               
059500                                                                          
059600******************************************************************        
059700*     - WRITE THE STORE TOTAL LINE                               *        
059800******************************************************************        
059900 E100-WRITE-STORE-LINE.                                                   
060000                                                                          
060100     MOVE PV-HOLD-STORE-NBR        TO RPT-DTL-STORE-NO.                   
060200     MOVE PV-STORE-TOTAL           TO RPT-DTL-EXT-RTL-AMT.                
060300     ADD  PV-STORE-TOTAL           TO PV-TYPE-TOTAL.                      
060400                                                                          
060500     MOVE ' '                      TO RPT-DTL-NEGATIVE.                   
060600     IF  RT-TOTAL (PV-NUM-CATEGORY, PV-NUM-TYP-CDE) POSITIVE              
060700     AND PV-STORE-TOTAL NEGATIVE                                          
060800         MOVE '-'                  TO RPT-DTL-NEGATIVE                    
060900     END-IF.                                                              
061000                                                                          
061100     COMPUTE PV-REVERSE-TOTAL =                                           
061200            RT-TOTAL (PV-NUM-CATEGORY, PV-NUM-TYP-CDE) * +1.              
061300     COMPUTE PV-REVERSE-STORE = PV-STORE-TOTAL * +1.                      
061400                                                                          
061500     IF PV-REVERSE-STORE = ZERO                                           
061600         MOVE ZERO                 TO PV-ROUNDED-PERCENT                  
061700     ELSE                                                                 
061800         COMPUTE PV-WORK-PERCENT = (PV-REVERSE-STORE /                    
061900                                    PV-REVERSE-TOTAL) * 100               
062000         MULTIPLY PV-WORK-PERCENT BY 1                                    
062100                  GIVING PV-ROUNDED-PERCENT ROUNDED                       
062200     END-IF.                                                              
062300                                                                          
062400     MOVE PV-ROUNDED-PERCENT       TO RPT-DTL-PERCENT.                    
062500     MOVE REPORT-DETAIL-LINE       TO REPORT-BLANK-LINE.                  
062600     MOVE PC-ONE                   TO PV-ADVANCE.                         
062700     PERFORM Z300-PRINT-LINE.                                             
062800                                                                          
062900     MOVE ML023-PC-STORE-NBR       TO PV-HOLD-STORE-NBR.                  
063000     MOVE ZERO                     TO PV-STORE-TOTAL.                     
063100     ADD ML023-PC-EXTD-RTL-AMT     TO PV-STORE-TOTAL.                     
063200                                                                          
063300******************************************************************        
063400*     - WRITE THE TYPE CODE TOTAL LINE                           *        
063500******************************************************************        
063600 F100-WRITE-TOTAL-TYPE-CODE.                                              
063700                                                                          
063800     MOVE PV-TYPE-TOTAL            TO RPT-SUBT-RTL-AMT.                   
063900     ADD  PV-TYPE-TOTAL            TO PV-CATEGORY-TOTAL.                  
064000                                                                          
064100     MOVE REPORT-SUB-TOTAL-LINE    TO REPORT-BLANK-LINE.                  
064200     MOVE PC-TWO                   TO PV-ADVANCE.                         
064300     PERFORM Z300-PRINT-LINE.                                             
064400                                                                          
064500     MOVE ML023-PC-TYP-CDE         TO PV-HOLD-TYP-CDE.                    
064600     MOVE ZERO                     TO PV-TYPE-TOTAL.                      
064700                                                                          
064800******************************************************************        
064900*     - WRITE THE CATEGORY TOTAL LINE                            *        
065000******************************************************************        
065100 G100-WRITE-TOTAL-CATEGORY.                                               
065200                                                                          
065300     EVALUATE PV-HOLD-CATEGORY                                            
065400         WHEN '1'                                                         
065500             MOVE 'BUYER:      '   TO RPT-TYP-CODE-VALUE                  
065600         WHEN '2'                                                         
065700             MOVE 'STORE:      '   TO RPT-TYP-CODE-VALUE                  
065800         WHEN '3'                                                         
065900             MOVE 'PROMOTIONAL:'   TO RPT-TYP-CODE-VALUE                  
066000         WHEN '4'                                                         
066100             MOVE 'REGISTER:   '   TO RPT-TYP-CODE-VALUE                  
066200         WHEN '5'                                                         
066300             MOVE 'UNMATCHED:  '   TO RPT-TYP-CODE-VALUE                  
066400     END-EVALUATE.                                                        
066500                                                                          
066600     MOVE PV-CATEGORY-TOTAL        TO RPT-TYP-CODE-TOTAL.                 
066700     ADD  PV-CATEGORY-TOTAL        TO PV-GRAND-TOTAL.                     
066800                                                                          
066900     MOVE REPORT-TYP-CODE-TOTAL-LINE                                      
067000                                   TO REPORT-BLANK-LINE.                  
067100     MOVE PC-TWO                   TO PV-ADVANCE.                         
067200     PERFORM Z300-PRINT-LINE.                                             
067300                                                                          
067400     MOVE ML023-PC-CATEGORY        TO PV-HOLD-CATEGORY.                   
067500     MOVE ZERO                     TO PV-CATEGORY-TOTAL.                  
067600                                                                          
067700******************************************************************        
067800*     - REPORT 2 LISTS EACH STORES NET MARKDOWN/MARKUP AMOUNT.   *        
067900******************************************************************        
068000 I120-PROCESS-REPORT2.                                                    
068100                                                                          
068200     PERFORM J100-INITIAL-READ.                                           
068300                                                                          
068500     IF PS-END-OF-FILE3                                                   
068500        CONTINUE                                                          
068500     ELSE                                                                 
068400        PERFORM K200-FILE3-LOOP                                           
068500            UNTIL PS-END-OF-FILE3                                         
068600                                                                          
068700        PERFORM L100-WRITE-STORE-LINE                                     
068800                                                                          
068900        PERFORM Z200-GRAND-TOTAL-RTN                                      
069000                                                                          
069100        PERFORM Z400-PRINT-LINE                                           
069200                                                                          
069300        PERFORM Z998-EOJ-ROUTINE                                          
068500     END-IF.                                                              
069400                                                                          
069500******************************************************************        
069600*     - READ THE FIRST RECORD FROM INPUT-FILE3                   *        
069700******************************************************************        
069800 J100-INITIAL-READ.                                                       
069900                                                                          
070000     READ INPUT-FILE3 INTO ML023-PC-RECORD                                
070100       AT END                                                             
070200         SET PS-END-OF-FILE3       TO TRUE.                               
070300                                                                          
070400     IF PS-END-OF-FILE3                                                   
055000         MOVE PC-TWO               TO DP132O-REPORT-NUMBER                
054000         PERFORM Z550-PRINT-MAIN-HDG                                      
 54100         WRITE REPORT-RECORD                                              
 54100            FROM REPORT-NO-DATA-LINE                                      
054200               AFTER ADVANCING 3 LINES                                    
089400         WRITE REPORT-RECORD                                              
089500            FROM REPORT-ENDING-LINE                                       
089600               AFTER ADVANCING 2 LINES                                    
071400     ELSE                                                                 
071500        MOVE ML023-PC-STORE-NBR    TO PV-HOLD-STORE-NBR                   
071600        MOVE ZERO                  TO PV-STORE-TOTAL                      
071700                                      PA-PAGE-COUNTER                     
071800        MOVE PC-TWO                TO DP132O-REPORT-NUMBER                
071900                                                                          
072000        PERFORM Z550-PRINT-MAIN-HDG                                       
072100                                                                          
072200        WRITE REPORT-RECORD                                               
072300          FROM REPORT-NET-TOTAL-HEADING                                   
072400            AFTER ADVANCING 3 LINES                                       
072500                                                                          
072600        MOVE +6                    TO PA-LINE-COUNTER                     
071300     END-IF.                                                              
072700                                                                          
072800******************************************************************        
072900*     - EVALUATE EACH RECORD READ:                               *        
073000*       IF STORE NUMBER = HOLD STORE NUMBER                      *        
073100*           INCREMENT THE ACCUMULATOR                            *        
073200*         OTHERWISE, WRITE THE STORE TOTAL LINE.                 *        
073300******************************************************************        
073400 K200-FILE3-LOOP.                                                         
073500                                                                          
073600     IF ML023-PC-STORE-NBR = PV-HOLD-STORE-NBR                            
073700         ADD ML023-PC-EXTD-RTL-AMT TO PV-STORE-TOTAL                      
073800     ELSE                                                                 
073900         PERFORM L100-WRITE-STORE-LINE                                    
074000     END-IF.                                                              
074100                                                                          
074200     READ INPUT-FILE3 INTO ML023-PC-RECORD                                
074300       AT END                                                             
074400         SET PS-END-OF-FILE3       TO TRUE.                               
074500                                                                          
074600******************************************************************        
074700*     - WRITE THE STORE TOTAL LINE.                              *        
074800******************************************************************        
074900 L100-WRITE-STORE-LINE.                                                   
075000                                                                          
075100     MOVE PV-HOLD-STORE-NBR        TO RPT-DTL-STORE-NO.                   
075200     MOVE PV-STORE-TOTAL           TO RPT-DTL-EXT-RTL-AMT.                
075300                                                                          
075400     MOVE ' '                      TO RPT-DTL-NEGATIVE.                   
075500     IF  PV-GRAND-TOTAL POSITIVE                                          
075600     AND PV-STORE-TOTAL NEGATIVE                                          
075700         MOVE '-'                  TO RPT-DTL-NEGATIVE                    
075800     END-IF.                                                              
075900                                                                          
076000     COMPUTE PV-REVERSE-TOTAL = PV-GRAND-TOTAL * +1.                      
076100     COMPUTE PV-REVERSE-STORE = PV-STORE-TOTAL * +1.                      
076200                                                                          
076300     IF PV-REVERSE-STORE = ZERO                                           
076400         MOVE ZERO                 TO PV-ROUNDED-PERCENT                  
076500     ELSE                                                                 
076600         COMPUTE PV-WORK-PERCENT =  (PV-REVERSE-STORE /                   
076700                                     PV-REVERSE-TOTAL) * 100              
076800         MULTIPLY PV-WORK-PERCENT BY 1                                    
076900                  GIVING PV-ROUNDED-PERCENT ROUNDED                       
077000     END-IF.                                                              
077100                                                                          
077200     MOVE PV-ROUNDED-PERCENT       TO RPT-DTL-PERCENT.                    
077300     MOVE REPORT-DETAIL-LINE       TO REPORT-BLANK-LINE.                  
077400     MOVE PC-ONE                   TO PV-ADVANCE.                         
077500     PERFORM Z400-PRINT-LINE.                                             
077600                                                                          
077700     MOVE ML023-PC-STORE-NBR       TO PV-HOLD-STORE-NBR.                  
077800     MOVE ZERO                     TO PV-STORE-TOTAL.                     
077900     ADD ML023-PC-EXTD-RTL-AMT     TO PV-STORE-TOTAL.                     
078000                                                                          
078100******************************************************************        
078200*     - WRITE THE TYPE CODE HEADING                              *        
078300******************************************************************        
078400 Z100-TYPE-CODE-HEADING.                                                  
078500                                                                          
078600     MOVE RT-VALUE (PV-NUM-CATEGORY, PV-NUM-TYP-CDE)                      
078700                                   TO REPORT-TYPE-CODE.                   
078800                                                                          
078900     MOVE REPORT-TYPE-CODE-HEADING TO REPORT-BLANK-LINE.                  
079000     MOVE PC-TWO                   TO PV-ADVANCE.                         
079100     MOVE 'A'                      TO PS-WRITE-HEADING-SW.                
079200                                                                          
079300     PERFORM Z300-PRINT-LINE.                                             
079400                                                                          
079500******************************************************************        
079600*     - WRITE THE GRAND TOTAL LINE                               *        
079700******************************************************************        
079800 Z200-GRAND-TOTAL-RTN.                                                    
079900                                                                          
080000     MOVE PV-GRAND-TOTAL           TO RPT-GRAND-TOTAL.                    
080100                                                                          
080200     MOVE REPORT-GRAND-TOTAL-LINE  TO REPORT-BLANK-LINE.                  
080300     MOVE PC-TWO                   TO PV-ADVANCE.                         
080400                                                                          
080500******************************************************************        
080600 Z300-PRINT-LINE.                                                         
080700                                                                          
080800     IF PA-LINE-COUNTER + PV-ADVANCE > PC-MAX-LINES-PER-PAGE              
080900         PERFORM Z500-PROCESS-PAGE-BREAK                                  
081000     END-IF.                                                              
081100                                                                          
081200     IF PS-DID-PAGE-BREAK                                                 
081300         MOVE 'N'                  TO PS-WRITE-HEADING-SW                 
081400     ELSE                                                                 
081500         MOVE 'N'                  TO PS-WRITE-HEADING-SW                 
081600         WRITE REPORT-RECORD                                              
081700           FROM REPORT-BLANK-LINE                                         
081800             AFTER PV-ADVANCE                                             
081900         ADD PV-ADVANCE            TO PA-LINE-COUNTER                     
082000         MOVE SPACES               TO REPORT-BLANK-LINE                   
082100     END-IF.                                                              
082200******************************************************************        
082300 Z400-PRINT-LINE.                                                         
082400                                                                          
082500     IF PA-LINE-COUNTER + PV-ADVANCE > PC-MAX-LINES-PER-PAGE              
082600         PERFORM Z550-PRINT-MAIN-HDG                                      
082700         WRITE REPORT-RECORD                                              
082800           FROM REPORT-NET-TOTAL-HEADING                                  
082900             AFTER ADVANCING 3 LINES                                      
083000         MOVE +6                   TO PA-LINE-COUNTER                     
083100     END-IF.                                                              
083200                                                                          
083300     WRITE REPORT-RECORD                                                  
083400       FROM REPORT-BLANK-LINE                                             
083500         AFTER PV-ADVANCE.                                                
083600                                                                          
083700     ADD PV-ADVANCE                TO PA-LINE-COUNTER.                    
083800     MOVE SPACES                   TO REPORT-BLANK-LINE.                  
083900                                                                          
084000******************************************************************        
084100*     - ADVANCES TO A NEW PAGE AND WRITES THE REPORT HEADINGS    *        
084200******************************************************************        
084300 Z500-PROCESS-PAGE-BREAK.                                                 
084400                                                                          
084500     PERFORM Z550-PRINT-MAIN-HDG.                                         
084600                                                                          
084700     WRITE REPORT-RECORD                                                  
084800       FROM REPORT-STORE-HEADING                                          
084900         AFTER ADVANCING 3 LINES.                                         
085000                                                                          
085100     MOVE RT-VALUE (PV-NUM-CATEGORY, PV-NUM-TYP-CDE)                      
085200                                   TO REPORT-TYPE-CODE.                   
085300                                                                          
085400     WRITE REPORT-RECORD                                                  
085500       FROM REPORT-TYPE-CODE-HEADING                                      
085600         AFTER ADVANCING 1 LINE.                                          
085700                                                                          
085800     IF PS-WRITE-TYPE-HEADING                                             
085900         MOVE 'B'                  TO PS-WRITE-HEADING-SW                 
086000     END-IF.                                                              
086100                                                                          
086200     MOVE +7                       TO PA-LINE-COUNTER.                    
086300                                                                          
086400******************************************************************        
086500 Z550-PRINT-MAIN-HDG.                                                     
086600                                                                          
086700     ADD PC-ONE                    TO PA-PAGE-COUNTER.                    
086800     MOVE PA-PAGE-COUNTER          TO DP132O-PAGE-NUMBER.                 
086900                                                                          
087000     WRITE REPORT-RECORD                                                  
087100       FROM DP132O-STANDRD-HEADER-132-COLS                                
087200         AFTER ADVANCING PAGE.                                            
087300     INITIALIZE DP132O-LASER-NEW-PAGE-INDICATR.                           
087400                                                                          
087500     WRITE REPORT-RECORD                                                  
087600       FROM REPORT-TITLE-LINE1                                            
087700         AFTER ADVANCING 1 LINE.                                          
087800                                                                          
087900     WRITE REPORT-RECORD                                                  
088000       FROM REPORT-TITLE-LINE2                                            
088100         AFTER ADVANCING 1 LINE.                                          
088200                                                                          
088300******************************************************************        
088400*     - WRITES AN END OF REPORT LINE TO THE REPORT               *        
088500******************************************************************        
088600 Z998-EOJ-ROUTINE.                                                        
088700                                                                          
088800     ADD PC-TWO                    TO PA-LINE-COUNTER.                    
088900                                                                          
089000     IF PA-LINE-COUNTER > PC-MAX-LINES-PER-PAGE                           
089100         PERFORM Z500-PROCESS-PAGE-BREAK                                  
089200     END-IF.                                                              
089300                                                                          
089400     WRITE REPORT-RECORD                                                  
089500       FROM REPORT-ENDING-LINE                                            
089600         AFTER ADVANCING 2 LINES.                                         
089700                                                                          
089800******************************************************************        
089900*     - CLOSES THE FILES                                         *        
090000******************************************************************        
090100 Z999-CLOSE-FILES.                                                        
090200                                                                          
090300     CLOSE INPUT-FILE2                                                    
090400           INPUT-FILE3                                                    
090500           CONTROL-FILE                                                   
090600           REPORT-FILE                                                    
090700           TOTAL-FILE.                                                    
090800                                                                          
