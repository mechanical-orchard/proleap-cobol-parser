      ******************************************************************00010003
      *  I M P O R T A N T --- WHEN COMPILING THIS PROGRAM THE MQ SERIES00020003
      *  SWITCH ON THE 2ND COMPILE SCREEN NEEDS TO BE SET TO 'Y'.       00030003
      *  THIS PROGRAM USES MQSERIES.                                    00040003
      *                                                                 00050003
      ******************************************************************00060003
       IDENTIFICATION DIVISION.                                         00070003
       PROGRAM-ID.             GVKUT020.                                00080003
       AUTHOR.                 JIM HUNTER.                              00090003
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00100003
       DATE-WRITTEN.           JULY 2016.                               00110003
       DATE-COMPILED.                                                   00120003
                                                                        00130003
      ******************************************************************00140003
      *        G I V   M E S S A G E   Q U E U E   D O W N L O A D      00150003
      *                                                                 00160003
      * THIS PROGRAM WILL READ THE FASHION ADJUSTMENT RECORDS CREATED   00170003
      * IN GIV AND FORMAT THEM INTO A SEQUENTIAL FILE MATCHING THE      00180003
      * FSRD035 COPYBOOK FOR PROCESSING BY THE FASHION SYSTEM.          00190003
      *                                                                 00200003
      *                                                                 00210003
      * GENERAL PROCESSING:                                             00220003
      * -------------------                                             00230003
      * 1) INITIALIZE VARIABLES, SET SWITCHES, OPEN ITEM QUEUE.         00240003
      *                                                                 00250003
      * 2) WRITE ENTIRE MESSAGE TO THE OUTPUT FILE.                     00260003
      *                                                                 00270003
      * 3) EVERYTHING WILL GET INITIALIZED AND RESET - READY FOR THE    00280003
      *    NEXT MESSAGE WHEN IT IS ENCOUNTERED.                         00290003
      *                                                                 00300003
      *                                                                 00310003
      * UNITS OF WORK AND COMMITS:                                      00320003
      * --------------------------                                      00330003
      * MQ COMMITS WILL BE PERFORMED TO PERMANENTLY 'REMOVE' THE        00340003
      * MESSAGES FROM THE ITEM QUEUE.                                   00350003
      *                                                                 00360003
      *  10/16/17 - JIM HUNTER   CHG0180589  INITIAL PROGRAM VERSION.   00370013
      ******************************************************************00380003
       ENVIRONMENT DIVISION.                                            00390003
      ******************************************************************00400003
       CONFIGURATION SECTION.                                           00410003
       SOURCE-COMPUTER.        IBM-3090.                                00420003
       OBJECT-COMPUTER.        IBM-3090.                                00430003
                                                                        00440003
       INPUT-OUTPUT SECTION.                                            00450003
       FILE-CONTROL.                                                    00460003
                                                                        00470003
           SELECT OUTPUT-FILE                                           00480003
                  ASSIGN TO OUT01.                                      00490003
      ******************************************************************00500003
       DATA DIVISION.                                                   00510003
      ******************************************************************00520003
       FILE SECTION.                                                    00530003
                                                                        00540003
       FD  OUTPUT-FILE                                                  00550003
           RECORDING MODE IS F                                          00560003
           LABEL RECORDS ARE STANDARD                                   00570003
           BLOCK CONTAINS 0 RECORDS.                                    00580003
       01  OUTPUT-RECORD               PIC  X(200).                     00590003
      *                                                                 00600003
      ******************************************************************00610003
       WORKING-STORAGE SECTION.                                         00620003
      ******************************************************************00630003
      *  COMMUNICATIONS AREA FOR DB2                                    00640003
      ******************************************************************00650003
           EXEC SQL                                                     00660003
                INCLUDE SQLCA                                           00670003
           END-EXEC.                                                    00680003
                                                                        00690003
      ******************************************************************00700003
      *   DB2 VIEW DECLARATION - CONSUMER QUEUE VIEW (GET/READ)         00710003
      *         (WSV_CNSMR_QUE) WSV00006                                00720003
      ******************************************************************00730003
           EXEC SQL                                                     00740003
               INCLUDE WSV00006                                         00750003
           END-EXEC.                                                    00760003
                                                                        00770003
033200******************************************************************00780004
      *  DB2 VIEW DECLARATION - XLT_SKU TABLE                           00790004
      *        XLT00003                                                 00800004
      ******************************************************************00810004
           EXEC SQL                                                     00820004
               INCLUDE XLT00003                                         00830004
           END-EXEC.                                                    00840004
                                                                        00850004
      ******************************************************************00860003
      * MQ GIV INPUT LAYOUT                                             00870003
      ******************************************************************00880003
       01  GIV-FASHION-RECORD.                                          00890003
           05  GIV-FASHION-SKU            PIC 9(8)   VALUE ZEROES.      00900003
           05  GIV-FASHION-LOC            PIC 9(4)   VALUE ZEROES.      00910003
           05  GIV-FASHION-ADJ-QTY-SIGN   PIC X      VALUE SPACE.       00920007
           05  GIV-FASHION-ADJ-QTY        PIC 9(9)   VALUE ZEROES.      00930007
           05  FILLER                     PIC X(58)  VALUE SPACES.      00940007
                                                                        00950003
      ******************************************************************00960003
      * FASHION ADJUSTMENT LAYOUT - USED FOR OUTPUT LAYOUT              00970003
      ******************************************************************00980003
           COPY FSRD035.                                                00990003
                                                                        01000003
      ******************************************************************01010003
      *  WORK AREA FOR SQL ERROR ROUTINE 'DSNTIAR'.                     01020003
      ******************************************************************01030003
           COPY DPWS004.                                                01040003
                                                                        01050003
       01  ABEND-CODE                     PIC S9(04) VALUE +0 COMP SYNC.01060003
                                                                        01070003
       01  PA-PROGRAM-ACCUMULATORS.                                     01080003
           05  PA-TOTALS.                                               01090003
               10 PA-TOTAL-MQ-GETS        PIC S9(09) VALUE +0 COMP-3.   01100003
               10 PA-TOTAL-MQ-COMMITS     PIC S9(09) VALUE +0 COMP-3.   01110003
               10 PA-TOTAL-OUTPUT-RECS    PIC S9(09) VALUE +0 COMP-3.   01120003
               10 PA-TOTAL-ERROR-COUNT    PIC S9(09) VALUE +0 COMP-3.   01130003
                                                                        01140003
       01  PS-PROGRAM-SWITCHES.                                         01150003
           05  PS-END-OF-INPUT-FILE-SW    PIC X(01) VALUE 'N'.          01160003
               88  PS-END-OF-INPUT                  VALUE 'Y'.          01170003
               88  PS-NOT-END-OF-INPUT              VALUE 'N'.          01180003
           05  PS-ERROR-SW                PIC X(01) VALUE 'N'.          01190003
               88  PS-ERROR-YES                     VALUE 'Y'.          01200003
               88  PS-ERROR-NO                      VALUE 'N'.          01210003
           05  PS-MQGET-SUCCESSFUL-IND    PIC X(01) VALUE SPACES.       01220003
               88  PS-MQGET-SUCCESSFUL              VALUE 'Y'.          01230003
                                                                        01240003
       01  PC-PROGRAM-CONSTANTS.                                        01250003
           05  PC-CURRENT-PROGRAM-NAME    PIC X(08)  VALUE 'GVKUT020'.  01260003
                                                                        01270003
       01  PV-PROGRAM-VARIABLES.                                        01280003
           05  PV-PARAGRAPH               PIC X(48)  VALUE SPACES.      01290003
           05  PV-MSG-LENGTH              PIC S9(9)  BINARY.            01300003
           05  PV-DATA-LENGTH             PIC S9(9)  BINARY.            01310003
           05  PV-HOLD-COMPLETION-CODE    PIC S9(9)  BINARY.            01320003
           05  PV-HOLD-REASON             PIC S9(9)  BINARY.            01330003
           05  PV-MQ-CNT                  PIC 9(05)  VALUE ZEROES.      01340003
                                                                        01350003
       01  PV-MSG                         PIC X(200).                   01360003
                                                                        01370003
       01  PV-ABEND-VARIABLES.                                          01380003
           05  PV-ABEND-CODE              PIC S9(04) VALUE +0 COMP SYNC.01390003
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001. 01400003
           88  ABEND-4002-READ-ERROR                       VALUE +4002. 01410003
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003. 01420003
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004. 01430003
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005. 01440003
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006. 01450003
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007. 01460003
           88  ABEND-4008-DATA-ERROR                       VALUE +4008. 01470003
           88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009. 01480003
           88  ABEND-4013-DB2-ERROR                        VALUE +4013. 01490003
           88  ABEND-4019-CAL-SUB-ERROR                    VALUE +4019. 01500003
           88  ABEND-4020-MQ-ERROR                         VALUE +4020. 01510003
           88  ABEND-4444-FORCED-ABEND                     VALUE +4444. 01520003
                                                                        01530003
       01  PE-MQ-ABEND-VARIABLES.                                       01540003
           10  PE-MQ-QMANAGER             PIC X(05) VALUE SPACES.       01550003
           10  PE-MQ-QNAME                PIC X(30) VALUE SPACES.       01560003
           10  PE-MQ-COMPLETION-CODE      PIC 9(04) VALUE ZEROS.        01570003
           10  PE-MQ-REASON-CODE          PIC 9(04) VALUE ZEROS.        01580003
                                                                        01590003
      ****************************************************************  01600003
      **     M Q   S E R I E S    W O R K I N G   S T O R A G E     *   01610003
      **                                                                01620003
      **                  M  Q    C O P Y B O O K S                     01630003
      *                                                                 01640003
      ****************************************************************  01650003
       01  MQM-OBJECT-DESCRIPTOR.                                       01660003
           COPY CMQODV.                                                 01670003
                                                                        01680003
       01  MQM-MESSAGE-DESCRIPTOR.                                      01690003
           COPY CMQMDV.                                                 01700003
                                                                        01710003
       01  MQM-GET-MESSAGE-OPTIONS.                                     01720003
           COPY CMQGMOV.                                                01730003
                                                                        01740003
       01  MQM-PUT-MESSAGE-OPTIONS.                                     01750003
           COPY CMQPMOV.                                                01760003
                                                                        01770003
       01  MQM-CONSTANTS.                                               01780003
           COPY CMQV.                                                   01790003
                                                                        01800003
                                                                        01810003
      ****************************************************************  01820003
      *  MQ SERIES CONSTANTS FOR CALLING MODULES SUCH AS PUT AND     *  01830003
      *  OPEN, CONNECT, DISCONNECT.                                  *  01840003
      ****************************************************************  01850003
       01  MQ-WORKING-STORAGE.                                          01860003
           05  PC-MQCONN                PIC X(08)   VALUE 'CSQBCONN'.   01870003
           05  PC-MQOPEN                PIC X(08)   VALUE 'CSQBOPEN'.   01880003
           05  PC-MQINQ                 PIC X(08)   VALUE 'CSQBINQ'.    01890003
           05  PC-MQPUT                 PIC X(8)    VALUE 'CSQBPUT'.    01900003
           05  PC-MQGET                 PIC X(08)   VALUE 'CSQBGET'.    01910003
           05  PC-MQCMIT                PIC X(8)    VALUE 'CSQBCOMM'.   01920003
           05  PC-MQBACK                PIC X(8)    VALUE 'CSQBBACK'.   01930003
           05  PC-MQCLOSE               PIC X(08)   VALUE 'CSQBCLOS'.   01940003
           05  PC-MQDISC                PIC X(08)   VALUE 'CSQBDISC'.   01950003
           05  PC-QMGR-NAME                 PIC X(4)  VALUE SPACES.     01960003
               88  PC-PROD-QMGR-NAME                  VALUE 'QKSP'.     01970003
               88  PC-ACCEPT-QMGR-NAME                VALUE 'QKSQ'.     01980003
               88  PC-TEST-QMGR-NAME                  VALUE 'QKST'.     01990003
           05  PC-RESPONSE-QNAME            PIC X(30) VALUE SPACES.     02000003
           05  PC-WAIT-15-SECS              PIC S9(9) VALUE 15000.      02010003
           05  PC-WAIT-6-SECS               PIC S9(9) VALUE 6000.       02020003
           05  PC-DELAY-INTERVAL.                                       02030003
               10  FILLER                   PIC X(2)  VALUE SPACES.     02040003
               10  PC-DELAY-TIME            PIC 9(8)  VALUE 1500.       02050003
                                                                        02060003
      ****************************************************************  02070003
      ** QUEUE MANAGER HANDLE                                           02080003
      *  QUEUE HANDLES                                                  02090003
      ****************************************************************  02100003
       01 PV-MQ-PROGRAM-HANDLES.                                        02110003
           05  PV-QUEUE-NAME                PIC X(50) VALUE SPACES.     02120003
           05  PV-QMGR-NAME                 PIC X(50) VALUE SPACES.     02130003
           05  PV-HCONN                     PIC S9(9) BINARY VALUE 0.   02140003
           05  PV-GQ-HANDLE                 PIC S9(9) BINARY VALUE 0.   02150003
           05  PV-PQ-HANDLE                 PIC S9(9) BINARY VALUE 0.   02160003
                                                                        02170003
      ****************************************************************  02180003
      * ERROR PROCESSING VARIABLES                                      02190003
      * CODES FROM CALLS ARE STORED HERE                                02200003
      ****************************************************************  02210003
       01 PV-MQ-PROGRAM-VARIABLES.                                      02220003
           05  PV-COMPLETION-CODE           PIC S9(9) BINARY.           02230003
           05  PV-REASON                    PIC S9(9) BINARY.           02240003
           05  PV-CON-REASON                PIC S9(9) BINARY.           02250003
           05  PV-ERROR-MESSAGE             PIC X(48) VALUE SPACES.     02260003
           05  PV-OPEN-OPTIONS              PIC S9(9) BINARY.           02270003
           05  PV-MSGBUFFER                 PIC X(450) VALUE SPACES.    02280003
           05  PV-MQCHECK                   PIC X(8)  VALUE 'MQR2C'.    02290003
           05  PV-MQ-REASON-TEXT            PIC X(30) VALUE SPACES.     02300003
                                                                        02310003
      ****************************************************************  02320003
       PROCEDURE DIVISION.                                              02330003
      ******************************************************************02340003
       0000-PROGRAM-MAINLINE.                                           02350003
           DISPLAY '************ START OF GVKUT020 ***************'     02360003
                                                                        02370003
           PERFORM 1000-INITIALIZE.                                     02380003
                                                                        02390003
           PERFORM 3100-MQ-CONNECT-TO-QMGR.                             02400003
                                                                        02410003
           SET PS-NOT-END-OF-INPUT TO TRUE.                             02420003
                                                                        02430003
           PERFORM 3200-MQ-QUEUE-OPEN.                                  02440003
                                                                        02450003
           PERFORM 3300-MQ-GET-MSG.                                     02460003
                                                                        02470003
           PERFORM 2000-PROCESS-MSGS                                    02480003
               UNTIL PS-END-OF-INPUT.                                   02490003
                                                                        02500003
           PERFORM 5000-END-RUN-ROUTINE.                                02510003
                                                                        02520003
           GOBACK.                                                      02530003
                                                                        02540003
      ******************************************************************02550003
      * INITIALIZE VARIABLES.                                           02560003
      * SET SWITCHES.                                                   02570003
      ******************************************************************02580003
       1000-INITIALIZE.                                                 02590003
                                                                        02600003
           INITIALIZE   PA-TOTAL-MQ-GETS                                02610003
                        PA-TOTAL-MQ-COMMITS                             02620003
                        PV-MSG-LENGTH.                                  02630003
                                                                        02640003
           PERFORM 1200-GET-MQ-QUEUE-INFO.                              02650003
                                                                        02660003
           OPEN OUTPUT OUTPUT-FILE.                                     02670003
                                                                        02680003
      ****************************************************************  02690003
      * GETS MQ INFO FROM WSV_CNSMR_QUE (GET/READ)                      02700003
      ****************************************************************  02710003
       1200-GET-MQ-QUEUE-INFO.                                          02720003
                                                                        02730003
           EXEC SQL                                                     02740003
            SELECT QUE_MGR,                                             02750003
                   QUE_NM                                               02760003
             INTO :WSV00006-QUE-MGR                                     02770003
                 ,:WSV00006-QUE-NM                                      02780003
             FROM  WSV_CNSMR_QUE                                        02790003
            WHERE  APPL_FUNC_DESC  = 'QUEUE'                            02800008
              AND  INTGRTN_TYP_CDE = 'GIVTOFSHN'                        02810009
            WITH UR                                                     02820012
           END-EXEC.                                                    02830003
                                                                        02840003
           EVALUATE TRUE                                                02850003
               WHEN SQLCODE = 0                                         02860003
                   MOVE WSV00006-QUE-MGR-TEXT TO  PV-QMGR-NAME          02870003
                   MOVE WSV00006-QUE-NM-TEXT  TO  PV-QUEUE-NAME         02880003
                                                                        02890003
               WHEN OTHER                                               02900003
                   MOVE 'NO QUEUE/GIVTOFSHN ROW IN WSV_CNSMR_QUE'       02910009
                     TO PV-ERROR-MESSAGE                                02920003
                   SET ABEND-4013-DB2-ERROR   TO TRUE                   02930003
                   PERFORM 9100-DB2-ABEND                               02940003
           END-EVALUATE.                                                02950003
                                                                        02960003
           DISPLAY 'QMGR-NAME       :' PV-QMGR-NAME.                    02970003
           DISPLAY 'QUEUE-NAME      :' PV-QUEUE-NAME.                   02980003
                                                                        02990003
      ******************************************************************03000003
      * WRITE MESSAGE TO A NEW SEQUENTIAL FILE AND GET THE NEXT MSG.    03010003
      ******************************************************************03020003
       2000-PROCESS-MSGS.                                               03030003
                                                                        03040003
           PERFORM 2500-PROCESS-ADJUSTMENT.                             03050003
                                                                        03060003
           PERFORM 2300-REINITIALIZE.                                   03070003
                                                                        03080003
           PERFORM 3600-MQ-COMMIT.                                      03090003
                                                                        03100003
           PERFORM 3300-MQ-GET-MSG.                                     03110003
                                                                        03120003
                                                                        03130003
      ****************************************************************  03140003
      * INIT SOME FIELDS BEFORE THE NEXT MESSAGE IS PROCESSED.          03150003
      * RESET SOME SWITCHES.                                            03160003
      ****************************************************************  03170003
       2300-REINITIALIZE.                                               03180003
                                                                        03190003
           INITIALIZE PV-MSG                                            03200003
                      PV-MSGBUFFER.                                     03210003
                                                                        03220003
           SET PS-ERROR-NO  TO TRUE.                                    03230003
                                                                        03240003
      ******************************************************************03250003
      * WRITE AN OUTPUT RECORD EVEN IF THE ITEM IS NOT FOUND            03260011
      ******************************************************************03270003
       2500-PROCESS-ADJUSTMENT.                                         03280004
                                                                        03290003
           PERFORM 2600-GET-ITM-NBR.                                    03300003
                                                                        03310011
           IF FS035-ITM-NBR = ZERO                                      03320003
               ADD +1    TO PA-TOTAL-ERROR-COUNT                        03330003
           END-IF.                                                      03340011
                                                                        03350011
           PERFORM 2700-WRITE-RECORD.                                   03360011
                                                                        03370003
                                                                        03380003
      ***************************************************************** 03390003
      * GET ITEM NUMBER FROM AN SQL CALL TO XLT_SKU TABLE.              03400003
      ***************************************************************** 03410003
       2600-GET-ITM-NBR.                                                03420003
                                                                        03430003
           MOVE GIV-FASHION-SKU   TO XLT00003-SKU-NBR.                  03440006
                                                                        03450006
           EXEC SQL                                                     03460003
               SELECT ITM_NBR                                           03470003
                 INTO :XLT00003-ITM-NBR                                 03480003
                 FROM XLT_SKU                                           03490003
                WHERE SKU_NBR       = :XLT00003-SKU-NBR                 03500006
               WITH UR                                                  03510012
           END-EXEC.                                                    03520003
                                                                        03530003
           EVALUATE TRUE                                                03540003
             WHEN SQLCODE = -811                                        03550003
             WHEN SQLCODE = ZERO                                        03560003
                 MOVE XLT00003-ITM-NBR TO FS035-ITM-NBR                 03570003
             WHEN SQLCODE = +100                                        03580003
                 MOVE ZERO             TO FS035-ITM-NBR                 03590011
                 DISPLAY 'RECORD = '   GIV-FASHION-RECORD               03600011
                 DISPLAY 'ITEM NUMBER NOT FOUND FOR SKU '               03610003
                 GIV-FASHION-SKU                                        03620003
             WHEN OTHER                                                 03630003
                 MOVE ZERO             TO FS035-ITM-NBR                 03640011
                 DISPLAY 'RECORD = '   GIV-FASHION-RECORD               03650011
                 DISPLAY 'ERROR ' SQLCODE ' LOOKING UP ITEM FOR SKU '   03660003
                 GIV-FASHION-SKU                                        03670003
           END-EVALUATE.                                                03680003
                                                                        03690003
                                                                        03700003
      ******************************************************************03710003
      * WRITE MESSAGE TO A NEW SEQUENTIAL FILE                          03720003
      ******************************************************************03730003
       2700-WRITE-RECORD.                                               03740003
           MOVE GIV-FASHION-LOC          TO FS035-LOC-NBR.              03750007
           MOVE '121'                    TO FS035-ADJ-TYP.              03760007
      *    IF GIV-FASHION-ADJ-QTY < 0                                   03770007
      *        COMPUTE FS035-ADJ-QTY = GIV-FASHION-ADJ-QTY * -1         03780007
      *        MOVE '-'                  TO FS035-ACTN-CDE              03790007
      *    ELSE                                                         03800007
      *        MOVE GIV-FASHION-ADJ-QTY  TO FS035-ADJ-QTY               03810007
      *        MOVE '+'                  TO FS035-ACTN-CDE              03820007
      *    END-IF.                                                      03830007
           MOVE GIV-FASHION-ADJ-QTY-SIGN TO FS035-ACTN-CDE.             03840007
           MOVE GIV-FASHION-ADJ-QTY      TO FS035-ADJ-QTY.              03850007
           MOVE 'GIV '                   TO FS035-BTCH-NBR.             03860007
           MOVE 'N'                      TO FS035-UNT-BKG-IND.          03870007
           MOVE 'GVKUT020'               TO FS035-ADJ-SRC-PGM-NM.       03880007
                                                                        03890003
           WRITE OUTPUT-RECORD                                          03900003
             FROM FS035-ADJUSTMENT-RECORD.                              03910003
                                                                        03920003
           ADD +1 TO PA-TOTAL-OUTPUT-RECS.                              03930003
                                                                        03940003
                                                                        03950003
      ****************************************************************  03960003
      *                                                                 03970003
      **   B E G I N     M Q    S E R I E S     P A R A G R A P H S **  03980003
      *                                                                 03990003
      ****************************************************************  04000003
                                                                        04010003
      ****************************************************************  04020003
      ** CONNECT TO THE QUEUE MANAGER                                   04030003
      ** GET THE HANDLE TO THE QUEUE MANAGER - PV-HCONN                 04040003
      ****************************************************************  04050003
       3100-MQ-CONNECT-TO-QMGR.                                         04060003
                                                                        04070003
           MOVE '3100-MQ-CONNECT-TO-QMGR' TO PV-PARAGRAPH.              04080003
      *                                                                 04090003
      * CONNECT TO THE QUEUE                                            04100003
      *                                                                 04110003
           CALL PC-MQCONN USING PV-QMGR-NAME                            04120003
                                PV-HCONN                                04130003
                                PV-COMPLETION-CODE                      04140003
                                PV-REASON.                              04150003
                                                                        04160003
      *IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE AND EXIT         04170003
           MOVE PV-REASON TO PV-CON-REASON.                             04180003
           IF PV-COMPLETION-CODE = MQCC-OK THEN                         04190003
               CONTINUE                                                 04200003
           ELSE                                                         04210003
               CALL PV-MQCHECK USING PV-REASON                          04220003
                                     PV-MQ-REASON-TEXT                  04230003
               MOVE PV-QMGR-NAME         TO PE-MQ-QMANAGER              04240003
               MOVE PV-COMPLETION-CODE   TO PE-MQ-COMPLETION-CODE       04250003
               MOVE PV-REASON            TO PE-MQ-REASON-CODE           04260003
               MOVE 'CONNECT TO QMGR'    TO PV-ERROR-MESSAGE            04270003
               SET ABEND-4020-MQ-ERROR   TO TRUE                        04280003
               PERFORM 9200-MQ-ABEND                                    04290003
           END-IF.                                                      04300003
                                                                        04310003
      ****************************************************************  04320003
      ** OPEN THE QUEUE FOR GETS.                                       04330003
      ****************************************************************  04340003
       3200-MQ-QUEUE-OPEN.                                              04350003
                                                                        04360003
           MOVE '3200-MQ-QUEUE-OPEN'     TO PV-PARAGRAPH.               04370003
                                                                        04380003
           MOVE PV-QUEUE-NAME            TO MQOD-OBJECTNAME             04390003
           MOVE PV-QMGR-NAME             TO MQOD-OBJECTQMGRNAME.        04400003
                                                                        04410003
           COMPUTE PV-OPEN-OPTIONS       = MQOO-INPUT-SHARED            04420003
                                         + MQOO-OUTPUT                  04430003
                                         + MQOO-SET-IDENTITY-CONTEXT    04440003
                                         + MQOO-FAIL-IF-QUIESCING.      04450003
                                                                        04460003
           CALL PC-MQOPEN USING PV-HCONN                                04470003
                                MQM-OBJECT-DESCRIPTOR                   04480003
                                PV-OPEN-OPTIONS                         04490003
                                PV-GQ-HANDLE                            04500003
                                PV-COMPLETION-CODE                      04510003
                                PV-REASON.                              04520003
                                                                        04530003
           IF PV-COMPLETION-CODE = MQCC-OK THEN                         04540003
               CONTINUE                                                 04550003
           ELSE                                                         04560003
               CALL PV-MQCHECK USING PV-REASON                          04570003
                                     PV-MQ-REASON-TEXT                  04580003
               MOVE PV-QMGR-NAME            TO PE-MQ-QMANAGER           04590003
               MOVE PV-QUEUE-NAME           TO PE-MQ-QNAME              04600003
               MOVE PV-COMPLETION-CODE      TO PE-MQ-COMPLETION-CODE    04610003
               MOVE PV-REASON               TO PE-MQ-REASON-CODE        04620003
               MOVE 'OPEN ITEM QUEUE'       TO PV-ERROR-MESSAGE         04630003
               SET ABEND-4020-MQ-ERROR   TO TRUE                        04640003
               PERFORM 9200-MQ-ABEND                                    04650003
           END-IF.                                                      04660003
                                                                        04670003
                                                                        04680003
      ****************************************************************  04690003
      ** GET THE NEXT MESSAGE FROM THE QUEUE.                           04700003
      ** THE QUEUE IS SET UP SO MSGS ARE RETRIEVED IN FIFO ORDER.       04710003
      ****************************************************************  04720003
       3300-MQ-GET-MSG.                                                 04730003
                                                                        04740003
           INITIALIZE PV-MSGBUFFER                                      04750003
                      PV-MSG.                                           04760003
                                                                        04770003
      *MSG ID IS NOT POPULATED IN THIS PGM.                             04780003
           MOVE MQMI-NONE TO MQMD-MSGID.                                04790003
                                                                        04800003
      *MSGS ARE UNQUALIFIED IN THIS PGM.                                04810003
           MOVE MQCI-NONE         TO MQMD-CORRELID.                     04820003
                                                                        04830003
      *FORMAT TRANSLATES THE DATA ACROSS PLATFORMS, 4K IS THE           04840003
      *MAX LENGTH FOR MSGS IN THE PGM.                                  04850003
           MOVE MQFMT-STRING         TO MQMD-FORMAT.                    04860003
           MOVE +100                 TO PV-MSG-LENGTH.                  04870008
                                                                        04880003
      *THE UNLIMITED WAIT INTERVAL MEANS THAT PROCESSING WILL JUST      04890003
      * 'WAIT' UNTIL A MESSAGE APPEARAS ON THE Q.                       04900003
      * USED FOR STARTED TRANSACTIONS BUT NOT BATCH PROCESSES           04910003
      *    MOVE MQWI-UNLIMITED       TO MQGMO-WAITINTERVAL.             04920003
                                                                        04930003
           COMPUTE MQGMO-OPTIONS  =  MQGMO-SYNCPOINT +                  04940003
                                     MQGMO-CONVERT   +                  04950003
                                     MQGMO-ACCEPT-TRUNCATED-MSG +       04960003
                                     MQGMO-NO-WAIT.                     04970003
                                                                        04980003
           CALL PC-MQGET USING PV-HCONN                                 04990003
                               PV-GQ-HANDLE                             05000003
                               MQM-MESSAGE-DESCRIPTOR                   05010003
                               MQM-GET-MESSAGE-OPTIONS                  05020003
                               PV-MSG-LENGTH                            05030003
                               PV-MSGBUFFER                             05040003
                               PV-DATA-LENGTH                           05050003
                               PV-COMPLETION-CODE                       05060003
                               PV-REASON.                               05070003
                                                                        05080003
      *IF GET FAILED THEN DISPLAY ERROR MESSAGE                         05090003
      *AND BREAK OUT OF LOOP                                            05100003
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                        05110003
               IF ((PV-REASON NOT = MQRC-NO-MSG-AVAILABLE)              05120003
                AND (PV-REASON NOT = MQRC-TRUNCATED-MSG-ACCEPTED))      05130003
                   CALL PV-MQCHECK USING PV-REASON                      05140003
                                         PV-MQ-REASON-TEXT              05150003
                 MOVE 'MQGET ERROR'          TO PV-ERROR-MESSAGE        05160003
                 MOVE '3300-MQ-GET-MSG'      TO PV-PARAGRAPH            05170003
                 MOVE PV-COMPLETION-CODE     TO PV-HOLD-COMPLETION-CODE 05180003
                 MOVE PV-REASON              TO PV-HOLD-REASON          05190003
                 PERFORM 3700-BACK-OUT-MQ                               05200003
                 MOVE PV-HOLD-COMPLETION-CODE  TO PV-COMPLETION-CODE    05210003
                                                  PE-MQ-COMPLETION-CODE 05220003
                 MOVE PV-HOLD-REASON           TO PV-REASON             05230003
                                                  PE-MQ-REASON-CODE     05240003
                 SET ABEND-4020-MQ-ERROR   TO TRUE                      05250003
                 PERFORM 9200-MQ-ABEND                                  05260003
               END-IF                                                   05270003
      * THIS MEANS A MSG WAS TRUNCATED - COMMIT TO CLEAR IT FROM QUEUE  05280003
      * BEFORE ABENDING SO THAT IT WON'T BE READ BACK IN ON A RESTART.  05290003
               IF PV-COMPLETION-CODE = +1                               05300003
                AND PV-REASON = +2079                                   05310003
                  ADD +1                     TO PA-TOTAL-MQ-GETS        05320003
                  MOVE 'MQGET TRUNCATION ERROR' TO PV-ERROR-MESSAGE     05330003
                  MOVE '3300-MQ-GET-MSG'     TO PV-PARAGRAPH            05340003
                  MOVE PV-COMPLETION-CODE    TO PE-MQ-COMPLETION-CODE   05350003
                  MOVE PV-REASON             TO PE-MQ-REASON-CODE       05360003
                  MOVE SPACES                TO PV-MQ-REASON-TEXT       05370003
                  DISPLAY 'PV-MSGBUFF:' PV-MSGBUFFER (1:PV-DATA-LENGTH) 05380003
                  DISPLAY 'PV-MSGLNGT:' PV-MSG-LENGTH                   05390003
                  DISPLAY 'PV-DATLNGT:' PV-DATA-LENGTH                  05400003
                  PERFORM 3600-MQ-COMMIT                                05410003
                  SET ABEND-4020-MQ-ERROR  TO TRUE                      05420003
                  PERFORM 9200-MQ-ABEND                                 05430003
               END-IF                                                   05440003
               IF (PV-REASON = MQRC-NO-MSG-AVAILABLE)                   05450003
                   SET PS-END-OF-INPUT TO TRUE                          05460003
               END-IF                                                   05470003
           ELSE                                                         05480003
               ADD +1                           TO PA-TOTAL-MQ-GETS     05490003
                                                                        05500003
      *PV-MSG IS AN ELEMENT THAT JUST HOLDS THE ENTIRE MESSAGE.         05510003
               MOVE PV-MSGBUFFER                TO PV-MSG               05520003
                                                                        05530003
               INITIALIZE GIV-FASHION-RECORD                            05540003
               MOVE PV-MSGBUFFER TO GIV-FASHION-RECORD                  05550003
      *********DISPLAY 'PV-MSGBUFFER = '  PV-MSGBUFFER                  05560003
           END-IF.                                                      05570003
                                                                        05580003
      ****************************************************************  05590003
      ** CLOSES THE TRANS QUEUE.                                        05600003
      ****************************************************************  05610003
       3400-MQ-QUEUE-CLOSE.                                             05620003
                                                                        05630003
           MOVE '3400-MQ-QUEUE-CLOSE'    TO PV-PARAGRAPH.               05640003
                                                                        05650003
           MOVE PV-QUEUE-NAME            TO MQOD-OBJECTNAME.            05660003
           MOVE PV-QMGR-NAME             TO MQOD-OBJECTQMGRNAME.        05670003
           CALL PC-MQCLOSE USING PV-HCONN                               05680003
                                 PV-GQ-HANDLE                           05690003
                                 MQCO-NONE                              05700003
                                 PV-COMPLETION-CODE                     05710003
                                 PV-REASON.                             05720003
                                                                        05730003
           IF PV-COMPLETION-CODE = MQCC-OK THEN                         05740003
               CONTINUE                                                 05750003
           ELSE                                                         05760003
               CALL PV-MQCHECK USING PV-REASON                          05770003
                                     PV-MQ-REASON-TEXT                  05780003
               MOVE PV-QMGR-NAME            TO PE-MQ-QMANAGER           05790003
               MOVE PV-QUEUE-NAME           TO PE-MQ-QNAME              05800003
               MOVE PV-COMPLETION-CODE      TO PE-MQ-COMPLETION-CODE    05810003
               MOVE PV-REASON               TO PE-MQ-REASON-CODE        05820003
               MOVE 'PE-MQ-CLOSE'           TO PV-ERROR-MESSAGE         05830003
               SET ABEND-4020-MQ-ERROR      TO TRUE                     05840003
               PERFORM 9200-MQ-ABEND                                    05850003
           END-IF.                                                      05860003
                                                                        05870003
                                                                        05880003
      ****************************************************************  05890003
      ** DISCONNECT FROM THE QUEUE MANAGER                              05900003
      ****************************************************************  05910003
       3500-DISCONNECT-QMGR.                                            05920003
                                                                        05930003
           MOVE '3500-DISCONNECT-QMGR'    TO PV-PARAGRAPH.              05940003
           CALL PC-MQDISC USING PV-HCONN                                05950003
                                PV-COMPLETION-CODE                      05960003
                                PV-REASON                               05970003
                                                                        05980003
           IF PV-COMPLETION-CODE = MQCC-OK THEN                         05990003
              CONTINUE                                                  06000003
           ELSE                                                         06010003
               CALL PV-MQCHECK USING PV-REASON                          06020003
                                     PV-MQ-REASON-TEXT                  06030003
              MOVE PV-QMGR-NAME            TO PE-MQ-QMANAGER            06040003
              MOVE PV-QUEUE-NAME           TO PE-MQ-QNAME               06050003
              MOVE PV-COMPLETION-CODE      TO PE-MQ-COMPLETION-CODE     06060003
              MOVE PV-REASON               TO PE-MQ-REASON-CODE         06070003
              MOVE '3500-DISCONNECT-QMGR'  TO PV-ERROR-MESSAGE          06080003
              SET ABEND-4020-MQ-ERROR      TO TRUE                      06090003
              PERFORM 9200-MQ-ABEND                                     06100003
           END-IF.                                                      06110003
                                                                        06120003
      ******************************************************************06130003
      *  COMMIT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT        06140003
      ******************************************************************06150003
       3600-MQ-COMMIT.                                                  06160003
                                                                        06170003
           CALL PC-MQCMIT USING PV-HCONN                                06180003
                                PV-COMPLETION-CODE                      06190003
                                PV-REASON.                              06200003
                                                                        06210003
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                        06220003
               CALL PV-MQCHECK USING PV-REASON                          06230003
                                     PV-MQ-REASON-TEXT                  06240003
              MOVE '3600-COMMIT-MQ'   TO PV-PARAGRAPH                   06250003
              MOVE 'MQCMIT ERROR'     TO PV-ERROR-MESSAGE               06260003
              MOVE PV-COMPLETION-CODE TO PV-HOLD-COMPLETION-CODE        06270003
              MOVE PV-REASON          TO PV-HOLD-REASON                 06280003
              PERFORM 3700-BACK-OUT-MQ                                  06290003
              MOVE PV-HOLD-COMPLETION-CODE TO PV-COMPLETION-CODE        06300003
                                              PE-MQ-COMPLETION-CODE     06310003
              MOVE PV-HOLD-REASON          TO PV-REASON                 06320003
                                              PE-MQ-REASON-CODE         06330003
              SET ABEND-4020-MQ-ERROR      TO TRUE                      06340003
              PERFORM 9200-MQ-ABEND                                     06350003
           ELSE                                                         06360003
              ADD +1 TO PA-TOTAL-MQ-COMMITS                             06370003
           END-IF.                                                      06380003
                                                                        06390003
      ******************************************************************06400003
      * BACK OUT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT       06410003
      ******************************************************************06420003
       3700-BACK-OUT-MQ.                                                06430003
                                                                        06440003
           CALL PC-MQBACK USING PV-HCONN                                06450003
                                PV-COMPLETION-CODE                      06460003
                                PV-REASON.                              06470003
                                                                        06480003
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   06490003
               CALL PV-MQCHECK USING PV-REASON                          06500003
                                     PV-MQ-REASON-TEXT                  06510003
              MOVE '3700-BACK-OUT-MQ'  TO PV-PARAGRAPH                  06520003
              MOVE 'MQBACK ERROR'      TO PV-ERROR-MESSAGE              06530003
              SET ABEND-4020-MQ-ERROR  TO TRUE                          06540003
              PERFORM 9200-MQ-ABEND                                     06550003
           END-IF.                                                      06560003
                                                                        06570003
      ****************************************************************  06580003
      * END RUN OF PROGRAM ROUTINE:                                     06590003
      * CLOSE QUEUE(S)                                                  06600003
      * DISCONNECT FROM QMGR                                            06610003
      * DISPLAY SUMMARY STATISTICS                                      06620003
      ****************************************************************  06630003
       5000-END-RUN-ROUTINE.                                            06640003
                                                                        06650003
      *CLOSE THE TRANS QUEUE.                                           06660003
           PERFORM 3400-MQ-QUEUE-CLOSE.                                 06670003
                                                                        06680003
                                                                        06690003
      *DISCONNECT FROM THE QMGR.                                        06700003
           PERFORM 3500-DISCONNECT-QMGR.                                06710003
                                                                        06720003
                                                                        06730003
      *END OF JOB ROUTINE.                                              06740003
           PERFORM 8000-EOJ-ROUTINE.                                    06750003
                                                                        06760003
      *END PROGRAM.                                                     06770003
           DISPLAY '************  END OF GVKUT020  ***************'     06780010
           STOP RUN.                                                    06790003
                                                                        06800003
                                                                        06810003
      ****************************************************************  06820003
      *                                                                 06830003
      ** E N D    O F    M Q    S E R I E S     P A R A G R A P H S **  06840003
      *                                                                 06850003
      ****************************************************************  06860003
                                                                        06870003
      ******************************************************************06880003
      * CLOSE OUTPUT FILE AND DISPLAY STATISTICS FOR THE PROGRAM.       06890003
      ******************************************************************06900003
       8000-EOJ-ROUTINE.                                                06910003
                                                                        06920003
               CLOSE OUTPUT-FILE.                                       06930003
                                                                        06940003
               DISPLAY ' ********************************************'  06950003
               DISPLAY '     * GVKUT020 SUMMARY STATISTICS *         '  06960003
               DISPLAY ' '                                              06970003
               DISPLAY '       ----------------------------          '  06980003
               DISPLAY '        GIV TO FASHION ADJUSTMENTS           '  06990003
               DISPLAY '       ----------------------------          '  07000003
               DISPLAY ' '                                              07010003
               DISPLAY ' TOTAL MESSAGES READ:     '                     07020003
                                              PA-TOTAL-MQ-GETS          07030003
               DISPLAY ' TOTAL MQ COMMITS:        '                     07040003
                                              PA-TOTAL-MQ-COMMITS       07050003
               DISPLAY ' TOTAL RECORDS WRITTEN:   '                     07060003
                                              PA-TOTAL-OUTPUT-RECS      07070003
               DISPLAY ' TOTAL ERROR RECORDS:     '                     07080003
                                              PA-TOTAL-ERROR-COUNT      07090003
               DISPLAY '********************************************'.  07100003
                                                                        07110003
                                                                        07120003
      ****************************************************************  07130003
      ** PERFORM DB2 ABEND PROCESSING                                   07140003
      ****************************************************************  07150003
       9100-DB2-ABEND.                                                  07160003
                                                                        07170003
           DISPLAY '**** ABEND *************************************'.  07180003
           DISPLAY '** DB2 ERROR **'.                                   07190003
           DISPLAY '** PROGRAM:    ', PC-CURRENT-PROGRAM-NAME.          07200003
           DISPLAY '** SQLCODE:    ', SQLCODE.                          07210003
           DISPLAY '** ABEND CODE: ', PV-ABEND-CODE.                    07220003
           DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH.                     07230003
           DISPLAY '** MESSAGE:    ', PV-ERROR-MESSAGE.                 07240003
           DISPLAY '************************************************'.  07250003
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         07260003
                                                                        07270003
      ****************************************************************  07280003
      ** PERFORM MQ-SERIES ABEND PROCESSING                             07290003
      ****************************************************************  07300003
       9200-MQ-ABEND.                                                   07310003
                                                                        07320003
           DISPLAY '**** ABEND *************************************'.  07330003
           DISPLAY '** MQSERIES ERROR **'.                              07340003
           DISPLAY '** PROGRAM:    ', PC-CURRENT-PROGRAM-NAME.          07350003
           DISPLAY '** ABEND CODE: ', PV-ABEND-CODE.                    07360003
           DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH.                     07370003
           DISPLAY '** MESSAGE:    ', PV-ERROR-MESSAGE.                 07380003
           DISPLAY '** COMP CODE:  ', PE-MQ-COMPLETION-CODE.            07390003
           DISPLAY '** RSN CODE:   ', PE-MQ-REASON-CODE.                07400003
           DISPLAY '** RSN TEXT:   ', PV-MQ-REASON-TEXT.                07410003
           DISPLAY '************************************************'.  07420003
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         07430003
                                                                        07440003
      ******************************************************************07450003
      **                      END OF PROGRAM                            07460003
      ******************************************************************07470003
