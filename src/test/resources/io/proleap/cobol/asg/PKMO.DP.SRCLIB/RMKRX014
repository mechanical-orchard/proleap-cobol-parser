/* REXX */                                                                      
/* RMKRX014 - Prepare Infopac recipient add and change transactions.  */        
/*      Match/merge candidate recipients from XREF with extracts from */        
/*      the Infopac distribution file recipient records.  Create      */        
/*      recipient add records and add report detail lines when a new  */        
/*      recipient is found on the cross reference.  Create recipient  */        
/*      change records when there is a recipient name and title on    */        
/*      the cross reference, and either the title or address has      */        
/*      changed.  Read through the xref file when getting the next    */        
/*      recipient to detect any duplicates with that key, and echo    */        
/*      duplicate detail to sysout.                                   */        
/**********************************************************************/        
ARG IPACRGN                                                                     
INFOPAC_REGION = STRIP(IPACRGN)                                                 
UPPER INFOPAC_REGION                                                            
/**********************************************************************/        
/* Set values for constants.                                          */        
/**********************************************************************/        
CURRPGM = ' RMKRX014 - '                                                        
CURRENT_PROGRAM_NAME = 'RMKRX014'                                               
XREF_FILE_END_OF_FILE = 'Y'                                                     
XREF_FILE_NOT_END_OF_FILE = 'N'                                                 
RECIP_FILE_END_OF_FILE = 'Y'                                                    
RECIP_FILE_NOT_END_OF_FILE = 'N'                                                
EOF_RECIPIENT_ID = '9999999999'                                                 
XREF_TYPE_ANALYST = 'ANALYST'                                                   
XREF_TYPE_GMM = 'GMM____'                                                       
XREF_TYPE_DMM = 'DMM____'                                                       
XREF_TYPE_BUYER = 'BUYER__'                                                     
ADD_TRANSACTION = 'A'                                                           
CHANGE_TRANSACTION = 'C'                                                        
TRAN_RECIP_HEADER = '##RECIPIENT   '                                            
TRAN_RECIP_CONTINUATION = '              '                                      
CHANGE_DETECTED = 'Y'                                                           
NO_CHANGE_DETECTED = 'N'                                                        
CC_NEW_PAGE = '1'                                                               
CC_NEXT_LINE = ' '                                                              
CC_DOUBLE_SPACE = '0'                                                           
ADD_REPORT_NUMBER = '1'                                                         
CHANGE_REPORT_NUMBER = '2'                                                      
PAGE_SIZE = 60                                                                  
KOHLS_BANNER = 'K O H L S   D E P A R T M E N T   S T O R E S'                  
                                                                                
/**********************************************************************/        
/* Initialize local variables.                                        */        
/**********************************************************************/        
XREF_FILE_EOF = XREF_FILE_NOT_END_OF_FILE                                       
RECIP_FILE_EOF = RECIP_FILE_NOT_END_OF_FILE                                     
XREF_RECIPIENT_ID = '          '                                                
RECIP_RECIPIENT_ID = '          '                                               
ADD_REPORT_PAGE_NUMBER = 0                                                      
CHANGE_REPORT_PAGE_NUMBER = 0                                                   
ADD_REPORT_CC = CC_NEW_PAGE                                                     
CHANGE_REPORT_CC = CC_NEW_PAGE                                                  
ADD_REPORT_LINE_COUNT = PAGE_SIZE                                               
CHANGE_REPORT_LINE_COUNT = PAGE_SIZE                                            
CURRENT_DATE_STANDARD = DATE('S')                                               
CURRENT_DATE = SUBSTR(CURRENT_DATE_STANDARD,5,2) || '/' ||,                     
          SUBSTR(CURRENT_DATE_STANDARD,7,2) || '/' ||,                          
          SUBSTR(CURRENT_DATE_STANDARD,1,4)                                     
CURRENT_TIME = TIME()                                                           
TITLE_LINE = '  ' || CURRENT_DATE || '  ' ||,                                   
          CURRENT_TIME ||  '                      ' ||,                         
          KOHLS_BANNER || '                     ' ||,                           
          CURRENT_PROGRAM_NAME || '-'                                           
REGION_HEADER = ,                                                               
          LEFT(' ',45) ||,                                                      
          'INFOPAC REPORT DISTRIBUTION SYSTEM - ' || INFOPAC_REGION             
ADD_REPORT_HEADER = ,                                                           
          LEFT(' ',49) || 'RECIPIENT RECORDS ADDED IN INFOPAC'                  
ADD_REPORT_COLUMN_HEADER = ,                                                    
          LEFT(' ',21) ||,                                                      
          LEFT('RECIPIENT ID',15) ||,                                           
          LEFT('RECIPIENT NAME',40) ||,                                         
          LEFT('RECIPIENT TITLE',35)                                            
CHANGE_REPORT_HEADER = ,                                                        
          LEFT(' ',48) || 'RECIPIENT RECORDS CHANGED IN INFOPAC'                
CHANGE_REPORT_COLUMN_HEADER = ,                                                 
          LEFT('RECIPIENT',10) ||,                                              
          LEFT('TITLE',6) ||,                                                   
          LEFT('ADDR1',6) ||,                                                   
          LEFT('XREF NAME',25) ||,                                              
          LEFT('RECIP NAME',25) ||,                                             
          LEFT('XREF TITLE',21) ||,                                             
          LEFT('RECIP TITLE',21) ||,                                            
          LEFT('XREF AD1',9) ||,                                                
          LEFT('RECIP AD1',9)                                                   
                                                                                
/**********************************************************************/        
/* Program mainline.                                                  */        
/**********************************************************************/        
SAY CURRPGM 'INFOPAC REGION =' INFOPAC_REGION                                   
CALL READ_XREF_RECORD                                                           
CALL READ_RECIP_RECORD                                                          
DO WHILE ((XREF_FILE_EOF = XREF_FILE_NOT_END_OF_FILE) |,                        
        (RECIP_FILE_EOF = RECIP_FILE_NOT_END_OF_FILE))                          
    SELECT                                                                      
        WHEN (XREF_RECIPIENT_ID < RECIP_RECIPIENT_ID) THEN DO                   
            IF (STRIP(XREF_RECIPIENT_TITLE) ^= '') THEN DO                      
                TRANSACTION_TYPE = ADD_TRANSACTION                              
                TRAN_RECIPIENT_ID = XREF_RECIPIENT_ID                           
                TRAN_RECIPIENT_NAME = XREF_RECIPIENT_NAME                       
                TRAN_RECIPIENT_ADDR_LINE_1 = ,                                  
                          XREF_RECIPIENT_ADDR_LINE_1                            
                TRAN_RECIPIENT_ID_SEQUENCE = ,                                  
                          SUBSTR(TRAN_RECIPIENT_ADDR_LINE_1,1,4)                
                TRAN_RECIPIENT_TITLE = XREF_RECIPIENT_TITLE                     
                TRAN_RECIPIENT_PRINTER_ID = XREF_RECIPIENT_PRINTER_ID           
                TRAN_ACCESS_CODE = XREF_ACCESS_CODE                             
                TRAN_JCL_ID = XREF_JCL_ID                                       
                CALL CREATE_RECIP_TRANS                                         
/*                                                                    */        
/* Create/write a recipient add report detail line.                   */        
/*                                                                    */        
                RECIP_ADD_RPT_LINE = ,                                          
                          LEFT(' ',21) ||,                                      
                          XREF_RECIPIENT_ID || LEFT(' ',5) ||,                  
                          XREF_RECIPIENT_NAME || LEFT(' ',5) ||,                
                          XREF_RECIPIENT_TITLE                                  
                CALL CHECK_RECIP_ADD_RPT_LINE                                   
            END                                                                 
            CALL GET_NEXT_XREF_RECIP                                            
        END                                                                     
        WHEN (XREF_RECIPIENT_ID = RECIP_RECIPIENT_ID) THEN DO                   
/*                                                                    */        
/* Always use the current recipient address when addressing changes,  */        
/* not what was managed in the source of the xref.                    */        
/*                                                                    */        
            IF (STRIP(XREF_RECIPIENT_ADDR_LINE_1) = '') THEN                    
                XREF_RECIPIENT_ADDR_LINE_1 = ,                                  
                          RECIP_RECIPIENT_ADDR_LINE_1                           
/*                                                                    */        
/* If the recipient has a name and has a name in the corporate        */        
/* directory, use that name when addressing changes.  This allows     */        
/* all store users to be managed and controlled by what is in the     */        
/* xref, and pick up any changes based on what is managed there.      */        
/*                                                                    */        
            IF (STRIP(RECIP_RECIPIENT_NAME) ^= '') THEN                         
                XREF_RECIPIENT_NAME = RECIP_RECIPIENT_NAME                      
 /*         IF (STRIP(RECIP_RECIPIENT_NAME) ^= '') THEN           */            
 /*             IF (STRIP(XREF_RECIPIENT_NAME_DIR) ^= '') THEN    */            
 /*                 XREF_RECIPIENT_NAME = RECIP_RECIPIENT_NAME    */            
/*                                                                    */        
/* Only process recipient changes where the xref has a recipient      */        
/* name and title, and where either the name, title, address or       */        
/* access has changed.                                                */        
/*                                                                    */        
            IF ((STRIP(XREF_RECIPIENT_NAME) ^= '') &,                           
                    (STRIP(XREF_RECIPIENT_TITLE) ^= '') &,                      
                    ((XREF_RECIPIENT_TITLE ^= ,                                 
                                 RECIP_RECIPIENT_TITLE) |,                      
                    (XREF_RECIPIENT_ADDR_LINE_1 ^= ,                            
                                 RECIP_RECIPIENT_ADDR_LINE_1) |,                
                    (XREF_ACCESS_CODE ^= ,                                      
                                 RECIP_ACCESS_CODE))) THEN DO                   
  /*                (XREF_RECIPIENT_NAME ^= ,                   */              
  /*                             RECIP_RECIPIENT_NAME) |,       */              
                                                                                
                TRANSACTION_TYPE = CHANGE_TRANSACTION                           
                TRAN_RECIPIENT_ID = XREF_RECIPIENT_ID                           
                IF (STRIP(RECIP_RECIPIENT_NAME) ^= '') THEN                     
                    TRAN_RECIPIENT_NAME = RECIP_RECIPIENT_NAME                  
                ELSE                                                            
                    TRAN_RECIPIENT_NAME = XREF_RECIPIENT_NAME                   
  /*            TRAN_RECIPIENT_NAME = XREF_RECIPIENT_NAME       */              
                TRAN_RECIPIENT_ADDR_LINE_1 = ,                                  
                              XREF_RECIPIENT_ADDR_LINE_1                        
                TRAN_RECIPIENT_ID_SEQUENCE = ,                                  
                          RECIP_RECIPIENT_ID_SEQUENCE                           
                TRAN_RECIPIENT_TITLE = XREF_RECIPIENT_TITLE                     
                TRAN_RECIPIENT_PRINTER_ID = XREF_RECIPIENT_PRINTER_ID           
                TRAN_ACCESS_CODE = XREF_ACCESS_CODE                             
                IF (STRIP(RECIP_JCL_ID) ^= '') THEN                             
                    TRAN_JCL_ID = RECIP_JCL_ID                                  
                ELSE                                                            
                    TRAN_JCL_ID = XREF_JCL_ID                                   
                CALL CREATE_RECIP_TRANS                                         
/*                                                                    */        
/* Create/write a recipient change report detail line.                */        
/*                                                                    */        
                IF (XREF_RECIPIENT_TITLE ^= ,                                   
                                     RECIP_RECIPIENT_TITLE) THEN                
                    TITLE_CHANGE_IND = CHANGE_DETECTED                          
                ELSE                                                            
                    TITLE_CHANGE_IND = NO_CHANGE_DETECTED                       
                IF (XREF_RECIPIENT_ADDR_LINE_1 ^= ,                             
                                     RECIP_RECIPIENT_ADDR_LINE_1) THEN          
                    ADDR_LINE_1_CHANGE_IND = CHANGE_DETECTED                    
                ELSE                                                            
                    ADDR_LINE_1_CHANGE_IND = NO_CHANGE_DETECTED                 
                RECIP_CHG_RPT_LINE = ,                                          
                          XREF_RECIPIENT_ID || '  ' ||,                         
                          TITLE_CHANGE_IND || '     ' ||,                       
                          ADDR_LINE_1_CHANGE_IND || '   ' ||,                   
                          LEFT(XREF_RECIPIENT_NAME,24) || ' ' ||,               
                          LEFT(RECIP_RECIPIENT_NAME,24) || ' ' ||,              
                          LEFT(XREF_RECIPIENT_TITLE,20) || ' ' ||,              
                          LEFT(RECIP_RECIPIENT_TITLE,20) || ' ' ||,             
                          LEFT(XREF_RECIPIENT_ADDR_LINE_1,9) ||,                
                          LEFT(RECIP_RECIPIENT_ADDR_LINE_1,9)                   
                CALL CHECK_RECIP_CHG_RPT_LINE                                   
            END                                                                 
            CALL GET_NEXT_XREF_RECIP                                            
            CALL READ_RECIP_RECORD                                              
        END                                                                     
        OTHERWISE CALL READ_RECIP_RECORD                                        
    END                                                                         
END                                                                             
                                                                                
"EXECIO 0 DISKR INP01 (FINIS"                                                   
"EXECIO 0 DISKR INP02 (FINIS"                                                   
"EXECIO 0 DISKR OUT01 (FINIS"                                                   
"EXECIO 0 DISKR RPT01 (FINIS"                                                   
"EXECIO 0 DISKR RPT02 (FINIS"                                                   
EXIT 0                                                                          
/* ----------------------------------------------------------------- */         
/* Subroutines                                                       */         
/* ----------------------------------------------------------------- */         
/* ----------------------------------------------------------------- */         
/* GET_NEXT_XREF_RECIP                                               */         
/*                                                                   */         
/* Get the next recipient on the XREF file.  Display any duplicates  */         
/* detected.                                                         */         
/* ----------------------------------------------------------------- */         
GET_NEXT_XREF_RECIP:                                                            
    CURRENT_XREF_RECIPIENT_ID = XREF_RECIPIENT_ID                               
    CALL READ_XREF_RECORD                                                       
    DO WHILE ((XREF_FILE_EOF = XREF_FILE_NOT_END_OF_FILE) &,                    
            (XREF_RECIPIENT_ID = CURRENT_XREF_RECIPIENT_ID))                    
        SAY CURRPGM 'DUPLICATE XREF RECIPIENT FOR ' ||,                         
                  XREF_RECIPIENT_ID || ' - TITLE: ' ||,                         
                  XREF_RECIPIENT_TITLE                                          
        CALL READ_XREF_RECORD                                                   
    END                                                                         
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* READ_XREF_RECORD                                                  */         
/*                                                                   */         
/* Read an Summary XREF recipient record.                            */         
/* ----------------------------------------------------------------- */         
READ_XREF_RECORD:                                                               
    "EXECIO 1 DISKR INP01"              /* read one record           */         
    IF RC = 2 THEN DO                                                           
        XREF_FILE_EOF = XREF_FILE_END_OF_FILE                                   
        XREF_RECIPIENT_ID = EOF_RECIPIENT_ID                                    
    END                                                                         
    ELSE DO                                                                     
        PARSE PULL INP01_RECORD                                                 
        XREF_RECIPIENT_ID = SUBSTR(INP01_RECORD,1,10)                           
        XREF_RECIPIENT_NAME = LEFT(SUBSTR(INP01_RECORD,11,30),35)               
        XREF_RECIPIENT_TITLE = SUBSTR(INP01_RECORD,41,35)                       
        XREF_RECIPIENT_ADDR_LINE_1 = SUBSTR(INP01_RECORD,76,35)                 
        XREF_RECIPIENT_PRINTER_ID = SUBSTR(INP01_RECORD,111,8)                  
        XREF_ACCESS_CODE = SUBSTR(INP01_RECORD,119,1)                           
        XREF_JCL_ID = SUBSTR(INP01_RECORD,120,10)                               
        XREF_RECIPIENT_NAME_DIR = LEFT(SUBSTR(INP01_RECORD,151,30),35)          
    END                                                                         
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* READ_RECIP_RECORD                                                 */         
/*                                                                   */         
/* Read an Infopac recipient extract record.                         */         
/* ----------------------------------------------------------------- */         
READ_RECIP_RECORD:                                                              
    "EXECIO 1 DISKR INP02"              /* read one record           */         
    IF RC = 2 THEN DO                                                           
        RECIP_FILE_EOF = RECIP_FILE_END_OF_FILE                                 
        RECIP_RECIPIENT_ID = EOF_RECIPIENT_ID                                   
    END                                                                         
    ELSE DO                                                                     
        PARSE PULL INP02_RECORD                                                 
        RECIP_RECIPIENT_ID = SUBSTR(INP02_RECORD,1,10)                          
        RECIP_RECIPIENT_NAME = SUBSTR(INP02_RECORD,11,35)                       
        RECIP_RECIPIENT_TITLE = SUBSTR(INP02_RECORD,46,35)                      
        RECIP_RECIPIENT_ADDR_LINE_1 = SUBSTR(INP02_RECORD,81,35)                
        RECIP_RECIPIENT_ID_SEQUENCE = SUBSTR(INP02_RECORD,116,4)                
        RECIP_ACCESS_CODE = SUBSTR(INP02_RECORD,129,1)                          
        RECIP_JCL_ID = SUBSTR(INP02_RECORD,130,10)                              
    END                                                                         
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* CREATE_RECIP_TRANS                                                */         
/*                                                                   */         
/* Create the records to support a single recipient transaction      */         
/* ----------------------------------------------------------------- */         
CREATE_RECIP_TRANS:                                                             
    INFOPAC_TRAN_RECORD = TRAN_RECIP_HEADER ||,                                 
              '01 ' || TRANSACTION_TYPE || ' ' ||,                              
              TRAN_RECIPIENT_ID || ' ' || TRAN_RECIPIENT_NAME ||,               
              ' ' || TRAN_RECIPIENT_ID_SEQUENCE                                 
    CALL WRITE_RECIP_TRAN_RECORD                                                
    INFOPAC_TRAN_RECORD = TRAN_RECIP_CONTINUATION ||,                           
              '02 ' || TRAN_RECIPIENT_TITLE || ' ' ||,                          
              TRAN_RECIPIENT_PRINTER_ID                                         
    CALL WRITE_RECIP_TRAN_RECORD                                                
    INFOPAC_TRAN_RECORD = TRAN_RECIP_CONTINUATION ||,                           
              '03 ' || TRAN_RECIPIENT_ADDR_LINE_1                               
    CALL WRITE_RECIP_TRAN_RECORD                                                
    INFOPAC_TRAN_RECORD = TRAN_RECIP_CONTINUATION ||,                           
              '07 N           ' || TRAN_ACCESS_CODE ||,                         
              '                       ' || TRAN_JCL_ID ||,                      
              '          Y'                                                     
    CALL WRITE_RECIP_TRAN_RECORD                                                
    INFOPAC_TRAN_RECORD = TRAN_RECIP_CONTINUATION ||,                           
              '08 ' || TRAN_RECIPIENT_PRINTER_ID                                
    CALL WRITE_RECIP_TRAN_RECORD                                                
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_RECIP_TRAN_RECORD                                           */         
/*                                                                   */         
/* Write an Infopac recipient add/change transaction record.         */         
/* ----------------------------------------------------------------- */         
WRITE_RECIP_TRAN_RECORD:                                                        
    OUT01_RECORD = LEFT(INFOPAC_TRAN_RECORD,80)                                 
    PUSH OUT01_RECORD                                                           
    "EXECIO 1 DISKW OUT01"                                                      
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* CHECK_RECIP_ADD_RPT_LINE                                          */         
/*                                                                   */         
/* Check pagination and write the next Infopac recipent add report   */         
/* line.                                                             */         
/* ----------------------------------------------------------------- */         
CHECK_RECIP_ADD_RPT_LINE:                                                       
    ADD_REPORT_LINE_COUNT = ADD_REPORT_LINE_COUNT + 1                           
    IF (ADD_REPORT_LINE_COUNT > PAGE_SIZE) THEN DO                              
        RECIP_ADD_RPT_LINE_SAVE = RECIP_ADD_RPT_LINE                            
        ADD_REPORT_PAGE_NUMBER = ADD_REPORT_PAGE_NUMBER + 1                     
        ADD_REPORT_CC = CC_NEW_PAGE                                             
        RECIP_ADD_RPT_LINE = TITLE_LINE || ADD_REPORT_NUMBER ||,                
                  '  PAGE ' || RIGHT(ADD_REPORT_PAGE_NUMBER,5)                  
        CALL WRITE_RECIP_ADD_RPT_LINE                                           
        ADD_REPORT_CC = CC_NEXT_LINE                                            
        RECIP_ADD_RPT_LINE = REGION_HEADER                                      
        CALL WRITE_RECIP_ADD_RPT_LINE                                           
        RECIP_ADD_RPT_LINE = ADD_REPORT_HEADER                                  
        CALL WRITE_RECIP_ADD_RPT_LINE                                           
        ADD_REPORT_CC = CC_DOUBLE_SPACE                                         
        RECIP_ADD_RPT_LINE = ADD_REPORT_COLUMN_HEADER                           
        CALL WRITE_RECIP_ADD_RPT_LINE                                           
        RECIP_ADD_RPT_LINE = RECIP_ADD_RPT_LINE_SAVE                            
        ADD_REPORT_LINE_COUNT = 7                                               
        ADD_REPORT_CC = CC_DOUBLE_SPACE                                         
    END                                                                         
    CALL WRITE_RECIP_ADD_RPT_LINE                                               
    ADD_REPORT_CC = CC_NEXT_LINE                                                
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_RECIP_ADD_RPT_LINE                                          */         
/*                                                                   */         
/* Write an Infopac recipient add report line.                       */         
/* ----------------------------------------------------------------- */         
WRITE_RECIP_ADD_RPT_LINE:                                                       
    RPT01_RECORD = ADD_REPORT_CC || LEFT(RECIP_ADD_RPT_LINE,132)                
    PUSH RPT01_RECORD                                                           
    "EXECIO 1 DISKW RPT01"                                                      
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* CHECK_RECIP_CHG_RPT_LINE                                          */         
/*                                                                   */         
/* Check pagination and write the next Infopac recipent change       */         
/* report line.                                                      */         
/* ----------------------------------------------------------------- */         
CHECK_RECIP_CHG_RPT_LINE:                                                       
    CHANGE_REPORT_LINE_COUNT = CHANGE_REPORT_LINE_COUNT + 1                     
    IF (CHANGE_REPORT_LINE_COUNT > PAGE_SIZE) THEN DO                           
        RECIP_CHG_RPT_LINE_SAVE = RECIP_CHG_RPT_LINE                            
        CHANGE_REPORT_PAGE_NUMBER = CHANGE_REPORT_PAGE_NUMBER + 1               
        CHANGE_REPORT_CC = CC_NEW_PAGE                                          
        RECIP_CHG_RPT_LINE = TITLE_LINE || CHANGE_REPORT_NUMBER ||,             
                  '  PAGE ' || RIGHT(CHANGE_REPORT_PAGE_NUMBER,5)               
        CALL WRITE_RECIP_CHG_RPT_LINE                                           
        CHANGE_REPORT_CC = CC_NEXT_LINE                                         
        RECIP_CHG_RPT_LINE = REGION_HEADER                                      
        CALL WRITE_RECIP_CHG_RPT_LINE                                           
        RECIP_CHG_RPT_LINE = CHANGE_REPORT_HEADER                               
        CALL WRITE_RECIP_CHG_RPT_LINE                                           
        CHANGE_REPORT_CC = CC_DOUBLE_SPACE                                      
        RECIP_CHG_RPT_LINE = CHANGE_REPORT_COLUMN_HEADER                        
        CALL WRITE_RECIP_CHG_RPT_LINE                                           
        RECIP_CHG_RPT_LINE = RECIP_CHG_RPT_LINE_SAVE                            
        CHANGE_REPORT_LINE_COUNT = 7                                            
        CHANGE_REPORT_CC = CC_DOUBLE_SPACE                                      
    END                                                                         
    CALL WRITE_RECIP_CHG_RPT_LINE                                               
    CHANGE_REPORT_CC = CC_NEXT_LINE                                             
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_RECIP_CHG_RPT_LINE                                          */         
/*                                                                   */         
/* Write an Infopac recipient change report line.                    */         
/* ----------------------------------------------------------------- */         
WRITE_RECIP_CHG_RPT_LINE:                                                       
    RPT02_RECORD = CHANGE_REPORT_CC || LEFT(RECIP_CHG_RPT_LINE,132)             
    PUSH RPT02_RECORD                                                           
    "EXECIO 1 DISKW RPT02"                                                      
    RETURN                                                                      
