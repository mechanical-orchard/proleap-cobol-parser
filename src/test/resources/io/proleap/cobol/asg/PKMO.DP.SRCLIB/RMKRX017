/* REXX */                                                                      
/* RMKRX017 - Prepare Infopac recipient delete transactions for       */        
/*      manually requested deletions.  Match/merge request records    */        
/*      with extracts from the Infopac distribution file recipient    */        
/*      records.  Create a recipient delete record when the requested */        
/*      recipient is found on file.  If the requested recipient does  */        
/*      not have a recipient record on file, display detail           */        
/*      information to sysout.  Match/merge with extracts from the    */        
/*      Infopac distribution file copy records.  If the recipient to  */        
/*      be deleted has any report copies defined, prepare copy delete */        
/*      records and display the detail in sysout.                     */        
/**********************************************************************/        
ARG IPACRGN                                                                     
INFOPAC_REGION = STRIP(IPACRGN)                                                 
UPPER INFOPAC_REGION                                                            
/**********************************************************************/        
/* Set values for constants.                                          */        
/**********************************************************************/        
CURRPGM = ' RMKRX017 - '                                                        
CURRENT_PROGRAM_NAME = 'RMKRX017'                                               
REQ_FILE_END_OF_FILE = 'Y'                                                      
REQ_FILE_NOT_END_OF_FILE = 'N'                                                  
RECIP_FILE_END_OF_FILE = 'Y'                                                    
RECIP_FILE_NOT_END_OF_FILE = 'N'                                                
COPY_FILE_END_OF_FILE = 'Y'                                                     
COPY_FILE_NOT_END_OF_FILE = 'N'                                                 
EOF_RECIPIENT_ID = '9999999999'                                                 
EOF_REPORT_ID = '9999999999'                                                    
EOF_HIERARCHY_CODE = '999999999999999999999999999999'                           
EOF_DISTRIB_KEY = '99999999999'                                                 
DELETE_TRANSACTION = 'D'                                                        
TRAN_RECIP_HEADER = '##RECIPIENT   '                                            
TRAN_COPY_HEADER = '##COPY        '                                             
                                                                                
/**********************************************************************/        
/* Initialize local variables.                                        */        
/**********************************************************************/        
REQ_FILE_EOF = REQ_FILE_NOT_END_OF_FILE                                         
RECIP_FILE_EOF = RECIP_FILE_NOT_END_OF_FILE                                     
COPY_FILE_EOF = COPY_FILE_NOT_END_OF_FILE                                       
TRAN_RECIPIENT_ID = '          '                                                
RECIP_RECIPIENT_ID = '          '                                               
COPY_RECIPIENT_ID = '          '                                                
                                                                                
/**********************************************************************/        
/* Program mainline.                                                  */        
/**********************************************************************/        
SAY CURRPGM 'INFOPAC REGION =' INFOPAC_REGION                                   
CALL READ_REQ_RECORD                                                            
CALL READ_RECIP_RECORD                                                          
CALL READ_COPY_RECORD                                                           
DO WHILE ((REQ_FILE_EOF = REQ_FILE_NOT_END_OF_FILE) |,                          
        (RECIP_FILE_EOF = RECIP_FILE_NOT_END_OF_FILE))                          
    SELECT                                                                      
        WHEN (REQ_RECIPIENT_ID < RECIP_RECIPIENT_ID) THEN DO                    
            SAY CURRPGM '- REQUESTED RECIPIENT DELETE IS NOT ON FILE'           
            SAY CURRPGM '-   RECIPIENT ID =' REQ_RECIPIENT_ID                   
            CALL READ_REQ_RECORD                                                
        END                                                                     
        WHEN (REQ_RECIPIENT_ID = RECIP_RECIPIENT_ID) THEN DO                    
            TRANSACTION_TYPE = DELETE_TRANSACTION                               
            TRAN_RECIPIENT_ID = REQ_RECIPIENT_ID                                
            CALL CREATE_RECIP_TRANS                                             
            DO WHILE ((COPY_FILE_EOF = COPY_FILE_NOT_END_OF_FILE) &,            
                     (REQ_RECIPIENT_ID >= COPY_RECIPIENT_ID))                   
                IF (REQ_RECIPIENT_ID = COPY_RECIPIENT_ID) THEN DO               
                    TRANSACTION_TYPE = DELETE_TRANSACTION                       
                    TRAN_REPORT_GROUP = COPY_REPORT_GROUP                       
                    TRAN_RECIPIENT_ID = COPY_RECIPIENT_ID                       
                    TRAN_COPY_TYPE = COPY_COPY_TYPE                             
                    TRAN_REPORT_ID = COPY_REPORT_ID                             
                    TRAN_HIERARCHY_CODE = COPY_HIERARCHY_CODE                   
                    CALL CREATE_COPY_DEL_TRANS                                  
                    SAY CURRPGM '- REQUESTED RECIPIENT DELETE REQUIRES',        
                              'COPY CLEANUP'                                    
                    SAY CURRPGM '-   RECIPIENT ID =' REQ_RECIPIENT_ID           
                    SAY CURRPGM '-   REPORT ID =' COPY_REPORT_ID,               
                              'REPORT GROUP =' COPY_REPORT_GROUP,               
                              'COPY TYPE =' COPY_COPY_TYPE,                     
                              'HIER =' COPY_HIERARCHY_CODE                      
                END                                                             
                CALL READ_COPY_RECORD                                           
            END                                                                 
            CALL READ_REQ_RECORD                                                
            CALL READ_RECIP_RECORD                                              
        END                                                                     
        OTHERWISE CALL READ_RECIP_RECORD                                        
    END                                                                         
END                                                                             
                                                                                
"EXECIO 0 DISKR INP01 (FINIS"                                                   
"EXECIO 0 DISKR INP02 (FINIS"                                                   
"EXECIO 0 DISKR INP03 (FINIS"                                                   
"EXECIO 0 DISKR OUT01 (FINIS"                                                   
"EXECIO 0 DISKR OUT02 (FINIS"                                                   
EXIT 0                                                                          
/* ----------------------------------------------------------------- */         
/* Subroutines                                                       */         
/* ----------------------------------------------------------------- */         
/* ----------------------------------------------------------------- */         
/* READ_REQ_RECORD                                                   */         
/*                                                                   */         
/* Read a recipient add request transaction record.                  */         
/* ----------------------------------------------------------------- */         
READ_REQ_RECORD:                                                                
    "EXECIO 1 DISKR INP01"              /* read one record           */         
    IF RC = 2 THEN DO                                                           
        REQ_FILE_EOF = REQ_FILE_END_OF_FILE                                     
        REQ_RECIPIENT_ID = EOF_RECIPIENT_ID                                     
    END                                                                         
    ELSE DO                                                                     
        PARSE PULL INP01_RECORD                                                 
        REQ_RECIPIENT_ID = LEFT(SUBSTR(INP01_RECORD,1,8),10)                    
        REQ_RECIPIENT_NAME = LEFT(SUBSTR(INP01_RECORD,9,30),35)                 
        REQ_RECIPIENT_TITLE = SUBSTR(INP01_RECORD,39,35)                        
        REQ_RECIPIENT_ADDR_LINE_1 = SUBSTR(INP01_RECORD,74,35)                  
        REQ_RECIPIENT_ID_SEQUENCE = SUBSTR(INP01_RECORD,109,4)                  
        REQ_RECIPIENT_PRINTER_ID = SUBSTR(INP01_RECORD,113,8)                   
        REQ_JCL_ID = SUBSTR(INP01_RECORD,121,10)                                
        REQ_SECURED_REGION = SUBSTR(INP01_RECORD,132,1)                         
    END                                                                         
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* READ_RECIP_RECORD                                                 */         
/*                                                                   */         
/* Read an Infopac recipient extract record.                         */         
/* ----------------------------------------------------------------- */         
READ_RECIP_RECORD:                                                              
    "EXECIO 1 DISKR INP02"              /* read one record           */         
    IF RC = 2 THEN DO                                                           
        RECIP_FILE_EOF = RECIP_FILE_END_OF_FILE                                 
        RECIP_RECIPIENT_ID = EOF_RECIPIENT_ID                                   
    END                                                                         
    ELSE DO                                                                     
        PARSE PULL INP02_RECORD                                                 
        RECIP_RECIPIENT_ID = SUBSTR(INP02_RECORD,1,10)                          
        RECIP_RECIPIENT_NAME = SUBSTR(INP02_RECORD,11,35)                       
        RECIP_RECIPIENT_TITLE = SUBSTR(INP02_RECORD,46,35)                      
        RECIP_RECIPIENT_ADDR_LINE_1 = SUBSTR(INP02_RECORD,81,35)                
        RECIP_RECIPIENT_ID_SEQUENCE = SUBSTR(INP02_RECORD,116,4)                
        RECIP_JCL_ID = SUBSTR(INP02_RECORD,130,10)                              
    END                                                                         
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* READ_COPY_RECORD                                                  */         
/*                                                                   */         
/* Read an Infopac copy extract record.                              */         
/* ----------------------------------------------------------------- */         
READ_COPY_RECORD:                                                               
    "EXECIO 1 DISKR INP03"              /* read one record           */         
    IF RC = 2 THEN DO                                                           
        COPY_FILE_EOF = COPY_FILE_END_OF_FILE                                   
        COPY_REPORT_ID = EOF_REPORT_ID                                          
        COPY_HIERARCHY_CODE = EOF_HIERARCHY_CODE                                
        COPY_DISTRIB_KEY = EOF_DISTRIB_KEY                                      
        COPY_RECIPIENT_ID = EOF_RECIPIENT_ID                                    
    END                                                                         
    ELSE DO                                                                     
        PARSE PULL INP03_RECORD                                                 
        COPY_COPY_TYPE = SUBSTR(INP03_RECORD,1,1)                               
        COPY_REPORT_ID = SUBSTR(INP03_RECORD,2,10)                              
        COPY_HIERARCHY_CODE = SUBSTR(INP03_RECORD,12,30)                        
        COPY_REPORT_GROUP = SUBSTR(INP03_RECORD,42,4)                           
        COPY_RECIPIENT_ID = SUBSTR(INP03_RECORD,46,10)                          
    END                                                                         
    COPY_KEY = COPY_REPORT_ID || COPY_HIERARCHY_CODE ||,                        
              COPY_DISTRIB_KEY || COPY_RECIPIENT_ID                             
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* CREATE_RECIP_TRANS                                                */         
/*                                                                   */         
/* Create the records to support a single recipient transaction      */         
/* ----------------------------------------------------------------- */         
CREATE_RECIP_TRANS:                                                             
    INFOPAC_TRAN_RECORD = TRAN_RECIP_HEADER ||,                                 
              '01 ' || TRANSACTION_TYPE || ' ' ||,                              
              TRAN_RECIPIENT_ID                                                 
    CALL WRITE_RECIP_TRAN_RECORD                                                
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_RECIP_TRAN_RECORD                                           */         
/*                                                                   */         
/* Write an Infopac recipient add/change transaction record.         */         
/* ----------------------------------------------------------------- */         
WRITE_RECIP_TRAN_RECORD:                                                        
    OUT01_RECORD = LEFT(INFOPAC_TRAN_RECORD,80)                                 
    PUSH OUT01_RECORD                                                           
    "EXECIO 1 DISKW OUT01"                                                      
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* CREATE_COPY_DEL_TRANS:                                            */         
/*                                                                   */         
/* Create the records to support a single copy delete transaction.   */         
/* ----------------------------------------------------------------- */         
CREATE_COPY_DEL_TRANS:                                                          
    INFOPAC_TRAN_RECORD = TRAN_COPY_HEADER ||,                                  
              '01 ' || TRANSACTION_TYPE || ' ' ||,                              
              TRAN_REPORT_GROUP || ' ' || TRAN_RECIPIENT_ID || ' ' ||,          
              TRAN_COPY_TYPE || ' ' || TRAN_REPORT_ID || ' ' ||,                
              TRAN_HIERARCHY_CODE                                               
    CALL WRITE_COPY_DEL_TRAN_RECORD                                             
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_COPY_DEL_TRAN_RECORD                                        */         
/*                                                                   */         
/* Write an Infopac copy delete transaction record.                  */         
/* ----------------------------------------------------------------- */         
WRITE_COPY_DEL_TRAN_RECORD:                                                     
    OUT02_RECORD = LEFT(INFOPAC_TRAN_RECORD,80)                                 
    PUSH OUT02_RECORD                                                           
    "EXECIO 1 DISKW OUT02"                                                      
    RETURN                                                                      
