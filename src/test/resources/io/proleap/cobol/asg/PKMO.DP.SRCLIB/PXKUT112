       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.           PXKUT112.                                          
       AUTHOR.               COGNIZANT                                          
       INSTALLATION.         KOHLS DEPARTMENT STORES.                           
       DATE-WRITTEN          08/12/2011.                                        
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * Remarks                                                                 
      * -------                                                                 
      *                                                                         
      * Title        - Preparing Where clause for Price change Unload           
      *                                                                         
      * Description  - This program Reads the date from control card            
      * It calculates two dates by adding three days with date in the           
      * control card and one day with date in the control card                  
      *                                                                         
      * Both dates used to prepare a where clause condition which is            
      * used to unload todays price change items and unload of upcomming        
      * yellow ticket items 2 days in the future                                
      *                                                                         
      * Change history                                                          
      ******************************************************************        
      * WR Number     Programmer       Date                                     
      * ----------------------------------------------------------------        
      * CRQ10428      Cognizant        12-AUG-2011                              
      * Original Version                                                        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
      *********************                                                     
       CONFIGURATION SECTION.                                                   
      *---------------------                                                    
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *--------------------                                                     
       FILE-CONTROL.                                                            
      *-------------                                                            
                                                                                
           SELECT DATE-CONTROL-CARD                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT BMCUNLOAD-OUT-FILE                                            
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT DATE-OUT-FILE                                                 
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
      *-------------                                                            
                                                                                
       FD  DATE-CONTROL-CARD                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DATE-CONTROL-RECORD               PIC X(80).                         
                                                                                
       FD  BMCUNLOAD-OUT-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  BMCUNLOAD-OUT-RECORD              PIC X(80).                         
                                                                                
       FD  DATE-OUT-FILE                                                        
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DATE-OUT-RECORD                   PIC X(80).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      * DATE ROUTINE COPY MEMBERS                                               
      ******************************************************************        
           COPY DPWS500.                                                        
                                                                                
      ******************************************************************        
      *DATE CONTROL CARD LAYOUT                                        *        
      ******************************************************************        
       01  CC-CONTROL-DATE.                                                     
           05  CC-DATE                       PIC  X(10) VALUE SPACES.           
           05  FILLER                        PIC  X(70) VALUE SPACE.            
                                                                                
      ******************************************************************        
      *DATE CONTROL CARD LAYOUT                                        *        
      ******************************************************************        
       01  CC-DATE-OUT-CARD.                                                    
           05  CC-TODAYS-DATE                PIC  X(10) VALUE SPACES.           
           05  FILLER                        PIC  X(01) VALUE SPACES.           
           05  CC-FUTURE-DATE                PIC  X(10) VALUE SPACES.           
           05  FILLER                        PIC  X(69) VALUE SPACES.           
                                                                                
      *****************************************************************         
      *  PROGRAM'S CONSTANTS                                          *         
      *****************************************************************         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME       PIC X(08)  VALUE                   
               'PXKUT112'.                                                      
                                                                                
      *****************************************************************         
      *  PROGRAM'S SWITCHES                                           *         
      *****************************************************************         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-DATE-CONTROL-SW     PIC  X(01) VALUE 'N'.              
               88  PS-DATE-CONTROL-END                  VALUE 'Y'.              
                                                                                
      *****************************************************************         
      *  PROGRAM'S VARIABLES                                          *         
      *****************************************************************         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME             PIC X(40)  VALUE SPACES.           
           05  PV-OPERATION-MSG              PIC X(60)  VALUE SPACES.           
           05  PV-DAYS-FR-EVNT-NBR-1         PIC S9(04) VALUE +3.               
           05  PV-DAYS-FR-EVNT-NBR-2         PIC S9(04) VALUE +1.               
           05  PV-DAYS-FR-EVNT-NBR           PIC S9(04) VALUE +0.               
           05  PV-DATE-FR-EVNT-NBR           PIC X(10)  VALUE SPACE.            
           05  PV-BMCUNLOAD-REC.                                                
               10  FILLER                    PIC  X(02) VALUE SPACES.           
               10  PV-BMCLOAD-REC            PIC  X(24) VALUE                   
                       'AND B.PRCHG_EFF_DTE IN ('.                              
               10  FILLER                    PIC  X(01) VALUE "'".              
               10  PV-DATE-TODAYS-UNLOAD     PIC  X(10) VALUE SPACES.           
               10  FILLER                    PIC  X(01) VALUE "'".              
               10  FILLER                    PIC  X(01) VALUE ",".              
               10  FILLER                    PIC  X(01) VALUE "'".              
               10  PV-DATE-FUTURE-UNLOAD     PIC  X(10) VALUE SPACES.           
               10  FILLER                    PIC  X(01) VALUE "'".              
               10  FILLER                    PIC  X(01) VALUE ")".              
               10  FILLER                    PIC  X(28) VALUE SPACES.           
                                                                                
      *****************************************************************         
      *  ABEND CODE AREA                                              *         
      *****************************************************************         
                                                                                
       01  ABEND-CODE                        PIC S9(04) VALUE +0 COMP.          
           88  ABEND-4001-WRITE-ERROR                   VALUE +4001.            
           88  ABEND-4002-READ-ERROR                    VALUE +4002.            
           88  ABEND-4003-CTRL-CARD-ERROR               VALUE +4003.            
           88  ABEND-4004-TABLE-ERROR                   VALUE +4004.            
           88  ABEND-4005-OPEN-ERROR                    VALUE +4005.            
           88  ABEND-4006-CLOSE-ERROR                   VALUE +4006.            
           88  ABEND-4007-SEQUENCE-ERROR                VALUE +4007.            
           88  ABEND-4008-DATA-ERROR                    VALUE +4008.            
           88  ABEND-4009-KEY-DATA-ERROR                VALUE +4009.            
           88  ABEND-4013-DB-ERROR                      VALUE +4013.            
           88  ABEND-4014-ORACLE-LOGON-ERROR            VALUE +4014.            
           88  ABEND-4019-DATE-ROUTINE-ERROR            VALUE +4019.            
           88  ABEND-4444-FORCED-ABEND                  VALUE +4444.            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      *    MAINLINE LOGIC                                              *        
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-INPUT                                           
                                                                                
           PERFORM 9900-EOJ-LOGIC.                                              
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *    LOGON TO ORACLE, AND DECLARE THE ORACLE CURSORS.            *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           DISPLAY                           '1000-INITIALIZATION'.             
                                                                                
           OPEN INPUT                         DATE-CONTROL-CARD.                
           OPEN OUTPUT                        BMCUNLOAD-OUT-FILE                
                                              DATE-OUT-FILE.                    
                                                                                
           READ DATE-CONTROL-CARD        INTO CC-CONTROL-DATE                   
               AT END                                                           
                   SET PS-DATE-CONTROL-END TO TRUE.                             
                                                                                
           IF  PS-DATE-CONTROL-END                                              
               MOVE 'DATE CONTROL CARD MISSING'                                 
                                           TO PV-OPERATION-MSG                  
               SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                           
               PERFORM Z100-ABEND                                               
           END-IF.                                                              
                                                                                
           DISPLAY PC-CURRENT-PROGRAM-NAME ' DATE CONTROL CARD:  '              
                   CC-CONTROL-DATE.                                             
                                                                                
      ******************************************************************        
      *    2000-PROCESS-INPUT                                          *        
      *      THIS IS PERFORMED ONLY VIA THE 0000-MAINLINE              *        
      *      PURPOSE: READ AND PROCESS THE PURCHASE ADJUSTMENT FILE    *        
      *               TO UPDATE THE DAT_AVL_VND_FUND TABLE             *        
      ******************************************************************        
       2000-PROCESS-INPUT.                                                      
                                                                                
           DISPLAY '2000-PROCESS-INPUT'.                                        
                                                                                
           INITIALIZE                        CC-DATE-OUT-CARD                   
           MOVE  PV-DAYS-FR-EVNT-NBR-1    TO PV-DAYS-FR-EVNT-NBR                
           PERFORM 2100-COMPUTE-FUTURE-DATE                                     
           MOVE PV-DATE-FR-EVNT-NBR       TO PV-DATE-FUTURE-UNLOAD              
                                             CC-TODAYS-DATE                     
           MOVE ZERO                      TO PV-DAYS-FR-EVNT-NBR                
                                                                                
           MOVE  PV-DAYS-FR-EVNT-NBR-2    TO PV-DAYS-FR-EVNT-NBR                
           PERFORM 2100-COMPUTE-FUTURE-DATE                                     
           MOVE PV-DATE-FR-EVNT-NBR       TO PV-DATE-TODAYS-UNLOAD              
                                             CC-FUTURE-DATE                     
           MOVE ZERO                      TO PV-DAYS-FR-EVNT-NBR                
                                                                                
           WRITE BMCUNLOAD-OUT-RECORD   FROM PV-BMCUNLOAD-REC.                  
                                                                                
           DISPLAY 'BMCUNLOAD '              PV-BMCUNLOAD-REC.                  
                                                                                
           WRITE DATE-OUT-RECORD        FROM CC-DATE-OUT-CARD.                  
                                                                                
                                                                                
      ******************************************************************        
      *    2100-COMPUTE-FUTURE-DATE                                    *        
      ******************************************************************        
       2100-COMPUTE-FUTURE-DATE.                                                
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           MOVE CC-CONTROL-DATE           TO DPG52-LK-DATE-INPUT.               
           SET DPG51-FISCAL-454-CALENDAR  TO TRUE.                              
           SET DPG52-LK-DTE-ISO-FMT       TO TRUE.                              
           SET DPG51-INCREMENT-DATE       TO TRUE                               
                                                                                
           MOVE PV-DAYS-FR-EVNT-NBR       TO DPG51-INCR-DECR-DAYS-9.            
           PERFORM 9999-DATE-ROUTINE.                                           
           MOVE DPG55-DB2-ISO-DATE-FMT    TO PV-DATE-FR-EVNT-NBR.               
                                                                                
           DISPLAY 'PV-DATE-FR-EVNT-NBR: '   PV-DATE-FR-EVNT-NBR.               
                                                                                
      ******************************************************************        
      *    9900-EOJ-LOGIC.                                                      
      *      THIS IS PERFORMED AT END OF INPUT FILE.                            
      *      PURPOSE: CLOSE INPUT FILE,                                         
      *      DISPLAY RECORD COUNTS.                                             
      ******************************************************************        
       9900-EOJ-LOGIC.                                                          
                                                                                
           DISPLAY '9900-EOJ-LOGIC'.                                            
                                                                                
           CLOSE                             DATE-CONTROL-CARD                  
                                             BMCUNLOAD-OUT-FILE.                
           DISPLAY ' '.                                                         
           DISPLAY '**------ END ' PC-CURRENT-PROGRAM-NAME ' ------**'.         
                                                                                
      ******************************************************************        
      **RUN DATE ROUTINE                                                        
      ******************************************************************        
       9999-DATE-ROUTINE.                                                       
                                                                                
           MOVE '9999-DATE-ROUTINE'       TO PV-PARAGRAPH-NAME.                 
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
                                       USING DPG51                              
                                             DPG52                              
                                             DPG53                              
                                             DPG54                              
                                             DPG55                              
                                             DPG56.                             
                                                                                
           EVALUATE TRUE                                                        
                WHEN DPG54-NO-ERROR                                             
                     CONTINUE                                                   
                WHEN OTHER                                                      
                   DISPLAY 'RETRIEVING END DATE...'                             
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *-----------------------------------------------------------              
      * Z100-ABEND                                                              
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE                      
      *-----------------------------------------------------------              
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      *    END OF SOURCE CODE FOR PXKUT112                             *        
      ******************************************************************        
