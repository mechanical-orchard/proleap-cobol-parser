       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUT473.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  09-01-00.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *    MLKUT473 -  CREATE DATA INTEGRITY TWKMCLC EXTRACT FILE.              
      * THIS PROGRAM WILL CREATE THE DATA INTEGRITY EXTRACT FILE FOR            
      * DB2 TABLE TWKMCLC.                                                      
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PROGRAMMER   DESCRIPTION                             
      * ------- ---------- ------------ --------------------------              
W02161* W02161  02/16/01   R. JANSSEN   DATA INTEGRITY ISSUE WITH TWO           
W02161*                                 MONTHS OPEN.                            
23565 * 23565   09-10-2003 ROD JANSSEN  DETAILED PURCH ADJUSTMENTS ENH.         
25554 * 25554   12-29-2003 PAUL KEBER   MARKDOWN REFINEMENT FOR PLANNING        
W26603* W26603  03-23-2004 STORE EXPANSION.  EXPANDED LRECL OF OUT01            
W26603*                    DUE TO INCREASE OF MLRD153.                          
W26603*                    ADDED 'WITH UR' TO DB2 SELECTS.                      
W26603*                    - RONNA MCDONALD                                     
W27511* W27511  12-28-2004 BROADCAST OPTIMIZATION SPLIT PLANNING                
W27511*                    - J LINDEMANN                                        
W28630* W28630  04-15-2005 LICENSE FEE SPLIT PLANNING                           
W28630*                    - S ASEN                                             
      *                                                                         
P18333*P18333   09-28-2012 ML PERFORMANCE TUNING FOR PEAK                       
P18333*                    REMOVED UNWANTED DISPLAYS.                           
P18333*                    -COGNIZANT                                           
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATA-INTEGRITY-CTL-FILE                                       
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT DATA-INTEGRITY-EXTR-FILE                                      
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  DATA-INTEGRITY-CTL-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DATA-INTEGRITY-CTL-REC      PIC X(80).                               
                                                                                
       FD  DATA-INTEGRITY-EXTR-FILE                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
W26603 01  DATA-INTEGRITY-EXTR-REC     PIC X(120).                              
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-TWKMCLC-DONE         PIC X       VALUE 'N'.                   
               88  PS-TWKMCLC-IS-DONE              VALUE 'Y'.                   
               88  PS-TWKMCLC-IS-NOT-DONE          VALUE 'N'.                   
25554      05  PS-MKDN-TTLS-INBAL-SW   PIC X       VALUE 'N'.                   
25554          88  PS-MKDN-TTLS-INBALANCE          VALUE 'Y'.                   
                                                                                
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
           05  PA-TWKMCLC-ROWS-FETCHED PIC S9(13)  VALUE ZERO.                  
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.                  
           05  PA-WORK-AMOUNT          PIC S9(11)V99 VALUE ZEROS.               
25554      05  PA-NEW-MKDN-RTL-AMT     PIC S9(11)V99 VALUE +0 COMP-3.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PREV-FSCL-MN-END-DTE PIC X(10) VALUE SPACE.                   
           05  PV-PREV-DEPT-NBR        PIC X(03) VALUE SPACE.                   
           05  PV-PREV-MAJ-CL-NBR      PIC X(02) VALUE SPACE.                   
           05  PV-PREV-CHNL-CDE        PIC X(01) VALUE SPACE.                   
           05  PV-STRT-DTE             PIC X(10).                               
           05  PV-END-DTE              PIC X(10).                               
                                                                                
       01  CD-COUNTER-DISPLAY.                                                  
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.                   
                                                                                
       01 ABEND-AREAS.                                                          
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO                   
                                                   COMP SYNC.                   
               88  AC-BAD-DATA                     VALUE +4003.                 
               88  AC-DB2-ERROR                    VALUE +4013.                 
           05  AA-ABEND-LIT            PIC X(40)   VALUE                        
                 '*****       ABEND'.                                           
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                        
                   '*****   PROGRAM: MLKUT473'.                                 
           05  AA-PARAGRAPH-LIT.                                                
               10  FILLER              PIC X(17)   VALUE                        
                   '***** PARAGRAPH: '.                                         
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.                
           05  AA-MESSAGE-LINE.                                                 
               10  FILLER              PIC X(06)   VALUE '*****'.               
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.                
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                        
                   '*****    DB2 ERROR'.                                        
           05  AA-DB2-OPERATION-LIT.                                            
               10  FILLER              PIC X(17)   VALUE                        
                   '***** OPERATION: '.                                         
               10  AA-DB2-OPERATION.                                            
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.                
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.                
                                                                                
       EJECT                                                                    
                                                                                
      *** DATA INTEGRITY CONTROL CARD                                           
           COPY MLRD110.                                                        
                                                                                
                                                                                
      *** DATA INTEGRITY TWKMCLC EXTRACT FILE                                   
           COPY MLRD153.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  DB2 ERRORS                                                             
      ******************************************************************        
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
      *  DB2 COMMUNICATION AREA                                                 
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DB2 TWKMCLC TABLE LAYOUT                                               
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE TWKMCLC                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DATE ATTRIBUTE TABLE LAYOUT                                            
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE TDTATTR                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  MERCHANDISE LEDGER CONTROL TABLE LAYOUT                                
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE TMLCNTL                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DB2 TWKMCLC CURSOR                                                     
      ******************************************************************        
           EXEC SQL                                                             
               DECLARE TWKMCLC_CURSOR CURSOR FOR                                
                SELECT B.FSCL_MN_NBR                                            
                      ,B.FSCL_MN_END_DTE                                        
                      ,A.FSCL_WK_NBR                                            
                      ,A.FSCL_WK_END_DTE                                        
                      ,DEPT_NBR                                                 
                      ,MAJ_CL_NBR                                               
                      ,CHNL_CDE                                                 
                      ,SLS_RTL_AMT                                              
                      ,MKDN_PLU_RTL_AMT                                         
                      ,MKDN_RGST_RTL_AMT                                        
                      ,MKDN_UMTCH_RTL_AMT                                       
                      ,MKDN_BYR_RTL_AMT                                         
                      ,MKDN_STR_RTL_AMT                                         
                      ,MKUP_RTL_AMT                                             
                      ,XFER_IN_RTL_AMT                                          
                      ,XFER_OUT_RTL_AMT                                         
                      ,RCPT_RTL_AMT                                             
                      ,RCPT_NET_COST_AMT                                        
                      ,PADJ_RTL_AMT                                             
                      ,PADJ_NET_COST_AMT                                        
                      ,PADJ_FRGT_COST_AMT                                       
                      ,INV_ADJ_RTL_AMT                                          
                      ,SHNK_RTL_AMT                                             
                      ,END_INV_RTL_AMT                                          
                      ,CUM_MKUP_PCT                                             
                      ,END_INV_COST_AMT                                         
                      ,SLS_COST_AMT                                             
                      ,GRO_MRGN_AMT                                             
23565                 ,PADJ_RTV_COST_AMT                                        
23565                 ,PADJ_RTV_RTL_AMT                                         
23565                 ,PADJ_DA_COST_AMT                                         
23565                 ,PADJ_DA_RTL_AMT                                          
23565                 ,PADJ_OTH_COST_AMT                                        
23565                 ,PADJ_OTH_RTL_AMT                                         
25554                 ,(MKDN_PROMO_RTL_AMT + MKDN_PRPT_RTL_AMT +                
25554                   MKDN_GLDST_RTL_AMT + MKDN_OTH_RTL_AMT +                 
25554                   MKDN_BYR_RTL_AMT + MKDN_EMP_RTL_AMT)                    
27511                 ,PADJ_BROP_COST_AMT                                       
28630                 ,LIC_FEE_COST_AMT                                         
                  FROM TWKMCLC A                                                
                      ,TDTATTR B                                                
                 WHERE (A.FSCL_WK_END_DTE                                       
                            BETWEEN :PV-STRT-DTE AND :PV-END-DTE)               
                   AND A.FSCL_WK_END_DTE = B.KY_DTE                             
***************    AND CHNL_CDE = 'A'                                           
W02161        ORDER BY  DEPT_NBR                                                
W02161                 ,MAJ_CL_NBR                                              
W02161                 ,CHNL_CDE                                                
W02161                 ,FSCL_WK_END_DTE DESC                                    
W26603        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
      ******************************************************************        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE-MLKUT473.                                                  
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-PROCESS.                                           
                                                                                
           PERFORM 8000-EOJ.                                                    
                                                                                
25554      IF PS-MKDN-TTLS-INBALANCE                                            
  |            MOVE 4095 TO RETURN-CODE                                         
  |        ELSE                                                                 
  |            MOVE 0 TO RETURN-CODE                                            
25554      END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       EJECT                                                                    
      ******************************************************************        
       1000-INITIALIZATION.                                                     
      ******************************************************************        
                                                                                
           OPEN INPUT  DATA-INTEGRITY-CTL-FILE.                                 
           OPEN OUTPUT DATA-INTEGRITY-EXTR-FILE.                                
                                                                                
           PERFORM 4000-READ-CONTROL-CARD.                                      
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*** MLKUT473 INPUT CONTROL CARD VALUES ***'                 
           DISPLAY ML110-DATA-INTEGRITY-CC-REC.                                 
                                                                                
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
           INITIALIZE PV-PROGRAM-VARIABLES.                                     
                                                                                
      *    DETERMINE RANGE OF DATES TO BE PROCESSED.                            
      *                                                                         
      *    THE START DATE WILL BE THE FIRST DAY OF THE FISCAL WEEK              
      *    INTO WHICH ML110-STRT-DTE FALLS.                                     
      *                                                                         
      *    THE END DATE WILL BE ML110-END-DTE OR THE MAX POSTING FSCL WK        
      *    DATE FROM TMLCNTL, WHICHEVER IS GREATER.  THE REASON THE END         
      *    DATE CANNOT BE GREATER THAN THE MAX POSTING FSCL WK DATE IS          
      *    THAT END INV RETAIL IS TAKEN FROM THE MOST RECENT WEEK               
      *    RETURNED BY THE TWKMCLC CURSOR. IF THE CURSOR RETURNS THE            
      *    FUTURE WEEK ROWS THAT EXIST FOR ON-ORDER, END INV RETAIL WILL        
      *    BE PICKED UP FROM THOSE ROWS.  END INV RTL WILL BE ZERO ON           
      *    THOSE ROWS BECAUSE WE ONLY RECALCULATE UP THROUGH THE MAX            
      *    POSTING FSCL WK DATE.                                                
                                                                                
           MOVE ML110-STRT-DTE         TO DTATTR-KY-DTE.                        
           PERFORM 7000-GET-FSCL-WK-BEG-DTE.                                    
           MOVE DTATTR-FSCL-MN-BEG-DTE TO PV-STRT-DTE.                          
                                                                                
           PERFORM 7050-GET-MXPSTG-FSCL-WK-DTE.                                 
           IF ML110-END-DTE > MLCNTL-MXPSTG-FSCL-WK-DTE                         
               MOVE MLCNTL-MXPSTG-FSCL-WK-DTE TO PV-END-DTE                     
           ELSE                                                                 
               MOVE ML110-END-DTE             TO PV-END-DTE                     
           END-IF.                                                              
                                                                                
           DISPLAY 'START DATE: ' PV-STRT-DTE.                                  
           DISPLAY 'END DATE:   ' PV-END-DTE.                                   
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
      *                                                                         
           PERFORM 7100-OPEN-CURSOR-TWKMCLC.                                    
                                                                                
           PERFORM 7200-FETCH-TWKMCLC.                                          
                                                                                
           PERFORM                                                              
             UNTIL PS-TWKMCLC-IS-DONE                                           
               PERFORM 5000-WRITE-EXTRACT                                       
               PERFORM 7200-FETCH-TWKMCLC                                       
           END-PERFORM.                                                         
                                                                                
           PERFORM 7300-CLOSE-CURSOR-TWKMCLC.                                   
                                                                                
                                                                                
      ******************************************************************        
       4000-READ-CONTROL-CARD.                                                  
      ******************************************************************        
      *                                                                         
           READ DATA-INTEGRITY-CTL-FILE                                         
             INTO ML110-DATA-INTEGRITY-CC-REC                                   
               AT END                                                           
                   MOVE '4000-READ-CONTROL-CARD'                                
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'CONTROL CARD IS MISSING'                               
                                       TO AA-MESSAGE                            
                   MOVE +4001          TO ABEND-CODE                            
                   PERFORM 9900-COMMON-ABEND-ROUTINE                            
           END-READ.                                                            
                                                                                
       EJECT                                                                    
      ******************************************************************        
       5000-WRITE-EXTRACT.                                                      
      ******************************************************************        
      *                                                                         
           MOVE DTATTR-FSCL-MN-END-DTE TO ML153-FSCL-MN-END-DTE.                
           MOVE DTATTR-FSCL-MN-NBR     TO ML153-FSCL-MN-NBR.                    
           MOVE WKMCLC-DEPT-NBR        TO ML153-DEPT-NBR.                       
           MOVE WKMCLC-MAJ-CL-NBR      TO ML153-MAJ-CL-NBR.                     
           MOVE '00'                   TO ML153-SUB-CL-NBR.                     
           MOVE ZEROS                  TO ML153-STR-NBR.                        
           MOVE WKMCLC-CHNL-CDE        TO ML153-CHNL-CDE.                       
027100     MOVE WKMCLC-SLS-RTL-AMT     TO ML153-SLS-RTL-AMT.                    
                                                                                
           COMPUTE PA-WORK-AMOUNT      = WKMCLC-MKDN-PLU-RTL-AMT                
                                       + WKMCLC-MKDN-RGST-RTL-AMT               
                                       + WKMCLC-MKDN-UMTCH-RTL-AMT              
                                       + WKMCLC-MKDN-BYR-RTL-AMT                
                                       + WKMCLC-MKDN-STR-RTL-AMT.               
027200     MOVE PA-WORK-AMOUNT         TO ML153-MKDN-RTL-AMT.                   
25554      IF PA-WORK-AMOUNT NOT = PA-NEW-MKDN-RTL-AMT                          
  |            DISPLAY '***'                                                    
  |            DISPLAY '*** MARKDOWN TOTALS DO NOT MATCH FOR CHANNEL COD        
  |   -                'E: ' WKMCLC-CHNL-CDE ', DEPT: ' WKMCLC-DEPT-NBR         
  |                    ', FISCAL WEEK END DATE: ' WKMCLC-FSCL-WK-END-DTE        
  |            DISPLAY '*** ORIGINAL MARKDOWN TOTALS: ' PA-WORK-AMOUNT          
  |                    '  NEW MARKDOWN TOTALS: ' PA-NEW-MKDN-RTL-AMT            
  |            SET PS-MKDN-TTLS-INBALANCE TO TRUE                               
25554      END-IF.                                                              
                                                                                
027300     MOVE WKMCLC-MKUP-RTL-AMT    TO ML153-MKUP-RTL-AMT.                   
027400     MOVE WKMCLC-XFER-IN-RTL-AMT TO ML153-XFER-IN-RTL-AMT.                
027500     MOVE WKMCLC-XFER-OUT-RTL-AMT                                         
027600                                 TO ML153-XFER-OUT-RTL-AMT.               
027700     MOVE WKMCLC-RCPT-RTL-AMT    TO ML153-RCPT-RTL-AMT.                   
           MOVE WKMCLC-RCPT-NET-COST-AMT                                        
                                       TO ML153-RCPT-NET-COST-AMT.              
23565      COMPUTE PA-WORK-AMOUNT      = WKMCLC-PADJ-RTV-RTL-AMT                
  |                                    + WKMCLC-PADJ-DA-RTL-AMT                 
  |                                    + WKMCLC-PADJ-OTH-RTL-AMT.               
  |        MOVE PA-WORK-AMOUNT         TO ML153-PADJ-RTL-AMT.                   
23565 **** MOVE WKMCLC-PADJ-RTL-AMT    TO ML153-PADJ-RTL-AMT.                   
                                                                                
23565 *    ENDING INVENTORY IS ONLY FILLED IN ON THE RECORD FOR THE             
P18333*    DISPLAY 'LIC FEE = ' WKMCLC-LIC-FEE-COST-AMT.                        
  |        COMPUTE PA-WORK-AMOUNT    = WKMCLC-PADJ-RTV-COST-AMT                 
  |                                  + WKMCLC-PADJ-DA-COST-AMT                  
  |                                  + WKMCLC-PADJ-OTH-COST-AMT                 
23565                                + WKMCLC-PADJ-FRGT-COST-AMT                
27511                                + WKMCLC-PADJ-BROP-COST-AMT                
28630                                + WKMCLC-LIC-FEE-COST-AMT.                 
23565      MOVE PA-WORK-AMOUNT         TO ML153-PADJ-NET-COST-AMT.              
  |   **** MOVE WKMCLC-PADJ-NET-COST-AMT                                        
23565 ****                             TO ML153-PADJ-NET-COST-AMT.              
                                                                                
028300     MOVE WKMCLC-INV-ADJ-RTL-AMT TO ML153-INV-ADJ-RTL-AMT.                
028400     MOVE WKMCLC-SHNK-RTL-AMT    TO ML153-SHNK-RTL-AMT.                   
028500                                                                          
028600*    ENDING INVENTORY IS ONLY FILLED IN ON THE RECORD FOR THE             
028700*    FIRST (MOST RECENT) FISCAL WEEK IN THE FISCAL WEEK.                  
028800*    (THE FISCAL WEEKS ARE PROCESSED IN DESCENDING SEQUENCE.)             
028900     IF  DTATTR-FSCL-MN-END-DTE  = PV-PREV-FSCL-MN-END-DTE                
029000     AND WKMCLC-DEPT-NBR         = PV-PREV-DEPT-NBR                       
W02161     AND WKMCLC-MAJ-CL-NBR       = PV-PREV-MAJ-CL-NBR                     
W02161     AND WKMCLC-CHNL-CDE         = PV-PREV-CHNL-CDE                       
029100         MOVE ZEROS                  TO ML153-END-INV-RTL-AMT             
029200     ELSE                                                                 
029300         MOVE WKMCLC-END-INV-RTL-AMT TO ML153-END-INV-RTL-AMT             
029400         MOVE DTATTR-FSCL-MN-END-DTE TO PV-PREV-FSCL-MN-END-DTE           
029500         MOVE WKMCLC-DEPT-NBR        TO PV-PREV-DEPT-NBR                  
W02161         MOVE WKMCLC-MAJ-CL-NBR      TO PV-PREV-MAJ-CL-NBR                
W02161         MOVE WKMCLC-CHNL-CDE        TO PV-PREV-CHNL-CDE                  
029600     END-IF.                                                              
029700                                                                          
           WRITE DATA-INTEGRITY-EXTR-REC                                        
               FROM ML153-DATA-INTEGRITY-EXTR-REC.                              
           ADD 1                       TO PA-OUTPUT-RECS-WRITTEN.               
                                                                                
       EJECT                                                                    
      ******************************************************************        
       7000-GET-FSCL-WK-BEG-DTE.                                                
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               SELECT  FSCL_MN_BEG_DTE                                          
                 INTO :DTATTR-FSCL-MN-BEG-DTE                                   
                 FROM TDTATTR                                                   
                WHERE KY_DTE = :DTATTR-KY-DTE                                   
W26603         WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7000-GET-FSCL-WK-BEG-DTE'                              
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'NO TDTATTR MATCH FOUND FOR KY_DTE'                     
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT FROM'  TO AA-DB2-OPERATION                      
                   PERFORM 9901-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
       7050-GET-MXPSTG-FSCL-WK-DTE.                                             
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
               SELECT  MXPSTG_FSCL_WK_DTE                                       
                 INTO :MLCNTL-MXPSTG-FSCL-WK-DTE                                
                 FROM TMLCNTL                                                   
W26603         WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7050-GET-MXPSTG-FSCL-WK-DTE'                           
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TMLCNTL ROW NOT FOUND'                                 
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9901-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
       7100-OPEN-CURSOR-TWKMCLC.                                                
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
               OPEN TWKMCLC_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7100-OPEN-CURSOR-TWKMCLC'                              
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TWKMCLC TABLE ERROR'                                   
                                       TO AA-MESSAGE                            
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION                      
                   PERFORM 9901-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7200-FETCH-TWKMCLC.                                                      
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
             FETCH TWKMCLC_CURSOR                                               
               INTO :DTATTR-FSCL-MN-NBR,                                        
                    :DTATTR-FSCL-MN-END-DTE,                                    
                    :WKMCLC-FSCL-WK-NBR,                                        
                    :WKMCLC-FSCL-WK-END-DTE,                                    
                    :WKMCLC-DEPT-NBR,                                           
                    :WKMCLC-MAJ-CL-NBR,                                         
                    :WKMCLC-CHNL-CDE,                                           
                    :WKMCLC-SLS-RTL-AMT,                                        
                    :WKMCLC-MKDN-PLU-RTL-AMT,                                   
                    :WKMCLC-MKDN-RGST-RTL-AMT,                                  
                    :WKMCLC-MKDN-UMTCH-RTL-AMT,                                 
                    :WKMCLC-MKDN-BYR-RTL-AMT,                                   
                    :WKMCLC-MKDN-STR-RTL-AMT,                                   
                    :WKMCLC-MKUP-RTL-AMT,                                       
                    :WKMCLC-XFER-IN-RTL-AMT,                                    
                    :WKMCLC-XFER-OUT-RTL-AMT,                                   
                    :WKMCLC-RCPT-RTL-AMT,                                       
                    :WKMCLC-RCPT-NET-COST-AMT,                                  
                    :WKMCLC-PADJ-RTL-AMT,                                       
                    :WKMCLC-PADJ-NET-COST-AMT,                                  
                    :WKMCLC-PADJ-FRGT-COST-AMT,                                 
                    :WKMCLC-INV-ADJ-RTL-AMT,                                    
                    :WKMCLC-SHNK-RTL-AMT,                                       
                    :WKMCLC-END-INV-RTL-AMT,                                    
                    :WKMCLC-CUM-MKUP-PCT,                                       
                    :WKMCLC-END-INV-COST-AMT,                                   
                    :WKMCLC-SLS-COST-AMT,                                       
23565               :WKMCLC-GRO-MRGN-AMT,                                       
23565               :WKMCLC-PADJ-RTV-COST-AMT,                                  
23565               :WKMCLC-PADJ-RTV-RTL-AMT,                                   
23565               :WKMCLC-PADJ-DA-COST-AMT,                                   
23565               :WKMCLC-PADJ-DA-RTL-AMT,                                    
23565               :WKMCLC-PADJ-OTH-COST-AMT,                                  
23565               :WKMCLC-PADJ-OTH-RTL-AMT,                                   
25554               :PA-NEW-MKDN-RTL-AMT,                                       
27511               :WKMCLC-PADJ-BROP-COST-AMT,                                 
28630               :WKMCLC-LIC-FEE-COST-AMT,                                   
W30578              :WKMCLC-MKDN-EMP-RTL-AMT                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   ADD +1              TO PA-TWKMCLC-ROWS-FETCHED               
               WHEN SQLCODE = +100                                              
                   MOVE 'Y'            TO PS-TWKMCLC-DONE                       
               WHEN OTHER                                                       
                   MOVE '7200-FETCH-TWKMCLC'                                    
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TWKMCLC TABLE ERROR'                                   
                                       TO AA-MESSAGE                            
                   MOVE 'FETCH'        TO AA-DB2-OPERATION                      
                   PERFORM 9901-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7300-CLOSE-CURSOR-TWKMCLC.                                               
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
               CLOSE TWKMCLC_CURSOR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7300-CLOSE-CURSOR-TWKMCLC'                             
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TWKMCLC TABLE ERROR'                                   
                                       TO AA-MESSAGE                            
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION                      
                   PERFORM 9901-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
       8000-EOJ.                                                                
      ******************************************************************        
      *                                                                         
           DISPLAY ' '.                                                         
           DISPLAY '  PROGRAM MLKUT473 PROCESSING SUMMARY'.                     
           DISPLAY '  -----------------------------------'.                     
           DISPLAY ' '.                                                         
      *                                                                         
           MOVE PA-TWKMCLC-ROWS-FETCHED TO CD-COUNT-HOLD1.                      
           DISPLAY 'TWKMCLC ROWS FETCHED =      ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.                       
           DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.               
      *                                                                         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE DATA-INTEGRITY-CTL-FILE.                                       
           CLOSE DATA-INTEGRITY-EXTR-FILE.                                      
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       9900-COMMON-ABEND-ROUTINE.                                               
      ******************************************************************        
      *                                                                         
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY ABEND-CODE.                                                  
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       9901-SQL-ERROR.                                                          
      ******************************************************************        
      *                                                                         
           CLOSE DATA-INTEGRITY-CTL-FILE                                        
                 DATA-INTEGRITY-EXTR-FILE.                                      
                                                                                
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-DB2-ERROR-LIT.                                            
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY AA-DB2-OPERATION-LIT.                                        
           SET AC-DB2-ERROR TO TRUE.                                            
           MOVE +4013                  TO ABEND-CODE.                           
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
