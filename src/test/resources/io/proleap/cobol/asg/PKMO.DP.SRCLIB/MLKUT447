       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              MLKUT447.                                       
       AUTHOR.                  RONNA BUTZ.                                     
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            08/27/99.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * MONTHLY ON ORDER INFORMATION FOR MERCHANDISE LEGDER                     
      *                                                                         
      * THIS PROGRAM PROCESSES THE WEEKLY EXTRACT OF PO ON ORDER DATA TO        
      * GET THE PAST DUE AND ON ORDER AMOUNTS USED IN THE COMBINED              
      * LEDGER REPORT                                                           
      *                                                                         
      * A CONTROL CARD MUST BE USED.  THERE ARE TWO DATES IN THE LAYOUT         
      * OF THE CONTROL CARD.  THE FIRST DATE IS REQUIRED AND IT IS A            
      * PROCESSING DATE.  THE SECOND DATE IS OPTIONAL AND IS A FISCAL           
      * MONTH END DATE.  THE FISCAL MONTH END DATE CAN BE USED TO               
      * OVERIDE WHAT IS IN THE ML CONTROL TABLE.  EITHER THE POSTING            
      * FISCAL MONTH END DATE OR MAX POSTING MONTH END DATE WILL BE             
      * USED FROM THE MLCNTL TABLE WHEN THE FISCAL MONTH END DATE IS            
      * NOT IN THE CONTROL CARD.                                                
      *                                                                         
      * THE PROCESSING DATE IN THE CONTROL CARD IS SET UP TO DEFAULT TO         
      * THE CURRENT SCHEDULE DATE VIA THE JOB SCHEDULER.  THAT WAY IF           
      * THE JOB NEEDS A SPECIAL RUN, YOU CAN USE ANY DATE.                      
      *                                                                         
      * THE PROCESSING DATE IS USED WHEN THIS PROGRAM RUNS DURING CUT           
      * OFF WEEK.  IT IS USED TO DETERMINE THE FSCL MONTH END DATE AND          
      * FISCAL MONTH FOR THE PAST DUE DATA ON THE OUTPUT FILE, BECAUSE          
      * AT THIS POINT IN TIME THE PAST DUE DATA BELONGS IN THE NEXT             
      * MONTH OR THE SAME MONTH AS THE PROCESSING MONTH, NOT THE OPEN           
      * FSCL MONTH END DATE.  ALL TRANSACTIONS WITH AN IN DC DATE THAT          
      * IS BEFORE THE FSCL PROCESSING MONTH END DATE ARE CONSIDERED PAST        
      * DUE FOR THE NEXT MONTH.                                                 
      *                                                                         
      * THE FISCAL MONTH END DATE IS USED TO COMPARE AGAINST THE IN DC          
      * DATE TO DETERMINE IF A TRANSACTION FALLS UNDER THE CATEGORY OF          
      * PAST DUE.  WHEN PROCESSING IS NOT DURING THE CUTOFF WEEK, THE           
      * FISCAL MONTH END DATE AND FISCAL MONTH ARE USED ON THE OUTPUT           
      * FILE FOR PAST DUE TRANSACTIONS.                                         
      *                                                                         
      * TRANSACTIONS THAT ARE NOT PAST DUE REGARDLESS OF WHEN PROCESSING        
      * OCCURS GET MAPPED TO THE OUTPUT FILE WITH A MONTH END DATE AND          
      * MONTH NUMBER THAT IS DETERMINED FROM THE IN DC DATE.                    
      *                                                                         
      *   INP01 - SORTED P.O. ON-ORDER EXTRACT FILE (PORD018)                   
      *   INP02 - CONTROL CARD DATE IN THE FORMAT CCYY-MM-DD                    
      *   OUT01 - REFORMATTED EXTRACT FILE (MLRD133) - MONTHLY DATA             
      *                                                                         
      *  RLB    02/08/00 - PERFORMANCE TUNING                                   
      *                    THE INPUT FILE IS SORTED BY ITEM NUMBER AND          
      *                    IN DC DATE.  ONLY CALL THE ROUTINE THAT GETS         
      *                    THE DATE ATTRIBUTE FOR THE IN DC DATE WHEN           
      *                    IT CHANGES.  THIS CUTS DOWN THE NUMBER OF            
      *                    CALLS TO DB2 AND IMPROVES PERFORMANCE                
      *  RLB    04/04/00 - MODIFIED THE CONTROL CARD.                           
      *                    THE CONTROL CARD CONTAINS A PROCESSING DATE          
      *                    AND A FISCAL WEEK END DATE.                          
      *                                                                         
      *  AML    06/06/00 - INCREASED ON-ORDER FILE TO 104 BYTES                 
      *                                                                         
      *  JBF    06/30/00 - CHANGED THE PROGRAM TO USE THE POSTING               
      *                    MONTH INSTEAD OF THE OPEN FISCAL MONTH WHEN          
      *                    THE FISCAL YEARS ARE NOT THE SAME.                   
31355 *                                                                         
31355 *  WR31355  11/17/2000 ROD JANSSEN                                        
31355 *    E-COMMERCE COMBINED LEDGER REPORT MODIFICATION.......                
31355 *                                                                         
W21731*                                                                         
W21731* 12/17/01  RLB  NON-INTELLIGENT SKU                                      
W21731*                TSK WILL BE GOING AWAY.  USE TSKXREF TO GET THE          
W21731*                DEPT AND CLASS FOR THE ITEM.                             
W21731*                                                                         
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES (BY WHOM)          *        
      * -------  ----------  ----------------------------------------  *        
21731A* WR21731  04-30-2003  REMOVE REFERENCE TO PO018-ONORD-DEPT-NBR  *        
      *                      PO WILL NOW CARRY SKU ONLY.  RECOMPILE    *        
      *                      TO PICKUP THE ON-ORDER LAYOUT CHANGES.    *        
      *                      THIS PROGRAM USE DEPT TO DISPLAY          *        
      *                      RENUMBERED DEPT IN SYSOUT. (V. LEE YANG)  *        
W26603* W26603   03-18-2004  STORE EXPANSION.  EXPANDED LRECL OF       *        
W26603*                      OUT01 BECAUSE OF CHANGES TO MLRD133.      *        
W26603*                      EXPANDED WORKING STORAGE VARIABLES.       *        
W26603*                      REPLACED TSTR000 WITH XMT_LOC.            *        
W26603*                      ADDED 'WITH UR' TO XOME DB2 SELECTS.      *        
W26603*                      AT THIS TIME THE REPORT IS NOT EXPANDING. *        
W26603*                      - RONNA MCDONALD                          *        
W26006* W26006   02-24-2005  REMOVE REFERENCES TO OBSOLETE STORE TABLE.*        
W26006*                      - ERIN SEARL                              *        
W26006* W26006   05-23-2005  NEED TO RECOMPILE FOR COPYBOOK PORD018    *        
W26006*                      STORE EXPANSION - RONNA MCDONALD          *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT PO-ONORDER-EXTRACT-FILE                                       
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CONTROL-CARD-FILE                                             
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT PO-ONORDER-EXTRACT-MN-FILE                                    
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PO-ONORDER-EXTRACT-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  PO-ONORDER-EXTRACT-RECORD        PIC X(104).                         
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CONTROL-CARD-RECORD              PIC X(80).                          
                                                                                
       FD  PO-ONORDER-EXTRACT-MN-FILE                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
W26603 01  PO-ONORDER-EXTRACT-MN-RECORD     PIC X(80).                          
                                                                                
31355 /***************************************************************          
       WORKING-STORAGE SECTION.                                                 
31355 ****************************************************************          
31355                                                                           
W26603 01  WS-STORE-NUM-ALPH           PIC X(05)  VALUE SPACES.                 
31355  01  WS-EDIT-STORE-NUM REDEFINES                                          
W26603     WS-STORE-NUM-ALPH           PIC 9(05).                               
31355  01  WS-STORE-NDX                PIC S9(05) COMP-3 VALUE ZEROS.           
31355                                                                           
31355  01  WS-STORE-TABLE.                                                      
W26603     05  WS-STORE-TBL-ELEMENTS   OCCURS 9999 TIMES.                       
31355          10  WS-E-BUS-LOC-IND    PIC X(01)  VALUE SPACE.                  
31355                                                                           
      ****************************************************************          
      * RECORD LAYOUT FOR INPUT FILE - PO ON-ORDER FILE                         
      ****************************************************************          
                                                                                
21731A     COPY PORD018.                                                        
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR OUTPUT LOAD FILE - WEEKLY PO ORDER FILE               
      ****************************************************************          
                                                                                
           COPY MLRD133.                                                        
                                                                                
      ****************************************************************          
      *  ERROR MESSAGE AREA FOR DB2                                             
      ****************************************************************          
                                                                                
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *    CALENDAR ROUTINE                                                     
      ******************************************************************        
           COPY DPWS500.                                                        
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR CONTROL CARD FILE.                                    
      ****************************************************************          
                                                                                
       01  PV-CONTROL-RECORD.                                                   
           05  PV-CONTROL-PROCESS-DATE      PIC X(10).                          
           05  FILLER                       PIC X(01).                          
           05  PV-WK-END-PROCESS-DATE       PIC X(10).                          
           05  FILLER                       PIC X(59).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-Y                         PIC X(01)  VALUE 'Y'.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-ONORDER-FILE-SW    PIC X      VALUE 'N'.               
               88  PS-END-OF-ONORDER-FILE              VALUE 'Y'.               
           05  PS-END-OF-CONTROL-CARDS-SW   PIC X      VALUE 'N'.               
               88  PS-END-OF-CONTROL-CARDS             VALUE 'Y'.               
W21731     05  PS-ITEM-FOUND-SW             PIC X      VALUE 'N'.               
W21731         88  PS-ITEM-FOUND                       VALUE 'Y'.               
W21731         88  PS-ITEM-NOT-FOUND                   VALUE 'N'.               
           05  PS-DATE-FOUND-SW             PIC X      VALUE 'N'.               
               88  PS-DATE-FOUND                       VALUE 'Y'.               
               88  PS-DATE-NOT-FOUND                   VALUE 'N'.               
           05  PS-CUT-OFF-WEEK-SW           PIC X      VALUE 'N'.               
               88  PS-CUT-OFF-WEEK-ON                  VALUE 'Y'.               
               88  PS-CUT-OFF-WEEK-OFF                 VALUE 'N'.               
           05  PS-END-OF-XMTLOC-CURSOR-IND  PIC X(01)  VALUE 'N'.               
               88  PS-END-OF-XMTLOC-CURSOR             VALUE 'Y'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SAVE-ONORD-ITEM-NBR       PIC S9(15)V COMP-3                  
                                                       VALUE ZERO.              
           05  PV-SAVE-IN-DC-DATE           PIC X(10)  VALUE SPACES.            
           05  PV-EXT-RTL-AMT               PIC S9(9)V99 COMP-3.                
           05  PV-EXT-CST-AMT               PIC S9(9)V99 COMP-3.                
           05  PV-DB2-OPER-ATTEMPTED        PIC X(30).                          
           05  PV-PARAGRAPH-NAME            PIC X(30).                          
           05  PV-READ-CNT                  PIC S9(15) COMP-3                   
                                                       VALUE ZERO.              
           05  PV-ERROR-CNT                 PIC S9(15) COMP-3                   
                                                       VALUE ZERO.              
           05  PV-WRITE-CNT                 PIC S9(15) COMP-3                   
                                                       VALUE ZERO.              
           05  PV-OLD-ONORD-WEEK-DATE       PIC X(10)  VALUE SPACES.            
           05  PV-FSCL-YR-NBR               PIC X(04)  VALUE SPACES.            
           05  PV-MN-END-DTE                PIC X(10)  VALUE SPACES.            
           05  PV-NEXT-YR-DTE.                                                  
               10  PV-NEXT-YR-YR            PIC X(04)  VALUE SPACES.            
               10  PV-NEXT-YR-MN            PIC X(02)  VALUE SPACES.            
               10  PV-NEXT-YR-DY            PIC X(02)  VALUE SPACES.            
           05  PV-MN-NBR                    PIC X(02)  VALUE SPACES.            
           05  PV-PROC-MN-END-DTE.                                              
               10 PV-PROC-MN-END-YR         PIC X(04)  VALUE SPACES.            
               10 FILLER                    PIC X      VALUE SPACES.            
               10 PV-PROC-MN-END-MN         PIC X(02)  VALUE SPACES.            
               10 FILLER                    PIC X      VALUE SPACES.            
               10 PV-PROC-MN-END-DY         PIC X(02)  VALUE SPACES.            
                                                                                
           05  PV-PROC-MN-NBR               PIC X(02)  VALUE SPACES.            
                                                                                
           05  PV-SYSTEM-DATE.                                                  
               10  PV-SYSTEM-YY           PIC 9(02) VALUE ZEROES.               
               10  PV-SYSTEM-MM           PIC 9(02) VALUE ZEROES.               
               10  PV-SYSTEM-DD           PIC 9(02) VALUE ZEROES.               
                                                                                
           05  PV-SYSTEM-DATE-ISO-FMT.                                          
               10  PV-SYSTEM-FMT-CC       PIC X(02) VALUE SPACES.               
               10  PV-SYSTEM-FMT-YY       PIC X(02) VALUE SPACES.               
               10  FILLER                 PIC X(01) VALUE '-'.                  
               10  PV-SYSTEM-FMT-MM       PIC X(02) VALUE SPACES.               
               10  FILLER                 PIC X(01) VALUE '-'.                  
               10  PV-SYSTEM-FMT-DD       PIC X(02) VALUE SPACES.               
                                                                                
W21731     05  PV-SKXREF-DEPT-C             PIC X(03)  VALUE SPACES.            
                                                                                
W26603*    05  PV-NULL-INDICATOR            PIC S9(04) COMP SYNC                
W26603*                                                VALUE ZERO.              
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP                     
                                                       VALUE ZERO.              
                                                                                
      ****************************************************************          
      * MERCHANDISE LEDGER CONTROL TABLE                                        
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
31355 ****************************************************************          
  |   * STORE TABLE DESCRIPTION.                                                
  |   ****************************************************************          
  |                                                                             
31355      EXEC SQL                                                             
W26603         INCLUDE XMT00001                                                 
31355      END-EXEC.                                                            
31355                                                                           
      ****************************************************************          
      * DATE ATTRIBUTE TABLE                                                    
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * SKU TABLE                                                               
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
W21731         INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
31355 ******************************************************************        
W26603*  SQL DB2 XMT_LOC CURSOR DECLARATION                                     
31355 ******************************************************************        
31355      EXEC SQL                                                             
W26603         DECLARE XMT_LOC_CSR CURSOR FOR                                   
W26603             SELECT LOC_NBR                                               
W26603                   ,VALUE(E_BUS_IND,'N')                                  
W26603             FROM XMT_LOC                                                 
W26603             WHERE LOC_TYP_CDE <> 'OP'                                    
W26603             ORDER BY LOC_NBR                                             
W26603             FOR FETCH ONLY                                               
W26603             WITH UR                                                      
31355      END-EXEC.                                                            
31355                                                                           
      ****************************************************************          
      *  COMMUNICATION AREAS FOR DB2                                            
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       1000-MAIN-MODULE.                                                        
                                                                                
           OPEN INPUT  PO-ONORDER-EXTRACT-FILE                                  
                       CONTROL-CARD-FILE                                        
                OUTPUT PO-ONORDER-EXTRACT-MN-FILE.                              
                                                                                
           INITIALIZE PV-PROGRAM-VARIABLES.                                     
                                                                                
      *    GET CURRENT SYSTEM DATE FOR EDITING OF PROCESSING DATE               
           ACCEPT PV-SYSTEM-DATE FROM DATE.                                     
           MOVE PV-SYSTEM-YY        TO PV-SYSTEM-FMT-YY.                        
           MOVE PV-SYSTEM-MM        TO PV-SYSTEM-FMT-MM.                        
           MOVE PV-SYSTEM-DD        TO PV-SYSTEM-FMT-DD.                        
                                                                                
           IF PV-SYSTEM-YY > '80'                                               
              MOVE '19' TO PV-SYSTEM-FMT-CC                                     
           ELSE                                                                 
              MOVE '20' TO PV-SYSTEM-FMT-CC                                     
           END-IF.                                                              
                                                                                
           DISPLAY '** SYSTEM DATE: ' PV-SYSTEM-DATE-ISO-FMT.                   
                                                                                
      *    GET THE PROCESSING DATE AND MONTH END DATE                           
           PERFORM 1100-READ-EDIT-CONTROL-CARD.                                 
           PERFORM 1200-VALIDATE-PROCESS-DATE.                                  
                                                                                
31355 *    LOAD THE WORKING STORAGE STORE TABLE WITH INFORMATION.               
  |        INITIALIZE WS-STORE-TABLE.                                           
31355      PERFORM 4000-OPEN-CURSOR.                                            
W26603     PERFORM 4100-PROCESS-XMT-LOC-CSR.                                    
31355      PERFORM 4200-CLOSE-CURSOR.                                           
31355                                                                           
      *    GET PROCESS DATE'S FSCL MONTH END DATE                               
           MOVE PV-CONTROL-PROCESS-DATE TO DTATTR-KY-DTE.                       
           PERFORM 8200-GET-DATE-ATTRIBUTES.                                    
           IF  PS-DATE-FOUND                                                    
               MOVE DTATTR-FSCL-MN-END-DTE TO PV-PROC-MN-END-DTE                
               MOVE DTATTR-FSCL-MN-NBR     TO PV-PROC-MN-NBR                    
           ELSE                                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM MLKUT447 **'                                 
               DISPLAY '** PARAGRAPH 1000-MAIN-MODULE '                         
               DISPLAY '** CONTROL PROCESS DATE NOT FOUND ON   '                
               DISPLAY '** DATE ATTRIBUTE TABLE.'                               
               MOVE +4003 TO ABEND-CODE                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
      *    GET THE FSCL MONTH EXTRACT DATE                                      
           IF PV-WK-END-PROCESS-DATE = SPACE                                    
               PERFORM 1170-USE-ML-CNTL-DTES                                    
           ELSE                                                                 
               PERFORM 1160-USE-CONTROL-CARD-DTE                                
           END-IF.                                                              
                                                                                
           DISPLAY '** SELECTED EXTRACT DATE: ' PV-MN-END-DTE.                  
                                                                                
           IF PV-CONTROL-PROCESS-DATE <= PV-MN-END-DTE                          
              MOVE 'N' TO PS-CUT-OFF-WEEK-SW                                    
           ELSE                                                                 
              MOVE 'Y' TO PS-CUT-OFF-WEEK-SW.                                   
      *                                                                         
           PERFORM 7100-READ-ONORDER-FILE.                                      
                                                                                
           PERFORM 2000-PROCESS-EXTRACT-DATA                                    
               UNTIL PS-END-OF-ONORDER-FILE.                                    
                                                                                
           SUBTRACT 1 FROM PV-READ-CNT.                                         
           DISPLAY  'INP01 RECORDS READ       ' PV-READ-CNT.                    
           DISPLAY  'OUT01 RECORDS WRITTEN    ' PV-WRITE-CNT.                   
           DISPLAY  'ERROR RECS WRITTEN TO SYSOUT(ITEM NBR NOT FOUND) ',        
                                                PV-ERROR-CNT.                   
                                                                                
           CLOSE PO-ONORDER-EXTRACT-FILE                                        
                 CONTROL-CARD-FILE                                              
                 PO-ONORDER-EXTRACT-MN-FILE.                                    
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1100-READ-EDIT-CONTROL-CARD.                                             
                                                                                
           PERFORM 7000-READ-CONTROL-CARD-FILE.                                 
                                                                                
      *    CHECK THAT THE CONTROL CARD IS A VALIDE DATE                         
           IF PS-END-OF-CONTROL-CARDS                                           
           OR PV-CONTROL-PROCESS-DATE = SPACE                                   
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM MLKUT447 **'                                 
               DISPLAY '** PARAGRAPH 1100-READ-EDIT-CONTROL-CARD '              
               DISPLAY '** CONTROL CARD DATE EMPTY             '                
               MOVE +4002 TO ABEND-CODE                                         
               PERFORM 9999-ABEND                                               
           ELSE                                                                 
               DISPLAY '** INPUT CONTROL CARD: ' PV-CONTROL-RECORD              
           END-IF.                                                              
                                                                                
       1160-USE-CONTROL-CARD-DTE.                                               
                                                                                
           DISPLAY '** USING CONTROL CARD DATE FOR EXTRACT DATE.'               
                                                                                
           MOVE PV-WK-END-PROCESS-DATE TO DTATTR-KY-DTE.                        
                                                                                
           PERFORM 8200-GET-DATE-ATTRIBUTES.                                    
                                                                                
           IF  PS-DATE-FOUND                                                    
           AND DTATTR-KY-DTE = DTATTR-FSCL-WK-END-DTE                           
               MOVE DTATTR-FSCL-MN-END-DTE TO PV-MN-END-DTE                     
               MOVE DTATTR-FSCL-MN-NBR     TO PV-MN-NBR                         
           ELSE                                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM MLKUT447 **'                                 
               DISPLAY '** PARAGRAPH 1160-USE-CONTROL-CARD-DTE'                 
               DISPLAY '** CONTROL CARD MN-END-PROCESS-DATE NOT A VALID'        
               DISPLAY '** FISCAL WEEK END DATE.'                               
               MOVE +4003 TO ABEND-CODE                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
                                                                                
       1170-USE-ML-CNTL-DTES.                                                   
                                                                                
           PERFORM 8000-GET-ML-CNTL-DTES.                                       
                                                                                
           DISPLAY                                                              
               '** POSTING FISCAL MONTH DATE: ' MLCNTL-PSTG-FSCL-MN-DTE.        
           DISPLAY                                                              
               '** MAX POSTING MONTH DATE: ' MLCNTL-MXPSTG-FSCL-MN-DTE.         
                                                                                
           MOVE MLCNTL-MXPSTG-FSCL-MN-DTE TO DTATTR-KY-DTE.                     
                                                                                
           PERFORM 8200-GET-DATE-ATTRIBUTES.                                    
                                                                                
           IF PS-DATE-NOT-FOUND                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM MLKUT447 **'                                 
               DISPLAY '** PARAGRAPH 1170-USE-ML-CNTL-DTE'                      
               DISPLAY '** MAX POSTING DATE IS NOT A VALID DATE.'               
               MOVE +4008 TO ABEND-CODE                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           MOVE DTATTR-FSCL-YR-NBR        TO PV-FSCL-YR-NBR.                    
           MOVE DTATTR-FSCL-MN-END-DTE    TO PV-MN-END-DTE.                     
           MOVE DTATTR-FSCL-MN-NBR        TO PV-MN-NBR.                         
                                                                                
           MOVE MLCNTL-PSTG-FSCL-MN-DTE TO DTATTR-KY-DTE.                       
                                                                                
           PERFORM 8200-GET-DATE-ATTRIBUTES.                                    
                                                                                
           IF PS-DATE-NOT-FOUND                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM MLKUT447 **'                                 
               DISPLAY '** PARAGRAPH 1170-USE-ML-CNTL-DTE'                      
               DISPLAY                                                          
               '** POSTING FISCAL MONTH DATE IS NOT A VALID DATE.'              
               MOVE +4008 TO ABEND-CODE                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           IF DTATTR-FSCL-YR-NBR NOT = PV-FSCL-YR-NBR                           
             DISPLAY '** USING POSTING FISCAL MONTH FOR EXTRACT DATE.'          
             MOVE DTATTR-FSCL-MN-END-DTE    TO PV-MN-END-DTE                    
             MOVE DTATTR-FSCL-MN-NBR        TO PV-MN-NBR                        
           ELSE                                                                 
               DISPLAY '** USING MAX POSTING MONTH FOR EXTRACT DATE.'           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * CALL THE CALENDAR ROUTINE USING THE CONTROL CARD PROCESS DATE           
      * TO VALIDATE IT IS A VALID DATE.                                         
      ******************************************************************        
       1200-VALIDATE-PROCESS-DATE.                                              
                                                                                
           INITIALIZE DPG51                                                     
                      DPG52                                                     
                      DPG53                                                     
                      DPG54                                                     
                      DPG55                                                     
                      DPG56.                                                    
                                                                                
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
                                                                                
           MOVE PV-SYSTEM-DATE           TO DPG51-SYSTEM-DATE.                  
           MOVE PV-CONTROL-PROCESS-DATE  TO DPG52-LK-DATE-INPUT.                
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE  USING DPG51                       
                                                    DPG52                       
                                                    DPG53                       
                                                    DPG54                       
                                                    DPG55                       
                                                    DPG56.                      
                                                                                
                                                                                
           IF DPG54-NO-ERROR                                                    
               DISPLAY '** PROCESSING DATE: ' PV-CONTROL-PROCESS-DATE           
           ELSE                                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM MLKUT447 **'                                 
               DISPLAY '** PARAGRAPH = 1200-VALIDATE-PROCESS-DATE'              
               DISPLAY '** BAD CALL TO CALENDAR ROUTINE USING '                 
                           PV-CONTROL-PROCESS-DATE                              
               DISPLAY DPG54-ERROR-MESSAGE                                      
               MOVE +4019 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
                                                                                
       2000-PROCESS-EXTRACT-DATA.                                               
                                                                                
           INITIALIZE ML133-PO-EXTRACT-MN-REC.                                  
                                                                                
           IF PV-SAVE-ONORD-ITEM-NBR NOT = PO018-ONORD-ITEM-NBR                 
W21731         MOVE 'N'                  TO PS-ITEM-FOUND-SW                    
W21731         MOVE PO018-ONORD-ITEM-NBR TO SKXREF-ITM-NBR                      
                                            PV-SAVE-ONORD-ITEM-NBR              
W21731         PERFORM 8100-SELECT-TSKXREF-SKU                                  
           ELSE                                                                 
W21731         MOVE 'Y'                  TO PS-ITEM-FOUND-SW                    
           END-IF.                                                              
                                                                                
W21731     IF PS-ITEM-FOUND                                                     
               PERFORM 3000-MAP-AND-WRITE-REC                                   
           ELSE                                                                 
               PERFORM 7300-WRITE-ONORDER-ERROR                                 
           END-IF                                                               
                                                                                
           PERFORM 7100-READ-ONORDER-FILE.                                      
                                                                                
                                                                                
       3000-MAP-AND-WRITE-REC.                                                  
                                                                                
           IF PO018-ONORD-IN-DC-DATE NOT = PV-SAVE-IN-DC-DATE                   
               MOVE PO018-ONORD-IN-DC-DATE TO DTATTR-KY-DTE                     
                                              PV-SAVE-IN-DC-DATE                
               PERFORM 8200-GET-DATE-ATTRIBUTES                                 
           END-IF.                                                              
                                                                                
           IF PS-DATE-FOUND                                                     
W21731         MOVE PV-SKXREF-DEPT-C                                            
                                   TO ML133-DEPT-NBR                            
31355          MOVE PO018-ONORD-STORE-NBR                                       
31355                              TO ML133-STR-NBR                             
31355          PERFORM 4300-STORE-TABLE                                         
               IF PS-CUT-OFF-WEEK-ON                                            
                   IF PO018-ONORD-IN-DC-DATE <= PV-MN-END-DTE                   
                       MOVE PV-PROC-MN-END-DTE                                  
                                   TO ML133-FSCL-MN-END-DTE                     
                       MOVE PV-PROC-MN-NBR                                      
                                   TO ML133-FSCL-MN-NBR                         
                       MOVE PO018-ONORD-RTL-AMT                                 
                                   TO ML133-PAST-DUE-RTL-AMT                    
                       MOVE PO018-ONORD-COST-AMT                                
                                   TO ML133-PAST-DUE-CST-AMT                    
                   ELSE                                                         
                       MOVE DTATTR-FSCL-MN-END-DTE                              
                                   TO ML133-FSCL-MN-END-DTE                     
                       MOVE DTATTR-FSCL-MN-NBR                                  
                                   TO ML133-FSCL-MN-NBR                         
                       MOVE PO018-ONORD-RTL-AMT                                 
                                   TO ML133-ONORD-RTL-AMT                       
                       MOVE PO018-ONORD-COST-AMT                                
                                   TO ML133-ONORD-CST-AMT                       
                   END-IF                                                       
               ELSE                                                             
                   IF DTATTR-FSCL-MN-END-DTE < PV-MN-END-DTE                    
                       MOVE PV-MN-END-DTE                                       
                                   TO ML133-FSCL-MN-END-DTE                     
                       MOVE PV-MN-NBR                                           
                                   TO ML133-FSCL-MN-NBR                         
                       MOVE PO018-ONORD-RTL-AMT                                 
                                   TO ML133-PAST-DUE-RTL-AMT                    
                       MOVE PO018-ONORD-COST-AMT                                
                                   TO ML133-PAST-DUE-CST-AMT                    
                   ELSE                                                         
                       MOVE DTATTR-FSCL-MN-END-DTE                              
                                   TO ML133-FSCL-MN-END-DTE                     
                       MOVE DTATTR-FSCL-MN-NBR                                  
                                   TO ML133-FSCL-MN-NBR                         
                       MOVE PO018-ONORD-RTL-AMT                                 
                                   TO ML133-ONORD-RTL-AMT                       
                       MOVE PO018-ONORD-COST-AMT                                
                                   TO ML133-ONORD-CST-AMT                       
                   END-IF                                                       
               END-IF                                                           
               PERFORM 7200-WRITE-ONORDER-EXTRACT                               
           ELSE                                                                 
               DISPLAY '** PARAGRAPH 3000-MAP-AND-WRITE-REC  '                  
               DISPLAY                                                          
                   '** ONORDER WEEK DATE NOT FOUND IN THE DTATTR '              
               DISPLAY                                                          
                   '** TABLE - THE FOLLOWING RECORD WAS DROPPED...'             
               PERFORM 7300-WRITE-ONORDER-ERROR                                 
           END-IF.                                                              
                                                                                
31355 ******************************************************************        
  |   * 4000-OPEN THE STORE TABLE CURSOR.                                       
  |   ******************************************************************        
  |    4000-OPEN-CURSOR.                                                        
  |                                                                             
31355      EXEC SQL                                                             
W26603         OPEN XMT_LOC_CSR                                                 
31355      END-EXEC.                                                            
  |                                                                             
  |        EVALUATE SQLCODE                                                     
  |            WHEN +0                                                          
  |                CONTINUE                                                     
31355          WHEN OTHER                                                       
W26603             MOVE 'OPEN XMT_LOC CURSOR' TO                                
31355                  PV-DB2-OPER-ATTEMPTED                                    
  |                PERFORM 9998-SQL-ABEND                                       
  |        END-EVALUATE.                                                        
  |                                                                             
  |   /*****************************************************************        
  |   * 4100-PROCESS THE STORE TABLE CURSOR, LOAD STORE DC INFORMATION.         
31355 ******************************************************************        
W26603 4100-PROCESS-XMT-LOC-CSR.                                                
31355                                                                           
W26603     PERFORM 4400-FETCH-XMT-LOC-TABLE.                                    
31355                                                                           
  |        PERFORM 4500-LOAD-WS-STORE-TABLE                                     
  |               UNTIL PS-END-OF-XMTLOC-CURSOR.                                
  |                                                                             
  |   ******************************************************************        
  |   * 4200-CLOSE THE STORE TABLE CURSOR.                                      
  |   ******************************************************************        
  |    4200-CLOSE-CURSOR.                                                       
  |                                                                             
31355      EXEC SQL                                                             
W26603         CLOSE XMT_LOC_CSR                                                
31355      END-EXEC.                                                            
  |                                                                             
  |        EVALUATE SQLCODE                                                     
  |            WHEN +0                                                          
  |                CONTINUE                                                     
  |            WHEN OTHER                                                       
  |                MOVE 'CLOSE TINVPAR CURSOR' TO                               
  |                    PV-DB2-OPER-ATTEMPTED                                    
  |                PERFORM 9998-SQL-ABEND                                       
  |        END-EVALUATE.                                                        
  |                                                                             
  |   /****************************************************************         
  |   * 4300-STORE-TABLE.                                                       
  |   *     THE ROUTINE DETERMINES WHAT TYPE OF STORE WE ARE WORKING            
  |   * WITH. (PHYSICAL STORE OR VIRTUAL STORE) TAKE THE STORE NUMBER           
  |   * COMING IN FROM PURCHASING. CHECK THE STORE TABLE E BUSINESS             
  |   * INDICATOR AND MOVE THE RESULTS TO THE TAMPORARY RECORD FOR              
  |   * THE COMBINED LEDGER.                                                    
  |   *****************************************************************         
  |    4300-STORE-TABLE.                                                        
  |                                                                             
  |        MOVE PO018-ONORD-STORE-NBR TO WS-STORE-NDX.                          
  |                                                                             
  |        MOVE WS-E-BUS-LOC-IND (WS-STORE-NDX)                                 
  |                                   TO ML133-E-BUS-LOC-IND.                   
  |                                                                             
  |        IF ML133-E-BUS-LOC-IND NOT EQUAL 'Y' AND 'N'                         
  |            MOVE 'N'               TO ML133-E-BUS-LOC-IND                    
  |        END-IF.                                                              
  |                                                                             
  |   ******************************************************************        
  |   * 4400-FETCH A STORE TABLE CURSOR ROW.                                    
31355 ******************************************************************        
W26603 4400-FETCH-XMT-LOC-TABLE.                                                
31355                                                                           
W26603     MOVE 0 TO XMT00001-LOC-NBR.                                          
W26603     MOVE SPACE TO XMT00001-E-BUS-IND.                                    
W26603                                                                          
31355      EXEC SQL                                                             
W26603         FETCH XMT_LOC_CSR                                                
W26603         INTO :XMT00001-LOC-NBR                                           
W26603             ,:XMT00001-E-BUS-IND                                         
31355      END-EXEC.                                                            
  |                                                                             
  |        EVALUATE SQLCODE                                                     
31355          WHEN +0                                                          
31355             CONTINUE                                                      
W26603*           IF PV-NULL-INDICATOR < 0                                      
W26603*              MOVE 'N'     TO XMT00001-E-BUS-IND                         
W26603*           END-IF                                                        
31355          WHEN +100                                                        
  |                SET PS-END-OF-XMTLOC-CURSOR TO TRUE                          
31355          WHEN OTHER                                                       
W26603             MOVE '4400-FETCH-XMT-LOC-TABLE'                              
31355                  TO PV-PARAGRAPH-NAME                                     
W26603             MOVE 'FETCH XMT_LOC RECORD' TO                               
31355                  PV-DB2-OPER-ATTEMPTED                                    
  |                PERFORM 9998-SQL-ABEND                                       
  |        END-EVALUATE.                                                        
  |                                                                             
  |   ******************************************************************        
  |   * 4500-LOAD-WS-STORE-TABLE.                                               
  |   *  LOAD THE STORE DATA INTO THE WORKING STORAGE TABLE.....                
  |   ******************************************************************        
  |    4500-LOAD-WS-STORE-TABLE.                                                
31355                                                                           
W26603     MOVE XMT00001-LOC-NBR        TO WS-STORE-NDX.                        
  |                                                                             
W26603     MOVE XMT00001-E-BUS-IND                                              
31355                            TO WS-E-BUS-LOC-IND (WS-STORE-NDX).            
31355                                                                           
W26603     PERFORM 4400-FETCH-XMT-LOC-TABLE.                                    
31355                                                                           
       7000-READ-CONTROL-CARD-FILE.                                             
                                                                                
           READ CONTROL-CARD-FILE INTO PV-CONTROL-RECORD                        
               AT END SET PS-END-OF-CONTROL-CARDS TO TRUE.                      
                                                                                
                                                                                
       7100-READ-ONORDER-FILE.                                                  
                                                                                
           READ PO-ONORDER-EXTRACT-FILE INTO PO018-ONORD-EXTRACT-RECORD         
               AT END SET PS-END-OF-ONORDER-FILE  TO TRUE.                      
                                                                                
           ADD 1 TO PV-READ-CNT.                                                
                                                                                
                                                                                
       7200-WRITE-ONORDER-EXTRACT.                                              
                                                                                
           WRITE PO-ONORDER-EXTRACT-MN-RECORD                                   
               FROM ML133-PO-EXTRACT-MN-REC.                                    
           ADD 1 TO PV-WRITE-CNT.                                               
                                                                                
                                                                                
       7300-WRITE-ONORDER-ERROR.                                                
                                                                                
           DISPLAY PO018-ONORD-EXTRACT-RECORD.                                  
           ADD 1 TO PV-ERROR-CNT.                                               
                                                                                
                                                                                
       8000-GET-ML-CNTL-DTES.                                                   
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    PSTG_FSCL_MN_DTE                                            
                   ,MXPSTG_FSCL_MN_DTE                                          
                INTO                                                            
                    :MLCNTL-PSTG-FSCL-MN-DTE                                    
                   ,:MLCNTL-MXPSTG-FSCL-MN-DTE                                  
                FROM                                                            
                    TMLCNTL                                                     
W26603          WITH UR                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8000-GET-ML-CNTL-DTES' TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT ROW FROM TMLCNTL'                               
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
W21731 8100-SELECT-TSKXREF-SKU.                                                 
                                                                                
           EXEC SQL                                                             
W21731         SELECT SUBSTR(DIGITS(DEPT),2,3)                                  
W21731         INTO  :PV-SKXREF-DEPT-C                                          
W21731         FROM TSKXREF                                                     
W21731         WHERE ITM_NBR = :SKXREF-ITM-NBR                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
W21731             MOVE 'Y'   TO PS-ITEM-FOUND-SW                               
               WHEN SQLCODE = +100                                              
W21731             MOVE 'N'   TO PS-ITEM-FOUND-SW                               
               WHEN OTHER                                                       
W21731             MOVE '8100-SELECT-TSKXREF-SKU'                               
                                                TO PV-PARAGRAPH-NAME            
W21731             MOVE 'SELECT TSKXREF TABLE'  TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       8200-GET-DATE-ATTRIBUTES.                                                
                                                                                
           EXEC SQL                                                             
               SELECT FSCL_YR_NBR                                               
                     ,FSCL_MN_NBR                                               
                     ,FSCL_MN_END_DTE                                           
                     ,FSCL_WK_END_DTE                                           
               INTO  :DTATTR-FSCL-YR-NBR                                        
                    ,:DTATTR-FSCL-MN-NBR                                        
                    ,:DTATTR-FSCL-MN-END-DTE                                    
                    ,:DTATTR-FSCL-WK-END-DTE                                    
               FROM TDTATTR                                                     
               WHERE KY_DTE    = :DTATTR-KY-DTE                                 
W26603          WITH UR                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE 'Y'   TO PS-DATE-FOUND-SW                               
               WHEN SQLCODE = +100                                              
                   MOVE 'N'   TO PS-DATE-FOUND-SW                               
               WHEN OTHER                                                       
                   MOVE '8200-GET-DATE-ATTRIBUTES'                              
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT DTATTR TABLE  ' TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       9998-SQL-ABEND.                                                          
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY ' '.                                                         
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = MLKUT447'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ABEND.                                                  
                                                                                
                                                                                
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
