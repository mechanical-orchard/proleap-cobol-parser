       IDENTIFICATION DIVISION.                                                 
      *                                                                         
       PROGRAM-ID.         INKUT114.                                            
       AUTHOR.             TROY BIRK.                                           
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       10-93.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM WILL UNLOAD THE TSUMINV TABLE, THE TADJDTL TABLE,         
      *  AND THE TSTKRMW TABLE.                                                 
      ******************************************************************        
      * CHANGES                                                                 
      ******************************************************************        
      * DATE  11/30/94                                                          
      *     REMOVED THE WHERE CLAUSE FROM TSUMINV_CURSOR TO EXCLUDE             
      *     ROWS IN 9000 AREAS.                                                 
      *     MADE BY: TROY BIRK______________ WR/IR #: 2325__                    
      ******************************************************************        
      * DATE  05/30/98                                                          
      *     TO IMPROVE RUN TIME PERFORMANCE, THE EXTRACTING OF DATA             
      *     FROM TSK/TSKST WILL NOT BE DONE IN THIS PROGRAM.  LOGIC             
      *     PERTAINING TO TSK/TSKST HAS BEEN REMOVED FROM THE PROGRAM.          
      *                                                                         
      *     MADE BY: TROY BIRK______________ WR/IR #: 2325__                    
      ******************************************************************        
      * DATE  03/01/99                                                          
      *     ADDED SALES ADJUSTMENTS TO CALCULATION FOR DETERMINING              
      *     INVENTORY PHYSICAL COUNT.  (ADDED 'SA' TO CRITERIA IN               
      *     TADJDTL CURSOR)                                                     
      *     MADE BY: MARIA KLAMIK___________ WR/IR #: 9999__                    
      ******************************************************************        
      ******************************************************************        
      * DATE  03/05/99                                                          
      *     ADDED ONLINE ADJUSTMENT FLAG                                        
      *     MADE BY: SCOTT SWESSEL__________ WR/IR #: 9999__                    
      ******************************************************************        
      ******************************************************************        
      * DATE  04/10/99                                                          
      *     AS PART OF AN ENHANCEMENT TO THE VARIANCE FILE/REPORT,              
      *     ONLINE ADJUSTMENTS TO THE THE PHYSICAL INVENTORY, WILL              
      *     NOW BE INCLUDED IN THE VARIANCE FILE/REPORT.                        
      *     MADE BY: JULIE SEIDLER__________ WR/IR #: 9999__                    
      * DATE 05/28/99                                                           
      *      ALSO ADD GWP INDICATOR, SO THE GIFT WITH PURCHASE CAN BE           
      *      INCLUDED OR EXCLUDED - THE GWP ITEMS SHOULD NOT BE SCANNED         
      *      SO THIS IS MORE OF A SAFEGARD.                  LINDA MOYER        
      ******************************************************************        
20246 *  CHANGED: 11/04/99     ROLLIN KRING    WR# 20246               *        
20246 *                                                                *        
20246 *  REMOVED THE STORE CONTROL CARD AND REPLACE WITH TINVPAR READ. *        
20246 *  REVISED TO USE THE TINVPAR TABLE FOR CHECKING WHICH STORES TO *        
W33304*  PROCESS.  ONLY STORES WITH 'AP' IN THE UNT-BKG-STAT-CDE WILL  *        
W33304*  BE PROCESSED. VALID VALUES FOR THE UNIT BOOKING STATUS CODE   *        
W33304*  ARE:                                                          *        
W33304*  SL - SELECTED AS CANDIDATE                                    *        
W33304*  AP - SELECTED BY USER TO BOOK(APPROVED)                       *        
W33304*  IP - BOOKING IN PROCESS                                       *        
W33304*  TC - TRANSACTION CREATED                                      *        
W33304*  ADDED TO UPDATE THE UNT-BKG-STAT-CDE TO 'IP' FOR THE STORES   *        
20246 *  THAT ARE PROCESSED.  THIS WILL BE USED IDENTIFY STORES THAT   *        
20246 *  ARE IN THE PROCESS OF BOOKING AND OTHER PROCESSES SHOULD NOT  *        
20246 *  AFFECT THIS STORE.                                            *        
      ******************************************************************        
      ******************************************************************        
      *  CHANGED: 03/24/00     LINDA MOYER     WR# 20925               *        
      *  ADD A CONTROL CARD THAT WILL LIMIT THE NUMBER OF STORES       *        
      *  SELECTED TO BOOK BECAUSE IT IS POSSIBLE THAT INK330 COULD     *        
      *  RUN MULTIPLE TIMES FOR ONE RUN OF THIS AND THEREFORE THERE    *        
      *  MAY BE MORE THAN 20 STORES TO PROCESS AND IT HAS BE DETERMINED*        
      *  THAT FROM A PERFORMANCE STAND POINT THAT IS ALL WE WANT TO    *        
      *  SEND THROUGH.  WHATEVER VALUE IS READ IN ON THE CONTROL CARD  *        
      *  WILL DETERMINE THE NUMBER OF INVPAR ROWS READ.                *        
      ******************************************************************        
      ******************************************************************        
JF0900*  CHANGED: 09/06/00     JON FIEDLER     WR# 99999               *        
JF0900*  REMOVED THE INPUT GWP-CONTROL-CARD. ALSO REMOVED REFERENCES TO         
JF0900*  PV-YN-IND AND REMOVED LOGIC THAT CHECKS THE PV-YN-IND BEFORE           
JF0900*  WRITING AN OUTPUT RECORD. CHANGE ID=JF0900                             
JF0900******************************************************************        
RB0401*  CHANGES:                                                               
RB0401*     04/30/01 WR# 21591 RONNA BUTZ (LOOK FOR RB0401 TO IDENTIFY          
RB0401*                                                                         
RB0401*     CHANGES ARE FOR MANIFESTING.                                        
RB0401*     COPYBOOK INWS016 INCREASED IN SIZE                                  
RB0401*                                                                         
RB0401******************************************************************        
W21732******************************************************************        
W21732* CHANGED: 03/03/03 ABRAHAM JACOB, IBM GLOBAL SERVICES WR# 21732 *        
W21732*          NON-INTELLIGENT SKU CONVERSION PROJECT :              *        
W21732*          - INWS016 WAS INCREASED IN SIZE TO 80 BYTES.          *        
W21732*          - INCLUDED TSKXREF TO GET DEPT AND SUBCLASS.          *        
W21732*          - NEW FILE MISSED-PHYSICAL-CNT-FILE ADDED.            *        
W26600* W26600  06-29-2004 ROD JANSSEN INV STORE EXPANSION                      
W21732******************************************************************        
W28545*  CHANGED: 06/28/2006  J SELWITSCHKA    WR# 28545               *        
W28545*           DEPARTMENT LEVEL INVENTORY PROJECT                   *        
W33304******************************************************************        
W33304*  CHANGED: 06-20-2008  JILL LIN         WR# 33304               *        
W33304*           HOLDING INVENTORY LEDGER OPEN ACROSS PERIODS         *        
W33304*           INVENTORY SHORTAGE REPORTING                         *        
      ******************************************************************        
                                                                                
       EJECT                                                                    
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT NBR-STORE-CONTROL-FILE       ASSIGN TO INP01.                 
           SELECT PHYSICAL-COUNT-FILE          ASSIGN TO OUT01.                 
           SELECT STORE-BOOKED-FILE            ASSIGN TO OUT02.                 
W21732     SELECT MISSED-PHYSICAL-CNT-FILE     ASSIGN TO OUT03.                 
                                                                                
                                                                                
       EJECT                                                                    
       DATA DIVISION.                                                           
      *                                                                         
       FILE SECTION.                                                            
                                                                                
       FD  NBR-STORE-CONTROL-FILE                                               
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  NBR-STR-RECORD              PIC  X(80).                              
                                                                                
       FD  PHYSICAL-COUNT-FILE                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W21732 01  PHYSICAL-COUNT-RECORD       PIC  X(80).                              
                                                                                
       FD  STORE-BOOKED-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-BOOKED-RECORD         PIC  X(80).                              
                                                                                
W21732 FD  MISSED-PHYSICAL-CNT-FILE                                             
W21732     LABEL RECORDS ARE STANDARD                                           
W21732     RECORDING MODE IS F                                                  
W21732     BLOCK CONTAINS 0 RECORDS.                                            
W21732 01  MISSED-PHYSICAL-CNT-RECORD  PIC  X(80).                              
        EJECT                                                                   
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-EMPTY-DATE        PIC X(10)   VALUE '9999-09-09'.             
           05  PC-TEST-YR           PIC X(10)   VALUE '1998-01-01'.             
           05  PC-MAX-RETRIES       PIC 9(03)   VALUE 3.                        
           05  PC-NEGATIVE-ONE      PIC S9(01)  VALUE -1.                       
W26600     05  PC-MAX-STORES        PIC 9(05)   VALUE 9998.                     
20246      05  PC-PROGRAM-ID         PIC X(08)    VALUE 'INKUT114'.             
W33304     05  PC-UNIT-BK-STAT-CDE   PIC X(02)    VALUE 'IP'.                   
           05  PC-OPERCO            PIC X(02)   VALUE '03'.                     
      *    05  PC-CO                PIC X(02)   VALUE 'CO'.                     
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SQL-SUB            PIC 9(03)  VALUE ZERO.                     
           05  PV-PARAGRAPH-NAME     PIC X(30)  VALUE SPACES.                   
           05  PV-DB2-OPER-ATTEMPTED PIC X(30)  VALUE SPACES.                   
W28545     05  PV-INV-ID             PIC S9(09) VALUE +0 COMP.                  
W26600     05  PV-STORE-NUMBER       PIC 9(04)  VALUE ZEROS.                    
           05  PV-PLND-INV-DTE       PIC X(10)  VALUE SPACES.                   
           05  PV-SCAN-INV-IND       PIC X(01)  VALUE SPACES.                   
           05  PV-OUTPUT-RECORD.                                                
W26600         10  PV-OUT-STORE      PIC X(04)  VALUE SPACES.                   
W26600         10  FILLER            PIC X(76)  VALUE SPACES.                   
           05  PV-NBR-STR-RECORD.                                               
               10 FILLER             PIC X(09) VALUE SPACES.                    
               10 PV-STR-COUNT       PIC 9(2).                                  
               10 FILLER             PIC X(69) VALUE SPACES.                    
W21732     05  PV-DEPT               PIC 9(04) VALUE ZEROES.                    
W21732     05  PV-CLASS              PIC 9(04) VALUE ZEROES.                    
W21732     05  PV-KOHLS-UPC-LOW.                                                
W21732         10  FILLER                    PIC 9(04)  VALUE 0400.             
W21732         10  PV-KOHLS-SKU-MIN          PIC 9(08)  VALUE ZEROS.            
W21732         10  FILLER                    PIC 9(01)  VALUE 0.                
W21732                                                                          
W21732     05  PV-KOHLS-UPC-HIGH.                                               
W21732         10  FILLER                    PIC 9(04)  VALUE 0400.             
W21732         10  PV-KOHLS-SKU-MAX          PIC 9(08)  VALUE ZEROS.            
W21732         10  FILLER                    PIC 9(01)  VALUE 9.                
W21732                                                                          
W21732     05  PV-KOHLS-SIGN-UPC.                                               
W21732         10  PV-KOHLS-UPC-MIN-SIGNONLY     PIC S9(13).                    
W21732         10  PV-KOHLS-UPC-MAX-SIGNONLY     PIC S9(13).                    
W21732                                                                          
W21732     05  PV-KOHLS-MDSEAR-UPC.                                             
W21732         10  PV-KOHLS-UPC-MIN          PIC S9(13)V COMP-3.                
W21732         10  PV-KOHLS-UPC-MAX          PIC S9(13)V COMP-3.                
W21732                                                                          
W21732     05  PV-MISSED-PHYSICAL-CNT-REC.                                      
W21732         10  PV-ERROR-SKU              PIC 9(08).                         
W21732         10  FILLER                    PIC X(01).                         
W21732         10  PV-TABLE-NAME             PIC X(07).                         
W21732         10  FILLER                    PIC X(01).                         
W26600         10  PV-STORE-NBR              PIC X(04).                         
W21732         10  FILLER                    PIC X(01).                         
W21732         10  PV-SHEET-AREA             PIC S9(09) COMP.                   
W21732         10  FILLER                    PIC X(55).                         
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-STORE-SW   PIC X       VALUE 'N'.                      
               88  PS-EOF-STORE-FILE            VALUE 'Y'.                      
           05  PS-MORE-TINVPAR-ROWS-SW   PIC X  VALUE 'Y'.                      
               88  PS-MORE-TINVPAR-ROWS         VALUE 'Y'.                      
               88  PS-NO-MORE-TINVPAR-ROWS      VALUE 'N'.                      
           05  PS-MORE-TSUMINV-ROWS-SW   PIC X  VALUE 'Y'.                      
               88  PS-MORE-TSUMINV-ROWS         VALUE 'Y'.                      
               88  PS-NO-MORE-TSUMINV-ROWS      VALUE 'N'.                      
           05  PS-MORE-TADJDTL-ROWS-SW   PIC X  VALUE 'Y'.                      
               88  PS-MORE-TADJDTL-ROWS         VALUE 'Y'.                      
               88  PS-NO-MORE-TADJDTL-ROWS      VALUE 'N'.                      
           05  PS-MORE-TADADDL-ROWS-SW   PIC X  VALUE 'Y'.                      
               88  PS-MORE-TADJADD-ROWS         VALUE 'Y'.                      
               88  PS-NO-MORE-TADJADD-ROWS      VALUE 'N'.                      
           05  PS-MORE-TSTKRMW-ROWS-SW   PIC X  VALUE 'Y'.                      
               88  PS-MORE-TSTKRMW-ROWS         VALUE 'Y'.                      
               88  PS-NO-MORE-TSTKRMW-ROWS      VALUE 'N'.                      
           05  PS-END-OF-STR-SW     PIC X       VALUE 'N'.                      
               88  PS-EOF-STR-FILE              VALUE 'Y'.                      
W21732     05  PS-PHYSICAL-REC PIC X            VALUE 'N'.                      
W21732         88  PS-PHY-REC-FOUND             VALUE 'Y'.                      
W21732         88  PS-PHY-REC-NOT-FOUND         VALUE 'N'.                      
W21732     05  PS-MISSED-TABLE        PIC X     VALUE 'N'.                      
W21732         88  PS-TADJDTL                   VALUE 'A'.                      
W21732         88  PS-TADJADD                   VALUE 'D'.                      
W21732         88  PS-TSTKRMW                   VALUE 'S'.                      
20246 *****************************************************************         
W33304*  STORE TABLE LOADED FROM TINVPAR WITH STORES WITH 'AP' IN THE           
20246 *  UNT-BKG-STAT-CDE                                                       
20246 *****************************************************************         
20246  01  STORE-TBL.                                                           
W26600     05  STORE-OCC OCCURS 9999 TIMES                                      
20246                    INDEXED BY ST-INDX.                                    
W28545         10  ST-INV-ID          PIC S9(09) COMP.                          
W26600         10  ST-STORE-NMBR      PIC  9(04).                               
20246          10  ST-PLND-INV-DTE    PIC  X(10).                               
20246          10  ST-SCAN-INV-IND    PIC  X(01).                               
      *****************************************************************         
      * OUTPUT FILE RECORD LAYOUT                                               
      *****************************************************************         
W26600     COPY INWS016.                                                        
      *                                                                         
W21732*****************************************************************         
W21732* NON-INTELLIGENT SKU RE-FORMATTING WORK FIELDS                           
W21732*****************************************************************         
W21732     COPY ISWS006.                                                        
      *****************************************************************         
      * ERROR MESSAGE AREA FOR DB2                                              
      *****************************************************************         
           COPY DPWS004.                                                        
      *                                                                         
W28545*****************************************************************         
W28545* TINCNTL DCLGEN                                                          
W28545*****************************************************************         
W28545                                                                          
W28545     EXEC SQL                                                             
W28545        INCLUDE TINCNTL                                                   
W28545     END-EXEC.                                                            
W28545                                                                          
      *****************************************************************         
      * TINVPAR DCLGEN                                                          
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              INCLUDE TINVPAR                                                   
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TSUMINV DCLGEN                                                          
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              INCLUDE TSUMINV                                                   
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TADJDTL DCLGEN                                                          
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              INCLUDE TADJDTL                                                   
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TADJADD DCLGEN                                                          
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              INCLUDE TADJADD                                                   
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TSTKRMW DCLGEN                                                          
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              INCLUDE TSTKRMW                                                   
           END-EXEC.                                                            
                                                                                
W21732*****************************************************************         
W21732* TSKXREF DCLGEN                                                          
W21732*****************************************************************         
W21732                                                                          
W21732     EXEC SQL                                                             
W21732        INCLUDE TSKXREF                                                   
W21732     END-EXEC.                                                            
W21732                                                                          
W21732*****************************************************************         
W21732* TUPC DCLGEN                                                             
W21732*****************************************************************         
W21732                                                                          
W21732     EXEC SQL                                                             
W21732        INCLUDE TUPC                                                      
W21732     END-EXEC.                                                            
W21732                                                                          
W21732*****************************************************************         
W21732* TMDARVS DCLGEN                                                          
W21732*****************************************************************         
W21732                                                                          
W21732     EXEC SQL                                                             
W21732        INCLUDE TMDARVS                                                   
W21732     END-EXEC.                                                            
W21732                                                                          
      *****************************************************************         
      * SQL COMM AREA DCLGEN                                                    
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
20246 *****************************************************************         
20246 * DB2 CURSOR FOR TINVPAR                                                  
20246 *****************************************************************         
20246      EXEC SQL   DECLARE TINVPAR_CURSOR CURSOR FOR                         
20246                                                                           
W28545         SELECT A.INV_ID,                                                 
W28545                LOC_NBR,                                                  
20246                 PLND_INV_DTE,                                             
20246                 SCAN_INV_IND                                              
W28545           FROM TINVPAR A                                                 
W28545               ,TINCNTL B                                                 
W28545          WHERE B.ACTV_IND       = 'Y'                                    
W28545            AND ACTL_UNIT_BK_DTE = '9999-09-09'                           
W33304            AND UNT_BKG_STAT_CDE = 'AP'                                   
W28545            AND A.INV_ID         = B.INV_ID                               
W33304            AND A.LOC_INV_STAT_CDE = 'IN'                                 
W26600       ORDER BY LOC_NBR                                                   
20246                                                                           
20246      END-EXEC.                                                            
      *****************************************************************         
      * DB2 CURSOR.                                                             
      *****************************************************************         
           EXEC SQL                                                             
              DECLARE TSUMINV_CURSOR CURSOR FOR                                 
W21732            SELECT A.SKU_NBR,                                             
                         A.INV_QTY,                                             
W21732                   A.INV_UNIT_PR_AMT,                                     
W21732                   A.DEPT_NBR,                                            
W21732                   A.SUB_CL_NBR                                           
                  FROM TSUMINV A                                                
W26600            WHERE (A.LOC_NBR = :SUMINV-LOC-NBR) AND                       
W21732                  (SUBSTR(DIGITS(A.SKU_NBR),6,3) <> '000') AND            
W21732                  (SUBSTR(DIGITS(A.SKU_NBR),6,3) <> '999')                
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      * DB2 CURSOR.                                                             
      *****************************************************************         
           EXEC SQL                                                             
              DECLARE TADJDTL_CURSOR CURSOR FOR                                 
W21732           SELECT A.SKU_NBR,                                              
                        A.ADJ_QTY,                                              
W21732                  A.ADJ_UNIT_PR_AMT,                                      
W26600                  A.LOC_NBR,                                              
W21732                  A.AREA_NBR                                              
                 FROM TADJDTL A                                                 
W26600           WHERE (A.LOC_NBR = :ADJDTL-LOC-NBR) AND                        
W21732                 (SUBSTR(DIGITS(A.SKU_NBR),6,3) <> '000') AND             
W21732                 (SUBSTR(DIGITS(A.SKU_NBR),6,3) <> '999') AND             
                       (A.ADJ_TYPE_CDE IN ('CO','SA','AD','SI'))                
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      * DB2 CURSOR.                                                             
      *****************************************************************         
           EXEC SQL                                                             
              DECLARE TADJADD_CURSOR CURSOR FOR                                 
W21732          SELECT A.SKU_NBR,                                               
                       A.ADJ_QTY,                                               
W21732                 A.ADJ_UNIT_PR_AMT,                                       
W26600                 A.LOC_NBR,                                               
W21732                 A.AREA_NBR                                               
                FROM TADJADD A                                                  
W26600          WHERE (A.LOC_NBR = :ADJADD-LOC-NBR) AND                         
W21732                (SUBSTR(DIGITS(A.SKU_NBR),6,3) <> '000') AND              
W21732                (SUBSTR(DIGITS(A.SKU_NBR),6,3) <> '999')                  
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      * DB2 CURSOR.                                                             
      *****************************************************************         
           EXEC SQL                                                             
              DECLARE TSTKRMW_CURSOR CURSOR FOR                                 
W21732          SELECT A.SKU_NBR,                                               
                       A.INV_QTY,                                               
W21732                 A.ITM_UNIT_PR_AMT,                                       
W26600                 A.LOC_NBR,                                               
W21732                 A.SHEET_NBR                                              
                FROM TSTKRMW A                                                  
W26600          WHERE (A.LOC_NBR = :STKRMW-LOC-NBR) AND                         
W21732                (SUBSTR(DIGITS(A.SKU_NBR),6,3) <> '000') AND              
W21732                (SUBSTR(DIGITS(A.SKU_NBR),6,3) <> '999')                  
           END-EXEC.                                                            
                                                                                
       01  ABEND-CODE               PIC S9(04)  VALUE ZERO                      
                                                COMP SYNC.                      
      *                                                                         
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      *      CONTROLS FLOW OF THE PROGRAM.                             *        
      ******************************************************************        
       A100-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM B100-OPEN-FILES.                                             
           PERFORM Z175-READ-CONTROLS.                                          
20246      PERFORM Z250-OPEN-TINVPAR-CURSOR.                                    
20246      SET PS-MORE-TINVPAR-ROWS TO TRUE.                                    
20246      INITIALIZE STORE-TBL.                                                
20246      SET ST-INDX TO +1.                                                   
20246      PERFORM Z100-FETCH-TINVPAR                                           
20246          UNTIL (PS-NO-MORE-TINVPAR-ROWS                                   
                  OR ST-INDX > PV-STR-COUNT).                                   
20246      PERFORM Z275-CLOSE-TINVPAR-CURSOR.                                   
      *                                                                         
W28545     DISPLAY 'PV-STR-COUNT ' PV-STR-COUNT                                 
20246      PERFORM B200-PROCESS-STORES                                          
20246        VARYING ST-INDX FROM 1 BY 1                                        
20246          UNTIL ST-STORE-NMBR (ST-INDX) = ZEROS                            
20246             OR ST-INDX > PC-MAX-STORES                                    
                                                                                
           PERFORM B300-CLOSE-FILES.                                            
                                                                                
           STOP RUN.                                                            
      *                                                                         
      ******************************************************************        
      *    OPEN ALL FILES.                                                      
      ******************************************************************        
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  NBR-STORE-CONTROL-FILE                                   
                                                                                
                OUTPUT PHYSICAL-COUNT-FILE                                      
W21732                 STORE-BOOKED-FILE                                        
W21732                 MISSED-PHYSICAL-CNT-FILE.                                
      *                                                                         
      ****************************************************************          
      *                                                                         
      ****************************************************************          
       B200-PROCESS-STORES.                                                     
                                                                                
W28545     MOVE ST-INV-ID (ST-INDX)     TO PV-INV-ID.                           
20246      MOVE ST-STORE-NMBR (ST-INDX) TO PV-STORE-NUMBER.                     
20246      MOVE ST-PLND-INV-DTE (ST-INDX) TO PV-PLND-INV-DTE.                   
20246      MOVE ST-SCAN-INV-IND (ST-INDX) TO PV-SCAN-INV-IND.                   
                                                                                
           SET PS-MORE-TSUMINV-ROWS TO TRUE.                                    
           PERFORM Z400-OPEN-TSUMINV-CURSOR.                                    
           PERFORM Z450-FETCH-TSUMINV-ROW.                                      
           PERFORM C100-PROCESS-TSUMINV-ROWS                                    
               UNTIL PS-NO-MORE-TSUMINV-ROWS.                                   
           PERFORM Z475-CLOSE-TSUMINV-CURSOR.                                   
                                                                                
           SET PS-MORE-TADJDTL-ROWS TO TRUE.                                    
           PERFORM Z500-OPEN-TADJDTL-CURSOR.                                    
           PERFORM Z550-FETCH-TADJDTL-ROW.                                      
           PERFORM C200-PROCESS-TADJDTL-ROWS                                    
               UNTIL PS-NO-MORE-TADJDTL-ROWS.                                   
           PERFORM Z575-CLOSE-TADJDTL-CURSOR.                                   
                                                                                
           SET PS-MORE-TADJADD-ROWS TO TRUE.                                    
           PERFORM Z700-OPEN-TADJADD-CURSOR.                                    
           PERFORM Z750-FETCH-TADJADD-ROW.                                      
           PERFORM C400-PROCESS-TADJADD-ROWS                                    
               UNTIL PS-NO-MORE-TADJADD-ROWS.                                   
           PERFORM Z775-CLOSE-TADJADD-CURSOR.                                   
                                                                                
           SET PS-MORE-TSTKRMW-ROWS TO TRUE.                                    
           PERFORM Z600-OPEN-TSTKRMW-CURSOR.                                    
           PERFORM Z650-FETCH-TSTKRMW-ROW.                                      
           PERFORM C300-PROCESS-TSTKRMW-ROWS                                    
               UNTIL PS-NO-MORE-TSTKRMW-ROWS.                                   
           PERFORM Z675-CLOSE-TSTKRMW-CURSOR.                                   
                                                                                
20246      PERFORM C500-UPDATE-TINVPAR.                                         
                                                                                
20246      PERFORM C600-WRITE-STORE-BOOKED.                                     
                                                                                
      *                                                                         
      ******************************************************************        
      *   CLOSE ALL FILES.                                                      
      ******************************************************************        
       B300-CLOSE-FILES.                                                        
                                                                                
           CLOSE       NBR-STORE-CONTROL-FILE                                   
                       PHYSICAL-COUNT-FILE                                      
W21732                 STORE-BOOKED-FILE                                        
W21732                 MISSED-PHYSICAL-CNT-FILE.                                
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       C100-PROCESS-TSUMINV-ROWS.                                               
                                                                                
           INITIALIZE IN016-PHYSICAL-COUNT-RECORD.                              
           MOVE PV-PLND-INV-DTE           TO IN016-INV-DATE.                    
           MOVE PV-SCAN-INV-IND           TO IN016-SCAN-IND.                    
W21732     MOVE SUMINV-SKU-NBR            TO IN016-SKU.                         
W21732     MOVE PV-DEPT                   TO IN016-DEPT.                        
W21732     MOVE PV-CLASS                  TO IN016-CLASS.                       
           MOVE SUMINV-INV-QTY            TO IN016-QTY.                         
           MOVE PV-STORE-NUMBER           TO IN016-STORE.                       
W26600**** MOVE 'N'                       TO IN016-OL-IND.                      
           PERFORM Z300-WRITE-PHYSCNT-RECORD.                                   
           PERFORM Z450-FETCH-TSUMINV-ROW.                                      
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       C200-PROCESS-TADJDTL-ROWS.                                               
                                                                                
           INITIALIZE IN016-PHYSICAL-COUNT-RECORD.                              
           MOVE PV-PLND-INV-DTE           TO IN016-INV-DATE.                    
           MOVE PV-SCAN-INV-IND           TO IN016-SCAN-IND.                    
W21732     MOVE ADJDTL-SKU-NBR            TO IN016-SKU.                         
W21732     MOVE PV-DEPT                   TO IN016-DEPT.                        
W21732     MOVE PV-CLASS                  TO IN016-CLASS.                       
           MOVE ADJDTL-ADJ-QTY            TO IN016-QTY.                         
           MOVE PV-STORE-NUMBER           TO IN016-STORE.                       
W26600**** MOVE 'N'                       TO IN016-OL-IND.                      
W21732     IF PS-PHY-REC-FOUND                                                  
W21732         PERFORM Z300-WRITE-PHYSCNT-RECORD                                
W21732     END-IF.                                                              
           PERFORM Z550-FETCH-TADJDTL-ROW.                                      
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       C400-PROCESS-TADJADD-ROWS.                                               
                                                                                
           INITIALIZE IN016-PHYSICAL-COUNT-RECORD.                              
           MOVE PV-PLND-INV-DTE           TO IN016-INV-DATE.                    
           MOVE PV-SCAN-INV-IND           TO IN016-SCAN-IND.                    
W21732     MOVE ADJADD-SKU-NBR            TO IN016-SKU.                         
W21732     MOVE PV-DEPT                   TO IN016-DEPT.                        
W21732     MOVE PV-CLASS                  TO IN016-CLASS.                       
           MOVE ADJADD-ADJ-QTY            TO IN016-QTY.                         
           MOVE PV-STORE-NUMBER           TO IN016-STORE.                       
W26600**** MOVE 'N'                       TO IN016-OL-IND.                      
W21732     IF PS-PHY-REC-FOUND                                                  
W21732         PERFORM Z300-WRITE-PHYSCNT-RECORD                                
W21732     END-IF.                                                              
           PERFORM Z750-FETCH-TADJADD-ROW.                                      
                                                                                
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       C300-PROCESS-TSTKRMW-ROWS.                                               
                                                                                
           INITIALIZE IN016-PHYSICAL-COUNT-RECORD.                              
           MOVE PV-PLND-INV-DTE           TO IN016-INV-DATE.                    
           MOVE PV-SCAN-INV-IND           TO IN016-SCAN-IND.                    
W21732     MOVE STKRMW-SKU-NBR            TO IN016-SKU.                         
W21732     MOVE PV-DEPT                   TO IN016-DEPT.                        
W21732     MOVE PV-CLASS                  TO IN016-CLASS.                       
           COMPUTE IN016-QTY =  STKRMW-INV-QTY * PC-NEGATIVE-ONE.               
           MOVE PV-STORE-NUMBER           TO IN016-STORE.                       
W26600**** MOVE 'N'                       TO IN016-OL-IND.                      
W21732     IF PS-PHY-REC-FOUND                                                  
W21732         PERFORM Z300-WRITE-PHYSCNT-RECORD                                
W21732     END-IF.                                                              
           PERFORM Z650-FETCH-TSTKRMW-ROW.                                      
                                                                                
      ****************************************************************          
      * UPDATE THE UNT-BKG-STAT-CDE TO 'IP' TO IDENTIFY THIS STORE IS           
      * IN PROCESS OF UNIT BOOKING.                                             
      ****************************************************************          
       C500-UPDATE-TINVPAR.                                                     
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                        OR SQLCODE = 0)                                         
                                                                                
W28545         MOVE PV-INV-ID       TO INVPAR-INV-ID                            
W26600         MOVE PV-STORE-NUMBER TO INVPAR-LOC-NBR                           
W26600                                                                          
               EXEC SQL                                                         
                                                                                
               UPDATE TINVPAR                                                   
20246              SET UNT_BKG_STAT_CDE = :PC-UNIT-BK-STAT-CDE,                 
20246                 CHG_ID_NBR       = :PC-PROGRAM-ID,                        
20246                 CHG_TMST         = CURRENT TIMESTAMP                      
W26600            WHERE (LOC_NBR = :INVPAR-LOC-NBR) AND                         
W28545                  (INV_ID  = :INVPAR-INV-ID)  AND                         
                        (ACTL_UNIT_BK_DTE = :PC-EMPTY-DATE)                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       DISPLAY 'ERROR FOR STORE: ' PV-STORE-NUMBER              
                       MOVE 'C500-UPDATE-TINVPAR'                               
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'STORE NOT FOUND UPDATING TINVPAR'                  
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < 0                                             
                   WHEN SQLCODE > 0                                             
                       MOVE 'C500-UPDATE-TINVPAR'                               
                           TO PV-PARAGRAPH-NAME                                 
                       MOVE 'UPDATING TINVPAR TABLE'                            
                           TO PV-DB2-OPER-ATTEMPTED                             
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PC-MAX-RETRIES                                       
               MOVE 'C500-UPDATE-UNIT-BK-DATE'                                  
                   TO PV-PARAGRAPH-NAME                                         
               MOVE 'MAX RETRIES EXCEEDED   '                                   
                   TO PV-DB2-OPER-ATTEMPTED                                     
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      ****************************************************************          
      * WRITE THE STORE BOOKED RECORD                                           
      ****************************************************************          
       C600-WRITE-STORE-BOOKED.                                                 
                                                                                
           MOVE PV-STORE-NUMBER TO PV-OUT-STORE.                                
           WRITE STORE-BOOKED-RECORD                                            
               FROM PV-OUTPUT-RECORD.                                           
      *                                                                         
20246 ****************************************************************          
20246 *  LOADS THE STORE TABLE WITH TINVPAR STORES WITH 'A' IN                  
20246 *  UNT-BKF-STAT-CDE.                                                      
20246 ****************************************************************          
20246  Z100-FETCH-TINVPAR.                                                      
20246                                                                           
W28545     DISPLAY 'Z100'                                                       
20246      EXEC SQL FETCH TINVPAR_CURSOR                                        
W28545*       INTO :INVPAR-LOC-NBR,                                             
W28545        INTO :INVPAR-INV-ID,                                              
W28545             :INVPAR-LOC-NBR,                                             
20246              :INVPAR-PLND-INV-DTE,                                        
20246              :INVPAR-SCAN-INV-IND                                         
20246      END-EXEC.                                                            
20246                                                                           
20246      EVALUATE SQLCODE                                                     
20246          WHEN +0                                                          
W28545            MOVE INVPAR-INV-ID  TO ST-INV-ID (ST-INDX)                    
W26600            MOVE INVPAR-LOC-NBR TO ST-STORE-NMBR (ST-INDX)                
20246             MOVE INVPAR-PLND-INV-DTE TO ST-PLND-INV-DTE (ST-INDX)         
20246             MOVE INVPAR-SCAN-INV-IND TO ST-SCAN-INV-IND (ST-INDX)         
20246             SET ST-INDX UP BY 1                                           
20246          WHEN +100                                                        
20246              SET PS-NO-MORE-TINVPAR-ROWS TO TRUE                          
20246          WHEN OTHER                                                       
20246              MOVE 'FETCH TINVPAR_CURSOR'                                  
20246                                 TO  PV-DB2-OPER-ATTEMPTED                 
20246              PERFORM Z900-SQL-ABEND                                       
20246      END-EVALUATE.                                                        
                                                                                
      *                                                                         
      ****************************************************************          
      *  READS THE INPUT FILE TO SEE IF GWP'S SHOULD BE INCLUDED                
      *  AND THE COUNT OF STORES TO BE PROCESS IN THIS RUN.                     
      ****************************************************************          
      *                                                                         
       Z175-READ-CONTROLS.                                                      
                                                                                
           READ NBR-STORE-CONTROL-FILE                                          
                 AT END                                                         
                   SET PS-EOF-STR-FILE TO TRUE.                                 
                                                                                
           MOVE NBR-STR-RECORD TO PV-NBR-STR-RECORD.                            
      *                                                                         
      ******************************************************************        
      * OPEN TINVPAR CURSOR                                                     
      ******************************************************************        
       Z250-OPEN-TINVPAR-CURSOR.                                                
           EXEC SQL                                                             
               OPEN TINVPAR_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'OPEN TINVPAR_CURSOR' TO PV-DB2-OPER-ATTEMPTED              
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      ******************************************************************        
      *CLOSE THE TINVPAR CURSOR                                                 
      ******************************************************************        
       Z275-CLOSE-TINVPAR-CURSOR.                                               
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                   CLOSE TINVPAR_CURSOR                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z275-CLOSE-TINVPAR-CURSOR'                         
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE ON TINVPAR CURSOR'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z275-CLOSE-TINVPAR-CURSOR'                                 
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE RETRIES EXCEEDED'                                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      ****************************************************************          
      * WRITE OUTPUT RECORD                                                     
      ****************************************************************          
       Z300-WRITE-PHYSCNT-RECORD.                                               
                                                                                
           WRITE PHYSICAL-COUNT-RECORD                                          
               FROM IN016-PHYSICAL-COUNT-RECORD.                                
      *                                                                         
      *                                                                         
W21732****************************************************************          
W21732* WRITE MISSED OUTPUT RECORD                                              
W21732****************************************************************          
W21732 Z350-WRITE-MISSED-PHYCNT-REC.                                            
W21732                                                                          
W21732     INITIALIZE PV-MISSED-PHYSICAL-CNT-REC.                               
W21732                                                                          
W21732     MOVE SKXREF-SKU             TO PV-ERROR-SKU.                         
W21732                                                                          
W21732     IF (PS-PHY-REC-NOT-FOUND AND PS-TADJDTL)                             
W21732         MOVE 'TADJDTL'  TO PV-TABLE-NAME                                 
W26600         MOVE ADJDTL-LOC-NBR   TO PV-STORE-NBR                            
W21732         MOVE ADJDTL-AREA-NBR    TO PV-SHEET-AREA                         
W21732     END-IF.                                                              
W21732                                                                          
W21732     IF (PS-PHY-REC-NOT-FOUND AND PS-TADJADD)                             
W21732         MOVE 'TADJADD'  TO PV-TABLE-NAME                                 
W26600         MOVE ADJADD-LOC-NBR   TO PV-STORE-NBR                            
W21732         MOVE ADJADD-AREA-NBR    TO PV-SHEET-AREA                         
W21732     END-IF.                                                              
W21732                                                                          
W21732     IF (PS-PHY-REC-NOT-FOUND AND PS-TSTKRMW)                             
W21732         MOVE 'TSTKRMW'  TO PV-TABLE-NAME                                 
W26600         MOVE STKRMW-LOC-NBR   TO PV-STORE-NBR                            
W21732         MOVE STKRMW-SHEET-NBR   TO PV-SHEET-AREA                         
W21732     END-IF.                                                              
W21732                                                                          
W21732     WRITE MISSED-PHYSICAL-CNT-RECORD                                     
W21732           FROM PV-MISSED-PHYSICAL-CNT-REC.                               
W21732                                                                          
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       Z400-OPEN-TSUMINV-CURSOR.                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
W26600         MOVE PV-STORE-NUMBER TO SUMINV-LOC-NBR                           
W21732                                                                          
               EXEC SQL                                                         
                   OPEN TSUMINV_CURSOR                                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z400-OPEN-TSUMINV-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN ON TSUMINV CURSOR'                            
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z400-OPEN-TSUMINV-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       Z450-FETCH-TSUMINV-ROW.                                                  
                                                                                
           EXEC SQL                                                             
               FETCH TSUMINV_CURSOR                                             
W21732         INTO  :SUMINV-SKU-NBR,                                           
                     :SUMINV-INV-QTY,                                           
W21732               :SUMINV-INV-UNIT-PR-AMT,                                   
W21732               :SUMINV-DEPT-NBR,                                          
W21732               :SUMINV-SUB-CL-NBR                                         
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +100                                                    
               SET PS-NO-MORE-TSUMINV-ROWS        TO TRUE                       
           ELSE                                                                 
W21732         IF SQLCODE = 0                                                   
W21732              MOVE SUMINV-DEPT-NBR   TO PV-DEPT                           
W21732              MOVE SUMINV-SUB-CL-NBR TO PV-CLASS                          
W21732         ELSE IF SQLCODE NOT = 0                                          
                    MOVE 'Z450-FETCH-TSUMINV-ROW'                               
                                             TO PV-PARAGRAPH-NAME               
                    MOVE 'FETCH ROW FROM TSUMINV CURSOR'                        
                                            TO PV-DB2-OPER-ATTEMPTED            
                   PERFORM Z900-SQL-ABEND                                       
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       Z475-CLOSE-TSUMINV-CURSOR.                                               
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                   CLOSE TSUMINV_CURSOR                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z475-CLOSE-TSUMINV-CURSOR'                         
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE ON TSUMINV CURSOR'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z475-CLOSE-TSUMINV-CURSOR'                                 
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE RETRIES EXCEEDED'                                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       Z500-OPEN-TADJDTL-CURSOR.                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
W26600         MOVE PV-STORE-NUMBER TO ADJDTL-LOC-NBR                           
               EXEC SQL                                                         
                   OPEN TADJDTL_CURSOR                                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z500-OPEN-TADJDTL-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN ON TADJDTL CURSOR'                            
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z500-OPEN-TADJDTL-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       Z550-FETCH-TADJDTL-ROW.                                                  
                                                                                
           EXEC SQL                                                             
               FETCH TADJDTL_CURSOR                                             
W21732         INTO  :ADJDTL-SKU-NBR,                                           
                     :ADJDTL-ADJ-QTY,                                           
W21732               :ADJDTL-ADJ-UNIT-PR-AMT,                                   
W21732               :ADJDTL-LOC-NBR,                                           
W21732               :ADJDTL-AREA-NBR                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +100                                                    
               SET PS-NO-MORE-TADJDTL-ROWS        TO TRUE                       
W21732     ELSE                                                                 
W21732         IF SQLCODE = 0                                                   
W21732              MOVE ADJDTL-SKU-NBR TO SKXREF-SKU                           
W21732              SET PS-TADJDTL TO TRUE                                      
W21732              PERFORM Z800-GET-TSKXREF-VALUES                             
W21732         ELSE IF SQLCODE NOT = 0                                          
                    MOVE 'Z550-FETCH-TADJDTL-ROW'                               
                                             TO PV-PARAGRAPH-NAME               
                    MOVE 'FETCH ROW FROM TADJDTL CURSOR'                        
                                            TO PV-DB2-OPER-ATTEMPTED            
                   PERFORM Z900-SQL-ABEND                                       
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       Z575-CLOSE-TADJDTL-CURSOR.                                               
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                   CLOSE TADJDTL_CURSOR                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z575-CLOSE-TADJDTL-CURSOR'                         
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE ON TADJDTL CURSOR'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z575-CLOSE-TADJDTL-CURSOR'                                 
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE RETRIES EXCEEDED'                                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       Z600-OPEN-TSTKRMW-CURSOR.                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
W26600         MOVE PV-STORE-NUMBER TO STKRMW-LOC-NBR                           
               EXEC SQL                                                         
                   OPEN TSTKRMW_CURSOR                                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z600-OPEN-TSTKRMW-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN ON TSTKRMW CURSOR'                            
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z600-OPEN-TSTKRMW-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       Z650-FETCH-TSTKRMW-ROW.                                                  
                                                                                
           EXEC SQL                                                             
               FETCH TSTKRMW_CURSOR                                             
W21732         INTO  :STKRMW-SKU-NBR,                                           
                     :STKRMW-INV-QTY,                                           
W21732               :STKRMW-ITM-UNIT-PR-AMT,                                   
W26600               :STKRMW-LOC-NBR,                                           
W21732               :STKRMW-SHEET-NBR                                          
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +100                                                    
               SET PS-NO-MORE-TSTKRMW-ROWS TO TRUE                              
W21732     ELSE                                                                 
W21732         IF SQLCODE = 0                                                   
W21732              MOVE STKRMW-SKU-NBR TO SKXREF-SKU                           
W21732              SET PS-TSTKRMW TO TRUE                                      
W21732              PERFORM Z800-GET-TSKXREF-VALUES                             
W21732         ELSE IF SQLCODE NOT = 0                                          
                    MOVE 'Z650-FETCH-TSTKRMW-ROW'                               
                                             TO PV-PARAGRAPH-NAME               
                    MOVE 'FETCH ROW FROM TSTKRMW CURSOR'                        
                                            TO PV-DB2-OPER-ATTEMPTED            
                   PERFORM Z900-SQL-ABEND                                       
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       Z675-CLOSE-TSTKRMW-CURSOR.                                               
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                   CLOSE TSTKRMW_CURSOR                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z675-CLOSE-TSTKRMW-CURSOR'                         
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE ON TSTKRMW CURSOR'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z675-CLOSE-TSTKRMW-CURSOR'                                 
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE RETRIES EXCEEDED'                                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       Z700-OPEN-TADJADD-CURSOR.                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
W26600         MOVE PV-STORE-NUMBER TO ADJADD-LOC-NBR                           
               EXEC SQL                                                         
                   OPEN TADJADD_CURSOR                                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z700-OPEN-TADJADD-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN ON TADJADD CURSOR'                            
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z700-OPEN-TADJADD-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       Z750-FETCH-TADJADD-ROW.                                                  
                                                                                
           EXEC SQL                                                             
               FETCH TADJADD_CURSOR                                             
W21732         INTO  :ADJADD-SKU-NBR,                                           
                     :ADJADD-ADJ-QTY,                                           
W21732               :ADJADD-ADJ-UNIT-PR-AMT,                                   
W26600               :ADJADD-LOC-NBR,                                           
W21732               :ADJADD-AREA-NBR                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +100                                                    
               SET PS-NO-MORE-TADJADD-ROWS        TO TRUE                       
W21732     ELSE                                                                 
W21732         IF SQLCODE = 0                                                   
W21732              MOVE ADJADD-SKU-NBR TO SKXREF-SKU                           
W21732              SET PS-TADJADD TO TRUE                                      
W21732              PERFORM Z800-GET-TSKXREF-VALUES                             
W21732         ELSE IF SQLCODE NOT = 0                                          
                    MOVE 'Z750-FETCH-TADJADD-ROW'                               
                                             TO PV-PARAGRAPH-NAME               
                    MOVE 'FETCH ROW FROM TADJADD CURSOR'                        
                                            TO PV-DB2-OPER-ATTEMPTED            
                   PERFORM Z900-SQL-ABEND                                       
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       Z775-CLOSE-TADJADD-CURSOR.                                               
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                   CLOSE TADJADD_CURSOR                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z775-CLOSE-TADJADD-CURSOR'                         
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE ON TADJADD CURSOR'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z775-CLOSE-TADJADD-CURSOR'                                 
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE RETRIES EXCEEDED'                                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
W21732******************************************************************        
W21732*                                                                         
W21732*                                                                         
W21732******************************************************************        
W21732 Z800-GET-TSKXREF-VALUES.                                                 
W21732                                                                          
W21732     EXEC SQL                                                             
W21732       SELECT  DEPT                                                       
W21732              ,SUBCLASS                                                   
W21732         INTO :SKXREF-DEPT                                                
W21732             ,:SKXREF-SUBCLASS                                            
W21732         FROM TSKXREF                                                     
W21732        WHERE SKU = :SKXREF-SKU                                           
W21732     END-EXEC                                                             
W21732                                                                          
W21732     EVALUATE TRUE                                                        
W21732         WHEN SQLCODE = ZERO                                              
W21732              SET PS-PHY-REC-FOUND TO TRUE                                
W21732              MOVE SKXREF-DEPT     TO PV-DEPT                             
W21732              MOVE SKXREF-SUBCLASS TO PV-CLASS                            
W21732         WHEN SQLCODE = +100                                              
W21732              PERFORM Z825-GET-DKEY-FROM-TUPC                             
W21732         WHEN OTHER                                                       
W21732              MOVE 'Z800-GET-TSKXREF-VALUES'                              
W21732                    TO  PV-PARAGRAPH-NAME                                 
W21732              MOVE 'SELECT TSKXREF'                                       
W21732                    TO  PV-DB2-OPER-ATTEMPTED                             
W21732              PERFORM Z900-SQL-ABEND                                      
W21732     END-EVALUATE.                                                        
W21732*                                                                         
W21732******************************************************************        
W21732*                                                                         
W21732*                                                                         
W21732******************************************************************        
W21732 Z825-GET-DKEY-FROM-TUPC.                                                 
W21732                                                                          
W21732     MOVE SKXREF-SKU                TO PV-KOHLS-SKU-MIN                   
W21732                                       PV-KOHLS-SKU-MAX.                  
W21732     MOVE PV-KOHLS-UPC-LOW          TO                                    
W21732                                PV-KOHLS-UPC-MIN-SIGNONLY.                
W21732     MOVE PV-KOHLS-UPC-HIGH         TO                                    
W21732                                PV-KOHLS-UPC-MAX-SIGNONLY.                
W21732     MOVE PV-KOHLS-UPC-MIN-SIGNONLY TO PV-KOHLS-UPC-MIN.                  
W21732     MOVE PV-KOHLS-UPC-MAX-SIGNONLY TO PV-KOHLS-UPC-MAX.                  
W21732                                                                          
W21732     EXEC SQL                                                             
W21732          SELECT SUB_CL_MDSEAR_ID                                         
W21732                ,VS_ID                                                    
W21732            INTO :UPC-SUB-CL-MDSEAR-ID                                    
W21732                ,:UPC-VS-ID                                               
W21732            FROM TUPC                                                     
W21732           WHERE (UPC_NBR >= :PV-KOHLS-UPC-MIN                            
W21732             AND  UPC_NBR <= :PV-KOHLS-UPC-MAX)                           
W21732     END-EXEC.                                                            
W21732                                                                          
W21732     EVALUATE SQLCODE                                                     
W21732        WHEN 0                                                            
W21732             PERFORM Z850-GET-ATTR-FROM-TMDARVS                           
W21732        WHEN 100                                                          
W21732             SET PS-PHY-REC-NOT-FOUND TO TRUE                             
W21732             PERFORM Z350-WRITE-MISSED-PHYCNT-REC                         
W21732        WHEN OTHER                                                        
W21732             MOVE 'Z825-GET-DKEY-FROM-TUPC'                               
W21732                       TO  PV-PARAGRAPH-NAME                              
W21732             MOVE 'ERROR ON SELECT FROM TUPC'                             
W21732                       TO PV-DB2-OPER-ATTEMPTED                           
W21732             PERFORM Z900-SQL-ABEND                                       
W21732     END-EVALUATE.                                                        
W21732*                                                                         
W21732******************************************************************        
W21732*                                                                         
W21732*                                                                         
W21732******************************************************************        
W21732 Z850-GET-ATTR-FROM-TMDARVS.                                              
W21732                                                                          
W21732     MOVE UPC-SUB-CL-MDSEAR-ID TO MDARVS-SUB-CL-MDSEAR-ID.                
W21732     MOVE UPC-VS-ID            TO  MDARVS-VS-ID.                          
W21732                                                                          
W21732     EXEC SQL                                                             
W21732         SELECT DEPT_NBR                                                  
W21732               ,SUB_CL_NBR                                                
W21732           INTO :MDARVS-DEPT-NBR                                          
W21732               ,:MDARVS-SUB-CL-NBR                                        
W21732           FROM TMDARVS                                                   
W21732          WHERE SUB_CL_MDSEAR_ID = :MDARVS-SUB-CL-MDSEAR-ID               
W21732            AND VS_ID = :MDARVS-VS-ID                                     
W21732     END-EXEC.                                                            
W21732                                                                          
W21732     EVALUATE SQLCODE                                                     
W21732        WHEN 0                                                            
W21732             SET PS-PHY-REC-FOUND TO TRUE                                 
W21732             MOVE MDARVS-DEPT-NBR   TO PV-DEPT                            
W21732             MOVE MDARVS-SUB-CL-NBR TO PV-CLASS                           
W21732        WHEN 100                                                          
W21732             SET PS-PHY-REC-NOT-FOUND TO TRUE                             
W21732             PERFORM Z350-WRITE-MISSED-PHYCNT-REC                         
W21732        WHEN OTHER                                                        
W21732             MOVE 'Z850-GET-ATTR-FROM-TMDARVS'                            
W21732                       TO  PV-PARAGRAPH-NAME                              
W21732             MOVE 'ERROR ON SELECT FROM TMDARVS'                          
W21732                       TO PV-DB2-OPER-ATTEMPTED                           
W21732             PERFORM Z900-SQL-ABEND                                       
W21732     END-EVALUATE.                                                        
      ****************************************************************          
      *    THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.        *          
      ****************************************************************          
       Z900-SQL-ABEND.                                                          
      *                                                                         
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUT114'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
      *                                                                         
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
      *                                                                         
                                                                               
