       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKUT018.                                            
       AUTHOR.             THERESE RAMSEYER.                            00040024
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       03-16-2009.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      ***    N E W   P R O G R A M   F O R   H I L O                 ***        
      ******************************************************************        
      *      INKUT018 - SOFT DELETED STORE FILTER FOR CUM REPORTS      *        
      *                                                                *        
      *  THIS PROGRAM TAKES THE STORE EXPECTED EXTRACT SUMM AS INPUT & *        
      *  WRITES OUT EACH RECORD TO EITHER A CUM FILE OR DROPPED FILE   *        
      *  DEPENDING ON THE STORE NUMBER ON EACH RECORD.                 *        
      *  IF THE STORE IS PART OF AN ACTIVE CYCLE, NOT FINANCIALLY      *        
      *  BOOKED AND NOT SOFT DELETED, THEN THAT RECORD GOES TO THE CUM *        
      *  FILE, OTHERS GO TO THE DROPPED FILE.                          *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                     00113027
      * -------  ----------  ----------------------------------------   00114027
      * WR33304  04/03/2009  THERESE RAMSEYER - INITIAL INSTALLATION.   00119027
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT STORE-EXP-ALL-FILE      ASSIGN TO INP01.                      
           SELECT STORE-EXP-CUM-FILE      ASSIGN TO OUT01.                      
           SELECT STORE-EXP-DRP-FILE      ASSIGN TO OUT02.                      
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  STORE-EXP-ALL-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-EXP-ALL-REC                PIC  X(75).                         
                                                                                
       FD  STORE-EXP-CUM-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-EXP-CUM-REC                PIC  X(75).                         
                                                                                
       FD  STORE-EXP-DRP-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-EXP-DRP-REC                PIC  X(75).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP SYNC                
                                                       VALUE +0.                
                                                                                
       01  PROGRAM-ACCUMULATORS.                                                
           05  PA-TOT-RECS-READ             PIC S9(09) COMP-3 VALUE +0.         
           05  PA-TOT-CUM-RECS-WRIT         PIC S9(09) COMP-3 VALUE +0.         
           05  PA-TOT-DRP-RECS-WRIT         PIC S9(09) COMP-3 VALUE +0.         
                                                                                
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'INKUT018'.        
           05  PC-MAX-RETRIES               PIC S9(04) COMP SYNC                
                                                       VALUE +2.                
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  PS-END-OF-INPUT-SW           PIC X      VALUE 'N'.               
               88  PS-END-OF-INPUT                     VALUE 'Y'.               
               88  PS-NOT-END-INPUT                    VALUE 'N'.               
           05  PS-TINVPAR-FOUND-SW          PIC X      VALUE 'N'.               
               88  PS-TINVPAR-FOUND                    VALUE 'Y'.               
               88  PS-TINVPAR-NOT-FOUND                VALUE 'N'.               
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-EXISTS                    PIC S9(04) VALUE +0 COMP.           
           05  PV-HOLD-STR-NBR              PIC  9(05) COMP VALUE ZERO.         
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30) VALUE SPACE.             
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.             
           05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.              
           05  PV-DISPLAY                   PIC S9(05) COMP-3 VALUE +0.         
                                                                                
      *****************************************************************         
      * STORE EXPECTED -FILE LAYOUT COPYBOOK - INPUT & OUTPUT                   
      *****************************************************************         
           COPY INRD237.                                                        
                                                                                
      *****************************************************************         
      * DB2 FORMATTED ERROR MESSAGE                                             
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * DCLGEN FOR THE INVENTORY PARAMETERS TABLE                               
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DCLGEN FOR THE INVENTORY CYCLE CONTROL TABLE                            
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * SQL COMMUNICATIONS WORK AREA                                            
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-INPUT                                           
               UNTIL PS-END-OF-INPUT.                                           
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  OPEN ALL FILES AND READ AN INPUT FILE RECORD.                 *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  STORE-EXP-ALL-FILE                                       
                OUTPUT STORE-EXP-CUM-FILE                                       
                       STORE-EXP-DRP-FILE.                                      
                                                                                
           PERFORM 1100-READ-INPUT-FILE.                                        
                                                                                
      ******************************************************************        
      *  FOR EACH INPUT FILE RECORD, VALIDATE THE STORE                *        
      ******************************************************************        
       2000-PROCESS-INPUT.                                                      
                                                                                
           IF IN237-STORE-NBR-9 EQUAL PV-HOLD-STR-NBR                           
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 3000-STORE-VALIDATION                                    
               MOVE IN237-STORE-NBR-9 TO PV-HOLD-STR-NBR                        
           END-IF.                                                              
                                                                                
           IF PS-TINVPAR-FOUND                                                  
              PERFORM 4000-WRITE-OUT-CUM-FILE                                   
           ELSE                                                                 
              PERFORM 5000-WRITE-OUT-DRP-FILE                                   
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-INPUT-FILE.                                        
                                                                                
      ******************************************************************        
      *  PART OF ACTIVE CYCLE? NOT FINANCIALLY BKD, NOT SOFT DELETED            
      ******************************************************************        
       3000-STORE-VALIDATION.                                                   
                                                                                
           MOVE IN237-STORE-NBR-9 TO INVPAR-LOC-NBR                             
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   SELECT 1                                                     
                   INTO :PV-EXISTS                                              
                   FROM TINVPAR A                                               
                       ,TINCNTL B                                               
                   WHERE A.LOC_NBR = :INVPAR-LOC-NBR                            
                     AND A.LOC_INV_STAT_CDE = 'IN'                              
                     AND A.ACTL_FIN_BK_DTE = '9999-09-09'                       
                     AND B.ACTV_IND = 'Y'                                       
                     AND A.INV_ID   = B.INV_ID                                  
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +100                                          
                       SET PS-TINVPAR-NOT-FOUND TO TRUE                         
                       MOVE IN237-STORE-NBR-9 TO PV-DISPLAY                     
                       DISPLAY 'DROP LOC:' PV-DISPLAY                           
                   WHEN SQLCODE = 0                                             
                       SET PS-TINVPAR-FOUND TO TRUE                             
                       MOVE IN237-STORE-NBR-9 TO PV-DISPLAY                     
                       DISPLAY 'KEEP LOC:' PV-DISPLAY                           
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '3000-STORE-VALIDATION'                             
                           TO PV-PARAGRAPH-NAME                                 
                       MOVE 'SELECT TINVPAR'                                    
                           TO PV-DB2-OPER-ATTEMPTED                             
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '3000-STORE-VALIDATION' TO PV-PARAGRAPH-NAME                
               MOVE 'MAX RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************03500024
      * READS THE INPUT SALES ADJ ALL FILE                              03510024
      ******************************************************************03520024
       1100-READ-INPUT-FILE.                                            03530024
                                                                                
           READ STORE-EXP-ALL-FILE INTO IN237-STR-EXPTED-RECORD         03540024
               AT END                                                   03550024
                   SET PS-END-OF-INPUT TO TRUE                          03550024
               NOT AT END                                                       
                   ADD 1 TO PA-TOT-RECS-READ.                                   
                                                                                
      ******************************************************************        
      *  WRITE TO OUTPUT CUM FILE                                     *         
      ******************************************************************        
       4000-WRITE-OUT-CUM-FILE.                                                 
                                                                                
           WRITE STORE-EXP-CUM-REC FROM IN237-STR-EXPTED-RECORD.                
           ADD +1 TO PA-TOT-CUM-RECS-WRIT.                                      
                                                                                
      ******************************************************************        
      *  WRITE TO OUTPUT DROPPED FILE                                  *        
      ******************************************************************        
       5000-WRITE-OUT-DRP-FILE.                                                 
                                                                                
           WRITE STORE-EXP-DRP-REC FROM IN237-STR-EXPTED-RECORD.                
           ADD +1 TO PA-TOT-DRP-RECS-WRIT.                                      
                                                                                
      ******************************************************************        
      *    - CLOSE THE FILES PROCESSED BY PROGRAM                      *        
      *    - DISPLAY OUTPUT RECORD COUNTS                              *        
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE STORE-EXP-ALL-FILE                                             
                 STORE-EXP-CUM-FILE                                             
                 STORE-EXP-DRP-FILE.                                            
                                                                                
           DISPLAY SPACE.                                                       
           DISPLAY '*************************************************'. 06840000
           DISPLAY '***********    INKUT018 RUN STATS    ************'. 06840000
           DISPLAY '*************************************************'. 06840000
           DISPLAY '*** TOT CUM RECS WRIT......' PA-TOT-CUM-RECS-WRIT.          
           DISPLAY '*** TOT DRP RECS WRIT......' PA-TOT-DRP-RECS-WRIT.          
           DISPLAY '                           ----------'.                     
           DISPLAY '*** TOT INPUT RECS READ....' PA-TOT-RECS-READ.              
           DISPLAY SPACE.                                                       
                                                                                
      ******************************************************************        
      * ABEND THE PROGRAM IN THE EVENT THAT AN SQL ERROR OR WARNING             
      * CODE OCCURS.                                                            
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
                                                                                
      *-----------------------------------------------------------------        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      *-----------------------------------------------------------------        
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
