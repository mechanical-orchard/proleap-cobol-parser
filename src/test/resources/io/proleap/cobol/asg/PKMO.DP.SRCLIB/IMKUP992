       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    IMKUP992.                                                 
       AUTHOR.        JOHN SELWITSCHKA.                                         
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11/21/2014.                                               
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *  IMKUP992 - TPPCPRC UPDATE AND INSERT                                   
      *****************************************************************         
      *                                                                         
      *  THIS PROGRAM WILL UPDATE TPPCPRC                                       
      *                                                                         
      *  TABLES ACCESSED ARE:                                                   
      *      TSKXREF - SKU CROS REFERENCE                                       
      *      TPPCPRC - PRICING HISTORY                                          
      *  TABLES UPDATED ARE:                                                    
      *      TPPCPRC - PRICING HISTORY                                          
      *  FILES READ ARE:                                                        
      *      IMRDXXX SKU UPDATE FILE                                            
      *****************************************************************         
      * 11/21/14 - JOHN SELWITSCHKA                                             
      *    - NEW PROGRAM                                                        
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT SKU-UPDATE-FILE              ASSIGN TO INP01.                 
           SELECT CONTROL-CARD-FILE            ASSIGN TO INP02.                 
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
       FILE SECTION.                                                            
                                                                                
       FD  SKU-UPDATE-FILE                                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS PRICE-UPDATE-RECORD.                                  
       01  SKU-UPDATE-RECORD           PIC  X(80).                              
      *                                                                         
       FD  CONTROL-CARD-FILE                                            00590094
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                          00600094
           BLOCK CONTAINS 0 RECORDS.                                    00610094
       01  CONTROL-RECORD              PIC X(80).                       00620094
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME                PIC  X(08)   VALUE                
                                                           'IMKUP992'.          
           05  PC-OUT-MSGLENGTH               PIC S9(09)   VALUE +450           
                                                           COMP.                
           05  PC-MAXIMUM-RETRY-COUNT         PIC S9(04)   VALUE +2             
                                                           COMP SYNC.           
           05  PC-MAX-STORES                  PIC  9(02)   VALUE 40.            
           05  PC-CLEARANCE                   PIC  X(02)   VALUE '30'.          
           05  PC-MIXED                       PIC  X(02)   VALUE '25'.          
           05  PC-ORIGIN-CNTRY                PIC  X(03)   VALUE 'US '.         
           05  PC-DEFAULT-TIMESTAMP           PIC  X(26)   VALUE                
               '9999-09-09-23.59.00.000000'.                                    
           05  PC-TIMESTAMP-INACTIVE-TIME     PIC  X(16)   VALUE                
               '-23.59.59.999999'.                                              
           05  PC-TIMESTAMP-ACTIVE-TIME       PIC  X(16)   VALUE                
               '-00.00.00.000000'.                                              
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-SKU-RECORD.                                                       
           05  PV-SKU                         PIC S9(08)   VALUE +0.            
           05  FILLER                         PIC  X(72)   VALUE SPACES.        
                                                                                
       01  PV-WORK-VARIABLES.                                                   
           05  PV-STAT-CHNGD-DATE             PIC  9(06)   VALUE ZEROS.         
           05  PV-STAT-CHNGD-DATE-REFORMAT REDEFINES PV-STAT-CHNGD-DATE.        
               10  PV-STAT-CHNGD-DATE-YY      PIC  9(02).                       
               10  PV-STAT-CHNGD-DATE-MM      PIC  9(02).                       
               10  PV-STAT-CHNGD-DATE-DD      PIC  9(02).                       
                                                                                
           05  PV-UPC-COMP-10                 PIC S9(10)   VALUE +0             
                                                           COMP.                
           05  PV-COMPLETION-CODE             PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-REASON                      PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-REASON-TEXT                 PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-UNT-COST-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-ORIG-CLR-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-MKDN-CDE-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-ORIG-EFF-END-NULL-IND       PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-EFF-END-NULL-IND            PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-EFF-END-NULL-IND-UNP        PIC S9(04)   VALUE +0.            
           05  PV-EFF-END-NULL-IND-DIS        PIC ----9.                        
           05  PV-RETRY-COUNT                 PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-PREV-STR-NBR                PIC S9(04)   VALUE +0             
                                                           COMP.                
                                                                                
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.        
           05  PV-OPERATION-MSG-1             PIC  X(40)   VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.        
           05  PV-NEW-SKU-STATUS              PIC  X(02)   VALUE SPACES.        
               88  PV-NEW-SKU-STATUS-ACTIVE                VALUE '20'.          
               88  PV-NEW-SKU-STATUS-CLEARANCE             VALUE '30'.          
               88  PV-NEW-SKU-STATUS-MIXED                 VALUE '25'.          
           05  PV-HOLD-UIR-EFF-DATE.                                            
               10  PV-HOLD-UIR-EFF-CC         PIC  X(02)   VALUE SPACES.        
               10  PV-HOLD-UIR-EFF-YY         PIC  X(02)   VALUE SPACES.        
               10  PV-HOLD-UIR-EFF-DASH1      PIC  X(01)   VALUE '-'.           
               10  PV-HOLD-UIR-EFF-MM         PIC  X(02)   VALUE SPACES.        
               10  PV-HOLD-UIR-EFF-DASH2      PIC  X(01)   VALUE '-'.           
               10  PV-HOLD-UIR-EFF-DD         PIC  X(02)   VALUE SPACES.        
           05  PV-COMMIT-TIMESTAMP            PIC  X(26)   VALUE SPACES.        
           05  PV-CURRENT-TIMESTAMP           PIC  X(26)   VALUE SPACES.        
           05  PV-RUN-DATE                    PIC  X(10)   VALUE SPACES.        
           05  PV-SAVE-SKU                    PIC S9(08)   VALUE +0             
                                                           COMP-3.              
           05  PV-PREV-SKU                    PIC S9(08)   VALUE +0             
                                                           COMP-3.              
           05  PV-PREV-PRCHG-TYP-CDE          PIC  X(03)   VALUE SPACES.        
           05  PV-ORIG-UNT-RTL-AMT            PIC S9(7)V99 VALUE +0             
                                                           COMP-3.              
           05  PV-ORIG-UNT-COST-AMT           PIC S9(7)V999 VALUE +0            
                                                           COMP-3.              
           05  PV-CURR-UNT-RTL-AMT            PIC S9(7)V99 VALUE +0             
                                                           COMP-3.              
           05  PV-CURR-UNT-COST-AMT           PIC S9(7)V999 VALUE +0            
                                                           COMP-3.              
           05  PV-OLD-MEITGP-NBR              PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-RTL-EQ-TO-CORP              PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-SAVE-REGN-PRIC-IND          PIC  X(01)   VALUE SPACES.        
           05  PV-STORE0-UPC-STAT-CDE         PIC  X(02)   VALUE SPACES.        
           05  PV-STORE0-ORIG-CLR-DTE         PIC  X(10)   VALUE SPACES.        
           05  PV-RECS-READ-CNTR              PIC  9(09)   VALUE ZEROES.00830000
           05  PV-SQLCODE-DISPLAY             PIC +(8)9    VALUE ZEROES.00830000
           05  PV-UPCPLS-UPC-ID-NUM           PIC  9(10)   VALUE ZERO.          
           05  PV-DETAIL-KEY.                                                   
               10  PV-DK-SKU                  PIC  X(08)   VALUE SPACES.        
               10  PV-DK-STORE-NBR            PIC  9(04)   VALUE ZERO.          
           05  PV-DELAY-INTERVAL.                                               
               10  FILLER                   PIC X(02)   VALUE SPACES.           
               10  PV-DELAY-TIME            PIC 9(08)   VALUE 6000.             
           05  WS-SYSWAIT                   PIC X(08)   VALUE 'SYSWAIT'.        
           05  PV-PPCPRC-STRT-TMST          PIC X(26)   VALUE SPACES.           
           05  PV-PPCPRC-END-TMST           PIC X(26)   VALUE SPACES.           
           05  PV-DISPLAY-10                PIC 9(10)   VALUE ZERO.             
           05  PV-DISPLAY-4                 PIC 9(04)   VALUE ZERO.             
           05  PV-DISPLAY-8                 PIC 9(08)   VALUE ZERO.             
           05  PV-DISPLAY-15                PIC 9(15)   VALUE ZERO.             
                                                                                
      ******************************************************************        
      * CR CHECKPOINT RESTART VARIABLES                                *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                    PIC S9(04) VALUE +138             
                                                         COMP.                  
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-DETAIL-KEY              PIC  X(12) VALUE SPACES.          
               10  CR-RECS-PROCESSED-CNTR     PIC  9(09) VALUE ZERO.            
               10  CR-RECS-READ-CNTR          PIC  9(09) VALUE ZERO.            
               10  CR-SKUS-READ-CNTR          PIC  9(09) VALUE ZERO.            
               10  CR-SKUS-NOT-FOUND-CNTR     PIC  9(09) VALUE ZERO.            
               10  CR-TPPCPRC-ROW-UPD-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-TPPCPRC-ROW-INS-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-TPPCPRC-ROW-STR-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-TSKXREF-ROW-UPD-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-TSK-ROW-UPD-CNTR        PIC  9(09) VALUE ZERO.            
               10  CR-TUPC-ROW-UPD-CNTR       PIC  9(09) VALUE ZERO.            
               10  CR-TSKXREF-ROW-NOT-FOUND   PIC  9(09) VALUE ZERO.            
               10  CR-TSK-ROW-NOT-FOUND       PIC  9(09) VALUE ZERO.            
               10  CR-TUPC-ROW-NOT-FOUND      PIC  9(09) VALUE ZERO.            
               10  CR-MSGS-TO-XSITEM-QUEUE    PIC  9(09) VALUE ZERO.            
               10  CR-TYPE-75-MSGS-CREATED    PIC  9(09) VALUE ZERO.            
               10  CR-TYPE-80-MSGS-CREATED    PIC  9(09) VALUE ZERO.            
               10  CR-IMT-EVNT-MSG-ROW-INS-CNTR                                 
                                              PIC  9(09) VALUE ZERO.            
               10  CR-COMMIT-CNTR             PIC  9(09) VALUE ZERO.            
               10  CR-SKU-STRS-BYPASSED-CNTR  PIC  9(09) VALUE ZERO.            
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW              PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-FILE                          VALUE 'Y'.           
           05  PS-LOC-CSR-SW                  PIC X(01)    VALUE 'N'.           
               88  PS-END-OF-LOC-CSR                       VALUE 'Y'.           
               88  PS-MORE-LOC-ROWS                        VALUE 'N'.           
           05  PS-FIRST-COMMIT-SW             PIC  X(01)   VALUE 'N'.           
               88  PS-FIRST-COMMIT                         VALUE 'Y'.           
           05  PS-FIRST-SKU-STORE-SW          PIC  X(01)   VALUE 'N'.           
               88  PS-FIRST-SKU-STORE                      VALUE 'Y'.           
               88  PS-NOT-FIRST-SKU-STORE                  VALUE 'N'.           
           05  PS-NO-DOCUMENTS-SW             PIC  X(01)   VALUE 'N'.           
               88  PS-NO-DOCUMENTS                         VALUE 'Y'.           
           05  PS-CURRENT-ROW-FOUND-SW        PIC  X(01)   VALUE 'N'.           
               88  PS-CURRENT-ROW-FOUND                    VALUE 'Y'.           
               88  PS-CURRENT-ROW-NOT-FOUND                VALUE 'N'.           
           05  PS-CURRENT-ROW-FOUND-SW        PIC  X(01)   VALUE 'N'.           
               88  PS-CURRENT-LOC-ROW-FOUND                VALUE 'Y'.           
               88  PS-CURRENT-LOC-ROW-NOT-FOUND            VALUE 'N'.           
           05  PS-HAVE-UPCPLS-COST-INFO-SW    PIC  X(01)   VALUE 'N'.           
               88  PS-HAVE-UPCPLS-COST-INFO                VALUE 'Y'.           
               88  PS-NOT-HAVE-UPCPLS-COST-INFO            VALUE 'N'.           
           05  PS-HAVE-STORE0-TPPCPRC-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-HAVE-STORE0-TPPCPRC                  VALUE 'Y'.           
               88  PS-NOT-HAVE-STORE0-TPPCPRC              VALUE 'N'.           
           05  PS-IMT-EVNT-MSG-STARTED-SW     PIC  X(01)   VALUE 'N'.           
               88  PS-IMT-EVNT-MSG-STARTED                 VALUE 'Y'.           
               88  PS-IMT-EVNT-MSG-NOT-STARTED             VALUE 'N'.           
           05  PS-SKU-INFO-FOUND-SW           PIC  X(01)   VALUE 'N'.           
               88  PS-SKU-INFO-FOUND                       VALUE 'Y'.           
               88  PS-SKU-INFO-NOT-FOUND                   VALUE 'N'.           
           05  PS-NEW-CLR-RCP-SW              PIC  X(01)   VALUE 'N'.           
               88  PS-NEW-CLR-RCP                          VALUE 'Y'.           
               88  PS-NOT-NEW-CLR-RCP                      VALUE 'N'.           
           05  PS-CHECKPOINT-RESTART-SW       PIC  X(01)   VALUE 'N'.           
               88  PS-CHECKPOINT-RESTART                   VALUE 'Y'.           
               88  PS-NOT-CHECKPOINT-RESTART               VALUE 'N'.           
           05  PS-STRS-ALL-SAME-PRICE-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-STRS-ALL-SAME-PRICE                  VALUE 'Y'.           
               88  PS-NOT-STRS-ALL-SAME-PRICE              VALUE 'N'.           
                                                                                
       01  ABEND-CODE                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           88  PV-ABEND-4001                   VALUE +4001.                     
      *        NO CONTROL CARD 1                                                
           88  PV-ABEND-4002                   VALUE +4002.                     
      *        INCORRECT CONTROL CARD DATA                                      
           88  PV-ABEND-4003                   VALUE +4003.                     
      *        EFFECTIVE DATE LESS THAN CURRENT DATE                            
           88  PV-ABEND-4004                   VALUE +4004.                     
      *        INVALID CONTROL CARD JOB NAME                                    
           88  PV-ABEND-4005                   VALUE +4005.                     
      *        INVALID CONTROL CARD XS ITEM QUEUE NAME                          
           88  PV-ABEND-4006                   VALUE +4006.                     
      *        NO QUEUE MANAGER CONTROL CARD                                    
           88  PV-ABEND-4007                   VALUE +4007.                     
      *        INVALID CONTROL CARD QUEUE MANAGER NAME                          
           88  PV-ABEND-4008                   VALUE +4008.                     
      *        NO TPPCPRC ROW FOR FINAL OR MARK2                                
           88  PV-ABEND-4013                   VALUE +4013.                     
      *        DB2 SQL ABEND                                                    
           88  PV-ABEND-4020                   VALUE +4020.                     
      *        MQSERIES ABEND                                                   
                                                                                
       01  DISPLAY-EDITS.                                                       
           05  DI-RECS-PROCESSED-CNTR       PIC  Z(08)9  VALUE ZEROS.           
           05  DI-RECS-READ-CNTR            PIC  Z(08)9  VALUE ZEROS.           
           05  DI-SKUS-READ-CNTR            PIC  Z(08)9  VALUE ZEROS.           
           05  DI-SKUS-NOT-FOUND-CNTR       PIC  Z(08)9  VALUE ZEROS.           
           05  DI-SKU-STRS-BYPASSED-CNTR    PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TPPCPRC-ROW-UPDATE-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TPPCPRC-ROW-INSERT-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TPPCPRC-ROW-INACTV-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TPPCPRC-ROW-DELETE-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TPPCPRC-ROW-UPDSTR-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TSKXREF-ROW-UPDATE-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TSK-ROW-UPDATE-CNTR       PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPC-ROW-UPDATE-CNTR      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TSKXREF-ROW-NOT-FOUND     PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TSK-ROW-NOT-FOUND         PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPC-ROW-NOT-FOUND        PIC  Z(08)9  VALUE ZEROS.           
           05  DI-MSGS-TO-XSITEM-QUEUE      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TYPE-75-MSGS-CREATED      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TYPE-80-MSGS-CREATED      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-IMT-EVNT-ROW-INSERT-CNTR  PIC  Z(08)9  VALUE ZEROS.           
           05  DI-COMMIT-CNTR               PIC  Z(08)9  VALUE ZEROS.           
           05  DI-INPUT-RECS-DISPLAY        PIC  9(06)   VALUE ZEROS.           
                                                                                
       01  PT-PROGRAM-TABLES.                                                   
           05  LOC-TBL-MAX               PIC 9(5)    VALUE 10000.               
           05  LOCATION-TABLE.                                                  
               10  LOC-TBL-SIZE          PIC 9(5)    VALUE ZEROES.              
               10  LOC-ENTRY  OCCURS 10000 TIMES INDEXED BY LOC-INDEX.          
                   15  LOC-NBR           PIC 9(05).                             
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR CONTROL CARD FILE.                                    
      * CC PROCESS OLD DATE WILL DEFAULT TO N.  IT WILL BE Y ONLY IF    00840094
      * WE NEED TO PROCESS A PRICE CHANGE THAT IS IN THE PAST.          00840094
      ****************************************************************          
       01  CC-CONTROL-CARD.                                                     
           05  CC-END-DATE             PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  CC-BEG-DATE             PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  CC-UPDATE-IND           PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(57) VALUE SPACES.                  
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      *  TSKXREF - SKU CROSS REFERENCE TABLE                                    
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TSKXREF                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TPPCPRC - UPC PHYSICAL LOCATION STATUS TABLE                           
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TPPCPRC                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    COBOL DECL DB2 COMMUNICATION AREA                                    
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * 0000-MAIN-MODULE.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-SKU-UPDATES                                     
             UNTIL PS-END-OF-FILE.                                              
                                                                                
           PERFORM 3000-EOJ-PROCESSING.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                           *        
      *  ==> PROCESSES:                                                *        
      *      - CHECKS FOR RESTART PROCESSING.                          *        
      *      - INITIALIZES VARIABLES.                                  *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           OPEN INPUT SKU-UPDATE-FILE                                           
                      CONTROL-CARD-FILE.                                        
                                                                                
           PERFORM 4100-READ-CONTROL-CARD-1.                                    
                                                                                
           MOVE CC-END-DATE TO PV-PPCPRC-END-TMST (1:10).                       
           MOVE PC-TIMESTAMP-INACTIVE-TIME TO                                   
                     PV-PPCPRC-END-TMST (11:16).                                
           MOVE CC-BEG-DATE TO PV-PPCPRC-STRT-TMST (1:10).                      
           MOVE PC-TIMESTAMP-ACTIVE-TIME TO                                     
                     PV-PPCPRC-STRT-TMST (11:16).                               
                                                                                
           PERFORM 4000-READ-RECORD.                                            
                                                                                
      ******************************************************************        
      * 2000-PROCESS-SKU-UPDATES.                                      *        
      *  ==> PROCESSES:                                                *        
      *    - PROCESSES A SKU UPDATE RECORD                             *        
      *    - UPDATE TPPCPRC ROWS                                       *        
      ******************************************************************        
       2000-PROCESS-SKU-UPDATES.                                                
           MOVE '2000-PROCESS-SKU-UPDATES' TO PV-PARAGRAPH-NAME.                
                                                                                
           DISPLAY 'PV-SKU ' PV-SKU                                             
           MOVE PV-SKU                TO SKXREF-SKU.                            
           PERFORM 2010-SELECT-TSKXREF.                                         
           IF SQLCODE = ZERO                                                    
               MOVE SKXREF-ITM-NBR     TO PPCPRC-TSK-ITM-NBR                    
                                          PV-DISPLAY-15                         
               MOVE ZERO               TO PPCPRC-STR-NBR                        
                                          PV-DISPLAY-4                          
               PERFORM 2015-SELECT-TPPCPRC                                      
           END-IF.                                                              
           IF SQLCODE = +100                                                    
               DISPLAY 'TPPCPRC LOC ZERO NOT READ, TSKXREF NOT FOUND'           
               DISPLAY 'ITM NBR          '  PV-DISPLAY-15                       
               DISPLAY 'LOC NBR          '  PV-DISPLAY-4                        
               ADD +1 TO CR-SKU-STRS-BYPASSED-CNTR                              
           ELSE                                                                 
               MOVE PV-PPCPRC-END-TMST TO PPCPRC-END-TMST                       
               IF CC-UPDATE-IND = 'U'                                           
                   PERFORM 2020-UPDATE-TPPCPRC                                  
                   IF SQLCODE = +100                                            
                       DISPLAY 'TPPCPRC LOC ZERO NOT FOUND'                     
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                   ELSE                                                         
                       DISPLAY 'TPPCPRC LOC ZERO FOUND'                         
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                       PERFORM 2030-SELECT-TPPCPRC                              
                       PERFORM 2045-INSERT-TPPCPRC                              
                       IF SQLCODE = ZERO                                        
                           PERFORM 2095-UPDATE-TPPCPRC                          
                       END-IF                                                   
                   END-IF                                                       
                   PERFORM 7800-COMMIT                                          
               ELSE                                                             
                   PERFORM 2025-SELECT-TPPCPRC                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE SKXREF-SKU         TO PV-DISPLAY-8.                             
           IF CC-UPDATE-IND = 'U'                                               
              DISPLAY 'SKU PROCESSED/COMMITTED ' PV-DISPLAY-8                   
           ELSE                                                                 
              DISPLAY 'SKU PROCESSED           ' PV-DISPLAY-8                   
           END-IF.                                                              
                                                                                
           ADD +1 TO CR-RECS-PROCESSED-CNTR.                                    
                                                                                
           PERFORM 4000-READ-RECORD.                                            
                                                                                
      ******************************************************************        
      * 2010-SELECT-TSKXREF.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - READS TSKXREF                                             *        
      ******************************************************************        
       2010-SELECT-TSKXREF.                                                     
           MOVE '2010-SELECT-TSKXREF' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                      
               EXEC SQL                                                         
                   SELECT  ITM_NBR                                              
                          ,INTR_UPC_ID                                          
                     INTO :SKXREF-ITM-NBR                                       
                         ,:SKXREF-INTR-UPC-ID                                   
                     FROM  TSKXREF                                              
                    WHERE  SKU        = :SKXREF-SKU                             
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD +1      TO CR-SKUS-READ-CNTR                         
                   WHEN SQLCODE = +100                                          
                       ADD +1      TO CR-SKUS-NOT-FOUND-CNTR                    
                       MOVE 'READ TSKXREF TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'READ TSKXREF TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'READ TSKXREF TABLE'                                        
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2015-SELECT-TPPCPRC.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT THE CURRENT LOC ZERO ROW FROM THE TPPCPRC TABLE.   *        
      ******************************************************************        
       2015-SELECT-TPPCPRC.                                                     
           MOVE '2015-SELECT-TPPCPRC' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
               EXEC SQL                                                         
                   SELECT TSK_ITM_NBR                                           
                        , STRT_TMST                                             
                        , STR_NBR                                               
                        , END_TMST                                              
                        , MEITGP_NBR                                            
                        , RTL_AMT                                               
                        , STATUS_CDE                                            
                        , UID_NBR                                               
                    INTO :PPCPRC-TSK-ITM-NBR                                    
                        ,:PPCPRC-STRT-TMST                                      
                        ,:PPCPRC-STR-NBR                                        
                        ,:PPCPRC-END-TMST                                       
                        ,:PPCPRC-MEITGP-NBR                                     
                        ,:PPCPRC-RTL-AMT                                        
                        ,:PPCPRC-STATUS-CDE                                     
                        ,:PPCPRC-UID-NBR                                        
                     FROM TPPCPRC                                               
                    WHERE TSK_ITM_NBR    = :PPCPRC-TSK-ITM-NBR                  
                      AND STR_NBR        = :PPCPRC-STR-NBR                      
                      AND END_TMST       = '9999-09-09-23.59.00.000000'         
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       SET PS-CURRENT-ROW-FOUND       TO TRUE                   
      *                                                                         
      *  A 'NOT FOUND' IS VALID FOR A STORE SPECIFIC ROW, BUT SHOULD            
      *  NEVER HAPPEN FOR A '000' STORE ROW.                                    
      *                                                                         
                   WHEN SQLCODE = +100                                          
                       IF PPCPRC-STR-NBR  = ZEROES                              
                           MOVE 'READ TPPCPRC TABLE '                           
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                           PERFORM 9999-SQL-ABEND                               
                       ELSE                                                     
                           SET PS-CURRENT-ROW-NOT-FOUND TO TRUE                 
                       END-IF                                                   
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'READ TPPCPRC TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'READ TPPCPRC TABLE'                                        
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2020-UPDATE-TPPCPRC.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - UPDATE THE CURRENT LOC ZERO ROW FROM THE TPPCPRC TABLE.   *        
      ******************************************************************        
       2020-UPDATE-TPPCPRC.                                                     
           MOVE '2020-UPDATE-TPPCPRC' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    UPDATE TPPCPRC                                              
                        SET END_TMST      = :PV-PPCPRC-END-TMST                 
                           ,UID_NBR       = 'IMKUP992'                          
                        WHERE TSK_ITM_NBR = :PPCPRC-TSK-ITM-NBR                 
                          AND STR_NBR     = 0                                   
                          AND END_TMST    = '9999-09-09-23.59.00.000000'        
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                         ADD SQLERRD(3) TO CR-TPPCPRC-ROW-UPD-CNTR              
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'UPDATE TPPCPRC TABLE - STORE ZERO'               
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TPPCPRC TABLE - STORE ZERO'                         
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2025-SELECT-TPPCPRC.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT THE CURRENT LOC ROW FROM THE TPPCPRC TABLE.        *        
      ******************************************************************        
       2025-SELECT-TPPCPRC.                                                     
           MOVE '2025-SELECT-TPPCPRC' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    SELECT  STATUS_CDE                                          
                      INTO :PPCPRC-STATUS-CDE                                   
                      FROM TPPCPRC                                              
                    WHERE TSK_ITM_NBR  = :PPCPRC-TSK-ITM-NBR                    
                      AND STR_NBR      = :PPCPRC-STR-NBR                        
                      AND END_TMST     = '9999-09-09-23.59.00.000000'           
                    WITH UR                                                     
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                       DISPLAY 'TPPCPRC LOC ZERO FOUND'                         
                       DISPLAY 'ITM NBR           '  PV-DISPLAY-15              
                       DISPLAY 'LOC NBR           '  PV-DISPLAY-4               
                       DISPLAY 'LOC ZERO STAT CDE ' PPCPRC-STATUS-CDE           
                     WHEN SQLCODE = +100                                        
                       DISPLAY 'TPPCPRC LOC ZERO NOT FOUND'                     
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'SELECT TPPCPRC TABLE'                            
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TPPCPRC TABLE'                                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2030-SELECT-TPPCPRC.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT MAXIMUM STORE SPECIFIC RETAIL FROM TPPCPRC TABLE.  *        
      ******************************************************************        
       2030-SELECT-TPPCPRC.                                                     
           MOVE '2030-SELECT-TPPCPRC' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    SELECT  MAX(RTL_AMT)                                        
                      INTO :PPCPRC-RTL-AMT                                      
                      FROM TPPCPRC                                              
                    WHERE TSK_ITM_NBR  = :PPCPRC-TSK-ITM-NBR                    
                      AND STATUS_CDE   = 30                                     
                      AND STR_NBR      <> 0                                     
                      AND END_TMST     = '9999-09-09-23.59.00.000000'           
                    WITH UR                                                     
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
      *                DISPLAY 'TPPCPRC MAX RETAIL FOUND'                       
      *                DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
      *                DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                       CONTINUE                                                 
                     WHEN SQLCODE = +100                                        
                       MOVE ZERO TO PPCPRC-RTL-AMT                              
                       DISPLAY 'TPPCPRC MAX RETAIL NOT FOUND'                   
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'SELECT TPPCPRC RETAIL'                           
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TPPCPRC RETAIL'                                     
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2045-INSERT-TPPCPRC.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - INSERT A NEW STORE ROW FOR A UPC ID ON THE TPPCPRC TABLE. *        
      ******************************************************************        
       2045-INSERT-TPPCPRC.                                                     
           MOVE '2045-INSERT-TPPCPRC' TO PV-PARAGRAPH-NAME.                     
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           MOVE PV-PPCPRC-STRT-TMST          TO PPCPRC-STRT-TMST                
           MOVE '9999-09-09-23.59.00.000000' TO PPCPRC-END-TMST                 
                                                                                
           DISPLAY ':PV-PPCPRC-STRT-TMST ' PV-PPCPRC-STRT-TMST                  
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR SQLCODE = -803                                                
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   INSERT INTO TPPCPRC                                          
                           (TSK_ITM_NBR                                         
                           ,STRT_TMST                                           
                           ,STR_NBR                                             
                           ,END_TMST                                            
                           ,MEITGP_NBR                                          
                           ,RTL_AMT                                             
                           ,STATUS_CDE                                          
                           ,UID_NBR)                                            
                   VALUES (:PPCPRC-TSK-ITM-NBR                                  
                          ,:PPCPRC-STRT-TMST                                    
                          ,:PPCPRC-STR-NBR                                      
                          ,:PPCPRC-END-TMST                                     
                          ,:PPCPRC-MEITGP-NBR                                   
                          ,:PPCPRC-RTL-AMT                                      
                          ,30                                                   
                          ,'IMKUP992')                                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD SQLERRD(3) TO CR-TPPCPRC-ROW-INS-CNTR                
                   WHEN SQLCODE = -803                                          
                       DISPLAY 'DUPLICATE SKU NUMBER: ' PV-DISPLAY-8    0672    
                               ' STORE: ' PV-DISPLAY-4                  0672    
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'INSERT TPPCPRC TABLE'                              
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'INSERT TPPCPRC TABLE'                                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2095-UPDATE-TPPCPRC.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - UPDATE THE LOC SPECIFIC ROWS. SET END_TMST ON ROWS        *        
      *      WHERE RETAIL EQUALS RETAIL OF LOC ZERO ROW.               *        
      ******************************************************************        
       2095-UPDATE-TPPCPRC.                                                     
           MOVE '2095-UPDATE-TPPCPRC' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    UPDATE TPPCPRC                                              
                        SET END_TMST       = :PV-PPCPRC-END-TMST                
                           ,UID_NBR        = 'IMKUP992'                         
                        WHERE TSK_ITM_NBR = :PPCPRC-TSK-ITM-NBR                 
                          AND STR_NBR     <> 0                                  
                          AND RTL_AMT     = :PPCPRC-RTL-AMT                     
                          AND STATUS_CDE  = 30                                  
                          AND END_TMST    = '9999-09-09-23.59.00.000000'        
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                         ADD SQLERRD(3) TO CR-TPPCPRC-ROW-STR-CNTR              
                     WHEN SQLCODE = +100                                        
                         DISPLAY 'LOC SPECIFIC ROWS NOT FOUND FOR UPDT'         
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'UPDATE TPPCPRC TABLE - STORE SPECIFIC'           
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TPPCPRC TABLE - STORE SPECIFIC'                     
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 3000-EOJ-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - CHECK IF LAST SKU PROCESSED SHOULD BE COMPRESSED          *        
      *    - DELETE STYLE RESERVATION FOR LAST SKU PROCESSED           *        
      *    - CLOSE FILES                                               *        
      *    - DISPLAYS PROCESSING TOTALS                                *        
      *    - TAKES A FINAL COMMIT AND UPDATES THE CHECKPOINT STATUS    *        
      *      TO INDICATE SUCESSFULL COMPLETION                         *        
      ******************************************************************        
       3000-EOJ-PROCESSING.                                                     
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.                     
                                                                                
           CLOSE SKU-UPDATE-FILE                                                
                 CONTROL-CARD-FILE.                                             
                                                                                
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
      ******************************************************************        
      * 4000-READ-RECORD.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - READS THE NEXT RECORD IN THE INPUT FILE.                  *        
      ******************************************************************        
       4000-READ-RECORD.                                                        
           MOVE '4000-READ-RECORD' TO PV-PARAGRAPH-NAME.                        
                                                                                
           READ SKU-UPDATE-FILE                                                 
               INTO PV-SKU-RECORD                                               
                   AT END                                                       
                       MOVE 'Y'     TO PS-END-OF-FILE-SW                        
                   NOT AT END                                                   
                       ADD +1       TO PV-RECS-READ-CNTR                        
                       ADD 1        TO DI-INPUT-RECS-DISPLAY                    
                       MOVE PV-SKU  TO PV-DISPLAY-8                             
           END-READ.                                                            
                                                                                
      ******************************************************************07160094
      * READ CONTROL FILE FOR JOB NAME, XS ITEM QUEUE NAME, AND        *07170094
      * PROCESS OLD DATE INDICATOR.                                    *07170094
      ******************************************************************07180094
       4100-READ-CONTROL-CARD-1.                                        07140094
           MOVE '4100-READ-CONTROL-CARD-1'  TO PV-PARAGRAPH-NAME.       07150094
                                                                        07210094
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
               AT END                                                           
                   DISPLAY '*** NO CONTROL CARD 1 ***'                          
                   SET PV-ABEND-4001 TO TRUE                                    
                   PERFORM 9999-ABEND.                                          
                                                                                
           DISPLAY CC-CONTROL-CARD.                                             
                                                                                
           IF CC-BEG-DATE = SPACES                                              
               DISPLAY '*** INVALID BEGIN DATE ***'                             
               SET PV-ABEND-4004 TO TRUE                                        
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           IF CC-END-DATE = SPACES                                              
               DISPLAY '*** INVALID END DATE NAME ***'                          
               SET PV-ABEND-4005 TO TRUE                                        
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7200-SET-CURRENT-TIMESTAMP.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - GET THE CURRENT TIMESTAMP FROM DB2.                       *        
      ******************************************************************        
       7200-SET-CURRENT-TIMESTAMP.                                              
           MOVE '7200-SET-CURRENT-TIMESTAMP' TO PV-PARAGRAPH-NAME.              
                                                                                
           EXEC SQL                                                             
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE PV-CURRENT-TIMESTAMP (1:10) TO                          
                          PV-RUN-DATE                                           
               WHEN OTHER                                                       
                   MOVE 'ERROR ON SET OF TIMESTAMP '                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 7800-COMMIT.                                                   *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.           *        
      *    - TAKE A COMMIT.                                            *        
      ******************************************************************        
       7800-COMMIT.                                                             
           MOVE '7800-COMMIT'      TO PV-PARAGRAPH-NAME.                        
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               ADD +1              TO CR-COMMIT-CNTR                            
           ELSE                                                                 
               MOVE 'ERROR ON COMMIT'                                           
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 9990-DISPLAY-CONTROL-INFO.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
      ******************************************************************        
       9990-DISPLAY-CONTROL-INFO.                                               
                                                                                
           MOVE CR-RECS-PROCESSED-CNTR                                          
                                   TO DI-RECS-PROCESSED-CNTR.                   
           MOVE PV-RECS-READ-CNTR  TO CR-RECS-READ-CNTR.                        
           MOVE CR-RECS-READ-CNTR  TO DI-RECS-READ-CNTR.                        
           MOVE CR-SKUS-READ-CNTR  TO DI-SKUS-READ-CNTR.                        
           MOVE CR-SKUS-NOT-FOUND-CNTR                                          
                                   TO DI-SKUS-NOT-FOUND-CNTR.                   
           MOVE CR-SKU-STRS-BYPASSED-CNTR                                       
                                   TO DI-SKU-STRS-BYPASSED-CNTR.                
           MOVE CR-TPPCPRC-ROW-UPD-CNTR                                         
                                   TO DI-TPPCPRC-ROW-UPDATE-CNTR.               
           MOVE CR-TPPCPRC-ROW-INS-CNTR                                         
                                   TO DI-TPPCPRC-ROW-INSERT-CNTR.               
           MOVE CR-TPPCPRC-ROW-STR-CNTR                                         
                                   TO DI-TPPCPRC-ROW-UPDSTR-CNTR.               
           MOVE CR-TSKXREF-ROW-NOT-FOUND                                        
                                   TO DI-TSKXREF-ROW-NOT-FOUND.                 
           MOVE CR-COMMIT-CNTR     TO DI-COMMIT-CNTR.                           
                                                                                
           DISPLAY '********************************************'.              
           DISPLAY '**** RUN STATISTICS FOR PROGRAM IMKUP992 ***'.              
           DISPLAY '****'.                                                      
           DISPLAY 'INPUT RECS PROCESSED:  ' DI-RECS-PROCESSED-CNTR.            
           DISPLAY 'INPUT RECORDS READ:    ' DI-RECS-READ-CNTR.                 
           DISPLAY 'SKUS READ:             ' DI-SKUS-READ-CNTR.                 
           DISPLAY 'SKUS NOT FOUND:        ' DI-SKUS-NOT-FOUND-CNTR.            
           DISPLAY 'SKU/STORES BYPASSED:   ' DI-SKU-STRS-BYPASSED-CNTR.         
           DISPLAY 'TPPCPRC ROWS UPDATED:  ' DI-TPPCPRC-ROW-UPDATE-CNTR.        
           DISPLAY 'TPPCPRC ROWS INSERTED: ' DI-TPPCPRC-ROW-INSERT-CNTR.        
           DISPLAY 'TPPCPRC STR ROWS UPD:  ' DI-TPPCPRC-ROW-UPDSTR-CNTR.        
           DISPLAY 'TSKXREF ROWS NOT FND:  ' DI-TSKXREF-ROW-NOT-FOUND.          
           DISPLAY 'COMMITS TAKEN:         ' DI-COMMIT-CNTR.                    
           DISPLAY '****'.                                                      
           DISPLAY '**** RUN STATS END FOR PROGRAM IMKUP992  ***'.              
           DISPLAY '********************************************'.              
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND.                                                *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *        
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
                                                                                
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'IMKUP992 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                           
           DISPLAY 'INPUT SKU = ' PV-DISPLAY-8.                                 
           DISPLAY 'STORE NBR = ' PV-DISPLAY-4.                                 
      *    DISPLAY 'EFFECTIVE DATE = ' IM048-UIR-EFF-DTE.                       
                                                                                
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       9999-ABEND.                                                      07730094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PV-PARAGRAPH-NAME.         07780094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
