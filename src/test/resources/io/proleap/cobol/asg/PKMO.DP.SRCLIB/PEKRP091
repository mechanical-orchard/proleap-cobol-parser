       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         PEKRP091.                                            
       AUTHOR.             JIM ARENDT.                                          
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       FERRUARY 1998.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM PRODUCES THE 'IN STORE PRICE CHANGE REPORT'.  THE *        
      * REPORT IS USED TO TRACK IN-STORE PRICE CHANGES CREATED AND     *        
      * COMPLETED BY THE DC.  THE REPORT IS PRINTED AT WEEK/MONTH END  *        
      * AND IS SORTED BY ORIGINAL NAME, CREATOR ID, STORE NUMBER AND   *        
      * DOCUMENT NUMBER.  THE TOTAL ACTUAL PRICE CHANGE AMOUNT IS      *        
      * ACCUMULATED AND A TOTAL LINE IS PRINTED WHEN THE DC CHANGES.   *        
      * A GRAND TOTAL LINE IS PRINTED AT THE END OF THE PROGRAM.       *        
      *                                                                *        
      * THE PROGRAM READS A CONTROL FILE THAT CONTAINS THE WEEK/MONTH  *        
      * ENDING DATE THAT THE PROGRAM IS BEING RUN FOR.                 *        
      *                                                                *        
      * THIS REPORT RUNS ONCE EVERY WEEK AND AT MONTH END.             *        
      ******************************************************************        
      * 03/10/98 - JIM ARENDT -  WR 2872                               *        
      *     -  MODIFIED THE PROGRAM SO NO PRICE CHANGES CREATED AND    *        
      *        COMPLETED BY THE DC FOR CENTRAL STOCK WERE RETURNED.    *        
      ******************************************************************        
      * 04/08/98 - JIM ARENDT -  WR 2872                               *        
      *     -  CORRECTED PAGE BREAKING PROBLEM.                        *        
      ******************************************************************        
      * 01/10/00 - JIM ARENDT WR NO. PM00336970                        *        
      *    - CHANGED ALL ILB0ABNO TO ILBOABN0.                         *        
      ******************************************************************        
      * 02/25/05 - TCM -  WR NO. 27559                                 *        
      *    - CHANGED ALL STORE NUMBER LENGTH REFERENCES FROM 3 TO 4    *        
      *      DIGITS IN LENGTH                                          *        
      *    - REPLACED TSTR000 STORE TABLE CALL(S) WITH STORE TABLE     *        
      *      CALL(S) TO XST_LOC                                        *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
                                                                                
      ******************************************************************        
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CONTROL-FILE              ASSIGN TO INP01.                    
           SELECT REPORT-FILE               ASSIGN TO RPT01.                    
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-FILE                                                         
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
                                                                                
       01  CONTROL-RECORD                   PIC  X(80).                         
                                                                                
       FD  REPORT-FILE                                                          
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
                                                                                
       01  REPORT-RECORD                    PIC  X(132).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *    CONTROL RECORD                                              *        
      ******************************************************************        
       01  CR-CONTROL-RECORD.                                                   
           05  CR-CALENDAR-TYPE             PIC  X(02)    VALUE SPACE.          
           05  FILLER                       PIC  X(01)    VALUE SPACE.          
           05  CR-PERIOD-END-DATE.                                              
               10  CR-PERIOD-END-DTE-CC     PIC  X(02)    VALUE SPACE.          
               10  CR-PERIOD-END-DTE-YY     PIC  X(02)    VALUE SPACE.          
               10  FILLER                   PIC  X(01)    VALUE SPACE.          
               10  CR-PERIOD-END-DTE-MM     PIC  X(02)    VALUE SPACE.          
               10  FILLER                   PIC  X(01)    VALUE SPACE.          
               10  CR-PERIOD-END-DTE-DD     PIC  X(02)    VALUE SPACE.          
           05  FILLER                       PIC  X(01)    VALUE SPACE.          
           05  CR-MONTH-WEEK-VALUE          PIC  X(01)    VALUE SPACE.          
               88  CR-WEEK                                VALUE 'W'.            
               88  CR-MONTH                               VALUE 'M'.            
           05  FILLER                       PIC  X(65)    VALUE SPACE.          
                                                                                
      ******************************************************************        
      *    PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC  X(08)    VALUE                 
                                                          'PEKRP091'.           
           05  PC-OPERATING-COMPANY         PIC  X(02)    VALUE '03'.           
           05  PC-ONE                       PIC  9(01)    VALUE 1.              
           05  PC-TWO                       PIC  9(01)    VALUE 2.              
           05  PC-MAX-LINES-PER-PAGE        PIC S9(03)    VALUE +65             
                                                          COMP.                 
           05  PC-MAX-RETRIES               PIC S9(04)    VALUE +3              
                                                          COMP.                 
           05  PC-NOT-FOUND                 PIC S9(04)    VALUE +100            
                                                          COMP SYNC.            
           05  PC-UNAVAILABLE-RESOURCES     PIC S9(04)    VALUE -904            
                                                          COMP SYNC.            
           05  PC-DEADLOCK                  PIC S9(04)    VALUE -911            
                                                          COMP SYNC.            
           05  PC-DEADLOCK2                 PIC S9(04)    VALUE -913            
                                                          COMP SYNC.            
           05  PC-DATABASE-CONFLICT         PIC  X(31)    VALUE                 
               'DATABASE CONFLICT              '.                               
           05  PC-WEEK-VALUE                PIC  X(17)    VALUE                 
               ' FOR WEEK ENDING '.                                             
           05  PC-MONTH-VALUE               PIC  X(17)    VALUE                 
               'FOR MONTH ENDING '.                                             
           05  PC-NOT-FOUND-MSG             PIC  X(30)    VALUE                 
               '** NAME NOT FOUND **          '.                                
           05  PC-DC-TOTAL                  PIC  X(15)    VALUE                 
               '   DC TOTAL:   '.                                               
           05  PC-GRAND-TOTAL               PIC  X(15)    VALUE                 
               'GRAND TOTAL:   '.                                               
                                                                                
      ******************************************************************        
      *    PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACE.          
           05  PV-ADVANCE                   PIC  9(01)    VALUE ZERO.           
           05  PV-SELECT-COUNTER            PIC S9(04)    VALUE +0              
                                                          COMP SYNC.            
           05  PV-START-DATE                PIC  X(10)    VALUE SPACE.          
           05  PV-END-DATE                  PIC  X(10)    VALUE SPACE.          
           05  PV-HOLD-ORGN-LOC-NBR         PIC S9(04)    VALUE ZEROS.          
           05  PV-HOLD-STORE-NBR            PIC S9(04)    VALUE ZEROS.          
           05  PV-HOLD-CRE-ID               PIC  X(08)    VALUE SPACE.          
           05  PV-DC-TOTAL                  PIC S9(12)V9(2)                     
                                                          VALUE ZERO.           
           05  PV-GRAND-TOTAL               PIC S9(12)V9(2)                     
                                                          VALUE ZERO.           
           05  PV-DB2-OPERATION-ATTEMPTED   PIC  X(30)    VALUE SPACE.          
           05  PV-DB2-TABLE-NAME            PIC  X(12)    VALUE SPACE.          
           05  PV-DATE-EXPAND.                                                  
               10  PV-DATE-CCYY             PIC  X(04)    VALUE SPACE.          
               10  FILLER                   PIC  X(01)    VALUE SPACE.          
               10  PV-DATE-MM               PIC  X(02)    VALUE SPACE.          
               10  FILLER                   PIC  X(01)    VALUE SPACE.          
               10  PV-DATE-DD               PIC  X(02)    VALUE SPACE.          
           05  PV-REFORMAT-DATE.                                                
               10  PV-DATE-R-MM             PIC  X(02).                         
               10  FILLER                   PIC  X(01)    VALUE '/'.            
               10  PV-DATE-R-DD             PIC  X(02).                         
               10  FILLER                   PIC  X(01)    VALUE '/'.            
               10  PV-DATE-R-CCYY           PIC  X(04).                         
           05  PV-CURRENT-DATE.                                                 
               10  PV-CURRENT-YEAR          PIC  9(02)    VALUE ZERO.           
               10  PV-CURRENT-MONTH         PIC  9(02)    VALUE ZERO.           
               10  PV-CURRENT-DAY           PIC  9(02)    VALUE ZERO.           
           05  PV-CURRENT-TIME.                                                 
               10  PV-CURRENT-HOUR          PIC  9(02)    VALUE ZERO.           
               10  PV-CURRENT-MINUTE        PIC  9(02)    VALUE ZERO.           
               10  FILLER                   PIC  9(04)    VALUE ZERO.           
                                                                                
       01  ABEND-CODE                       PIC S9(04)    VALUE +0 COMP.        
                                                                                
      ******************************************************************        
      *    PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-NEED-DC-SW                PIC  X(01)    VALUE 'N'.            
               88  PS-NEED-DC                             VALUE 'Y'.            
           05  PS-NEED-STORE-SW             PIC  X(01)    VALUE 'N'.            
               88  PS-NEED-STORE                          VALUE 'Y'.            
           05  PS-NEED-CRE-ID-SW            PIC  X(01)    VALUE 'N'.            
               88  PS-NEED-CRE-ID                         VALUE 'Y'.            
           05  PS-END-OF-CSR-SW             PIC  X(01)    VALUE 'N'.            
               88  PS-END-OF-CSR                          VALUE 'Y'.            
                                                                                
      ******************************************************************        
      *    PROGRAM ACCUMULATORS                                        *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-LINE-COUNTER              PIC S9(11)    COMP-3                
                                                          VALUE ZERO.           
           05  PA-PAGE-COUNTER              PIC S9(11)    COMP-3                
                                                          VALUE ZERO.           
                                                                                
      ******************************************************************        
      *    STANDARD HEADER FOR 132 COLUMN REPORT                       *        
      ******************************************************************        
           COPY DPWS132O.                                                       
                                                                                
      ******************************************************************        
      *    PERMANENT PRICE CHANGE COPYBOOK                             *        
      ******************************************************************        
           COPY PEWS049.                                                        
                                                                                
      ******************************************************************        
      *    HEADING/DETAIL LINES                                        *        
      ******************************************************************        
       01  REPORT-TITLE-LINE1.                                                  
           05  FILLER                         PIC  X(51)  VALUE SPACE.          
           05  FILLER                         PIC  X(28)  VALUE                 
               'IN STORE PRICE CHANGE REPORT'.                                  
           05  FILLER                         PIC  X(53)  VALUE SPACE.          
                                                                                
       01  REPORT-TITLE-LINE2.                                                  
           05  FILLER                         PIC  X(52)  VALUE SPACE.          
           05  RPT-TL2-TIME-PERIOD            PIC  X(17)  VALUE SPACE.          
           05  RPT-TL2-WEEKLY-MM              PIC  X(02)  VALUE SPACE.          
           05  FILLER                         PIC  X(01)  VALUE '/'.            
           05  RPT-TL2-WEEKLY-DD              PIC  X(02)  VALUE SPACE.          
           05  FILLER                         PIC  X(01)  VALUE '/'.            
           05  RPT-TL2-WEEKLY-CC              PIC  X(02)  VALUE SPACE.          
           05  RPT-TL2-WEEKLY-YY              PIC  X(02)  VALUE SPACE.          
           05  FILLER                         PIC  X(44)  VALUE SPACE.          
                                                                                
       01  REPORT-STORE-HEADING1.                                               
           05  FILLER                         PIC  X(03)  VALUE SPACE.          
           05  FILLER                         PIC  X(39)  VALUE                 
               'DC     STORE   DOCUMENT   TYPE   REASON'.                       
           05  FILLER                         PIC  X(05)  VALUE SPACE.          
           05  FILLER                         PIC  X(33)  VALUE                 
               'ISSUE        ENTERED        TOTAL'.                             
           05  FILLER                         PIC  X(07)  VALUE SPACE.          
           05  FILLER                         PIC  X(23)  VALUE                 
               'CREATE           CREATE'.                                       
           05  FILLER                         PIC  X(22)  VALUE SPACE.          
                                                                                
       01  REPORT-STORE-HEADING2.                                               
           05  FILLER                         PIC  X(41)  VALUE                 
               ' NUMBER  NUMBER    NUMBER    CODE    CODE'.                     
           05  FILLER                         PIC  X(06)  VALUE SPACE.          
           05  FILLER                         PIC  X(34)  VALUE                 
               'DATE          DATE          RETAIL'.                            
           05  FILLER                         PIC  X(08)  VALUE SPACE.          
           05  FILLER                         PIC  X(02)  VALUE                 
               'ID'.                                                            
           05  FILLER                         PIC  X(14)  VALUE SPACE.          
           05  FILLER                         PIC  X(04)  VALUE                 
               'NAME'.                                                          
           05  FILLER                         PIC  X(23)  VALUE SPACE.          
                                                                                
       01  REPORT-STORE-HEADING3.                                               
           05  FILLER                         PIC  X(41)  VALUE                 
               ' ------  ------   -------    ----    ----'.                     
           05  FILLER                         PIC  X(04)  VALUE SPACE.          
           05  FILLER                         PIC  X(39)  VALUE                 
               '----------   ----------    ------------'.                       
           05  FILLER                         PIC  X(10)  VALUE                 
               '   -------'.                                                    
           05  FILLER                         PIC  X(04)  VALUE SPACE.          
           05  FILLER                         PIC  X(31)  VALUE                 
               '-------------------------------'.                               
           05  FILLER                         PIC  X(03)  VALUE SPACE.          
                                                                                
       01  REPORT-DETAIL-LINE.                                                  
           05  FILLER                         PIC  X(02)  VALUE SPACE.          
           05  RPT-DTL-DC-NBR                 PIC  ZZZZ.                        
           05  FILLER                         PIC  X(04)  VALUE SPACE.          
           05  RPT-DTL-STORE-NBR              PIC  ZZZZ.                        
           05  FILLER                         PIC  X(04)  VALUE SPACE.          
           05  RPT-DTL-DOCUMENT-NBR           PIC  ZZZZZZZ.                     
           05  FILLER                         PIC  X(06)  VALUE SPACE.          
           05  RPT-DTL-TYPE-CODE              PIC  X(01).                       
           05  FILLER                         PIC  X(06)  VALUE SPACE.          
           05  RPT-DTL-REASON-CODE            PIC  ZZ.                          
           05  FILLER                         PIC  X(05)  VALUE SPACE.          
           05  RPT-DTL-ISSUE-DATE             PIC  X(10).                       
           05  FILLER                         PIC  X(03)  VALUE SPACE.          
           05  RPT-DTL-ENTERED-DATE           PIC  X(10).                       
           05  FILLER                         PIC  X(03)  VALUE SPACE.          
           05  RPT-DTL-RETAIL-AMT             PIC  ZZ,ZZZ,ZZZ.99.               
           05  FILLER                         PIC  X(03)  VALUE SPACE.          
           05  RPT-DTL-CREATE-ID              PIC  X(08).                       
           05  FILLER                         PIC  X(04)  VALUE SPACE.          
           05  RPT-DTL-CREATE-NAME            PIC  X(30).                       
           05  FILLER                         PIC  X(03)  VALUE SPACE.          
                                                                                
       01  REPORT-TOTAL-LINE.                                                   
           05  FILLER                         PIC  X(53)  VALUE SPACE.          
           05  RPT-TOTAL-VARIABLE             PIC  X(15)  VALUE SPACE.          
           05  RPT-TOTAL-AMT                  PIC  Z,ZZZ,ZZZ,ZZZ.99.            
           05  FILLER                         PIC  X(48)  VALUE SPACE.          
                                                                                
       01  REPORT-BLANK-LINE                  PIC  X(132) VALUE SPACE.          
                                                                                
       01  REPORT-ENDING-LINE.                                                  
           05  FILLER                         PIC  X(90)  VALUE SPACE.          
           05  FILLER                         PIC  X(19)  VALUE                 
               '***END OF REPORT***'.                                           
           05  FILLER                         PIC  X(91)  VALUE SPACE.          
                                                                                
      ******************************************************************        
      *    DATE ROUTINE COPY MEMBERS                                   *        
      ******************************************************************        
           COPY DPWS500.                                                        
                                                                                
      ******************************************************************        
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *    STORE PPC HEADER TABLE                                      *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSTRPPC                                                  
           END-EXEC.                                                            
      ******************************************************************        
      *    SELLING AND CUSTOMER SERVICE LOCATION TABLE                          
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE XST00001                                                
           END-EXEC.                                                            
      ******************************************************************        
      *    WORKER NAME TABLE                                           *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TWORKER                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    STANDARD LAYOUT FOR THE SQL COMMUNCATIONS AREA              *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                DECLARE DC_CSR CURSOR                                           
                FOR SELECT                                                      
                      A.DOC_NBR                                                 
                    , A.DEST_LOC_NBR                                            
                    , A.ORGN_LOC_NBR                                            
                    , A.EFF_DTE                                                 
                    , A.TYP_CDE                                                 
                    , A.RSN_CDE                                                 
                    , A.STATUS_CDE                                              
                    , A.PREV_STATUS_CDE                                         
                    , A.TOT_ACT_PR_CHG_AMT                                      
                    , A.CRE_DTE                                                 
                    , A.ENTR_DTE                                                
                    , A.CRE_ID                                                  
                FROM                                                            
                      TSTRPPC AS A                                              
                    , XST_LOC AS S                                              
                WHERE A.DEST_LOC_NBR  = S.LOC_NBR                               
                  AND A.DEST_LOC_NBR ^= A.ORGN_LOC_NBR                          
                  AND A.STATUS_CDE    = :PE049-COMPLETED-STATUS-CDE             
                  AND S.LOC_TYP_CDE NOT IN                                      
                                         (:PE049-DISTRB-CNTR-LOC-TYP-CDE        
                                         ,:PE049-RET-TO-VND-LOC-TYP-CDE         
                                         ,:PE049-CENT-STK-LOC-TYP-CDE)          
                  AND A.ENTR_DTE BETWEEN :PV-START-DATE                         
                                     AND :PV-END-DATE                           
                  AND EXISTS                                                    
                     (SELECT *                                                  
                         FROM XST_LOC                                           
                          WHERE LOC_NBR     = A.ORGN_LOC_NBR                    
                            AND LOC_TYP_CDE =                                   
                                        :PE049-DISTRB-CNTR-LOC-TYP-CDE)         
                ORDER BY                                                        
                      A.ORGN_LOC_NBR                                            
                    , A.CRE_ID                                                  
                    , A.DEST_LOC_NBR                                            
                    , A.DOC_NBR                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAINLINE.                                                           
                                                                                
           PERFORM A150-INITIALIZE-RTN.                                         
                                                                                
           PERFORM B200-PROCESS-FILE-RTN.                                       
                                                                                
           PERFORM Z995-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *     - OPEN THE INPUT/OUTPUT FILES.                             *        
      *     - READ THE CONTROL RECORD.                                 *        
      *     - ACCEPTS THE CURRENT DATE AND TIME FOR THE REPORT HEADING *        
      ******************************************************************        
       A150-INITIALIZE-RTN.                                                     
                                                                                
           OPEN INPUT  CONTROL-FILE.                                            
           OPEN OUTPUT REPORT-FILE.                                             
                                                                                
           READ CONTROL-FILE INTO CR-CONTROL-RECORD                             
             AT END                                                             
               DISPLAY ' '                                                      
               DISPLAY '** ABEND     **'                                        
               DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME                    
               DISPLAY '** PARAGRAPH **  = A150-INITIALIZE-RTN'                 
               DISPLAY '** CONTROL FILE IS EMPTY  '                             
               DISPLAY ' '                                                      
               MOVE +4002                TO ABEND-CODE                          
               CALL 'ILBOABN0' USING ABEND-CODE.                                
                                                                                
           DISPLAY ' '                                                          
           DISPLAY 'CONTROLS FOR PROGRAM ' PC-PROGRAM-NAME ': '                 
           DISPLAY ' '                                                          
           DISPLAY ' CALENDAR TYPE:  ' CR-CALENDAR-TYPE                         
           DISPLAY 'PERIOD END DTE:  ' CR-PERIOD-END-DATE                       
           DISPLAY '   TYPE OF RUN:  ' CR-MONTH-WEEK-VALUE                      
           DISPLAY ' '                                                          
                                                                                
           MOVE CR-PERIOD-END-DATE       TO PV-END-DATE.                        
           MOVE CR-PERIOD-END-DTE-MM     TO RPT-TL2-WEEKLY-MM.                  
           MOVE CR-PERIOD-END-DTE-DD     TO RPT-TL2-WEEKLY-DD.                  
           MOVE CR-PERIOD-END-DTE-CC     TO RPT-TL2-WEEKLY-CC.                  
           MOVE CR-PERIOD-END-DTE-YY     TO RPT-TL2-WEEKLY-YY.                  
                                                                                
           IF CR-WEEK                                                           
               MOVE PC-WEEK-VALUE        TO RPT-TL2-TIME-PERIOD                 
               MOVE PC-ONE               TO DP132O-REPORT-NUMBER                
           ELSE                                                                 
               MOVE PC-MONTH-VALUE       TO RPT-TL2-TIME-PERIOD                 
               MOVE PC-TWO               TO DP132O-REPORT-NUMBER                
           END-IF.                                                              
           PERFORM Y100-SUBTRACT-WEEK-MTH.                                      
                                                                                
           ACCEPT PV-CURRENT-DATE      FROM DATE.                               
           MOVE PV-CURRENT-MONTH         TO DP132O-RUN-MONTH.                   
           MOVE PV-CURRENT-DAY           TO DP132O-RUN-DAY.                     
           MOVE PV-CURRENT-YEAR          TO DP132O-RUN-YEAR.                    
                                                                                
           ACCEPT PV-CURRENT-TIME      FROM TIME.                               
           MOVE PV-CURRENT-HOUR          TO DP132O-RUN-HOUR.                    
           MOVE PV-CURRENT-MINUTE        TO DP132O-RUN-MINUTE.                  
                                                                                
           MOVE PC-PROGRAM-NAME          TO DP132O-PROGRAM-NAME.                
                                                                                
      ******************************************************************        
       B200-PROCESS-FILE-RTN.                                                   
                                                                                
           PERFORM C200-OPEN-FETCH-RTN.                                         
                                                                                
           PERFORM D200-PROCESS-LOOP                                            
               UNTIL PS-END-OF-CSR.                                             
                                                                                
           PERFORM X300-CLOSE-DC-CSR.                                           
                                                                                
           PERFORM F100-WRITE-DC-TOTAL.                                         
                                                                                
           PERFORM H100-WRITE-GRAND-TOTAL.                                      
                                                                                
      ******************************************************************        
      *    OPEN THE CURSOR AND READ THE FIRST RECORD.                  *        
      ******************************************************************        
       C200-OPEN-FETCH-RTN.                                                     
                                                                                
           PERFORM X100-OPEN-DC-CSR.                                            
                                                                                
           PERFORM X200-FETCH-DC-CSR.                                           
                                                                                
           MOVE 'Y'                       TO PS-NEED-DC-SW                      
                                             PS-NEED-STORE-SW                   
                                             PS-NEED-CRE-ID-SW.                 
                                                                                
           MOVE STRPPC-ORGN-LOC-NBR       TO PV-HOLD-ORGN-LOC-NBR.              
           MOVE STRPPC-DEST-LOC-NBR       TO PV-HOLD-STORE-NBR.                 
           MOVE STRPPC-CRE-ID             TO PV-HOLD-CRE-ID.                    
                                                                                
           PERFORM R500-PROCESS-PAGE-BREAK.                                     
                                                                                
      ******************************************************************        
      *    EVALUATE EACH RECORD READ:                                  *        
      *     - IF DC = HOLD DC                                          *        
      *           WRITE DETAIL LINE                                    *        
      *     - IF DC NOT = HOLD DC                                      *        
      *           WRITE DC TOTAL LINE                                  *        
      *           WRITE DETAIL LINE                                    *        
      ******************************************************************        
       D200-PROCESS-LOOP.                                                       
                                                                                
           IF STRPPC-ORGN-LOC-NBR = PV-HOLD-ORGN-LOC-NBR                        
               PERFORM E100-WRITE-DETAIL-LINE                                   
           ELSE                                                                 
               PERFORM F100-WRITE-DC-TOTAL                                      
               PERFORM E100-WRITE-DETAIL-LINE                                   
           END-IF.                                                              
                                                                                
           PERFORM X200-FETCH-DC-CSR.                                           
                                                                                
      ******************************************************************        
       E100-WRITE-DETAIL-LINE.                                                  
                                                                                
           IF PS-NEED-DC                                                        
               MOVE 'N'                   TO PS-NEED-DC-SW                      
               MOVE PV-HOLD-ORGN-LOC-NBR  TO RPT-DTL-DC-NBR                     
           ELSE                                                                 
               MOVE ZEROS                 TO RPT-DTL-DC-NBR                     
           END-IF.                                                              
                                                                                
           IF PS-NEED-STORE                                                     
           OR STRPPC-DEST-LOC-NBR NOT = PV-HOLD-STORE-NBR                       
               MOVE 'N'                   TO PS-NEED-STORE-SW                   
               MOVE STRPPC-DEST-LOC-NBR   TO RPT-DTL-STORE-NBR                  
                                             PV-HOLD-STORE-NBR                  
           ELSE                                                                 
               MOVE ZEROS                 TO RPT-DTL-STORE-NBR                  
           END-IF.                                                              
                                                                                
           MOVE STRPPC-DOC-NBR            TO RPT-DTL-DOCUMENT-NBR.              
           MOVE STRPPC-TYP-CDE            TO RPT-DTL-TYPE-CODE.                 
           MOVE STRPPC-RSN-CDE            TO RPT-DTL-REASON-CODE.               
                                                                                
           MOVE STRPPC-CRE-DTE            TO PV-DATE-EXPAND.                    
           MOVE PV-DATE-MM                TO PV-DATE-R-MM.                      
           MOVE PV-DATE-DD                TO PV-DATE-R-DD.                      
           MOVE PV-DATE-CCYY              TO PV-DATE-R-CCYY.                    
           MOVE PV-REFORMAT-DATE          TO RPT-DTL-ISSUE-DATE.                
                                                                                
           MOVE STRPPC-ENTR-DTE           TO PV-DATE-EXPAND.                    
           MOVE PV-DATE-MM                TO PV-DATE-R-MM.                      
           MOVE PV-DATE-DD                TO PV-DATE-R-DD.                      
           MOVE PV-DATE-CCYY              TO PV-DATE-R-CCYY.                    
           MOVE PV-REFORMAT-DATE          TO RPT-DTL-ENTERED-DATE.              
                                                                                
           MOVE STRPPC-TOT-ACT-PR-CHG-AMT TO RPT-DTL-RETAIL-AMT.                
           ADD  STRPPC-TOT-ACT-PR-CHG-AMT TO PV-DC-TOTAL.                       
                                                                                
           MOVE STRPPC-CRE-ID             TO RPT-DTL-CREATE-ID.                 
                                                                                
           IF (PS-NEED-CRE-ID                                                   
           OR  STRPPC-CRE-ID NOT = PV-HOLD-CRE-ID)                              
           AND STRPPC-CRE-ID > SPACES                                           
               MOVE 'N'                   TO PS-NEED-CRE-ID-SW                  
               PERFORM X400-SELECT-CREATOR-NAME                                 
               MOVE STRPPC-CRE-ID         TO PV-HOLD-CRE-ID                     
           ELSE                                                                 
               MOVE 'N'                   TO PS-NEED-CRE-ID-SW                  
               MOVE SPACES                TO RPT-DTL-CREATE-NAME                
           END-IF.                                                              
                                                                                
           MOVE PC-ONE                    TO PV-ADVANCE.                        
           MOVE REPORT-DETAIL-LINE        TO REPORT-BLANK-LINE.                 
                                                                                
           PERFORM R200-PRINT-LINE.                                             
                                                                                
      ******************************************************************        
      *     - WRITE THE DC TOTAL LINE                                  *        
      *     - ACCUMULATE THE GRAND TOTAL                               *        
      ******************************************************************        
       F100-WRITE-DC-TOTAL.                                                     
                                                                                
           MOVE PC-DC-TOTAL               TO RPT-TOTAL-VARIABLE.                
           MOVE PV-DC-TOTAL               TO RPT-TOTAL-AMT.                     
           ADD  PV-DC-TOTAL               TO PV-GRAND-TOTAL.                    
                                                                                
           MOVE REPORT-TOTAL-LINE         TO REPORT-BLANK-LINE.                 
           MOVE PC-TWO                    TO PV-ADVANCE.                        
           PERFORM R200-PRINT-LINE.                                             
                                                                                
           MOVE STRPPC-ORGN-LOC-NBR       TO PV-HOLD-ORGN-LOC-NBR.              
           MOVE STRPPC-DEST-LOC-NBR       TO PV-HOLD-STORE-NBR.                 
           MOVE 'Y'                       TO PS-NEED-DC-SW                      
                                             PS-NEED-CRE-ID-SW                  
                                             PS-NEED-STORE-SW.                  
                                                                                
           MOVE ZERO                      TO PV-DC-TOTAL.                       
                                                                                
           MOVE SPACES                    TO REPORT-BLANK-LINE.                 
           MOVE PC-ONE                    TO PV-ADVANCE.                        
           PERFORM R200-PRINT-LINE.                                             
                                                                                
      ******************************************************************        
      *    WRITE THE GRAND TOTAL LINE                                  *        
      ******************************************************************        
       H100-WRITE-GRAND-TOTAL.                                                  
                                                                                
           MOVE PC-GRAND-TOTAL            TO RPT-TOTAL-VARIABLE.                
           MOVE PV-GRAND-TOTAL            TO RPT-TOTAL-AMT.                     
           MOVE REPORT-TOTAL-LINE         TO REPORT-BLANK-LINE.                 
           MOVE PC-TWO                    TO PV-ADVANCE.                        
                                                                                
           PERFORM R200-PRINT-LINE.                                             
                                                                                
      ******************************************************************        
       R200-PRINT-LINE.                                                         
                                                                                
           WRITE REPORT-RECORD                                                  
              FROM REPORT-BLANK-LINE                                            
                AFTER PV-ADVANCE.                                               
                                                                                
           ADD PV-ADVANCE                 TO PA-LINE-COUNTER.                   
           MOVE SPACES                    TO REPORT-BLANK-LINE.                 
                                                                                
           IF PA-LINE-COUNTER >= PC-MAX-LINES-PER-PAGE                          
               PERFORM R500-PROCESS-PAGE-BREAK                                  
               MOVE 'Y'                   TO PS-NEED-DC-SW                      
                                             PS-NEED-CRE-ID-SW                  
                                             PS-NEED-STORE-SW                   
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *   ADVANCE TO A NEW PAGE AND WRITE THE REPORT HEADINGS          *        
      ******************************************************************        
       R500-PROCESS-PAGE-BREAK.                                                 
                                                                                
           ADD PC-ONE                     TO PA-PAGE-COUNTER.                   
           MOVE PA-PAGE-COUNTER           TO DP132O-PAGE-NUMBER.                
                                                                                
           WRITE REPORT-RECORD                                                  
             FROM DP132O-STANDRD-HEADER-132-COLS                                
               AFTER ADVANCING PAGE.                                            
                                                                                
           INITIALIZE DP132O-LASER-NEW-PAGE-INDICATR.                           
                                                                                
           WRITE REPORT-RECORD                                                  
             FROM REPORT-TITLE-LINE1                                            
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE REPORT-RECORD                                                  
             FROM REPORT-TITLE-LINE2                                            
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE REPORT-RECORD                                                  
             FROM REPORT-STORE-HEADING1                                         
               AFTER ADVANCING 3 LINES.                                         
                                                                                
           WRITE REPORT-RECORD                                                  
             FROM REPORT-STORE-HEADING2                                         
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE REPORT-RECORD                                                  
             FROM REPORT-STORE-HEADING3                                         
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE +8                        TO PA-LINE-COUNTER.                   
                                                                                
      ******************************************************************        
      *  OPEN THE DISTRIBUTION CENTER CURSOR                           *        
      ******************************************************************        
       X100-OPEN-DC-CSR.                                                        
                                                                                
           EXEC SQL                                                             
               OPEN DC_CSR                                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'X100-OPEN-DC-CSR '                                     
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'TSTRPPC '   TO PV-DB2-TABLE-NAME                       
                   MOVE 'OPEN CURSOR '                                          
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  FETCH A ROW FROM THE DISTRIBUTION CENTER CURSOR               *        
      ******************************************************************        
       X200-FETCH-DC-CSR.                                                       
                                                                                
           EXEC SQL                                                             
               FETCH  DC_CSR                                                    
                INTO  :STRPPC-DOC-NBR                                           
                    , :STRPPC-DEST-LOC-NBR                                      
                    , :STRPPC-ORGN-LOC-NBR                                      
                    , :STRPPC-EFF-DTE                                           
                    , :STRPPC-TYP-CDE                                           
                    , :STRPPC-RSN-CDE                                           
                    , :STRPPC-STATUS-CDE                                        
                    , :STRPPC-PREV-STATUS-CDE                                   
                    , :STRPPC-TOT-ACT-PR-CHG-AMT                                
                    , :STRPPC-CRE-DTE                                           
                    , :STRPPC-ENTR-DTE                                          
                    , :STRPPC-CRE-ID                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN SQLCODE = PC-NOT-FOUND                                      
                   SET PS-END-OF-CSR TO TRUE                                    
               WHEN OTHER                                                       
                   MOVE 'X200-FETCH-DC-CSR '                                    
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'TSTRPPC '   TO PV-DB2-TABLE-NAME                       
                   MOVE 'FETCH CURSOR '                                         
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  CLOSE THE DISTRIBUTION CENTER CURSOR                          *        
      ******************************************************************        
       X300-CLOSE-DC-CSR.                                                       
                                                                                
           EXEC SQL                                                             
               CLOSE DC_CSR                                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'X300-CLOSE-DC-CSR '                                    
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'TSTRPPC '   TO PV-DB2-TABLE-NAME                       
                   MOVE 'CLOSE CURSOR '                                         
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  THIS ROUTINE IS USED TO SELECT THE NAME OF THE USER           *        
      *  WHO CREATED THE DOCUMENT.                                     *        
      ******************************************************************        
       X400-SELECT-CREATOR-NAME.                                                
                                                                                
           MOVE SPACES                   TO RPT-DTL-CREATE-NAME.                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SELECT-COUNTER                                        
               FROM 1 BY 1                                                      
               UNTIL ((PV-SELECT-COUNTER > PC-MAX-RETRIES)                      
                         OR (SQLCODE = ZERO)                                    
                         OR (SQLCODE = PC-NOT-FOUND))                           
                                                                                
               EXEC SQL                                                         
                   SELECT                                                       
                       FIR_NAME,                                                
                       LAST_NAME                                                
                   INTO                                                         
                       :WORKER-FIR-NAME,                                        
                       :WORKER-LAST-NAME                                        
                   FROM TWORKER                                                 
                   WHERE (OPERCO_NBR = :PC-OPERATING-COMPANY                    
                     AND  UID_NBR    = :STRPPC-CRE-ID)                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       STRING WORKER-FIR-NAME  DELIMITED BY '  '                
                              SPACE            DELIMITED BY SIZE                
                              WORKER-LAST-NAME DELIMITED BY SIZE                
                                       INTO RPT-DTL-CREATE-NAME                 
                   WHEN SQLCODE = PC-NOT-FOUND                                  
                       MOVE PC-NOT-FOUND-MSG                                    
                                         TO RPT-DTL-CREATE-NAME                 
                   WHEN SQLCODE = PC-DEADLOCK                                   
                   WHEN SQLCODE = PC-DEADLOCK2                                  
                   WHEN SQLCODE = PC-UNAVAILABLE-RESOURCES                      
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE 'X400-SELECT-CREATOR-NAME '                         
                                         TO PV-PARAGRAPH-NAME                   
                       MOVE 'TWORKER '   TO PV-DB2-TABLE-NAME                   
                       MOVE 'SELECT CREATOR NAME '                              
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                       PERFORM Z999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-SELECT-COUNTER > PC-MAX-RETRIES                                
               MOVE PC-DATABASE-CONFLICT TO RPT-DTL-CREATE-NAME                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WHEN RUNNING THIS JOB AT WEEK END, CALL THE DATE              *        
      *  ROUTINE TO DETERMINE THE FIRST DAY OF THE WEEK.               *        
      *  WHEN RUNNING THIS JOB AT MONTH END, CALL THE DATE             *        
      *  ROUTINE TO DETERMINE THE FIRST DAY OF THE FISCAL MONTH.       *        
      *  THIS DATE WILL BE USED IN THE CALL TO TABLE TSTRPPC           *        
      *  AS THE START DATE.                                            *        
      ******************************************************************        
       Y100-SUBTRACT-WEEK-MTH.                                                  
                                                                                
           ACCEPT DPG51-SYSTEM-DATE FROM DATE.                                  
           MOVE CR-CALENDAR-TYPE           TO DPG51-CALENDAR-TYPE.              
           SET DPG51-DO-NOT-INCR-DECR-DATE TO TRUE.                             
                                                                                
           MOVE CR-PERIOD-END-DATE         TO DPG52-LK-DATE-INPUT.              
           SET DPG52-LK-DTE-ISO-FMT        TO TRUE.                             
           INITIALIZE DPG53.                                                    
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                        
                                                   DPG52                        
                                                   DPG53                        
                                                   DPG54                        
                                                   DPG55                        
                                                   DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
               WHEN DPG54-DATE-INVALID                                          
                   DISPLAY ' '                                                  
                   DISPLAY '** ABEND     **'                                    
                   DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME                
                   DISPLAY '** PARAGRAPH **  = Y100-SUBTRACT-WEEK-MTH '         
                   DISPLAY '** BAD CALL TO CALENDAR MODULE '                    
                       DP500-KOHLS-CALENDAR-ROUTINE                             
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   DISPLAY ' '                                                  
                   MOVE +4019 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               WHEN CR-WEEK                                                     
                   MOVE DPG55-1ST-WKDY-DB2ISO-FMT                               
                                               TO PV-START-DATE                 
               WHEN CR-MONTH                                                    
                   MOVE DPG56-FIRST-FP-DB2-ISO-FMT                              
                                               TO PV-START-DATE                 
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *     - WRITES AN END OF REPORT LINE TO THE REPORT               *        
      ******************************************************************        
       Z995-EOJ-ROUTINE.                                                        
                                                                                
           ADD PC-TWO                     TO PA-LINE-COUNTER.                   
                                                                                
           IF PA-LINE-COUNTER > PC-MAX-LINES-PER-PAGE                           
               PERFORM R500-PROCESS-PAGE-BREAK                                  
           END-IF.                                                              
                                                                                
           WRITE REPORT-RECORD                                                  
             FROM REPORT-ENDING-LINE                                            
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           PERFORM Z997-CLOSE-FILES.                                            
                                                                                
      ******************************************************************        
      *     - CLOSES THE FILES                                         *        
      ******************************************************************        
       Z997-CLOSE-FILES.                                                        
                                                                                
           CLOSE CONTROL-FILE                                                   
                 REPORT-FILE.                                                   
                                                                                
      ******************************************************************        
      *  -PERFORMS ABEND PROCESSING FOR SQL ERRORS.                    *        
      ******************************************************************        
       Z999-SQL-ABEND.                                                          
                                                                                
           DISPLAY 'Z999-SQL-ABEND'.                                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** = ' PC-PROGRAM-NAME.                       
           DISPLAY '**  PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                     
           DISPLAY '**  OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.            
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
      * FORMAT.                                                        *        
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
