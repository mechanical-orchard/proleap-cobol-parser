       IDENTIFICATION DIVISION.                                         00010002
                                                                        00020002
       PROGRAM-ID.      TFKUT005.                                       00030002
       AUTHOR.          THOMAS HANSON.                                  00040002
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00050002
       DATE-WRITTEN.    11/30/09.                                       00060002
       DATE-COMPILED.                                                   00070002
                                                                        00080002
      ******************************************************************00090002
      *  MODULE TYPE:   COBOL2 BATCH.                                   00100002
      *                                                                 00110002
      * THIS PROGRAM PREPARES THE INVENTORY SNAPSHOT UNLOAD FILE TO BE  00120002
      * PROCESSED BY TFKUP001 UNDER THE DB2 CHECKPOINT RESTART SYSTEM.  00130002
      *                                                                 00140002
      * THE PROGRAM READS THE INVENTORY SNAPSHOT UNLOAD FILE, FORMATS   00150002
      * RECORDS AND SETS A CHECKPOINT INDICATOR ON THE RECORD BASED     00160002
      * ON HOW CHECKPOINTS WILL BE TAKEN BY THE MAINTENANCE PROGRAM.    00170002
      * AFTER A RECORD HAS BEEN REFORMATTED IT IS PASSED TO THE         00180002
      * TRANSACTION PREPARATION MODULE (DPKUT900) WHICH DOES SOME MORE  00190002
      * PROCESSING ON THE RECORDS AND WRITES THE REFORMATTED RECORDS    00200002
      * OUT TO A SEQUENTIAL FILE.                                       00210002
      *                                                                 00220002
      * THE PROGRAM COMMUNICATES WITH THE TRANSACTION PREPARATION       00230002
      * MODULE USING FUNCTION CODES (OPEN, WRITE, & CLOSE) AND CHECKS   00240002
      * THE RETURN CODE AFTER EACH REQUEST.  IF AN INVALID RETURN CODE  00250002
      * IS RECEIVED, ANY INFORMATION THAT MAY BE HELPFUL IN RESOLVING   00260002
      * THE BAD RETURN CODE IS DISPLAYED ON SYSOUT AND THE PROGRAM      00270002
      * ABENDS.                                                         00280002
      *                                                                 00290002
      *========================== CHANGE LOG ===========================00300002
      *  AUTH        BY           DATE    DESCRIPTION                   00310009
      *=================================================================00320002
      * WR37120     T. HANSON   11-30-09  INITIAL IMPLEMENTATION.       00330009
      *                                                                 00340002
      ******************************************************************00350002
      ******************************************************************00351007
SR3403**DATE:03/10/2012 MADE BY: ANUJ GUPTA LARGE RETAIL EXPANSION      00352007
      ******************************************************************00353007
260107* CHG0260107  JIM HUNTER  08/17/21  ADD COPYBOOK DPWS990 TO       00354009
260107*                                   MAKE CALL TO DPKUT900 DYNAMIC.00354109
      ******************************************************************00355009
                                                                        00360002
       ENVIRONMENT DIVISION.                                            00370002
       CONFIGURATION SECTION.                                           00380002
                                                                        00390002
       SOURCE-COMPUTER.    IBM-3090.                                    00400002
       OBJECT-COMPUTER.    IBM-3090.                                    00410002
                                                                        00420002
       INPUT-OUTPUT SECTION.                                            00430002
       FILE-CONTROL.                                                    00440002
                                                                        00450002
           SELECT UNLOAD-FILE   ASSIGN TO INP01.                        00460002
                                                                        00470002
       DATA DIVISION.                                                   00480002
       FILE SECTION.                                                    00490002
                                                                        00500002
       FD  UNLOAD-FILE                                                  00510002
           RECORDING MODE IS F                                          00520002
           LABEL RECORDS ARE STANDARD                                   00530002
SR3403      RECORD CONTAINS 79 CHARACTERS                               00541006
           BLOCK CONTAINS 0 RECORDS                                     00550002
           DATA RECORD IS UNLOAD-REC.                                   00560002
SR3403  01  UNLOAD-REC                       PIC  X(79).                00571006
                                                                        00580002
      ***************************************************************** 00590002
      *                       WORKING STORAGE                           00600002
      ***************************************************************** 00610002
       WORKING-STORAGE SECTION.                                         00620002
                                                                        00630002
       01  W-START                          PIC  X(40)                  00640002
           VALUE 'TFKUT005 - WORKING STORAGE BEGINS HERE'.              00650002
                                                                        00660002
                                                                        00670002
      ***************************************************************** 00680002
      *                         SWITCHES                                00690002
      ***************************************************************** 00700002
       01  PROGRAM-SWITCHES.                                            00710002
           05  PS-INPUT-UNLOAD-EOF-SW       PIC  X(01) VALUE 'N'.       00720002
               88  PS-INPUT-UNLOAD-EOF                 VALUE 'Y'.       00730002
           05  PS-FIRST-TRANS-READ-SW       PIC  X(01) VALUE 'Y'.       00740002
               88  FIRST-TRANS-READ                    VALUE 'Y'.       00750002
                                                                        00760002
      ******************************************************************00770002
      *                       ACCUMULATORS                              00780002
      ******************************************************************00790002
       01  PROGRAM-ACCUMULATORS.                                        00800002
           05  PA-READ                      PIC  9(11) VALUE ZEROS.     00810002
           05  PA-WRITTEN                   PIC  9(11) VALUE ZEROS.     00820002
                                                                        00830002
      ******************************************************************00840002
      *                           CONSTANTS                             00850002
      ******************************************************************00860002
       01  PROGRAM-CONSTANTS.                                           00870002
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'TFKUT005'.00880002
                                                                        00890002
           05  PC-TRAN-PREP-FUNC-CODES.                                 00900002
               10  PC-TRAN-PREP-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.   00910002
               10  PC-TRAN-PREP-FUNC-WRITE  PIC  X(05) VALUE 'WRITE'.   00920002
               10  PC-TRAN-PREP-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.   00930002
                                                                        00940002
           05  PC-JOB-NAME                  PIC  X(08) VALUE 'TFK026  '.00950002
           05  PC-PLAN-NAME                 PIC  X(08) VALUE 'TFKUP001'.00960002
           05  PC-TRANS-RECORD-LENGTH       PIC S9(04) VALUE +78 COMP.  00970005
                                                                        00980002
      ******************************************************************00990002
      *                          VARIABLES                              01000002
      ******************************************************************01010002
       01  PROGRAM-VARIABLES.                                           01020002
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.    01030002
           05  PV-OPERATION-MSG             PIC  X(60) VALUE SPACES.    01040002
           05  PV-RET-CODE                  PIC  9(04) VALUE ZEROS.     01050002
                                                                        01060002
           05  PV-CHECKPOINT-TYPE           PIC  X(01) VALUE SPACE.     01070002
               88  CONTROL-BREAK                       VALUE 'C'.       01080002
               88  LOGICAL-TRANSACTION                 VALUE 'L'.       01090002
               88  TIME-INTERVAL                       VALUE 'T'.       01100002
               88  REQUESTED-BY-PROGRAM                VALUE 'R'.       01110002
                                                                        01120002
      *--->UNLOAD RECORD KEYS                                           01130002
           05  PV-XFER-DKEY-ID              PIC  S9(9) COMP.            01140003
           05  PV-FR-LOC-NBR                PIC  S9(4) COMP.            01150003
                                                                        01160002
           05  PV-INTEGER                   PIC S9(11) VALUE +0.        01170002
           05  PV-DECIMAL                   PIC S9(11) VALUE +0.        01180002
                                                                        01190002
      ******************************************************************01200002
      *               SYSTEM TIME AND DATE VARIABLES                    01210002
      ******************************************************************01220002
       01  SYS-DATE-TIME.                                               01230002
           05  SYS-DATE                     PIC  X(08) VALUE SPACES.    01240002
           05  SYS-TIME                     PIC  X(08) VALUE SPACES.    01250002
                                                                        01260002
       01  ST-BEG-TIME.                                                 01270002
           05  ST-BEG-HR                    PIC  9(02) VALUE ZEROS.     01280002
           05  FILLER                       PIC  X(01) VALUE ':'.       01290002
           05  ST-BEG-MN                    PIC  9(02) VALUE ZEROS.     01300002
                                                                        01310002
       01  ST-END-TIME.                                                 01320002
           05  ST-END-HR                    PIC  9(02) VALUE ZEROS.     01330002
           05  FILLER                       PIC  X(01) VALUE ':'.       01340002
           05  ST-END-MN                    PIC  9(02) VALUE ZEROS.     01350002
                                                                        01360002
       01  SD-EDIT-DATE.                                                01370002
           05  SD-MONTH                     PIC  9(02) VALUE ZEROS.     01380002
           05  FILLER                       PIC  X(01) VALUE '-'.       01390002
           05  SD-DAY                       PIC  9(02) VALUE ZEROS.     01400002
           05  FILLER                       PIC  X(01) VALUE '-'.       01410002
           05  SD-CENT                      PIC  9(02) VALUE ZEROS.     01420002
           05  SD-YEAR                      PIC  9(02) VALUE ZEROS.     01430002
                                                                        01440002
      ******************************************************************01450002
      *                        ERROR HANDLING                           01460002
      ******************************************************************01470002
       01  ABEND-CODE                       PIC S9(04) VALUE ZEROS      01480002
                                                       COMP SYNC.       01490002
           88  ABEND-4002-READ-ERROR             VALUE +4002.           01500002
           88  ABEND-4444-FORCED-ABEND           VALUE +4444.           01510002
                                                                        01520002
                                                                        01530002
      ******************************************************************01540002
      * RECORD LAYOUT FOR UNLOAD KEY                                    01550002
      ******************************************************************01560002
           COPY TFRD060.                                                01570003
                                                                        01580002
      ******************************************************************01590002
      *  TRANSACTION PREPARATION MODULE LINKAGE SECTION                 01600002
      ******************************************************************01610002
260107     COPY DPWS990.                                                01620009
                                                                        01630002
           COPY DPLS000.                                                01631009
                                                                        01632009
      ******************************************************************01640002
      *  SQL COMMUNICATIONS WORK AREA                                   01650002
      ******************************************************************01660002
           COPY SQLCA2.                                                 01670002
                                                                        01680002
                                                                        01690002
       PROCEDURE DIVISION.                                              01700002
      ******************************************************************01710002
      * MAINLINE-PROCESSING                                             01720002
      ******************************************************************01730002
                                                                        01740002
           PERFORM A100-INITIALIZE.                                     01750002
                                                                        01760002
           PERFORM B100-PROCESS-UNLOAD-INPUT-FILE                       01770002
             UNTIL PS-INPUT-UNLOAD-EOF.                                 01780002
                                                                        01790002
           PERFORM Z990-END-OF-JOB-ROUTINE.                             01800002
                                                                        01810002
           GOBACK.                                                      01820002
                                                                        01830002
                                                                        01840002
      ******************************************************************01850002
      * PREFORMS TASK INITIATION                                        01860002
      ******************************************************************01870002
       A100-INITIALIZE.                                                 01880002
                                                                        01890002
           OPEN INPUT UNLOAD-FILE.                                      01900002
                                                                        01910002
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 01920002
                                                                        01930002
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            01940002
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            01950002
                                                                        01960002
           MOVE SYS-DATE (1:2) TO SD-CENT.                              01970002
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              01980002
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             01990002
           MOVE SYS-DATE (7:2) TO SD-DAY.                               02000002
                                                                        02010002
           PERFORM C100-ISSUE-OPEN-REQUEST.                             02020002
                                                                        02030002
           PERFORM C200-READ-UNLOAD-INPUT-FILE.                         02040002
                                                                        02050002
           IF PS-INPUT-UNLOAD-EOF                                       02060002
               MOVE 'C200-READ-UNLOAD-INPUT-FILE' TO PV-PARAGRAPH-NAME  02070002
               MOVE '** EMPTY UNLOAD FILE - NO DATA **'                 02080002
                                                  TO PV-OPERATION-MSG   02090002
           END-IF.                                                      02100002
                                                                        02110002
                                                                        02120002
      ******************************************************************02130002
      * PROCESS THE UNLOAD FILE. IF A RECORD IS READ, GATHER WHATEVER   02140002
      * OTHER DATA IS NECESSARY TO TAKE CHECKPOINTS AND ISSUE A WRITE   02150002
      * REQUEST TO THE TRANSACTION PREPARATION PROGRAM.                 02160002
      ******************************************************************02170002
       B100-PROCESS-UNLOAD-INPUT-FILE.                                  02180002
                                                                        02190002
           IF FIRST-TRANS-READ                                          02200002
               MOVE TFRD060-XFER-DKEY-ID  TO PV-XFER-DKEY-ID            02210003
               MOVE TFRD060-FR-LOC-NBR    TO PV-FR-LOC-NBR              02220004
               MOVE 'N'                   TO PS-FIRST-TRANS-READ-SW     02230003
           END-IF.                                                      02240002
                                                                        02250002
           IF CONTROL-BREAK                                             02260002
               PERFORM C300-CKPT-BY-CNTL-BREAK                          02270002
           ELSE                                                         02280002
               IF LOGICAL-TRANSACTION                                   02290002
                   PERFORM C400-CKPT-BY-LOGICAL-TRANS                   02300002
               ELSE                                                     02310002
                   PERFORM C500-CKPT-BY-TIME-OR-REQUEST                 02320002
               END-IF                                                   02330002
           END-IF.                                                      02340002
                                                                        02350002
           PERFORM C200-READ-UNLOAD-INPUT-FILE.                         02360002
                                                                        02370002
                                                                        02380002
      ******************************************************************02390002
      * ISSUE AN 'OPEN' REQUEST TO THE TRANSACTION PREPARATION MODULE   02400002
      ******************************************************************02410002
       C100-ISSUE-OPEN-REQUEST.                                         02420002
                                                                        02430002
           MOVE PC-TRAN-PREP-FUNC-OPEN TO DP000-FUNC-CODE.              02440002
           MOVE PC-JOB-NAME            TO DP000-JOB-NAME.               02450002
           MOVE PC-PLAN-NAME           TO DP000-PLAN-NAME.              02460002
                                                                        02470002
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         02480002
                                                                        02490002
           MOVE DP000-CKPT-TYPE-CODE   TO PV-CHECKPOINT-TYPE.           02500002
                                                                        02510002
                                                                        02520002
      ******************************************************************02530002
      * READ THE UNLOAD INPUT FILE                                      02540002
      ******************************************************************02550002
       C200-READ-UNLOAD-INPUT-FILE.                                     02560002
                                                                        02570002
           READ UNLOAD-FILE INTO TFRD060-INV-SNAPSHOT-EXTRACT           02580003
             AT END SET PS-INPUT-UNLOAD-EOF TO TRUE.                    02590002
                                                                        02600002
           IF PS-INPUT-UNLOAD-EOF                                       02610002
               CONTINUE                                                 02620002
           ELSE                                                         02630002
               ADD +1 TO PA-READ                                        02640002
           END-IF.                                                      02650002
                                                                        02660002
                                                                        02670002
      ******************************************************************02680002
      * THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSACATION  02690002
      * RECORD "ON" IF THERE IS A CHANGE IN THE KEY. AFTER              02700002
      * THE TRANSACTION RECORD HAS BEEN FORMATTED, A WRITE REQUEST IS   02710002
      * ISSUED TO THE TRANSACTION PREPARATION MODULE.                   02720002
      ******************************************************************02730002
       C300-CKPT-BY-CNTL-BREAK.                                         02740002
                                                                        02750002
           IF  TFRD060-XFER-DKEY-ID   = PV-XFER-DKEY-ID                 02760003
           AND TFRD060-FR-LOC-NBR     = PV-FR-LOC-NBR                   02770004
               MOVE 'N' TO DP000-CHECKPOINT-IND                         02780002
           ELSE                                                         02790002
               MOVE 'Y' TO DP000-CHECKPOINT-IND                         02800002
               MOVE TFRD060-XFER-DKEY-ID     TO PV-XFER-DKEY-ID         02810003
               MOVE TFRD060-FR-LOC-NBR       TO PV-FR-LOC-NBR           02820004
           END-IF.                                                      02830002
                                                                        02840002
           MOVE PC-TRANS-RECORD-LENGTH       TO DP000-TRANS-REC-LENGTH. 02850003
           MOVE TFRD060-INV-SNAPSHOT-EXTRACT TO DP000-TRANS-REC.        02860003
           MOVE PC-TRAN-PREP-FUNC-WRITE      TO DP000-FUNC-CODE.        02870003
           MOVE PC-JOB-NAME                  TO DP000-JOB-NAME.         02880003
           MOVE PC-PLAN-NAME                 TO DP000-PLAN-NAME.        02890003
                                                                        02900002
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         02910002
                                                                        02920002
           ADD +1 TO PA-WRITTEN.                                        02930002
                                                                        02940002
                                                                        02950002
      ******************************************************************02960002
      * THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSACTION   02970002
      * RECORD "ON".  THE UNLOAD PROGRAM WILL CHECK THE CHECKPOINT      02980002
      * FREQUENCY TO KNOW WHETHER A CHECKPOINT SHOULD BE TAKEN. AFTER   02990002
      * THE TRANSACTION RECORD HAS BEEN FORMATTED, A WRITE REQUEST IS   03000002
      * ISSUED TO THE TRANSACTION PREPARATION MODULE.                   03010002
      ******************************************************************03020002
       C400-CKPT-BY-LOGICAL-TRANS.                                      03030002
                                                                        03040002
           MOVE 'Y'                          TO DP000-CHECKPOINT-IND.   03050003
           MOVE PC-TRANS-RECORD-LENGTH       TO DP000-TRANS-REC-LENGTH. 03060003
           MOVE TFRD060-INV-SNAPSHOT-EXTRACT TO DP000-TRANS-REC.        03070003
           MOVE PC-TRAN-PREP-FUNC-WRITE      TO DP000-FUNC-CODE.        03080003
           MOVE PC-JOB-NAME                  TO DP000-JOB-NAME.         03090003
           MOVE PC-PLAN-NAME                 TO DP000-PLAN-NAME.        03100003
                                                                        03110002
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         03120002
                                                                        03130002
           ADD +1 TO PA-WRITTEN.                                        03140002
                                                                        03150002
                                                                        03160002
      ******************************************************************03170002
      * THIS ROUTINE BUILDS THE TRANSACTION RECORD AND ISSUES A WRITE   03180002
      * REQUEST TO THE TRANSACTION PREPARATION MODULE IF CHECKPOINTS ARE03190002
      * TAKEN BY TIME INTERVAL (SECONDS) OR REQUESTED BY THE MAINTENANCE03200002
      * PROGRAM.                                                        03210002
      ******************************************************************03220002
       C500-CKPT-BY-TIME-OR-REQUEST.                                    03230002
                                                                        03240002
           MOVE 'N' TO DP000-CHECKPOINT-IND.                            03250002
                                                                        03260002
           MOVE PC-TRANS-RECORD-LENGTH       TO DP000-TRANS-REC-LENGTH. 03270003
           MOVE TFRD060-INV-SNAPSHOT-EXTRACT TO DP000-TRANS-REC.        03280003
           MOVE PC-TRAN-PREP-FUNC-WRITE      TO DP000-FUNC-CODE.        03290003
           MOVE PC-JOB-NAME                  TO DP000-JOB-NAME.         03300003
           MOVE PC-PLAN-NAME                 TO DP000-PLAN-NAME.        03310003
                                                                        03320002
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         03330002
                                                                        03340002
           ADD +1 TO PA-WRITTEN.                                        03350002
                                                                        03360002
                                                                        03370002
      ******************************************************************03380002
      * ISSUE A 'CLOSE' REQUEST TO THE TRANSACTION PREPARATION MODULE   03390002
      ******************************************************************03400002
       C600-ISSUE-CLOSE-REQUEST.                                        03410002
                                                                        03420002
           MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.             03430002
           MOVE PC-JOB-NAME             TO DP000-JOB-NAME.              03440002
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.             03450002
                                                                        03460002
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         03470002
                                                                        03480002
                                                                        03490002
      ******************************************************************03500002
      * VERIFY THE RETURN CODE ON THE CALLED MODULE                     03510002
      ******************************************************************03520002
       Z100-CALL-TRANS-PREP-MODULE.                                     03530002
                                                                        03540002
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         03550002
                                                                        03560002
           IF  DP000-GOOD-RET-CODE                                      03570002
               CONTINUE                                                 03580002
                                                                        03590002
           ELSE                                                         03600002
               MOVE 'Z100-CALL-TRANS-PREP-MODULE' TO PV-PARAGRAPH-NAME  03610002
               MOVE '** BAD CALL TO TRANS PREP MODULE **'               03620002
                                                  TO PV-OPERATION-MSG   03630002
               MOVE DP000-RET-CODE                TO PV-RET-CODE        03640002
                                                                        03650002
               SET ABEND-4444-FORCED-ABEND TO TRUE                      03660002
                                                                        03670002
               PERFORM Z200-ABEND                                       03680002
           END-IF.                                                      03690002
                                                                        03700002
                                                                        03710002
      ******************************************************************03720002
      * NON-DB2 ABEND                                                   03730002
      ******************************************************************03740002
       Z200-ABEND.                                                      03750002
                                                                        03760002
           PERFORM Z999-CONTROLS.                                       03770002
                                                                        03780002
           DISPLAY '                     '.                             03790002
           DISPLAY '** ABEND           **'.                             03800002
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          03810002
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        03820002
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         03830002
           DISPLAY '** RETURN CODE     ** = ' PV-RET-CODE.              03840002
                                                                        03850002
           CALL 'ILBOABN0' USING ABEND-CODE.                            03860002
                                                                        03870002
                                                                        03880002
      ******************************************************************03890002
      * PREFORMS TASK TERMINATION PROCESSING                            03900002
      ******************************************************************03910002
       Z990-END-OF-JOB-ROUTINE.                                         03920002
                                                                        03930002
           CLOSE UNLOAD-FILE.                                           03940002
                                                                        03950002
           PERFORM C600-ISSUE-CLOSE-REQUEST.                            03960002
                                                                        03970002
           PERFORM Z999-CONTROLS.                                       03980002
                                                                        03990002
                                                                        04000002
      ******************************************************************04010002
      * DISPLAY CONTROLS FROM THE PROGRAM                               04020002
      ******************************************************************04030002
       Z999-CONTROLS.                                                   04040002
                                                                        04050002
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 04060002
                                                                        04070002
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            04080002
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            04090002
                                                                        04100002
           DISPLAY '                    '.                              04110002
           DISPLAY '**********************'.                            04120002
           DISPLAY '   TFKUT005 SUMMARY   '.                            04130002
           DISPLAY '   TRANS PREP DETAILS '.                            04140002
           DISPLAY '   TFT_PHY_INV_ITM    '.                            04150003
           DISPLAY '**********************'.                            04160002
           DISPLAY 'RUN DATE  = ' SD-EDIT-DATE.                         04170002
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          04180002
           DISPLAY 'END TIME  = ' ST-END-TIME.                          04190002
           DISPLAY '======================'.                            04200002
           DISPLAY 'RECORDS READ    = ' PA-READ.                        04210002
           DISPLAY '                  '.                                04220002
           DISPLAY 'RECORDS WRITTEN = ' PA-WRITTEN.                     04230002
           DISPLAY '                  '.                                04240002
                                                                        04250002
                                                                        04260002
      ******************************************************************04270002
      * TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES          04280002
      ******************************************************************04290002
           COPY DPPD000.                                                04300002
                                                                        04310002
      ******************************************************************04320002
