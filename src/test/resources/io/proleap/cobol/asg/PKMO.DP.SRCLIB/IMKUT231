      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    IMKUT231.                                                 
       AUTHOR.        DAVID FELLENZ.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  02/07/12.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *    IM DELIMIT THE CUSTOMER CHOICE EXTRACT FILE.                         
      ******************************************************************        
      *    THIS PROGRAM WILL CREATE A DELIMITED, FILE FROM THE                  
      *    ALREADY EXTRACTED CC FILE.                                           
      ******************************************************************        
      *-----------------------------------------------------------------        
      *    WR/CRQ#     DATE        DESCRIPTION                                  
      *    --------   --------     ---------------------------------            
      *    CRQ20260   02/16/2012   D. FELLENZ  CHANGE PGR TO CREATE             
      *               A FILTERED FILE FROM THE ALREADY EXTRACTED CC             
      *               FILE. FILTER CRITERIA AS FOLLOWS:                         
      *      1. IF CC EFF_BEG_DTE = CC EFF_END_DTE WRITE TO ERROR FILE          
      *      2. IF CC EFF_BEG_DTE = CC EFF_END_DTE DO NOT WRITE TO PROD         
      *          FILE (GDG).                                                    
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT IM-CC-EXT-FILE                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT IM-CC-FILTERED-FILE                                           
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT IM-CC-ERROR-FILE                                              
               ASSIGN TO OUT02.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  IM-CC-EXT-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CC-EXT-REC                    PIC X(150).                         
                                                                                
       FD  IM-CC-FILTERED-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CC-FILTERED-REC               PIC X(160).                         
                                                                                
       FD  IM-CC-ERROR-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CC-ERROR-REC                  PIC X(160).                         
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      ******************************************************************        
      * INPUT FILE LAYOUT FOR IM CC DAILY EXTRACT FILE                          
      ******************************************************************        
       01  IM-CC-EXTRACT-RECORD.                                                
           05 IM-CUST-CHC-ID                PIC  S9(9) BINARY.                  
           05 FILLER                        PIC  X(01).                         
           05 IM-AM-CUST-CHC-CDE            PIC  X(32).                         
           05 FILLER                        PIC  X(02).                         
           05 IM-CUST-CHC-DESC              PIC  X(50).                         
           05 IM-STYL-ID                    PIC  S9(8)V COMP-3.                 
           05 FILLER                        PIC  X(02).                         
           05 IM-CSTM-CLOR-DESC             PIC  X(10).                         
           05 IM-NRF-CLOR-CDE               PIC  X(03).                         
           05 IM-CRE-DTE                    PIC  X(10).                         
           05 FILLER                        PIC  X(02).                         
           05 IM-CRE-BY-USR-ID              PIC  X(08).                         
           05 IM-EFF-BEG-DTE                PIC  X(10).                         
           05 FILLER                        PIC  X(01).                         
           05 IM-EFF-END-DTE                PIC  X(10).                         
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(8)   VALUE 'IMKUT231'.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW            PIC X      VALUE 'N'.               
               88  PS-END-OF-FILE                      VALUE 'Y'.               
               88  PS-NOT-END-OF-FILE                  VALUE 'N'.               
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORD-COUNT        PIC 9(09)  VALUE 0.                 
           05  PA-OUTPUT-RECORD-COUNT1      PIC 9(12)  VALUE 0.                 
           05  PA-OUTPUT-RECORD-COUNT2      PIC 9(09)  VALUE 0.                 
                                                                                
       01  PROGRAM-VARIABLES.                                                   
                                                                                
           05  PV-IN-COUNTER1               PIC 9(9)    VALUE 0.                
           05  PV-OUT-COUNTER1              PIC 9(9)    VALUE 0.                
                                                                                
           05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT FOR IM CC DAILY DELIM FILE                           
      ******************************************************************        
           COPY IMRD123.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-FILE                                            
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  OPEN ALL FILES.                                                        
      *  READ THE FIRST RECORD FROM THE INPUT FILE.                             
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  IM-CC-EXT-FILE                                           
                OUTPUT IM-CC-FILTERED-FILE                                      
                       IM-CC-ERROR-FILE.                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 8000-READ-CC-EXT-FILE.                                       
                                                                                
      ******************************************************************        
      *  FORMAT AND WRITE THE OUTPUT RECORDS.                                   
      *  READ THE NEXT INPUT RECORD.                                            
      ******************************************************************        
       2000-PROCESS-FILE.                                                       
                                                                                
           INITIALIZE IM123-CC-DELIM-RECORD.                                    
                                                                                
                                                                                
           MOVE IM-EFF-END-DTE            TO  IM123-EFF-END-DTE.                
                                                                                
           MOVE IM-CUST-CHC-ID            TO  IM123-CUST-CHC-ID.                
                                                                                
           MOVE IM-AM-CUST-CHC-CDE           TO  IM123-AM-CUST-CHC-CDE.         
                                                                                
           MOVE IM-CUST-CHC-DESC           TO  IM123-CUST-CHC-DESC.             
                                                                                
           MOVE IM-STYL-ID                TO  IM123-STYL-ID.                    
                                                                                
           MOVE IM-CSTM-CLOR-DESC         TO  IM123-CSTM-CLOR-DESC.             
                                                                                
           MOVE IM-NRF-CLOR-CDE           TO  IM123-NRF-CLOR-CDE.               
                                                                                
           MOVE IM-CRE-DTE                TO  IM123-CRE-DTE.                    
                                                                                
           MOVE IM-CRE-BY-USR-ID          TO  IM123-CRE-BY-USR-ID.              
                                                                                
           MOVE IM-EFF-BEG-DTE            TO  IM123-EFF-BEG-DTE.                
                                                                                
           MOVE IM-EFF-END-DTE            TO  IM123-EFF-END-DTE.                
                                                                                
           IF IM-EFF-BEG-DTE = IM-EFF-END-DTE                                   
              PERFORM 8300-WRITE-CC-ERROR-REC                                   
           ELSE                                                                 
              PERFORM 8100-WRITE-CC-FILTERED-REC                                
           END-IF.                                                              
                                                                                
           PERFORM 8000-READ-CC-EXT-FILE.                                       
                                                                                
      ************************************************************              
      *  READ THE CC EXTRACT FILE                                               
      ************************************************************              
       8000-READ-CC-EXT-FILE.                                                   
                                                                                
           READ IM-CC-EXT-FILE INTO IM-CC-EXTRACT-RECORD                        
               AT END                                                           
                   SET PS-END-OF-FILE TO TRUE                                   
               NOT AT END                                                       
                   ADD 1          TO   PA-INPUT-RECORD-COUNT.                   
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE A CC FILTERED RECORD.                                            
      ******************************************************************        
       8100-WRITE-CC-FILTERED-REC.                                              
                                                                                
           WRITE IM-CC-FILTERED-REC                                             
               FROM IM123-CC-DELIM-RECORD.                                      
                                                                                
           ADD 1 TO PA-OUTPUT-RECORD-COUNT1.                                    
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE A CC ERROR RECORD.                                               
      ******************************************************************        
       8300-WRITE-CC-ERROR-REC.                                                 
                                                                                
           WRITE IM-CC-ERROR-REC                                                
               FROM IM123-CC-DELIM-RECORD.                                      
                                                                                
      ************************************************************              
      *  DISPLAY RECORD COUNTS AT END OF JOB                                    
      *  CLOSE THE FILES.                                                       
      ************************************************************              
       9000-END-OF-JOB.                                                         
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
                                                                                
           DISPLAY '   IM CC RECORDS INPUT         = '                          
                    PA-INPUT-RECORD-COUNT.                                      
                                                                                
           DISPLAY '   IM CC DELIM RECS OUTPUT      = '                         
                    PA-OUTPUT-RECORD-COUNT1.                                    
                                                                                
           SUBTRACT PA-OUTPUT-RECORD-COUNT1 FROM PA-INPUT-RECORD-COUNT          
                    GIVING PA-OUTPUT-RECORD-COUNT2.                             
                                                                                
           DISPLAY '   IM CC DELIM RECS ELIMINATED  = '                         
                    PA-OUTPUT-RECORD-COUNT2.                                    
                                                                                
           CLOSE IM-CC-EXT-FILE                                                 
                 IM-CC-FILTERED-FILE                                            
                 IM-CC-ERROR-FILE.                                              
