       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUT443.                                         00020000
       AUTHOR.        PAUL KEBER - STRATAGEM.                           00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  05-28-99.                                         00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      *                                                                 00080000
      * MLKUT443 - CREATE THE DATA INTEGRITY EXTRACT FILE FOR TVADPT.   00090000
      *                                                                 00100000
      * THIS PROGRAM WILL FETCH TVADPT DB2 TABLE ROWS USING A CURSOR    00110000
      * WHICH WILL SUM AMOUNTS AT THE FISCAL MONTH END DATE, FISCAL     00120000
      * MONTH NUMBER, AND DEPARTMENT LEVEL.  THESE AMOUNTS WILL BE USED 00130000
      * TO CREATE THE DATA INTEGRITY EXTRACT FILE.                      00131000
      *                                                                 00132000
      *CHANGE HISTORY:                                                  00132103
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00132203
      * ------- ---------- ----------- -------------------------------- 00132303
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION     00132403
      ******************************************************************00133000
      *                                                                 00134000
       ENVIRONMENT DIVISION.                                            00135000
      *                                                                 00136000
       CONFIGURATION SECTION.                                           00137000
      *                                                                 00138000
       SOURCE-COMPUTER.        IBM-3090.                                00139000
       OBJECT-COMPUTER.        IBM-3090.                                00140000
      *                                                                 00150000
       INPUT-OUTPUT SECTION.                                            00160000
       FILE-CONTROL.                                                    00170000
                                                                        00180000
           SELECT DATA-INTEGRITY-CTL-FILE                               00190000
               ASSIGN TO INP01.                                         00200000
                                                                        00210000
           SELECT DATA-INTEGRITY-EXTR-FILE                              00220000
               ASSIGN TO OUT01.                                         00230000
                                                                        00240000
      ******************************************************************00250000
       DATA DIVISION.                                                   00260000
      ******************************************************************00270000
                                                                        00280000
       FILE SECTION.                                                    00290000
                                                                        00300000
       FD  DATA-INTEGRITY-CTL-FILE                                      00310000
           LABEL RECORDS ARE STANDARD                                   00320000
           RECORDING MODE IS F                                          00330000
           BLOCK CONTAINS 0 RECORDS.                                    00340000
       01  DATA-INTEGRITY-CTL-REC      PIC X(80).                       00350000
                                                                        00360000
       FD  DATA-INTEGRITY-EXTR-FILE                                     00370000
           RECORDING MODE IS F                                          00380000
           LABEL RECORDS ARE STANDARD                                   00390000
           BLOCK CONTAINS 0 RECORDS.                                    00400000
                                                                        00410000
W26603 01  DATA-INTEGRITY-EXTR-REC     PIC X(120).                      00420003
                                                                        00430000
      ******************************************************************00450000
       WORKING-STORAGE SECTION.                                         00460000
      ******************************************************************00470000
                                                                        00480000
       01  PS-PROGRAM-SWITCHES.                                         00490000
           05  PS-TVADPT-DONE          PIC X       VALUE 'N'.           00500000
               88  PS-TVADPT-IS-DONE               VALUE 'Y'.           00510000
               88  PS-TVADPT-IS-NOT-DONE           VALUE 'N'.           00520000
                                                                        00530000
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.              00540000
           05  PA-TVADPT-ROWS-FETCHED  PIC S9(13)  VALUE ZERO.          00550000
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.          00560000
                                                                        00570000
       01  CD-COUNTER-DISPLAY.                                          00580000
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           00590000
                                                                        00600000
       01 ABEND-AREAS.                                                  00610000
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           00620000
                                                   COMP SYNC.           00630000
               88  AC-BAD-DATA                     VALUE +4003.         00640000
               88  AC-DB2-ERROR                    VALUE +4013.         00650000
           05  AA-ABEND-LIT            PIC X(40)   VALUE                00660000
                 '*****       ABEND'.                                   00670000
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                00680000
                   '*****   PROGRAM: MLKUT443'.                         00690000
           05  AA-PARAGRAPH-LIT.                                        00700000
               10  FILLER              PIC X(17)   VALUE                00710000
                   '***** PARAGRAPH: '.                                 00720000
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        00730000
           05  AA-MESSAGE-LINE.                                         00740000
               10  FILLER              PIC X(06)   VALUE '*****'.       00750000
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        00760000
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                00770000
                   '*****    DB2 ERROR'.                                00780000
           05  AA-DB2-OPERATION-LIT.                                    00790000
               10  FILLER              PIC X(17)   VALUE                00800000
                   '***** OPERATION: '.                                 00810000
               10  AA-DB2-OPERATION.                                    00820000
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        00830000
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        00840000
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        00850000
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        00860000
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        00870000
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        00880000
                                                                        00890000
       EJECT                                                            00900000
                                                                        00910000
      *** DATA INTEGRITY CONTROL CARD                                   00920000
           COPY MLRD110.                                                00930000
                                                                        00940000
                                                                        00950000
      *** DATA INTEGRITY TVADPT EXTRACT FILE                            00960000
W26603     COPY MLRD111.                                                00970003
                                                                        00980000
       EJECT                                                            00990000
                                                                        01000000
      ******************************************************************01010000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01020000
      ******************************************************************01030000
           COPY DPWS004.                                                01040000
                                                                        01050000
      ******************************************************************01060000
      *  DB2 ERRORS                                                     01070000
      ******************************************************************01080000
           COPY SQLCA2.                                                 01090000
                                                                        01100000
      ******************************************************************01110000
      *  DB2 COMMUNICATION AREA                                         01120000
      ******************************************************************01130000
           EXEC SQL                                                     01140000
                INCLUDE SQLCA                                           01150000
           END-EXEC.                                                    01160000
                                                                        01170000
      ******************************************************************01180000
      *  DB2 TVADPT TABLE LAYOUT                                        01190000
      ******************************************************************01200000
           EXEC SQL                                                     01210000
                INCLUDE TVADPT                                          01220000
           END-EXEC.                                                    01230000
                                                                        01240000
      ******************************************************************01250000
      *  DB2 TVADPT CURSOR                                              01260000
      ******************************************************************01270000
           EXEC SQL                                                     01280000
               DECLARE TVADPT_CURSOR CURSOR FOR                         01290000
                SELECT FSCL_MN_END_DTE                                  01300000
                      ,FSCL_MN_NBR                                      01310000
                      ,DEPT_NBR                                         01320000
                      ,SUM(SLS_REG_RTL_AMT)                             01340000
                      ,SUM(SLS_PROMO_RTL_AMT)                           01350000
                      ,SUM(SLS_PROCLR_RTL_AMT)                          01360000
                      ,SUM(SLS_CLR_RTL_AMT)                             01370000
                      ,SUM(SLS_OTH_RTL_AMT)                             01380000
                      ,SUM(MKDN_PLU_RTL_AMT)                            01390000
                      ,SUM(MKDN_RGST_RTL_AMT)                           01400000
                      ,SUM(MKDN_UMTCH_RTL_AMT)                          01410000
                      ,SUM(MKDN_BYR_RTL_AMT)                            01420000
                      ,SUM(MKDN_STR_RTL_AMT)                            01421000
                      ,SUM(MKUP_RTL_AMT)                                01421100
                      ,SUM(XFER_IN_RTL_AMT)                             01421200
                      ,SUM(XFER_OUT_RTL_AMT)                            01421300
                      ,SUM(RCPT_RTL_AMT)                                01421400
                      ,SUM(RCPT_NET_COST_AMT)                           01421500
                      ,SUM(PADJ_RTL_AMT)                                01421600
                      ,SUM(PADJ_NET_COST_AMT)                           01421700
                      ,SUM(INV_ADJ_RTL_AMT)                             01421800
                      ,SUM(SHNK_RTL_AMT)                                01421900
                      ,SUM(END_INV_RTL_AMT)                             01422002
                  FROM TVADPT                                           01422102
                 WHERE (FSCL_MN_END_DTE >= :ML110-STRT-DTE)             01423000
                   AND (FSCL_MN_END_DTE <= :ML110-END-DTE)              01424000
                GROUP BY FSCL_MN_END_DTE,                               01425000
                         FSCL_MN_NBR,                                   01426000
                         DEPT_NBR                                       01427000
                ORDER BY FSCL_MN_END_DTE,                               01429000
                         FSCL_MN_NBR,                                   01430000
                         DEPT_NBR                                       01440000
           END-EXEC.                                                    01460000
                                                                        01470000
       EJECT                                                            01480000
                                                                        01490000
      ******************************************************************01500000
       PROCEDURE DIVISION.                                              01510000
      ******************************************************************01520000
                                                                        01530000
      ******************************************************************01540000
       0000-MAINLINE-MLKUT443.                                          01550000
      ******************************************************************01560000
      *                                                                 01570000
           PERFORM 1000-INITIALIZATION.                                 01580000
                                                                        01590000
           PERFORM 2000-MAIN-PROCESS.                                   01600000
                                                                        01610000
           PERFORM 8000-EOJ.                                            01620000
                                                                        01630000
           STOP RUN.                                                    01640000
                                                                        01650000
       EJECT                                                            01660000
                                                                        01670000
      ******************************************************************01680000
       1000-INITIALIZATION.                                             01690000
      ******************************************************************01700000
      *                                                                 01710000
           OPEN INPUT  DATA-INTEGRITY-CTL-FILE.                         01720000
           OPEN OUTPUT DATA-INTEGRITY-EXTR-FILE.                        01730000
                                                                        01740000
           PERFORM 4000-READ-CONTROL-CARD.                              01750000
                                                                        01760000
           DISPLAY ' '.                                                 01770000
           DISPLAY '*** MLKUT443 INPUT CONTROL CARD VALUES ***'         01780000
           DISPLAY ML110-DATA-INTEGRITY-CC-REC.                         01790000
                                                                        01800000
       EJECT                                                            01810000
                                                                        01820000
      ******************************************************************01830000
       2000-MAIN-PROCESS.                                               01840000
      ******************************************************************01850000
      *                                                                 01860000
           PERFORM 7100-OPEN-CURSOR-TVADPT.                             01870000
                                                                        01880000
           PERFORM 7200-FETCH-TVADPT.                                   01890000
                                                                        01900000
           PERFORM                                                      01910000
             UNTIL PS-TVADPT-IS-DONE                                    01920000
               PERFORM 5000-WRITE-EXTRACT                               01930000
               PERFORM 7200-FETCH-TVADPT                                01940000
           END-PERFORM.                                                 01950000
                                                                        01960000
           PERFORM 7300-CLOSE-CURSOR-TVADPT.                            01970000
                                                                        01980000
       EJECT                                                            01990000
                                                                        02000000
      ******************************************************************02010000
       4000-READ-CONTROL-CARD.                                          02020000
      ******************************************************************02030000
      *                                                                 02040000
           READ DATA-INTEGRITY-CTL-FILE                                 02050000
             INTO ML110-DATA-INTEGRITY-CC-REC                           02060000
               AT END                                                   02070000
                   MOVE '4000-READ-CONTROL-CARD'                        02080000
                                       TO AA-PARAGRAPH-NAME             02090000
                   MOVE 'CONTROL CARD IS MISSING'                       02100000
                                       TO AA-MESSAGE                    02110000
                   MOVE +4001          TO ABEND-CODE                    02120000
                   PERFORM Z990-COMMON-ABEND-ROUTINE                    02130000
           END-READ.                                                    02140000
                                                                        02150000
       EJECT                                                            02160000
                                                                        02170000
      ******************************************************************02180000
       5000-WRITE-EXTRACT.                                              02190000
      ******************************************************************02200000
      *                                                                 02210000
           MOVE VADPT-FSCL-MN-END-DTE  TO ML111-FSCL-MN-END-DTE.        02220000
           MOVE VADPT-FSCL-MN-NBR      TO ML111-FSCL-MN-NBR.            02230000
           MOVE VADPT-DEPT-NBR         TO ML111-DEPT-NBR.               02240000
           MOVE '00'                   TO ML111-MAJ-CL-NBR.             02250000
           MOVE '00'                   TO ML111-SUB-CL-NBR.             02260000
           MOVE 0                      TO ML111-STR-NBR.                02270000
           COMPUTE ML111-SLS-RTL-AMT  = VADPT-SLS-REG-RTL-AMT           02280000
                                      + VADPT-SLS-PROMO-RTL-AMT         02290000
                                      + VADPT-SLS-PROCLR-RTL-AMT        02300000
                                      + VADPT-SLS-CLR-RTL-AMT           02310000
                                      + VADPT-SLS-OTH-RTL-AMT.          02320000
           COMPUTE ML111-MKDN-RTL-AMT = VADPT-MKDN-PLU-RTL-AMT          02330000
                                      + VADPT-MKDN-RGST-RTL-AMT         02340000
                                      + VADPT-MKDN-UMTCH-RTL-AMT        02350000
                                      + VADPT-MKDN-BYR-RTL-AMT          02360000
                                      + VADPT-MKDN-STR-RTL-AMT.         02370000
           MOVE VADPT-MKUP-RTL-AMT     TO ML111-MKUP-RTL-AMT.           02380000
           MOVE VADPT-XFER-IN-RTL-AMT  TO ML111-XFER-IN-RTL-AMT.        02390000
           MOVE VADPT-XFER-OUT-RTL-AMT TO ML111-XFER-OUT-RTL-AMT.       02400000
           MOVE VADPT-RCPT-RTL-AMT     TO ML111-RCPT-RTL-AMT.           02410000
           MOVE VADPT-RCPT-NET-COST-AMT                                 02420000
                                       TO ML111-RCPT-NET-COST-AMT.      02430000
           MOVE VADPT-PADJ-RTL-AMT     TO ML111-PADJ-RTL-AMT.           02440000
           MOVE VADPT-PADJ-NET-COST-AMT                                 02450000
                                       TO ML111-PADJ-NET-COST-AMT.      02460000
           MOVE VADPT-INV-ADJ-RTL-AMT  TO ML111-INV-ADJ-RTL-AMT.        02470000
           MOVE VADPT-SHNK-RTL-AMT     TO ML111-SHNK-RTL-AMT.           02480000
           MOVE VADPT-END-INV-RTL-AMT  TO ML111-END-INV-RTL-AMT.        02481001
                                                                        02490000
           WRITE DATA-INTEGRITY-EXTR-REC                                02500000
               FROM ML111-DATA-INTEGRITY-EXTR-REC.                      02510000
           ADD 1                       TO PA-OUTPUT-RECS-WRITTEN.       02520000
                                                                        02530000
       EJECT                                                            02540000
                                                                        02550000
      ******************************************************************02560000
       7100-OPEN-CURSOR-TVADPT.                                         02570000
      ******************************************************************02580000
      *                                                                 02590000
           EXEC SQL                                                     02600000
               OPEN TVADPT_CURSOR                                       02610000
           END-EXEC.                                                    02620000
                                                                        02630000
           EVALUATE TRUE                                                02640000
               WHEN SQLCODE = 0                                         02650000
                   CONTINUE                                             02660000
               WHEN OTHER                                               02670000
                   MOVE '7100-OPEN-CURSOR-TVADPT'                       02680000
                                       TO AA-PARAGRAPH-NAME             02690000
                   MOVE 'TVADPT TABLE ERROR'                            02690100
                                       TO AA-MESSAGE                    02690200
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              02690300
                   PERFORM Z998-SQL-ERROR                               02690400
           END-EVALUATE.                                                02690500
                                                                        02690600
       EJECT                                                            02690700
                                                                        02690800
      ******************************************************************02690900
       7200-FETCH-TVADPT.                                               02691000
      ******************************************************************02692000
      *                                                                 02693000
           EXEC SQL                                                     02694000
               FETCH TVADPT_CURSOR                                      02695000
                INTO :VADPT-FSCL-MN-END-DTE,                            02696000
                     :VADPT-FSCL-MN-NBR,                                02697000
                     :VADPT-DEPT-NBR,                                   02698000
                     :VADPT-SLS-REG-RTL-AMT,                            02700000
                     :VADPT-SLS-PROMO-RTL-AMT,                          02710000
                     :VADPT-SLS-PROCLR-RTL-AMT,                         02720000
                     :VADPT-SLS-CLR-RTL-AMT,                            02730000
                     :VADPT-SLS-OTH-RTL-AMT,                            02731000
                     :VADPT-MKDN-PLU-RTL-AMT,                           02732000
                     :VADPT-MKDN-RGST-RTL-AMT,                          02733000
                     :VADPT-MKDN-UMTCH-RTL-AMT,                         02734000
                     :VADPT-MKDN-BYR-RTL-AMT,                           02735000
                     :VADPT-MKDN-STR-RTL-AMT,                           02736000
                     :VADPT-MKUP-RTL-AMT,                               02737000
                     :VADPT-XFER-IN-RTL-AMT,                            02738000
                     :VADPT-XFER-OUT-RTL-AMT,                           02739000
                     :VADPT-RCPT-RTL-AMT,                               02739100
                     :VADPT-RCPT-NET-COST-AMT,                          02739200
                     :VADPT-PADJ-RTL-AMT,                               02739300
                     :VADPT-PADJ-NET-COST-AMT,                          02739400
                     :VADPT-INV-ADJ-RTL-AMT,                            02739500
                     :VADPT-SHNK-RTL-AMT,                               02739602
                     :VADPT-END-INV-RTL-AMT                             02739702
           END-EXEC.                                                    02739802
                                                                        02739902
           EVALUATE TRUE                                                02740002
               WHEN SQLCODE = 0                                         02741002
                   ADD +1              TO PA-TVADPT-ROWS-FETCHED        02750000
               WHEN SQLCODE = +100                                      02760000
                   MOVE 'Y'            TO PS-TVADPT-DONE                02770000
               WHEN OTHER                                               02780000
                   MOVE '7200-FETCH-TVADPT'                             02790000
                                       TO AA-PARAGRAPH-NAME             02800000
                   MOVE 'TVADPT TABLE ERROR'                            02810000
                                       TO AA-MESSAGE                    02820000
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              02830000
                   PERFORM Z998-SQL-ERROR                               02840000
           END-EVALUATE.                                                02850000
                                                                        02860000
       EJECT                                                            02870000
                                                                        02880000
      ******************************************************************02890000
       7300-CLOSE-CURSOR-TVADPT.                                        02900000
      ******************************************************************02910000
      *                                                                 02920000
           EXEC SQL                                                     02930000
               CLOSE TVADPT_CURSOR                                      02940000
           END-EXEC.                                                    02950000
                                                                        02960000
           EVALUATE TRUE                                                02970000
               WHEN SQLCODE = 0                                         02980000
                   CONTINUE                                             02990000
               WHEN OTHER                                               03000000
                   MOVE '7300-CLOSE-CURSOR-TVADPT'                      03010000
                                       TO AA-PARAGRAPH-NAME             03020000
                   MOVE 'TVADPT TABLE ERROR'                            03030000
                                       TO AA-MESSAGE                    03040000
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03050000
                   PERFORM Z998-SQL-ERROR                               03060000
           END-EVALUATE.                                                03070000
                                                                        03080000
       EJECT                                                            03090000
                                                                        03100000
      ******************************************************************03110000
       8000-EOJ.                                                        03120000
      ******************************************************************03130000
      *                                                                 03140000
           DISPLAY ' '.                                                 03150000
           DISPLAY '  PROGRAM MLKUT443 PROCESSING SUMMARY'.             03160000
           DISPLAY '  -----------------------------------'.             03170000
           DISPLAY ' '.                                                 03180000
      *                                                                 03190000
           MOVE PA-TVADPT-ROWS-FETCHED TO CD-COUNT-HOLD1.               03200000
           DISPLAY 'TVADPT ROWS FETCHED =       ' CD-COUNT-HOLD1.       03210000
      *                                                                 03220000
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.               03230000
           DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.       03240000
      *                                                                 03250000
           DISPLAY ' '.                                                 03260000
                                                                        03270000
           CLOSE DATA-INTEGRITY-CTL-FILE.                               03280000
           CLOSE DATA-INTEGRITY-EXTR-FILE.                              03290000
                                                                        03300000
       EJECT                                                            03310000
                                                                        03320000
      ******************************************************************03330000
       Z990-COMMON-ABEND-ROUTINE.                                       03340000
      ******************************************************************03350000
      *                                                                 03360000
           DISPLAY AA-ABEND-LIT.                                        03370000
           DISPLAY AA-PROGRAM-LIT.                                      03380000
           DISPLAY AA-PARAGRAPH-LIT.                                    03390000
           DISPLAY AA-MESSAGE-LINE.                                     03400000
           DISPLAY ABEND-CODE.                                          03410000
                                                                        03420000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03430000
                                                                        03440000
       EJECT                                                            03450000
                                                                        03460000
      ******************************************************************03470000
       Z998-SQL-ERROR.                                                  03480000
      ******************************************************************03490000
      *                                                                 03500000
           CLOSE DATA-INTEGRITY-CTL-FILE                                03510000
                 DATA-INTEGRITY-EXTR-FILE.                              03520000
                                                                        03530000
           DISPLAY AA-ABEND-LIT.                                        03540000
           DISPLAY AA-DB2-ERROR-LIT.                                    03550000
           DISPLAY AA-PROGRAM-LIT.                                      03560000
           DISPLAY AA-PARAGRAPH-LIT.                                    03570000
           DISPLAY AA-MESSAGE-LINE.                                     03580000
           DISPLAY AA-DB2-OPERATION-LIT.                                03590000
           SET AC-DB2-ERROR TO TRUE.                                    03600000
           MOVE +4013                  TO ABEND-CODE.                   03610000
                                                                        03620000
           COPY DPPD004.                                                03630000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03640000
                                                                        03650000
