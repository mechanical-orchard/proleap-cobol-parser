       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    PEKRP004.                                                 
       AUTHOR.        AMY REILAND                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  08-08-93.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      *  THIS PROGRAM CREATES AN EXCESSIVE UNITS AND PRICE DIFF REPORT          
      *  FOR THE PERM PRICE CHANGE IN-STORE DOCUMENT SYSTEM.                    
      *                                                                         
      *  THE SUMMARY REPORT WILL LIST BY STORE, ALL THOSE IN-STORE              
      *  PERMANENT PRICE CHANGE DOCUMENTS WHICH HAD A SKU THAT EXCEEDED         
      *  THE MAXIMUM UNITS.  THE MAXIMUM UNITS IS DEFINED ON                    
      *  THE PERM CONTROL TABLE (TPPCCTL).  ALSO, THOSE SKUS THAT               
      *  PERMANENT PRICE CHANGE DOCUMENTS WHICH HAD A SKU THAT EXCEEDED         
      *  THE MAXIMUM PRICE DIFFERENCE.  THE MAXIMUM PRICE DIFFERENCE IS         
      *  DEFINED ON THE PERM CONTROL TABLE (TPPCCTL).                           
      *                                                                         
      *  THE INPUT TO THIS PROGRAM WILL BE CREATED BY THE NIGHTLY               
      *  IN-STORE DOCUMENT COMPLETION PROCESS.                                  
      *                                                                         
      *  12/1/93 - A. REILAND                                                   
      *      CHANGED THE PROGRAM TO ONLY PRINT THE REPORT IF THE STORE          
      *      HAS EXCESSIVE UNITS.                                               
      *                                                                         
      *  12/6/93 - A. REILAND                                                   
      *      MODIFIED THE REPORT TO ALSO CONTAIN EXCESSIVE PRICE                
      *      DIFFERENCES.                                                       
      *                                                                         
      ******************************************************************        
      * 02/03/95 - TCM -  WR NO. 3289                                  *        
      *    - ADD CODING TO CURSOR "STORE-CURSOR" TO PROCESS LOCATION   *        
      *      TYPE: RV - RETURN TO VENDOR                               *        
      ******************************************************************        
      * 06/26/96 - TCM -  WR NO. 3966                                  *        
      *    - ADD COPYLIB PEWS049                                       *        
      *    - REPLACE PC-DISTRIBUTION-CENTER, PC-RETURN-TO-VENDOR,      *        
      *      PC-DEPARTMENT-STORE WITH APPROPRIATE                      *        
      *      PE049-LOCATION-TYPE-CODES                                 *        
      *    - ADD PE049-CENT-STK-LOC-TYP-CDE TO STORE-CURSOR CURSOR     *        
      *    - REPLACE ALL "++INCLUDE" REFERENCES WITH "COPY"            *        
      *      REFERENCES, PER STANDARDS                                 *        
      ******************************************************************        
      * 07/24/97 - TCM -  WR NO. P000078094                            *        
      *    - REMOVE DJDE PROCESSING                                    *        
      *    - EXPAND ST-STORE-TABLE FROM 200 TO 500 OCCURRENCES, CHANGE *        
      *      PC-STORE-INDEX-MAX FROM 200 TO 500                        *        
      *    - REMOVE: "AND CLOSED_DATE > CURRENT DATE"                  *        
      *      FROM STORE-CURSOR                                         *        
      ******************************************************************        
      * 02/12/98 - LONNA HANNAN  WR NO. P000161416                     *        
      *    - REMOVED WHERE CONDITION FROM THE STORE TABLE CURSOR THAT  *        
      *      PREVENTED STORES THAT WERE NOT OPEN FOR RECIEVING TO BE   *        
      *      RETRIEVED.  THE CURSOR WILL NOW RETRIEVE ALL STORES       *        
      *      REGARDLESS OF WHETHER THEY ARE OPEN OR NOT.               *        
      ******************************************************************        
      * 02/26/02 - JACK KRAMER   IR# PM00452132                        *        
      *    - EXPAND ST-STORE-TABLE FROM 500 TO 700 OCCURRENCES, CHANGE *        
      *      PC-STORE-INDEX-MAX FROM 500 TO 700                        *        
      ******************************************************************        
      * 07/12/02 - TODD C. MICHALEK  IR# PM00452132                    *        
      *    - EXPAND ST-STORE-TABLE FROM 700 TO 2000 OCCURRENCES,       *        
      *      CHANGE PC-STORE-INDEX-MAX FROM 700 TO 2000                *        
      ******************************************************************        
W24472* 03/04/03 - ALBERT A VINOD, IGSI    WR# 24472                   *        
W24472*    - REPLACED 3 DIGIT SEQUENCE NUMBER WITH 8 DIGIT SKU         *        
W24472*    - MODIFIED THE HEADING OF THE REPORT TO ACCOMUDATE SKU      *        
W24472*    - ADDED NEW LOGIC TO HANDLE DUMMY SKU                       *        
W24472*    - ADDEN NEW PARAGRAPH TO RETRIEVE ROW FROM TSKXREF TABLE    *        
W24472******************************************************************        
      * 01/28/05 - TODD C. MICHALEK        WR NO. 27559                *        
      *    - CHANGED ALL STORE NUMBER LENGTH REFERENCES FROM 3 TO 4    *        
      *      DIGITS IN LENGTH                                          *        
      *    - REPLACED TSTR000 STORE TABLE CALL(S) WITH STORE TABLE     *        
      *      CALL(S) TO XST_LOC                                        *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT EXCESSIVE-UNITS-FILE                                          
               ASSIGN TO  INP01.                                                
      *                                                                         
           SELECT EXCESSIVE-UNITS-REPORT                                        
               ASSIGN TO  RPT01.                                                
      *                                                                         
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  EXCESSIVE-UNITS-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 CHARACTERS.                                         
                                                                                
       01  EXCESSIVE-UNITS-RECORD      PIC X(060).                              
      *                                                                         
       FD  EXCESSIVE-UNITS-REPORT                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  EXCESSIVE-UNITS-LINE        PIC X(132).                              
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                                                                         
       01  ABEND-CODE                      PIC S9(4) VALUE 0                    
                                                     COMP SYNC.                 
           88 ABEND-4004-TABLE-ERROR                 VALUE +4004.               
           88 ABEND-4013-DB2-ERROR                   VALUE +4013.               
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME     PIC X(08) VALUE 'PEKRP004'.          
           05  PC-DATE-SLASH               PIC X(01) VALUE '/'.                 
           05  PC-LINE-MAX                 PIC 9(02) VALUE 60.                  
           05  PC-ONE-LINE                 PIC 9(01) VALUE 1.                   
           05  PC-OPERCO-NBR               PIC X(02) VALUE '03'.                
           05  PC-ROW-NOT-FOUND            PIC S9(4) VALUE +100.                
           05  PC-SEVEN-LINES              PIC 9(01) VALUE 7.                   
           05  PC-STORE-INDEX-MAX          PIC 9(04) VALUE 2000.                
           05  PC-STORE-WITH-NO-DOC-LINE.                                       
               10  FILLER                  PIC X(07) VALUE SPACES.              
               10  FILLER                  PIC X(41) VALUE                      
               '************* NO EXCESSIVE UNITS FOR THIS'.                     
               10  FILLER                  PIC X(19) VALUE                      
               ' STORE ************'.                                           
               10  FILLER                  PIC X(08) VALUE SPACES.              
           05  PC-THREE-LINES              PIC 9(01) VALUE 3.                   
           05  PC-TWO-LINES                PIC 9(01) VALUE 2.                   
           05  PC-FOUR-LINES               PIC 9(01) VALUE 4.                   
           05  PC-UNITS-MSG                PIC X(30) VALUE                      
               'EXCESSIVE UNITS               '.                                
           05  PC-PRICE-MSG                PIC X(30) VALUE                      
               'EXCESSIVE PRICE DIFF          '.                                
           05  PC-BOTH-UNITS-AND-PRICE-MSG                                      
                                           PIC X(30) VALUE                      
               'EXCESSIVE UNITS AND PRICE DIFF'.                                
                                                                                
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW           PIC X(01) VALUE 'N'.                 
               88  PS-END-OF-FILE                    VALUE 'Y'.                 
               88  PS-NOT-END-OF-FILE                VALUE 'N'.                 
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DATE.                                                         
               10  PV-YY                   PIC 9(02) VALUE 0.                   
               10  PV-MM                   PIC 9(02) VALUE 0.                   
               10  PV-DD                   PIC 9(02) VALUE 0.                   
           05  PV-DB2-FUNCTION             PIC X(30) VALUE SPACES.              
           05  PV-PREV-STORE-NBR           PIC 9(04) VALUE ZEROS.               
           05  PV-PREV-DOCUMENT-NBR        PIC 9(07) VALUE ZEROS.               
           05  PV-PREV-EFF-DATE.                                                
               10 PV-PREV-EFF-CC           PIC X(02) VALUE SPACES.              
               10 PV-PREV-EFF-YY           PIC X(02) VALUE SPACES.              
               10 FILLER                   PIC X(01) VALUE SPACES.              
               10 PV-PREV-EFF-MM           PIC X(02) VALUE SPACES.              
               10 FILLER                   PIC X(01) VALUE SPACES.              
               10 PV-PREV-EFF-DD           PIC X(02) VALUE SPACES.              
           05  PV-LINE-COUNT               PIC 9(02) VALUE 0.                   
           05  PV-LINES-TO-ADVANCE         PIC 9(01) VALUE 0.                   
           05  PV-PARAGRAPH-NAME           PIC X(30) VALUE SPACES.              
           05  PV-PAGE-NBR                 PIC 9(05) VALUE ZERO.                
           05  PV-TIME.                                                         
               10  PV-HH                   PIC 9(02) VALUE 0.                   
               10  PV-MIN                  PIC 9(02) VALUE 0.                   
               10  FILLER                  PIC 9(04) VALUE 0.                   
           05  PV-HOLD-OLD-PRICE           PIC 9(7)V99                          
                                                     VALUE 0.                   
           05  PV-HOLD-NEW-PRICE           PIC 9(7)V99                          
                                                     VALUE 0.                   
W24472     05  PV-SKU                      PIC 9(08) VALUE ZERO.                
W24472     05  PV-DEPT-NBR-9               PIC 9(04) VALUE ZERO.                
W24472     05  PV-CLASS-NBR-9              PIC 9(04) VALUE ZERO.                
      *                                                                         
       01  ST-STORE-TABLE.                                                      
           05  ST-STORE-INFORMATION        OCCURS 2000 TIMES                    
                                           INDEXED BY ST-STORE-INDEX.           
               10  ST-STORE-NMBR           PIC 9(04) VALUE ZEROS.               
               10  ST-STORE-NAME           PIC X(25) VALUE SPACES.              
               10  ST-STORE-ON-RPT-IND     PIC X(01).                           
                   88  ST-STORE-ON-RPT               VALUE 'Y'.                 
                   88  ST-STORE-NOT-ON-RPT           VALUE 'N'.                 
      *                                                                         
       01 PH-PAGE-HEADINGS.                                                     
          05  PH-PAGE-HEADER-2.                                                 
              10  FILLER                   PIC X(51) VALUE SPACES.              
              10  FILLER                   PIC X(29) VALUE                      
                 'PERM PC STORE DOCUMENT REPORT'.                               
              10  FILLER                   PIC X(51) VALUE SPACES.              
          05  PH-PAGE-HEADER-3.                                                 
              10  FILLER                   PIC X(37) VALUE SPACES.              
              10  FILLER                   PIC X(18) VALUE                      
                  'EXCESSIVE UNITS = '.                                         
              10  PH3-EXCESSIVE-UNITS      PIC ZZ9   VALUE ZEROS.               
              10  FILLER                   PIC X(30) VALUE                      
                  '/EXCESSIVE PRICE DIFFERENCE = '.                             
              10  PH3-EXCESSIVE-PRICE      PIC ZZZZ9 VALUE ZEROS.               
      *                                                                         
                                                                                
          05  RH-REPORT-HEADER-INFO-1.                                          
              10  FILLER                   PIC X(03) VALUE SPACES.              
              10  RH-INFO1-STORE-NUMBER    PIC X(04) VALUE SPACES.              
              10  FILLER                   PIC X(01) VALUE SPACES.              
              10  RH-INFO1-STORE-NAME      PIC X(25) VALUE SPACES.              
              10  FILLER                   PIC X(42) VALUE SPACES.              
                                                                                
       01 DH-DETAIL-HEADINGS.                                                   
          05  DH-DETAIL-HEADER-1.                                               
W24472        10  FILLER                   PIC X(62) VALUE SPACES.              
W24472        10  FILLER                   PIC X(34) VALUE                      
W24472            'OLD           NEW   PRICE CHANGE  '.                         
W24472        10  FILLER                   PIC X(08) VALUE SPACES.              
          05  DH-DETAIL-HEADER-2.                                               
              10  FILLER                   PIC X(03) VALUE SPACES.              
W24472        10  FILLER                   PIC X(50) VALUE                      
W24472            'COMPLETED   DOCUMENT   DPT CL   SKU     QUANTITY  '.         
W24472        10  FILLER                   PIC X(42) VALUE                      
W24472            '       PRICE         PRICE     AMOUNT     '.                 
W24472        10  FILLER                   PIC X(14) VALUE                      
W24472            'MESSAGE       '.                                             
                                                                                
       01 RD-REPORT-DETAIL-LINES.                                               
          05  RD-DETAIL-LINE1.                                                  
              10  FILLER                   PIC X(02) VALUE SPACES.              
              10  RD-LINE1-EFF-DATE.                                            
                  15  RD-LINE1-EFF-MM      PIC X(02) VALUE SPACES.              
                  15  RD-LINE1-EFF-SLASH1  PIC X(01) VALUE SPACES.              
                  15  RD-LINE1-EFF-DD      PIC X(02) VALUE SPACES.              
                  15  RD-LINE1-EFF-SLASH2  PIC X(01) VALUE SPACES.              
                  15  RD-LINE1-EFF-CC      PIC X(02) VALUE SPACES.              
                  15  RD-LINE1-EFF-YY      PIC X(02) VALUE SPACES.              
              10  FILLER                   PIC X(04) VALUE SPACES.              
              10  RD-LINE1-DOCUMENT-NBR    PIC X(07) VALUE SPACES.              
              10  FILLER                   PIC X(03) VALUE SPACES.              
              10  RD-LINE1-DEPT-NBR        PIC X(03) VALUE ZEROES.              
              10  FILLER                   PIC X(01) VALUE SPACES.              
              10  RD-LINE1-CLASS-NBR       PIC X(02) VALUE SPACES.              
              10  FILLER                   PIC X(01) VALUE SPACES.              
W24472        10  RD-LINE1-SKU-NBR         PIC X(08) VALUE SPACES.              
W24472        10  FILLER                   PIC X(03) VALUE SPACES.              
              10  RD-LINE1-QUANTITY        PIC ZZZ,ZZZ                          
                                                     VALUE ZEROS.               
W24472        10  FILLER                   PIC X(02) VALUE SPACES.              
              10  RD-LINE1-OLD-PRICE       PIC Z,ZZZ,ZZZ.99                     
                                                     VALUE ZEROS.               
W24472        10  FILLER                   PIC X(02) VALUE SPACES.              
              10  RD-LINE1-NEW-PRICE       PIC Z,ZZZ,ZZZ.99                     
                                                     VALUE ZEROS.               
W24472        10  FILLER                   PIC X(02) VALUE SPACES.              
              10  RD-LINE1-PR-CHG-AMT      PIC Z,ZZZ,ZZZ.99                     
                                                     VALUE ZEROS.               
W24472        10  FILLER                   PIC X(02) VALUE SPACES.              
W24472        10  RD-LINE1-MESSAGE         PIC X(30) VALUE SPACES.              
      *                                                                         
           EJECT                                                                
      *****************************************************************         
      *    PERMANENT PRICE CHANGE CONSTANTS AND WORK AREAS                      
      *****************************************************************         
           COPY PEWS049.                                                        
      *                                                                         
      *  DOCUMENT PRINT FILE INPUT LAYOUT                                       
      *                                                                         
           COPY PERD004.                                                        
           EJECT                                                                
      *                                                                         
      *  PERM PRICE CHANGE CONTROL AREAS                                        
      *                                                                         
           COPY PEWS048.                                                        
       EJECT                                                                    
      *                                                                         
      *  DB2 FORMATTED MESSAGES                                                 
      *                                                                         
           COPY DPWS004.                                                        
       EJECT                                                                    
      *                                                                         
      *    SELLING AND CUSTOMER SERVICE LOCATION TABLE                          
      *                                                                         
           EXEC SQL                                                             
                INCLUDE XST00001                                                
           END-EXEC.                                                            
      *                                                                         
      *   DB2 PERM PRICE CHANGE CONTROL TABLE                                   
      *                                                                         
            EXEC SQL                                                            
                INCLUDE TPPCCTL                                                 
            END-EXEC.                                                           
      ******************************************************************        
W24472*                                                                         
W24472*   DB2 CROSS REFERENCE TABLE                                             
W24472*                                                                         
W24472******************************************************************        
W24472      EXEC SQL                                                            
W24472          INCLUDE TSKXREF                                                 
W24472      END-EXEC.                                                           
       EJECT                                                                    
      *                                                                         
      *   DB2 STORE TABLE CURSOR                                                
      *                                                                         
            EXEC SQL                                                            
                DECLARE STORE-CURSOR CURSOR FOR                                 
                    SELECT                                                      
                        LOC_NBR,                                                
                        LOC_NM                                                  
                    FROM XST_LOC                                                
                    WHERE LOC_TYP_CDE  = :PE049-DISTRB-CNTR-LOC-TYP-CDE         
                       OR LOC_TYP_CDE  = :PE049-CENT-STK-LOC-TYP-CDE            
                       OR LOC_TYP_CDE  = :PE049-DEPT-STR-LOC-TYP-CDE            
                       OR LOC_TYP_CDE  = :PE049-RET-TO-VND-LOC-TYP-CDE          
                    ORDER BY LOC_NBR                                            
            END-EXEC.                                                           
       EJECT                                                                    
      *                                                                         
      *   DB2 COMMUNICATIONS AREA                                               
      *                                                                         
            EXEC SQL                                                            
                INCLUDE SQLCA                                                   
            END-EXEC.                                                           
       EJECT                                                                    
      *                                                                         
      *  LAYOUT FOR THE STANDARD 132 WIDE REPORT HEADER                         
      *                                                                         
           COPY DPWS132O.                                                       
           EJECT                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *                                                                         
      ******************************************************************        
      * A100-MAIN-MODULE  - HIGHEST LEVEL CONTROL ROUTINE.             *        
      ******************************************************************        
       A100-MAIN-MODULE.                                                        
                                                                                
           PERFORM B300-INITIALIZE.                                             
                                                                                
           PERFORM B200-READ-EXCESSIVE-UNITS-FILE.                              
                                                                                
           SET ST-STORE-INDEX TO 1.                                             
           PERFORM B100-PROCESS-RECORDS                                         
               UNTIL PS-END-OF-FILE.                                            
                                                                                
      *    PERFORM B400-PROCESS-STR-WITH-NO-DOCS                                
      *       VARYING ST-STORE-INDEX                                            
      *       FROM 1 BY 1                                                       
      *       UNTIL (ST-STORE-INDEX > PC-STORE-INDEX-MAX                        
      *          OR  ST-STORE-NMBR(ST-STORE-INDEX) = SPACES).                   
                                                                                
           PERFORM B500-CLOSE-FILES.                                            
           STOP RUN.                                                            
           EJECT                                                                
                                                                                
      ******************************************************************        
      * B100-PROCESS-RECORDS - PROCESS EACH RECORD ON THE SORTED INPUT          
      * EXCESSIVE UNITS FILE.                                                   
      *   1. DETERMINE IF THE STORE READ IS A NEW STORE NUMBER.  IF SO,         
      *      READ THE STORE TABLE TO RETRIEVE THE STORE DESCRIPTION,            
      *      AND WRITE NEW PAGE HEADINGS.                                       
      *   2. PRINT THE DETAIL REPORT LINE                                       
      *                                                                         
      *   PERFORMED BY : A100-MAIN-MODULE                                       
      ******************************************************************        
       B100-PROCESS-RECORDS.                                                    
           IF PE004-STORE-NBR = ST-STORE-NMBR(ST-STORE-INDEX)                   
               SET ST-STORE-ON-RPT (ST-STORE-INDEX)                             
                                           TO TRUE                              
               MOVE ST-STORE-NAME(ST-STORE-INDEX)                               
                                           TO RH-INFO1-STORE-NAME               
           ELSE                                                                 
               SET ST-STORE-INDEX TO 1                                          
               SEARCH ST-STORE-INFORMATION                                      
                   AT END                                                       
                     DISPLAY '** ABEND **'                                      
                     DISPLAY PC-CURRENT-PROGRAM-NAME                            
                     DISPLAY 'B100-PROCESS-RECORDS'                             
                     DISPLAY 'STR NOT FOUND IN ST-STORE-TABLE'                  
                     DISPLAY 'STORE: ' PE004-STORE-NBR                          
                     SET ABEND-4004-TABLE-ERROR                                 
                                           TO TRUE                              
                     CALL 'ILBOABN0' USING ABEND-CODE                           
                   WHEN PE004-STORE-NBR = ST-STORE-NMBR                         
                                             (ST-STORE-INDEX)                   
                     SET ST-STORE-ON-RPT (ST-STORE-INDEX)                       
                                           TO TRUE                              
                     MOVE ST-STORE-NAME(ST-STORE-INDEX)                         
                                           TO RH-INFO1-STORE-NAME               
               END-SEARCH                                                       
           END-IF.                                                              
           MOVE PE004-STORE-NBR            TO PV-PREV-STORE-NBR.                
           MOVE SPACES                     TO PV-PREV-EFF-DATE.                 
           MOVE ZEROS                      TO PV-PREV-DOCUMENT-NBR.             
           MOVE 0                          TO PV-PAGE-NBR.                      
           MOVE PE004-STORE-NBR            TO RH-INFO1-STORE-NUMBER.            
           MOVE PC-LINE-MAX                TO PV-LINE-COUNT.                    
           PERFORM                                                              
               UNTIL ((PS-END-OF-FILE)                                          
                   OR (PE004-STORE-NBR NOT = PV-PREV-STORE-NBR))                
               PERFORM C100-PRINT-DETAIL-INFO                                   
               PERFORM B200-READ-EXCESSIVE-UNITS-FILE                           
           END-PERFORM.                                                         
                                                                                
           EJECT                                                                
                                                                                
      ******************************************************************        
      * B200-READ-EXCESSIVE-UNITS-FILE - READS THE SORTED INPUT                 
      *     PERFORMED BY : A100-MAINLINE                                        
      *                    B100-PROCESS-RECORDS                                 
      ******************************************************************        
       B200-READ-EXCESSIVE-UNITS-FILE.                                          
           READ EXCESSIVE-UNITS-FILE                                            
               INTO PE004-EXCESSIVE-UNITS-RECORD                                
               AT END                                                           
               SET PS-END-OF-FILE           TO TRUE                             
           END-READ.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * B300-INITIALIZE - GET DATE,TIME, OPEN FILES.                            
      *     PERFORMED BY : A100-MAINLINE.                                       
      ******************************************************************        
       B300-INITIALIZE.                                                         
           ACCEPT PV-DATE FROM DATE.                                            
           ACCEPT PV-TIME FROM TIME.                                            
           OPEN INPUT  EXCESSIVE-UNITS-FILE                                     
                OUTPUT EXCESSIVE-UNITS-REPORT.                                  
                                                                                
           INITIALIZE ST-STORE-TABLE.                                           
           EXEC SQL                                                             
               OPEN STORE-CURSOR                                                
           END-EXEC.                                                            
           IF SQLCODE NOT = 0                                                   
               MOVE 'B300-INITIALIZE'                                           
                                    TO PV-PARAGRAPH-NAME                        
               MOVE 'OPEN STORE CURSOR '                                        
                                    TO PV-DB2-FUNCTION                          
               PERFORM Z999-DB2-ABEND                                           
           END-IF.                                                              
                                                                                
           PERFORM                                                              
               VARYING ST-STORE-INDEX                                           
               FROM 1 BY 1                                                      
               UNTIL (SQLCODE = PC-ROW-NOT-FOUND                                
                  OR ST-STORE-INDEX > PC-STORE-INDEX-MAX)                       
               EXEC SQL                                                         
                   FETCH STORE-CURSOR                                           
                    INTO :XST00001-LOC-NBR,                                     
                         :XST00001-LOC-NM                                       
               END-EXEC                                                         
               IF SQLCODE = 0                                                   
                   MOVE XST00001-LOC-NBR   TO ST-STORE-NMBR                     
                                              (ST-STORE-INDEX)                  
                   MOVE XST00001-LOC-NM    TO ST-STORE-NAME                     
                                              (ST-STORE-INDEX)                  
                   SET ST-STORE-NOT-ON-RPT (ST-STORE-INDEX)                     
                                           TO TRUE                              
               ELSE                                                             
                   IF SQLCODE = PC-ROW-NOT-FOUND                                
                       CONTINUE                                                 
                   ELSE                                                         
                       MOVE 'B300-INITIALIZE'                                   
                                           TO PV-PARAGRAPH-NAME                 
                       MOVE 'FETCH STORE CURSOR'                                
                                           TO PV-DB2-FUNCTION                   
                       PERFORM Z999-DB2-ABEND                                   
                   END-IF                                                       
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           EXEC SQL                                                             
               CLOSE STORE-CURSOR                                               
           END-EXEC.                                                            
           IF SQLCODE NOT = 0                                                   
               MOVE 'B300-INITIALIZE'      TO PV-PARAGRAPH-NAME                 
               MOVE 'CLOSE STORE CURSOR'   TO PV-DB2-FUNCTION                   
               PERFORM Z999-DB2-ABEND                                           
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               SELECT PARM_AMT                                                  
                 INTO :PPCCTL-PARM-AMT                                          
                 FROM TPPCCTL                                                   
                 WHERE (OPERCO_NBR = :PC-OPERCO-NBR                             
                  AND   PPCCTL_NBR = :PE048-INSTR-EXCESSIVE-UNIT-NBR)           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   MOVE PPCCTL-PARM-AMT    TO                                   
                                         PE048-INSTR-EXCESSIVE-UNIT-AMT         
                   MOVE PE048-INSTR-EXCESSIVE-UNIT-AMT                          
                                           TO PH3-EXCESSIVE-UNITS               
               WHEN OTHER                                                       
                   MOVE 'B300-INITIALIZE'                                       
                                       TO PV-PARAGRAPH-NAME                     
                   MOVE 'SELECT EXCESSIVE UNIT AMT'                             
                                       TO PV-DB2-FUNCTION                       
                   PERFORM Z999-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
           EXEC SQL                                                             
               SELECT PARM_AMT                                                  
                 INTO :PPCCTL-PARM-AMT                                          
                 FROM TPPCCTL                                                   
                 WHERE (OPERCO_NBR = :PC-OPERCO-NBR                             
                  AND   PPCCTL_NBR = :PE048-INSTR-EXCESSIVE-DLR-NBR)            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   MOVE PPCCTL-PARM-AMT    TO                                   
                                           PE048-INSTR-EXCESSIVE-DLR-AMT        
                   MOVE PE048-INSTR-EXCESSIVE-DLR-AMT                           
                                           TO PH3-EXCESSIVE-PRICE               
               WHEN OTHER                                                       
                   MOVE 'B300-INITIALIZE'                                       
                                       TO PV-PARAGRAPH-NAME                     
                   MOVE 'SELECT EXCESSIVE DLR AMT'                              
                                       TO PV-DB2-FUNCTION                       
                   PERFORM Z999-DB2-ABEND                                       
           END-EVALUATE.                                                        
       EJECT                                                                    
      ******************************************************************        
      * B400-PROCESS-STR-WITH-NO-DOCS - WRITES A REPORT WHICH STATES            
      *    THAT THE STORE DID NOT HAVE EXCESSIVE QUANITITES FOR ANY             
      *    OF THEIR DOCUMENTS.                                                  
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      ******************************************************************        
       B400-PROCESS-STR-WITH-NO-DOCS.                                           
           IF ST-STORE-NOT-ON-RPT(ST-STORE-INDEX)                               
               MOVE ST-STORE-NMBR(ST-STORE-INDEX) TO                            
                                                  RH-INFO1-STORE-NUMBER         
               MOVE ST-STORE-NAME(ST-STORE-INDEX) TO                            
                                                  RH-INFO1-STORE-NAME           
               MOVE 1                             TO PV-PAGE-NBR                
               PERFORM C200-PRINT-PAGE-HEADINGS                                 
               PERFORM C300-PRINT-HEADER-INFO                                   
               WRITE EXCESSIVE-UNITS-LINE                                       
                   FROM PC-STORE-WITH-NO-DOC-LINE                               
                   AFTER ADVANCING PC-TWO-LINES                                 
           END-IF.                                                              
       EJECT                                                                    
                                                                                
      ******************************************************************        
      * B500-CLOSE-FILES - CLOSES ALL FILES                                     
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      ******************************************************************        
       B500-CLOSE-FILES.                                                        
           CLOSE EXCESSIVE-UNITS-FILE                                           
                 EXCESSIVE-UNITS-REPORT.                                        
       EJECT                                                                    
                                                                                
      ******************************************************************        
      * C100-PRINT-DETAIL-INFO - PRINTS THE DETAIL LINE ON THE EXCESSIVE        
      * UNITS REPORT.                                                           
      *     PERFORMED BY : B100-PROCESS-RECORDS.                                
      ******************************************************************        
       C100-PRINT-DETAIL-INFO.                                                  
           ADD PC-TWO-LINES                 TO PV-LINE-COUNT.                   
           IF PV-LINE-COUNT > PC-LINE-MAX                                       
               ADD +1                       TO PV-PAGE-NBR                      
               PERFORM C200-PRINT-PAGE-HEADINGS                                 
               PERFORM C300-PRINT-HEADER-INFO                                   
               MOVE SPACES                  TO PV-PREV-EFF-DATE                 
               MOVE ZEROS                   TO PV-PREV-DOCUMENT-NBR             
           ELSE                                                                 
               COMPUTE PV-LINE-COUNT = PV-LINE-COUNT - PC-TWO-LINES             
           END-IF.                                                              
                                                                                
           IF PE004-DOC-NBR = PV-PREV-DOCUMENT-NBR                              
               MOVE SPACES                  TO RD-LINE1-DOCUMENT-NBR            
                                               RD-LINE1-EFF-DATE                
               ADD PC-ONE-LINE              TO PV-LINE-COUNT                    
               MOVE PC-ONE-LINE             TO PV-LINES-TO-ADVANCE              
           ELSE                                                                 
               IF PE004-EFF-DTE = PV-PREV-EFF-DATE                              
                   MOVE SPACES              TO RD-LINE1-EFF-DATE                
                   MOVE PE004-DOC-NBR       TO RD-LINE1-DOCUMENT-NBR            
                                               PV-PREV-DOCUMENT-NBR             
                   ADD PC-TWO-LINES         TO PV-LINE-COUNT                    
                   MOVE PC-TWO-LINES        TO PV-LINES-TO-ADVANCE              
               ELSE                                                             
                   MOVE PE004-EFF-DTE                                           
                                            TO PV-PREV-EFF-DATE                 
                   MOVE PV-PREV-EFF-MM      TO RD-LINE1-EFF-MM                  
                   MOVE PV-PREV-EFF-DD      TO RD-LINE1-EFF-DD                  
                   MOVE PV-PREV-EFF-CC      TO RD-LINE1-EFF-CC                  
                   MOVE PV-PREV-EFF-YY      TO RD-LINE1-EFF-YY                  
                   MOVE PC-DATE-SLASH       TO RD-LINE1-EFF-SLASH1              
                                               RD-LINE1-EFF-SLASH2              
                   MOVE PE004-DOC-NBR       TO RD-LINE1-DOCUMENT-NBR            
                                               PV-PREV-DOCUMENT-NBR             
                   ADD PC-TWO-LINES         TO PV-LINE-COUNT                    
                   MOVE PC-TWO-LINES        TO PV-LINES-TO-ADVANCE              
               END-IF                                                           
           END-IF.                                                              
W24472     MOVE PE004-SKU-NBR               TO RD-LINE1-SKU-NBR.                
W24472     IF PE004-SKU-NBR (6:3) = '000' OR '999'                              
W24472         MOVE PE004-SKU-NBR (1:3)     TO RD-LINE1-DEPT-NBR                
W24472         MOVE PE004-SKU-NBR (4:2)     TO RD-LINE1-CLASS-NBR               
W24472     ELSE                                                                 
W24472         PERFORM C150-GET-DPT-CL-XREF                                     
W24472     END-IF.                                                              
           MOVE PE004-QUANTITY              TO RD-LINE1-QUANTITY.               
           MOVE PE004-PR-CHG-AMT            TO RD-LINE1-PR-CHG-AMT.             
           MOVE PE004-OLD-PRICE             TO PV-HOLD-OLD-PRICE.               
           MOVE PV-HOLD-OLD-PRICE           TO RD-LINE1-OLD-PRICE.              
           MOVE PE004-NEW-PRICE             TO PV-HOLD-NEW-PRICE.               
           MOVE PV-HOLD-NEW-PRICE           TO RD-LINE1-NEW-PRICE.              
           IF PE004-EXCESSIVE-UNITS-PRICE                                       
               MOVE PC-BOTH-UNITS-AND-PRICE-MSG                                 
                                            TO RD-LINE1-MESSAGE                 
           END-IF.                                                              
           IF PE004-EXCESSIVE-UNITS                                             
               MOVE PC-UNITS-MSG            TO RD-LINE1-MESSAGE                 
           END-IF.                                                              
           IF PE004-EXCESSIVE-PRICE-DIFF                                        
               MOVE PC-PRICE-MSG            TO RD-LINE1-MESSAGE                 
           END-IF.                                                              
           WRITE EXCESSIVE-UNITS-LINE                                           
               FROM RD-DETAIL-LINE1                                             
               AFTER ADVANCING PV-LINES-TO-ADVANCE.                             
       EJECT                                                                    
W24472                                                                          
W24472******************************************************************        
W24472* C150-GET-DPT-CL-XREF.                                                   
W24472* THIS PARAGRAPH RETRIEVES DEPT AND CLASS FROM TSKXREF AND MOVES          
W24472* IT TO DEPT AND CLASS VARIABLES IN THE REPORT.                           
W24472******************************************************************        
W24472 C150-GET-DPT-CL-XREF.                                                    
W24472                                                                          
W24472     MOVE PE004-SKU-NBR   TO  PV-SKU.                                     
W24472     MOVE PV-SKU          TO SKXREF-SKU.                                  
W24472                                                                          
W24472         EXEC SQL                                                         
W24472             SELECT                                                       
W24472              DEPT,                                                       
W24472              SUBCLASS                                                    
W24472             INTO                                                         
W24472              :SKXREF-DEPT,                                               
W24472              :SKXREF-SUBCLASS                                            
W24472             FROM TSKXREF                                                 
W24472             WHERE SKU = :SKXREF-SKU                                      
W24472         END-EXEC                                                         
W24472                                                                          
W24472         IF SQLCODE = 0                                                   
W24472             MOVE SKXREF-DEPT          TO PV-DEPT-NBR-9                   
W24472             MOVE PV-DEPT-NBR-9(2:3)   TO RD-LINE1-DEPT-NBR               
W24472             MOVE SKXREF-SUBCLASS      TO PV-CLASS-NBR-9                  
W24472             MOVE PV-CLASS-NBR-9(3:2)  TO RD-LINE1-CLASS-NBR              
W24472         ELSE                                                             
W24472             IF SQLCODE = PC-ROW-NOT-FOUND                                
W24472                 MOVE ZEROS              TO RD-LINE1-DEPT-NBR             
W24472                                            RD-LINE1-CLASS-NBR            
W24472             ELSE                                                         
W24472                 MOVE 'C150-GET-DPT-CL-XREF'                              
W24472                                     TO PV-PARAGRAPH-NAME                 
W24472                 MOVE 'SELECT FROM TSKXREF'                               
W24472                                     TO PV-DB2-FUNCTION                   
W24472                 PERFORM Z999-DB2-ABEND                                   
W24472             END-IF                                                       
W24472         END-IF.                                                          
W24472*                                                                         
      ******************************************************************        
      * C200-PRINT-PAGE-HEADINGS - PRINTS THE PAGE HEADINGS OF THE              
      * EXCESSIVE UNITS REPORT                                                  
      *     PERFORMED BY : B100-PROCESS-RECORDS                                 
      *                    C100-PRINT-DETAIL-INFO                               
      ******************************************************************        
       C200-PRINT-PAGE-HEADINGS.                                                
           MOVE PV-MM                       TO DP132O-RUN-MONTH.                
           MOVE PV-DD                       TO DP132O-RUN-DAY.                  
           MOVE PV-YY                       TO DP132O-RUN-YEAR.                 
           MOVE PV-HH                       TO DP132O-RUN-HOUR.                 
           MOVE PV-MIN                      TO DP132O-RUN-MINUTE.               
           MOVE PC-CURRENT-PROGRAM-NAME     TO DP132O-PROGRAM-NAME.             
           MOVE 1                           TO DP132O-REPORT-NUMBER.            
           MOVE PV-PAGE-NBR                 TO DP132O-PAGE-NUMBER.              
           WRITE EXCESSIVE-UNITS-LINE                                           
               FROM DP132O-STANDRD-HEADER-132-COLS                              
               AFTER ADVANCING PAGE.                                            
           INITIALIZE DP132O-LASER-NEW-PAGE-INDICATR.                           
           MOVE PC-ONE-LINE                 TO PV-LINES-TO-ADVANCE.             
           WRITE EXCESSIVE-UNITS-LINE                                           
               FROM PH-PAGE-HEADER-2                                            
               AFTER ADVANCING PV-LINES-TO-ADVANCE.                             
           MOVE PC-ONE-LINE                 TO PV-LINES-TO-ADVANCE.             
           WRITE EXCESSIVE-UNITS-LINE                                           
               FROM PH-PAGE-HEADER-3                                            
               AFTER ADVANCING PV-LINES-TO-ADVANCE.                             
           MOVE PC-THREE-LINES              TO PV-LINE-COUNT.                   
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
      * C300-PRINT-HEADER-INFO - PRINTS THE PRINT DATE AND STORE HEADING        
      * LINES.                                                                  
      *     PERFORMED BY : B100-PROCESS-RECORDS                                 
      *                    C100-PRINT-DETAIL-INFO                               
      ******************************************************************        
       C300-PRINT-HEADER-INFO.                                                  
                                                                                
                                                                                
           MOVE PC-TWO-LINES                TO PV-LINES-TO-ADVANCE.             
           WRITE EXCESSIVE-UNITS-LINE                                           
               FROM RH-REPORT-HEADER-INFO-1                                     
               AFTER ADVANCING PV-LINES-TO-ADVANCE.                             
                                                                                
           MOVE PC-ONE-LINE                 TO PV-LINES-TO-ADVANCE.             
           WRITE EXCESSIVE-UNITS-LINE                                           
               FROM DH-DETAIL-HEADER-1                                          
               AFTER ADVANCING PV-LINES-TO-ADVANCE.                             
                                                                                
           MOVE PC-ONE-LINE                 TO PV-LINES-TO-ADVANCE.             
           WRITE EXCESSIVE-UNITS-LINE                                           
               FROM DH-DETAIL-HEADER-2                                          
               AFTER ADVANCING PV-LINES-TO-ADVANCE.                             
                                                                                
           ADD PC-FOUR-LINES                TO PV-LINE-COUNT.                   
       EJECT                                                                    
                                                                                
      ******************************************************************        
      * Z999-DB2-ABEND - DB2 ABEND ROUTINE                                      
      *   PERFORMED BY : B300-INITIALIZE                                        
      ******************************************************************        
       Z999-DB2-ABEND.                                                          
                                                                                
            DISPLAY '** ABEND **'.                                              
            DISPLAY '** PROGRAM ** = ' PC-CURRENT-PROGRAM-NAME.                 
            DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                     
            DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                       
      *                                                                         
      * ROUTINE TO DISPLAY ABEND IN READABLE FORMAT                             
           COPY DPPD004.                                                        
            SET ABEND-4013-DB2-ERROR        TO TRUE.                            
            CALL 'ILBOABN0' USING ABEND-CODE.                                   
