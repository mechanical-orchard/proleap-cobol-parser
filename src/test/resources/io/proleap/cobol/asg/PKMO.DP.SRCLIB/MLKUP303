       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP303                                          00030000
       AUTHOR.        SCOTT JACKSON                                     00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  5-6-98.                                           00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP303 - LEDGER DATABASE UPDATE PROGRAM            *00100000
      *                                                                *00110000
W26603******************************************************************00120000
W26603* CHG ID   DATE        DESCRIPTION                               *00130000
W26603* ------   ----------  ----------------------------------------- *00140000
W26603* W26603   04-14-2004  STORE EXPANSION.  EXPANDED SOME PROGRAM   *00150000
W26603*                      VARIABLES TO ACCOMODATE INCREASED LENGTH  *00160000
W26603*                      OF DOLLAR AMOUNTS.  ADDED 'WITH UR' TO    *00170000
W26603*                      A DB2 SELECT.                             *00180000
W26603*                                                                *00190000
W26603*                      - RONNA MCDONALD                          *00200000
      ******************************************************************00210000
                                                                        00220000
                                                                        00230000
       ENVIRONMENT DIVISION.                                            00240000
                                                                        00250000
       CONFIGURATION SECTION.                                           00260000
       SOURCE-COMPUTER.        IBM-3090.                                00270000
       OBJECT-COMPUTER.        IBM-3090.                                00280000
       INPUT-OUTPUT SECTION.                                            00290000
       FILE-CONTROL.                                                    00300000
           SELECT CONDENSED-FILE                                        00310000
               ASSIGN TO INP01.                                         00320000
                                                                        00330000
                                                                        00340000
       DATA DIVISION.                                                   00350000
       FILE SECTION.                                                    00360000
                                                                        00370000
       FD  CONDENSED-FILE                                               00380000
           LABEL RECORDS ARE STANDARD                                   00390000
           RECORDING MODE IS F                                          00400000
           BLOCK CONTAINS 0 RECORDS.                                    00410000
       01  CONDENSED-RECORD            PIC X(100).                      00420000
      *                                                                 00430000
                                                                        00440000
                                                                        00450000
       WORKING-STORAGE SECTION.                                         00460000
                                                                        00470000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00480000
                                                                        00490000
       01  PC-PROGRAM-CONSTANTS.                                        00500000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK235'.    00510000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP303'.  00520000
           05  PC-MAX-RETRIES          PIC S9(03) VALUE +3.             00530000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00540000
                                                                        00550000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00560000
           05  PE-MSG-01                       PIC X(50) VALUE          00570000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED   '.    00580000
           05  PE-MSG-02                       PIC X(50) VALUE          00590000
                'ATTEMPT TO UPDATE ROW ON TABLE TMNDPT FAILED     '.    00600000
           05  PE-MSG-03                       PIC X(50) VALUE          00610000
                'ATTEMPT TO INSERT ROW ON TABLE TMNDPT FAILED     '.    00620000
           05  PE-MSG-04                       PIC X(50) VALUE          00630000
                'ERROR IN SELECT ''STATUS CODE'' FROM TCKRSTCNTL  '.    00640000
           05  PE-MSG-05                       PIC X(50) VALUE          00650000
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00660000
           05  PE-MSG-06                       PIC X(50) VALUE          00670000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00680000
           05  PE-MSG-07                       PIC X(50) VALUE          00690000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00700000
           05  PE-MSG-08                       PIC X(50) VALUE          00710000
                'SELECT OF CHECKPOINT RESTART INFO FROM TCKRSTINF'.     00720000
           05  PE-MSG-09                       PIC X(50) VALUE          00730000
                'SET HOLD TIMESTAMP EQUAL TO CURRENT TIMESTAMP  '.      00740000
           05  PE-MSG-10                       PIC X(50) VALUE          00750000
                'UPDATE OF STATUS CODE ON TCKRSTCNTL UNSUCCESSFUL'.     00760000
           05  PE-MSG-11                       PIC X(50) VALUE          00770000
                'ATTEMPT TO SELECT TIMESTAMP + CKPT_FRQNCY_QTY   '.     00780000
      *                                                                 00790000
       01  PS-PROGRAM-SWITCHES.                                         00800000
           05  PS-CONDENSED-FILE-EOF-SW     PIC  X        VALUE 'N'.    00810000
               88  PS-MORE-INPUT-RECORDS                  VALUE 'N'.    00820000
               88  PS-NO-MORE-INPUT-RECORDS               VALUE 'Y'.    00830000
           05  PS-EMPTY-FILE-SW             PIC  X        VALUE 'N'.    00840000
               88  PS-EMPTY-INPUT-FILE                    VALUE 'Y'.    00850000
           05  PS-MNDPT-CURSOR-EOF-SW       PIC  X        VALUE 'N'.    00860000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00870000
           05  PS-FIRST-COMMIT-SW           PIC X         VALUE 'N'.    00880000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00890000
           05  PS-DEADLOCK-RESTART-SW       PIC X         VALUE 'N'.    00900000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00910000
           05  PS-PROCESSING-OPTIONS        PIC X         VALUE 'P'.    00920000
               88  PS-UPDATE-MNDPT-ROW                    VALUE 'U'.    00930000
               88  PS-INSERT-MNDPT-ROW                    VALUE 'I'.    00940000
      *                                                                 00950000
       01  PROGRAM-VARIABLES.                                           00960000
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     00970000
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 00980000
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 00990000
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01000000
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01010000
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01020000
      *                                                                 01030000
       01  PA-PROGRAM-ACCUMULATORS.                                     01040000
           05  PA-RECORD-COUNTS.                                        01050000
               10  PA-INPUT-COUNT              PIC  9(9)  VALUE ZEROES. 01060000
               10  PA-UPDATE-COUNT             PIC  9(9)  VALUE ZEROES. 01070000
               10  PA-INSERT-COUNT             PIC  9(9)  VALUE ZEROES. 01080000
               10  PA-DEPTS-PROCESSED          PIC  9(9)  VALUE ZEROES. 01090000
               10  PA-COMMIT-COUNT             PIC  9(9)  VALUE ZEROES. 01100000
      *                                                                 01110000
       01  SAVE-AREA.                                                   01120000
           05  SV-PRIMARY-KEY.                                          01130000
               10  SV-FSCL-MN-NBR        PIC X(02)         VALUE SPACES.01140000
               10  SV-FSCL-MN-END-DTE    PIC X(10)         VALUE SPACES.01150000
               10  SV-DEPT-NBR           PIC X(03)         VALUE SPACES.01160000
           05  SV-UPDATE-FIELDS.                                        01170000
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01180000
W26603         10  SV-RCPT-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01190000
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01200000
W26603         10  SV-PADJ-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01210000
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01220000
W26603         10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01230000
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01240000
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01250000
W26603         10  SV-XFER-OUT-RTL       PIC S9(11)V99  COMP-3 VALUE +0.01260000
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01270000
      *                                                                 01280000
      *                                                                 01290000
      ******************************************************************01300000
      * MLRD106 - M/L MONTHLY DEPT DATABASE UPDATE TRANSACTION LAYOUT   01310000
      ******************************************************************01320000
           COPY MLRD106.                                                01330000
      *                                                                 01340000
      ******************************************************************01350000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01360000
      ******************************************************************01370000
           COPY DPWS004.                                                01380000
      *                                                                 01390000
      ******************************************************************01400000
      *  USED FOR DB2 ERRORS                                            01410000
      ******************************************************************01420000
           EXEC SQL                                                     01430000
               INCLUDE SQLCA                                            01440000
           END-EXEC.                                                    01450000
      *                                                                 01460000
      ******************************************************************01470000
      *  M/L MONTH-TO-DATE DEPT TABLE                                   01480000
      ******************************************************************01490000
           EXEC SQL                                                     01500000
               INCLUDE TMNDPT                                           01510000
           END-EXEC.                                                    01520000
      *                                                                 01530000
      ***************************************************************** 01540000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01550000
      ***************************************************************** 01560000
           EXEC SQL                                                     01570000
               INCLUDE TCKRSTCN                                         01580000
           END-EXEC.                                                    01590000
      *                                                                 01600000
           EXEC SQL                                                     01610000
               INCLUDE TCKRSTIN                                         01620000
           END-EXEC.                                                    01630000
      *                                                                 01640000
      ******************************************************************01650000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01660000
      ******************************************************************01670000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01680000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +61  COMP. 01690000
           05  CR-CHECKPOINT-RESTART-VAR.                               01700000
               10  CR-PREV-DTL-KEY          PIC  X(15) VALUE SPACES.    01710000
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01720000
                   15  CR-FISCAL-NBR        PIC  X(02).                 01730000
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                 01740000
                   15  CR-DEPT-NBR          PIC  X(03).                 01750000
               10  FILLER                   PIC  X(01) VALUE SPACES.    01760000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01770000
               10  CR-UPDATE-COUNT          PIC  9(09) VALUE ZEROES.    01780000
               10  CR-INSERT-COUNT          PIC  9(09) VALUE ZEROES.    01790000
               10  CR-DEPTS-PROCESSED       PIC  9(09) VALUE ZEROES.    01800000
               10  CR-TMNDPT-COMMIT-COUNT   PIC  9(09) VALUE ZEROES.    01810000
      *                                                                 01820000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01830000
           05  CK-SAVE-PREV-KEY         PIC  X(15) VALUE SPACES.        01840000
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01850000
               10  CK-FISCAL-NBR        PIC  X(02).                     01860000
               10  CK-FISCAL-MN-END-DTE PIC  X(10).                     01870000
               10  CK-DEPT-NBR          PIC  X(03).                     01880000
      *                                                                 01890000
      *                                                                 01900000
      *                                                                 01910000
      *                                                                 01920000
      *-------------------*                                             01930000
       PROCEDURE DIVISION.                                              01940000
      *-------------------*                                             01950000
                                                                        01960000
       0000-MAIN-MODULE.                                                01970000
                                                                        01980000
           PERFORM 1000-INITIALIZATION.                                 01990000
      *                                                                 02000000
           PERFORM 2000-PROCESS-INPUT                                   02010000
                UNTIL PS-NO-MORE-INPUT-RECORDS.                         02020000
      *                                                                 02030000
           PERFORM 8000-EOJ-ROUTINE.                                    02040000
                                                                        02050000
           STOP RUN.                                                    02060000
      *                                                                 02070000
      *                                                                 02080000
      *                                                                 02090000
      ******************************************************************02100000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02110000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02120000
      *  TCKRSTCNTL-CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION  02130000
      ******************************************************************02140000
      *                                                                 02150000
       1000-INITIALIZATION.                                             02160000
      *                                                                 02170000
           OPEN INPUT CONDENSED-FILE.                                   02180000
      *                                                                 02190000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02200000
      *                                                                 02210000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02220000
                PERFORM 7500-SELECT-RESTART-INFO                        02230000
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                   02240000
                                              CR-CHECKPOINT-RESTART-VAR 02250000
                MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY       02260000
           ELSE                                                         02270000
               SET PS-FIRST-COMMIT TO TRUE                              02280000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02290000
           END-IF.                                                      02300000
      *                                                                 02310000
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02320000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02330000
            OR PS-NO-MORE-INPUT-RECORDS.                                02340000
      *                                                                 02350000
      * IF PROGRAM IN RESTART MODE, BUT NO RECORDS EXIST AFTER LAST CKPT02360000
      * MOVE RESTART COUNTS TO PA-FIELDS FOR ACCURATE DISPLAY AT EOJ    02370000
           IF PS-NO-MORE-INPUT-RECORDS                                  02380000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02390000
                   MOVE CR-INPUT-READ-COUNT     TO PA-INPUT-COUNT       02400000
                   MOVE CR-TMNDPT-COMMIT-COUNT  TO PA-COMMIT-COUNT      02410000
                   MOVE CR-UPDATE-COUNT         TO PA-UPDATE-COUNT      02420000
                   MOVE CR-INSERT-COUNT         TO PA-INSERT-COUNT      02430000
                   MOVE CR-DEPTS-PROCESSED      TO PA-DEPTS-PROCESSED   02440000
               ELSE                                                     02450000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02460000
           ELSE                                                         02470000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02480000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02490000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02500000
           END-IF.                                                      02510000
      *                                                                 02520000
      *                                                                 02530000
      ******************************************************************02540000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02550000
      ******************************************************************02560000
       2000-PROCESS-INPUT.                                              02570000
      *                                                                 02580000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02590000
      *                                                                 02600000
           PERFORM 7000-SELECT-DB-ROW.                                  02610000
      *                                                                 02620000
           EVALUATE TRUE                                                02630000
               WHEN PS-UPDATE-MNDPT-ROW                                 02640000
                   PERFORM 7100-UPDATE-MNDPT-ROW                        02650000
               WHEN PS-INSERT-MNDPT-ROW                                 02660000
                   PERFORM 7200-INSERT-MNDPT-ROW                        02670000
               WHEN OTHER                                               02680000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     02690000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             02700000
                   PERFORM 9999-SQL-ABEND                               02710000
           END-EVALUATE.                                                02720000
      *                                                                 02730000
           IF PS-PROGRAM-DEADLOCKED                                     02740000
               CONTINUE                                                 02750000
           ELSE                                                         02760000
               PERFORM 7700-COMMIT-PROCESSING                           02770000
               PERFORM 4000-READ-CONDENSED-FILE.                        02780000
      *                                                                 02790000
      *                                                                 02800000
      ******************************************************************02810000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         02820000
      ******************************************************************02830000
       2100-CALCULATE-UPDATES.                                          02840000
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     02850000
      *                                                                 02860000
           COMPUTE SV-SLS-RTL-AMT       =                               02870000
                            MNDPT-SLS-RTL-AMT + ML106-SLS-RTL-AMT       02880000
      *                                                                 02890000
           COMPUTE SV-RCPT-RTL-AMT      =                               02900000
                           MNDPT-RCPT-RTL-AMT + ML106-RCPT-RTL-AMT      02910000
      *                                                                 02920000
           COMPUTE SV-RCPT-NET-COST     =                               02930000
                      MNDPT-RCPT-NET-COST-AMT + ML106-RCPT-NET-COST-AMT 02940000
      *                                                                 02950000
           COMPUTE SV-PADJ-RTL-AMT      =                               02960000
                      MNDPT-PADJ-RTL-AMT      + ML106-PADJ-RTL-AMT      02970000
      *                                                                 02980000
           COMPUTE SV-PADJ-NET-COST     =                               02990000
                      MNDPT-PADJ-NET-COST-AMT + ML106-PADJ-NET-COST-AMT 03000000
      *                                                                 03010000
           COMPUTE SV-MKDN-RTL-AMT      =                               03020000
                      MNDPT-MKDN-RTL-AMT      + ML106-MKDN-RTL-AMT      03030000
      *                                                                 03040000
           COMPUTE SV-MKUP-RTL-AMT      =                               03050000
                      MNDPT-MKUP-RTL-AMT      + ML106-MKUP-RTL-AMT      03060000
      *                                                                 03070000
           COMPUTE SV-XFER-IN-RTL-AMT   =                               03080000
                      MNDPT-XFER-IN-RTL-AMT   + ML106-XFER-IN-RTL-AMT   03090000
      *                                                                 03100000
           COMPUTE SV-XFER-OUT-RTL      =                               03110000
                      MNDPT-XFER-OUT-RTL-AMT  + ML106-XFER-OUT-RTL-AMT  03120000
      *                                                                 03130000
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03140000
                      MNDPT-INV-ADJ-RTL-AMT   + ML106-INV-ADJ-RTL-AMT.  03150000
      *                                                                 03160000
      *                                                                 03170000
      ******************************************************************03180000
      * READS THE CONDENSED FILE INTO THE TMNDPT LAYOUT                 03190000
      ******************************************************************03200000
       4000-READ-CONDENSED-FILE.                                        03210000
            READ CONDENSED-FILE INTO ML106-UPDATE-REC                   03220000
               AT END MOVE 'Y' TO PS-CONDENSED-FILE-EOF-SW.             03230000
      *                                                                 03240000
            IF PS-MORE-INPUT-RECORDS                                    03250000
                ADD 1                        TO PA-INPUT-COUNT.         03260000
      *                                                                 03270000
      ******************************************************************03280000
      * SELECT A ROW FROM THE MONTHLY DEPT TABLE THAT MATCHES INPUT KEY 03290000
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      03300000
      ******************************************************************03310000
       7000-SELECT-DB-ROW.                                              03320000
      *                                                                 03330000
           EXEC SQL                                                     03340000
               SELECT SLS_RTL_AMT                                       03350000
                     ,MKDN_RTL_AMT                                      03360000
                     ,MKUP_RTL_AMT                                      03370000
                     ,XFER_IN_RTL_AMT                                   03380000
                     ,XFER_OUT_RTL_AMT                                  03390000
                     ,RCPT_RTL_AMT                                      03400000
                     ,RCPT_NET_COST_AMT                                 03410000
                     ,PADJ_RTL_AMT                                      03420000
                     ,PADJ_NET_COST_AMT                                 03430000
                     ,INV_ADJ_RTL_AMT                                   03440000
                 INTO :MNDPT-SLS-RTL-AMT                                03450000
                     ,:MNDPT-MKDN-RTL-AMT                               03460000
                     ,:MNDPT-MKUP-RTL-AMT                               03470000
                     ,:MNDPT-XFER-IN-RTL-AMT                            03480000
                     ,:MNDPT-XFER-OUT-RTL-AMT                           03490000
                     ,:MNDPT-RCPT-RTL-AMT                               03500000
                     ,:MNDPT-RCPT-NET-COST-AMT                          03510000
                     ,:MNDPT-PADJ-RTL-AMT                               03520000
                     ,:MNDPT-PADJ-NET-COST-AMT                          03530000
                     ,:MNDPT-INV-ADJ-RTL-AMT                            03540000
               FROM TMNDPT                                              03550000
              WHERE   FSCL_MN_NBR         = :ML106-FSCL-MN-NBR          03560000
                AND   FSCL_MN_END_DTE     = :ML106-FSCL-MN-END-DTE      03570000
                AND   DEPT_NBR            = :ML106-DEPT-NBR             03580000
W26603        WITH UR                                                   03590000
           END-EXEC.                                                    03600000
      *                                                                 03610000
           EVALUATE SQLCODE                                             03620000
               WHEN 0                                                   03630000
                   SET PS-UPDATE-MNDPT-ROW        TO TRUE               03640000
               WHEN +100                                                03650000
                   SET PS-INSERT-MNDPT-ROW        TO TRUE               03660000
               WHEN OTHER                                               03670000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     03680000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             03690000
                   PERFORM 9999-SQL-ABEND                               03700000
           END-EVALUATE.                                                03710000
      *                                                                 03720000
      *                                                                 03730000
      ***************************************************************** 03740000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  03750000
      *   CURRENT TABLE VALUES UPDATED TO INCLUDE VALUES FROM INPUT REC 03760000
      ***************************************************************** 03770000
       7100-UPDATE-MNDPT-ROW.                                           03780000
      *                                                                 03790000
      *                                                                 03800000
           PERFORM 2100-CALCULATE-UPDATES.                              03810000
      *                                                                 03820000
           EXEC SQL                                                     03830000
               UPDATE TMNDPT                                            03840000
                   SET RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT           03850000
                     , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST          03860000
                     , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT           03870000
                     , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST          03880000
                     , SLS_RTL_AMT         = :SV-SLS-RTL-AMT            03890000
                     , MKDN_RTL_AMT        = :SV-MKDN-RTL-AMT           03900000
                     , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT           03910000
                     , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT        03920000
                     , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL           03930000
                     , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT        03940000
              WHERE   FSCL_MN_NBR         = :ML106-FSCL-MN-NBR          03950000
                AND   FSCL_MN_END_DTE     = :ML106-FSCL-MN-END-DTE      03960000
                AND   DEPT_NBR            = :ML106-DEPT-NBR             03970000
           END-EXEC                                                     03980000
      *                                                                 03990000
           EVALUATE SQLCODE                                             04000000
               WHEN 0                                                   04010000
                   CONTINUE                                             04020000
               WHEN -911                                                04030000
                   ADD +1             TO PV-DEADLOCK-COUNT              04040000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04050000
                       MOVE '7100-UPDATE-MNDPT-ROW' TO                  04060000
                                                    PV-PARAGRAPH-NAME   04070000
                       PERFORM 9000-DEADLOCK-ERROR                      04080000
                   ELSE                                                 04090000
                       PERFORM 7999-RESTART-PROCESS                     04100000
                   END-IF                                               04110000
               WHEN OTHER                                               04120000
                   MOVE '7100-UPDATE-MNDPT-ROW' TO PV-PARAGRAPH-NAME    04130000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED04140000
                   PERFORM 9999-SQL-ABEND                               04150000
           END-EVALUATE.                                                04160000
      *                                                                 04170000
           IF PS-PROGRAM-DEADLOCKED                                     04180000
               CONTINUE                                                 04190000
           ELSE                                                         04200000
               ADD 1 TO PA-UPDATE-COUNT                                 04210000
                        PA-DEPTS-PROCESSED.                             04220000
      *                                                                 04230000
      *                                                                 04240000
      ***************************************************************** 04250000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY DEPT TABLE. INITIAL  * 04260000
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 04270000
      * - CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN RECALC04280000
      ***************************************************************** 04290000
       7200-INSERT-MNDPT-ROW.                                           04300000
      *                                                                 04310000
      *                                                                 04320000
           EXEC SQL                                                     04330000
               INSERT INTO TMNDPT                                       04340000
                   (   FSCL_MN_NBR                                      04350000
                     , FSCL_MN_END_DTE                                  04360000
                     , DEPT_NBR                                         04370000
                     , RCPT_RTL_AMT                                     04380000
                     , RCPT_NET_COST_AMT                                04390000
                     , PADJ_RTL_AMT                                     04400000
                     , PADJ_NET_COST_AMT                                04410000
                     , SLS_RTL_AMT                                      04420000
                     , MKDN_RTL_AMT                                     04430000
                     , MKUP_RTL_AMT                                     04440000
                     , XFER_IN_RTL_AMT                                  04450000
                     , XFER_OUT_RTL_AMT                                 04460000
                     , INV_ADJ_RTL_AMT                                  04470000
                     , SHNK_RTL_AMT                                     04480000
                     , END_INV_RTL_AMT                                  04490000
                     , CUM_MKUP_PCT                                     04500000
                     , END_INV_COST_AMT                                 04510000
                     , SLS_COST_AMT                                     04520000
                     , GRO_MRGN_AMT      )                              04530000
               VALUES                                                   04540000
                   (   :ML106-FSCL-MN-NBR                               04550000
                     , :ML106-FSCL-MN-END-DTE                           04560000
                     , :ML106-DEPT-NBR                                  04570000
                     , :ML106-RCPT-RTL-AMT                              04580000
                     , :ML106-RCPT-NET-COST-AMT                         04590000
                     , :ML106-PADJ-RTL-AMT                              04600000
                     , :ML106-PADJ-NET-COST-AMT                         04610000
                     , :ML106-SLS-RTL-AMT                               04620000
                     , :ML106-MKDN-RTL-AMT                              04630000
                     , :ML106-MKUP-RTL-AMT                              04640000
                     , :ML106-XFER-IN-RTL-AMT                           04650000
                     , :ML106-XFER-OUT-RTL-AMT                          04660000
                     , :ML106-INV-ADJ-RTL-AMT                           04670000
                     , 0                                                04680000
                     , 0                                                04690000
                     , 0                                                04700000
                     , 0                                                04710000
                     , 0                                                04720000
                     , 0                       )                        04730000
           END-EXEC                                                     04740000
      *                                                                 04750000
           EVALUATE SQLCODE                                             04760000
               WHEN 0                                                   04770000
                   CONTINUE                                             04780000
               WHEN -911                                                04790000
                   ADD +1             TO PV-DEADLOCK-COUNT              04800000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04810000
                       MOVE '7200-INSERT-MNDPT-ROW' TO                  04820000
                                                    PV-PARAGRAPH-NAME   04830000
                       PERFORM 9000-DEADLOCK-ERROR                      04840000
                   ELSE                                                 04850000
                       PERFORM 7999-RESTART-PROCESS                     04860000
                   END-IF                                               04870000
               WHEN OTHER                                               04880000
                   MOVE '7200-INSERT-MNDPT-ROW' TO PV-PARAGRAPH-NAME    04890000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED04900000
                   PERFORM 9999-SQL-ABEND                               04910000
           END-EVALUATE                                                 04920000
      *                                                                 04930000
           IF PS-PROGRAM-DEADLOCKED                                     04940000
               CONTINUE                                                 04950000
           ELSE                                                         04960000
               ADD 1 TO PA-INSERT-COUNT                                 04970000
                        PA-DEPTS-PROCESSED.                             04980000
      *                                                                 04990000
      *                                                                 05000000
      ******************************************************************05010000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05020000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05030000
      *            CONTROL TABLE                                       *05040000
      ******************************************************************05050000
       7300-GET-COMMIT-TIMESTAMP.                                       05060000
                                                                        05070000
           EXEC SQL                                                     05080000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05090000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05100000
               INTO :PV-COMMIT-TIMESTAMP                                05110000
               FROM TCKRSTCNTL                                          05120000
              WHERE JOB_NAME  = :PC-JOB-NAME                            05130000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05140000
           END-EXEC.                                                    05150000
                                                                        05160000
           EVALUATE TRUE                                                05170000
               WHEN SQLCODE = 0                                         05180000
                   CONTINUE                                             05190000
               WHEN SQLCODE NOT EQUAL ZERO                              05200000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05210000
                   MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED 05220000
                   PERFORM 9999-SQL-ABEND                               05230000
           END-EVALUATE.                                                05240000
      *                                                                 05250000
      *                                                                 05260000
      ******************************************************************05270000
      * 7400-INIT-CHECKPOINT-SELECT                                    *05280000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05290000
      *            IF WE ARE IN A RESTART CONDITION.                   *05300000
      ******************************************************************05310000
       7400-INIT-CHECKPOINT-SELECT.                                     05320000
                                                                        05330000
           EXEC SQL                                                     05340000
             SELECT  CKPT_STAT_CODE                                     05350000
                    ,CKPT_FRQNCY_QTY                                    05360000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05370000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05380000
               FROM  TCKRSTCNTL                                         05390000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05400000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05410000
           END-EXEC.                                                    05420000
                                                                        05430000
           IF SQLCODE = 0                                               05440000
               CONTINUE                                                 05450000
           ELSE                                                         05460000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05470000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05480000
               PERFORM 9999-SQL-ABEND                                   05490000
           END-IF.                                                      05500000
      *                                                                 05510000
      *                                                                 05520000
      ******************************************************************05530000
      * 7500-SELECT-RESTART-INFO                                       *05540000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05550000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05560000
      ******************************************************************05570000
       7500-SELECT-RESTART-INFO.                                        05580000
                                                                        05590000
           EXEC SQL                                                     05600000
             SELECT  WORK_STRG_DESC                                     05610000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05620000
               FROM  TCKRSTINF                                          05630000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05640000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05650000
           END-EXEC.                                                    05660000
                                                                        05670000
           IF SQLCODE = 0                                               05680000
               CONTINUE                                                 05690000
           ELSE                                                         05700000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   05710000
               MOVE PE-MSG-08             TO PV-OPERATION-ATTEMPTED     05720000
               PERFORM 9999-SQL-ABEND                                   05730000
           END-IF.                                                      05740000
      *                                                                 05750000
      *                                                                 05760000
      ******************************************************************05770000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *05780000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *05790000
      ******************************************************************05800000
       7600-SET-CURRENT-TIMESTAMP.                                      05810000
                                                                        05820000
           EXEC SQL                                                     05830000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           05840000
           END-EXEC.                                                    05850000
                                                                        05860000
           IF SQLCODE = 0                                               05870000
               CONTINUE                                                 05880000
           ELSE                                                         05890000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 05900000
               MOVE PE-MSG-09             TO PV-OPERATION-ATTEMPTED     05910000
               PERFORM 9999-SQL-ABEND                                   05920000
           END-IF.                                                      05930000
      *                                                                 05940000
      *                                                                 05950000
      ******************************************************************05960000
      * 7700-COMMIT-PROCESSING.                                        *05970000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *05980000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*05990000
      ******************************************************************06000000
       7700-COMMIT-PROCESSING.                                          06010000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06020000
                                                                        06030000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06040000
      *                                                                 06050000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06060000
      *        PREPARE COMMIT RECORD FOR UPDATE                         06070000
               ADD 1                          TO PA-COMMIT-COUNT        06080000
               MOVE ML106-FSCL-MN-NBR         TO CR-FISCAL-NBR          06090000
               MOVE ML106-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   06100000
               MOVE ML106-DEPT-NBR            TO CR-DEPT-NBR            06110000
               MOVE PA-COMMIT-COUNT           TO CR-TMNDPT-COMMIT-COUNT 06120000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06130000
               MOVE PA-UPDATE-COUNT           TO CR-UPDATE-COUNT        06140000
               MOVE PA-INSERT-COUNT           TO CR-INSERT-COUNT        06150000
               MOVE PA-DEPTS-PROCESSED        TO CR-DEPTS-PROCESSED     06160000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06170000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06180000
                                             TCKRSTINF-WORK-STRG-DESC   06190000
               IF PS-FIRST-COMMIT                                       06200000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06210000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06220000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06230000
               END-IF                                                   06240000
               PERFORM 7800-COMMIT-CHANGES                              06250000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06260000
           END-IF.                                                      06270000
      *                                                                 06280000
      *                                                                 06290000
      ******************************************************************06300000
      * 7800-COMMIT-CHANGES.                                            06310000
      *   PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE. TAKE A COMMIT 06320000
      ******************************************************************06330000
       7800-COMMIT-CHANGES.                                             06340000
                                                                        06350000
           EXEC SQL                                                     06360000
             UPDATE TCKRSTINF                                           06370000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06380000
              WHERE JOB_NAME       = :PC-JOB-NAME                       06390000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06400000
           END-EXEC.                                                    06410000
                                                                        06420000
           IF SQLCODE = 0                                               06430000
               CONTINUE                                                 06440000
           ELSE                                                         06450000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06460000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06470000
               PERFORM 9999-SQL-ABEND                                   06480000
           END-IF.                                                      06490000
                                                                        06500000
           EXEC SQL                                                     06510000
               COMMIT                                                   06520000
           END-EXEC.                                                    06530000
                                                                        06540000
           IF SQLCODE = 0                                               06550000
               CONTINUE                                                 06560000
           ELSE                                                         06570000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED06580000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06590000
               PERFORM 9999-SQL-ABEND                                   06600000
           END-IF.                                                      06610000
      *                                                                 06620000
      *                                                                 06630000
      ******************************************************************06640000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   06650000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   06660000
      *                                                                 06670000
      ******************************************************************06680000
       7900-UPDATE-CHECKPOINT-STATUS.                                   06690000
                                                                        06700000
           EXEC SQL                                                     06710000
             UPDATE  TCKRSTCNTL                                         06720000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        06730000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06740000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06750000
           END-EXEC.                                                    06760000
                                                                        06770000
           IF SQLCODE = 0                                               06780000
               CONTINUE                                                 06790000
           ELSE                                                         06800000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME06810000
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED06820000
               PERFORM 9999-SQL-ABEND                                   06830000
           END-IF.                                                      06840000
                                                                        06850000
           EJECT                                                        06860000
      *                                                                 06870000
      *                                                                 06880000
      ******************************************************************06890000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TMNDPT ROW FOR UPDATE 06900000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  06910000
      ******************************************************************06920000
       7999-RESTART-PROCESS.                                            06930000
      *                                                                 06940000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           06950000
      *                                                                 06960000
           CLOSE CONDENSED-FILE.                                        06970000
      *                                                                 06980000
           PERFORM 7500-SELECT-RESTART-INFO.                            06990000
      *                                                                 07000000
           DISPLAY '**** RESTART **** '.                                07010000
           DISPLAY '** SQLCODE -911 ** ', PV-DEADLOCK-COUNT, 'TIMES'.   07020000
           DISPLAY 'TCKRSTINF-LAST CKPT ', TCKRSTINF-WORK-STRG-DESC.    07030000
      *                                                                 07040000
           MOVE TCKRSTINF-WORK-STRG-DESC TO CR-CHECKPOINT-RESTART-AREA. 07050000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 07060000
      *                                                                 07070000
           OPEN INPUT CONDENSED-FILE.                                   07080000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07090000
      *                                                                 07100000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07110000
           PERFORM 4000-READ-CONDENSED-FILE                             07120000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              07130000
      *                                                                 07140000
      *RE-ESTABLISH COUNTS FOR PROCESSED INPUT (FROM CHECKPOINT RECORD) 07150000
           MOVE CR-UPDATE-COUNT        TO PA-UPDATE-COUNT.              07160000
           MOVE CR-INSERT-COUNT        TO PA-INSERT-COUNT.              07170000
           MOVE CR-DEPTS-PROCESSED     TO PA-DEPTS-PROCESSED.           07180000
      *                                                                 07190000
      *                                                                 07200000
      ******************************************************************07210000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07220000
      ******************************************************************07230000
       8000-EOJ-ROUTINE.                                                07240000
      *                                                                 07250000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07260000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07270000
      *                                                                 07280000
           DISPLAY '                                             '.     07290000
           DISPLAY 'INPUT RECORDS   ', PA-INPUT-COUNT.                  07300000
           DISPLAY 'UPDATE COUNT    ', PA-UPDATE-COUNT.                 07310000
           DISPLAY 'INSERT COUNT    ', PA-INSERT-COUNT.                 07320000
           DISPLAY 'DEPTS PROCESSED ', PA-DEPTS-PROCESSED.              07330000
           DISPLAY '                                             '.     07340000
           DISPLAY 'COMMITS TAKEN   ', PA-COMMIT-COUNT.                 07350000
      *                                                                 07360000
           CLOSE CONDENSED-FILE.                                        07370000
      *                                                                 07380000
      *                                                                 07390000
      ******************************************************************07400000
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       07410000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   07420000
      ******************************************************************07430000
       9000-DEADLOCK-ERROR.                                             07440000
      *                                                                 07450000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     07460000
               PERFORM 9999-SQL-ABEND.                                  07470000
      *                                                                 07480000
      ******************************************************************07490000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               07500000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07510000
      ******************************************************************07520000
       9999-SQL-ABEND.                                                  07530000
                                                                        07540000
           MOVE +4013                 TO ABEND-CODE.                    07550000
           DISPLAY '**  ABEND     **'.                                  07560000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              07570000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            07580000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       07590000
                                                                        07600000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         07610000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        07620000
                                                                        07630000
           COPY DPPD004.                                                07640000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           07650000
