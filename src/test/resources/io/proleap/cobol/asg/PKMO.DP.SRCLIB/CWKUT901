      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT901.                                                
       AUTHOR.         CARL RICHTER.                                            
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   04/16/03.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM WILL READ DATES  FROM A CONTROL CARD AND CALL    *        
      *  THE CALENDAR ROUTINE TO RETURN FISCAL DATE INFORMATION.       *        
      *                                                                *        
      *  CONTROL CARD START AND END DATES: YYYY-MM-DD YYYY-MM-DD       *        
      *  START DATE MUST BE 1 DAY PRIOR TO THE ACTUAL DATE YOU WISH TO *        
      *     BEGIN WITH!!!                                              *        
      *  END DATE MUST BE THE ACTUAL DATE YOU WISH TO END WITH.        *        
      ******************************************************************        
      *    CHANGED: 01/03/04    BY: STACY BIESZK                                
      *                         INSTALLED INTO PRODUCTION.                      
      ******************************************************************        
PG0701*  CHANGED:  01/15/07         BY:  PATI GREEN                    *        
PG0701*  UPDATED FOR 53 WEEK YEAR CHANGE - SHIFT "LY-TDA-DTE" BACK 371 *        
PG0701*  DAYS INSTEAD OF 364 DAYS, FOR THE 52 WEEK YEAR FOLLOWING A 53 *        
PG0701*  WEEK YEAR.  ALSO CHANGED TO SET "LY-TDA-DTE" TO DEFAULT DATE *         
PG0701*  OF 9999-09-09 FOR WEEK 53, SO WEEK 53 COMPARES TO NO DATES.   *        
      ******************************************************************        
      *  CHANGED:  07/07/07         BY:  BETTY DORST                   *        
      *  ADDED 1 NEW COLUMN TO OUTPUT FILE - FSCL_WK_OF_MN_NBR         *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-CONTROL-CARD                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CW-DATE-COBOL-FILE                                            
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DATE-CONTROL-CARD                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DATE-CONTROL-RECORD         PIC X(80).                               
                                                                                
       FD  CW-DATE-COBOL-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-DATE-COBOL-RECORD         PIC X(282).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DATE-CONTROL-REC.                                                    
           05  DC-START-DATE                  PIC   X(10).                      
           05                                 PIC   X(01).                      
           05  DC-END-DATE                    PIC   X(10).                      
           05                                 PIC   X(59).                      
                                                                                
                                                                                
      *  TIME DIMENSION                                                         
           COPY CWRD901.                                                        
                                                                                
      *  CALENDAR INCLUDES                                                      
           COPY DPWS500.                                                        
                                                                                
       01  ABEND-CODE                    PIC S9(4)  VALUE ZEROS                 
                                         COMP SYNC.                             
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-DATE-CONTROL-CARD-SW                                   
                                         PIC X(1)   VALUE 'N'.                  
               88  PS-END-OF-DATE-CONTROL-CARD      VALUE 'Y'.                  
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(8)   VALUE 'CWKUT901'.           
           05  PC-OPERCO                 PIC X(2)   VALUE '03'.                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-WORK-DATE-NUM          PIC 9(6)   VALUE ZERO.                 
           05  PV-MONTH                  PIC X(2).                              
               88 PV-FIRST-QUARTER       VALUE '01' '02' '03'.                  
               88 PV-SECOND-QUARTER      VALUE '04' '05' '06'.                  
               88 PV-THIRD-QUARTER       VALUE '07' '08' '09'.                  
               88 PV-FOURTH-QUARTER      VALUE '10' '11' '12'.                  
           05  PV-WEEKDAY                PIC X(1).                              
               88 PV-WEEKDAY-NUM         VALUE '2' '3' '4' '5' '6'.             
           05  PV-WORK-DATE-BRK.                                                
               10  PV-WK-YY              PIC 9(2)   VALUE ZERO.                 
               10  PV-WK-MMDD.                                                  
                   15  PV-WK-MM          PIC 9(2)   VALUE ZERO.                 
                   15  PV-WK-DD          PIC 9(2)   VALUE ZERO.                 
           05  PV-WORK-DATE              REDEFINES PV-WORK-DATE-BRK             
                                         PIC X(6).                              
                                                                                
           05  PV-WORK-DATE-MDY-BRK.                                            
               10  PV-MDY-MM             PIC 9(2)   VALUE ZERO.                 
               10  PV-MDY-DD             PIC 9(2)   VALUE ZERO.                 
               10  PV-MDY-YY             PIC 9(2)   VALUE ZERO.                 
           05  PV-WORK-MDY-DATE          REDEFINES PV-WORK-DATE-MDY-BRK         
                                         PIC X(6).                              
PG0701     05  PV-53-WEEK-CCYY           PIC X(4).                              
PG0701         88  PV-VALID-53-WEEK-CCYY VALUES                                 
PG0701                                   '1995', '2000', '2006', '2012',        
PG0701                                   '2017', '2023', '2028', '2034'.        
PG0701     05  PV-53-52-WEEK-CCYY        PIC X(4).                              
PG0701         88  PV-VALID-53-52-WEEK-CCYY VALUES                              
PG0701                                   '1996', '2001', '2007', '2013',        
PG0701                                   '2018', '2024', '2029', '2035'.        
PG0701     05  PV-DEFAULT-DATE           PIC X(10)  VALUE '9999-09-09'.         
                                                                                
       01  PV-DATES.                                                            
           05  PV-PROCESS-DATE           PIC   X(10).                           
           05  PV-END-DATE               PIC   X(10).                           
           05  PV-TEMP-DATE              PIC   X(10).                           
           05  PV-PREV-YY                PIC   9(2).                            
           05  PV-LEAP-WHOLE             PIC   9(2).                            
           05  PV-LEAP-REMAINDER         PIC   9(2).                            
                                                                                
       01  PV-SUMMARY-COUNTS.                                                   
           05  PV-REC-READ               PIC S9(9)     VALUE ZERO.              
           05  PV-CW-DATE-LOAD-REC-OUT   PIC  9(9)     VALUE ZERO.              
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-DATES                                           
               UNTIL PV-PROCESS-DATE >= DC-END-DATE                             
                  OR PS-END-OF-DATE-CONTROL-CARD.                               
                                                                                
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  SET UP FOR PROGRAM PROCESSING BY OPENING ALL FILES, AND       *        
      *  READING THE FIRST DAILY SALES FILE RECORD.                    *        
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  DATE-CONTROL-CARD.                                       
           OPEN OUTPUT CW-DATE-COBOL-FILE.                                      
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           ACCEPT DPG51-SYSTEM-DATE FROM DATE.                                  
                                                                                
           PERFORM 7000-READ-DATE-CONTROL.                                      
           DISPLAY 'CONTROL CARD: '.                                            
           DISPLAY 'START DATE/END DATE'.                                       
           DISPLAY DATE-CONTROL-REC.                                            
                                                                                
           MOVE DC-START-DATE TO PV-PROCESS-DATE.                               
                                                                                
      ******************************************************************        
      *  PROCESS DATES.                                                *        
      ******************************************************************        
                                                                                
       2000-PROCESS-DATES.                                                      
                                                                                
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-INCREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT        TO TRUE.                             
           MOVE 1                TO DPG51-INCR-DECR-DAYS-9.                     
           MOVE PV-PROCESS-DATE  TO DPG52-LK-DATE-INPUT.                        
                                                                                
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
                                                                                
           PERFORM 4000-CREATE-CW-DATE-LOAD-REC.                                
                                                                                
      ******************************************************************        
      *  CREATE AND WRITE THE DW DATE RECORD.                          *        
      ******************************************************************        
                                                                                
       4000-CREATE-CW-DATE-LOAD-REC.                                            
                                                                                
           INITIALIZE CW901-TIME-RECORD.                                        
                                                                                
           MOVE DPG55-DB2-ISO-DATE-FMT    TO CW901-TM-DIM-KY-DTE.               
           MOVE DPG55-JULIAN-DATE-CCYYDDD TO CW901-JLN-DTE.                     
           MOVE DPG55-ALPHA-WEEK-DAY      TO CW901-DAY-OF-WK-NM.                
           MOVE DPG55-ALPHA-MONTH         TO CW901-CAL-MN-NM.                   
           MOVE DPG56-FISCAL-YEAR-CCYY    TO CW901-FSCL-WK-ID-CCYY              
                                             CW901-FSCL-QTR-ID-CCYY             
                                             CW901-SEA-ID-CCYY                  
                                             CW901-FSCL-YR-NBR                  
                                             CW901-FSCL-MN-ID-CCYY              
                                             CW901-FSCL-YR-DAY-CCYY.            
                                                                                
           MOVE DPG56-NBR-FISCAL-DAYS     TO CW901-FSCL-YR-DAY-DDD.             
           MOVE DPG55-DB2-ISO-DATE-FMT-DY TO CW901-CAL-DAY-OF-MN-NBR.           
           MOVE DPG55-DB2-ISO-DATE-FMT-YR TO CW901-CAL-YR-NBR.                  
           MOVE DPG56-FISCAL-WEEK-NBR     TO CW901-FSCL-WK-NBR                  
                                             CW901-FSCL-WK-ID-WK.               
           MOVE DPG56-FISCAL-PERIOD-NBR   TO CW901-FSCL-MN-NBR                  
                                             CW901-FSCL-MN-ID-MN.               
           MOVE DPG56-FISCAL-PERIOD-WEEK  TO CW901-FSCL-WK-OF-MN-NBR.           
           MOVE DPG56-FISCAL-QTR-NBR      TO CW901-FSCL-QTR-NBR                 
                                             CW901-FSCL-QTR-ID-QTR.             
           MOVE DPG56-CURR-FISCAL-SEASON  TO CW901-SEA-DESC.                    
                                                                                
           MOVE DPG55-DB2-ISO-DATE-FMT-MO TO PV-MONTH                           
                                             CW901-CAL-MN-NBR.                  
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT  TO CW901-BEG-WTD-DTE.                
           MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO CW901-BEG-FSCL-MTD-DTE.           
           MOVE DPG56-FIRST-FQ-DB2-ISO-FMT TO CW901-BEG-FSCL-QTD-DTE.           
           MOVE DPG56-FIRST-FS-DB2-ISO-FMT TO CW901-BEG-STD-DTE.                
           MOVE DPG56-FIRST-FD-DB2-ISO-FMT TO CW901-BEG-FSCL-YTD-DTE.           
                                                                                
           EVALUATE TRUE                                                        
              WHEN PV-FIRST-QUARTER                                             
                   MOVE '1' TO CW901-CAL-QTR-NBR                                
              WHEN PV-SECOND-QUARTER                                            
                   MOVE '2' TO CW901-CAL-QTR-NBR                                
              WHEN PV-THIRD-QUARTER                                             
                   MOVE '3' TO CW901-CAL-QTR-NBR                                
              WHEN PV-FOURTH-QUARTER                                            
                   MOVE '4' TO CW901-CAL-QTR-NBR                                
           END-EVALUATE.                                                        
           IF DPG55-DB2-ISO-DATE-FMT-DY = 1                                     
             MOVE 'Y' TO CW901-CAL-BOM-IND                                      
           ELSE                                                                 
             MOVE 'N' TO CW901-CAL-BOM-IND.                                     
           IF DPG55-DB2-ISO-DATE-FMT-DY = DPG55-DAYS-IN-MONTH                   
             MOVE 'Y' TO CW901-CAL-EOM-IND                                      
           ELSE                                                                 
             MOVE 'N' TO CW901-CAL-EOM-IND.                                     
           MOVE DPG55-NUMERIC-WEEK-DAY TO PV-WEEKDAY                            
                                          CW901-DAY-OF-WK-NBR.                  
           IF PV-WEEKDAY-NUM                                                    
              MOVE 'Y' TO CW901-WKDA-IND                                        
           ELSE                                                                 
              MOVE 'N' TO CW901-WKDA-IND.                                       
           IF DPG56-CURR-FISCAL-SEASON = 'SPRING'                               
               MOVE 'S'             TO CW901-SEA-S-F                            
           ELSE                                                                 
               MOVE 'F'             TO CW901-SEA-S-F.                           
                                                                                
      *  DETERMINE IF THIS DAY IS THE FIRST OR LAST DAY OF A SEASON             
           IF DPG55-DB2-ISO-DATE-FMT = DPG56-FIRST-FS-DB2-ISO-FMT               
               MOVE 'Y'         TO CW901-BOS-IND                                
           ELSE                                                                 
               MOVE 'N'         TO CW901-BOS-IND                                
           END-IF.                                                              
                                                                                
           IF DPG55-DB2-ISO-DATE-FMT = DPG56-LAST-FS-DB2-ISO-FMT                
               MOVE 'Y'         TO CW901-EOS-IND                                
           ELSE                                                                 
               MOVE 'N'         TO CW901-EOS-IND                                
           END-IF.                                                              
                                                                                
      *  DETERMINE IF THIS DAY IS THE FIRST OR LAST DAY OF A MONTH/WEEK         
           IF DPG55-DB2-ISO-DATE-FMT = DPG56-FIRST-FP-DB2-ISO-FMT               
               MOVE 'Y'         TO CW901-FSCL-BOM-IND                           
           ELSE                                                                 
               MOVE 'N'         TO CW901-FSCL-BOM-IND                           
           END-IF.                                                              
                                                                                
           IF DPG55-DB2-ISO-DATE-FMT = DPG56-LAST-FP-DB2-ISO-FMT                
               MOVE 'Y'         TO CW901-FSCL-EOM-IND                           
           ELSE                                                                 
               MOVE 'N'         TO CW901-FSCL-EOM-IND                           
           END-IF.                                                              
                                                                                
           IF DPG55-DB2-ISO-DATE-FMT = DPG55-1ST-WKDY-DB2ISO-FMT                
               MOVE 'Y'         TO CW901-BOW-IND                                
           ELSE                                                                 
               MOVE 'N'         TO CW901-BOW-IND                                
           END-IF.                                                              
                                                                                
           IF DPG55-DB2-ISO-DATE-FMT = DPG55-LAST-WKDY-DB2ISO-FMT               
               MOVE 'Y'         TO CW901-EOW-IND                                
           ELSE                                                                 
               MOVE 'N'         TO CW901-EOW-IND                                
           END-IF.                                                              
                                                                                
           MOVE DPG56-FISCAL-PERIOD-NAME  TO CW901-FSCL-MN-NM.                  
                                                                                
           MOVE DPG55-STORE-HOLIDAY-IND   TO CW901-HOL-IND.                     
                                                                                
      *  FILL HOLIDAY NAME IF THIS DATE IS A STORE HOLIDAY                      
           IF DPG55-STORE-HOLIDAY                                               
              MOVE DPG55-HOLIDAY-NAME TO CW901-HOL-NM                           
           END-IF.                                                              
                                                                                
      *  MOVE THIS WEEKS 1ST DAY OF WEEK (SUNDAY DATE)                          
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT                                       
                                TO CW901-BOW-DTE.                               
                                                                                
      *  SAVE THE CURRENT DATE BEING PROCESSED (FOR LOOPING)                    
           MOVE DPG55-DB2-ISO-DATE-FMT TO PV-PROCESS-DATE.                      
                                                                                
      *  DETERMINE LAST YEAR THIS DAY (THERE ARE 364 DAYS IN OUR                
PG0701*  FISCAL CALENDAR FOR NORMAL 52 WEEK YEARS. FOR A 52 WEEK YEAR           
PG0701*  THAT FOLLOWS A 53 WEEK YEAR, USE 371 DAYS BACK FOR LY DAY.)            
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
PG0701     MOVE CW901-FSCL-YR-NBR TO PV-53-52-WEEK-CCYY.                        
PG0701     IF PV-VALID-53-52-WEEK-CCYY                                          
PG0701         MOVE 371                    TO DPG51-INCR-DECR-DAYS-9            
PG0701     ELSE                                                                 
PG0701         MOVE 364                    TO DPG51-INCR-DECR-DAYS-9            
PG0701     END-IF.                                                              
PG0701*    MOVE 364                    TO DPG51-INCR-DECR-DAYS-9.               
           MOVE PV-PROCESS-DATE        TO DPG52-LK-DATE-INPUT.                  
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT TO CW901-LY-TDA-DTE.                     
PG0701                                                                          
PG0701*  IF THE CURRENT DATE FALLS IN WEEK 53, SET THE LY DAY TO                
PG0701*  DEFAULT DATE, SO WEEK 53 COMPARES TO NO REAL DATE IN LY.               
PG0701     IF CW901-FSCL-WK-NBR = '53'                                          
PG0701         MOVE PV-DEFAULT-DATE    TO CW901-LY-TDA-DTE                      
PG0701     END-IF.                                                              
                                                                                
      *  DETERMINE LAST WEEKS 1ST DAY (SUNDAY DATE)                             
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
           MOVE 7                      TO DPG51-INCR-DECR-DAYS-9.               
           MOVE CW901-BOW-DTE TO DPG52-LK-DATE-INPUT.                           
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT TO CW901-PWK-BOW-DTE.                    
                                                                                
      *  DETERMINE 2 WEEKS AGO 1ST DAY (SUNDAY DATE)                            
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT         TO TRUE.                            
           MOVE 14                          TO DPG51-INCR-DECR-DAYS-9.          
           MOVE CW901-BOW-DTE TO DPG52-LK-DATE-INPUT.                           
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT TO CW901-PWK2-BOW-DTE.                   
                                                                                
      *  DETERMINE 3 WEEKS AGO 1ST DAY (SUNDAY DATE)                            
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
           MOVE 21                     TO DPG51-INCR-DECR-DAYS-9.               
           MOVE CW901-BOW-DTE TO DPG52-LK-DATE-INPUT.                           
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT TO CW901-PWK3-BOW-DTE.                   
                                                                                
      *  DETERMINE 4 WEEKS AGO 1ST DAY (SUNDAY DATE)                            
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
           MOVE 28                     TO DPG51-INCR-DECR-DAYS-9.               
           MOVE CW901-BOW-DTE TO DPG52-LK-DATE-INPUT.                           
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT TO CW901-PWK4-BOW-DTE.                   
                                                                                
      *  DETERMINE NEXT WEEKS 1ST DAY (SUNDAY DATE)                             
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-INCREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
           MOVE 7                      TO DPG51-INCR-DECR-DAYS-9.               
           MOVE CW901-BOW-DTE TO DPG52-LK-DATE-INPUT.                           
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT TO CW901-NWK-BOW-DTE.                    
                                                                                
      *  DETERMINE PREVIOUS SEASONS LAST SUNDAY DATE                            
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
      *  INCLUDE LOGIC FOR 53 WEEK FISCAL YEAR.                                 
           IF CW901-FSCL-WK-ID-WK = '53'                                        
               MOVE 189 TO DPG51-INCR-DECR-DAYS-9                               
           ELSE                                                                 
               MOVE 182 TO DPG51-INCR-DECR-DAYS-9                               
           END-IF.                                                              
      *  REPLACE INPUT DATE WITH CURRENT DATE BEING PROCESSED                   
           MOVE PV-PROCESS-DATE TO DPG52-LK-DATE-INPUT.                         
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG56-LAST-FISCAL-SEASON-DAY TO PV-TEMP-DATE.                   
           MOVE PV-TEMP-DATE TO DPG52-LK-DATE-INPUT.                            
           MOVE 0 TO DPG51-INCR-DECR-DAYS-9.                                    
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO                                    
                                       CW901-LAST-SEA-LAST-BOW-DTE.             
                                                                                
      *  DETERMINE PREVIOUS YEARS LAST SUNDAY DATE.                             
      *  IF FISCAL SEASON IS SPRING , DECREMENT 182, ELSE 364                   
           IF CW901-SEA-S-F = 'S'                                               
               IF CW901-FSCL-WK-ID-WK = '53'                                    
                   MOVE 189 TO DPG51-INCR-DECR-DAYS-9                           
               ELSE                                                             
                   MOVE 182 TO DPG51-INCR-DECR-DAYS-9                           
               END-IF                                                           
           ELSE                                                                 
               IF CW901-FSCL-WK-ID-WK = '53'                                    
                   MOVE 371 TO DPG51-INCR-DECR-DAYS-9                           
               ELSE                                                             
                   MOVE 364 TO DPG51-INCR-DECR-DAYS-9                           
               END-IF                                                           
           END-IF.                                                              
           MOVE PV-PROCESS-DATE TO DPG52-LK-DATE-INPUT.                         
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG56-LAST-FISCAL-SEASON-DAY TO PV-TEMP-DATE.                   
           MOVE 0 TO DPG51-INCR-DECR-DAYS-9.                                    
           MOVE PV-TEMP-DATE TO DPG52-LK-DATE-INPUT.                            
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                                  
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO CW901-LY-LAST-BOW-DTE.             
                                                                                
      *  CALCULATE FISCAL MONTH ID FOR 1 YEAR PREVIOUS (ADDED FIELD             
      *    FOR ML PROJECT)                                                      
           MOVE ZEROES TO CW901-LY-FSCL-MN-ID.                                  
           COMPUTE CW901-LY-FSCL-MN-ID = CW901-FSCL-MN-ID                       
                   - 100.                                                       
                                                                                
      *  DETERMINE IF THIS DAY IS THE LAST SUNDAY DATE OF THE MONTH.            
           IF CW901-DAY-OF-WK-NM = 'SUNDAY'                                     
               SET DPG51-FISCAL-454-CALENDAR                                    
                   DPG51-INCREMENT-DATE                                         
                   DPG52-LK-DTE-ISO-FMT    TO TRUE                              
               MOVE 6 TO DPG51-INCR-DECR-DAYS-9                                 
               MOVE PV-PROCESS-DATE TO DPG52-LK-DATE-INPUT                      
               PERFORM 7200-CALL-CALENDAR-ROUTINE                               
               IF DPG55-DB2-ISO-DATE-FMT = DPG56-LAST-FP-DB2-ISO-FMT            
                   MOVE 'Y' TO CW901-FSCL-MN-LST-BOW-IND                        
               ELSE                                                             
                   MOVE 'N' TO CW901-FSCL-MN-LST-BOW-IND                        
               END-IF                                                           
           ELSE                                                                 
               MOVE 'N' TO CW901-FSCL-MN-LST-BOW-IND                            
           END-IF.                                                              
                                                                                
      *  UPDATE RECORD COUNT                                                    
           ADD +1                        TO PV-CW-DATE-LOAD-REC-OUT.            
           PERFORM 7150-WRITE-CW-DATE-COBOL-REC.                                
                                                                                
      ******************************************************************        
      *  READ THE DATE CONTROL CARD.                                   *        
      ******************************************************************        
                                                                                
       7000-READ-DATE-CONTROL.                                                  
                                                                                
           READ DATE-CONTROL-CARD                                               
               INTO DATE-CONTROL-REC                                            
               AT END                                                           
                   SET PS-END-OF-DATE-CONTROL-CARD TO TRUE                      
           END-READ.                                                            
                                                                                
           IF PS-END-OF-DATE-CONTROL-CARD                                       
               DISPLAY 'EMPTY DATE CONTROL CARD'                                
               MOVE +4008 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  MOVE MW003 TO DATE-LOAD-REC                                   *        
      ******************************************************************        
                                                                                
      ******************************************************************        
      *  WRITE A DATE LOAD FILE RECORD.                                *        
      ******************************************************************        
                                                                                
       7150-WRITE-CW-DATE-COBOL-REC.                                            
                                                                                
           WRITE CW-DATE-COBOL-RECORD FROM CW901-TIME-RECORD.                   
                                                                                
      ******************************************************************        
      *    CALL THE CALENDAR ROUTINE                                   *        
      ******************************************************************        
                                                                                
       7200-CALL-CALENDAR-ROUTINE.                                              
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE  USING DPG51 DPG52                 
                                                    DPG53 DPG54                 
                                                    DPG55 DPG56.                
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
               WHEN DPG54-DATE-INVALID                                          
                   DISPLAY '** ABEND - PROGRAM '                                
                           PC-PROGRAM-NAME ' **'                                
                   DISPLAY '** BAD CALL TO CALENDAR MODULE DPKUT500'            
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   MOVE +4019 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  CLOSE ALL FILES AND PRINT CONTROL TOTALS.                     *        
      ******************************************************************        
                                                                                
       8000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY 'DATE RECS WRITTEN: ' PV-CW-DATE-LOAD-REC-OUT.               
                                                                                
           CLOSE DATE-CONTROL-CARD                                              
                 CW-DATE-COBOL-FILE.                                            
                                                                                
      ******************************************************************        
      ******************************************************************        
