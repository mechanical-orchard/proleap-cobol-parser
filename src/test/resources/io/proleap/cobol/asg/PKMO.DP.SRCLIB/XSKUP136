       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.   XSKUP136.                                                  
       AUTHOR.       COGNIZANT.                                                 
       INSTALLATION. KOHLS DEPARTMENT STORES.                                   
       DATE-WRITTEN. MARCH 2013.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM UPDATES THE FLR_STFNG_CDE FROM 'EX' TO 'S7'       *        
      * FOR NEWLY OPENED STORES 70 DAYS IN ADVANCE IN THE              *        
      * XST_LOC_ATTR TABLE.                                            *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT XSTLOC-EXTR-FILE1                                             
           ASSIGN TO INP01.                                                     
                                                                                
           SELECT XSTLOC-EXTR-FILE2                                             
           ASSIGN TO INP02.                                                     
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD XSTLOC-EXTR-FILE1                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS XSTLOC-EXTR-REC1.                                     
       01 XSTLOC-EXTR-REC1               PIC X(80).                             
                                                                                
       FD XSTLOC-EXTR-FILE2                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS XSTLOC-EXTR-REC2.                                     
       01 XSTLOC-EXTR-REC2               PIC X(80).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *-----------------------------------------------------------------        
      * PROGRAM VARIABLES.                                                      
      *-----------------------------------------------------------------        
                                                                                
       01 XSTLOC-EXTR-FILE-WK1.                                                 
          05 FL-STORE-NBR1               PIC S9(04) COMP.                       
          05 FL-STFNG-CDE1               PIC X(02).                             
          05 FL-OPEN-DATE1               PIC X(10).                             
          05 FILLER                      PIC X(66).                             
                                                                                
       01 XSTLOC-EXTR-FILE-WK2.                                                 
          05 FL-STORE-NBR2               PIC S9(04) COMP.                       
          05 FL-STFNG-CDE2               PIC X(02).                             
          05 FL-OPEN-DATE2               PIC X(10).                             
          05 FILLER                      PIC X(66).                             
                                                                                
      *-----------------------------------------------------------------        
      * PROGRAM SWITCHES.                                                       
      *-----------------------------------------------------------------        
                                                                                
       01 PS-PROGRAM-SWITCHES.                                                  
          05  PS-EOF-XSTLOC-FILE1-SW         PIC X(01) VALUE 'N'.               
              88  PS-EOF-XSTLOC-EXTR-FILE1             VALUE 'Y'.               
              88  PS-NOT-EOF-XSTLOC-EXTR-FILE1         VALUE 'N'.               
                                                                                
          05  PS-EOF-XSTLOC-FILE2-SW         PIC X(01) VALUE 'N'.               
              88  PS-EOF-XSTLOC-EXTR-FILE2             VALUE 'Y'.               
              88  PS-NOT-EOF-XSTLOC-EXTR-FILE2         VALUE 'N'.               
      *-----------------------------------------------------------------        
      * PROGRAM CONSTANTS.                                                      
      *-----------------------------------------------------------------        
                                                                                
       01 PC-PROGRAM-CONSTANT.                                                  
          05  PC-PGM-NAME                PIC X(09)  VALUE 'XSKUP136'.           
          05  PC-BEGIN-PGM-MSG           PIC X(20)  VALUE                       
                                             'BEGIN RUN OF PROGRAM'.            
          05  PC-END-PGM-MSG             PIC X(18)  VALUE                       
                                             'END RUN OF PROGRAM'.              
      *-----------------------------------------------------------------        
      * PROGRAM COUNTERS.                                                       
      *-----------------------------------------------------------------        
                                                                                
       01 PC-PROGRAM-COUNTER.                                                   
          05 PC-UPDATED-CNT              PIC 9(04)  VALUE 0.                    
          05 PC-INPUT-CNT                PIC 9(04)  VALUE 0.                    
          05 PC-COMMIT-CNT               PIC 9(04)  VALUE 0.                    
                                                                                
      *-----------------------------------------------------------------        
      * ABEND VARIABLES.                                                        
      *-----------------------------------------------------------------        
                                                                                
       01  ABEND-CODE                    PIC S9(04) VALUE +0                    
                                                           COMP SYNC.           
       01 PV-PROGRAM-VARIABLE.                                                  
          05  PV-PARAGRAPH-NAME          PIC X(50).                             
          05  PV-MESSAGE                 PIC X(50).                             
          05  PV-CNVRTED-SQLCODE         PIC Z9999-.                            
                                                                                
      *-----------------------------------------------------------------        
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                 
      *-----------------------------------------------------------------        
           COPY DPWS004.                                                        
                                                                                
      *-----------------------------------------------------------------        
      * STANDARD LAYOUT FOR SQL COMMUNICATION AREA                              
      *-----------------------------------------------------------------        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *-----------------------------------------------------------------        
      * SQL AND COBOL DECLARATIONS FOR XST_LOC_ATTR TABLE                       
      *-----------------------------------------------------------------        
           EXEC SQL                                                             
               INCLUDE XST00025                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
      *-----------------------------------------------------------------        
      * 0000-MAINLINE.                                                          
      *-----------------------------------------------------------------        
       0000-MAINLINE.                                                           
                                                                                
           DISPLAY PC-BEGIN-PGM-MSG.                                            
                                                                                
           PERFORM 1000-INITIALIZE-PARA.                                        
                                                                                
           PERFORM 2000-PROCESS-RECORD                                          
             UNTIL PS-EOF-XSTLOC-EXTR-FILE1.                                    
                                                                                
           PERFORM 3000-CLOSE-FILE.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
      *-----------------------------------------------------------------        
      * 1000-INITIALIZE-PARA.                                                   
      *-----------------------------------------------------------------        
       1000-INITIALIZE-PARA.                                                    
           MOVE '1000-INITIALIZE-PARA' TO PV-PARAGRAPH-NAME                     
                                                                                
           INITIALIZE PC-UPDATED-CNT                                            
                      PC-INPUT-CNT.                                             
                                                                                
           SET PS-NOT-EOF-XSTLOC-EXTR-FILE1 TO TRUE.                            
           SET PS-NOT-EOF-XSTLOC-EXTR-FILE2 TO TRUE.                            
                                                                                
           OPEN INPUT XSTLOC-EXTR-FILE1.                                        
                                                                                
           PERFORM 1100-PRINT-DETAILS.                                          
                                                                                
           PERFORM 1200-READ-SECTION.                                           
                                                                                
      *-----------------------------------------------------------------        
      * 1100-PRINT-DETAILS.                                                     
      *-----------------------------------------------------------------        
       1100-PRINT-DETAILS.                                                      
           MOVE '1100-PRINT-DETAILS' TO PV-PARAGRAPH-NAME                       
                                                                                
           OPEN INPUT XSTLOC-EXTR-FILE2.                                        
                                                                                
           DISPLAY '************************************'.                      
           DISPLAY '*STR_NUM  FLR_STFNG_CDE  OPN_DTE   *'.                      
           DISPLAY '************************************'.                      
                                                                                
           PERFORM UNTIL PS-EOF-XSTLOC-EXTR-FILE2                               
             READ XSTLOC-EXTR-FILE2 INTO XSTLOC-EXTR-FILE-WK2                   
               AT END                                                           
                 SET PS-EOF-XSTLOC-EXTR-FILE2 TO TRUE                           
               NOT AT END                                                       
               DISPLAY '*' FL-STORE-NBR2 '     ' FL-STFNG-CDE2 '                
      -                '     ' FL-OPEN-DATE2 '*'                                
             END-READ                                                           
           END-PERFORM.                                                         
                                                                                
           DISPLAY '************************************'.                      
                                                                                
           CLOSE XSTLOC-EXTR-FILE2.                                             
                                                                                
      *-----------------------------------------------------------------        
      * 1200-READ-SECTION.                                                      
      *-----------------------------------------------------------------        
       1200-READ-SECTION.                                                       
           MOVE '1200-READ-SECTION'       TO PV-PARAGRAPH-NAME.                 
                                                                                
           READ XSTLOC-EXTR-FILE1 INTO XSTLOC-EXTR-FILE-WK1                     
             AT END                                                             
             SET PS-EOF-XSTLOC-EXTR-FILE1 TO TRUE                               
           END-READ.                                                            
                                                                                
           IF PS-NOT-EOF-XSTLOC-EXTR-FILE1                                      
             ADD +1                       TO   PC-INPUT-CNT                     
           END-IF.                                                              
                                                                                
      *-----------------------------------------------------------------        
      * 2000-PROCESS-RECORD.                                                    
      *-----------------------------------------------------------------        
       2000-PROCESS-RECORD.                                                     
           MOVE '2000-PROCESS-RECORD' TO PV-PARAGRAPH-NAME                      
                                                                                
           PERFORM 2100-UPDATE-PARA.                                            
           PERFORM 1200-READ-SECTION.                                           
                                                                                
      *-----------------------------------------------------------------        
      * 2100-UPDATE-PARA.                                                       
      *-----------------------------------------------------------------        
       2100-UPDATE-PARA.                                                        
           MOVE '2100-UPDATE-PARA' TO PV-PARAGRAPH-NAME                         
           DISPLAY 'UPDATING LOCATION NUMBER: ' FL-STORE-NBR1.                  
                                                                                
           MOVE  FL-STORE-NBR1     TO XST00025-LOC-NBR                          
                                                                                
           EXEC SQL                                                             
                UPDATE XST_LOC_ATTR                                             
                SET    FLR_STFNG_CDE = 'S7'                                     
                WHERE  FLR_STFNG_CDE = 'EX'                                     
                   AND LOC_NBR = :XST00025-LOC-NBR                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
               ADD +1 TO PC-UPDATED-CNT                                         
             WHEN SQLWARN0 NOT = SPACES                                         
             WHEN SQLCODE  NOT = ZERO                                           
               MOVE SQLCODE TO PV-CNVRTED-SQLCODE                               
               MOVE '2100-UPDATE-PARA'     TO PV-PARAGRAPH-NAME                 
               MOVE 'UNSUCCESSFUL UPDATE ' TO PV-MESSAGE                        
               PERFORM 9999-DB2-ERROR-PARA                                      
            END-EVALUATE.                                                       
                                                                                
           EXEC SQL                                                             
               COMMIT WORK                                                      
           END-EXEC.                                                            
           DISPLAY 'COMMIT COMPLETED'.                                          
           ADD +1 TO PC-COMMIT-CNT.                                             
                                                                                
      *-----------------------------------------------------------------        
      * 3000-CLOSE-FILE.                                                        
      *-----------------------------------------------------------------        
       3000-CLOSE-FILE.                                                         
           MOVE '3000-CLOSE-FILE' TO PV-PARAGRAPH-NAME                          
                                                                                
           CLOSE XSTLOC-EXTR-FILE1.                                             
                                                                                
           DISPLAY PC-END-PGM-MSG.                                              
           DISPLAY ' '.                                                         
                                                                                
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
      *-----------------------------------------------------------------        
      * DISPLAYING INFO AFTER RUN                                               
      *-----------------------------------------------------------------        
       9990-DISPLAY-CONTROL-INFO.                                               
                                                                                
           DISPLAY '*****************************************'.                 
           DISPLAY '** RUN STATISTICS FOR PROGRAM XSKUP136 **'.                 
           DISPLAY '**                                     **'.                 
           DISPLAY '   TOTAL NO. OF RECORDS READ   : ' PC-INPUT-CNT.            
           DISPLAY '   TOTAL NO. OF RECORDS UPDATED: ' PC-UPDATED-CNT.          
           DISPLAY '   TOTAL NO. OF COMMITS TAKEN  : ' PC-COMMIT-CNT.           
           DISPLAY '**                                     **'.                 
           DISPLAY '** RUN STATS END FOR PROGRAM XSKUP136  **'.                 
           DISPLAY '*****************************************'.                 
                                                                                
      *-----------------------------------------------------------------        
      *  PERFORM ERROR HANDLING                                                 
      *-----------------------------------------------------------------        
       9999-DB2-ERROR-PARA.                                                     
           DISPLAY '*** PROGRAM   = '     PC-PGM-NAME                           
           DISPLAY '*** PARAGRAPH = '     PV-PARAGRAPH-NAME                     
           DISPLAY '*** MESSAGE   = '     PV-MESSAGE                            
           DISPLAY '*** DB2 ERROR '                                             
           DISPLAY '*** SQL CODE = '      PV-CNVRTED-SQLCODE                    
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013                    TO ABEND-CODE                          
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
