      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    MXKUP005.                                                 
       AUTHOR.        TOM PRIMEAU TEKSYSTEMS.                                   
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN   03/06/12.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * CHANGE LOG                                                              
      ******************************************************************        
      * 03/27/2012  T. PRIMEAU INITIAL IMPLEMENTATION  WR/IR #: WR37507         
      ******************************************************************        
      *   APPLY SKU PARM CHANGES FROM MPX MR ITEM AND TRAN TABLES.              
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT MPX-INPUT-FILE                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT COMMIT-CONTROL-FILE                                           
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT DATE-CONTROL-FILE                                             
               ASSIGN TO INP03.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  MPX-INPUT-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 97 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  MPX-INPUT-RECORD         PIC X(97).                                  
                                                                                
       FD  COMMIT-CONTROL-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  COMMIT-CONTROL-RECORD    PIC X(80).                                  
                                                                                
       FD  DATE-CONTROL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  DATE-CONTROL-RECORD    PIC X(80).                                    
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE F                                                     
           RECORD CONTAINS 97 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS OUTPUT-ERROR-RECORD.                                  
                                                                                
       01  OUTPUT-ERROR-RECORD         PIC X(97).                               
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE                  PIC S9(4) COMP SYNC VALUE ZERO.          
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
           88  ABEND-4002-READ-ERROR                       VALUE +4002.         
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
           88  ABEND-4008-DATA-ERROR                       VALUE +4008.         
           88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009.         
           88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME             PIC  X(8) VALUE 'MXKUP005'.              
           05  PC-MAX-RETRIES          PIC S9(3) VALUE 3   COMP-3.              
           05  PC-MAX-BEFORE-COMMIT    PIC S9(04) COMP SYNC                     
                                                  VALUE +0.                     
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-SWITCH           PIC X     VALUE 'N'.                     
               88  PS-NOT-EOF                    VALUE 'N'.                     
               88  PS-EOF                        VALUE 'Y'.                     
           05  PS-FND-SWITCH           PIC X     VALUE 'N'.                     
               88  PS-FOUND                      VALUE 'N'.                     
               88  PS-NOT-FOUND                  VALUE 'Y'.                     
           05  PS-PS-END-OF-DATE-SW    PIC X(1)  VALUE 'N'.                     
               88  PS-END-OF-DATE-FILE           VALUE 'Y'.                     
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DB2-DATE-TIMESTAMP.                                           
               10  PV-DB2-CCYY-MM-DD PIC  X(10)   VALUE SPACE.                  
               10  PV-TIME-STAMP     PIC  X(16)                                 
                   VALUE '-19.00.00.000000'.                                    
           05  PV-COMMIT-CNT           PIC S9(04) COMP SYNC                     
                                                  VALUE 0.                      
           05  PV-ONE                  PIC  X(1)  VALUE SPACES.                 
           05  PV-INPUT-CNT            PIC  9(9)  VALUE 0.                      
           05  PV-UPDATE-CNT           PIC  9(9)  VALUE 0.                      
           05  PV-ERROR-REC-CNT        PIC  9(9)  VALUE 0.                      
           05  PV-TMRITEM-UPDATE-CNT   PIC  9(9)  VALUE 0.                      
           05  PV-TMRTRN-INSERT-CNT    PIC  9(9)  VALUE 0.                      
           05  PV-TMRTRN-MAX-CNT       PIC  9(9)  VALUE 0.                      
           05  PV-TMRSKTR-INSERT-CNT   PIC  9(9)  VALUE 0.                      
                                                                                
           05  PV-PREV-DEPT-NBR        PIC  X(03)  VALUE SPACES.                
           05  PV-PREV-PARM-CDE        PIC  X(10)  VALUE SPACES.                
           05  PV-SAVE-TRN-ID          PIC  9(10)  VALUE 0.                     
           05  PV-EFF-STRT-DTE         PIC  X(10)  VALUE SPACES.                
           05  PV-PREV-ITM-NBR         PIC  X(15)  VALUE SPACES.                
           05  PV-SKU-NBR              PIC  S9(08) COMP-3 VALUE 0.              
           05  PV-TRN-ID-NULL          PIC  S9(4) COMP VALUE 0.                 
           05  PV-RNK-ID-NULL          PIC  S9(4) COMP VALUE 0.                 
           05  PV-ORIG-TRN-ID          PIC  S9(4) COMP VALUE 0.                 
                                                                                
           05  PV-PARAGRAPH-NAME       PIC  X(30) VALUE SPACES.                 
           05  PV-DB2-OPER-ATTEMPTED   PIC  X(30) VALUE SPACES.                 
                                                                                
           05  PV-DELAY-INTERVAL.                                               
               10  FILLER                 PIC X(02)  VALUE SPACES.              
               10  PV-DELAY-TIME          PIC 9(08)  VALUE 6000.                
           05  WS-SYSWAIT                 PIC X(08)  VALUE 'SYSWAIT'.           
                                                                                
           05  PV-COMMIT-CONTROL-RECORD.                                        
               10  PV-COMMIT-CONTROL-CNT  PIC 9(09).                            
               10  FILLER                 PIC X(71)  VALUE SPACES.              
                                                                                
      ******************************************************************        
      * RECORD LAYOUT FOR INPUT DATE CARD                                       
      ******************************************************************        
       01  CC-CONTROL-CARD.                                                     
           05  CC-PROCDT-CCYY-MM-DD.                                            
               10 CC-PROCESS-DT-CCYY       PIC X(04)  VALUE SPACES.             
               10 FILLER                   PIC X(01)  VALUE SPACES.             
               10 CC-PROCESS-DT-MM         PIC X(02)  VALUE SPACES.             
               10 FILLER                   PIC X(01)  VALUE SPACES.             
               10 CC-PROCESS-DT-DD         PIC X(02)  VALUE SPACES.             
           05  FILLER                      PIC X(70)  VALUE SPACES.             
                                                                                
      ******************************************************************        
      *   INPUT MPX FILE LAYOUT:  MX006-SKU-TRAN-RECORD.                        
      ******************************************************************        
           COPY MXRD006.                                                        
                                                                                
      ******************************************************************        
      *   ERROR MESSAGE AREA FOR DB2                                            
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
       EJECT                                                                    
      ************************                                                  
      * DB2 TABLES                                                              
      ************************                                                  
                                                                                
      *****************************************************************         
      *  TMRTRN   - MR TRAN TABLE                                               
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TMRTRN                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TMRSKTR  - MR SKU TRAN TABLE                                           
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TMRSKTR                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TMRITEM - MR ITEM TABLE                                                
      *****************************************************************         
           EXEC SQL                                                        CL**2
               INCLUDE TMRITEM                                             CL**2
           END-EXEC.                                                       CL**2
                                                                                
      ************************                                                  
      * DB2 COMMUNICATION AREA                                                  
      ************************                                                  
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM 1000-PRE-PROCESSING.                                         
                                                                                
           PERFORM 2000-PROCESS-INFILE                                          
                   UNTIL PS-EOF.                                                
                                                                                
           PERFORM 9000-POST-PROCESSING.                                        
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *   OPEN FILES                                                            
      *   READ FIRST INPUT RECORD                                               
      ******************************************************************        
       1000-PRE-PROCESSING.                                                     
                                                                                
           OPEN INPUT  MPX-INPUT-FILE                                           
                       COMMIT-CONTROL-FILE                                      
                       DATE-CONTROL-FILE                                        
                OUTPUT OUTPUT-FILE.                                             
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '** PROGRAM: ' PC-PGM-NAME.                                  
           DISPLAY SPACES.                                                      
                                                                                
           PERFORM 7000-READ-COMMIT-CONTROL-FILE.                               
                                                                                
           PERFORM 7010-READ-DATE-CONTROL-FILE.                                 
                                                                                
           PERFORM 7050-READ-MPX-INPUT-FILE.                                    
                                                                                
           PERFORM 1050-FIND-1ST-UNPROCESSED-REC                                
               UNTIL PS-NOT-FOUND OR PS-EOF.                                    
      ******************************************************************        
      *   CHECK FOR EXISTANCE OF A RECORD                                       
      *                                                                         
      ******************************************************************        
       1050-FIND-1ST-UNPROCESSED-REC.                                           
           MOVE '1050-FIND-1ST-UNPROCESSED-REC'  TO PV-PARAGRAPH-NAME.          
           MOVE CC-PROCDT-CCYY-MM-DD TO PV-EFF-STRT-DTE.                        
           MOVE MX006-SKU-NBR TO PV-SKU-NBR.                                    
                                                                                
           EXEC SQL                                                             
              SELECT A.DEPT_NBR                                                 
                   , A.TRN_ID                                                   
                   , A.PARM_CDE                                                 
                   , A.EFF_STRT_DTE                                             
                   , A.LAST_UPD_TMST                                            
                   , A.APLD_IND                                                 
                   , B.DEPT_NBR                                                 
                   , B.TRN_ID                                                   
                   , B.SKU_NBR                                                  
              INTO        :MRTRN-DEPT-NBR                                       
                   , :MRTRN-TRN-ID                                              
                   , :MRTRN-PARM-CDE                                            
                   , :MRTRN-EFF-STRT-DTE                                        
                   , :MRTRN-LAST-UPD-TMST                                       
                   , :MRTRN-APLD-IND                                            
                   , :MRSKTR-DEPT-NBR                                           
                   , :MRSKTR-TRN-ID                                             
                   , :MRSKTR-SKU-NBR                                            
              FROM       TMRTRN A                                               
                   ,TMRSKTR B                                                   
              WHERE A.DEPT_NBR = B.DEPT_NBR                                     
              AND A.DEPT_NBR = :MX006-DEPT-NBR                                  
              AND A.TRN_ID = B.TRN_ID                                           
              AND B.SKU_NBR = :PV-SKU-NBR                                       
              AND A.PARM_CDE = :MX006-PARM-CDE                                  
              AND A.EFF_STRT_DTE = :PV-EFF-STRT-DTE                             
              AND A.LAST_UPD_TMST = :MX006-SOR-CRE-TMST                         
              AND A.APLD_IND = 'N'                                              
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
              WHEN SQLCODE = ZERO                                               
                  PERFORM 7050-READ-MPX-INPUT-FILE                              
                                                                                
              WHEN SQLCODE = +100                                               
                  SET PS-NOT-FOUND TO TRUE                                      
                                                                                
                  DISPLAY '1050 FIND 1ST UNPROCESSED REC'                       
                                                                                
              WHEN OTHER                                                        
                  MOVE 'SELECT TRAN ID'                                         
                                   TO PV-DB2-OPER-ATTEMPTED                     
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      ******************************************************************        
      *   COMMIT CHANGES AFTER SPECIFIED NUMBER OF UPDATES                      
      *   READ THE NEXT INPUT RECORD                                            
      ******************************************************************        
       2000-PROCESS-INFILE.                                                     
           PERFORM 2100-PROCESS-INPUT-MPXREC.                                   
                                                                                
           IF  PV-COMMIT-CNT = PC-MAX-BEFORE-COMMIT                             
               PERFORM 7600-COMMIT-UPDATE                                       
           END-IF.                                                              
                                                                                
           PERFORM 7050-READ-MPX-INPUT-FILE.                                    
                                                                                
      ******************************************************************        
      *   PROCESS THE INPUT RECORD FOR SKU PARM CODE CHANGE                     
      ******************************************************************        
       2100-PROCESS-INPUT-MPXREC.                                               
           IF MX006-DEPT-NBR NOT = PV-PREV-DEPT-NBR                             
               MOVE SPACES TO PV-PREV-PARM-CDE                                  
               PERFORM 2200-SELECT-TRAN-ID                                      
           END-IF.                                                              
                                                                                
           IF MX006-PARM-CDE NOT = PV-PREV-PARM-CDE                             
               ADD 1 TO PV-SAVE-TRN-ID                                          
               PERFORM 4000-FORMAT-INSERT-TMRTRN                                
               PERFORM 6000-INSERT-TMRTRN-RECORD                                
           ELSE                                                                 
               PERFORM 6010-INSERT-TMRSKTR-RECORD                               
           END-IF.                                                              
                                                                                
      ****************************************************                      
      *   ESTABLISH A START ROW.                         *                      
      ****************************************************                      
       2200-SELECT-TRAN-ID.                                                     
           MOVE '2200-SELECT-TRAN-ID'  TO PV-PARAGRAPH-NAME.                    
           MOVE MX006-DEPT-NBR TO MRTRN-DEPT-NBR.                               
           EXEC SQL                                                             
             SELECT MAX(TRN_ID) INTO :MRTRN-TRN-ID :PV-TRN-ID-NULL              
             FROM TMRTRN                                                        
             WHERE DEPT_NBR = :MRTRN-DEPT-NBR                                   
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
              WHEN SQLCODE = ZERO                                               
                  MOVE MRTRN-TRN-ID TO PV-SAVE-TRN-ID                           
                                                                                
              WHEN SQLCODE = +100                                               
                  DISPLAY 'ESTABLISH TRAN ID FOR 1ST TIME DEPT'                 
                  MOVE 0 TO PV-SAVE-TRN-ID                                      
                                                                                
              WHEN OTHER                                                        
                  MOVE 'SELECT TRAN ID'                                         
                                   TO PV-DB2-OPER-ATTEMPTED                     
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 4000-FORMAT-INSERT-TMRTRN.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - FORMATS A ROW INTO TMRTRN TO APPLY DAILY SKU'S            *        
      ******************************************************************        
       4000-FORMAT-INSERT-TMRTRN.                                               
           MOVE MX006-DEPT-NBR        TO MRTRN-DEPT-NBR.                        
           MOVE MX006-PARM-CDE        TO MRTRN-PARM-CDE.                        
           MOVE MX006-SOR-CRE-TMST    TO MRTRN-LAST-UPD-TMST.                   
           MOVE MX006-SOR-CRE-BY-USR-ID TO                                      
                               MRTRN-LAST-UPD-BY-USR-ID.                        
           MOVE CC-PROCDT-CCYY-MM-DD  TO MRTRN-EFF-STRT-DTE.                    
           MOVE 'N'                   TO MRTRN-RVRT-VAL-OBTND-CDE.              
           IF MX006-PARM-CDE = 'RTDAY' OR 'RT'                                  
              MOVE 'Y'      TO MRTRN-APLD-IND                                   
           ELSE                                                                 
              MOVE 'N'      TO MRTRN-APLD-IND                                   
           END-IF.                                                              
      ******************************************************************        
      *   PERFORM INSERT TMRTRN RECORD. KEY= DEPT-NBR AND TRN-ID.               
      ******************************************************************        
       6000-INSERT-TMRTRN-RECORD.                                               
           MOVE '6000-INSERT-TMRTRN-RECORD' TO PV-PARAGRAPH-NAME.               
                                                                                
           MOVE PV-SAVE-TRN-ID TO MRTRN-TRN-ID.                                 
                                                                                
           EXEC SQL                                                             
               INSERT INTO TMRTRN                                               
                 ( DEPT_NBR                                                     
                  ,TRN_ID                                                       
                  ,PARM_CDE                                                     
                  ,EFF_STRT_DTE                                                 
                  ,LAST_UPD_TMST                                                
                  ,LAST_UPD_BY_USR_ID                                           
                  ,RNK_ID                                                       
                  ,RVRT_VAL_OBTND_CDE                                           
                  ,ORIG_TRN_ID                                                  
                  ,APLD_IND )                                                   
          VALUES (:MRTRN-DEPT-NBR,                                              
                  :MRTRN-TRN-ID,                                                
                  :MRTRN-PARM-CDE,                                              
                  :MRTRN-EFF-STRT-DTE,                                          
                  :MRTRN-LAST-UPD-TMST,                                         
                  :MRTRN-LAST-UPD-BY-USR-ID,                                    
                  :MRTRN-RNK-ID,                                                
                  :MRTRN-RVRT-VAL-OBTND-CDE,                                    
                  :MRTRN-ORIG-TRN-ID,                                           
                  :MRTRN-APLD-IND )                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD +1      TO PV-TMRTRN-INSERT-CNT                          
                   MOVE MRTRN-DEPT-NBR TO PV-PREV-DEPT-NBR                      
                   MOVE MRTRN-PARM-CDE TO PV-PREV-PARM-CDE                      
                   PERFORM 6010-INSERT-TMRSKTR-RECORD                           
                   MOVE 0 TO SQLCODE                                            
                                                                                
               WHEN OTHER                                                       
                   MOVE 'INSERT TMRTRN TABLE'                                   
                                    TO PV-DB2-OPER-ATTEMPTED                    
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *   FORMAT AND INSERT TMRSKTR RECORD.                                     
      ******************************************************************        
       6010-INSERT-TMRSKTR-RECORD.                                              
           MOVE '6010-INSERT-TMRSKTR-RECORD' TO PV-PARAGRAPH-NAME.              
                                                                                
           MOVE MX006-DEPT-NBR        TO MRSKTR-DEPT-NBR.                       
           MOVE PV-SAVE-TRN-ID         TO MRSKTR-TRN-ID.                        
           MOVE MX006-SKU-NBR         TO MRSKTR-SKU-NBR.                        
           MOVE MX006-MAJ-CL-NBR      TO MRSKTR-MAJ-CL-NBR.                     
           MOVE MX006-SUB-CL-NBR      TO MRSKTR-SUB-CL-NBR.                     
           MOVE MX006-ITM-NBR         TO MRSKTR-ITM-NBR                         
                                         PV-PREV-ITM-NBR.                       
           MOVE MX006-PARM-VAL-TXT    TO MRSKTR-PARM-VAL-TXT.                   
                                                                                
           EXEC SQL                                                             
               INSERT INTO TMRSKTR                                              
                 ( DEPT_NBR                                                     
                  ,TRN_ID                                                       
                  ,SKU_NBR                                                      
                  ,MAJ_CL_NBR                                                   
                  ,SUB_CL_NBR                                                   
                  ,ITM_NBR                                                      
                  ,PARM_VAL_TXT )                                               
          VALUES (:MRSKTR-DEPT-NBR,                                             
                  :MRSKTR-TRN-ID,                                               
                  :MRSKTR-SKU-NBR,                                              
                  :MRSKTR-MAJ-CL-NBR,                                           
                  :MRSKTR-SUB-CL-NBR,                                           
                  :MRSKTR-ITM-NBR,                                              
                  :MRSKTR-PARM-VAL-TXT )                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD +1      TO PV-TMRSKTR-INSERT-CNT                         
                   ADD +1      TO PV-COMMIT-CNT                                 
                   PERFORM 6020-UPDATE-TMRITEM                                  
                                                                                
               WHEN SQLCODE = -803                                              
                   DISPLAY 'DUP INSERT - TMRSKTR ' MX006-ITM-NBR                
                   MOVE 0 TO SQLCODE                                            
                                                                                
               WHEN OTHER                                                       
                   MOVE 'INSERT TMRSKTR TABLE'                                  
                                    TO PV-DB2-OPER-ATTEMPTED                    
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PERFORMS FORMAT FOR UPDATE OF TMRITEM TABLE     *        
      ******************************************************************        
       6020-UPDATE-TMRITEM.                                                     
           MOVE '6020-UPDATE-TMRITEM'  TO PV-PARAGRAPH-NAME.                    
           MOVE 'UPDATE TMRITEM'       TO PV-DB2-OPER-ATTEMPTED.                
                                                                                
           IF MX006-PARM-CDE = 'RTDAY'                                          
              MOVE MRSKTR-PARM-VAL-TXT TO MRITEM-RT-DAY-NBR                     
              MOVE MX006-ITM-NBR       TO MRITEM-ITEM-NBR                       
              PERFORM 6030-UPDATE-RTDAY                                         
           ELSE                                                                 
              IF MX006-PARM-CDE = 'RT'                                          
                 MOVE MRSKTR-PARM-VAL-TXT TO MRITEM-RT-NBR                      
                 MOVE MX006-ITM-NBR       TO MRITEM-ITEM-NBR                    
                 PERFORM 6040-UPDATE-RT-NBR.                                    
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PERFORMS THE UPDATE FOR RTDAY                   *        
      ******************************************************************        
       6030-UPDATE-RTDAY.                                                       
           MOVE '6030-UPDATE-RTDAY'  TO PV-PARAGRAPH-NAME.                      
           EXEC SQL                                                             
               UPDATE TMRITEM                                                   
                  SET     RT_DAY_NBR = :MRITEM-RT-DAY-NBR                       
               WHERE ITEM_NBR        = :MRITEM-ITEM-NBR                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PV-UPDATE-CNT                                       
                                                                                
               WHEN SQLCODE = +100                                              
                   DISPLAY 'TMRITEM ROW NOT UPDATED, ROW NOT FOUND'             
                           ' MX006 ITEM NBR = ' MX006-ITM-NBR                   
                   PERFORM 8000-WRITE-ERROR-RECORD                              
                                                                                
               WHEN OTHER                                                       
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PERFORMS THE UPDATE FOR RT NBR                  *        
      ******************************************************************        
       6040-UPDATE-RT-NBR.                                                      
           MOVE '6040-UPDATE-RT-NBR'  TO PV-PARAGRAPH-NAME.                     
           EXEC SQL                                                             
               UPDATE TMRITEM                                                   
                  SET     RT_NBR    = :MRITEM-RT-NBR                            
               WHERE ITEM_NBR       = :MRITEM-ITEM-NBR                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PV-UPDATE-CNT                                       
                                                                                
               WHEN SQLCODE = +100                                              
                   DISPLAY 'TMRITEM ROW NOT FOUND '     MX006-ITM-NBR           
                   PERFORM 8000-WRITE-ERROR-RECORD                              
                                                                                
               WHEN OTHER                                                       
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *   READ COMMIT CONTROL RECORD TO GET COMMIT COUNT MAX.                   
      ******************************************************************        
       7000-READ-COMMIT-CONTROL-FILE.                                           
           MOVE '7000-READ-COMMIT-CONTROL-FILE' TO PV-PARAGRAPH-NAME.           
                                                                                
           READ COMMIT-CONTROL-FILE                                             
               INTO PV-COMMIT-CONTROL-RECORD                                    
               AT END                                                           
                   DISPLAY '** ABEND     **'                                    
                   DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME                     
                   DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME               
                   DISPLAY '** EMPTY COMMIT CONTROL CARD **'                    
                   SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                       
                   CALL 'ILBOABN0' USING ABEND-CODE.                            
                                                                                
           MOVE PV-COMMIT-CONTROL-CNT TO PC-MAX-BEFORE-COMMIT.                  
                                                                                
      ******************************************************************        
      *   READ DATE CONTROL RECORD.                                             
      ******************************************************************        
       7010-READ-DATE-CONTROL-FILE.                                             
           MOVE '7010-READ-DATE-CONTROL-FILE' TO PV-PARAGRAPH-NAME.             
                                                                                
           READ DATE-CONTROL-FILE INTO CC-CONTROL-CARD                          
               AT END                                                           
                   SET PS-END-OF-DATE-FILE TO TRUE.                             
                                                                                
           IF PS-END-OF-DATE-FILE                                               
              DISPLAY '** ABEND     **'                                         
              DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME                          
              DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME                    
              DISPLAY '** EMPTY DATE CONTROL CARD **'                           
              SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                            
              CALL 'ILBOABN0' USING ABEND-CODE                                  
           END-IF.                                                              
                                                                                
           DISPLAY PC-PGM-NAME                                                  
                   ' - CONTROL CARD = (' CC-PROCDT-CCYY-MM-DD ')'.              
                                                                                
           MOVE CC-PROCDT-CCYY-MM-DD TO PV-DB2-CCYY-MM-DD.                      
                                                                                
           CLOSE DATE-CONTROL-FILE.                                             
                                                                                
      ******************************************************************        
      *   READ MPX INPUT RECORD.                                                
      ******************************************************************        
       7050-READ-MPX-INPUT-FILE.                                                
           READ MPX-INPUT-FILE                                                  
               INTO MX006-SKU-TRAN-RECORD                                       
               AT END                                                           
                   SET PS-EOF TO TRUE.                                          
           IF PS-NOT-EOF                                                        
              ADD 1 TO PV-INPUT-CNT                                             
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    COMMIT THE UPDATES AND RESET COMMIT COUNTER TO 0.                    
      ******************************************************************        
       7600-COMMIT-UPDATE.                                                      
           EXEC SQL                                                             
              COMMIT                                                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
               DISPLAY '** ' PV-COMMIT-CNT ' RECORDS COMMITTED.'                
               MOVE 0 TO PV-COMMIT-CNT                                          
           ELSE                                                                 
               MOVE '7600-COMMIT-UPDATE' TO PV-PARAGRAPH-NAME                   
               MOVE 'COMMIT'             TO PV-DB2-OPER-ATTEMPTED               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *   WRITE ERROR RECORD TO OUTPUT FILE (FROM INPUT FILE).                  
      ******************************************************************        
       8000-WRITE-ERROR-RECORD.                                                 
           WRITE OUTPUT-ERROR-RECORD                                            
               FROM MPX-INPUT-RECORD.                                           
                                                                                
           ADD 1 TO PV-ERROR-REC-CNT.                                           
                                                                                
      ******************************************************************        
      *   PERFORM FINAL COMMIT, DISPLAY END OF PGM MESSAGES,                    
      *   AND CLOSE FILES                                                       
      ******************************************************************        
       9000-POST-PROCESSING.                                                    
           PERFORM 7600-COMMIT-UPDATE.                                          
                                                                                
           DISPLAY 'MPX SKU INPUT RECORDS READ    = ' PV-INPUT-CNT.             
           DISPLAY 'MPX SKU ERROR RECORDS WRITTEN = ' PV-ERROR-REC-CNT.         
           DISPLAY 'TMRITEM RECORDS UPDATED  = ' PV-UPDATE-CNT.                 
           DISPLAY 'TMRTRN ROWS INSERTED = ' PV-TMRTRN-INSERT-CNT.              
           DISPLAY 'TMRSKTR ROWS INSERTED = ' PV-TMRSKTR-INSERT-CNT.            
                                                                                
           CLOSE MPX-INPUT-FILE                                                 
                 COMMIT-CONTROL-FILE                                            
                 OUTPUT-FILE.                                                   
                                                                                
      ******************************************************************        
      *   PERFORM SQL ABEND ROUTINE                                             
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           SET ABEND-4013-DB2-ERROR  TO TRUE.                                   
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PGM-NAME.                          
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
           DISPLAY ' '.                                                         
           DISPLAY ' MX006 ITEM NBR   = ' MX006-ITM-NBR.                        
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *****************************************************************         
      ***********************END OF PROGRAM****************************         
