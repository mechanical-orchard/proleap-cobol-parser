000100 IDENTIFICATION DIVISION.                                                 
000200 TITLE 'PREPACK TRANSCRIPTION STORE ALLOCATION'.                          
000300 PROGRAM-ID.    RCKCS810.                                                 
000400 AUTHOR.        DENNIS STEFFEN.                                           
000500 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000600 DATE-WRITTEN.  AUTUMN 1999.                                              
000700 DATE-COMPILED.                                                           
000800*                                                                         
000900******************************************************************        
001000*    E810 - PRORATION FOR PREPACK                                         
001100******************************************************************        
001200*                                                                         
001300 ENVIRONMENT DIVISION.                                                    
001400 CONFIGURATION SECTION.                                                   
001500 SOURCE-COMPUTER.        IBM-3090.                                        
001600 OBJECT-COMPUTER.        IBM-3090.                                        
001700 DATA DIVISION.                                                           
001800 WORKING-STORAGE SECTION.                                                 
001900                                                                          
002000 01  PROGRAM-DB2-KEYS.                                                    
002100     05  DK-PO-LNE-NBR                PIC  S9(9) COMP VALUE ZERO.         
002200     05  DK-PLAN-NBR                  PIC  S9(4) COMP VALUE ZERO.         
002300                                                                          
002400 01  PS-PROGRAM-SWITCHES.                                                 
002500     05  PS-DONE-SW                   PIC  X    VALUE 'N'.                
002600         88  PS-NOT-DONE                        VALUE 'N'.                
002700         88  PS-DONE                            VALUE 'Y'.                
002800     05  PS-TAKEAWAY-SW               PIC  X    VALUE 'N'.                
002900         88  PS-TAKEAWAY-NOT-DONE               VALUE 'N'.                
003000         88  PS-TAKEAWAY-DONE                   VALUE 'Y'.                
003100     05  PS-GIVEAWAY-SW               PIC  X    VALUE 'N'.                
003200         88  PS-GIVEAWAY-NOT-DONE               VALUE 'N'.                
003300         88  PS-GIVEAWAY-DONE                   VALUE 'Y'.                
003400     05  PS-PLAN-SW                   PIC  X    VALUE 'M'.                
003500         88  PS-MASTER-PLAN                     VALUE 'M'.                
003600         88  PS-SPECIFIC-PLAN                   VALUE 'S'.                
003700     05  PS-TSPSTDS-FOUND-SW          PIC  X    VALUE 'N'.                
003800         88  PS-TSPSTDS-FOUND                   VALUE 'Y'.                
003900         88  PS-TSPSTDS-NOT-FOUND               VALUE 'N'.                
004000     05  PS-MESSAGE-SW                PIC  X    VALUE 'N'.                
004100         88  PS-USE-SPECIFIC-MSG                VALUE 'Y'.                
004200     05  PS-TALLPLN-SW                PIC  X    VALUE 'N'.                
004300         88  PS-INACT-TALLPLN                   VALUE 'Y'.                
004400                                                                          
004500                                                                          
004600 01  PV-PROGRAM-VARIABLES.                                                
004700     05  PV-ITEM                      PIC S9(4) VALUE ZERO                
004800                                                      COMP SYNC.          
004900     05  PV-CICS-RESP                 PIC S9(4) VALUE ZERO                
005000                                                COMP SYNC.                
005100     05  PV-DB2-COUNT                 PIC S9(9) COMP VALUE ZERO.          
005200     05  PV-TEMP-OTR-QTY              PIC S9(9) COMP VALUE ZERO.          
005300     05  PV-WORK-QTY                  PIC S9(9) COMP VALUE ZERO.          
005400                                                                          
005500     05  PV-FACTOR                    PIC S9(4)V9(04) VALUE ZERO          
005600                                                COMP SYNC.                
005700     05  PV-SORT-KEY-1.                                                   
005800         10  PV-SORT-ALLOC-CDE-1      PIC X(1) VALUE SPACE.               
005900         10  PV-SORT-RANK-1           PIC 9(4) VALUE ZERO.                
006000     05  PV-SORT-KEY-2.                                                   
006100         10  PV-SORT-ALLOC-CDE-2      PIC X(1) VALUE SPACE.               
006200         10  PV-SORT-RANK-2           PIC 9(4) VALUE ZERO.                
006300                                                                          
006400     05  PV-SUM-STR-PLAN-QTY          PIC S9(9) COMP.                     
006500     05  PV-SUM-STR-REC-QTY           PIC S9(9) COMP.                     
006600     05  PV-SUM-STR-OTR-UNITS         PIC S9(9) COMP.                     
006700     05  PV-UNITS-TO-ALLOC            PIC S9(9) COMP.                     
006800     05  PV-PACKS-TO-ALLOC            PIC S9(9) COMP.                     
006900     05  PV-LEFT-TO-ALLOC             PIC S9(9) COMP-3.                   
007000     05  PV-ORIG-QTY-TO-ALLOC         PIC S9(9) COMP-3.                   
007100     05  PV-REMAINDER                 PIC S9(9) COMP.                     
007200     05  PV-WHOLE-UNITS               PIC S9(9) COMP.                     
007300     05  PV-TEMP                      PIC X(55).                          
007400                                                                          
007500     05  PV-TAKE-AWAY                 PIC S9(9) COMP VALUE ZERO.          
007600     05  PV-GIVE-AWAY                 PIC S9(9) COMP VALUE ZERO.          
007700     05  PV-STR-COUNT                 PIC S9(4) COMP VALUE ZERO.          
007800     05  PV-STRS-AT-MIN-CNT           PIC S9(4) COMP VALUE ZERO.          
007900     05  PV-STRS-AT-MAX-CNT           PIC S9(4) COMP VALUE ZERO.          
008000     05  PV-DRIVER-RATIO-QTY          PIC S9(4) COMP VALUE ZERO.          
008100     05  PV-DRIVER-PUR-RATIO-QTY      PIC S9(4) COMP VALUE ZERO.          
008200     05  PV-FIXED-ALLOC-STR-CNT       PIC S9(4) COMP VALUE ZERO.          
008300     05  PV-PREPACK-ALLOC-STR-CNT     PIC S9(4) COMP VALUE ZERO.          
008400     05  PV-PRESENTATION-MIN          PIC S9(9) COMP VALUE ZERO.          
008500     05  PV-PRESENTATION-MAX          PIC S9(9) COMP VALUE ZERO.          
008600     05  PV-ALLOC-TYPE-FILTER         PIC X(01).                          
008700     05  PV-TOTAL-RECDIS-ALLOC        PIC S9(9) COMP VALUE ZERO.          
008800                                                                          
008900     05  PV-OP.                                                           
009000         10  PV-OP-N                  PIC 999.                            
009100     05  PV-FCLTY.                                                        
009200         10  PV-FCLTY-N               PIC 9.                              
009300     05  PV-PO.                                                           
009400         10  PV-PO-N                  PIC 9(8).                           
009500     05  PV-LINE.                                                         
009600         10  PV-LINE-N                PIC 999.                            
009700     05  PV-REC-SEQ-NBR.                                                  
009800         10  PV-REC-SEQ-NBR-N         PIC 999.                            
009900                                                                          
010000 01  PV-POM-VARIABLES.                                                    
010100* LIFTED FROM RCKCS233                                                    
010200     05  PV-OTR-STR-COUNT             PIC S9(4) VALUE ZERO                
010300                                                COMP SYNC.                
010400     05  PS-UNITS-PROCESSED-SW        PIC  X    VALUE 'Y'.                
010500         88  PS-UNITS-PROCESSED                 VALUE 'Y'.                
010600         88  PS-NO-UNITS-PROCESSED              VALUE 'N'.                
010700     05  PV-ALLOCATED-PACKS           PIC S9(4) COMP.                     
010800     05  PV-UNITS-FOR-STORE           PIC S9(9) COMP.                     
010900     05  PV-PACKS-FOR-STORE           PIC S9(4) COMP.                     
011000                                                                          
011100 01  PV-MA-VARIABLES.                                                     
011200* LIFTED FROM RCKCS233                                                    
011300     05  PV-STR-SUB                   PIC S9(4) VALUE ZERO                
011400                                                COMP SYNC.                
011500     05  PV-ALLOC-OUTR-PK-QTY         PIC S9(7)   COMP-3.                 
011600     05  PV-ALLOC-OUTR-PACKS          PIC S9(7)   COMP-3.                 
011700     05  PV-ALLOC-OUTR-UNITS          PIC S9(7)   COMP-3.                 
011800     05  PV-ALLOC-TOTAL               PIC S9(7)V9(4)   COMP-3.            
011900     05  PV-ALLOC-TOTAL-INTEGER       PIC S9(7)        COMP-3.            
012000     05  PV-ALLOC-TOTAL-DECIMAL       PIC SV9(4)       COMP-3.            
012100     05  PV-TOTAL-ALLOCATED           PIC S9(7)   COMP-3.                 
012200     05  PV-FILL-FIRST-OTR-UNITS      PIC S9(7)   COMP-3.                 
012300     05  PV-FILL-FIRST-OTR-PACKS      PIC S9(7)   COMP-3.                 
012400     05  PV-FILL-FIRST-OTR-TOTALS     PIC S9(7)   COMP-3.                 
012500     05  PV-STANDARD-OTR-UNITS        PIC S9(7)   COMP-3.                 
012600     05  PV-STANDARD-OTR-PACKS        PIC S9(7)   COMP-3.                 
012700     05  PV-STANDARD-OTR-TOTALS       PIC S9(7)   COMP-3.                 
012800     05  PV-FILL-LAST-OTR-UNITS       PIC S9(7)   COMP-3.                 
012900     05  PV-FILL-LAST-OTR-PACKS       PIC S9(7)   COMP-3.                 
013000     05  PV-FILL-LAST-OTR-TOTALS      PIC S9(7)   COMP-3.                 
013100     05  PV-CSTOCK-OTR-UNITS          PIC S9(7)   COMP-3.                 
013200     05  PV-CSTOCK-OTR-PACKS          PIC S9(7)   COMP-3.                 
013300     05  PV-CSTOCK-OTR-TOTALS         PIC S9(7)   COMP-3.                 
013400     05  PV-OVER-SHORT-UNITS          PIC S9(7)   COMP-3.                 
013500     05  PV-OVER-SHORT-PACKS          PIC S9(7)   COMP-3.                 
013600     05  PV-OVER-SHORT-TOTALS         PIC S9(7)   COMP-3.                 
013700     05  PV-OVER-SHORT-AMT            PIC S9(7)   COMP-3.                 
013800     05  PV-REMAINING-STR-OTR         PIC S9(7)   COMP-3.                 
013900     05  PV-LOOP-COUNT                PIC S9(7)   COMP-3.                 
014000     05  PV-PK-SIZE-RND-CDE           PIC X(01)  VALUE SPACE.             
014100         88  PV-VALID-PK-SIZE-RND-CDE     VALUES ARE                      
014200             '1'  '2'  '3'  '4'  '5'  '6'  '7'  '8'  '9'                  
014300             'D'  'R'  'U'.                                               
014400                                                                          
014500     05  PV-HOLD-ALLOC-SEQ-CODE       PIC X(01).                          
014600     05  PV-SHIPT-OVRG-PCT            PIC S9(2)V  COMP-3.                 
014700     05  PV-SHIPT-SHTG-PCT            PIC S9(2)V  COMP-3.                 
014800                                                                          
014900     05  PS-STORE-SW                  PIC  X    VALUE 'N'.                
015000         88  PS-STORE-IN-TABLE                  VALUE 'Y'.                
015100         88  PS-STORE-NOT-IN-TABLE              VALUE 'N'.                
015200     05  PC-10-PERCENT          PIC S9V99 VALUE +0.10 COMP-3.             
015300     05  PC-20-PERCENT          PIC S9V99 VALUE +0.20 COMP-3.             
015400     05  PC-30-PERCENT          PIC S9V99 VALUE +0.30 COMP-3.             
015500     05  PC-40-PERCENT          PIC S9V99 VALUE +0.40 COMP-3.             
015600     05  PC-49-PERCENT          PIC S9V99 VALUE +0.49 COMP-3.             
015700     05  PC-50-PERCENT          PIC S9V99 VALUE +0.50 COMP-3.             
015800     05  PC-60-PERCENT          PIC S9V99 VALUE +0.60 COMP-3.             
015900     05  PC-70-PERCENT          PIC S9V99 VALUE +0.70 COMP-3.             
016000     05  PC-80-PERCENT          PIC S9V99 VALUE +0.80 COMP-3.             
016100     05  PC-90-PERCENT          PIC S9V99 VALUE +0.90 COMP-3.             
016200                                                                          
016300                                                                          
016400                                                                          
016500 01  PRV-PROGRAM-RETURN-VARIABLES.                                        
016600     05  PRV-00                       PIC X(42) VALUE                     
016700         '00PROGRAM CALL WAS SUCCESSFUL             '.                    
016800     05  PRV-01                       PIC X(42) VALUE                     
016900         '01INVALID KEY FIELDS                      '.                    
017000     05  PRV-02                       PIC X(42) VALUE                     
017100         '02DB2 TIMEOUT ERROR                       '.                    
017200     05  PRV-03                       PIC X(42) VALUE                     
017300         '03SQL ERROR                               '.                    
017400     05  PRV-30                       PIC X(42) VALUE                     
017500         '30PREPACK LINE QTY ERROR                  '.                    
017600     05  PRV-04                       PIC X(42) VALUE                     
017700         '04UNRECOVERABLE ERROR                     '.                    
017800     05  PRV-61                       PIC X(42) VALUE                     
017900         '61RECEIVER IS NOT PRE-PACK                '.                    
018000     05  PRV-62                       PIC X(42) VALUE                     
018100         '62DRIVER LINE MISSING                     '.                    
018200                                                                          
018300     05  PRV-RC809-RETURN-MSG.                                            
018400         10  PRV-PGM                  PIC X(3).                           
018500         10  FILLER                   PIC X(1).                           
018600         10  PRV-PARA                 PIC X(4).                           
018700         10  FILLER                   PIC X(1).                           
018800         10  PRV-TBL                  PIC X(7).                           
018900         10  FILLER                   PIC X(1).                           
019000         10  PRV-OP                   PIC X(3).                           
019100         10  FILLER                   PIC X(1).                           
019200         10  PRV-FCLTY                PIC X(1).                           
019300         10  FILLER                   PIC X(1).                           
019400         10  PRV-PO                   PIC X(8).                           
019500         10  FILLER                   PIC X(1).                           
019600         10  PRV-LINE                 PIC X(3).                           
019700         10  FILLER                   PIC X(1).                           
019800         10  PRV-REC-SEQ-NBR          PIC 9(3).                           
019900         10  FILLER                   PIC X(1).                           
020000                                                                          
020100 01  PC-PROGRAM-CONSTANTS.                                                
020200     05  PC-CURRENT-PROGRAM-NAME      PIC X(08) VALUE 'RCKCS810'.         
020300     05  PC-MAX-STORES                PIC S9(4) COMP VALUE +200.          
020400     05  PC-DEFAULT-DATE              PIC X(10) VALUE                     
020500                                                '9999-09-09'.             
020600                                                                          
020700 01  PT-STORE-TABLE.                                                      
020800     02  PT-STORE-DATA                                                    
020900             OCCURS 200 TIMES                                             
021000             INDEXED BY PT-STR-IDX                                        
021100                        PT-LAST-STR-IDX                                   
021200                        PT-STR-IDX-1                                      
021300                        PT-STR-IDX-2.                                     
021400                                                                          
021500         05  PT-STR-NBR               PIC S9(4) VALUE ZERO COMP.          
021600         05  PT-STR-ALLOC-TOTALS      PIC S9(9) VALUE ZERO COMP.          
021700         05  PT-STR-OTR-TOTALS        PIC S9(9) VALUE ZERO COMP.          
021800         05  PT-STR-PLAN-QTY          PIC S9(9) VALUE ZERO COMP.          
021900         05  PT-STR-REC-QTY           PIC S9(9) VALUE ZERO COMP.          
022000         05  PT-STR-OTR-UNITS         PIC S9(9) VALUE ZERO COMP.          
022100         05  PT-STR-OTR-PACKS         PIC S9(9) VALUE ZERO COMP.          
022200         05  PT-STR-ALLOC-PCT         PIC S99V9(5)                        
022300                                                VALUE ZERO COMP-3.        
022400         05  PT-STR-ALLOC-UNITS       PIC S9(9) VALUE ZERO COMP.          
022500         05  PT-STR-ALLOC-PACKS       PIC S9(9) VALUE ZERO COMP.          
022600                                                                          
022700         05  PT-STR-ALLOC-SEQ-CDE     PIC X(1)  VALUE SPACE.              
022800         05  PT-STR-RNK-SEQ-NBR       PIC S9(5) VALUE ZERO                
022900                                                   COMP-3.                
023000         05  PT-STR-CNSTR-CDE         PIC X(1).                           
023100         05  PT-STR-CNSTR-QTY         PIC S9(9) COMP.                     
023200         05  PT-STR-OTR-TOTALS-MA     PIC S9(9) VALUE ZERO COMP.          
023300         05  PT-STR-ALLOC-AD-TOTALS   PIC S9(9) VALUE ZERO COMP.          
023400/                                                                         
023500 01  PT-SAVE-QTYS-TABLE.                                                  
023600     02  PT-SAVE-STORE-DATA                                               
023700             OCCURS 200 TIMES                                             
023800             INDEXED BY PT-SAVE-IDX.                                      
023900                                                                          
024000         05  PT-SAVE-STR-NBR          PIC S9(4) VALUE ZERO COMP.          
024100         05  PT-SAVE-STR-ALLOC        PIC S9(9) VALUE ZERO COMP.          
024200         05  PT-SAVE-STR-AD-ALLOC     PIC S9(9) VALUE ZERO COMP.          
024300     COPY RCWS808.                                                        
024400/                                                                         
024500     COPY RCWS809.                                                        
024600/                                                                         
024700     COPY RCWS810.                                                        
024800/                                                                         
024900     COPY RCWS001.                                                        
025000/                                                                         
025100*    GLOBAL COMMAREA                                                      
025200     COPY DPWS020.                                                        
025300/                                                                         
025400     EXEC SQL                                                             
025500          INCLUDE SQLCA                                                   
025600     END-EXEC.                                                            
025700                                                                          
025800     EXEC SQL                                                             
025900          INCLUDE TRECDIS                                                 
026000     END-EXEC.                                                            
026100/                                                                         
026200     EXEC SQL                                                             
026300          INCLUDE TALLPLN                                                 
026400     END-EXEC.                                                            
026500/                                                                         
026600     EXEC SQL                                                             
026700         INCLUDE TSPSTDS                                                  
026800     END-EXEC.                                                            
026900/                                                                         
026910     EXEC SQL                                                             
026920         INCLUDE RCT00009                                                 
026930     END-EXEC.                                                            
026940/                                                                         
027000 LINKAGE SECTION.                                                         
027100                                                                          
027200 01  DFHCOMMAREA.                                                         
027300     05  FILLER                       OCCURS 1 TO 4072 TIMES              
027400                                          DEPENDING ON EIBCALEN.          
027500         10  FILLER                   PIC X.                              
027600                                                                          
027700/                                                                         
027800 PROCEDURE DIVISION.                                                      
027900                                                                          
028000 0000-MAIN-MODULE.                                                        
028100                                                                          
028200     PERFORM 1000-INITIAL-PROCESS.                                        
028300                                                                          
028400* PRORATE STORE QTYS FOR THE DRIVER LINE                                  
028500     IF PS-NOT-DONE                                                       
028600                                                                          
028700         EVALUATE TRUE                                                    
028800*            WHEN PV-LEFT-TO-ALLOC <= ZERO                                
028900* ALLOCATION OF THE AD QTYS HAS USED UP ALL THE PACKS                     
029000*                PERFORM 0100-FINISH-ALLOCATION                           
029100                                                                          
029200             WHEN RC809-DISTRO-REQ-ID = ZERO                              
029300*                     (OLD POM PRORATION)                                 
029400                 PERFORM 10000-POM-ALLOCATION                             
029500                                                                          
029600             WHEN OTHER                                                   
029700                 IF (PV-FIXED-ALLOC-STR-CNT +                             
029800                        PV-PREPACK-ALLOC-STR-CNT) = PV-STR-COUNT          
029900                                                                          
030000                     IF RC809-AD-YES                                      
030100                         PERFORM 2000-ALLOC-PICTURED-AD                   
030200                     END-IF                                               
030300                     IF PV-LEFT-TO-ALLOC <= ZERO                          
030400*             ALLOCATION OF THE AD QTYS HAS USED UP ALL THE PACKS         
030500                         PERFORM 0100-FINISH-ALLOCATION                   
030600                     END-IF                                               
030700                                                                          
030800                     IF PV-FIXED-ALLOC-STR-CNT > ZERO                     
030900                            AND PV-LEFT-TO-ALLOC > ZERO                   
031000*                        (NEW MA PRORATION)                               
031100                         PERFORM 3000-ALLOC-FIXED                         
031200                         IF RC809-AD-YES                                  
031300                          AND PV-LEFT-TO-ALLOC <= ZERO                    
031400*             ALLOCATION OF THE FIXED QTYS HAS USED UP ALL THE PAC        
031500                               PERFORM 0050-FINISH-PIC-AD-ALLOC           
031600                         END-IF                                           
031700                     END-IF                                               
031800                                                                          
031900                     IF PS-NOT-DONE                                       
032000                         IF PV-PREPACK-ALLOC-STR-CNT > ZERO               
032100                            AND PV-LEFT-TO-ALLOC > ZERO                   
032200                             PERFORM 4000-ALLOC-PREPACK                   
032300                         END-IF                                           
032400                     END-IF                                               
032500                                                                          
032600                 ELSE                                                     
032700*                        (OLD MA PRORATION)                               
032800                                                                          
032900                     PERFORM 20000-MA-ALLOCATION                          
033000                                                                          
033100                 END-IF                                                   
033200                                                                          
033300         END-EVALUATE                                                     
033400     END-IF.                                                              
033500                                                                          
033600* BEFORE CLONING THE DRIVER LINE STORES, ADD ANY UNPROCESSED              
033700* STORES TO THE WORK TABLE.  THESE ARE STORES THAT DIDN'T PASS            
033800* THROUGH THE FILTERS WHEN THE WORK TABLE WAS FIRST BUILT.  AS            
033900* THEY ARE ADDED, INSERT THEM TO RECDIS FOR THE DRIVER LINE ONLY.         
034000* THIS ASSURES THAT ALL THE STORES IN TMESTDS/TSPSTDS ARE                 
034100* ACCOUNTED FOR EVEN IF THEY HAD NO PART IN THE PRORATION PROCESS.        
034200*                                                                         
034300        PERFORM 0200-UNUSED-STORES.                                       
034400                                                                          
034500                                                                          
034600                                                                          
034700                                                                          
034800* CLONE THE DRIVER LINE STORE DISTROS INTO THE OTHER LINES                
034900     PERFORM 5200-CLONE-DRIVER-RECDIS                                     
035000             VARYING RC808-LINE-IDX FROM 1 BY 1                           
035100             UNTIL RC808-PRPK-LNE-NBR(RC808-LINE-IDX) = ZERO              
035200             OR PS-DONE                                                   
035300                                                                          
035400                                                                          
035500     IF PV-TOTAL-RECDIS-ALLOC > RC809-UNITS-TO-ALLOC                      
035600         SET RC809-INVALID-PRORATION  TO TRUE                             
035700     END-IF.                                                              
035800                                                                          
035900*    IF PS-NOT-DONE AND PS-INACT-TALLPLN                                  
036000* * *  (SET WHEN THE TALLPLN QTY DOEN'T MATCH THE STORE QTYS ON A         
036100* * *        MASTER PLAN)                                                 
036200*        PERFORM 7100-INACTIVATE-ALLPLN                                   
036300*            VARYING RC808-LINE-IDX FROM 1 BY 1                           
036400*            UNTIL RC808-PRPK-LNE-NBR(RC808-LINE-IDX) = ZERO              
036500*            OR PS-DONE                                                   
036600*    END-IF.                                                              
036700                                                                          
036800* THIS HAS TO BE SET WHEN A SPECIFIC PLAN IS USED AND                     
036900* THE PLAN QTY DOES NOT EQUAL THE STORE QTY                               
037000*        SET PS-USE-SPECIFIC-MSG  TO TRUE                                 
037100                                                                          
037200     IF PS-NOT-DONE AND RC809-SPECIFIC-PLAN                               
037300        PERFORM 7110-DELETE-SPSTDS                                        
037400             VARYING RC808-LINE-IDX FROM 1 BY 1                           
037500             UNTIL RC808-PRPK-LNE-NBR(RC808-LINE-IDX) = ZERO              
037600             OR PS-DONE                                                   
037700     END-IF.                                                              
037800                                                                          
037900     PERFORM 0000-GOBACK.                                                 
038000                                                                          
038100 0000-GOBACK.                                                             
038200     MOVE RC809-COMMAREA              TO DFHCOMMAREA.                     
038300     GOBACK.                                                              
038400/                                                                         
038500                                                                          
038600 0050-FINISH-PIC-AD-ALLOC.                                                
038700                                                                          
038800     INITIALIZE PT-STORE-TABLE                                            
038900     MOVE 'M'                         TO PV-ALLOC-TYPE-FILTER             
039000     SET PT-STR-IDX                                                       
039100         PT-LAST-STR-IDX              TO 1                                
039200     MOVE ZERO                        TO PV-STR-COUNT                     
039300                            PV-SUM-STR-OTR-UNITS                          
039400     MOVE RC808-LNE-OTR-PACK-QTY (RC808-LINE-IDX)                         
039500                                      TO PV-TEMP-OTR-QTY                  
039600     PERFORM 6350-ADJUST-TEMP-OTR                                         
039700                                                                          
039800     PERFORM 1400-BUILD-STORE-ARRAY                                       
039900             VARYING RC808-STR-IDX FROM 1 BY 1                            
040000             UNTIL                                                        
040100             RC808-STR-NBR (RC808-LINE-IDX, RC808-STR-IDX) = ZERO         
040200             OR RC808-STR-IDX > PC-MAX-STORES.                            
040300                                                                          
040400     SET PT-STR-IDX                                                       
040500         PT-LAST-STR-IDX DOWN BY 1                                        
040600                                                                          
040700     PERFORM 0100-FINISH-ALLOCATION.                                      
040800/                                                                         
040900 0100-FINISH-ALLOCATION.                                                  
041000                                                                          
041100     PERFORM 1420-APPLY-PHANTOM-RECEIPTS                                  
041200           VARYING PT-STR-IDX FROM 1 BY 1                                 
041300           UNTIL PT-STR-IDX > PT-LAST-STR-IDX                             
041400                                                                          
041500     PERFORM 6310-SUBTRACT-PRIOR-RECEIPTS                                 
041600                                                                          
041700     IF RC809-CREATE-AUTO                                                 
041800         PERFORM 5000-ALLOCATE-STORES                                     
041900           VARYING PT-STR-IDX FROM 1 BY 1                                 
042000           UNTIL PT-STR-NBR (PT-STR-IDX) = ZERO                           
042100                OR PS-DONE                                                
042200                OR PT-STR-IDX > PC-MAX-STORES                             
042300                                                                          
042400     END-IF.                                                              
042500/                                                                         
042600 0200-UNUSED-STORES.                                                      
042700     SET RC808-LINE-IDX               TO RC809-DRIVER-LNE-PTR             
042800     PERFORM 0210-MOVE-TO-WORK-TABLE                                      
042900         VARYING RC808-STR-IDX FROM 1 BY 1 UNTIL                          
043000                 RC808-STR-NBR (RC808-LINE-IDX, RC808-STR-IDX)            
043100                              = ZERO.                                     
043200                                                                          
043300 0210-MOVE-TO-WORK-TABLE.                                                 
043400     IF RC808-STR-PROCESSED (RC808-LINE-IDX, RC808-STR-IDX)               
043500         CONTINUE                                                         
043600     ELSE                                                                 
043700         SET PT-LAST-STR-IDX UP BY 1                                      
043800         SET PT-STR-IDX               TO PT-LAST-STR-IDX                  
043900* MOVE ONLY THE FIELDS NEEDED TO POPULATE RECDIS                          
044000         MOVE ZERO                    TO PT-STR-ALLOC-TOTALS              
044100                                         (PT-STR-IDX)                     
044200                                         PT-STR-OTR-TOTALS                
044300                                         (PT-STR-IDX)                     
044400         MOVE RC808-STR-NBR (RC808-LINE-IDX, RC808-STR-IDX)               
044500                                      TO PT-STR-NBR                       
044600                                         (PT-STR-IDX)                     
044700         PERFORM 5100-PREPARE-TRECDIS                                     
044800         IF PS-NOT-DONE                                                   
044900             PERFORM 7000-INSERT-TRECDIS                                  
045000         END-IF                                                           
045100     END-IF.                                                              
045200                                                                          
045300                                                                          
045400                                                                          
045500                                                                          
045600 1000-INITIAL-PROCESS.                                                    
045700                                                                          
045800     INITIALIZE RC808-PACK-CONTENTS                                       
045900                RC809-COMMAREA                                            
046000                PV-PROGRAM-VARIABLES                                      
046100                PT-STORE-TABLE                                            
046200     SET PS-TAKEAWAY-NOT-DONE                                             
046300         PS-GIVEAWAY-NOT-DONE         TO TRUE                             
046400                                                                          
046500* BUILD RC808 TABLE FROM TEMP STORAGE QUEUE                               
046600     IF EIBCALEN = ZERO                                                   
046700         SET PS-DONE                  TO TRUE                             
046800     ELSE                                                                 
046900         MOVE DFHCOMMAREA             TO RC809-COMMAREA                   
047000         MOVE 810                     TO RC809-PREPACK-PGM-NBR            
047100         PERFORM 8100-READ-TEMP-STORAGE                                   
047200             VARYING PV-ITEM FROM 1 BY 1                                  
047300             UNTIL PV-ITEM > RC809-QUEUE-REC-COUNT                        
047400             OR PS-DONE                                                   
047500                                                                          
047600         MOVE ZERO                    TO RC809-RETURN-CODE                
047700         PERFORM 8150-DELETE-TEMP-QUEUE                                   
047800     END-IF.                                                              
047900                                                                          
048000     SET RC808-LINE-IDX               TO RC809-DRIVER-LNE-PTR             
048100     MOVE RC808-PRPK-LNE-NBR(RC808-LINE-IDX)                              
048200                                      TO DK-PO-LNE-NBR.                   
048300                                                                          
048400*     (EDIT FOR LINE NBR > 999 OR < ZERO IS DELETED)                      
048500                                                                          
048600     PERFORM 1200-FIND-OUT-PLAN.                                          
048700                                                                          
048800     PERFORM 1300-VALIDATE-PACK                                           
048900                                                                          
049000                                                                          
049100* COUNT THE STORES THAT ARE TO BE ALLOCATED WITH FIXED AMOUNTS            
049200* AND THE NORMAL PREPACK METHOD.                                          
049300                                                                          
049400     MOVE ZERO                        TO PV-STR-COUNT                     
049500     PERFORM VARYING RC808-STR-IDX FROM 1 BY 1                            
049600             UNTIL RC808-STR-NBR(RC808-LINE-IDX, RC808-STR-IDX)           
049700                                      = ZERO                              
049800                     OR RC808-STR-IDX > PC-MAX-STORES                     
049900         ADD 1                        TO PV-STR-COUNT                     
050000         EVALUATE TRUE                                                    
050100             WHEN RC808-STR-CNSTR-CDE                                     
050200                     (RC808-LINE-IDX, RC808-STR-IDX) = 'F'                
050300                 ADD 1                TO PV-FIXED-ALLOC-STR-CNT           
050400             WHEN RC808-STR-CNSTR-CDE                                     
050500                      (RC808-LINE-IDX, RC808-STR-IDX) = 'M'               
050600                 ADD 1                TO PV-PREPACK-ALLOC-STR-CNT         
050700         END-EVALUATE                                                     
050800     END-PERFORM.                                                         
050900                                                                          
051000                                                                          
051100* IF THE RECITM RATIO DIFFERS FROM THE PURITM RATIO OF THE DRIVER         
051200* LINE, SOMEBODY HAS CHANGED THE PACK CONFIGURATION.  ADJUST THE          
051300* DISTRO QUANTITIES FOR ALL STORES TO AGREE WITH THE NEW RATIO.           
051400*                                                                         
051500                                                                          
051600     IF RC808-RECITM-RATO-QTY (RC808-LINE-IDX) NOT =                      
051700             RC808-PURITM-RATO-QTY (RC808-LINE-IDX)                       
051800                                                                          
051900         PERFORM VARYING RC808-STR-IDX FROM 1 BY 1                        
052000                 UNTIL RC808-STR-IDX > PV-STR-COUNT                       
052100                                                                          
052200             PERFORM 6353-ADJUST-DISTRO-QTY                               
052300                                                                          
052400         END-PERFORM                                                      
052500                                                                          
052600     END-IF.                                                              
052700                                                                          
052800     MOVE RC809-PO-NBR                TO RECDIS-ORD-NBR.                  
052900     MOVE RC809-REC-SEQ-NBR           TO RECDIS-REC-SEQ-NBR.              
053000     MOVE RC809-OPER-UNT-ID           TO RECDIS-OPER-UNT-ID.              
053100     MOVE 1                           TO RECDIS-FCLTY-ID                  
053200                                                                          
053300     PERFORM 1500-DELETE-RECDIS-FOR-PACK                                  
053400             VARYING RC808-LINE-IDX FROM 1 BY 1                           
053500             UNTIL RC808-PRPK-LNE-NBR(RC808-LINE-IDX) = ZERO              
053600             OR PS-DONE                                                   
053700                                                                          
053800* POINT THE INDEX BACK TO THE DRIVER LINE OCCURRENCE                      
053900     SET RC808-LINE-IDX               TO RC809-DRIVER-LNE-PTR.            
054000                                                                          
054100 1200-FIND-OUT-PLAN.                                                      
054200                                                                          
054300* THE LINE/STORE TABLE IS BUILT FROM TMESTDS UNLESS SPSTDS                
054400* ROWS ARE AVAILABLE.  DONE IN PROGRAM RCKCS808.                          
054500                                                                          
054600     MOVE RC808-LNE-DISTRO-QTY(RC808-LINE-IDX)                            
054700                                      TO PV-SUM-STR-PLAN-QTY              
054800                                                                          
054900     IF RC809-MASTER-PLAN                                                 
055000         IF RC808-CURR-SPLIT-QTY (RC808-LINE-IDX)                         
055100                                  = PV-SUM-STR-PLAN-QTY                   
055200             CONTINUE                                                     
055300         ELSE                                                             
055400             SET PS-INACT-TALLPLN     TO TRUE                             
055500         END-IF                                                           
055600     END-IF.                                                              
055700/                                                                         
055800 1300-VALIDATE-PACK.                                                      
055900******************************************************************        
056000*    THIS PARAGRAPH VALIDATES UNITS RECEIVED MAKING SURE THAT             
056100*    IT IS EVENLY DIVISIBLE BY THE INNR-PACK-QTY OF TABLE TRECITM.        
056200******************************************************************        
056300                                                                          
056400* INNR-PACK-QTY IS PASSED AS NON-ZERO FROM RCKCS808                       
056500                                                                          
056600     MOVE RC809-UNITS-TO-ALLOC        TO PV-LEFT-TO-ALLOC.                
056700     MOVE RC808-RECITM-RATO-QTY (RC809-DRIVER-LNE-PTR)                    
056800                                      TO PV-DRIVER-RATIO-QTY.             
056900     MOVE RC808-PURITM-RATO-QTY (RC809-DRIVER-LNE-PTR)                    
057000                                      TO PV-DRIVER-PUR-RATIO-QTY.         
057100                                                                          
057200 1400-BUILD-STORE-ARRAY.                                                  
057300*                                                                         
057400*    LOAD STORE DATA FROM RC808 INTO PROGRAM TABLE                        
057500*                                                                         
057600                                                                          
057700* WHEN THE FILTER IS 'X', WE WANT ALL OF THE STORES REGARDLESS            
057800* OF THEIR CONSTRAINT CODE.                                               
057900                                                                          
058000     IF PV-ALLOC-TYPE-FILTER = 'X'                                        
058100         PERFORM 1410-MOVE-STORE-INFO                                     
058200                                                                          
058300     ELSE                                                                 
058400                                                                          
058500         IF RC808-STR-CNSTR-CDE (RC808-LINE-IDX, RC808-STR-IDX)           
058600                     = PV-ALLOC-TYPE-FILTER                               
058700             PERFORM 1410-MOVE-STORE-INFO                                 
058800         ELSE                                                             
058900           IF RC808-STR-OTR-PK (RC808-LINE-IDX, RC808-STR-IDX)            
059000                   > ZERO                                                 
059100             PERFORM 1401-SUBTRACT-FROM-TEMP-OTR                          
059200           END-IF                                                         
059300         END-IF                                                           
059400     END-IF.                                                              
059500                                                                          
059600 1401-SUBTRACT-FROM-TEMP-OTR.                                             
059700     MULTIPLY PV-DRIVER-RATIO-QTY BY                                      
059800              RC808-STR-OTR-PK (RC808-LINE-IDX, RC808-STR-IDX)            
059900              GIVING PV-WHOLE-UNITS                                       
060000     SUBTRACT PV-WHOLE-UNITS FROM PV-TEMP-OTR-QTY.                        
060100                                                                          
060200                                                                          
060300 1410-MOVE-STORE-INFO.                                                    
060400         MOVE RC808-STR-NBR (RC808-LINE-IDX, RC808-STR-IDX)               
060500                                      TO PT-STR-NBR (PT-STR-IDX)          
060600         SET RC808-STR-PROCESSED (RC808-LINE-IDX, RC808-STR-IDX)          
060700                                      TO TRUE                             
060800         MOVE RC808-STR-DISTRO-QTY(RC808-LINE-IDX, RC808-STR-IDX)         
060900                                      TO                                  
061000                                 PT-STR-PLAN-QTY (PT-STR-IDX)             
061100                                                                          
061200                                                                          
061300         MOVE RC808-ALLOC-SEQ-CDE (RC808-LINE-IDX, RC808-STR-IDX)         
061400                                      TO                                  
061500                           PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX)             
061600         MOVE RC808-RNK-SEQ-NBR (RC808-LINE-IDX, RC808-STR-IDX)           
061700                                      TO                                  
061800                              PT-STR-RNK-SEQ-NBR (PT-STR-IDX)             
061900         MOVE RC808-STR-CNSTR-QTY (RC808-LINE-IDX, RC808-STR-IDX)         
062000                                      TO                                  
062100                              PT-STR-CNSTR-QTY (PT-STR-IDX)               
062200         PERFORM 6351-ADJUST-CNSTR-QTY                                    
062300                                                                          
062400         MOVE RC808-STR-CNSTR-CDE (RC808-LINE-IDX, RC808-STR-IDX)         
062500                                      TO                                  
062600                              PT-STR-CNSTR-CDE (PT-STR-IDX)               
062700                                                                          
062800         MULTIPLY RC808-STR-OTR-PK                                        
062900                      (RC808-LINE-IDX, RC808-STR-IDX)                     
063000             BY PV-DRIVER-RATIO-QTY                                       
063100             GIVING PT-STR-OTR-TOTALS (PT-STR-IDX)                        
063200                                                                          
063300                                                                          
063400* WHEN THE STORE OTR IS NEGATIVE, IT IMPLIES THAT AN OVERAGE              
063500* ALREADY EXISTS FOR THAT STORE.  WE MUST ADD THAT OVERAGE                
063600* QTY TO THE STORE PLAN QTY FOR THE TOTAL TO BE CORRECT.                  
063700                                                                          
063800         IF PT-STR-OTR-TOTALS (PT-STR-IDX) < ZERO                         
063900             SUBTRACT PT-STR-OTR-TOTALS (PT-STR-IDX) FROM                 
064000                                 PT-STR-PLAN-QTY (PT-STR-IDX)             
064100         ELSE                                                             
064200             ADD 1                    TO PV-OTR-STR-COUNT                 
064300         END-IF                                                           
064400                                                                          
064500         ADD PT-STR-OTR-TOTALS (PT-STR-IDX)                               
064600                                      TO PV-SUM-STR-OTR-UNITS             
064700                                                                          
064800         DIVIDE PV-DRIVER-RATIO-QTY                                       
064900             INTO PT-STR-OTR-TOTALS (PT-STR-IDX)                          
065000             GIVING PT-STR-OTR-PACKS (PT-STR-IDX)                         
065100                                                                          
065200         IF PV-TEMP-OTR-QTY > ZERO                                        
065300             COMPUTE PT-STR-ALLOC-PCT (PT-STR-IDX) =                      
065400                 PT-STR-OTR-TOTALS(PT-STR-IDX) /                          
065500                  PV-TEMP-OTR-QTY                                         
065600         ELSE                                                             
065700             MOVE ZERO                TO                                  
065800                 PT-STR-ALLOC-PCT (PT-STR-IDX)                            
065900         END-IF                                                           
066000                                                                          
066100     IF RC809-AD-YES                                                      
066200         PERFORM 1420-APPLY-PHANTOM-RECEIPTS                              
066300     END-IF                                                               
066400                                                                          
066500         SET PT-STR-IDX                                                   
066600             PT-LAST-STR-IDX UP BY 1                                      
066700         ADD 1                        TO PV-STR-COUNT.                    
066800                                                                          
066900 1420-APPLY-PHANTOM-RECEIPTS.                                             
067000* IF ANY STORE HAS BEEN SHORTED FOR A PICTURED AD, THIS                   
067100* RESTORES ITS ONE PACK GUARANTEE.                                        
067200                                                                          
067300     SET PT-SAVE-IDX                  TO 1                                
067400     SEARCH PT-SAVE-STORE-DATA                                            
067500         WHEN PT-SAVE-STR-NBR (PT-SAVE-IDX) =                             
067600                 PT-STR-NBR (PT-STR-IDX)                                  
067700             MOVE PT-SAVE-STR-ALLOC (PT-SAVE-IDX)                         
067800                                      TO                                  
067900                 PT-STR-ALLOC-TOTALS (PT-STR-IDX)                         
068000             MOVE PT-SAVE-STR-AD-ALLOC (PT-SAVE-IDX)                      
068100                                      TO                                  
068200                 PT-STR-ALLOC-AD-TOTALS (PT-STR-IDX)                      
068300             SUBTRACT PT-SAVE-STR-ALLOC (PT-SAVE-IDX)                     
068400                     FROM PV-TEMP-OTR-QTY                                 
068500         WHEN PT-SAVE-STR-NBR (PT-SAVE-IDX) = ZERO                        
068600             NEXT SENTENCE                                                
068700     END-SEARCH.                                                          
068800/                                                                         
068900 1500-DELETE-RECDIS-FOR-PACK.                                             
069000     MOVE RC808-PRPK-LNE-NBR (RC808-LINE-IDX)                             
069100                                      TO RECDIS-ORD-LNE-NBR               
069200     PERFORM 7200-DELETE-PACK-RECDIS.                                     
069300     PERFORM 7400-DELETE-ODD-UNIT.                                        
069400/                                                                         
069500 2000-ALLOC-PICTURED-AD.                                                  
069600                                                                          
069700* BUILD ARRAY WITH ONLY AD EVENT STORES (REALLY ALL STORES)               
069800     INITIALIZE PT-STORE-TABLE                                            
069900                PT-SAVE-QTYS-TABLE                                        
070000     MOVE 'X'                         TO PV-ALLOC-TYPE-FILTER             
070100     SET PT-STR-IDX                                                       
070200         PT-LAST-STR-IDX              TO 1                                
070300     MOVE ZERO                        TO PV-STR-COUNT                     
070400                            PV-SUM-STR-OTR-UNITS                          
070500     MOVE RC808-LNE-OTR-PACK-QTY (RC808-LINE-IDX)                         
070600                                      TO PV-TEMP-OTR-QTY                  
070700     PERFORM 6350-ADJUST-TEMP-OTR                                         
070800                                                                          
070900     PERFORM 1400-BUILD-STORE-ARRAY                                       
071000             VARYING RC808-STR-IDX FROM 1 BY 1                            
071100             UNTIL                                                        
071200             RC808-STR-NBR (RC808-LINE-IDX, RC808-STR-IDX) = ZERO         
071300             OR RC808-STR-IDX > PC-MAX-STORES.                            
071400                                                                          
071500     SET PT-STR-IDX                                                       
071600         PT-LAST-STR-IDX DOWN BY 1                                        
071700                                                                          
071800     PERFORM 6100-SORT-RANK-ASC                                           
071900                                                                          
072000     SET PT-SAVE-IDX                  TO 1                                
072100* CHECK IF ANY STORES HAVE NOT YET RECEIVED PACKS                         
072200     PERFORM VARYING PT-STR-IDX FROM 1 BY 1 UNTIL                         
072300         PT-STR-IDX > PT-LAST-STR-IDX                                     
072400         OR PV-LEFT-TO-ALLOC <= ZERO                                      
072500                                                                          
072600         IF PT-STR-PLAN-QTY (PT-STR-IDX) =                                
072700                 PT-STR-OTR-TOTALS (PT-STR-IDX)                           
072800* PHANTOM RECEIPT.  TREAT THESE AS IF THEY WERE A SEPARATE                
072900* RECEIPT.  WE WILL APPLY THEM TO THE PT-STR TABLE WITHIN                 
073000* THE SEPARATE ENGINES.                                                   
073100             MOVE PT-STR-NBR (PT-STR-IDX)                                 
073200                                      TO PT-SAVE-STR-NBR                  
073300                                         (PT-SAVE-IDX)                    
073400             ADD PV-DRIVER-RATIO-QTY  TO                                  
073500                        PT-SAVE-STR-ALLOC (PT-SAVE-IDX)                   
073600             ADD PV-DRIVER-RATIO-QTY  TO                                  
073700                        PT-SAVE-STR-AD-ALLOC (PT-SAVE-IDX)                
073800             SUBTRACT PV-DRIVER-RATIO-QTY                                 
073900                        FROM PV-LEFT-TO-ALLOC                             
074000             SET PT-SAVE-IDX UP BY 1                                      
074100         END-IF                                                           
074200                                                                          
074300     END-PERFORM.                                                         
074400/                                                                         
074500 3000-ALLOC-FIXED.                                                        
074600     INITIALIZE PT-STORE-TABLE                                            
074700     MOVE 'F'                         TO PV-ALLOC-TYPE-FILTER             
074800     SET PT-STR-IDX                                                       
074900         PT-LAST-STR-IDX              TO 1                                
075000     MOVE ZERO                        TO PV-STR-COUNT                     
075100                            PV-SUM-STR-OTR-UNITS                          
075200     MOVE RC808-LNE-OTR-PACK-QTY (RC808-LINE-IDX)                         
075300                                      TO PV-TEMP-OTR-QTY                  
075400     PERFORM 6350-ADJUST-TEMP-OTR                                         
075500                                                                          
075600     PERFORM 1400-BUILD-STORE-ARRAY                                       
075700             VARYING RC808-STR-IDX FROM 1 BY 1                            
075800             UNTIL                                                        
075900             RC808-STR-NBR (RC808-LINE-IDX, RC808-STR-IDX) = ZERO         
076000             OR RC808-STR-IDX > PC-MAX-STORES.                            
076100                                                                          
076200                                                                          
076300     SET PT-STR-IDX                                                       
076400         PT-LAST-STR-IDX DOWN BY 1                                        
076500                                                                          
076600     PERFORM 5300-CALC-PRESENTATION-QTYS.                                 
076700                                                                          
076800     IF RC809-AD-YES                                                      
076900        PERFORM VARYING PT-STR-IDX FROM 1 BY 1                            
077000            UNTIL PT-STR-IDX > PT-LAST-STR-IDX                            
077100               SUBTRACT PT-STR-ALLOC-AD-TOTALS (PT-STR-IDX)               
077200                   FROM PT-STR-ALLOC-TOTALS (PT-STR-IDX)                  
077300       END-PERFORM                                                        
077400     END-IF.                                                              
077500                                                                          
077600     PERFORM 5500-FIXED-ALLOC-ENGINE.                                     
077700                                                                          
077800     PERFORM 6300-SUBTRACT-PRIOR-RECEIPTS                                 
077900                                                                          
078000     IF RC809-AD-YES                                                      
078100        PERFORM VARYING PT-STR-IDX FROM 1 BY 1                            
078200            UNTIL PT-STR-IDX > PT-LAST-STR-IDX                            
078300               ADD PT-STR-ALLOC-AD-TOTALS (PT-STR-IDX)                    
078400                   TO PT-STR-ALLOC-TOTALS (PT-STR-IDX)                    
078500       END-PERFORM                                                        
078600     END-IF.                                                              
078700                                                                          
078800     IF RC809-CREATE-AUTO                                                 
078900         PERFORM 5000-ALLOCATE-STORES                                     
079000           VARYING PT-STR-IDX FROM 1 BY 1                                 
079100           UNTIL PT-STR-NBR (PT-STR-IDX) = ZERO                           
079200                OR PS-DONE                                                
079300                OR PT-STR-IDX > PC-MAX-STORES                             
079400* CLONE THE DRIVER LINE STORE DISTRO INTO THE OTHER LINES IN THE P        
079500* ONLY UNDER CERTAIN CONDITIONS ARE LINES CLONED AT THIS POINT.           
079600         IF (PV-PREPACK-ALLOC-STR-CNT > ZERO                              
079700                            AND PV-LEFT-TO-ALLOC > ZERO )                 
079800           OR                                                             
079900            (RC809-AD-YES                                                 
080000                            AND PV-LEFT-TO-ALLOC <= ZERO )                
080100               PERFORM 5200-CLONE-DRIVER-RECDIS                           
080200                 VARYING RC808-LINE-IDX FROM 1 BY 1                       
080300                 UNTIL RC808-PRPK-LNE-NBR(RC808-LINE-IDX) = ZERO          
080400                 OR PS-DONE                                               
080500         END-IF                                                           
080600     END-IF.                                                              
080700                                                                          
080800* POINT THE INDEX BACK TO THE DRIVER LINE OCCURRENCE                      
080900     SET RC808-LINE-IDX               TO RC809-DRIVER-LNE-PTR.            
081000/                                                                         
081100 4000-ALLOC-PREPACK.                                                      
081200                                                                          
081300     INITIALIZE PT-STORE-TABLE                                            
081400     MOVE 'M'                         TO PV-ALLOC-TYPE-FILTER             
081500     SET PT-STR-IDX                                                       
081600         PT-LAST-STR-IDX              TO 1                                
081700     MOVE ZERO                        TO PV-STR-COUNT                     
081800                            PV-SUM-STR-OTR-UNITS                          
081900     MOVE RC808-LNE-OTR-PACK-QTY (RC808-LINE-IDX)                         
082000                                      TO PV-TEMP-OTR-QTY                  
082100     PERFORM 6350-ADJUST-TEMP-OTR                                         
082200                                                                          
082300     PERFORM 1400-BUILD-STORE-ARRAY                                       
082400             VARYING RC808-STR-IDX FROM 1 BY 1                            
082500             UNTIL                                                        
082600             RC808-STR-NBR (RC808-LINE-IDX, RC808-STR-IDX) = ZERO         
082700             OR RC808-STR-IDX > PC-MAX-STORES.                            
082800                                                                          
082900     SET PT-STR-IDX                                                       
083000         PT-LAST-STR-IDX DOWN BY 1                                        
083100                                                                          
083200     PERFORM 5300-CALC-PRESENTATION-QTYS.                                 
083300                                                                          
083400     PERFORM 5400-ALLOC-ENGINE                                            
083500                                                                          
083600     PERFORM 6300-SUBTRACT-PRIOR-RECEIPTS                                 
083700                                                                          
083800     IF RC809-CREATE-AUTO                                                 
083900         PERFORM 5000-ALLOCATE-STORES                                     
084000           VARYING PT-STR-IDX FROM 1 BY 1                                 
084100           UNTIL PT-STR-NBR (PT-STR-IDX) = ZERO                           
084200                OR PS-DONE                                                
084300                OR PT-STR-IDX > PC-MAX-STORES                             
084400     END-IF.                                                              
084500                                                                          
084600                                                                          
084700 5000-ALLOCATE-STORES.                                                    
084800                                                                          
084900* ADJUST THE TOTAL OTR FOR THE LINE TO ALLOW FOR THE                      
085000* UNITS JUST ALLOCATED                                                    
085100                                                                          
085200*    SUBTRACT PT-STR-ALLOC-TOTALS(PT-STR-IDX)  FROM                       
085300*            RC808-LNE-OTR-QTY(RC808-LINE-IDX)                            
085400                                                                          
085500     PERFORM 5100-PREPARE-TRECDIS.                                        
085600                                                                          
085700     IF PS-NOT-DONE                                                       
085800         PERFORM 7000-INSERT-TRECDIS                                      
085900     END-IF.                                                              
086000                                                                          
086100 5100-PREPARE-TRECDIS.                                                    
086200                                                                          
086300     MOVE RC809-PO-NBR                TO RECDIS-ORD-NBR.                  
086400     MOVE RC808-PRPK-LNE-NBR(RC808-LINE-IDX)                              
086500                                      TO RECDIS-ORD-LNE-NBR.              
086600     MOVE RC809-REC-SEQ-NBR           TO RECDIS-REC-SEQ-NBR.              
086700     MOVE RC809-OPER-UNT-ID           TO RECDIS-OPER-UNT-ID.              
086800     MOVE RC809-NOTIF-UID             TO RECDIS-CRE-UID.                  
086900     MOVE 'A'                         TO RECDIS-VER-STATUS-CDE.           
087000     MOVE PT-STR-ALLOC-TOTALS(PT-STR-IDX)                                 
087100                                      TO RECDIS-EXPTED-ITM-QTY            
087200                                         RECDIS-VERIFY-ITM-QTY            
087300                                                                          
087400                                                                          
087500     IF RECDIS-EXPTED-ITM-QTY < ZERO                                      
087600         MOVE ZERO                    TO RECDIS-EXPTED-ITM-QTY            
087700     END-IF                                                               
087800                                                                          
087900     IF RECDIS-VERIFY-ITM-QTY < ZERO                                      
088000         MOVE ZERO                    TO RECDIS-VERIFY-ITM-QTY            
088100     END-IF                                                               
088200                                                                          
088300     ADD RECDIS-EXPTED-ITM-QTY        TO PV-TOTAL-RECDIS-ALLOC            
088400                                                                          
088500     MOVE PT-STR-NBR (PT-STR-IDX)     TO RECDIS-STR-NBR.                  
088600                                                                          
088700     SET RC808-STR-IDX    TO 1                                            
088800     SEARCH RC808-PACK-STORE-TABLE                                        
088900            WHEN RC808-STR-NBR                                            
089000                (RC808-LINE-IDX, RC808-STR-IDX)                           
089100                           = PT-STR-NBR(PT-STR-IDX)                       
089200                  IF RC808-STR-OTR-QTY                                    
089300                       (RC808-LINE-IDX, RC808-STR-IDX) > ZERO             
089400                     MOVE RC808-STR-OTR-QTY                               
089500                             (RC808-LINE-IDX, RC808-STR-IDX)              
089600                                    TO RECDIS-PLAN-ITM-QTY                
089700                  ELSE                                                    
089800                     MOVE ZERO      TO RECDIS-PLAN-ITM-QTY                
089900                  END-IF                                                  
090000     END-SEARCH.                                                          
090100                                                                          
090200     MOVE 1                           TO RECDIS-FCLTY-ID                  
090300                                         RECDIS-STR-FCLTY-ID.             
090400     MOVE ZERO                        TO                                  
090500                                   RECDIS-AP-VERIFY-ITM-QTY.              
090600                                                                          
090700                                                                          
090800     MOVE SPACES                      TO RECDIS-AP-PRC-CDE.               
090900     MOVE PC-DEFAULT-DATE             TO RECDIS-AP-PRC-DTE.               
091000/                                                                         
091100 5200-CLONE-DRIVER-RECDIS.                                                
091200     IF RC808-LINE-IDX = RC809-DRIVER-LNE-PTR                             
091300         CONTINUE                                                         
091400     ELSE                                                                 
091500         COMPUTE PV-FACTOR =                                              
091600                    RC808-RECITM-RATO-QTY (RC808-LINE-IDX)                
091700                           / RC808-RECITM-RATO-QTY                        
091800                                        (RC809-DRIVER-LNE-PTR)            
091900                                                                          
092000         MOVE RC808-PRPK-LNE-NBR(RC808-LINE-IDX)                          
092100                                      TO RECDIS-ORD-LNE-NBR               
092200                                                                          
092300         PERFORM VARYING PT-STR-IDX FROM 1 BY 1                           
092400               UNTIL PT-STR-NBR (PT-STR-IDX) = ZERO                       
092500                OR PT-STR-IDX > PC-MAX-STORES                             
092600                                                                          
092700                   MULTIPLY PT-STR-ALLOC-TOTALS(PT-STR-IDX)               
092800                   BY PV-FACTOR GIVING   RECDIS-EXPTED-ITM-QTY            
092900                                         ROUNDED                          
093000                                         RECDIS-VERIFY-ITM-QTY            
093100                                         ROUNDED                          
093200                                                                          
093300                 IF RECDIS-EXPTED-ITM-QTY < ZERO                          
093400                     MOVE ZERO        TO RECDIS-EXPTED-ITM-QTY            
093500                 END-IF                                                   
093600                                                                          
093700                 IF RECDIS-VERIFY-ITM-QTY < ZERO                          
093800                     MOVE ZERO        TO RECDIS-VERIFY-ITM-QTY            
093900                 END-IF                                                   
094000                                                                          
094100                                                                          
094200                                                                          
094300                 MOVE PT-STR-NBR (PT-STR-IDX)                             
094400                                      TO RECDIS-STR-NBR                   
094500                                                                          
094600                                                                          
094700                 SET RC808-STR-IDX    TO 1                                
094800                 SEARCH RC808-PACK-STORE-TABLE                            
094900                     WHEN RC808-STR-NBR                                   
095000                        (RC808-LINE-IDX, RC808-STR-IDX)                   
095100                       = PT-STR-NBR(PT-STR-IDX)                           
095200                            IF RC808-STR-OTR-QTY                          
095300                           (RC808-LINE-IDX, RC808-STR-IDX) > ZERO         
095400                                MOVE RC808-STR-OTR-QTY                    
095500                                  (RC808-LINE-IDX, RC808-STR-IDX)         
095600                                      TO RECDIS-PLAN-ITM-QTY              
095700                            ELSE                                          
095800                                MOVE ZERO                                 
095900                                      TO RECDIS-PLAN-ITM-QTY              
096000                            END-IF                                        
096100                 END-SEARCH                                               
096200                                                                          
096300                 PERFORM 7000-INSERT-TRECDIS                              
096400                                                                          
096500     END-PERFORM                                                          
096600                                                                          
096700                                                                          
096800     END-IF.                                                              
096900/                                                                         
097000 5300-CALC-PRESENTATION-QTYS.                                             
097100     MOVE ZERO                        TO PV-PRESENTATION-MAX              
097200                                         PV-GIVE-AWAY                     
097300                                         PV-TAKE-AWAY                     
097400     MOVE +999999999                  TO PV-PRESENTATION-MIN              
097500                                                                          
097600* MOVE EACH STORE'S PLAN QTY TO ITS ALLOCATION QTY                        
097700* COUNT THE TOTAL NUMBER OF STORES                                        
097800* ALSO FIGURE PRESENTATION MIN AND MAX ACROSS ALL STORES                  
097900                                                                          
098000     PERFORM VARYING PT-STR-IDX FROM 1 BY 1                               
098100             UNTIL PT-STR-NBR (PT-STR-IDX) = ZERO                         
098200                OR PT-STR-IDX > PC-MAX-STORES                             
098300         MOVE PT-STR-PLAN-QTY(PT-STR-IDX)                                 
098400                                      TO PT-STR-ALLOC-TOTALS              
098500                                         (PT-STR-IDX)                     
098600                                                                          
098700         IF PT-STR-CNSTR-QTY (PT-STR-IDX)                                 
098800                                        > PV-PRESENTATION-MAX             
098900             MOVE PT-STR-CNSTR-QTY (PT-STR-IDX)                           
099000                                      TO PV-PRESENTATION-MAX              
099100         END-IF                                                           
099200         IF PT-STR-CNSTR-QTY (PT-STR-IDX)                                 
099300                                        < PV-PRESENTATION-MIN             
099400             MOVE PT-STR-CNSTR-QTY (PT-STR-IDX)                           
099500                                      TO PV-PRESENTATION-MIN              
099600         END-IF                                                           
099700     END-PERFORM                                                          
099800                                                                          
099900* IF THE MAX IS ZERO THIS IMPLIES THAT THERE IS NO MAX                    
100000     IF PV-PRESENTATION-MAX = ZERO                                        
100100         MOVE 999999999               TO PV-PRESENTATION-MAX              
100200     END-IF.                                                              
100300/                                                                         
100400 5400-ALLOC-ENGINE.                                                       
100500* COMPARE THE QTY OF PACKS IN THE DISTRO TO THE OTR OF ALL THE            
100600* STORES                                                                  
100700*  IF THE QTY-TO-ALLOC EQUALS THE STORE DISTRO QTY, THEN                  
100800*  WE SKIP ALL THE TAKE-AWAY / GIVE-AWAY LOGIC AND WRITE THE              
100900*  RECDIS FILE WITH THEIR QTYS UNCHANGED                                  
101000                                                                          
101100     IF PV-LEFT-TO-ALLOC > PV-TEMP-OTR-QTY                                
101200*           (OVERAGE)                                                     
101300                                                                          
101400* WE CAN'T ALLOW THE OTR TO BE NEGATIVE HERE OTHERWISE                    
101500* PV-GIVE-AWAY WOULD BE TOO LARGE                                         
101600         IF PV-TEMP-OTR-QTY < ZERO                                        
101700             MOVE ZERO                TO PV-TEMP-OTR-QTY                  
101800         END-IF                                                           
101900                                                                          
102000         SUBTRACT PV-TEMP-OTR-QTY                                         
102100                     FROM PV-LEFT-TO-ALLOC                                
102200                     GIVING PV-GIVE-AWAY                                  
102300         PERFORM 6100-SORT-RANK-ASC                                       
102400                                                                          
102500* GIVE AWAY PACKS UNTIL ALL STORES ARE AT THEIR INDIVIDUAL                
102600* MINIMUMS OR YOU RUN OUT OF PACKS                                        
102700         MOVE ZERO                    TO PV-STRS-AT-MIN-CNT               
102800         PERFORM 5405-GIVEAWAY-TO-MIN                                     
102900                 UNTIL PV-GIVE-AWAY <= ZERO                               
103000                     OR PV-STRS-AT-MIN-CNT = PV-STR-COUNT                 
103100                                                                          
103200* IF THERE ARE PACKS LEFT TO GIVE AWAY, GIVE TO ALL STORES                
103300* UP TO THE PRESENTATION MAX                                              
103400                                                                          
103500         IF PV-GIVE-AWAY > ZERO                                           
103600             MOVE ZERO                TO PV-STRS-AT-MAX-CNT               
103700             PERFORM 5410-GIVEAWAY                                        
103800                     UNTIL PV-GIVE-AWAY <= ZERO                           
103900                         OR PV-STRS-AT-MAX-CNT = PV-STR-COUNT             
104000         END-IF                                                           
104100                                                                          
104200*  THERE ARE STILL MORE PACKS TO GIVE BUT ALL THE STORES ARE              
104300*  AT THE MAXIMUM.  GIVE TO ALL STORES EVENLY ABOVE THE MAX.              
104400                                                                          
104500         IF PV-GIVE-AWAY > ZERO                                           
104600             PERFORM 5420-GIVE-ABOVE-MAX                                  
104700                         UNTIL PV-GIVE-AWAY <= ZERO                       
104800         END-IF                                                           
104900                                                                          
105000     ELSE                                                                 
105100*           ( SHORTAGE OR EQUAL)                                          
105200                                                                          
105300         SUBTRACT PV-LEFT-TO-ALLOC FROM                                   
105400                         PV-TEMP-OTR-QTY                                  
105500                     GIVING PV-TAKE-AWAY                                  
105600                                                                          
105700         PERFORM 6000-SORT-RANK-DESC                                      
105800                                                                          
105900*  TAKE ONE PACK AT A TIME AWAY FROM THE STORES UNTIL                     
106000*  THERE ARE NO STORES ABOVE THEIR INDIVIDUAL MINIMUMS OR                 
106100*  YOU RUN OUT OF PACKS TO TAKE AWAY                                      
106200                                                                          
106300         MOVE ZERO                    TO PV-STRS-AT-MIN-CNT               
106400         PERFORM 5430-TAKE-AWAY-TO-MIN                                    
106500                     UNTIL PV-TAKE-AWAY <= ZERO                           
106600                     OR PV-STRS-AT-MIN-CNT >= PV-STR-COUNT                
106700                                                                          
106800*  THERE ARE STILL MORE PACKS TO REMOVE BUT ALL THE STORES ARE            
106900*  AT THEIR INDIVIDUAL MINIMUMS.  THE NEXT STEP IS TO TAKE AWAY           
107000*  PACKS UNTIL ALL STORES ARE AT THE PRESENTATION MINIMUM OR              
107100*  OR YOU RUN OUT OF PACKS TO TAKE AWAY                                   
107200                                                                          
107300         MOVE ZERO                    TO PV-STRS-AT-MIN-CNT               
107400         IF PV-TAKE-AWAY > ZERO                                           
107500             PERFORM 5435-TAKE-TO-PRES-MIN                                
107600                         UNTIL                                            
107700                 PV-STRS-AT-MIN-CNT = PV-STR-COUNT                        
107800                 OR PS-TAKEAWAY-DONE                                      
107900         END-IF                                                           
108000                                                                          
108100*  IF ALL STORES ARE AT THE PRESENTATION MINIMUM AND THERE ARE            
108200*  STILL PACKS TO REMOVE, CONTINUE TAKING AWAY PACKS BENEATH              
108300*  THE MINIMUM                                                            
108400*                                                                         
108500         MOVE ZERO                    TO PV-STRS-AT-MIN-CNT               
108600         IF PV-TAKE-AWAY > ZERO                                           
108700             PERFORM 5440-TAKE-BELOW-MIN                                  
108800                         UNTIL PV-TAKE-AWAY <= ZERO                       
108900         END-IF                                                           
109000     END-IF.                                                              
109100/                                                                         
109200 5405-GIVEAWAY-TO-MIN.                                                    
109300     MOVE ZERO                        TO PV-STRS-AT-MIN-CNT               
109400     PERFORM 5406-GIVE-TO-MIN                                             
109500                VARYING PT-STR-IDX FROM 1 BY 1                            
109600                 UNTIL PT-STR-NBR (PT-STR-IDX) = ZERO                     
109700                    OR PS-GIVEAWAY-DONE                                   
109800                    OR PV-GIVE-AWAY <= ZERO                               
109900                    OR PT-STR-IDX > PC-MAX-STORES.                        
110000                                                                          
110100 5406-GIVE-TO-MIN.                                                        
110200     IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) <                                
110300                                PT-STR-CNSTR-QTY (PT-STR-IDX)             
110400         ADD PV-DRIVER-RATIO-QTY      TO                                  
110500                 PT-STR-ALLOC-TOTALS (PT-STR-IDX)                         
110600         SUBTRACT PV-DRIVER-RATIO-QTY FROM PV-GIVE-AWAY                   
110700         IF PV-GIVE-AWAY <= ZERO                                          
110800             SET PS-GIVEAWAY-DONE    TO TRUE                              
110900         ELSE                                                             
111000             IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) <                        
111100                                PT-STR-CNSTR-QTY (PT-STR-IDX)             
111200                 CONTINUE                                                 
111300             ELSE                                                         
111400                 ADD 1                TO PV-STRS-AT-MIN-CNT               
111500             END-IF                                                       
111600         END-IF                                                           
111700     ELSE                                                                 
111800         ADD 1                        TO PV-STRS-AT-MIN-CNT               
111900     END-IF.                                                              
112000                                                                          
112100 5410-GIVEAWAY.                                                           
112200     MOVE ZERO                        TO PV-STRS-AT-MAX-CNT               
112300     PERFORM 5411-GIVE-TO-MAX                                             
112400                VARYING PT-STR-IDX FROM 1 BY 1                            
112500                 UNTIL PT-STR-NBR (PT-STR-IDX) = ZERO                     
112600                    OR PS-GIVEAWAY-DONE                                   
112700                    OR PV-GIVE-AWAY <= ZERO                               
112800                    OR PT-STR-IDX > PC-MAX-STORES.                        
112900                                                                          
113000 5411-GIVE-TO-MAX.                                                        
113100     IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) < PV-PRESENTATION-MAX            
113200         ADD PV-DRIVER-RATIO-QTY      TO                                  
113300                 PT-STR-ALLOC-TOTALS (PT-STR-IDX)                         
113400         SUBTRACT PV-DRIVER-RATIO-QTY FROM PV-GIVE-AWAY                   
113500         IF PV-GIVE-AWAY <= ZERO                                          
113600             SET PS-GIVEAWAY-DONE    TO TRUE                              
113700         ELSE                                                             
113800             IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) <                        
113900                                      PV-PRESENTATION-MAX                 
114000                 CONTINUE                                                 
114100             ELSE                                                         
114200                 ADD 1                TO PV-STRS-AT-MAX-CNT               
114300             END-IF                                                       
114400         END-IF                                                           
114500     ELSE                                                                 
114600         IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) >=                           
114700                           PV-PRESENTATION-MAX                            
114800             ADD 1                    TO PV-STRS-AT-MAX-CNT               
114900     END-IF.                                                              
115000                                                                          
115100 5420-GIVE-ABOVE-MAX.                                                     
115200     PERFORM 5421-GIVE-ONE-PACK                                           
115300                VARYING PT-STR-IDX FROM 1 BY 1                            
115400                 UNTIL PT-STR-NBR (PT-STR-IDX) = ZERO                     
115500                    OR PV-GIVE-AWAY <= ZERO                               
115600                    OR PT-STR-IDX > PC-MAX-STORES.                        
115700                                                                          
115800 5421-GIVE-ONE-PACK.                                                      
115900     ADD PV-DRIVER-RATIO-QTY          TO                                  
116000                      PT-STR-ALLOC-TOTALS (PT-STR-IDX)                    
116100     SUBTRACT PV-DRIVER-RATIO-QTY     FROM PV-GIVE-AWAY.                  
116200                                                                          
116300 5430-TAKE-AWAY-TO-MIN.                                                   
116400     MOVE ZERO                        TO PV-STRS-AT-MIN-CNT               
116500     PERFORM 5431-TAKE-ONE-PACK                                           
116600                VARYING PT-STR-IDX FROM 1 BY 1                            
116700                 UNTIL PS-TAKEAWAY-DONE                                   
116800                    OR PT-STR-NBR (PT-STR-IDX) = ZERO                     
116900                    OR PT-STR-IDX > PC-MAX-STORES.                        
117000                                                                          
117100 5431-TAKE-ONE-PACK.                                                      
117200     IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) >                                
117300                                PT-STR-CNSTR-QTY (PT-STR-IDX)             
117400         SUBTRACT PV-DRIVER-RATIO-QTY FROM                                
117500                 PT-STR-ALLOC-TOTALS (PT-STR-IDX)                         
117600                 PV-TAKE-AWAY                                             
117700         IF PV-TAKE-AWAY <= ZERO                                          
117800             SET PS-TAKEAWAY-DONE    TO TRUE                              
117900         ELSE                                                             
118000             IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) >                        
118100                                PT-STR-CNSTR-QTY (PT-STR-IDX)             
118200                 CONTINUE                                                 
118300             ELSE                                                         
118400                 ADD 1                TO PV-STRS-AT-MIN-CNT               
118500             END-IF                                                       
118600         END-IF                                                           
118700     ELSE                                                                 
118800         ADD 1                        TO PV-STRS-AT-MIN-CNT               
118900     END-IF.                                                              
119000                                                                          
119100 5435-TAKE-TO-PRES-MIN.                                                   
119200     MOVE ZERO                        TO PV-STRS-AT-MIN-CNT               
119300     PERFORM 5436-TAKE-ONE-PACK                                           
119400                VARYING PT-STR-IDX FROM 1 BY 1                            
119500                 UNTIL PS-TAKEAWAY-DONE                                   
119600                    OR PT-STR-NBR (PT-STR-IDX) = ZERO                     
119700                    OR PT-STR-IDX > PC-MAX-STORES.                        
119800                                                                          
119900 5436-TAKE-ONE-PACK.                                                      
120000     IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) >                                
120100                           PV-PRESENTATION-MIN                            
120200         SUBTRACT PV-DRIVER-RATIO-QTY FROM                                
120300                 PT-STR-ALLOC-TOTALS (PT-STR-IDX)                         
120400                 PV-TAKE-AWAY                                             
120500         IF PV-TAKE-AWAY <= ZERO                                          
120600             SET PS-TAKEAWAY-DONE    TO TRUE                              
120700         ELSE                                                             
120800             IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) >                        
120900                           PV-PRESENTATION-MIN                            
121000                 CONTINUE                                                 
121100             ELSE                                                         
121200                 ADD 1                TO PV-STRS-AT-MIN-CNT               
121300             END-IF                                                       
121400         END-IF                                                           
121500     ELSE                                                                 
121600         IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) <=                           
121700                           PV-PRESENTATION-MIN                            
121800             ADD 1                    TO PV-STRS-AT-MIN-CNT               
121900     END-IF.                                                              
122000                                                                          
122100 5440-TAKE-BELOW-MIN.                                                     
122200     PERFORM 5441-TAKE-ONE-PACK                                           
122300                VARYING PT-STR-IDX FROM 1 BY 1                            
122400                 UNTIL PS-TAKEAWAY-DONE                                   
122500                    OR PT-STR-NBR (PT-STR-IDX) = ZERO                     
122600                    OR PT-STR-IDX > PC-MAX-STORES.                        
122700                                                                          
122800 5441-TAKE-ONE-PACK.                                                      
122900     SUBTRACT PV-DRIVER-RATIO-QTY FROM                                    
123000             PT-STR-ALLOC-TOTALS (PT-STR-IDX)                             
123100             PV-TAKE-AWAY                                                 
123200                                                                          
123300     IF PV-TAKE-AWAY <= ZERO                                              
123400         SET PS-TAKEAWAY-DONE         TO TRUE                             
123500     END-IF.                                                              
123600/                                                                         
123700 5500-FIXED-ALLOC-ENGINE.                                                 
123800* COMPARE THE QTY OF PACKS IN THE DISTRO TO THE OTR OF ALL THE            
123900* STORES                                                                  
124000*  IF THE QTY-TO-ALLOC EQUALS THE STORE DISTRO QTY, THEN                  
124100*  WE SKIP ALL THE TAKE-AWAY / GIVE-AWAY LOGIC AND WRITE THE              
124200*  RECDIS FILE WITH THEIR QTYS UNCHANGED                                  
124300                                                                          
124400     IF PV-LEFT-TO-ALLOC > PV-TEMP-OTR-QTY                                
124500*           (OVERAGE)                                                     
124600                                                                          
124700* WE CAN'T ALLOW THE OTR TO BE NEGATIVE HERE OTHERWISE                    
124800* PV-GIVE-AWAY WOULD BE TOO LARGE                                         
124900         IF PV-TEMP-OTR-QTY < ZERO                                        
125000             MOVE ZERO                TO PV-TEMP-OTR-QTY                  
125100         END-IF                                                           
125200                                                                          
125300         SUBTRACT PV-TEMP-OTR-QTY                                         
125400                     FROM PV-LEFT-TO-ALLOC                                
125500                     GIVING PV-GIVE-AWAY                                  
125600                                                                          
125700         PERFORM 6100-SORT-RANK-ASC                                       
125800         MOVE ZERO                    TO PV-STRS-AT-MAX-CNT               
125900         PERFORM 5510-GIVEAWAY                                            
126000                 UNTIL PV-GIVE-AWAY <= ZERO                               
126100                     OR PV-STRS-AT-MAX-CNT = PV-STR-COUNT                 
126200                                                                          
126300*  THERE ARE STILL MORE PACKS TO GIVE BUT ALL THE STORES ARE              
126400*  AT THEIR MAXIMUM                                                       
126500                                                                          
126600*  IF THERE ARE PREPACK STORES THEN WE MUST QUIT ALLOCATING               
126700*  THE FIXED STORES IN ORDER TO HAVE PACKS LEFT FOR THEM                  
126800         IF PV-PREPACK-ALLOC-STR-CNT > ZERO                               
126900             CONTINUE                                                     
127000         ELSE                                                             
127100             IF PV-GIVE-AWAY > ZERO                                       
127200                 PERFORM 5520-GIVE-ABOVE-MAX                              
127300                         UNTIL PV-GIVE-AWAY <= ZERO                       
127400             END-IF                                                       
127500         END-IF                                                           
127600                                                                          
127700     ELSE                                                                 
127800*           ( SHORTAGE OR EQUAL)                                          
127900                                                                          
128000         SUBTRACT PV-LEFT-TO-ALLOC FROM                                   
128100                         PV-TEMP-OTR-QTY                                  
128200                     GIVING PV-TAKE-AWAY                                  
128300                                                                          
128400         PERFORM 6000-SORT-RANK-DESC                                      
128500                                                                          
128600*  TAKE ONE PACK AT A TIME AWAY FROM THE STORES UNTIL                     
128700*  THERE ARE NO STORES ABOVE THE PRESENTATION CONSTRAINT OR               
128800*  YOU RUN OUT OF PACKS TO TAKE AWAY                                      
128900                                                                          
129000         MOVE ZERO                    TO PV-STRS-AT-MIN-CNT               
129100         PERFORM 5530-TAKE-AWAY-TO-MIN                                    
129200                     UNTIL PV-TAKE-AWAY <= ZERO                           
129300                     OR PV-STRS-AT-MIN-CNT >= PV-STR-COUNT                
129400                                                                          
129500*  THERE ARE STILL MORE PACKS TO REMOVE BUT ALL THE STORES ARE            
129600*  AT THE MINIMUM                                                         
129700                                                                          
129800         IF PV-TAKE-AWAY > ZERO                                           
129900             PERFORM 5540-TAKE-BELOW-MIN                                  
130000                 UNTIL PV-TAKE-AWAY <= ZERO                               
130100         END-IF                                                           
130200     END-IF.                                                              
130300/                                                                         
130400 5510-GIVEAWAY.                                                           
130500     MOVE ZERO                        TO PV-STRS-AT-MAX-CNT               
130600     PERFORM 5511-GIVE-TO-MAX                                             
130700                VARYING PT-STR-IDX FROM 1 BY 1                            
130800                 UNTIL PT-STR-NBR (PT-STR-IDX) = ZERO                     
130900                    OR PS-GIVEAWAY-DONE                                   
131000                    OR PV-GIVE-AWAY <= ZERO                               
131100                    OR PT-STR-IDX > PC-MAX-STORES.                        
131200                                                                          
131300 5511-GIVE-TO-MAX.                                                        
131400     IF (PT-STR-ALLOC-TOTALS (PT-STR-IDX) +                               
131500         PT-STR-ALLOC-AD-TOTALS (PT-STR-IDX))                             
131600                              < PT-STR-CNSTR-QTY (PT-STR-IDX)             
131700         ADD PV-DRIVER-RATIO-QTY      TO                                  
131800                 PT-STR-ALLOC-TOTALS (PT-STR-IDX)                         
131900         SUBTRACT PV-DRIVER-RATIO-QTY FROM PV-GIVE-AWAY                   
132000         IF PV-GIVE-AWAY <= ZERO                                          
132100             SET PS-GIVEAWAY-DONE    TO TRUE                              
132200         ELSE                                                             
132300             IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) <                        
132400                                PT-STR-CNSTR-QTY (PT-STR-IDX)             
132500                 CONTINUE                                                 
132600             ELSE                                                         
132700                 ADD 1                TO PV-STRS-AT-MAX-CNT               
132800             END-IF                                                       
132900         END-IF                                                           
133000     ELSE                                                                 
133100         ADD 1                        TO PV-STRS-AT-MAX-CNT               
133200     END-IF.                                                              
133300                                                                          
133400 5520-GIVE-ABOVE-MAX.                                                     
133500     PERFORM 5521-GIVE-ONE-PACK                                           
133600                VARYING PT-STR-IDX FROM 1 BY 1                            
133700                 UNTIL PT-STR-NBR (PT-STR-IDX) = ZERO                     
133800                    OR PV-GIVE-AWAY <= ZERO                               
133900                    OR PT-STR-IDX > PC-MAX-STORES.                        
134000                                                                          
134100 5521-GIVE-ONE-PACK.                                                      
134200     ADD PV-DRIVER-RATIO-QTY          TO                                  
134300                      PT-STR-ALLOC-TOTALS (PT-STR-IDX)                    
134400     SUBTRACT PV-DRIVER-RATIO-QTY     FROM PV-GIVE-AWAY.                  
134500                                                                          
134600 5530-TAKE-AWAY-TO-MIN.                                                   
134700     MOVE ZERO                        TO PV-STRS-AT-MIN-CNT               
134800     PERFORM 5531-TAKE-ONE-PACK                                           
134900                VARYING PT-STR-IDX FROM 1 BY 1                            
135000                 UNTIL PS-TAKEAWAY-DONE                                   
135100                    OR PT-STR-NBR (PT-STR-IDX) = ZERO                     
135200                    OR PT-STR-IDX > PC-MAX-STORES.                        
135300                                                                          
135400 5531-TAKE-ONE-PACK.                                                      
135500     IF (PT-STR-ALLOC-TOTALS (PT-STR-IDX) +                               
135600         PT-STR-ALLOC-AD-TOTALS (PT-STR-IDX))                             
135700                         > PT-STR-CNSTR-QTY (PT-STR-IDX)                  
135800         SUBTRACT PV-DRIVER-RATIO-QTY FROM                                
135900                 PT-STR-ALLOC-TOTALS (PT-STR-IDX)                         
136000                 PV-TAKE-AWAY                                             
136100         IF PV-TAKE-AWAY <= ZERO                                          
136200             SET PS-TAKEAWAY-DONE    TO TRUE                              
136300         ELSE                                                             
136400             IF (PT-STR-ALLOC-TOTALS (PT-STR-IDX) +                       
136500                 PT-STR-ALLOC-AD-TOTALS (PT-STR-IDX))                     
136600                            > PT-STR-CNSTR-QTY (PT-STR-IDX)               
136700                 CONTINUE                                                 
136800             ELSE                                                         
136900                 ADD 1                TO PV-STRS-AT-MIN-CNT               
137000             END-IF                                                       
137100         END-IF                                                           
137200     ELSE                                                                 
137300         ADD 1                        TO PV-STRS-AT-MIN-CNT               
137400     END-IF.                                                              
137500                                                                          
137600 5540-TAKE-BELOW-MIN.                                                     
137700     PERFORM 5541-TAKE-ONE-PACK                                           
137800                VARYING PT-STR-IDX FROM 1 BY 1                            
137900                 UNTIL PS-TAKEAWAY-DONE                                   
138000                    OR PT-STR-NBR (PT-STR-IDX) = ZERO                     
138100                    OR PT-STR-IDX > PC-MAX-STORES.                        
138200                                                                          
138300 5541-TAKE-ONE-PACK.                                                      
138400     IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) > ZERO                           
138500         SUBTRACT PV-DRIVER-RATIO-QTY FROM                                
138600                 PT-STR-ALLOC-TOTALS (PT-STR-IDX)                         
138700                 PV-TAKE-AWAY                                             
138800     END-IF                                                               
138900                                                                          
139000     IF PV-TAKE-AWAY <= ZERO                                              
139100         SET PS-TAKEAWAY-DONE         TO TRUE                             
139200     END-IF.                                                              
139300/                                                                         
139400*************************************************************             
139500* COMMON SORT ROUTINES                                                    
139600*************************************************************             
139700 6000-SORT-RANK-DESC.                                                     
139800                                                                          
139900     SET PT-STR-IDX-1                 TO 1                                
140000                                                                          
140100* PT-LAST-STR-IDX MARKS THE LAST STORE IN THE WORK TABLE                  
140200     PERFORM 6010-BUBBLE-SORT                                             
140300       UNTIL PT-STR-IDX-1 NOT < PT-LAST-STR-IDX.                          
140400                                                                          
140500                                                                          
140600 6010-BUBBLE-SORT.                                                        
140700                                                                          
140800     SET PT-STR-IDX-2                 TO PT-STR-IDX-1                     
140900     SET PT-STR-IDX-2 UP BY 1                                             
141000                                                                          
141100     PERFORM 6011-LOOP-BUBBLE-SORT                                        
141200       UNTIL PT-STR-IDX-2 > PT-LAST-STR-IDX.                              
141300                                                                          
141400     SET PT-STR-IDX-1       UP BY 1.                                      
141500                                                                          
141600 6011-LOOP-BUBBLE-SORT.                                                   
141700*    SORTS THE STORES TABLE IN DESCENDING ORDER                           
141800*    BY STORE RANK                                                        
141900                                                                          
142000     MOVE PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1)                             
142100                                      TO PV-SORT-ALLOC-CDE-1              
142200     MOVE PT-STR-RNK-SEQ-NBR(PT-STR-IDX-1)                                
142300                                      TO PV-SORT-RANK-1                   
142400     MOVE PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-2)                             
142500                                      TO PV-SORT-ALLOC-CDE-2              
142600     MOVE PT-STR-RNK-SEQ-NBR(PT-STR-IDX-2)                                
142700                                      TO PV-SORT-RANK-2                   
142800                                                                          
142900     IF PV-SORT-KEY-1 <  PV-SORT-KEY-2                                    
143000         MOVE PT-STORE-DATA  (PT-STR-IDX-1)                               
143100                                      TO PV-TEMP                          
143200         MOVE PT-STORE-DATA  (PT-STR-IDX-2)                               
143300                                      TO                                  
143400              PT-STORE-DATA  (PT-STR-IDX-1)                               
143500         MOVE PV-TEMP                 TO                                  
143600              PT-STORE-DATA  (PT-STR-IDX-2)                               
143700     END-IF.                                                              
143800                                                                          
143900     SET PT-STR-IDX-2 UP BY 1.                                            
144000                                                                          
144100/                                                                         
144200 6100-SORT-RANK-ASC.                                                      
144300                                                                          
144400     SET PT-STR-IDX-1                 TO 1.                               
144500                                                                          
144600*                                                                         
144700     PERFORM 6110-BUBBLE-SORT                                             
144800       UNTIL PT-STR-IDX-1 NOT < PT-LAST-STR-IDX.                          
144900                                                                          
145000                                                                          
145100 6110-BUBBLE-SORT.                                                        
145200*    THIS PARAGRAPH CALLS 3095 TO SORT THE STORES TABLE.                  
145300                                                                          
145400     SET PT-STR-IDX-2                 TO PT-STR-IDX-1                     
145500     SET PT-STR-IDX-2  UP BY 1                                            
145600                                                                          
145700     PERFORM 6111-LOOP-BUBBLE-SORT                                        
145800       UNTIL PT-STR-IDX-2 > PT-LAST-STR-IDX.                              
145900                                                                          
146000     SET PT-STR-IDX-1                 UP BY 1.                            
146100                                                                          
146200 6111-LOOP-BUBBLE-SORT.                                                   
146300*    THIS PARAGRAPH SORTS THE STORES TABLE IN ASCENDING ORDER             
146400*    BY STORE RANK                                                        
146500                                                                          
146600     MOVE PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1)                             
146700                                      TO PV-SORT-ALLOC-CDE-1              
146800     MOVE PT-STR-RNK-SEQ-NBR(PT-STR-IDX-1)                                
146900                                      TO PV-SORT-RANK-1                   
147000     MOVE PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-2)                             
147100                                      TO PV-SORT-ALLOC-CDE-2              
147200     MOVE PT-STR-RNK-SEQ-NBR(PT-STR-IDX-2)                                
147300                                      TO PV-SORT-RANK-2                   
147400                                                                          
147500     IF PV-SORT-KEY-1 >  PV-SORT-KEY-2                                    
147600         MOVE PT-STORE-DATA  (PT-STR-IDX-1)                               
147700                                      TO PV-TEMP                          
147800         MOVE PT-STORE-DATA  (PT-STR-IDX-2)                               
147900                                      TO                                  
148000              PT-STORE-DATA  (PT-STR-IDX-1)                               
148100         MOVE PV-TEMP                 TO                                  
148200              PT-STORE-DATA  (PT-STR-IDX-2)                               
148300     END-IF.                                                              
148400                                                                          
148500     SET PT-STR-IDX-2 UP BY 1.                                            
148600                                                                          
148700 6200-SORT-OTR-DESC.                                                      
148800                                                                          
148900     SET PT-STR-IDX-1                 TO 1.                               
149000                                                                          
149100*                                                                         
149200     PERFORM 6210-BUBBLE-SORT                                             
149300       UNTIL PT-STR-IDX-1 NOT < PT-LAST-STR-IDX.                          
149400                                                                          
149500                                                                          
149600 6210-BUBBLE-SORT.                                                        
149700                                                                          
149800     SET PT-STR-IDX-2                 TO PT-STR-IDX-1                     
149900     SET PT-STR-IDX-2  UP BY 1                                            
150000                                                                          
150100     PERFORM 6211-LOOP-BUBBLE-SORT                                        
150200       UNTIL PT-STR-IDX-2 > PT-LAST-STR-IDX.                              
150300                                                                          
150400     SET PT-STR-IDX-1                 UP BY 1.                            
150500                                                                          
150600 6211-LOOP-BUBBLE-SORT.                                                   
150700*    THIS PARAGRAPH SORTS THE STORES TABLE IN DESCENDING ORDER            
150800*    BY STORE OTR THEN STORE NUMBER                                       
150900                                                                          
151000     IF PT-STR-OTR-TOTALS (PT-STR-IDX-1)                                  
151100                       < PT-STR-OTR-TOTALS(PT-STR-IDX-2)                  
151200                              OR                                          
151300                          (PT-STR-OTR-TOTALS(PT-STR-IDX-1)                
151400                          = PT-STR-OTR-TOTALS(PT-STR-IDX-2)               
151500                              AND                                         
151600               PT-STR-NBR (PT-STR-IDX-1)                                  
151700                           > PT-STR-NBR (PT-STR-IDX-2))                   
151800                                                                          
151900         MOVE PT-STORE-DATA (PT-STR-IDX-1)                                
152000                                      TO PV-TEMP                          
152100         MOVE PT-STORE-DATA (PT-STR-IDX-2)                                
152200                                      TO                                  
152300                            PT-STORE-DATA (PT-STR-IDX-1)                  
152400         MOVE PV-TEMP                 TO                                  
152500                            PT-STORE-DATA (PT-STR-IDX-2)                  
152600     END-IF.                                                              
152700                                                                          
152800     SET PT-STR-IDX-2 UP BY 1.                                            
152900/                                                                         
153000 6300-SUBTRACT-PRIOR-RECEIPTS.                                            
153100* SUBTRACT PRIOR RECEIPTS FROM THE CURRENT ALLOCATIONS                    
153200     PERFORM VARYING PT-STR-IDX FROM 1 BY 1                               
153300                 UNTIL PT-STR-IDX > PT-LAST-STR-IDX                       
153400                                                                          
153500         IF PT-STR-OTR-TOTALS (PT-STR-IDX) < ZERO                         
153600             MOVE ZERO                TO                                  
153700                  PT-STR-OTR-TOTALS (PT-STR-IDX)                          
153800         END-IF                                                           
153900                                                                          
154000         COMPUTE PT-STR-ALLOC-TOTALS (PT-STR-IDX) =                       
154100                 PT-STR-ALLOC-TOTALS (PT-STR-IDX)                         
154200              - (PT-STR-PLAN-QTY (PT-STR-IDX) -                           
154300                 PT-STR-OTR-TOTALS (PT-STR-IDX) )                         
154400                                                                          
154500         SUBTRACT PT-STR-ALLOC-TOTALS (PT-STR-IDX)                        
154600             FROM PV-LEFT-TO-ALLOC                                        
154700                                                                          
154800     END-PERFORM.                                                         
154900/                                                                         
155000 6310-SUBTRACT-PRIOR-RECEIPTS.                                            
155100* SUBTRACT PRIOR RECEIPTS FROM THE CURRENT ALLOCATIONS                    
155200     PERFORM VARYING PT-STR-IDX FROM 1 BY 1                               
155300                 UNTIL PT-STR-IDX > PT-LAST-STR-IDX                       
155400                                                                          
155500         IF PT-STR-OTR-TOTALS (PT-STR-IDX) < ZERO                         
155600             MOVE ZERO                TO                                  
155700                  PT-STR-OTR-TOTALS (PT-STR-IDX)                          
155800         END-IF                                                           
155900                                                                          
156000* ONLY SUBTRACT THE PRIOR RECIPTS IF THE STORE RECEIVED AT                
156100* LEAST 1 PACK.                                                           
156200         IF PT-STR-ALLOC-TOTALS (PT-STR-IDX) >  ZERO                      
156300            COMPUTE PT-STR-ALLOC-TOTALS (PT-STR-IDX) =                    
156400                    PT-STR-ALLOC-TOTALS (PT-STR-IDX)                      
156500                     - (PT-STR-PLAN-QTY (PT-STR-IDX) -                    
156600                    PT-STR-OTR-TOTALS (PT-STR-IDX) )                      
156700         END-IF                                                           
156800                                                                          
156900     END-PERFORM.                                                         
157000                                                                          
157100 6350-ADJUST-TEMP-OTR.                                                    
157200                                                                          
157300     MULTIPLY PV-DRIVER-RATIO-QTY BY PV-TEMP-OTR-QTY                      
157400              GIVING PV-WHOLE-UNITS                                       
157500                                                                          
157600     MOVE PV-WHOLE-UNITS              TO PV-TEMP-OTR-QTY.                 
157700                                                                          
157800 6351-ADJUST-CNSTR-QTY.                                                   
157900                                                                          
158000     IF PT-STR-CNSTR-QTY (PT-STR-IDX) > PV-DRIVER-RATIO-QTY               
158100         DIVIDE PT-STR-CNSTR-QTY (PT-STR-IDX)                             
158200             BY PV-DRIVER-RATIO-QTY                                       
158300             GIVING PV-WHOLE-UNITS                                        
158400     ELSE                                                                 
158500         MOVE 1                       TO PV-WHOLE-UNITS                   
158600     END-IF                                                               
158700                                                                          
158800     MULTIPLY PV-DRIVER-RATIO-QTY BY PV-WHOLE-UNITS                       
158900     MOVE PV-WHOLE-UNITS              TO                                  
159000                            PT-STR-CNSTR-QTY (PT-STR-IDX).                
159100                                                                          
159200*                                                                         
159300*6352-ADJUST-STR-PLAN-QTY.                                                
159400*                                                                         
159500*    IF PT-STR-PLAN-QTY (PT-STR-IDX) > PV-DRIVER-PUR-RATIO-QTY            
159600*        DIVIDE PT-STR-PLAN-QTY (PT-STR-IDX)                              
159700*            BY     PV-DRIVER-PUR-RATIO-QTY                               
159800*            GIVING PV-WHOLE-UNITS                                        
159900*    ELSE                                                                 
160000*        MOVE 1                       TO PV-WHOLE-UNITS                   
160100*    END-IF                                                               
160200*                                                                         
160300*    MULTIPLY PV-DRIVER-RATIO-QTY                                         
160400*             BY PV-WHOLE-UNITS                                           
160500*    MOVE PV-WHOLE-UNITS              TO                                  
160600*                           PT-STR-PLAN-QTY (PT-STR-IDX).                 
160700                                                                          
160800 6353-ADJUST-DISTRO-QTY.                                                  
160900                                                                          
161000     IF RC808-STR-DISTRO-QTY (RC808-LINE-IDX, RC808-STR-IDX)              
161100                 >  PV-DRIVER-PUR-RATIO-QTY                               
161200                                                                          
161300         DIVIDE RC808-STR-DISTRO-QTY                                      
161400                          (RC808-LINE-IDX, RC808-STR-IDX)                 
161500             BY     PV-DRIVER-PUR-RATIO-QTY                               
161600                                                                          
161700             GIVING PV-WHOLE-UNITS                                        
161800     ELSE                                                                 
161900         MOVE 1                       TO PV-WHOLE-UNITS                   
162000     END-IF                                                               
162100                                                                          
162200     MULTIPLY PV-DRIVER-RATIO-QTY BY PV-WHOLE-UNITS                       
162300                                                                          
162400     MOVE PV-WHOLE-UNITS              TO                                  
162500         RC808-STR-DISTRO-QTY (RC808-LINE-IDX, RC808-STR-IDX).            
162600*                                                                         
162700*6354-ADJUST-STR-OTR.                                                     
162800*    IF PT-STR-OTR-TOTALS (PT-STR-IDX) > PV-DRIVER-RATIO-QTY              
162900*        DIVIDE PT-STR-OTR-TOTALS (PT-STR-IDX)                            
163000*            BY     PV-DRIVER-RATIO-QTY                                   
163100*            GIVING PV-WHOLE-UNITS                                        
163200*    ELSE                                                                 
163300*        MOVE 1                       TO PV-WHOLE-UNITS                   
163400*    END-IF                                                               
163500*                                                                         
163600*    MULTIPLY PV-DRIVER-RATIO-QTY BY PV-WHOLE-UNITS                       
163700*    MOVE PV-WHOLE-UNITS              TO PT-STR-OTR-TOTALS                
163800*                                        (PT-STR-IDX).                    
163900/                                                                         
164000 7000-INSERT-TRECDIS.                                                     
164100******************************************************************        
164200*    THIS PARAGRAPH ADDS A RECDIS RECORD                                  
164300******************************************************************        
164400                                                                          
164500     EXEC SQL                                                             
164600         INSERT INTO TRECDIS                                              
164700             (ORD_NBR                                                     
164800           ,  ORD_LNE_NBR                                                 
164900           ,  REC_SEQ_NBR                                                 
165000           ,  OPER_UNT_ID                                                 
165100           ,  FCLTY_ID                                                    
165200           ,  CRE_TMST                                                    
165300           ,  CRE_UID                                                     
165400           ,  VER_STATUS_CDE                                              
165500           ,  EXPTED_ITM_QTY                                              
165600           ,  VERIFY_ITM_QTY                                              
165700           ,  AP_VERIFY_ITM_QTY                                           
165800           ,  STR_NBR                                                     
165900           ,  STR_FCLTY_ID                                                
166000           ,  PLAN_ITM_QTY                                                
166100           ,  AP_PRC_CDE                                                  
166200           ,  AP_PRC_DTE)                                                 
166300     VALUES (:RECDIS-ORD-NBR                                              
166400           ,  :RECDIS-ORD-LNE-NBR                                         
166500           ,  :RECDIS-REC-SEQ-NBR                                         
166600           ,  :RECDIS-OPER-UNT-ID                                         
166700           ,  :RECDIS-FCLTY-ID                                            
166800           ,  CURRENT TIMESTAMP                                           
166900           ,  :RECDIS-CRE-UID                                             
167000           ,  :RECDIS-VER-STATUS-CDE                                      
167100           ,  :RECDIS-EXPTED-ITM-QTY                                      
167200           ,  :RECDIS-VERIFY-ITM-QTY                                      
167300           ,  :RECDIS-AP-VERIFY-ITM-QTY                                   
167400           ,  :RECDIS-STR-NBR                                             
167500           ,  :RECDIS-STR-FCLTY-ID                                        
167600           ,  :RECDIS-PLAN-ITM-QTY                                        
167700           ,  :RECDIS-AP-PRC-CDE                                          
167800           ,  :RECDIS-AP-PRC-DTE)                                         
167900     END-EXEC                                                             
168000                                                                          
168100     EVALUATE TRUE                                                        
168200         WHEN SQLCODE = ZERO                                              
168300             CONTINUE                                                     
168400                                                                          
168500         WHEN SQLCODE = -904                                              
168600         WHEN SQLCODE = -911                                              
168700         WHEN SQLCODE = -913                                              
168800             SET PS-DONE                                                  
168900                 RC809-DB2-TIMEOUT-ERROR                                  
169000                                      TO TRUE                             
169100             MOVE RECDIS-ORD-LNE-NBR  TO RC001-PO-LNE-NBR                 
169200             MOVE RECDIS-STR-NBR      TO RC001-STORE-NBR-N                
169300             MOVE 1                   TO RC809-PREPACK-ERROR-NBR          
169400             PERFORM RC001-RETURN-MSG                                     
169500                                                                          
169600         WHEN OTHER                                                       
169700             SET PS-DONE              TO TRUE                             
169800             MOVE RECDIS-ORD-LNE-NBR  TO RC001-PO-LNE-NBR                 
169900             MOVE 2                   TO RC809-PREPACK-ERROR-NBR          
170000             PERFORM RC001-RETURN-MSG                                     
170100                                                                          
170200     END-EVALUATE.                                                        
170300/                                                                         
170400*7100-INACTIVATE-ALLPLN.                                                  
170500******************************************************************        
170600*    THIS PARAGRAPH UPDATES THE STATUS CODE OF THE TALLPLN RECORD         
170700*    TO INACTIVE.                                                         
170800******************************************************************        
170900                                                                          
171000*    MOVE RC808-PRPK-LNE-NBR(RC808-LINE-IDX)                              
171100*                                     TO DK-PO-LNE-NBR                    
171200*    EXEC SQL                                                             
171300*        UPDATE TALLPLN                                                   
171400*            SET STATUS_CDE = 'IN'                                        
171500*            WHERE PO_NBR     = :RC809-PO-NBR                             
171600*                AND PO_LNE_NBR = :DK-PO-LNE-NBR                          
171700*                AND DEST_NBR   = :RC809-OPER-UNT-ID                      
171800*                AND STATUS_CDE = 'AC'                                    
171900*    END-EXEC                                                             
172000                                                                          
172100*    EVALUATE TRUE                                                        
172200*        WHEN SQLCODE = ZERO                                              
172300*            CONTINUE                                                     
172400                                                                          
172500*        WHEN SQLCODE = -904                                              
172600*        WHEN SQLCODE = -911                                              
172700*        WHEN SQLCODE = -913                                              
172800*            SET PS-DONE                                                  
172900*                RC809-DB2-TIMEOUT-ERROR                                  
173000*                                     TO TRUE                             
173100*            MOVE DK-PO-LNE-NBR       TO RC001-PO-LNE-NBR                 
173200*            MOVE 3                   TO RC809-PREPACK-ERROR-NBR          
173300*            PERFORM RC001-RETURN-MSG                                     
173400                                                                          
173500*        WHEN OTHER                                                       
173600*            SET PS-DONE              TO TRUE                             
173700*            MOVE DK-PO-LNE-NBR       TO RC001-PO-LNE-NBR                 
173800*            MOVE 4                   TO RC809-PREPACK-ERROR-NBR          
173900*            PERFORM RC001-RETURN-MSG                                     
174000                                                                          
174100*    END-EVALUATE.                                                        
174200/                                                                         
174300 7110-DELETE-SPSTDS.                                                      
174400******************************************************************        
174500*    THIS PARAGRAPH DELETES ALL ROWS OF THE TSPSTDS RECORD                
174600*    BASED ON PO, LINE AND PLAN.                                          
174700******************************************************************        
174800                                                                          
174900     MOVE RC808-PRPK-LNE-NBR(RC808-LINE-IDX)                              
175000                                      TO DK-PO-LNE-NBR                    
175100     PERFORM 7115-GET-PLAN-NBR                                            
175200                                                                          
175300     EXEC SQL                                                             
175400         DELETE                                                           
175500         FROM TSPSTDS                                                     
175600         WHERE PO_NBR     = :RC809-PO-NBR                                 
175700         AND PO_LNE_NBR = :DK-PO-LNE-NBR                                  
175800         AND PLAN_NBR   = :ALLPLN-PLAN-NBR                                
175900         END-EXEC                                                         
176000                                                                          
176100     EVALUATE TRUE                                                        
176200         WHEN SQLCODE = ZERO                                              
176300         WHEN SQLCODE = +100                                              
176400             CONTINUE                                                     
176500                                                                          
176600         WHEN SQLCODE = -904                                              
176700         WHEN SQLCODE = -911                                              
176800         WHEN SQLCODE = -913                                              
176900             SET PS-DONE                                                  
177000                 RC809-DB2-TIMEOUT-ERROR                                  
177100                                      TO TRUE                             
177200             MOVE DK-PO-LNE-NBR       TO RC001-PO-LNE-NBR                 
177300             MOVE 5                   TO RC809-PREPACK-ERROR-NBR          
177400             PERFORM RC001-RETURN-MSG                                     
177500                                                                          
177600         WHEN OTHER                                                       
177700             SET PS-DONE              TO TRUE                             
177800             MOVE DK-PO-LNE-NBR       TO RC001-PO-LNE-NBR                 
177900             MOVE 6                   TO RC809-PREPACK-ERROR-NBR          
178000             PERFORM RC001-RETURN-MSG                                     
178100                                                                          
178200     END-EVALUATE.                                                        
178300/                                                                         
178400 7115-GET-PLAN-NBR.                                                       
178500     EXEC SQL                                                             
178600         SELECT PLAN_NBR                                                  
178700         INTO :ALLPLN-PLAN-NBR                                            
178800         FROM TALLPLN                                                     
178900             WHERE PO_NBR     = :RC809-PO-NBR                             
179000                 AND PO_LNE_NBR = :DK-PO-LNE-NBR                          
179100                 AND DEST_NBR   = :RC809-OPER-UNT-ID                      
179200                 AND STATUS_CDE = 'AC'                                    
179300     END-EXEC                                                             
179400                                                                          
179500     EVALUATE TRUE                                                        
179600         WHEN SQLCODE = ZERO                                              
179700             CONTINUE                                                     
179800                                                                          
179900         WHEN SQLCODE = -904                                              
180000         WHEN SQLCODE = -911                                              
180100         WHEN SQLCODE = -913                                              
180200             SET PS-DONE                                                  
180300                 RC809-DB2-TIMEOUT-ERROR                                  
180400                                      TO TRUE                             
180500             MOVE DK-PO-LNE-NBR       TO RC001-PO-LNE-NBR                 
180600             MOVE 7                   TO RC809-PREPACK-ERROR-NBR          
180700             PERFORM RC001-RETURN-MSG                                     
180800                                                                          
180900         WHEN OTHER                                                       
181000             SET PS-DONE                                                  
181100                                      TO TRUE                             
181200             MOVE DK-PO-LNE-NBR       TO RC001-PO-LNE-NBR                 
181300             MOVE 8                   TO RC809-PREPACK-ERROR-NBR          
181400             PERFORM RC001-RETURN-MSG                                     
181500             SET  PS-DONE             TO TRUE                             
181600             MOVE SQLCA               TO RC809-SQLCA                      
181700             MOVE SQLCODE             TO RC809-SQLCODE                    
181800                                                                          
181900     END-EVALUATE.                                                        
182000/                                                                         
182100 7200-DELETE-PACK-RECDIS.                                                 
182200                                                                          
182300     EXEC SQL                                                             
182400          DELETE                                                          
182500            FROM TRECDIS                                                  
182600            WHERE ORD_NBR        = :RECDIS-ORD-NBR                        
182700              AND ORD_LNE_NBR    = :RECDIS-ORD-LNE-NBR                    
182800              AND REC_SEQ_NBR    = :RECDIS-REC-SEQ-NBR                    
182900              AND OPER_UNT_ID    = :RECDIS-OPER-UNT-ID                    
183000              AND FCLTY_ID       = :RECDIS-FCLTY-ID                       
183200     END-EXEC.                                                            
183300                                                                          
183400     EVALUATE TRUE                                                        
183500         WHEN SQLCODE = ZERO                                              
183600         WHEN SQLCODE = +100                                              
183700              CONTINUE                                                    
183800         WHEN SQLCODE = -904                                              
183900         WHEN SQLCODE = -911                                              
184000         WHEN SQLCODE = -913                                              
184100             SET PS-DONE                                                  
184200                 RC809-DB2-TIMEOUT-ERROR                                  
184300                                      TO TRUE                             
184400             MOVE RECDIS-ORD-LNE-NBR  TO RC001-PO-LNE-NBR                 
184500             MOVE 11                  TO RC809-PREPACK-ERROR-NBR          
184600             PERFORM RC001-RETURN-MSG                                     
184700                                                                          
184800         WHEN SQLWARN0 NOT = SPACES                                       
184900         WHEN SQLCODE  NOT = ZERO                                         
185000             SET PS-DONE                                                  
185100                                      TO TRUE                             
185200             MOVE RECDIS-ORD-LNE-NBR  TO RC001-PO-LNE-NBR                 
185300             MOVE 12                  TO RC809-PREPACK-ERROR-NBR          
185400             PERFORM RC001-RETURN-MSG                                     
185500             SET  PS-DONE             TO TRUE                             
185600             MOVE SQLCA               TO RC809-SQLCA                      
185700             MOVE SQLCODE             TO RC809-SQLCODE                    
185800                                                                          
185900     END-EVALUATE.                                                        
186000                                                                          
186100 7400-DELETE-ODD-UNIT.                                                    
186200                                                                          
186300     EXEC SQL                                                             
186400          DELETE                                                          
186500            FROM RCT_VRFD_ODD_UNT                                         
186600            WHERE PO_NBR         = :RECDIS-ORD-NBR                        
186700              AND PO_LNE_NBR     = :RECDIS-ORD-LNE-NBR                    
186800              AND RCVR_SEQ_NBR   = :RECDIS-REC-SEQ-NBR                    
186900     END-EXEC.                                                            
187000                                                                          
187100     EVALUATE TRUE                                                        
187200         WHEN SQLCODE = ZERO                                              
187300         WHEN SQLCODE = +100                                              
187400              CONTINUE                                                    
187500         WHEN SQLCODE = -904                                              
187600         WHEN SQLCODE = -911                                              
187700         WHEN SQLCODE = -913                                              
187800             SET PS-DONE                                                  
187900                 RC809-DB2-TIMEOUT-ERROR                                  
188000                                      TO TRUE                             
188100             MOVE RECDIS-ORD-LNE-NBR  TO RC001-PO-LNE-NBR                 
188200             MOVE 11                  TO RC809-PREPACK-ERROR-NBR          
188300             PERFORM RC001-RETURN-MSG                                     
188400                                                                          
188500         WHEN SQLWARN0 NOT = SPACES                                       
188600         WHEN SQLCODE  NOT = ZERO                                         
188700             SET PS-DONE              TO TRUE                             
188800             MOVE RECDIS-ORD-LNE-NBR  TO RC001-PO-LNE-NBR                 
188900             MOVE 12                  TO RC809-PREPACK-ERROR-NBR          
189000             PERFORM RC001-RETURN-MSG                                     
189100             SET  PS-DONE             TO TRUE                             
189200             MOVE SQLCA               TO RC809-SQLCA                      
189300             MOVE SQLCODE             TO RC809-SQLCODE                    
189400                                                                          
189500     END-EVALUATE.                                                        
189600                                                                          
189700                                                                          
189800 8100-READ-TEMP-STORAGE.                                                  
189900     EXEC CICS READQ TS                                                   
190000         QUEUE (RC809-QUEUE-NAME)                                         
190100         INTO  (RC808-PACK-LINE-TABLE(PV-ITEM))                           
190200         ITEM  (PV-ITEM)                                                  
190300         RESP  (PV-CICS-RESP)                                             
190400     END-EXEC.                                                            
190500                                                                          
190600     IF ((PV-CICS-RESP = DFHRESP (NORMAL)) OR                             
190700               (PV-CICS-RESP = DFHRESP (ITEMERR)))                        
190800         CONTINUE                                                         
190900     ELSE                                                                 
191000         SET PS-DONE                  TO TRUE                             
191100         MOVE DK-PO-LNE-NBR           TO RC001-PO-LNE-NBR                 
191200         MOVE 9                       TO RC809-PREPACK-ERROR-NBR          
191300         PERFORM RC001-RETURN-MSG                                         
191400                                                                          
191500     END-IF.                                                              
191600                                                                          
191700 8150-DELETE-TEMP-QUEUE.                                                  
191800                                                                          
191900     EXEC CICS DELETEQ TS                                                 
192000         QUEUE   (RC809-QUEUE-NAME)                                       
192100         RESP    (PV-CICS-RESP)                                           
192200     END-EXEC.                                                            
192300                                                                          
192400     IF PV-CICS-RESP  = DFHRESP (NORMAL)                                  
192500        OR PV-CICS-RESP = DFHRESP (QIDERR)                                
192600         CONTINUE                                                         
192700     ELSE                                                                 
192800         SET PS-DONE                  TO TRUE                             
192900         MOVE DK-PO-LNE-NBR           TO RC001-PO-LNE-NBR                 
193000         MOVE 10                      TO RC809-PREPACK-ERROR-NBR          
193100         PERFORM RC001-RETURN-MSG                                         
193200     END-IF.                                                              
193300/                                                                         
193400 10000-POM-ALLOCATION.                                                    
193500                                                                          
193600     INITIALIZE PT-STORE-TABLE                                            
193700     MOVE 'X'                         TO PV-ALLOC-TYPE-FILTER             
193800     SET PT-STR-IDX                                                       
193900         PT-LAST-STR-IDX              TO 1                                
194000     MOVE ZERO                        TO PV-STR-COUNT                     
194100                            PV-SUM-STR-OTR-UNITS                          
194200     MOVE RC808-LNE-OTR-PACK-QTY (RC808-LINE-IDX)                         
194300                                      TO PV-TEMP-OTR-QTY                  
194400                                                                          
194500     PERFORM 6350-ADJUST-TEMP-OTR                                         
194600                                                                          
194700     PERFORM 1400-BUILD-STORE-ARRAY                                       
194800             VARYING RC808-STR-IDX FROM 1 BY 1                            
194900             UNTIL                                                        
195000             RC808-STR-NBR (RC808-LINE-IDX, RC808-STR-IDX) = ZERO         
195100             OR RC808-STR-IDX > PC-MAX-STORES.                            
195200                                                                          
195300     SET PT-STR-IDX                                                       
195400         PT-LAST-STR-IDX DOWN BY 1                                        
195500                                                                          
195600     PERFORM 6200-SORT-OTR-DESC                                           
195700     PERFORM 12000-POM-PRORATE.                                           
195800                                                                          
195900     IF RC809-CREATE-AUTO                                                 
196000         PERFORM 5000-ALLOCATE-STORES                                     
196100           VARYING PT-STR-IDX FROM 1 BY 1                                 
196200           UNTIL PT-STR-NBR (PT-STR-IDX) = ZERO                           
196300                OR PS-DONE                                                
196400                OR PT-STR-IDX > PC-MAX-STORES                             
196500     END-IF.                                                              
196600/                                                                         
196700 12000-POM-PRORATE.                                                       
196800                                                                          
196900     SET PT-STR-IDX-1                 TO 1.                               
197000                                                                          
197100                                                                          
197200*-----------------------------------------------------------------        
197300*    IF THE UNITS RECEIVED < UNITS OPEN TO RECEIVE (OTR)         *        
197400*       IF PACKS RECEIVED > NO OF STORES OPEN TO RECEIVE         *        
197500*          ALLOCATE PACKS ACCORDING TO ITS PERCENT OF TOTAL OTR  *        
197600*       IF THERE ARE STILL PACKS TO ALLOCATE                     *        
197700*          ALLOCATE 1 PACK TO STORES WITH OTR PACKS BUT DID NOT  *        
197800*          RECEIVE A PACK YET                                    *        
197900*       IF THERE ARE STILL PACKS TO ALLOCATE                     *        
198000*          ALLOCATE 1 PACK TO STORES THAT STILL HAS PACKS OTR    *        
198100*       IF THERE ARE STILL PACKS TO ALLOCATE                     *        
198200*          ALLOCATE THE NEEDED UNITS TO STORES WITH OTR UNITS    *        
198300*          BUT DID NOT RECEIVE A UNIT YET                        *        
198400*       IF THERE ARE STILL UNITS TO ALLOCATE                     *        
198500*          ALLOCATE THE NEEDED UNITS TO STORES THAT STILL HAS    *        
198600*          UNITS OTR UNTIL OUT OF UNITS                          *        
198700*                                                                *        
198800                                                                          
198900     IF PV-LEFT-TO-ALLOC < PV-TEMP-OTR-QTY                                
199000                                                                          
199100         IF RC809-AD-YES                                                  
199200             IF PV-PACKS-TO-ALLOC < PV-OTR-STR-COUNT                      
199300                                                                          
199400                 PERFORM 12501-ALLOC-ONE-UNIT                             
199500                       UNTIL PV-LEFT-TO-ALLOC NOT > ZERO                  
199600                                                                          
199700                 MOVE ZERO            TO PV-PACKS-TO-ALLOC                
199800                                         PV-UNITS-TO-ALLOC                
199900             ELSE                                                         
200000                 PERFORM 12510-ALLOC-ONE-PACK                             
200100             END-IF                                                       
200200         END-IF                                                           
200300                                                                          
200400         IF PV-LEFT-TO-ALLOC > PV-OTR-STR-COUNT                           
200500             PERFORM 12520-ALLOC-PACKS-PER-OTR                            
200600         END-IF                                                           
200700                                                                          
200800         IF PV-LEFT-TO-ALLOC > ZERO                                       
200900             PERFORM 12530-ALLOC-PAC-PER-ZERO-ALLOC                       
201000         END-IF                                                           
201100                                                                          
201200         IF PV-LEFT-TO-ALLOC > ZERO                                       
201300             PERFORM 12510-ALLOC-ONE-PACK                                 
201400         END-IF                                                           
201500                                                                          
201600         IF PV-LEFT-TO-ALLOC > ZERO                                       
201700             PERFORM 12540-ALLOC-UNT-PER-ZERO-ALLOC                       
201800         END-IF                                                           
201900                                                                          
202000         SET PS-UNITS-PROCESSED TO TRUE                                   
202100                                                                          
202200*        PERFORM 12550-ALLOC-UNITS                                        
202300*          UNTIL PV-LEFT-TO-ALLOC > ZERO                                  
202400*             OR PS-NO-UNITS-PROCESSED                                    
202500*                                                                *        
202600*-----------------------------------------------------------------        
202700*    IF THE UNITS RECEIVED >= UNITS OPEN TO RECEIVE (OTR)        *        
202800*       ALLOCATE ALL UNITS NEEDED TO THE STORES WITH OTR         *        
202900*       IF PACKS LEFTOVER > NO OF STORES PLANNED TO RECEIVE      *        
203000*          ALLOCATE PACKS ACCORDING TO ITS PERCENT OF PLAN       *        
203100*       IF THERE ARE STILL PACKS TO ALLOCATE                     *        
203200*          ALLOCATE 1 PACK TO STORES THAT HAS NOT RECEIVED MORE  *        
203300*          THAN WHAT WAS PLANNED TO RECEIVE                      *        
203400*       IF THERE ARE STILL PACKS TO ALLOCATE                     *        
203500*          ALLOCATE 1 PACK TO STORES UNTIL OUT OF PACKS          *        
203600*       IF THERE ARE STILL UNITS TO ALLOCATE                     *        
203700*          ALLOCATE THE LEFTOVER UNITS TO THE NEXT STORE         *        
203800*                                                                *        
203900*                                                                *        
204000*    ( O V E R A G E)                                            *        
204100                                                                          
204200     ELSE                                                                 
204300                                                                          
204400         PERFORM 12560-ALLOC-ALL-STRS                                     
204500           VARYING PT-STR-IDX-1 FROM 1 BY 1                               
204600           UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX                           
204700              OR PV-LEFT-TO-ALLOC NOT > ZERO                              
204800                                                                          
204900         DIVIDE PV-DRIVER-RATIO-QTY                                       
205000           INTO PV-LEFT-TO-ALLOC                                          
205100                GIVING PV-PACKS-TO-ALLOC                                  
205200                REMAINDER PV-UNITS-TO-ALLOC                               
205300                                                                          
205400         IF PV-PACKS-TO-ALLOC > PT-LAST-STR-IDX                           
205500             PERFORM 12570-ALLOC-PACKS-PER-PLAN                           
205600         END-IF                                                           
205700                                                                          
205800         IF PV-LEFT-TO-ALLOC > ZERO                                       
205900             PERFORM 12580-ALLOC-PAC-PER-ZERO-ALLOC                       
206000         END-IF                                                           
206100                                                                          
206200         IF PV-LEFT-TO-ALLOC > ZERO                                       
206300             PERFORM 12590-ALLOC-LEFTOVERS                                
206400                 UNTIL PV-LEFT-TO-ALLOC NOT > ZERO                        
206500         END-IF                                                           
206600     END-IF.                                                              
206700                                                                          
206800 12501-ALLOC-ONE-UNIT.                                                    
206900******************************************************************        
207000*    THIS PARAGRAPH CALLS PARAGRAPH 2501 WHICH ALLOCATES 1 UNIT           
207100*    TO EACH STORE UNTIL NO MORE UNITS OR NO MORE STORES                  
207200******************************************************************        
207300                                                                          
207400     SET PT-STR-IDX-1                 TO 1                                
207500                                                                          
207600     PERFORM 12501-PRORATE-ONE-UNIT                                       
207700       UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX                               
207800          OR PV-LEFT-TO-ALLOC NOT > ZERO.                                 
207900                                                                          
208000                                                                          
208100 12501-PRORATE-ONE-UNIT.                                                  
208200******************************************************************        
208300*    THIS PARAGRAPH ALLOCATES 1 UNIT IF THE STORE STILL HAS UNITS         
208400*    OPEN TO RECEIVE.                                                     
208500******************************************************************        
208600                                                                          
208700     IF PT-STR-OTR-TOTALS  (PT-STR-IDX-1) >                               
208800        PT-STR-ALLOC-TOTALS(PT-STR-IDX-1)                                 
208900                                                                          
209000         ADD PV-DRIVER-RATIO-QTY      TO PT-STR-ALLOC-TOTALS              
209100                                         (PT-STR-IDX-1)                   
209200         SUBTRACT PV-DRIVER-RATIO-QTY                                     
209300                                   FROM PV-LEFT-TO-ALLOC                  
209400     END-IF.                                                              
209500                                                                          
209600     SET PT-STR-IDX-1 UP BY 1.                                            
209700                                                                          
209800                                                                          
209900 12510-ALLOC-ONE-PACK.                                                    
210000******************************************************************        
210100*    THIS PARAGRAPH CALLS PARAGRAPH 2511 WHICH ALLOCATES 1 PACK           
210200*    TO EACH STORE UNTIL NO MORE PACKS OR NO MORE STORES                  
210300******************************************************************        
210400                                                                          
210500     SET PT-STR-IDX-1                 TO 1                                
210600                                                                          
210700     PERFORM 12511-PRORATE-ONE-PACK                                       
210800       UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX                               
210900          OR PV-LEFT-TO-ALLOC NOT > ZERO.                                 
211000                                                                          
211100                                                                          
211200 12511-PRORATE-ONE-PACK.                                                  
211300******************************************************************        
211400*    THIS PARAGRAPH ALLOCATES 1 PACK IF THE STORE STILL HAS PACKS         
211500*    OPEN TO RECEIVE.                                                     
211600******************************************************************        
211700                                                                          
211800     IF PT-STR-OTR-PACKS   (PT-STR-IDX-1) >                               
211900        PT-STR-ALLOC-PACKS (PT-STR-IDX-1)                                 
212000                                                                          
212100         ADD PV-DRIVER-RATIO-QTY      TO PT-STR-ALLOC-PACKS               
212200                              (PT-STR-IDX-1)                              
212300                                         PT-STR-ALLOC-TOTALS              
212400                              (PT-STR-IDX-1)                              
212500                                                                          
212600         SUBTRACT PV-DRIVER-RATIO-QTY  FROM PV-LEFT-TO-ALLOC              
212700                                                                          
212800     END-IF.                                                              
212900                                                                          
213000     SET PT-STR-IDX-1 UP BY 1.                                            
213100                                                                          
213200                                                                          
213300 12520-ALLOC-PACKS-PER-OTR.                                               
213400******************************************************************        
213500*    THIS PARAGRAPH CALLS THE FOLLOWING PARAGRAPH:                        
213600*    - 2521 CALCULATES EACH STORE PERCENTAGE FROM THE TOTAL OTR           
213700*    - 2522 ALLOCATES PACKS ACCORDING TO EACH STORES CALCULATED           
213800*           PERCENTAGE OF TOTAL OTR                                       
213900******************************************************************        
214000                                                                          
214100     PERFORM 12521-CALC-OTR-STR-PCT                                       
214200        VARYING PT-STR-IDX-1 FROM 1 BY 1                                  
214300        UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX.                             
214400                                                                          
214500     MOVE PV-LEFT-TO-ALLOC            TO PV-ORIG-QTY-TO-ALLOC             
214600     PERFORM 12522-PRORATE-PER-PCT                                        
214700        VARYING PT-STR-IDX-1 FROM 1 BY 1                                  
214800        UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX                              
214900        OR PV-LEFT-TO-ALLOC NOT > ZERO.                                   
215000                                                                          
215100 12521-CALC-OTR-STR-PCT.                                                  
215200******************************************************************        
215300*    THIS PARAGRAPH CALCULATES THE STORE'S PERCENTAGE FROM THE            
215400*    TOTAL OTR.                                                           
215500******************************************************************        
215600                                                                          
215700     COMPUTE PT-STR-ALLOC-PCT (PT-STR-IDX-1) =                            
215800             PT-STR-OTR-TOTALS(PT-STR-IDX-1) /                            
215900             PV-TEMP-OTR-QTY                                              
216000         ON SIZE ERROR                                                    
216100             MOVE ZERO                TO PT-STR-ALLOC-PCT                 
216200                                         (PT-STR-IDX-1)                   
216300     END-COMPUTE.                                                         
216400                                                                          
216500                                                                          
216600 12522-PRORATE-PER-PCT.                                                   
216700******************************************************************        
216800*    THIS PARAGRAPH ALLOCATES THE PACKS ACCORDING TO ITS PERCENT          
216900*    FROM TOTAL OTR, ROUNDED DOWN.                                        
217000******************************************************************        
217100                                                                          
217200     IF PT-STR-OTR-TOTALS (PT-STR-IDX-1) >                                
217300                            PT-STR-ALLOC-TOTALS (PT-STR-IDX-1)            
217400         COMPUTE PV-PACKS-FOR-STORE =                                     
217500                 PT-STR-ALLOC-PCT (PT-STR-IDX-1) *                        
217600                 PV-ORIG-QTY-TO-ALLOC                                     
217700                                                                          
217800         IF PV-PACKS-FOR-STORE > PV-DRIVER-RATIO-QTY                      
217900             DIVIDE PV-PACKS-FOR-STORE BY PV-DRIVER-RATIO-QTY             
218000                 GIVING PV-WHOLE-UNITS                                    
218100         ELSE                                                             
218200             MOVE 1                   TO PV-WHOLE-UNITS                   
218300         END-IF                                                           
218400         MULTIPLY PV-DRIVER-RATIO-QTY BY PV-WHOLE-UNITS                   
218500                                                                          
218600         ADD PV-WHOLE-UNITS           TO PT-STR-ALLOC-TOTALS              
218700                                         (PT-STR-IDX-1)                   
218800         SUBTRACT PV-WHOLE-UNITS FROM PV-LEFT-TO-ALLOC                    
218900     END-IF.                                                              
219000                                                                          
219100 12530-ALLOC-PAC-PER-ZERO-ALLOC.                                          
219200******************************************************************        
219300*    THIS PARAGRAPH CALLS PARAGRAPH 2531 WHICH ALLOCATES 1 PACK           
219400*    TO EACH STORE THAT HAS PACKS OPEN TO RECEIVE AND HAS NOT             
219500*    RECEIVED ANY PACKS YET.                                              
219600******************************************************************        
219700                                                                          
219800     PERFORM 12531-PRORATE-ONE-PACK                                       
219900       VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
220000       UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX                               
220100          OR PV-LEFT-TO-ALLOC NOT > ZERO.                                 
220200                                                                          
220300                                                                          
220400 12531-PRORATE-ONE-PACK.                                                  
220500******************************************************************        
220600*    THIS PARAGRAPH ALLOCATES 1 PACK TO EACH STORE THAT HAS               
220700*    PACKS OPEN TO RECEIVE AND HAS NOT RECEIVED ANY PACKS YET.            
220800******************************************************************        
220900                                                                          
221000     IF (PT-STR-OTR-TOTALS (PT-STR-IDX-1) > ZERO) AND                     
221100        (PT-STR-ALLOC-TOTALS (PT-STR-IDX-1) NOT > ZERO)                   
221200                                                                          
221300         ADD PV-DRIVER-RATIO-QTY      TO PT-STR-ALLOC-TOTALS              
221400                                         (PT-STR-IDX-1)                   
221500                                                                          
221600         SUBTRACT PV-DRIVER-RATIO-QTY FROM PV-LEFT-TO-ALLOC               
221700     END-IF.                                                              
221800                                                                          
221900 12540-ALLOC-UNT-PER-ZERO-ALLOC.                                          
222000******************************************************************        
222100*    THIS PARAGRAPH CALLS PARAGRAPH 2541 WHICH ALLOCATES THE              
222200*    NEEDED UNITS TO THE STORES WITH OTR BUT HAS NOT RECEIVED             
222300*    ANY UNITS YET.                                                       
222400******************************************************************        
222500                                                                          
222600     SET PT-STR-IDX-1                 TO 1.                               
222700                                                                          
222800     PERFORM 12541-PRORATE-UNITS                                          
222900       VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
223000       UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX                               
223100          OR PV-LEFT-TO-ALLOC NOT > ZERO.                                 
223200                                                                          
223300 12541-PRORATE-UNITS.                                                     
223400******************************************************************        
223500*    THIS PARAGRAPH ALLOCATES THE NEEDED UNITS TO THE STORES WITH         
223600*    OTR BUT HAS NOT RECEIVED ANY UNITS YET.                              
223700******************************************************************        
223800                                                                          
223900     IF (PT-STR-ALLOC-TOTALS(PT-STR-IDX-1) NOT > ZERO) AND                
224000        (PT-STR-OTR-TOTALS  (PT-STR-IDX-1) > ZERO)                        
224100                                                                          
224200         IF PV-LEFT-TO-ALLOC > PT-STR-OTR-TOTALS(PT-STR-IDX-1)            
224300             ADD PT-STR-OTR-TOTALS(PT-STR-IDX-1)                          
224400                                      TO                                  
224500                               PT-STR-ALLOC-TOTALS(PT-STR-IDX-1)          
224600                                                                          
224700             SUBTRACT PT-STR-OTR-TOTALS(PT-STR-IDX-1)                     
224800                 FROM PV-LEFT-TO-ALLOC                                    
224900         ELSE                                                             
225000             ADD PV-LEFT-TO-ALLOC     TO                                  
225100                                PT-STR-ALLOC-TOTALS(PT-STR-IDX-1)         
225200                                                                          
225300             MOVE ZERO                TO PV-LEFT-TO-ALLOC                 
225400         END-IF                                                           
225500     END-IF.                                                              
225600*                                                                         
225700*12550-ALLOC-UNITS.                                                       
225800******************************************************************        
225900*    THIS PARAGRAPH CALLS PARAGRAPH 12551 WHICH ALLOCATES THE             
226000*    NEEDED UNITS TO THE STORES THAT STILL HAS OTR.                       
226100******************************************************************        
226200*                                                                         
226300*    SET PS-NO-UNITS-PROCESSED        TO TRUE.                            
226400*                                                                         
226500*    IF PT-STR-IDX-1 > PT-LAST-STR-IDX                                    
226600*        SET PT-STR-IDX-1             TO 1                                
226700*    END-IF.                                                              
226800*                                                                         
226900*    PERFORM 12551-PRORATE-UNITS                                          
227000*      VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
227100*      UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX                               
227200*                       OR PV-LEFT-TO-ALLOC NOT > ZERO.                   
227300*                                                                         
227400*                                                                         
227500*12551-PRORATE-UNITS.                                                     
227600******************************************************************        
227700*    THIS PARAGRAPH ALLOCATES THE NEEDED UNITS TO THE STORES THAT         
227800*    STILL HAS OTR.                                                       
227900******************************************************************        
228000*                                                                         
228100*    IF PT-STR-OTR-TOTALS   (PT-STR-IDX-1) >                              
228200*                 PT-STR-ALLOC-TOTALS (PT-STR-IDX-1)                      
228300*                                                                         
228400*        SET PS-UNITS-PROCESSED       TO TRUE                             
228500*                                                                         
228600*        IF PV-LEFT-TO-ALLOC >                                            
228700*           (PT-STR-OTR-TOTALS (PT-STR-IDX-1) -                           
228800*            PT-STR-ALLOC-TOTALS (PT-STR-IDX-1))                          
228900*                                                                         
229000*            COMPUTE PV-LEFT-TO-ALLOC =                                   
229100*                    PV-LEFT-TO-ALLOC -                                   
229200*                    (PT-STR-OTR-TOTALS (PT-STR-IDX-1) -                  
229300*                     PT-STR-ALLOC-TOTALS (PT-STR-IDX-1))                 
229400*                                                                         
229500*            MOVE PT-STR-OTR-TOTALS   (PT-STR-IDX-1)                      
229600*              TO PT-STR-ALLOC-TOTALS (PT-STR-IDX-1)                      
229700*        ELSE                                                             
229800*            ADD PV-LEFT-TO-ALLOC     TO PT-STR-ALLOC-TOTALS              
229900*                                        (PT-STR-IDX-1)                   
230000*                                                                         
230100*            MOVE ZERO                TO PV-LEFT-TO-ALLOC                 
230200*        END-IF                                                           
230300*    END-IF.                                                              
230400*                                                                         
230500*    SET PT-STR-IDX-1 UP BY 1.                                            
230600*                                                                         
230700*                                                                         
230800 12560-ALLOC-ALL-STRS.                                                    
230900******************************************************************        
231000*    THIS PARAGRAPH ALLOCATES THE STORES OTR UNITS.                       
231100******************************************************************        
231200                                                                          
231300     IF PT-STR-OTR-TOTALS (PT-STR-IDX-1) > ZERO                           
231400         IF PT-STR-OTR-TOTALS (PT-STR-IDX-1)                              
231500                          > PV-DRIVER-RATIO-QTY                           
231600             DIVIDE PT-STR-OTR-TOTALS (PT-STR-IDX-1) BY                   
231700                    PV-DRIVER-RATIO-QTY                                   
231800                 GIVING PV-WHOLE-UNITS                                    
231900         ELSE                                                             
232000             MOVE 1                   TO PV-WHOLE-UNITS                   
232100         END-IF                                                           
232200     MULTIPLY PV-DRIVER-RATIO-QTY BY PV-WHOLE-UNITS                       
232300                                                                          
232400     ADD PV-WHOLE-UNITS               TO                                  
232500                          PT-STR-ALLOC-TOTALS (PT-STR-IDX-1)              
232600     SUBTRACT PV-WHOLE-UNITS  FROM PV-LEFT-TO-ALLOC                       
232700     END-IF.                                                              
232800                                                                          
232900 12570-ALLOC-PACKS-PER-PLAN.                                              
233000******************************************************************        
233100*    THIS PARAGRAPH CALLS THE FOLLOWING PARAGRAPH:                        
233200*    - 2571 CALCULATES EACH STORE PERCENTAGE FROM THE TOTAL PLAN          
233300*    - 2572 ALLOCATES PACKS ACCORDING TO EACH STORES CALCULATED           
233400*           PERCENTAGE OF TOTAL PLAN                                      
233500******************************************************************        
233600                                                                          
233700                                                                          
233800     PERFORM 12571-CALC-ALL-STR-PCT                                       
233900       VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
234000       UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX.                              
234100                                                                          
234200     PERFORM 12522-PRORATE-PER-PCT                                        
234300       VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
234400       UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX.                              
234500                                                                          
234600 12571-CALC-ALL-STR-PCT.                                                  
234700******************************************************************        
234800*    THIS PARAGRAPH CALCULATES THE STORE'S PERCENTAGE FROM THE            
234900*    TOTAL PLAN.                                                          
235000******************************************************************        
235100                                                                          
235200     COMPUTE PT-STR-ALLOC-PCT (PT-STR-IDX-1) =                            
235300             PT-STR-PLAN-QTY  (PT-STR-IDX-1) /                            
235400             PV-SUM-STR-PLAN-QTY                                          
235500         ON SIZE ERROR                                                    
235600             MOVE ZERO                TO PT-STR-ALLOC-PCT                 
235700                                         (PT-STR-IDX-1)                   
235800     END-COMPUTE.                                                         
235900                                                                          
236000 12580-ALLOC-PAC-PER-ZERO-ALLOC.                                          
236100******************************************************************        
236200*    THIS PARAGRAPH CALLS PARAGRAPH 2581 WHICH ALLOCATES 1 PACK           
236300*    TO EACH STORE THAT HAS NOT RECEIVED OVER IT'S PLANNED UNITS.         
236400******************************************************************        
236500                                                                          
236600                                                                          
236700     PERFORM 12581-PRORATE-ONE-PACK                                       
236800       VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
236900       UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX                               
237000          OR PV-LEFT-TO-ALLOC NOT > ZERO.                                 
237100                                                                          
237200                                                                          
237300 12581-PRORATE-ONE-PACK.                                                  
237400******************************************************************        
237500*    THIS PARAGRAPH ALLOCATES 1 PACK TO EACH STORE THAT HAS NOT           
237600*    RECEIVED OVER ITS PLANNED UNITS.                                     
237700******************************************************************        
237800                                                                          
237900     IF PT-STR-ALLOC-TOTALS (PT-STR-IDX-1) NOT >                          
238000        PT-STR-OTR-TOTALS (PT-STR-IDX-1)                                  
238100                                                                          
238200         ADD PV-DRIVER-RATIO-QTY      TO PT-STR-ALLOC-TOTALS              
238300                                         (PT-STR-IDX-1)                   
238400                                                                          
238500         SUBTRACT PV-DRIVER-RATIO-QTY                                     
238600                                       FROM PV-LEFT-TO-ALLOC              
238700     END-IF.                                                              
238800/                                                                         
238900 12590-ALLOC-LEFTOVERS.                                                   
239000******************************************************************        
239100*    THIS PARAGRAPH CALLS PARAGRAPH 2591 WHICH ALLOCATES 1 PACK           
239200*    TO EACH STORE UNTIL OUT OF PACKS.  THEN IT ALLOCATES THE             
239300*    UNITS LEFTOVER TO THE NEXT STORE AVAILABLE.                          
239400******************************************************************        
239500                                                                          
239600     SET PT-STR-IDX-1                 TO 1                                
239700                                                                          
239800     PERFORM 12591-PRORATE-ONE-PACK                                       
239900         VARYING PT-STR-IDX-1 FROM 1 BY 1                                 
240000         UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX                             
240100          OR PV-LEFT-TO-ALLOC NOT > ZERO.                                 
240200                                                                          
240300 12591-PRORATE-ONE-PACK.                                                  
240400******************************************************************        
240500*    THIS PARAGRAPH ALLOCATES 1 PACK TO EACH STORE.                       
240600******************************************************************        
240700                                                                          
240800     ADD PV-DRIVER-RATIO-QTY          TO PT-STR-ALLOC-TOTALS              
240900                                         (PT-STR-IDX-1).                  
241000                                                                          
241100     SUBTRACT PV-DRIVER-RATIO-QTY  FROM PV-LEFT-TO-ALLOC.                 
241200/                                                                         
241300 20000-MA-ALLOCATION.                                                     
241400* THIS CODE WAS LIFTED FROM RCKCS233 AND CHNGED ONLY TO USE               
241500* INDICES INSTEAD OF SUBSRIPTS.  INNER PACK IS RETRIEVED FROM             
241600* THE RC808 TABLE INSTEAD OF TRECITM                                      
241700                                                                          
241800******************************************************************        
241900*    THIS PARAGRAPH CALLS THE FOLLOWING PARAGRAPHS BASED                  
242000*    ON THE DIFFERENCE BETWEEN THE RECEIPT QTY AND THE OTR                
242100*    - 24050-MOVE-OTR  MOVES THE OTR TO A MA OTR WORK FIELD               
242200*                     AND MANAGES THE ALLOCATION SEQ CODE                 
242300*    - 24100-MA-SORT-STORE-ARRAY                                          
242400*                     DETERMINES SORT SEQUENCE TO BE USED                 
242500*                     AND DIRECTS TO APPROPRIATE SORT ROUTINES            
242600*    - 24200-MA-SEQ-CODE-TOTALS                                           
242700*                     ADDS UP FILL FIRST, STANDARD, FILL LAST,            
242800*                     AND CENTRAL STOCK QUANTITIES                        
242900*    - 24300-ALLOCATE-OTR-QUANTITIES                                      
243000*                     ALLOCATES OTR QUANTITIES                            
243100*    - 24400-ALLOCATE-SHORTAGE                                            
243200*                     MANAGES ALLOCATION WHEN RECEIPT QTY IS              
243300*                     LESS THAN THE OTR                                   
243400*    - 24900-ALLOCATE-OVERAGE                                             
243500*                     MANAGES ALLOCATION WHEN RECEIPT QTY IS              
243600*                     GREATER THAN THE OTR                                
243700******************************************************************        
243800                                                                          
243900*    (BUILD WS TABLE FROM RC808 HERE)                                     
244000     INITIALIZE PT-STORE-TABLE                                            
244100     MOVE 'X'                         TO PV-ALLOC-TYPE-FILTER             
244200     SET PT-STR-IDX                                                       
244300         PT-LAST-STR-IDX              TO 1                                
244400     MOVE ZERO                        TO PV-STR-COUNT                     
244500                                 PV-SUM-STR-OTR-UNITS                     
244600     MOVE RC808-LNE-OTR-PACK-QTY (RC808-LINE-IDX)                         
244700                                      TO PV-TEMP-OTR-QTY                  
244800                                                                          
244900     PERFORM 6350-ADJUST-TEMP-OTR                                         
245000                                                                          
245100     PERFORM 1400-BUILD-STORE-ARRAY                                       
245200             VARYING RC808-STR-IDX FROM 1 BY 1                            
245300             UNTIL                                                        
245400             RC808-STR-NBR (RC808-LINE-IDX, RC808-STR-IDX) = ZERO         
245500             OR RC808-STR-IDX > PC-MAX-STORES.                            
245600                                                                          
245700                                                                          
245800     SET PT-STR-IDX                                                       
245900         PT-LAST-STR-IDX DOWN BY 1                                        
246000                                                                          
246100* NOTE:  PV-STR-SUB IS NEVER USED AS A SUBSCRIPT BUT IS REALLY            
246200*        A MARKER FOR THE LAST STORE IN THE ARRAY                         
246300                                                                          
246400     SET PV-STR-SUB                   TO PT-LAST-STR-IDX                  
246500                                                                          
246600*    COMPUTE PV-ALLOC-OUTR-PK-QTY = PV-DRIVER-RATIO-QTY *                 
246700*                 RC808-CASE-PACK-QTY (RC808-LINE-IDX)                    
246800*                                                                         
246900*    DIVIDE PV-DRIVER-RATIO-QTY                                           
247000*      INTO    PV-ALLOC-OUTR-PK-QTY                                       
247100                                                                          
247200     MOVE RC808-PK-SIZE-RND-CDE(RC808-LINE-IDX)                           
247300                                      TO PV-PK-SIZE-RND-CDE.              
247400                                                                          
247500     IF NOT PV-VALID-PK-SIZE-RND-CDE                                      
247600         MOVE  '5'                    TO                                  
247700                        RC808-PK-SIZE-RND-CDE(RC808-LINE-IDX)             
247800     END-IF                                                               
247900                                                                          
248000     SET  PS-STORE-NOT-IN-TABLE       TO TRUE.                            
248100                                                                          
248200     PERFORM 24050-MOVE-OTR                                               
248300       VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
248400       UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX                               
248500                                                                          
248600     PERFORM 24100-MA-SORT-STORE-ARRAY.                                   
248700                                                                          
248800     EVALUATE TRUE                                                        
248900                                                                          
249000     WHEN PV-LEFT-TO-ALLOC  =  PV-TEMP-OTR-QTY                            
249100         PERFORM 24300-ALLOCATE-OTR-QUANTITES                             
249200     WHEN  PV-LEFT-TO-ALLOC  <  PV-TEMP-OTR-QTY                           
249300         PERFORM 24200-MA-SEQ-CODE-TOTALS                                 
249400         PERFORM 24400-ALLOCATE-SHORTAGE                                  
249500     WHEN OTHER                                                           
249600         PERFORM 24300-ALLOCATE-OTR-QUANTITES                             
249700         PERFORM 24900-ALLOCATE-OVERAGE                                   
249800                                                                          
249900     END-EVALUATE                                                         
250000                                                                          
250100     IF  PV-LEFT-TO-ALLOC  > ZERO                                         
250200         PERFORM 24900-ALLOCATE-OVERAGE                                   
250300     END-IF.                                                              
250400                                                                          
250500     IF RC809-CREATE-AUTO                                                 
250600         PERFORM 5000-ALLOCATE-STORES                                     
250700           VARYING PT-STR-IDX FROM 1 BY 1                                 
250800           UNTIL PT-STR-NBR (PT-STR-IDX) = ZERO                           
250900                OR PS-DONE                                                
251000                OR PT-STR-IDX > PC-MAX-STORES                             
251100     END-IF.                                                              
251200                                                                          
251300 24050-MOVE-OTR.                                                          
251400******************************************************************        
251500*    MOVES THE OTR TO THE MA OTR AND CHECKS SEQUENCE CODE                 
251600******************************************************************        
251700                                                                          
251800     IF  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) > '4'                       
251900         MOVE  '4' TO  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1)               
252000     END-IF                                                               
252100                                                                          
252200     IF  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) NOT = '1'                   
252300     AND PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) NOT = '2'                   
252400     AND PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) NOT = '3'                   
252500     AND PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) NOT = '4'                   
252600         MOVE  '2'  TO  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1)              
252700     END-IF                                                               
252800                                                                          
252900     IF  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) = '1'                       
253000     OR  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) = '2'                       
253100     OR  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) = '3'                       
253200         SET  PS-STORE-IN-TABLE       TO TRUE                             
253300     END-IF                                                               
253400                                                                          
253500     MOVE PT-STR-OTR-TOTALS (PT-STR-IDX-1)                                
253600                                      TO                                  
253700                       PT-STR-OTR-TOTALS-MA  (PT-STR-IDX-1).              
253800                                                                          
253900 24100-MA-SORT-STORE-ARRAY.                                               
254000******************************************************************        
254100*    THIS PARAGRAPH CALLS THE APPROPRIATE SORT PARAGRAPHS                 
254200*    BASED ON THE PICTURED AD FLAG.                                       
254300******************************************************************        
254400                                                                          
254500     PERFORM 24110-SORT-SEQ-CODE                                          
254600                                                                          
254700     IF  RC808-AD-YES (RC808-LINE-IDX)                                    
254800             AND PV-LEFT-TO-ALLOC  <  PV-TEMP-OTR-QTY                     
254900                                                                          
255000         PERFORM 24120-SORT-OTR                                           
255100                                                                          
255200         PERFORM 24130-SORT-OTR-RANK                                      
255300                                                                          
255400     ELSE                                                                 
255500                                                                          
255600         PERFORM 24140-SORT-CODE-RANK                                     
255700                                                                          
255800     END-IF.                                                              
255900                                                                          
256000 24110-SORT-SEQ-CODE.                                                     
256100******************************************************************        
256200*    SORTS THE STORES TABLE IN ASCENDING ORDER                            
256300*    OF ALLOCATION SEQUENCE CODE.                                         
256400******************************************************************        
256500                                                                          
256600     SET PT-STR-IDX-1                 TO 1                                
256700                                                                          
256800     PERFORM 24111-BUBBLE-SORT                                            
256900       UNTIL PT-STR-IDX-1 NOT < PT-LAST-STR-IDX.                          
257000                                                                          
257100 24111-BUBBLE-SORT.                                                       
257200     SET PT-STR-IDX-2                 TO PT-STR-IDX-1                     
257300     SET PT-STR-IDX-2 UP BY 1                                             
257400                                                                          
257500     PERFORM 24112-LOOP-BUBBLE-SORT                                       
257600         UNTIL PT-STR-IDX-2 > PT-LAST-STR-IDX.                            
257700                                                                          
257800     SET PT-STR-IDX-1 UP BY 1.                                            
257900                                                                          
258000 24112-LOOP-BUBBLE-SORT.                                                  
258100                                                                          
258200     IF (PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-2) <                           
258300         PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1))                            
258400                                                                          
258500         MOVE PT-STORE-DATA (PT-STR-IDX-1) TO PV-TEMP                     
258600         MOVE PT-STORE-DATA (PT-STR-IDX-2) TO                             
258700              PT-STORE-DATA (PT-STR-IDX-1)                                
258800         MOVE PV-TEMP                   TO                                
258900              PT-STORE-DATA (PT-STR-IDX-2)                                
259000     END-IF.                                                              
259100                                                                          
259200     SET PT-STR-IDX-2 UP BY 1.                                            
259300                                                                          
259400 24120-SORT-OTR.                                                          
259500******************************************************************        
259600*    THIS PARAGRAPH SORTS THE STORES TABLE IN ASCENDING ORDER             
259700*    OF ALLOCATION SEQUENCE CODE AND DESCENDING OTR                       
259800******************************************************************        
259900                                                                          
260000     SET PT-STR-IDX-1                 TO 1                                
260100                                                                          
260200     PERFORM 24121-BUBBLE-SORT                                            
260300       UNTIL PT-STR-IDX-1 NOT < PT-LAST-STR-IDX.                          
260400                                                                          
260500 24121-BUBBLE-SORT.                                                       
260600     SET PT-STR-IDX-2                 TO PT-STR-IDX-1                     
260700     SET PT-STR-IDX-2 UP BY 1                                             
260800                                                                          
260900     PERFORM 24122-LOOP-BUBBLE-SORT                                       
261000         UNTIL PT-STR-IDX-2 > PT-LAST-STR-IDX.                            
261100                                                                          
261200     SET PT-STR-IDX-1 UP BY 1.                                            
261300                                                                          
261400 24122-LOOP-BUBBLE-SORT.                                                  
261500                                                                          
261600     IF (PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-2) =                           
261700         PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1))                            
261800                              AND                                         
261900        (PT-STR-OTR-TOTALS-MA   (PT-STR-IDX-2) >                          
262000         PT-STR-OTR-TOTALS-MA   (PT-STR-IDX-1))                           
262100                                                                          
262200         MOVE PT-STORE-DATA (PT-STR-IDX-1) TO PV-TEMP                     
262300         MOVE PT-STORE-DATA (PT-STR-IDX-2) TO                             
262400              PT-STORE-DATA (PT-STR-IDX-1)                                
262500         MOVE PV-TEMP                   TO                                
262600              PT-STORE-DATA (PT-STR-IDX-2)                                
262700     END-IF.                                                              
262800                                                                          
262900     SET PT-STR-IDX-2 UP BY 1.                                            
263000                                                                          
263100 24130-SORT-OTR-RANK.                                                     
263200******************************************************************        
263300*    THIS PARAGRAPH SORTS THE STORES TABLE IN ASCENDING ORDER             
263400*    OF ALLOCATION SEQUENCE CODE AND DESCENDING OTR AND                   
263500*    ASCENDING RANK SEQUENCE.                                             
263600******************************************************************        
263700                                                                          
263800     SET PT-STR-IDX-1                 TO 1                                
263900                                                                          
264000     PERFORM 24131-BUBBLE-SORT                                            
264100       UNTIL PT-STR-IDX-1 NOT < PT-LAST-STR-IDX.                          
264200                                                                          
264300 24131-BUBBLE-SORT.                                                       
264400     SET PT-STR-IDX-2                 TO PT-STR-IDX-1                     
264500     SET PT-STR-IDX-2 UP BY 1                                             
264600                                                                          
264700     PERFORM 24132-LOOP-BUBBLE-SORT                                       
264800       UNTIL PT-STR-IDX-2 > PT-LAST-STR-IDX.                              
264900                                                                          
265000     SET PT-STR-IDX-1 UP BY 1.                                            
265100                                                                          
265200 24132-LOOP-BUBBLE-SORT.                                                  
265300                                                                          
265400     IF (PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-2) =                           
265500         PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1))                            
265600                              AND                                         
265700        (PT-STR-OTR-TOTALS-MA   (PT-STR-IDX-2) =                          
265800         PT-STR-OTR-TOTALS-MA   (PT-STR-IDX-1))                           
265900                              AND                                         
266000        (PT-STR-RNK-SEQ-NBR    (PT-STR-IDX-2) <                           
266100         PT-STR-RNK-SEQ-NBR    (PT-STR-IDX-1))                            
266200                                                                          
266300         MOVE PT-STORE-DATA (PT-STR-IDX-1) TO PV-TEMP                     
266400         MOVE PT-STORE-DATA (PT-STR-IDX-2) TO                             
266500              PT-STORE-DATA (PT-STR-IDX-1)                                
266600         MOVE PV-TEMP                   TO                                
266700              PT-STORE-DATA (PT-STR-IDX-2)                                
266800     END-IF.                                                              
266900                                                                          
267000     SET PT-STR-IDX-2 UP BY 1.                                            
267100                                                                          
267200 24140-SORT-CODE-RANK.                                                    
267300******************************************************************        
267400*    THIS PARAGRAPH SORTS THE STORES TABLE IN ASCENDING ORDER             
267500*    OF ALLOCATION SEQUENCE CODE AND STORE RANK.                          
267600******************************************************************        
267700                                                                          
267800     SET PT-STR-IDX-1                 TO 1                                
267900                                                                          
268000     PERFORM 24141-BUBBLE-SORT                                            
268100       UNTIL PT-STR-IDX-1 NOT < PT-LAST-STR-IDX.                          
268200                                                                          
268300 24141-BUBBLE-SORT.                                                       
268400                                                                          
268500     SET PT-STR-IDX-2                 TO PT-STR-IDX-1                     
268600     SET PT-STR-IDX-2 UP BY 1                                             
268700                                                                          
268800     PERFORM 24142-LOOP-BUBBLE-SORT                                       
268900       UNTIL PT-STR-IDX-2 > PV-STR-SUB.                                   
269000                                                                          
269100     SET PT-STR-IDX-1 UP BY 1.                                            
269200                                                                          
269300 24142-LOOP-BUBBLE-SORT.                                                  
269400                                                                          
269500     IF (PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-2) =                           
269600         PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1))                            
269700                              AND                                         
269800        (PT-STR-RNK-SEQ-NBR    (PT-STR-IDX-2) <                           
269900         PT-STR-RNK-SEQ-NBR    (PT-STR-IDX-1))                            
270000                                                                          
270100         MOVE PT-STORE-DATA (PT-STR-IDX-1) TO PV-TEMP                     
270200         MOVE PT-STORE-DATA (PT-STR-IDX-2) TO                             
270300              PT-STORE-DATA (PT-STR-IDX-1)                                
270400         MOVE PV-TEMP                   TO                                
270500              PT-STORE-DATA (PT-STR-IDX-2)                                
270600     END-IF.                                                              
270700                                                                          
270800     SET PT-STR-IDX-2 UP BY 1.                                            
270900                                                                          
271000 24200-MA-SEQ-CODE-TOTALS.                                                
271100******************************************************************        
271200*   COMPUTE TOTAL OPEN-TO-RECEIVE FOR FILL FIRST, STANDARD,               
271300*   FILL LAST AND CENTRAL STOCK LOCATIONS.                                
271400******************************************************************        
271500                                                                          
271600     MOVE  ZERO                       TO PV-FILL-FIRST-OTR-UNITS          
271700                                         PV-FILL-FIRST-OTR-PACKS          
271800                                         PV-FILL-FIRST-OTR-TOTALS         
271900                                         PV-STANDARD-OTR-UNITS            
272000                                         PV-STANDARD-OTR-PACKS            
272100                                         PV-STANDARD-OTR-TOTALS           
272200                                         PV-FILL-LAST-OTR-UNITS           
272300                                         PV-FILL-LAST-OTR-PACKS           
272400                                         PV-FILL-LAST-OTR-TOTALS          
272500                                         PV-CSTOCK-OTR-UNITS              
272600                                         PV-CSTOCK-OTR-PACKS              
272700                                         PV-CSTOCK-OTR-TOTALS.            
272800                                                                          
272900                                                                          
273000                                                                          
273100     PERFORM 24210-COMP-ALLOC-SEQ-AMTS                                    
273200       VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
273300       UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX.                              
273400                                                                          
273500 24210-COMP-ALLOC-SEQ-AMTS.                                               
273600******************************************************************        
273700*    THIS PARAGRAPH ADDS UP OTR QUANTITIES BY ALLOC SEQ CODE              
273800******************************************************************        
273900                                                                          
274000     EVALUATE PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1)                         
274100     WHEN '1'                                                             
274200          COMPUTE PV-FILL-FIRST-OTR-TOTALS                                
274300               =  PV-FILL-FIRST-OTR-TOTALS                                
274400               +  PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1)                     
274500                                                                          
274600     WHEN '2'                                                             
274700          COMPUTE PV-STANDARD-OTR-TOTALS                                  
274800               =  PV-STANDARD-OTR-TOTALS                                  
274900               +  PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1)                     
275000                                                                          
275100     WHEN '3'                                                             
275200          COMPUTE PV-FILL-LAST-OTR-TOTALS                                 
275300               =  PV-FILL-LAST-OTR-TOTALS                                 
275400               +  PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1)                     
275500                                                                          
275600     WHEN '4'                                                             
275700          COMPUTE PV-CSTOCK-OTR-TOTALS                                    
275800               =  PV-CSTOCK-OTR-TOTALS                                    
275900               +  PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1)                     
276000     END-EVALUATE.                                                        
276100                                                                          
276200 24300-ALLOCATE-OTR-QUANTITES.                                            
276300******************************************************************        
276400*    THIS PARAGRAPH CALLS 4310 TO ALLOCATE OTR QUANTITIES.                
276500******************************************************************        
276600                                                                          
276700     PERFORM 24310-LOOP-ALLOC-OTR-AMOUNTS                                 
276800       VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
276900       UNTIL PV-LEFT-TO-ALLOC <= ZERO                                     
277000          OR PT-STR-IDX-1 > PT-LAST-STR-IDX.                              
277100                                                                          
277200 24310-LOOP-ALLOC-OTR-AMOUNTS.                                            
277300******************************************************************        
277400*    THIS PARAGRAPH ALLOCS THE OTR QUANTITIES TO EACH STORE               
277500******************************************************************        
277600                                                                          
277700     PERFORM 29999-CONVERT-TO-WHOLE-UNITS.                                
277800                                                                          
277900     MOVE PV-WHOLE-UNITS              TO                                  
278000                          PT-STR-ALLOC-TOTALS (PT-STR-IDX-1)              
278100                                                                          
278200     SUBTRACT PV-WHOLE-UNITS  FROM PV-LEFT-TO-ALLOC.                      
278300/                                                                         
278400 24400-ALLOCATE-SHORTAGE.                                                 
278500******************************************************************        
278600*  THIS PARAGRAPH CALLS PARAGRAPHS TO ALLOCATE A SHORTAGE                 
278700******************************************************************        
278800                                                                          
278900      IF  RC809-AD-YES                                                    
279000          PERFORM  25000-ALLOCATE-PICTURED-AD                             
279100      END-IF                                                              
279200                                                                          
279300     IF PV-LEFT-TO-ALLOC > ZERO                                           
279400         IF PV-LEFT-TO-ALLOC NOT < PV-FILL-FIRST-OTR-TOTALS               
279500             PERFORM  24410-FILL-FIRST-OTR-QUANTITES                      
279600                                                                          
279700             IF PV-LEFT-TO-ALLOC NOT < PV-STANDARD-OTR-TOTALS             
279800                 PERFORM  24510-STANDARD-OTR-QUANTITES                    
279900                                                                          
280000            IF PV-LEFT-TO-ALLOC NOT < PV-FILL-LAST-OTR-TOTALS             
280100               PERFORM  24610-FILL-LAST-OTR-QUANTITES                     
280200                                                                          
280300               IF PV-LEFT-TO-ALLOC NOT < PV-CSTOCK-OTR-TOTALS             
280400                  PERFORM  24710-CSTOCK-OTR-QUANTITES                     
280500               ELSE                                                       
280600                  PERFORM  24750-PRORATE-CSTOCK-QTYS                      
280700               END-IF                                                     
280800                                                                          
280900            ELSE                                                          
281000               PERFORM  24650-PRORATE-FILL-LAST-QTYS                      
281100            END-IF                                                        
281200         ELSE                                                             
281300            PERFORM  24550-PRORATE-STANDARD-QTYS                          
281400         END-IF                                                           
281500       ELSE                                                               
281600         PERFORM  24450-PRORATE-FILL-FIRST-QTYS                           
281700       END-IF                                                             
281800      END-IF.                                                             
281900                                                                          
282000 24410-FILL-FIRST-OTR-QUANTITES.                                          
282100******************************************************************        
282200*    THIS PARAGRAPH CALLS 4420 TO ALLOCATE FILL FIRST OTR QTYS.           
282300******************************************************************        
282400                                                                          
282500     PERFORM  24420-LOOP-ALLOC-FILL-FIRST                                 
282600       VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
282700       UNTIL  PT-STR-IDX-1 > PT-LAST-STR-IDX                              
282800          OR  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) > '1'.                 
282900                                                                          
283000 24420-LOOP-ALLOC-FILL-FIRST.                                             
283100******************************************************************        
283200*    THIS PARAGRAPH ALLOCS THE OTR QTYS TO EACH FILL FIRST STORE          
283300******************************************************************        
283400                                                                          
283500     PERFORM 29999-CONVERT-TO-WHOLE-UNITS.                                
283600                                                                          
283700      IF PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) = '1'                       
283800         ADD PV-WHOLE-UNITS           TO                                  
283900                        PT-STR-ALLOC-TOTALS  (PT-STR-IDX-1)               
284000         SUBTRACT PV-WHOLE-UNITS      FROM PV-LEFT-TO-ALLOC               
284100      END-IF.                                                             
284200                                                                          
284300 24450-PRORATE-FILL-FIRST-QTYS.                                           
284400******************************************************************        
284500*    THIS PARAGRAPH CALLS 4460 TO CALC/ROUND ALLOC QUANTITIES.            
284600******************************************************************        
284700                                                                          
284800     IF  PV-LEFT-TO-ALLOC  >  ZERO                                        
284900         SET PT-STR-IDX-1             TO 1                                
285000         MOVE  ZERO                   TO PV-TOTAL-ALLOCATED               
285100                                                                          
285200         PERFORM  24460-LOOP-CALC-FILL-FIRST                              
285300           UNTIL  PT-STR-IDX-1 > PV-STR-SUB                               
285400              OR  PV-FILL-FIRST-OTR-TOTALS  <  1                          
285500              OR  PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) > '1'               
285600                                                                          
285700         IF  PV-TOTAL-ALLOCATED  NOT =  PV-LEFT-TO-ALLOC                  
285800             MOVE  '1'         TO  PV-HOLD-ALLOC-SEQ-CODE                 
285900             PERFORM  24810-BALANCE-MA-PRORATION                          
286000         END-IF                                                           
286100                                                                          
286200         MOVE  ZERO                   TO PV-LEFT-TO-ALLOC                 
286300                                                                          
286400     END-IF.                                                              
286500                                                                          
286600 24460-LOOP-CALC-FILL-FIRST.                                              
286700******************************************************************        
286800*    THIS PARAGRAPH CALCS/ROUNDS ALLOC QTYS FOR FILL FIRST STORES         
286900******************************************************************        
287000                                                                          
287100      IF  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) = '1'                      
287200          COMPUTE  PV-ALLOC-TOTAL                                         
287300              =  ((PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1)                    
287400              /    PV-FILL-FIRST-OTR-TOTALS) * PV-LEFT-TO-ALLOC)          
287500              /  PV-DRIVER-RATIO-QTY                                      
287600          PERFORM 24800-ROUND-PACKS                                       
287700          COMPUTE  PV-TOTAL-ALLOCATED                                     
287800              =    PV-TOTAL-ALLOCATED                                     
287900              +    PV-ALLOC-TOTAL                                         
288000      END-IF                                                              
288100                                                                          
288200      SET PT-STR-IDX-1 UP BY 1.                                           
288300                                                                          
288400 24510-STANDARD-OTR-QUANTITES.                                            
288500******************************************************************        
288600*    THIS PARAGRAPH CALLS 4520 TO ALLOCATE STANDARD OTR QTYS              
288700******************************************************************        
288800                                                                          
288900                                                                          
289000     PERFORM  24520-LOOP-ALLOC-STANDARD                                   
289100       VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
289200       UNTIL  PT-STR-IDX-1 > PV-STR-SUB                                   
289300          OR  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) > '2'.                 
289400                                                                          
289500 24520-LOOP-ALLOC-STANDARD.                                               
289600******************************************************************        
289700*    THIS PARAGRAPH ALLOCS THE OTR QTYS TO EACH STANDARD STORE            
289800******************************************************************        
289900                                                                          
290000      IF PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) = '2'                       
290100         PERFORM 29999-CONVERT-TO-WHOLE-UNITS                             
290200         ADD PV-WHOLE-UNITS           TO                                  
290300                        PT-STR-ALLOC-TOTALS (PT-STR-IDX-1)                
290400         SUBTRACT PV-WHOLE-UNITS      FROM PV-LEFT-TO-ALLOC               
290500      END-IF.                                                             
290600/                                                                         
290700 24550-PRORATE-STANDARD-QTYS.                                             
290800******************************************************************        
290900*    THIS PARAGRAPH CALLS 4560 TO CALC/ROUND ALLOC QUANTITIES.            
291000******************************************************************        
291100                                                                          
291200     IF PV-LEFT-TO-ALLOC  >  ZERO                                         
291300         MOVE  ZERO                   TO PV-TOTAL-ALLOCATED               
291400                                                                          
291500         PERFORM  24560-LOOP-CALC-STANDARD                                
291600           VARYING PT-STR-IDX-1 FROM 1 BY 1                               
291700           UNTIL  PT-STR-IDX-1 > PV-STR-SUB                               
291800              OR  PV-STANDARD-OTR-TOTALS  <  1                            
291900              OR  PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) > '2'               
292000                                                                          
292100         IF  PV-TOTAL-ALLOCATED  NOT =  PV-LEFT-TO-ALLOC                  
292200             MOVE  '2'         TO  PV-HOLD-ALLOC-SEQ-CODE                 
292300             PERFORM 24810-BALANCE-MA-PRORATION                           
292400         END-IF                                                           
292500                                                                          
292600         MOVE  ZERO                   TO PV-LEFT-TO-ALLOC                 
292700                                                                          
292800     END-IF.                                                              
292900                                                                          
293000 24560-LOOP-CALC-STANDARD.                                                
293100******************************************************************        
293200*    THIS PARAGRAPH CALCS/ROUNDS ALLOC QTYS FOR STANDARD STORES           
293300******************************************************************        
293400                                                                          
293500      IF PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) = '2'                        
293600         COMPUTE  PV-ALLOC-TOTAL                                          
293700              =  ((PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1)                    
293800              /    PV-STANDARD-OTR-TOTALS) * PV-LEFT-TO-ALLOC)            
293900              /  PV-DRIVER-RATIO-QTY                                      
294000            ON SIZE ERROR                                                 
294100            MOVE ZERO                 TO  PV-ALLOC-TOTAL                  
294200         END-COMPUTE                                                      
294300                                                                          
294400          PERFORM 24800-ROUND-PACKS                                       
294500                                                                          
294600          COMPUTE  PV-TOTAL-ALLOCATED                                     
294700              =    PV-TOTAL-ALLOCATED                                     
294800              +    PV-ALLOC-TOTAL                                         
294900      END-IF.                                                             
295000                                                                          
295100 24610-FILL-LAST-OTR-QUANTITES.                                           
295200******************************************************************        
295300*    THIS PARAGRAPH CALLS 4620 TO ALLOCATE FILL LAST OTR QTYS.            
295400******************************************************************        
295500                                                                          
295600     SET PT-STR-IDX-1                 TO 1                                
295700                                                                          
295800     PERFORM  24620-LOOP-ALLOC-FILL-LAST                                  
295900       UNTIL  PT-STR-IDX-1 > PV-STR-SUB                                   
296000          OR  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) > '3'.                 
296100                                                                          
296200 24620-LOOP-ALLOC-FILL-LAST.                                              
296300******************************************************************        
296400*    THIS PARAGRAPH ALLOCS THE OTR QTYS TO EACH FILL LAST STORE           
296500******************************************************************        
296600                                                                          
296700      IF  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) = '3'                      
296800             PERFORM 29999-CONVERT-TO-WHOLE-UNITS                         
296900             ADD PV-WHOLE-UNITS       TO                                  
297000                      PT-STR-ALLOC-TOTALS (PT-STR-IDX-1)                  
297100             SUBTRACT PV-WHOLE-UNITS FROM PV-LEFT-TO-ALLOC                
297200      END-IF                                                              
297300                                                                          
297400      SET PT-STR-IDX-1 UP BY 1.                                           
297500                                                                          
297600 24650-PRORATE-FILL-LAST-QTYS.                                            
297700******************************************************************        
297800*    THIS PARAGRAPH CALLS 4660 TO CALC/ROUND ALLOC QUANTITIES.            
297900******************************************************************        
298000                                                                          
298100     IF  PV-LEFT-TO-ALLOC  >  ZERO                                        
298200         SET PT-STR-IDX-1             TO 1                                
298300         MOVE  ZERO                   TO PV-TOTAL-ALLOCATED               
298400                                                                          
298500         PERFORM  24660-LOOP-CALC-FILL-LAST                               
298600           UNTIL  PT-STR-IDX-1 > PV-STR-SUB                               
298700              OR  PV-FILL-LAST-OTR-TOTALS  <  1                           
298800              OR  PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) > '3'               
298900                                                                          
299000         IF  PV-TOTAL-ALLOCATED  NOT =  PV-LEFT-TO-ALLOC                  
299100             MOVE  '3'         TO  PV-HOLD-ALLOC-SEQ-CODE                 
299200             PERFORM 24810-BALANCE-MA-PRORATION                           
299300         END-IF                                                           
299400                                                                          
299500         MOVE  ZERO                   TO PV-LEFT-TO-ALLOC                 
299600                                                                          
299700     END-IF.                                                              
299800                                                                          
299900 24660-LOOP-CALC-FILL-LAST.                                               
300000******************************************************************        
300100*    THIS PARAGRAPH CALCS/ROUNDS ALLOC QTYS FOR FILL LAST STORES          
300200******************************************************************        
300300                                                                          
300400      IF  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) = '3'                      
300500          COMPUTE  PV-ALLOC-TOTAL                                         
300600              =  ((PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1)                    
300700              /    PV-FILL-LAST-OTR-TOTALS) * PV-LEFT-TO-ALLOC)           
300800              /  PV-DRIVER-RATIO-QTY                                      
300900          PERFORM 24800-ROUND-PACKS                                       
301000          COMPUTE  PV-TOTAL-ALLOCATED                                     
301100              =    PV-TOTAL-ALLOCATED                                     
301200              +    PV-ALLOC-TOTAL                                         
301300      END-IF                                                              
301400                                                                          
301500      SET PT-STR-IDX-1 UP BY 1.                                           
301600                                                                          
301700 24710-CSTOCK-OTR-QUANTITES.                                              
301800******************************************************************        
301900*    THIS PARAGRAPH CALLS 4720 TO ALLOCATE CENTRAL STK OTR QTYS.          
302000******************************************************************        
302100                                                                          
302200     SET PT-STR-IDX-1                 TO 1                                
302300                                                                          
302400     PERFORM 24720-LOOP-ALLOC-CSTOCK                                      
302500       UNTIL  PT-STR-IDX-1 > PV-STR-SUB                                   
302600          OR  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) > '4'.                 
302700                                                                          
302800 24720-LOOP-ALLOC-CSTOCK.                                                 
302900******************************************************************        
303000*    THIS PARAGRAPH ALLOCS THE OTR QTYS TO EACH CENTRAL STOCK             
303100*    LOCATION.                                                            
303200******************************************************************        
303300                                                                          
303400      IF  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) = '4'                      
303500         PERFORM 29999-CONVERT-TO-WHOLE-UNITS                             
303600         ADD PV-WHOLE-UNITS           TO                                  
303700                     PT-STR-ALLOC-TOTALS  (PT-STR-IDX-1)                  
303800         SUBTRACT PV-WHOLE-UNITS FROM PV-LEFT-TO-ALLOC                    
303900      END-IF                                                              
304000                                                                          
304100      SET PT-STR-IDX-1 UP BY 1.                                           
304200                                                                          
304300 24750-PRORATE-CSTOCK-QTYS.                                               
304400******************************************************************        
304500*    THIS PARAGRAPH CALLS 4760 TO CALC/ROUND ALLOC QUANTITIES.            
304600******************************************************************        
304700                                                                          
304800     IF  PV-LEFT-TO-ALLOC  >  ZERO                                        
304900         SET PT-STR-IDX-1             TO 1                                
305000         MOVE  ZERO                   TO PV-TOTAL-ALLOCATED               
305100                                                                          
305200         PERFORM 24760-LOOP-CALC-CSTOCK                                   
305300           UNTIL  PT-STR-IDX-1 > PV-STR-SUB                               
305400              OR  PV-CSTOCK-OTR-TOTALS  <  1                              
305500              OR  PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) > '4'               
305600                                                                          
305700         IF  PV-TOTAL-ALLOCATED  NOT =  PV-LEFT-TO-ALLOC                  
305800             MOVE  '4'                TO PV-HOLD-ALLOC-SEQ-CODE           
305900             PERFORM 24810-BALANCE-MA-PRORATION                           
306000         END-IF                                                           
306100                                                                          
306200         MOVE  ZERO                   TO  PV-LEFT-TO-ALLOC                
306300                                                                          
306400     END-IF.                                                              
306500                                                                          
306600 24760-LOOP-CALC-CSTOCK.                                                  
306700******************************************************************        
306800*    THIS PARAGRAPH CALCS/ROUNDS ALLOC QTYS FOR CENTRAL STOCK             
306900******************************************************************        
307000                                                                          
307100      IF  PT-STR-ALLOC-SEQ-CDE  (PT-STR-IDX-1) = '4'                      
307200          COMPUTE  PV-ALLOC-TOTAL                                         
307300              =  ((PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1)                    
307400              /    PV-CSTOCK-OTR-TOTALS) * PV-LEFT-TO-ALLOC)              
307500              /  PV-DRIVER-RATIO-QTY                                      
307600          PERFORM 24800-ROUND-PACKS                                       
307700          COMPUTE  PV-TOTAL-ALLOCATED                                     
307800              =    PV-TOTAL-ALLOCATED                                     
307900              +    PV-ALLOC-TOTAL                                         
308000      END-IF                                                              
308100                                                                          
308200      SET PT-STR-IDX-1 UP BY 1.                                           
308300                                                                          
308400 24800-ROUND-PACKS.                                                       
308500******************************************************************        
308600*    THIS PARAGRAPH ROUNDS ALLOCS TO THE NEAREST PACK BASED UPON          
308700*    THE MA PACK/SIZE ROUNDING RULE                                       
308800******************************************************************        
308900                                                                          
309000                                                                          
309100     MOVE  PV-ALLOC-TOTAL             TO PV-ALLOC-TOTAL-INTEGER.          
309200     MOVE  PV-ALLOC-TOTAL             TO PV-ALLOC-TOTAL-DECIMAL.          
309300                                                                          
309400     EVALUATE RC808-PK-SIZE-RND-CDE(RC808-LINE-IDX)                       
309500     WHEN '1'                                                             
309600         IF  PV-ALLOC-TOTAL-DECIMAL  NOT <  PC-10-PERCENT                 
309700             ADD  1                   TO PV-ALLOC-TOTAL-INTEGER           
309800         END-IF                                                           
309900                                                                          
310000     WHEN '2'                                                             
310100         IF  PV-ALLOC-TOTAL-DECIMAL  NOT <  PC-20-PERCENT                 
310200             ADD  1                   TO PV-ALLOC-TOTAL-INTEGER           
310300         END-IF                                                           
310400                                                                          
310500     WHEN '3'                                                             
310600         IF  PV-ALLOC-TOTAL-DECIMAL  NOT <  PC-30-PERCENT                 
310700             ADD  1                   TO PV-ALLOC-TOTAL-INTEGER           
310800         END-IF                                                           
310900                                                                          
311000     WHEN '4'                                                             
311100         IF  PV-ALLOC-TOTAL-DECIMAL  NOT <  PC-40-PERCENT                 
311200             ADD  1                   TO PV-ALLOC-TOTAL-INTEGER           
311300         END-IF                                                           
311400                                                                          
311500     WHEN '5'                                                             
311600         IF  PV-ALLOC-TOTAL-DECIMAL  NOT <  PC-50-PERCENT                 
311700             ADD  1                   TO PV-ALLOC-TOTAL-INTEGER           
311800         END-IF                                                           
311900                                                                          
312000     WHEN '6'                                                             
312100         IF  PV-ALLOC-TOTAL-DECIMAL  NOT <  PC-60-PERCENT                 
312200             ADD  1                   TO PV-ALLOC-TOTAL-INTEGER           
312300         END-IF                                                           
312400                                                                          
312500     WHEN '7'                                                             
312600         IF  PV-ALLOC-TOTAL-DECIMAL  NOT <  PC-70-PERCENT                 
312700             ADD  1                   TO PV-ALLOC-TOTAL-INTEGER           
312800         END-IF                                                           
312900                                                                          
313000     WHEN '8'                                                             
313100         IF  PV-ALLOC-TOTAL-DECIMAL  NOT <  PC-80-PERCENT                 
313200             ADD  1                   TO PV-ALLOC-TOTAL-INTEGER           
313300         END-IF                                                           
313400                                                                          
313500     WHEN '9'                                                             
313600         IF  PV-ALLOC-TOTAL-DECIMAL  NOT <  PC-90-PERCENT                 
313700             ADD  1                   TO PV-ALLOC-TOTAL-INTEGER           
313800         END-IF                                                           
313900                                                                          
314000     WHEN 'U'                                                             
314100         IF  PV-ALLOC-TOTAL-DECIMAL  NOT =  ZERO                          
314200             ADD  1                   TO PV-ALLOC-TOTAL-INTEGER           
314300         END-IF                                                           
314400                                                                          
314500     WHEN 'R'                                                             
314600         IF  PV-ALLOC-TOTAL-DECIMAL  >  PC-49-PERCENT                     
314700             ADD  1                   TO PV-ALLOC-TOTAL-INTEGER           
314800         END-IF                                                           
314900                                                                          
315000     END-EVALUATE.                                                        
315100                                                                          
315200     IF PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) = '4'                         
315300        COMPUTE PV-ALLOC-TOTAL                                            
315400            =   PV-ALLOC-TOTAL-INTEGER  *  PV-DRIVER-RATIO-QTY            
315500     ELSE                                                                 
315600        COMPUTE PV-ALLOC-TOTAL                                            
315700            =   PV-ALLOC-TOTAL-INTEGER                                    
315800              *  PV-DRIVER-RATIO-QTY                                      
315900     END-IF                                                               
316000                                                                          
316100                                                                          
316200     COMPUTE  PT-STR-ALLOC-TOTALS  (PT-STR-IDX-1)                         
316300         =    PT-STR-ALLOC-TOTALS  (PT-STR-IDX-1)                         
316400         +    PV-ALLOC-TOTAL.                                             
316500                                                                          
316600 24810-BALANCE-MA-PRORATION.                                              
316700******************************************************************        
316800*  THIS PARAGRAPH CALLS ROUTINES WHICH BALANCE THE ALLOC IN TOTAL         
316900******************************************************************        
317000                                                                          
317100     SET PV-STR-SUB                   TO PT-LAST-STR-IDX                  
317200                                                                          
317300     COMPUTE  PV-OVER-SHORT-TOTALS                                        
317400         =    PV-LEFT-TO-ALLOC    -  PV-TOTAL-ALLOCATED                   
317500                                                                          
317600     IF  PV-HOLD-ALLOC-SEQ-CODE  =  '4'                                   
317700         DIVIDE      PV-DRIVER-RATIO-QTY                                  
317800           INTO      PV-OVER-SHORT-TOTALS                                 
317900         GIVING      PV-OVER-SHORT-PACKS                                  
318000     ELSE                                                                 
318100         DIVIDE  PV-DRIVER-RATIO-QTY                                      
318200           INTO      PV-OVER-SHORT-TOTALS                                 
318300         GIVING      PV-OVER-SHORT-PACKS                                  
318400     END-IF                                                               
318500                                                                          
318600     IF PV-OVER-SHORT-TOTALS  NOT =  ZERO                                 
318700         IF PV-OVER-SHORT-PACKS  >  ZERO                                  
318800             SET PT-STR-IDX-1         TO 1                                
318900             MOVE 1                   TO PV-LOOP-COUNT                    
319000             IF  PV-OVER-SHORT-PACKS  >  ZERO                             
319100                 PERFORM 24820-LOOP-ALLOC-MORE-PACKS                      
319200                     VARYING PT-STR-IDX-1 FROM 1 BY 1                     
319300                     UNTIL PV-OVER-SHORT-PACKS  <  1                      
319400             END-IF                                                       
319500             MOVE  1                  TO PV-LOOP-COUNT                    
319600      ELSE                                                                
319700         SET PT-STR-IDX-1             TO PV-STR-SUB                       
319800         IF PV-OVER-SHORT-PACKS  <  ZERO                                  
319900             MOVE  1                  TO PV-LOOP-COUNT                    
320000             PERFORM 24840-LOOP-SUBTRACT-PACKS                            
320100               VARYING PT-STR-IDX-1 FROM PT-LAST-STR-IDX BY -1            
320200               UNTIL PV-OVER-SHORT-PACKS  >  -1                           
320300         END-IF                                                           
320400      END-IF                                                              
320500     END-IF.                                                              
320600                                                                          
320700 24820-LOOP-ALLOC-MORE-PACKS.                                             
320800******************************************************************        
320900*    THIS PARAGRAPH ALLOCATES ADDITIONAL PACKS                            
321000******************************************************************        
321100                                                                          
321200     IF PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1)                               
321300                         =  PV-HOLD-ALLOC-SEQ-CODE                        
321400         IF RC808-AD-YES (RC808-LINE-IDX)                                 
321500             COMPUTE  PV-REMAINING-STR-OTR                                
321600               EQUAL  PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1)                 
321700                  -   PT-STR-ALLOC-TOTALS  (PT-STR-IDX-1)                 
321800                  +   PV-DRIVER-RATIO-QTY                                 
321900          ELSE                                                            
322000             COMPUTE  PV-REMAINING-STR-OTR                                
322100               EQUAL  PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1)                 
322200                  -   PT-STR-ALLOC-TOTALS  (PT-STR-IDX-1)                 
322300          END-IF                                                          
322400                                                                          
322500         IF PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) = '4'                     
322600             IF  PV-REMAINING-STR-OTR  >  PV-DRIVER-RATIO-QTY             
322700               OR PV-REMAINING-STR-OTR  =  PV-DRIVER-RATIO-QTY            
322800               OR PV-LOOP-COUNT  > (PV-STR-SUB * 2)                       
322900                 ADD PV-DRIVER-RATIO-QTY                                  
323000                                      TO PT-STR-ALLOC-TOTALS              
323100                                         (PT-STR-IDX-1)                   
323200                 SUBTRACT  1  FROM  PV-OVER-SHORT-PACKS                   
323300             END-IF                                                       
323400                                                                          
323500         ELSE                                                             
323600                                                                          
323700             IF  PV-REMAINING-STR-OTR  NOT < PV-DRIVER-RATIO-QTY          
323800                     OR PV-LOOP-COUNT  > (PV-STR-SUB * 2)                 
323900                 ADD  PV-DRIVER-RATIO-QTY                                 
324000                                      TO PT-STR-ALLOC-TOTALS              
324100                                         (PT-STR-IDX-1)                   
324200                 SUBTRACT  1  FROM  PV-OVER-SHORT-PACKS                   
324300             END-IF                                                       
324400           END-IF                                                         
324500     END-IF.                                                              
324600                                                                          
324700     ADD  1                           TO PV-LOOP-COUNT.                   
324800/                                                                         
324900 24840-LOOP-SUBTRACT-PACKS.                                               
325000*    THIS PARAGRAPH SUBTRACTS PACKS TO BALANCE THE ALLOCATION             
325100*    START FROM THE END OF THE ARRAY AND WORK BACKWARDS                   
325200                                                                          
325300                                                                          
325400     IF  RC808-AD-YES (RC808-LINE-IDX)                                    
325500         AND PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) NOT = '4'                
325600         AND PV-LOOP-COUNT  <  (PV-STR-SUB * 2)                           
325700         COMPUTE   PV-OVER-SHORT-AMT                                      
325800             =     PT-STR-ALLOC-TOTALS  (PT-STR-IDX-1)                    
325900             -     PV-DRIVER-RATIO-QTY                                    
326000     ELSE                                                                 
326100         MOVE  PT-STR-ALLOC-TOTALS  (PT-STR-IDX-1)                        
326200                                      TO PV-OVER-SHORT-AMT                
326300     END-IF                                                               
326400                                                                          
326500     IF PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1)                               
326600                               = PV-HOLD-ALLOC-SEQ-CODE                   
326700         AND PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) NOT = '4'                
326800         IF PV-OVER-SHORT-AMT  NOT < PV-DRIVER-RATIO-QTY                  
326900             SUBTRACT PV-DRIVER-RATIO-QTY                                 
327000                   FROM PT-STR-ALLOC-TOTALS (PT-STR-IDX-1)                
327100             ADD  1                   TO PV-OVER-SHORT-PACKS              
327200        END-IF                                                            
327300                                                                          
327400     ELSE                                                                 
327500                                                                          
327600        IF PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1)                            
327700                                = PV-HOLD-ALLOC-SEQ-CODE                  
327800            AND PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) = '4'                 
327900             IF PV-OVER-SHORT-AMT  NOT <  PV-DRIVER-RATIO-QTY             
328000                 SUBTRACT PV-DRIVER-RATIO-QTY                             
328100                        FROM PT-STR-ALLOC-TOTALS (PT-STR-IDX-1)           
328200                 ADD  1               TO PV-OVER-SHORT-PACKS              
328300            END-IF                                                        
328400        END-IF                                                            
328500     END-IF                                                               
328600                                                                          
328700     ADD  1                           TO PV-LOOP-COUNT.                   
328800/                                                                         
328900 24900-ALLOCATE-OVERAGE.                                                  
329000                                                                          
329100******************************************************************        
329200*    ALLOCATE OVERAGE QTYS TO EACH STORE STARTING FROM THE TOP            
329300******************************************************************        
329400                                                                          
329500     PERFORM 24910-LOOP-ALLOC-OVER-QUANTITY                               
329600       VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
329700       UNTIL PV-LEFT-TO-ALLOC  <  1.                                      
329800                                                                          
329900 24910-LOOP-ALLOC-OVER-QUANTITY.                                          
330000                                                                          
330100     PERFORM 24911-LOOP-ALLOC-OVER-QUANTITY                               
330200       VARYING PT-STR-IDX-1 FROM 1 BY 1                                   
330300       UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX.                              
330400                                                                          
330500 24911-LOOP-ALLOC-OVER-QUANTITY.                                          
330600     IF PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) NOT = '4'                     
330700         IF PV-LEFT-TO-ALLOC > PV-DRIVER-RATIO-QTY                        
330800             ADD PV-DRIVER-RATIO-QTY  TO                                  
330900                             PT-STR-ALLOC-TOTALS(PT-STR-IDX-1)            
331000             SUBTRACT PV-DRIVER-RATIO-QTY  FROM PV-LEFT-TO-ALLOC          
331100         ELSE                                                             
331200             ADD PV-LEFT-TO-ALLOC     TO PT-STR-ALLOC-TOTALS              
331300                                         (PT-STR-IDX-1)                   
331400             MOVE ZERO                TO PV-LEFT-TO-ALLOC                 
331500         END-IF                                                           
331600     END-IF                                                               
331700                                                                          
331800                                                                          
331900     IF PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) = '4'                         
332000         IF PS-STORE-NOT-IN-TABLE                                         
332100             IF PV-LEFT-TO-ALLOC > PV-DRIVER-RATIO-QTY                    
332200                 ADD PV-DRIVER-RATIO-QTY                                  
332300                                      TO PT-STR-ALLOC-TOTALS              
332400                                         (PT-STR-IDX-1)                   
332500                 SUBTRACT PV-DRIVER-RATIO-QTY                             
332600                                      FROM PV-LEFT-TO-ALLOC               
332700             ELSE                                                         
332800                 ADD PV-LEFT-TO-ALLOC TO                                  
332900                            PT-STR-ALLOC-TOTALS(PT-STR-IDX-1)             
333000                 MOVE ZERO            TO PV-LEFT-TO-ALLOC                 
333100             END-IF                                                       
333200         END-IF                                                           
333300     END-IF.                                                              
333400/                                                                         
333500 25000-ALLOCATE-PICTURED-AD.                                              
333600******************************************************************        
333700*    THIS PARAGRAPH CALLS 4490 TO ALLOCATE PICTURED AD QTYS.              
333800******************************************************************        
333900                                                                          
334000     SET PT-STR-IDX-1                 TO 1                                
334100     SET PV-STR-SUB                   TO PT-LAST-STR-IDX.                 
334200                                                                          
334300     PERFORM 25010-LOOP-ALLOC-PICTURED-AD                                 
334400          UNTIL PT-STR-IDX-1 > PT-LAST-STR-IDX                            
334500          OR PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) > '3'                    
334600          OR PV-LEFT-TO-ALLOC  <  1.                                      
334700                                                                          
334800                                                                          
334900 25010-LOOP-ALLOC-PICTURED-AD.                                            
335000******************************************************************        
335100*    THIS PARAGRAPH ALLOCS THE 1 PACK TO SATISFY PICTURED AD              
335200******************************************************************        
335300                                                                          
335400     IF  PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1) NOT = '4'                    
335500        AND                                                               
335600         PT-STR-REC-QTY (PT-STR-IDX-1) < PV-DRIVER-RATIO-QTY              
335700         IF PV-LEFT-TO-ALLOC > PV-DRIVER-RATIO-QTY                        
335800             ADD PV-DRIVER-RATIO-QTY  TO PT-STR-ALLOC-TOTALS              
335900                                         (PT-STR-IDX-1)                   
336000             SUBTRACT PV-DRIVER-RATIO-QTY                                 
336100                      FROM PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1)            
336200                           PV-LEFT-TO-ALLOC                               
336300             PERFORM 25020-SUB-FROM-CODE-BUCKETS                          
336400         ELSE                                                             
336500             ADD PV-LEFT-TO-ALLOC     TO                                  
336600                            PT-STR-ALLOC-TOTALS (PT-STR-IDX-1)            
336700             SUBTRACT PV-LEFT-TO-ALLOC                                    
336800                        FROM PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1)          
336900             MOVE ZERO                TO PV-LEFT-TO-ALLOC                 
337000         END-IF                                                           
337100     END-IF.                                                              
337200                                                                          
337300     SET PT-STR-IDX-1                 UP BY 1.                            
337400                                                                          
337500                                                                          
337600 25020-SUB-FROM-CODE-BUCKETS.                                             
337700                                                                          
337800     EVALUATE PT-STR-ALLOC-SEQ-CDE (PT-STR-IDX-1)                         
337900     WHEN '1'                                                             
338000         SUBTRACT PV-DRIVER-RATIO-QTY FROM                                
338100                  PV-FILL-FIRST-OTR-TOTALS                                
338200     WHEN '2'                                                             
338300         SUBTRACT PV-DRIVER-RATIO-QTY FROM                                
338400                  PV-STANDARD-OTR-TOTALS                                  
338500     WHEN '3'                                                             
338600         SUBTRACT PV-DRIVER-RATIO-QTY FROM                                
338700                  PV-FILL-LAST-OTR-TOTALS                                 
338800     END-EVALUATE.                                                        
338900                                                                          
339000 29999-CONVERT-TO-WHOLE-UNITS.                                            
339100     IF PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1) > PV-DRIVER-RATIO-QTY         
339200         DIVIDE PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1) BY                    
339300                    PV-DRIVER-RATIO-QTY                                   
339400         GIVING PV-WHOLE-UNITS                                            
339500     ELSE                                                                 
339600       IF PT-STR-OTR-TOTALS-MA (PT-STR-IDX-1) > 0                         
339700         MOVE 1                       TO PV-WHOLE-UNITS                   
339800       ELSE                                                               
339900         MOVE 0                       TO PV-WHOLE-UNITS                   
340000       END-IF                                                             
340100     END-IF                                                               
340200                                                                          
340300     MULTIPLY PV-DRIVER-RATIO-QTY BY PV-WHOLE-UNITS.                      
340400/                                                                         
340500     COPY RCPD001.                                                        
340600/                                                                         
