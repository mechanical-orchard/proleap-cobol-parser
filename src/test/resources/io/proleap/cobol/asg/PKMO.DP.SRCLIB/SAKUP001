      ****************************************************************  00010040
       IDENTIFICATION DIVISION.                                         00020006
      ****************************************************************  00030040
                                                                        00040006
245305 PROGRAM-ID.      SAKUP001.                                               
       AUTHOR.          DAN PAYNTER                                             
       INSTALLATION.    KOHLS DEPARTMENT STORES                                 
       DATE-WRITTEN.    07/14/11                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  MODULE TYPE:   COBOL2 DB2 BATCH.                                       
      *                                                                         
      *  DESCRIPTION: PROGRAM UPDATES THE XST_DEPT_RTL_BDRY_WRK TABLE           
      *      WITH THE DATA FROM THE PRODUCTION DEPT MAX MIN FILE                
      *                PKMO.PS.DEPT.MAX.MIN.AMOUNTS                             
      *                TKMA.PS.DEPT.MAX.MIN.AMOUNTS                             
      *========================== CHANGE LOG ===========================        
      *  AUTH      BY       DATE                 DESCRIPTION                    
      *=================================================================        
      * DAN PAYNTER       07-14-11   CREATE PROGRAM                             
      *=================================================================        
236265* JIM HUNTER        11-25-2019 RENAMING PROGRAM SHKUP001 TO               
236265*                              EPKUP001 AND MOVING IT INTO NEW            
236265*                              EPK620 JOB SO THAT SH APP ID CAN BE        
236265*                              RETIRED.  ALSO ADDED DISPLAYS OF           
236265*                              RECORDS READ AND DB2 ROWS INSERTED.        
236265*                              CHG0236265                                 
245305*=================================================================        
245305* BARB SCHULTZ      06-18-2010 RENAMING PROGRAM EPKUP001 TO               
245305*                              SAKUP001 AND MOVING IT INTO NEW            
245305*                              SAK103 JOB SO THAT ALL EP JOBS             
245305*                              UNDER TONY ALBERGO WILL BECOME SA          
245305*                              JOBS AND BE SPLIT FROM THE STORES          
245305*                              TEAM - JOE BROCHHAUSEN.                    
245305*                              CHG0245305                                 
245305******************************************************************        
      ****************************************************************  00530040
       ENVIRONMENT DIVISION.                                            00540006
      ****************************************************************  00550040
                                                                        00560006
       CONFIGURATION SECTION.                                           00570006
                                                                        00580006
       SOURCE-COMPUTER.        IBM-3090.                                00590006
                                                                        00600006
       OBJECT-COMPUTER.        IBM-3090.                                00610006
                                                                        00620006
       INPUT-OUTPUT SECTION.                                            00630006
                                                                        00640006
       FILE-CONTROL.                                                    00650006
                                                                        00660006
           SELECT DEPT-EXTRACT-FILE                                     00670006
               ASSIGN TO INP01.                                         00680006
                                                                        00690006
                                                                        00730006
      ***************************************************************** 00740040
       DATA DIVISION.                                                   00750006
      ***************************************************************** 00760040
                                                                        00770006
       FILE SECTION.                                                    00780006
                                                                        00790006
       FD  DEPT-EXTRACT-FILE                                            00800006
           RECORDING MODE IS F                                          00810006
           BLOCK CONTAINS 0 RECORDS                                     00820006
           LABEL RECORDS ARE STANDARD.                                  00830006
                                                                        00840006
       01  DEPT-EXTRACT-RECORD             PIC X(80).                   00850006
                                                                        00860006
                                                                        00930006
      ***************************************************************** 00940040
       WORKING-STORAGE SECTION.                                         00950006
      ***************************************************************** 00960040
       01  FILLER                            PIC X(50)  VALUE           00970006
245305     ' *** WORKING STORAGE STARTS HERE FOR SAKUP001 *** '.        00980006
                                                                        00990006
       01  DE-DEPT-EXTRACT-RECORD.                                              
           05  DE-DEPARTMENT                PIC  X(3)     VALUE SPACES.         
           05 FILLER REDEFINES DE-DEPARTMENT.                                   
               10 DE-DEPT-NBR               PIC 9(3).                           
           05  DE-MINIMUM-RETAIL-AMOUNT     PIC  9(5)V99  VALUE ZEROS           
                                                          COMP-3.               
           05  DE-MAXIMUM-RETAIL-AMOUNT     PIC  9(5)V99  VALUE ZEROS           
                                                          COMP-3.               
           05  FILLER                       PIC  X(69)    VALUE SPACES.         
                                                                                
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      01160006
                                                        COMP SYNC.      01170006
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    01180006
           88  AC-DB2-ERROR                             VALUE +4013.    01190006
           88  AC-CALENDAR-ROUTINE-ERROR                VALUE +4019.    01200006
                                                                        01230006
       01  PS-PROGRAM-SWITCHES.                                         01240006
           05  PS-END-OF-CARD-FILE-SW        PIC  X(01) VALUE 'N'.              
               88  PS-END-OF-CARD-FILE                  VALUE 'Y'.              
236265         88  PS-NOT-END-OF-CARD-FILE              VALUE 'N'.              
                                                                        01380006
       01  PC-CONSTANTS.                                                01390006
           05  PC-CURRENT-PROGRAM-NAME       PIC  X(08) VALUE           01440006
245305                                                  'SAKUP001'.             
                                                                        01810028
       01  PV-MISC-FIELDS.                                              01840006
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   01880048
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   01890048
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   01900048
           05  PV-DB2-OPERATION            PIC  X(30)    VALUE SPACE.   01910048
236265     05  PV-READ-COUNT               PIC  9(5)     VALUE ZEROES.  01910048
236265     05  PV-INSERT-COUNT             PIC  9(5)     VALUE ZEROES.  01910048
                                                                        03861046
      ***************************************************************** 03870040
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            03880006
      ***************************************************************** 03890040
           COPY DPWS004.                                                03900006
                                                                        03910006
      ***************************************************************** 03920040
      *  DB2 COMMUNICATION AREA.                                        03930006
      ***************************************************************** 03940040
           EXEC SQL                                                     03950006
                INCLUDE SQLCA                                           03960006
           END-EXEC.                                                    03970006
                                                                                
      ******************************************************************        
      *  DB2 TABLE XST_DEPT_RTL_BDRY_WRK - DEPT MIN MAX AMT TABLE               
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE XST00136                                                
           END-EXEC.                                                            
                                                                                
      ***************************************************************** 05723006
      * PROCEDURE DIVISION.                                           * 05730006
      ***************************************************************** 05740006
       PROCEDURE DIVISION.                                              05750006
                                                                        05760006
      ***************************************************************** 05770040
      * 0000-MAINLINE-PARAGRAPH                                         05780006
      ***************************************************************** 05800040
       0000-MAINLINE-PARAGRAPH.                                         05810006
                                                                        05820006
           PERFORM 1000-INITIALIZE-PROGRAM.                             05830006
                                                                        05840006
           PERFORM 2000-PROCESS-DEPT-RECORDS                                    
             UNTIL PS-END-OF-CARD-FILE.                                         
                                                                                
           PERFORM 8100-COMMIT-DB2.                                             
           CLOSE DEPT-EXTRACT-FILE.                                     06170006
                                                                        05900020
           DISPLAY '****************************************'.          05910020
245305     DISPLAY '*************** SAKUP001 ***************'.          05920009
           DISPLAY '****************************************'.          06030006
236265     DISPLAY 'INPUT RECORDS READ = ' PV-READ-COUNT.                       
236265     DISPLAY 'DB2 ROWS INSERTED  = ' PV-INSERT-COUNT.                     
                                                                        06050006
           STOP RUN.                                                    06060006
                                                                        06070006
      *0000-MAINLINE-EXIT.                                                      
                                                                        06080006
                                                                        06090006
      ***************************************************************** 06100040
      * 1000-INITIALIZE-PROGRAM                                         06110006
      ***************************************************************** 06140040
       1000-INITIALIZE-PROGRAM.                                         06150006
                                                                        06160006
           OPEN INPUT  DEPT-EXTRACT-FILE.                               06170006
                                                                        06190006
           PERFORM 7000-READ-DEPT-EXTRACT-FILE.                         06350006
                                                                        06210009
                                                                                
      *1000-EXIT.                                                               
                                                                                
      *****************************************************************         
      * 2000-PROCESS-DEPT-RECORDS.                                              
      *     - PROCESS AND INSERT DEPT MIN MAX RECORDS                           
      *****************************************************************         
       2000-PROCESS-DEPT-RECORDS.                                               
                                                                                
           MOVE DE-DEPT-NBR TO XST00136-DEPT-NBR.                               
                                                                                
           MOVE DE-MINIMUM-RETAIL-AMOUNT                                        
             TO XST00136-DEPT-MIN-UNT-RTL-AMT.                                  
                                                                                
           MOVE DE-MAXIMUM-RETAIL-AMOUNT                                        
             TO XST00136-DEPT-MAX-UNT-RTL-AMT.                                  
                                                                                
           PERFORM 8000-INSERT-INTO-DEPT-RTL.                                   
           PERFORM 7000-READ-DEPT-EXTRACT-FILE.                         06350006
                                                                                
                                                                                
      *2000-EXIT.                                                               
                                                                                
      ***************************************************************** 06480040
      * 7000-READ-DEPT-EXTRACT-FILE                                     06490006
      *     READ DEPT EXTRACT FILE.                                     06500006
      ***************************************************************** 06510040
       7000-READ-DEPT-EXTRACT-FILE.                                     06520006
           READ DEPT-EXTRACT-FILE INTO DE-DEPT-EXTRACT-RECORD           06530006
              AT END                                                    06540006
                 SET PS-END-OF-CARD-FILE     TO TRUE.                   06550006
                                                                                
236265     IF PS-NOT-END-OF-CARD-FILE                                           
236265         ADD 1  TO PV-READ-COUNT                                          
236265     END-IF.                                                              
                                                                                
      *7000-EXIT.                                                               
                                                                                
      *****************************************************************         
      * 8000-INSERT-INTO-DEPT-RTL.                                              
      *     INSERT ROWS INTO DEPT RTL TABLE.                                    
      *****************************************************************         
       8000-INSERT-INTO-DEPT-RTL.                                               
                                                                                
               EXEC SQL                                                         
                   INSERT  INTO XST_DEPT_RTL_BDRY_WRK                           
                        (   DEPT_NBR                                            
                           ,DEPT_MIN_UNT_RTL_AMT                                
                           ,DEPT_MAX_UNT_RTL_AMT                                
                           ,LAST_UPD_TMST)                                      
                    VALUES                                                      
                        (  :XST00136-DEPT-NBR                                   
                        ,  :XST00136-DEPT-MIN-UNT-RTL-AMT                       
                        ,  :XST00136-DEPT-MAX-UNT-RTL-AMT                       
                        ,  CURRENT TIMESTAMP)                                   
               END-EXEC.                                                        
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
236265             ADD 1  TO PV-INSERT-COUNT                                    
               WHEN SQLCODE = -803                                              
                   DISPLAY 'DUPE RECORD FOUND '                         05930006
                   DISPLAY 'XST00136-DEPT-NBR ', XST00136-DEPT-NBR              
               WHEN OTHER                                                       
                   MOVE '8000-INSERT-INTO-DEPTRTL'                              
                                             TO PV-PARAGRAPH-NAME               
                   MOVE 'ERROR ON INSERT INTO DEPTRTL'                          
                                             TO PV-DB2-OPERATION                
      *****************************************************************         
      *  MOVE CURSOR INFORMATION TO ABEND DISPLAYS                              
      *****************************************************************         
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *8000-EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
      * 8100-COMMIT-DB2                                                         
      *     COMMIT THE INSERTED ROWS IN DEPTRTL                                 
      *****************************************************************         
       8100-COMMIT-DB2.                                                         
                                                                                
           EXEC SQL                                                             
              COMMIT                                                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
              MOVE '8100-COMMIT-DB2'         TO PV-PARAGRAPH-NAME               
              MOVE 'ERROR ON DEPTRTL DB2 COMMIT'                                
                                             TO PV-DB2-OPERATION                
      *****************************************************************         
      *  MOVE CURSOR INFORMATION TO ABEND DISPLAYS                              
      *****************************************************************         
              PERFORM 9100-SQL-ABEND                                            
           END-IF.                                                              
                                                                                
      *8100-EXIT.                                                               
                                                                                
                                                                                
      ***************************************************************** 12620040
      * 9100-SQL-ABEND                                                  12630006
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.12640006
      ***************************************************************** 12650040
       9100-SQL-ABEND.                                                  12660006
           DISPLAY '** ABEND     **'.                                   12670006
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        12680006
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              12690006
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               12700006
                                                                        12710006
      ***************************************************************** 12720040
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     12730006
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      12740006
      ***************************************************************** 12750040
           COPY DPPD004.                                                12760006
                                                                        12770006
           SET AC-DB2-ERROR TO TRUE.                                    12780006
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         12790006
                                                                        12800006
