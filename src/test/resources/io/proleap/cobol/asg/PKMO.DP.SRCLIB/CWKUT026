      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT026.                                                
       AUTHOR.         CARL RICHTER.                                            
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   05/15/03.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  ITEM UPC EXTRACT PROGRAM                                      *        
      *  THIS PROGRAM CREATES A CHANGE EXTRACT FILE BY COMPARING       *        
      *  TWO GENERATIONS OF THE RMS COMMON EXTRACT FILE.               *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT RMS-CURRENT-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT RMS-PREVIOUS-FILE                                             
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT CHANGE-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  RMS-CURRENT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RMS-CURRENT-RECORD                  PIC X(250).                      
                                                                                
       FD  RMS-PREVIOUS-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RMS-PREVIOUS-RECORD                 PIC X(250).                      
                                                                                
       FD  CHANGE-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CHANGE-RECORD                   PIC   X(139).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CR-CURRENT-REC.                                                      
           05  CR-CURR-KEY-9                  PIC   9(10).                      
           05  CR-CURR-KEY-X  REDEFINES                                         
               CR-CURR-KEY-9                  PIC   X(10).                      
                                                                                
           05  CR-CURR-DATA                   PIC   X(128).                     
                                                                                
       01  PR-PREVIOUS-REC.                                                     
           05  PR-PREV-KEY-9                  PIC   9(10).                      
           05  PR-PREV-KEY-X  REDEFINES                                         
               PR-PREV-KEY-9                  PIC   X(10).                      
                                                                                
           05  PR-PREV-DATA                   PIC   X(128).                     
                                                                                
       01  CHANGE-OUTPUT-REC.                                                   
           05  COR-CHANGE-KEY                 PIC   X(10).                      
           05  COR-CHANGE-DATA                PIC   X(128).                     
           05  COR-CHANGE-INDICATOR           PIC   X(01).                      
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'CWKUT026'.            
           05  PC-OPERCO                 PIC X(02) VALUE '03'.                  
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-PRE-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-CUR-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-OUTPUT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-INSERT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-UPDATE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-DELETE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-NO-CHANGE-RECORDS      PIC  9(9)       VALUE ZERO.            
                                                                                
       01  WS-TEMP-REC.                                                         
           05  WS-TMP-UPC-ID                 PIC   9(10).                       
           05  WS-TMP-INTR-UPC-ID            PIC   9(10).                       
           05  WS-TMP-UPC-TYP-CDE            PIC   X(1).                        
           05  WS-TMP-UPC-NBR                PIC   9(15).                       
           05  WS-TMP-EDI-ORD-IND            PIC   X(1).                        
           05  WS-TMP-PREPK-IND              PIC   X(1).                        
           05  WS-TMP-CERTD-IND              PIC   X(1).                        
           05  WS-TMP-CERTD-DTE              PIC   X(10).                       
           05  WS-TMP-UPC-SRC-CDE            PIC   X(1).                        
           05  WS-TMP-KOHLS-SIZE-CDE         PIC   9(10).                       
           05  WS-TMP-KOHLS-SIZE-DESC        PIC   X(10).                       
           05  WS-TMP-NRF-CLOR-CDE           PIC   X(3).                        
           05  WS-TMP-NRF-CLOR-DESC          PIC   X(10).                       
           05  WS-TMP-SHRT-CLOR-DESC         PIC   X(4).                        
           05  WS-TMP-CSTM-CLOR-DESC         PIC   X(10).                       
           05  WS-TMP-WT-UM-CDE              PIC   X(2).                        
           05  WS-TMP-WT-UM-QTY              PIC  +9(5).9(3).                   
           05  WS-TMP-SIZE-UM-CDE            PIC   X(2).                        
           05  WS-TMP-WD-UM-QTY              PIC  +9(4).9(3).                   
           05  WS-TMP-HT-UM-QTY              PIC  +9(4).9(3).                   
           05  WS-TMP-DPTH-UM-QTY            PIC  +9(4).9(3).                   
                                                                                
      ******************************************************************        
      * INPUT FILE LAYOUT FOR RMS IM UPC EXTRACT FILE                           
      ******************************************************************        
           COPY IMRD055.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT FOR CW UPC EXTRACT FILE                              
      ******************************************************************        
           COPY CWRD006.                                                        
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL CR-CURR-KEY-X = HIGH-VALUES                                
                     AND                                                        
                     PR-PREV-KEY-X = HIGH-VALUES.                               
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  RMS-CURRENT-FILE                                         
                       RMS-PREVIOUS-FILE                                        
                OUTPUT CHANGE-FILE.                                             
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
           PERFORM 8000-READ-CURRENT.                                           
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
                                                                                
           INITIALIZE CHANGE-OUTPUT-REC                                         
                                                                                
           IF CR-CURR-KEY-9  =  PR-PREV-KEY-9                                   
               PERFORM 2100-UPDATE-OR-NOCHANGE                                  
                                                                                
           ELSE                                                                 
                                                                                
               IF CR-CURR-KEY-9  <  PR-PREV-KEY-9                               
                   PERFORM 2200-INSERT-CURRENT                                  
                                                                                
               ELSE                                                             
                   PERFORM 2300-DELETE-PREVIOUS                                 
                                                                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS RECORD FOR EQUALITY                                             
      *  IF DATA IS NOT EQUAL, THEN RECORD IS AN UPDATE                         
      ******************************************************************        
       2100-UPDATE-OR-NOCHANGE.                                                 
                                                                                
           IF CR-CURR-DATA  =  PR-PREV-DATA                                     
                                                                                
               ADD +1                 TO PA-NO-CHANGE-RECORDS                   
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
                                                                                
           ELSE                                                                 
                                                                                
               MOVE CR-CURR-KEY-9     TO COR-CHANGE-KEY                         
               MOVE CR-CURR-DATA      TO COR-CHANGE-DATA                        
               MOVE 'U'               TO COR-CHANGE-INDICATOR                   
                                                                                
               ADD +1                 TO PA-UPDATE-RECORDS                      
                                                                                
               PERFORM 7100-WRITE                                               
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  INSERT CURRENT DATA                                                    
      ******************************************************************        
       2200-INSERT-CURRENT.                                                     
                                                                                
           MOVE CR-CURR-KEY-9 TO COR-CHANGE-KEY                                 
           MOVE CR-CURR-DATA  TO COR-CHANGE-DATA                                
           MOVE 'I'           TO COR-CHANGE-INDICATOR                           
                                                                                
           ADD +1             TO PA-INSERT-RECORDS                              
                                                                                
           PERFORM 7100-WRITE                                                   
           PERFORM 8000-READ-CURRENT.                                           
                                                                                
                                                                                
      ******************************************************************        
      *  DELETE PREVIOUS DATA                                                   
      ******************************************************************        
       2300-DELETE-PREVIOUS.                                                    
                                                                                
           MOVE PR-PREV-KEY-9 TO COR-CHANGE-KEY                                 
           MOVE PR-PREV-DATA  TO COR-CHANGE-DATA                                
           MOVE 'D'           TO COR-CHANGE-INDICATOR                           
                                                                                
           ADD +1             TO PA-DELETE-RECORDS                              
                                                                                
           PERFORM 7100-WRITE                                                   
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      *  REFORMAT RMS DATA                                             *        
      ******************************************************************        
       3000-REFORMAT-RMS-DATA.                                                  
                                                                                
           INITIALIZE       CW006-UPC-RECORD.                                   
                                                                                
           MOVE IM055-UPC-ID            TO  CW006-UPC-ID-RED.                   
           MOVE IM055-INTR-UPC-ID       TO  CW006-INTR-UPC-ID-RED.              
           MOVE IM055-UPC-TYP-CDE       TO  CW006-UPC-TYP-CDE.                  
           MOVE IM055-UPC-NBR           TO  CW006-UPC-NBR-RED.                  
           MOVE IM055-EDI-ORD-IND       TO  CW006-EDI-ORD-IND.                  
           MOVE IM055-PREPK-IND         TO  CW006-PREPK-IND.                    
           MOVE IM055-CERTD-DTE         TO  CW006-CERTD-DTE.                    
           MOVE IM055-UPC-SRC-CDE       TO  CW006-UPC-SRC-CDE.                  
           MOVE IM055-KOHLS-SIZE-CDE    TO  CW006-KOHLS-SIZE-CDE-RED.           
           MOVE IM055-KOHLS-SIZE-DESC   TO  CW006-KOHLS-SIZE-DESC.              
           MOVE IM055-NRF-CLOR-CDE      TO  CW006-NRF-CLOR-CDE.                 
           MOVE IM055-NRF-CLOR-DESC     TO  CW006-NRF-CLOR-DESC.                
           MOVE IM055-SHRT-CLOR-DESC    TO  CW006-SHRT-CLOR-DESC.               
           MOVE IM055-CSTM-CLOR-DESC    TO  CW006-CSTM-CLOR-DESC.               
           MOVE IM055-WT-UM-CDE         TO  CW006-WT-UM-CDE.                    
           MOVE IM055-WT-UM-QTY         TO  CW006-WT-UM-QTY.                    
           MOVE IM055-SIZE-UM-CDE       TO  CW006-SIZE-UM-CDE.                  
           MOVE IM055-WD-UM-QTY         TO  CW006-WD-UM-QTY.                    
           MOVE IM055-HT-UM-QTY         TO  CW006-HT-UM-QTY.                    
           MOVE IM055-DPTH-UM-QTY       TO  CW006-DPTH-UM-QTY.                  
                                                                                
           IF IM055-CERTD-IND   =  SPACE                                        
              MOVE  'N'                 TO  CW006-CERTD-IND                     
           ELSE                                                                 
              MOVE IM055-CERTD-IND      TO  CW006-CERTD-IND                     
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  MOVE CWRD006 TO TEMP                                          *        
      ******************************************************************        
       4000-MOVE-TO-TEMP.                                                       
                                                                                
           INITIALIZE       WS-TEMP-REC.                                        
                                                                                
           MOVE CW006-UPC-ID            TO  WS-TMP-UPC-ID.                      
           MOVE CW006-INTR-UPC-ID       TO  WS-TMP-INTR-UPC-ID.                 
           MOVE CW006-UPC-TYP-CDE       TO  WS-TMP-UPC-TYP-CDE.                 
           MOVE CW006-UPC-NBR           TO  WS-TMP-UPC-NBR.                     
           MOVE CW006-EDI-ORD-IND       TO  WS-TMP-EDI-ORD-IND.                 
           MOVE CW006-PREPK-IND         TO  WS-TMP-PREPK-IND.                   
           MOVE CW006-CERTD-DTE         TO  WS-TMP-CERTD-DTE.                   
           MOVE CW006-UPC-SRC-CDE       TO  WS-TMP-UPC-SRC-CDE.                 
           MOVE CW006-KOHLS-SIZE-CDE    TO  WS-TMP-KOHLS-SIZE-CDE.              
           MOVE CW006-KOHLS-SIZE-DESC   TO  WS-TMP-KOHLS-SIZE-DESC.             
           MOVE CW006-NRF-CLOR-CDE      TO  WS-TMP-NRF-CLOR-CDE.                
           MOVE CW006-NRF-CLOR-DESC     TO  WS-TMP-NRF-CLOR-DESC.               
           MOVE CW006-SHRT-CLOR-DESC    TO  WS-TMP-SHRT-CLOR-DESC.              
           MOVE CW006-CSTM-CLOR-DESC    TO  WS-TMP-CSTM-CLOR-DESC.              
           MOVE CW006-WT-UM-CDE         TO  WS-TMP-WT-UM-CDE.                   
           MOVE CW006-WT-UM-QTY         TO  WS-TMP-WT-UM-QTY.                   
           MOVE CW006-SIZE-UM-CDE       TO  WS-TMP-SIZE-UM-CDE.                 
           MOVE CW006-WD-UM-QTY         TO  WS-TMP-WD-UM-QTY.                   
           MOVE CW006-HT-UM-QTY         TO  WS-TMP-HT-UM-QTY.                   
           MOVE CW006-DPTH-UM-QTY       TO  WS-TMP-DPTH-UM-QTY.                 
           MOVE CW006-CERTD-IND         TO  WS-TMP-CERTD-IND.                   
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE THE OUTPUT RECORD                                                
      ******************************************************************        
       7100-WRITE.                                                              
                                                                                
           WRITE  CHANGE-RECORD FROM CHANGE-OUTPUT-REC.                         
           ADD +1            TO PA-OUTPUT-RECORDS.                              
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE CURRENT FILE.                                        *        
      ******************************************************************        
       8000-READ-CURRENT.                                                       
                                                                                
           READ RMS-CURRENT-FILE INTO IM055-ITEM-UPC-DOMAIN-REC                 
                                                                                
               AT END                                                           
                   MOVE HIGH-VALUES  TO  CR-CURR-KEY-X                          
                                                                                
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-CUR-RECORDS                     
                                                                                
                   PERFORM 3000-REFORMAT-RMS-DATA                               
                                                                                
                   PERFORM 4000-MOVE-TO-TEMP                                    
                   MOVE WS-TEMP-REC       TO  CR-CURRENT-REC                    
                                                                                
           END-READ.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE PREVIOUS FILE.                                                
      ******************************************************************        
       8100-READ-PREVIOUS.                                                      
                                                                                
           READ RMS-PREVIOUS-FILE INTO IM055-ITEM-UPC-DOMAIN-REC                
                                                                                
               AT END                                                           
                   MOVE HIGH-VALUES  TO  PR-PREV-KEY-X                          
                                                                                
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-PRE-RECORDS                     
                                                                                
                   PERFORM 3000-REFORMAT-RMS-DATA                               
                                                                                
                   PERFORM 4000-MOVE-TO-TEMP                                    
                   MOVE WS-TEMP-REC       TO  PR-PREVIOUS-REC                   
                                                                                
           END-READ.                                                            
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
           DISPLAY '   '.                                                       
                                                                                
           DISPLAY 'INPUT RECS READ    : ' PA-INPUT-RECORDS.                    
           DISPLAY 'INPUT PREVIOUS RECS: ' PA-INPUT-PRE-RECORDS.                
           DISPLAY 'INPUT CURRENT RECS : ' PA-INPUT-CUR-RECORDS.                
           DISPLAY '   '.                                                       
           DISPLAY 'OUTPUT RECS WRITTEN: ' PA-OUTPUT-RECORDS.                   
           DISPLAY 'INSERT RECS WRITTEN: ' PA-INSERT-RECORDS.                   
           DISPLAY 'UPDATE RECS WRITTEN: ' PA-UPDATE-RECORDS.                   
           DISPLAY 'DELETE RECS WRITTEN: ' PA-DELETE-RECORDS.                   
           DISPLAY 'NO CHANGE RECORDS  : ' PA-NO-CHANGE-RECORDS.                
                                                                                
           CLOSE RMS-CURRENT-FILE                                               
                 RMS-PREVIOUS-FILE                                              
                 CHANGE-FILE.                                                   
                                                                                
      ******************************************************************        
      ******************************************************************        
