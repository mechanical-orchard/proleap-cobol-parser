       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    SDKUP025.                                                 
       AUTHOR.        JIM HUNTER                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  06-18-2019.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *    COBOL/DB2 PROGRAM                                                    
      *                                                                         
      *    LOAD SDT_STR_RCVG WITH FUTURE TRIP INFORMATION -                     
      *    STORE, TRAILER ARRIVAL DATE, TRAILER SIZE AND 'PR' FOR               
      *    VEHICLE NUMBER.                                                      
      ******************************************************************        
      *   CHANGE HISTORY                                                        
      ******************************************************************        
      *  06/18/2019  JIM HUNTER     CHG0225887   INITIAL PROGRAM                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT TRIP-FILE                                                     
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT AUDIT-FILE                                                    
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  TRIP-FILE                                                            
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 100 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS TRIP-RECORD.                                          
                                                                                
       01  TRIP-RECORD                     PIC X(100).                          
                                                                                
                                                                                
       FD  AUDIT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 100 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS TRIP-RECORD.                                          
                                                                                
       01  AUDIT-RECORD                    PIC X(100).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM                  PIC X(08) VALUE 'SDKUP025'.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARA-NAME                PIC X(25) VALUE SPACES.              
           05  PV-ABEND-NOTE               PIC X(40) VALUE SPACES.              
           05  PV-RECORDS-READ             PIC 9(07) VALUE ZEROES.              
           05  PV-ROWS-INSERTED            PIC 9(07) VALUE ZEROES.              
           05  PV-ROWS-DELETED             PIC 9(07) VALUE ZEROES.              
           05  PV-ERROR-RECORDS            PIC 9(07) VALUE ZEROES.              
           05  PV-AUDIT-RECORDS-OUT        PIC 9(07) VALUE ZEROES.              
           05  PV-NUM-ROWS-RCVG            PIC S9(9) VALUE ZEROS                
                                                     COMP SYNC.                 
           05  DATE-SUB                    PIC 99    VALUE ZEROS.               
           05  PV-DATE-MMDDYYYY            PIC X(10) VALUE SPACES.              
           05  PV-DATE-YYYYMMDDTIME        PIC X(26) VALUE SPACES.              
           05  FILLER           REDEFINES  PV-DATE-YYYYMMDDTIME.                
               10  PV-DATE-YYYYMMDD        PIC X(10).                           
               10  FILLER                  PIC X(16).                           
           05  PV-ARRIVAL-DATE-1           PIC X(10) VALUE SPACES.              
           05  PV-ARRIVAL-DATE-2           PIC X(10) VALUE SPACES.              
           05  PV-ARRIVAL-DATE-3           PIC X(10) VALUE SPACES.              
           05  PV-ARRIVAL-DATE-4           PIC X(10) VALUE SPACES.              
           05  PV-ARRIVAL-DATE-5           PIC X(10) VALUE SPACES.              
           05  PV-ARRIVAL-DATE-6           PIC X(10) VALUE SPACES.              
           05  PV-ARRIVAL-DATE-7           PIC X(10) VALUE SPACES.              
           05  PC-LOWER-CASE-LETTERS        PIC X(26)  VALUE                    
               'abcdefghijklmnopqrstuvwxyz'.                                    
           05  PC-UPPER-CASE-LETTERS        PIC X(26)  VALUE                    
               'ABCDEFGHIJKLMNOPQRSTUVWXYZ'.                                    
                                                                                
       01  PV-TRIP-RECORD               PIC X(100).                             
                                                                                
       01  PV-HDR-TRIP-RECORD.                                                  
           10 PV-HDR-STR-NBR            PIC X(5)  VALUE 'STORE'.                
           10 FILLER                    PIC X     VALUE ','.                    
           10 PV-HDR-TRLR-SIZE          PIC X(12) VALUE 'TRAILER SIZE'.         
           10 PV-HDR-TRLR-DATE-TABLE OCCURS 7 TIMES.                            
               15 FILLER                PIC X     VALUE ','.                    
               15 PV-HDR-ARRVL-DTE      PIC X(10) VALUE SPACES.                 
           10 FILLER                    PIC X(5)  VALUE SPACES.                 
                                                                                
       01  PV-DTL-TRIP-RECORD.                                                  
           10 PV-DTL-STR-NBR            PIC 9(4)  VALUE ZEROES.                 
           10 FILLER                    PIC X     VALUE ','.                    
           10 PV-DTL-TRLR-SIZE          PIC X(2)  VALUE ZEROES.                 
           10 PV-DTL-TRLR-DATE-TABLE OCCURS 7 TIMES.                            
               15 FILLER                PIC X     VALUE ','.                    
               15 PV-DTL-ARRVL-DTE      PIC X     VALUE SPACE.                  
           10 FILLER                    PIC X(79) VALUE SPACES.                 
                                                                                
       01  PV-AUDIT-RECORD                 PIC X(100).                          
                                                                                
       01  SD-TIME-STAMP                   PIC X(26)  VALUE SPACES.             
       01  FILLER REDEFINES SD-TIME-STAMP.                                      
           05  SD-CURR-DTE                 PIC  X(10).                          
           05  FILLER                      PIC  X(01).                          
           05  SD-HOUR                     PIC  X(02).                          
           05  FILLER                      PIC  X(01).                          
           05  SD-MINUTE                   PIC  X(02).                          
           05  FILLER                      PIC  X(01).                          
           05  SD-SECOND                   PIC  X(02).                          
           05  FILLER                      PIC  X(01).                          
           05  SD-MICROSEC                 PIC  X(06).                          
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE +0 COMP SYNC.          
           88  AC-OPEN-FILE-ERROR                  VALUE +4005.                 
           88  AC-CLOSE-FILE-ERROR                 VALUE +4006.                 
           88  AC-DB2-ERROR                        VALUE +4013.                 
           88  AC-DATE-ROUTINE-ERROR               VALUE +4019.                 
           88  AC-LOGIC-ERROR                      VALUE +4020.                 
                                                                                
       01  PS-END-OF-FILE-SW               PIC  X(01) VALUE 'N'.                
           88 PS-NOT-END-OF-FILE                      VALUE 'N'.                
           88 PS-END-OF-FILE                          VALUE 'Y'.                
                                                                                
       01  PS-HEADER-VALID-SW              PIC  X(01) VALUE 'N'.                
           88 PS-HEADER-VALID                         VALUE 'N'.                
           88 PS-HEADER-INVALID                       VALUE 'Y'.                
                                                                                
       01  PS-DATES-VALID-SW               PIC  X(01) VALUE 'N'.                
           88 PS-DATES-VALID                          VALUE 'N'.                
           88 PS-DATES-INVALID                        VALUE 'Y'.                
                                                                                
       01  PS-DATE-INDS-VALID-SW           PIC  X(01) VALUE 'N'.                
           88 PS-DATE-INDS-VALID                      VALUE 'N'.                
           88 PS-DATE-INDS-INVALID                    VALUE 'Y'.                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *        
      *----------------------------------------------------------------*        
           COPY DPWS004.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * DB2 MESSAGE AREA                                                        
      *----------------------------------------------------------------*        
           COPY SQLCA2.                                                         
                                                                                
      *----------------------------------------------------------------*        
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                         
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      * SDT_STR_RCVG TABLE                                                      
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE SDT00032                                                 
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           IF PS-HEADER-VALID                                                   
               PERFORM 1500-CLEAR-TABLE                                         
      **       IF PROGRAM ABENDS, TABLE WILL RETURN TO ORIGINAL STATE           
      **       PERFORM 5500-COMMIT                                              
               PERFORM 3000-READ-FILE                                           
               PERFORM 2000-PROCESS-TRIP-FILE                                   
                   UNTIL PS-END-OF-FILE                                         
               PERFORM 5500-COMMIT                                              
           ELSE                                                                 
               IF PV-RECORDS-READ > 0                                           
                   STRING '*** HEADER ERRORS - NO RECORDS PROCESSED'            
                          DELIMITED BY SIZE INTO  PV-AUDIT-RECORD               
                   PERFORM 4000-WRITE-AUDIT                                     
                   ADD +1  TO PV-ERROR-RECORDS                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 9900-END-OF-JOB.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
       1000-INITIALIZE.                                                         
      ******************************************************************        
                                                                                
           DISPLAY 'START SDKUP025'.                                            
                                                                                
           OPEN INPUT  TRIP-FILE.                                               
           OPEN OUTPUT AUDIT-FILE.                                              
                                                                                
           PERFORM 1100-GET-TIMESTAMP.                                          
                                                                                
           MOVE SPACES  TO  PV-AUDIT-RECORD.                                    
           PERFORM 4000-WRITE-AUDIT.                                            
                                                                                
           PERFORM 3000-READ-FILE.                                              
                                                                                
           IF PS-END-OF-FILE                                                    
               STRING '*** INPUT FILE IS EMPTY - NO RECORDS PROCESSED'          
                      DELIMITED BY SIZE INTO  PV-AUDIT-RECORD                   
               PERFORM 4000-WRITE-AUDIT                                         
               SET PS-HEADER-INVALID TO TRUE                                    
               ADD +1  TO PV-ERROR-RECORDS                                      
           ELSE                                                                 
               MOVE 'HEADER BEING PROCESSED CONTAINS: '                         
                     TO  PV-AUDIT-RECORD                                        
               PERFORM 4000-WRITE-AUDIT                                         
      **       EMAIL UTILITY IS LIMITED TO 80 CHARACTERS                        
               MOVE  PV-TRIP-RECORD (1:19)  TO  PV-AUDIT-RECORD                 
               PERFORM 4000-WRITE-AUDIT                                         
               MOVE  PV-TRIP-RECORD (20:80) TO  PV-AUDIT-RECORD                 
               PERFORM 4000-WRITE-AUDIT                                         
               MOVE  SPACES                 TO  PV-AUDIT-RECORD                 
               PERFORM 4000-WRITE-AUDIT                                         
               PERFORM 1800-PROCESS-HEADER-RECORD                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
       1100-GET-TIMESTAMP.                                                      
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             SET :SD-TIME-STAMP = CURRENT TIMESTAMP                             
           END-EXEC.                                                            
                                                                                
           IF SQLCODE EQUAL ZERO                                                
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '1100-GET-TIMESTAMP' TO PV-PARA-NAME                        
               MOVE 'UNSUCCESSFUL GET TIMESTAMP' TO PV-ABEND-NOTE               
               PERFORM 9000-DB2-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
       1500-CLEAR-TABLE.                                                        
      ******************************************************************        
                                                                                
           MOVE PV-HDR-ARRVL-DTE(1)     TO   PV-ARRIVAL-DATE-1.                 
           MOVE PV-HDR-ARRVL-DTE(2)     TO   PV-ARRIVAL-DATE-2.                 
           MOVE PV-HDR-ARRVL-DTE(3)     TO   PV-ARRIVAL-DATE-3.                 
           MOVE PV-HDR-ARRVL-DTE(4)     TO   PV-ARRIVAL-DATE-4.                 
           MOVE PV-HDR-ARRVL-DTE(5)     TO   PV-ARRIVAL-DATE-5.                 
           MOVE PV-HDR-ARRVL-DTE(6)     TO   PV-ARRIVAL-DATE-6.                 
           MOVE PV-HDR-ARRVL-DTE(7)     TO   PV-ARRIVAL-DATE-7.                 
                                                                                
           EXEC SQL                                                             
             SELECT COUNT(*)                                                    
               INTO :PV-NUM-ROWS-RCVG                                           
             FROM SDT_STR_RCVG                                                  
             WHERE (TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-1                         
                OR  TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-2                         
                OR  TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-3                         
                OR  TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-4                         
                OR  TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-5                         
                OR  TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-6                         
                OR  TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-7)                        
               AND  TRLR_VEH_NBR =  'PR'                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = ZERO                                               
                  MOVE PV-NUM-ROWS-RCVG  TO  PV-ROWS-DELETED                    
              WHEN SQLCODE = -904                                               
              WHEN SQLCODE = -913                                               
              WHEN SQLCODE = +100                                               
                  CONTINUE                                                      
                                                                                
              WHEN SQLWARN0 NOT = SPACES                                        
              WHEN SQLCODE < ZERO                                               
              WHEN SQLCODE > ZERO                                               
                  MOVE '1500-CLEAR-TABLE, COUNT RCVG' TO PV-PARA-NAME           
                  PERFORM 9000-DB2-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
           EXEC SQL                                                             
             DELETE FROM SDT_STR_RCVG                                           
             WHERE (TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-1                         
                OR  TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-2                         
                OR  TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-3                         
                OR  TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-4                         
                OR  TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-5                         
                OR  TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-6                         
                OR  TRLR_ARRVL_DTE = :PV-ARRIVAL-DATE-7)                        
               AND  TRLR_VEH_NBR =  'PR'                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = ZERO                                               
                  CONTINUE                                                      
              WHEN SQLCODE = -904                                               
              WHEN SQLCODE = -913                                               
              WHEN SQLCODE = +100                                               
                  CONTINUE                                                      
                                                                                
              WHEN SQLWARN0 NOT = SPACES                                        
              WHEN SQLCODE < ZERO                                               
              WHEN SQLCODE > ZERO                                               
                  MOVE '1500-CLEAR-TABLE, DELETE RCVG' TO PV-PARA-NAME          
                  PERFORM 9000-DB2-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
       1800-PROCESS-HEADER-RECORD.                                              
      ******************************************************************        
                                                                                
           INSPECT PV-TRIP-RECORD (1:5)                                         
              CONVERTING PC-LOWER-CASE-LETTERS                                  
                      TO PC-UPPER-CASE-LETTERS.                                 
           IF PV-TRIP-RECORD (1:5) =  'STORE'                                   
               UNSTRING PV-TRIP-RECORD                                          
                   DELIMITED BY ','                                             
                       INTO PV-HDR-STR-NBR                                      
                            PV-HDR-TRLR-SIZE                                    
                            PV-HDR-ARRVL-DTE (1)                                
                            PV-HDR-ARRVL-DTE (2)                                
                            PV-HDR-ARRVL-DTE (3)                                
                            PV-HDR-ARRVL-DTE (4)                                
                            PV-HDR-ARRVL-DTE (5)                                
                            PV-HDR-ARRVL-DTE (6)                                
                            PV-HDR-ARRVL-DTE (7)                                
               END-UNSTRING                                                     
               SET PS-HEADER-INVALID TO TRUE                                    
               INSPECT PV-HDR-TRLR-SIZE                                         
                  CONVERTING PC-LOWER-CASE-LETTERS                              
                          TO PC-UPPER-CASE-LETTERS                              
               IF PV-HDR-TRLR-SIZE = 'TRAILER SIZE'                             
                   SET PS-DATES-VALID TO TRUE                                   
                   PERFORM 1900-VALIDATE-DATES                                  
                       VARYING DATE-SUB FROM 1 BY 1                             
                           UNTIL DATE-SUB > 7                                   
                   IF PS-DATES-VALID                                            
                       PERFORM 1950-CHECK-FOR-DUP-DATES                         
                       IF PS-DATES-VALID                                        
                           SET PS-HEADER-VALID TO TRUE                          
                       END-IF                                                   
                   END-IF                                                       
               ELSE                                                             
                   STRING 'REC ' PV-RECORDS-READ                                
                          ' HDR TLR SIZE = ' PV-HDR-TRLR-SIZE                   
                          ' SHOULD BE = TRAILER SIZE'                           
                          DELIMITED BY SIZE INTO  PV-AUDIT-RECORD               
                   PERFORM 4000-WRITE-AUDIT                                     
               END-IF                                                           
           ELSE                                                                 
               STRING 'REC ' PV-RECORDS-READ                                    
                      ' HDR STORE = ' PV-TRIP-RECORD (1:5)                      
                      ' , SHOULD BE = STORE'                                    
                      DELIMITED BY SIZE INTO  PV-AUDIT-RECORD                   
               PERFORM 4000-WRITE-AUDIT                                         
               SET PS-HEADER-INVALID TO TRUE                                    
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
       1900-VALIDATE-DATES.                                                     
      ******************************************************************        
                                                                                
      **   CONVERT MM/DD/YYYY WITHOUT LEADING ZEROES (EX. '5/1/2019')           
      **        TO YYYY-MM-DD FORMAT ('2019-05-01')                             
           MOVE PV-HDR-ARRVL-DTE (DATE-SUB) TO PV-DATE-MMDDYYYY                 
           EXEC SQL                                                             
             SELECT TO_DATE(:PV-DATE-MMDDYYYY, 'MM/DD/YYYY')                    
               INTO :PV-DATE-YYYYMMDDTIME                                       
               FROM SYSIBM.SYSDUMMY1                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   IF PV-DATE-YYYYMMDD  < SD-CURR-DTE                           
                       STRING 'REC ' PV-RECORDS-READ                            
                              ' HDR DATE ' DATE-SUB ' = '                       
                              PV-HDR-ARRVL-DTE (DATE-SUB)                       
                              ' IS LESS THAN CURRENT DATE '                     
                              DELIMITED BY SIZE INTO  PV-AUDIT-RECORD           
                       PERFORM 4000-WRITE-AUDIT                                 
                       SET  PS-DATES-INVALID  TO TRUE                           
                   END-IF                                                       
               WHEN OTHER                                                       
                   STRING 'REC ' PV-RECORDS-READ                                
                          ' HDR DATE ' DATE-SUB ' INVALID = '                   
                          PV-HDR-ARRVL-DTE (DATE-SUB)                           
                          DELIMITED BY SIZE INTO  PV-AUDIT-RECORD               
                   PERFORM 4000-WRITE-AUDIT                                     
                   SET  PS-DATES-INVALID  TO TRUE                               
           END-EVALUATE.                                                        
                                                                                
           DISPLAY 'DATE ' DATE-SUB ' CONVERTED TO ' PV-DATE-YYYYMMDD.          
                                                                                
                                                                                
      ******************************************************************        
       1950-CHECK-FOR-DUP-DATES.                                                
      *   DUPLICATE DATES WOULD RESULT IN AN ABEND DUE TO DUP INSERTS           
      ******************************************************************        
                                                                                
           IF PV-HDR-ARRVL-DTE (1) = PV-HDR-ARRVL-DTE (2)                       
           OR PV-HDR-ARRVL-DTE (1) = PV-HDR-ARRVL-DTE (3)                       
           OR PV-HDR-ARRVL-DTE (1) = PV-HDR-ARRVL-DTE (4)                       
           OR PV-HDR-ARRVL-DTE (1) = PV-HDR-ARRVL-DTE (5)                       
           OR PV-HDR-ARRVL-DTE (1) = PV-HDR-ARRVL-DTE (6)                       
           OR PV-HDR-ARRVL-DTE (1) = PV-HDR-ARRVL-DTE (7)                       
           OR PV-HDR-ARRVL-DTE (2) = PV-HDR-ARRVL-DTE (3)                       
           OR PV-HDR-ARRVL-DTE (2) = PV-HDR-ARRVL-DTE (4)                       
           OR PV-HDR-ARRVL-DTE (2) = PV-HDR-ARRVL-DTE (5)                       
           OR PV-HDR-ARRVL-DTE (2) = PV-HDR-ARRVL-DTE (6)                       
           OR PV-HDR-ARRVL-DTE (2) = PV-HDR-ARRVL-DTE (7)                       
           OR PV-HDR-ARRVL-DTE (3) = PV-HDR-ARRVL-DTE (4)                       
           OR PV-HDR-ARRVL-DTE (3) = PV-HDR-ARRVL-DTE (5)                       
           OR PV-HDR-ARRVL-DTE (3) = PV-HDR-ARRVL-DTE (6)                       
           OR PV-HDR-ARRVL-DTE (3) = PV-HDR-ARRVL-DTE (7)                       
           OR PV-HDR-ARRVL-DTE (4) = PV-HDR-ARRVL-DTE (5)                       
           OR PV-HDR-ARRVL-DTE (4) = PV-HDR-ARRVL-DTE (6)                       
           OR PV-HDR-ARRVL-DTE (4) = PV-HDR-ARRVL-DTE (7)                       
           OR PV-HDR-ARRVL-DTE (5) = PV-HDR-ARRVL-DTE (6)                       
           OR PV-HDR-ARRVL-DTE (5) = PV-HDR-ARRVL-DTE (7)                       
           OR PV-HDR-ARRVL-DTE (6) = PV-HDR-ARRVL-DTE (7)                       
               STRING 'REC ' PV-RECORDS-READ                                    
                      ' HEADER DATE CONTAINS DUPLICATE DATE(S) '                
                      DELIMITED BY SIZE INTO  PV-AUDIT-RECORD                   
               PERFORM 4000-WRITE-AUDIT                                         
               SET  PS-DATES-INVALID  TO TRUE                                   
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
       2000-PROCESS-TRIP-FILE.                                                  
      ******************************************************************        
                                                                                
           UNSTRING PV-TRIP-RECORD                                              
               DELIMITED BY ','                                                 
                   INTO PV-DTL-STR-NBR                                          
                        PV-DTL-TRLR-SIZE                                        
                        PV-DTL-ARRVL-DTE (1)                                    
                        PV-DTL-ARRVL-DTE (2)                                    
                        PV-DTL-ARRVL-DTE (3)                                    
                        PV-DTL-ARRVL-DTE (4)                                    
                        PV-DTL-ARRVL-DTE (5)                                    
                        PV-DTL-ARRVL-DTE (6)                                    
                        PV-DTL-ARRVL-DTE (7)                                    
           END-UNSTRING.                                                        
           IF PV-DTL-STR-NBR  NOT  NUMERIC                                      
               STRING 'REC ' PV-RECORDS-READ                                    
                      '  NON-NUMERIC STORE = ' PV-DTL-STR-NBR                   
                      DELIMITED BY SIZE INTO  PV-AUDIT-RECORD                   
               PERFORM 4000-WRITE-AUDIT                                         
           END-IF.                                                              
                                                                                
           IF PV-DTL-TRLR-SIZE  NOT  NUMERIC                                    
               STRING 'REC ' PV-RECORDS-READ                                    
                      '  NON-NUMERIC TRAILER SIZE = '                           
                                              PV-DTL-TRLR-SIZE                  
                      DELIMITED BY SIZE INTO  PV-AUDIT-RECORD                   
               PERFORM 4000-WRITE-AUDIT                                         
           END-IF.                                                              
                                                                                
           SET PS-DATE-INDS-VALID TO TRUE.                                      
           PERFORM 2100-VALIDATE-DATE-INDS                                      
               VARYING DATE-SUB FROM 1 BY 1                                     
                   UNTIL DATE-SUB > 7.                                          
                                                                                
           IF  PV-DTL-STR-NBR NUMERIC                                           
           AND PV-DTL-TRLR-SIZE    NUMERIC                                      
           AND PS-DATE-INDS-VALID                                               
      **   DETAIL RECORD IS GOOD - ALL FIELDS VALIDATED                         
               PERFORM 5000-INSERT-PROCESS                                      
                   VARYING DATE-SUB FROM 1 BY 1                                 
                       UNTIL DATE-SUB > 7                                       
           ELSE                                                                 
               ADD +1  TO PV-ERROR-RECORDS                                      
               STRING 'REC ' PV-RECORDS-READ ' = '                              
                   PV-DTL-TRIP-RECORD (1:65)                                    
                   DELIMITED BY SIZE INTO  PV-AUDIT-RECORD                      
               PERFORM 4000-WRITE-AUDIT                                         
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-FILE.                                              
                                                                                
                                                                                
      ******************************************************************        
       2100-VALIDATE-DATE-INDS.                                                 
      ******************************************************************        
                                                                                
           IF PV-DTL-ARRVL-DTE (DATE-SUB) = ' ' OR 'X' or 'x'                   
               CONTINUE                                                         
           ELSE                                                                 
               STRING 'REC ' PV-RECORDS-READ                                    
                      ' DATE IND ' DATE-SUB ' INVALID (NOT X OR SPACE)'         
                      ' = ' PV-DTL-ARRVL-DTE (DATE-SUB)                         
                      DELIMITED BY SIZE INTO  PV-AUDIT-RECORD                   
               PERFORM 4000-WRITE-AUDIT                                         
               SET PS-DATE-INDS-INVALID  TO  TRUE                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
       3000-READ-FILE.                                                          
      ******************************************************************        
                                                                                
           READ TRIP-FILE INTO PV-TRIP-RECORD                                   
               AT END                                                           
                   SET PS-END-OF-FILE TO TRUE.                                  
                                                                                
           IF PS-NOT-END-OF-FILE                                                
               ADD +1  TO  PV-RECORDS-READ                                      
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
       4000-WRITE-AUDIT.                                                        
      ******************************************************************        
                                                                                
           WRITE AUDIT-RECORD  FROM PV-AUDIT-RECORD.                            
                                                                                
           ADD +1  TO  PV-AUDIT-RECORDS-OUT.                                    
                                                                                
           MOVE SPACES           TO PV-AUDIT-RECORD.                            
                                                                                
      ******************************************************************        
       5000-INSERT-PROCESS.                                                     
      * HEADER AND CURRENT DETAIL RECORDS ARE VALID                             
      ******************************************************************        
                                                                                
           MOVE PV-DTL-STR-NBR         TO SDT00032-STR-NBR.                     
           MOVE PV-DTL-TRLR-SIZE       TO SDT00032-TRLR-SIZE-MU.                
           MOVE 'PR'                   TO SDT00032-TRLR-VEH-NBR.                
           PERFORM VARYING DATE-SUB FROM 1 BY 1 UNTIL DATE-SUB > 7              
               IF PV-DTL-ARRVL-DTE (DATE-SUB) = 'X' or 'x'                      
                   MOVE PV-HDR-ARRVL-DTE (DATE-SUB) TO                          
                                             SDT00032-TRLR-ARRVL-DTE            
                   PERFORM 5100-INSERT                                          
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
                                                                                
      ******************************************************************        
       5100-INSERT.                                                             
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
           INSERT INTO SDT_STR_RCVG                                             
             (STR_NBR                                                           
             ,TRLR_ARRVL_DTE                                                    
             ,TRLR_VEH_NBR                                                      
             ,TRLR_SIZE_MU)                                                     
           VALUES ( :SDT00032-STR-NBR                                           
                   ,:SDT00032-TRLR-ARRVL-DTE                                    
                   ,:SDT00032-TRLR-VEH-NBR                                      
                   ,:SDT00032-TRLR-SIZE-MU)                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   ADD +1 TO PV-ROWS-INSERTED                                   
               WHEN SQLCODE = -803                                              
                   CONTINUE                                                     
               WHEN SQLCODE = -913                                              
               WHEN SQLCODE = -904                                              
                   MOVE '5000-INSERT' TO PV-PARA-NAME                           
                   MOVE 'RESOURCE UNAVAILABLE' TO PV-ABEND-NOTE                 
                   PERFORM 9000-DB2-ABEND                                       
               WHEN OTHER                                                       
                   MOVE '5000-INSERT' TO PV-PARA-NAME                           
                   DISPLAY '                                          '         
                   DISPLAY '******************************************'         
                   DISPLAY 'STR           = ' SDT00032-STR-NBR                  
                   DISPLAY 'ARRIVAL DATE  = ' SDT00032-TRLR-ARRVL-DTE           
                   DISPLAY 'TRLR-VEH-NBR  = ' SDT00032-TRLR-VEH-NBR             
                   DISPLAY 'TRUCK SIZE    = ' SDT00032-TRLR-SIZE-MU             
                   DISPLAY '******************************************'         
                   PERFORM 9000-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5500-COMMIT.                                                             
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
              COMMIT                                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                 CONTINUE                                                       
               WHEN OTHER                                                       
                   MOVE '9500-COMMIT' TO PV-PARA-NAME                           
                   PERFORM 9000-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
       8000-NON-DB2-ABEND.                                                      
      ******************************************************************        
                                                                                
           PERFORM 9900-END-OF-JOB.                                             
                                                                                
           DISPLAY '***************************************************'        
           DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM                            
           DISPLAY '** PARAGRAPH        ' PV-PARA-NAME                          
           DISPLAY '**' PV-ABEND-NOTE                                           
           DISPLAY '***************************************************'        
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
                                                                                
      ******************************************************************        
       9000-DB2-ABEND.                                                          
      ******************************************************************        
                                                                                
           MOVE SQLCODE TO SQLCODE2.                                            
           MOVE SQLERRD(3) TO SQLERRD3.                                         
                                                                                
           PERFORM 9900-END-OF-JOB.                                             
                                                                                
           DISPLAY '**************************************************'         
           DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM                            
           DISPLAY '** PARAGRAPH        ' PV-PARA-NAME                          
           DISPLAY '** ' PV-ABEND-NOTE                                          
           DISPLAY '** SQL ERROR      = ' SQLCODE2.                             
           DISPLAY '** SQLERRM        = ' SQLERRM.                              
           DISPLAY '**************************************************'.        
                                                                                
      *----------------------------------------------------------------*        
      *  THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-           
      *  MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      *  FORMAT.                                                                
      *----------------------------------------------------------------*        
           COPY DPPD004.                                                        
                                                                                
           CALL 'DSNTIAR' USING SQLCA                                           
                                DP004-FORMATTED-MESSAGE                         
                                DP004-ERROR-LINE-LENGTH.                        
                                                                                
           DISPLAY DP004-ASTERISK-LINE.                                         
                                                                                
           PERFORM                                                              
              VARYING DP004-MSG-IDX                                             
              FROM 1 BY 1                                                       
              UNTIL DP004-MSG-IDX > DP004-MAX-MESSAGE-LINES                     
                  DISPLAY '**'                                                  
                          DP004-MESSAGE-LINE (DP004-MSG-IDX)                    
                          ' **'                                                 
           END-PERFORM.                                                         
                                                                                
           DISPLAY DP004-ASTERISK-LINE.                                         
                                                                                
           IF SQLCODE NOT = -911                                                
               EXEC SQL                                                         
                   COMMIT                                                       
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           SET AC-DB2-ERROR TO TRUE.                                            
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
                                                                                
      ******************************************************************        
       9900-END-OF-JOB.                                                         
      ******************************************************************        
                                                                                
           DISPLAY '                    '.                                      
           DISPLAY '*************************************'.                     
           DISPLAY '  SDKUP025 WEEKLY DELIVERY SCHEDULE  '                      
           DISPLAY '====================================='.                     
           DISPLAY 'PROCESSING TIME   : ' SD-TIME-STAMP.                        
           DISPLAY '# RECORDS READ    : ' PV-RECORDS-READ.                      
           DISPLAY '# ROWS DELETED    : ' PV-ROWS-DELETED.                      
           DISPLAY '# ROWS INSERTED   : ' PV-ROWS-INSERTED.                     
           DISPLAY '# ERROR RECORDS   : ' PV-ERROR-RECORDS.                     
                                                                                
           MOVE SPACES  TO  PV-AUDIT-RECORD.                                    
           PERFORM 4000-WRITE-AUDIT.                                            
           MOVE '=========================================='                    
                TO  PV-AUDIT-RECORD.                                            
           PERFORM 4000-WRITE-AUDIT.                                            
           MOVE '  SDKUP025 WEEKLY DELIVERY SCHEDULE  '                         
                TO  PV-AUDIT-RECORD.                                            
           PERFORM 4000-WRITE-AUDIT.                                            
           MOVE '=========================================='                    
                TO  PV-AUDIT-RECORD.                                            
           PERFORM 4000-WRITE-AUDIT.                                            
           STRING 'PROCESSING TIME   : ' SD-TIME-STAMP                          
                DELIMITED BY SIZE INTO  PV-AUDIT-RECORD.                        
           PERFORM 4000-WRITE-AUDIT.                                            
           STRING '# RECORDS READ    : ' PV-RECORDS-READ                        
                DELIMITED BY SIZE INTO  PV-AUDIT-RECORD.                        
           PERFORM 4000-WRITE-AUDIT.                                            
           STRING '# ROWS DELETED    : ' PV-ROWS-DELETED                        
                DELIMITED BY SIZE INTO  PV-AUDIT-RECORD.                        
           PERFORM 4000-WRITE-AUDIT.                                            
           STRING '# ROWS INSERTED   : ' PV-ROWS-INSERTED                       
                DELIMITED BY SIZE INTO  PV-AUDIT-RECORD.                        
           PERFORM 4000-WRITE-AUDIT.                                            
           STRING '# ERROR RECORDS   : ' PV-ERROR-RECORDS                       
                DELIMITED BY SIZE INTO  PV-AUDIT-RECORD.                        
           PERFORM 4000-WRITE-AUDIT.                                            
           MOVE '=========================================='                    
             TO  PV-AUDIT-RECORD.                                               
           PERFORM 4000-WRITE-AUDIT.                                            
                                                                                
           DISPLAY '# AUDIT RECORDS   : ' PV-AUDIT-RECORDS-OUT.                 
           DISPLAY '====================================='.                     
           DISPLAY 'END SDKUP025'.                                              
                                                                                
           CLOSE TRIP-FILE                                                      
                 AUDIT-FILE.                                                    
