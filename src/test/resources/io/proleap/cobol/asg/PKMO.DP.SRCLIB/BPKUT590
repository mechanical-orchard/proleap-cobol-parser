      ******************************************************************        
      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
      ******************************************************************        
                                                                                
       PROGRAM-ID.     BPKUT590.                                                
       AUTHOR.         DANIEL ANDERSON                                          
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   03/26/01.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *                                                                         
      * 07/07/06  JASON LINDEMANN                                               
      * - CHANGED CODE TO HANDLE A W27 FOR THE 53RD WEEK IN FALL                
C71482*                                                                         
C71482* 09/05/17  JOHN SELWITSCHKA                                              
C71482* - CHANGES FOR EP UPGRADE                                                
      *                                                                         
      ******************************************************************        
                                                                                
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      ******************************************************************        
                                                                                
      ******************************************************************        
       CONFIGURATION SECTION.                                                   
      ******************************************************************        
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
      ******************************************************************        
       INPUT-OUTPUT SECTION.                                                    
      ******************************************************************        
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT COMMA-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT FIXED-WIDTH-FILE                                              
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      ******************************************************************        
                                                                                
      ******************************************************************        
       FILE SECTION.                                                            
      ******************************************************************        
                                                                                
       FD  COMMA-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  COMMA-FMT-REC           PIC X(600).                                  
                                                                                
       FD  FIXED-WIDTH-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  FIXED-WIDTH-FMT-REC     PIC X(600).                                  
                                                                                
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-COMMA-DATA-SW      PIC X(01)  VALUE 'N'.               
               88  PS-END-OF-COMMA-DATA                VALUE 'Y'.               
                                                                                
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-COMMA-DATA              PIC X(600).                           
                                                                                
       01  FW-FIXED-WIDTH-OUTPUT.                                               
           05  FW-PRODUCT                 PIC X(18).                            
           05  FW-LOCATION                PIC X(18).                            
           05  FW-CHANNEL                 PIC X(18).                            
           05  FW-ELEMENT                 PIC X(18).                            
           05  FW-COMPONENT               PIC X(18).                            
           05  FW-VALUE-1                 PIC X(18) VALUE SPACES.               
           05  FW-VALUE-2                 PIC X(18) VALUE SPACES.               
           05  FW-VALUE-3                 PIC X(18) VALUE SPACES.               
           05  FW-VALUE-4                 PIC X(18) VALUE SPACES.               
           05  FW-VALUE-5                 PIC X(18) VALUE SPACES.               
           05  FW-VALUE-6                 PIC X(18) VALUE SPACES.               
           05  FW-VALUE-7                 PIC X(18) VALUE SPACES.               
           05  FW-VALUE-8                 PIC X(18) VALUE SPACES.               
           05  FW-VALUE-9                 PIC X(18) VALUE SPACES.               
           05  FW-VALUE-10                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-11                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-12                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-13                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-14                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-15                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-16                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-17                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-18                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-19                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-20                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-21                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-22                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-23                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-24                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-25                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-26                PIC X(18) VALUE SPACES.               
           05  FW-VALUE-27                PIC X(18) VALUE SPACES.               
      ******************************************************************        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-UNSTRING                                                
                   UNTIL PS-END-OF-COMMA-DATA.                                  
                                                                                
           PERFORM 3000-END-OF-JOB.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *                                                                *        
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  COMMA-FILE.                                              
           OPEN OUTPUT FIXED-WIDTH-FILE.                                        
                                                                                
           PERFORM 8000-READ-COMMA-RECORD.                                      
                                                                                
                                                                                
      ******************************************************************        
      *                                                                *        
      ******************************************************************        
       2000-UNSTRING.                                                           
                                                                                
           UNSTRING PV-COMMA-DATA DELIMITED BY ","                              
               INTO FW-PRODUCT                                                  
                    FW-LOCATION                                                 
                    FW-CHANNEL                                                  
                    FW-ELEMENT                                                  
                    FW-COMPONENT                                                
                    FW-VALUE-1                                                  
                    FW-VALUE-2                                                  
                    FW-VALUE-3                                                  
                    FW-VALUE-4                                                  
                    FW-VALUE-5                                                  
                    FW-VALUE-6                                                  
                    FW-VALUE-7                                                  
                    FW-VALUE-8                                                  
                    FW-VALUE-9                                                  
                    FW-VALUE-10                                                 
                    FW-VALUE-11                                                 
                    FW-VALUE-12                                                 
                    FW-VALUE-13                                                 
                    FW-VALUE-14                                                 
                    FW-VALUE-15                                                 
                    FW-VALUE-16                                                 
                    FW-VALUE-17                                                 
                    FW-VALUE-18                                                 
                    FW-VALUE-19                                                 
                    FW-VALUE-20                                                 
                    FW-VALUE-21                                                 
                    FW-VALUE-22                                                 
                    FW-VALUE-23                                                 
                    FW-VALUE-24                                                 
                    FW-VALUE-25                                                 
                    FW-VALUE-26                                                 
                    FW-VALUE-27.                                                
                                                                                
C71482     INSPECT FW-FIXED-WIDTH-OUTPUT REPLACING ALL X'0D' BY X'40'.          
           WRITE FIXED-WIDTH-FMT-REC FROM FW-FIXED-WIDTH-OUTPUT.                
                                                                                
           PERFORM 8000-READ-COMMA-RECORD.                                      
                                                                                
                                                                                
      ******************************************************************        
      *                                                                *        
      ******************************************************************        
       3000-END-OF-JOB.                                                         
           CLOSE COMMA-FILE                                                     
                 FIXED-WIDTH-FILE.                                              
                                                                                
                                                                                
      ******************************************************************        
      *                                                                *        
      ******************************************************************        
       8000-READ-COMMA-RECORD.                                                  
           READ COMMA-FILE INTO PV-COMMA-DATA                                   
             AT END SET PS-END-OF-COMMA-DATA TO TRUE.                           
                                                                                
                                                                                
