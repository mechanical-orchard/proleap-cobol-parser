       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    SDKUT173.                                         00030000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  10/02/12.                                         00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      * EXTRACT OF TRUCK PROJECTED CANCELLED DELIVERIES FOR EDW         00090112
      ******************************************************************00090700
                                                                        00090800
       ENVIRONMENT DIVISION.                                            00090900
       CONFIGURATION SECTION.                                           00091000
                                                                        00092000
       SOURCE-COMPUTER.  IBM-3090.                                      00093000
       OBJECT-COMPUTER.  IBM-3090.                                      00094000
                                                                        00095000
       INPUT-OUTPUT SECTION.                                            00096000
       FILE-CONTROL.                                                    00097000
                                                                        00098000
           SELECT LAST-RUN-TMST-FILE    ASSIGN TO INP01.                00098104
           SELECT EXTRACT-FILE          ASSIGN TO OUT01.                00099004
           SELECT COUNT-FILE            ASSIGN TO OUT02.                00100004
                                                                        00110000
       DATA DIVISION.                                                   00120000
       FILE SECTION.                                                    00130000
                                                                        00140000
       FD  LAST-RUN-TMST-FILE                                           00141004
           RECORDING MODE IS F                                          00142004
           LABEL RECORDS ARE OMITTED                                    00143004
           RECORD CONTAINS 80 CHARACTERS                                00144004
           BLOCK CONTAINS 0 RECORDS                                     00145007
           DATA RECORD IS LAST-RUN-TMST-RECORD.                         00145106
       01  LAST-RUN-TMST-RECORD             PIC  X(80).                 00146004
                                                                        00147004
       FD  EXTRACT-FILE                                                 00150000
           RECORDING MODE IS F                                          00160000
           LABEL RECORDS ARE STANDARD                                   00170000
           RECORD CONTAINS 80 CHARACTERS                                00180019
           BLOCK CONTAINS 0 RECORDS                                     00190000
           DATA RECORD IS EXTRACT-RECORD.                               00200000
                                                                        00210000
       01  EXTRACT-RECORD                  PIC X(80).                   00220019
                                                                        00230000
       FD  COUNT-FILE                                                   00240000
           RECORDING MODE IS F                                          00250000
           LABEL RECORDS ARE STANDARD                                   00260000
           RECORD CONTAINS 7 CHARACTERS                                 00270000
           BLOCK CONTAINS 0 RECORDS                                     00280000
           DATA RECORD IS COUNT-RECORD.                                 00290000
                                                                        00300000
       01  COUNT-RECORD                    PIC X(07).                   00310000
                                                                        00320000
      ******************************************************************00330000
      *                     WORKING STORAGE                             00340000
      ******************************************************************00350000
       WORKING-STORAGE SECTION.                                         00360000
                                                                        00370000
      ******************************************************************00380000
      * PS - PROGRAM SWITCHES                                           00390000
      ******************************************************************00400000
       01  PS-PROGRAM-SWITCHES.                                         00410000
           05  PS-END-OF-CSR-SW             PIC X         VALUE SPACE.  00420000
               88  PS-END-OF-CSR                          VALUE 'Y'.    00430000
               88  PS-NOT-END-OF-CSR                      VALUE 'N'.    00440000
                                                                        00440104
           05  PS-TMST-FILE-SW              PIC  X(01)  VALUE 'N'.      00441004
               88  PS-TMST-FILE-END                     VALUE 'Y'.      00442004
               88  PS-TMST-FILE-START                   VALUE 'N'.      00443004
                                                                        00450000
      ******************************************************************00460000
      * PC - PROGRAM CONSTANTS                                          00470000
      ******************************************************************00480000
       01  PC-PROGRAM-CONSTANTS.                                        00490000
           05  PC-PROGRAM-NAME             PIC X(08) VALUE 'SDKUT173'.  00500000
           05  PC-FIELD-DELIMITER          PIC X(01) VALUE '|'.         00510000
                                                                        00520000
      ******************************************************************00530000
      * PV - PROGRAM VARIABLES                                          00540000
      ******************************************************************00550000
       01  PV-PROGRAM-VARIABLES.                                        00560000
           05  PV-CURRENT-DATE                PIC X(10) VALUE SPACES.   00570001
           05  PV-TIMESTAMP                   PIC X(26) VALUE SPACES.   00570106
                                                                        00570204
       01  CC-LAST-RUN-TMST-RECORD.                                     00571006
           05  CC-LAST-RUN-TIMESTAMP          PIC X(26).                00572004
                                                                        00580000
         05  PV-ABEND-VARIABLES.                                        00590000
           10  PV-PARAGRAPH-NAME              PIC X(30)  VALUE SPACES.  00600000
           10  PV-OPERATION-MSG               PIC X(60)  VALUE SPACES.  00601005
           10  PV-OPERATION-MSG-1             PIC X(40)  VALUE SPACES.  00610000
           10  PV-MSG-TEXT                    PIC X(40)  VALUE SPACES.  00620000
           10  PV-DB2-OPERATION               PIC X(30)  VALUE SPACES.  00630000
                                                                        00640000
         05  PV-COUNT-RECORD.                                           00650000
             10  PV-RECORDS-WRITTEN-TO-FILE   PIC 9(07)  VALUE ZEROES.  00660001
                                                                        00670000
      ******************************************************************00680000
      * PA - PROGRAM ACCUMULATORS (COUNTERS)                            00690000
      ******************************************************************00700000
       01  PA-PROGRAM-ACCUMULATORS.                                     00710000
           05  PA-FETCH-COUNT              PIC 9(07) VALUE ZEROES.      00720000
           05  PA-WRITE-COUNT              PIC 9(07) VALUE ZEROES.      00730000
                                                                        00740000
      ******************************************************************00750000
      * AC - ABEND ERROR CODES                                          00760000
      ******************************************************************00770000
       01  ABEND-CODE                  PIC S9(04)  VALUE +0 COMP SYNC.  00780000
         88  ABEND-4013-DB2-ERROR                        VALUE +4013.   00790000
                                                                        00800000
      ******************************************************************00810000
      * EXTRACT RECORD LAYOUT                                           00820000
      ******************************************************************00830000
           COPY SDRD173.                                                00840000
                                                                        00850000
      ******************************************************************00860000
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         00870000
      ******************************************************************00880000
           COPY DPWS004.                                                00890000
                                                                        00900000
      ******************************************************************00910000
      * DB2 MESSAGE AREA                                                00920000
      ******************************************************************00930000
           COPY SQLCA2.                                                 00940000
                                                                        00950000
      ******************************************************************00960000
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                 00970000
      ******************************************************************00980000
           EXEC SQL                                                     00990000
               INCLUDE SQLCA                                            01000000
           END-EXEC.                                                    01010000
                                                                        01020000
      ******************************************************************01030000
      * DB2 TABLE SDT_DLY_STR_TRLR                                      01040001
      ******************************************************************01050000
           EXEC SQL                                                     01060000
               INCLUDE SDT00019                                         01070001
           END-EXEC.                                                    01080000
                                                                        01090000
      ******************************************************************01100000
      * SDT_DLY_STR_TRLR CURSOR - CANCELLED TRUCK PROJECTED DELIVERIES  01110001
      ******************************************************************01120000
           EXEC SQL                                                     01130000
             DECLARE CNCL-TRK-PROJ-CSR CURSOR FOR                       01140001
               SELECT STR_NBR                                           01150001
                    , TRLR_ARRVL_DTE                                    01160001
                    , BGTD_TYP_CDE                                      01170001
                    , LAST_UPD_USR_ID                                   01180001
                    , LAST_UPD_TMST                                     01190001
                    , CURRENT DATE AS CURRENT_DATE                      01200002
                    , TRIP_QTY                                          01200117
               FROM SDT_DLY_STR_TRLR                                    01201001
               WHERE BGTD_TYP_CDE IN ('CL','CG')                        01202018
                 AND LAST_UPD_TMST > :CC-LAST-RUN-TIMESTAMP             01203007
               ORDER BY STR_NBR                                         01204001
                      , TRLR_ARRVL_DTE                                  01205001
                      , BGTD_TYP_CDE                                    01206001
               FOR FETCH ONLY                                           01210000
               WITH UR                                                  01220000
           END-EXEC.                                                    01230000
                                                                        01240000
      ******************************************************************01250000
       PROCEDURE DIVISION.                                              01260000
      ******************************************************************01270000
      * MAIN DRIVER                                                     01280000
      ******************************************************************01290000
       0000-MAINLINE.                                                   01300000
                                                                        01310000
           PERFORM 1000-INITIALIZATION.                                 01320000
                                                                        01330000
           PERFORM 2000-EXTRACT-PROCESSING                              01340000
                   UNTIL PS-END-OF-CSR.                                 01350000
                                                                        01360000
           PERFORM 3000-WRAP-UP.                                        01370000
                                                                        01380000
           PERFORM 9999-SUMMARY.                                        01390000
                                                                        01400000
           GOBACK.                                                      01410000
                                                                        01420000
      ******************************************************************01430000
      * 1000-INITIALIZATION                                             01440000
      ******************************************************************01450000
       1000-INITIALIZATION.                                             01460000
                                                                        01470000
           DISPLAY '*************************************'.             01480000
           DISPLAY '* SDKUT173 - SDT_DLY_STR_TRLR       *'.             01490001
           DISPLAY '*            DB2 TABLE EXTRACT      *'.             01500000
           DISPLAY '*            TRUCK CANCELLED        *'.             01500112
           DISPLAY '*            PROJECTED DELIVERIES   *'.             01500212
           DISPLAY '*************************************'.             01510000
           DISPLAY '*'.                                                 01520000
           DISPLAY '* - PROCESSING...'.                                 01530000
                                                                        01531004
           OPEN INPUT LAST-RUN-TMST-FILE.                               01531104
                                                                        01532004
           READ LAST-RUN-TMST-FILE INTO CC-LAST-RUN-TMST-RECORD         01532107
              AT END                                                    01532204
                 SET PS-TMST-FILE-END TO TRUE                           01532304
           END-READ.                                                    01532404
                                                                        01532504
           MOVE SPACE TO SDRD173-EXTRACT-RECORD.                        01532620
           IF PS-TMST-FILE-END                                          01532704
               MOVE 'NO TIMESTAMP FILE TO PROCESS' TO PV-OPERATION-MSG  01532804
               PERFORM Z910-ABEND                                       01532904
           ELSE                                                         01533004
               DISPLAY 'CC-LAST-RUN-TMST = ' CC-LAST-RUN-TIMESTAMP      01533104
           END-IF.                                                      01533204
                                                                        01533304
           CLOSE LAST-RUN-TMST-FILE.                                    01533405
                                                                        01533504
           OPEN OUTPUT EXTRACT-FILE.                                    01550000
           PERFORM 2300-OPEN-CNCL-TRK-PROJ-CSR.                         01560001
           PERFORM 2400-FETCH-CNCL-TRK-PROJ-CSR.                        01570001
                                                                        01570106
      *    SAVE OFF THE TIMESTAMP RIGHT AWAY AFTER RUNNING THE SQL      01570206
           PERFORM 9000-SELECT-DB2-TIMESTAMP.                           01571006
                                                                        01580000
      ******************************************************************01594000
      * 2000-EXTRACT-PROCESSING                                         01600000
      ******************************************************************01610000
       2000-EXTRACT-PROCESSING.                                         01620000
                                                                        01630000
           PERFORM 2100-MOVE-TO-LAYOUT.                                 01640000
           PERFORM 2200-WRITE-EXTRACT-RECORD.                           01650000
                                                                        01660000
           PERFORM 2400-FETCH-CNCL-TRK-PROJ-CSR.                        01670001
                                                                        01680000
      ******************************************************************01690000
      * 2100-MOVE-TO-LAYOUT.                                            01700000
      ******************************************************************01710000
       2100-MOVE-TO-LAYOUT.                                             01720000
                                                                        01730000
           MOVE SDT00019-STR-NBR         TO SDRD173-STR-NBR.            01740011
           MOVE PC-FIELD-DELIMITER       TO SDRD173-DELIMITER-1.        01750011
           MOVE SDT00019-TRLR-ARRVL-DTE  TO SDRD173-TRLR-ARRVL-DTE.     01770011
           MOVE PC-FIELD-DELIMITER       TO SDRD173-DELIMITER-2.        01771011
           MOVE SDT00019-BGTD-TYP-CDE    TO SDRD173-BGTD-TYP-CDE.       01780011
           MOVE PC-FIELD-DELIMITER       TO SDRD173-DELIMITER-3.        01781011
           MOVE SDT00019-LAST-UPD-USR-ID TO SDRD173-LAST-UPD-USR-ID.    01790011
           MOVE PC-FIELD-DELIMITER       TO SDRD173-DELIMITER-4.        01790111
           MOVE SDT00019-LAST-UPD-TMST   TO SDRD173-LAST-UPD-TMST.      01791011
           MOVE PC-FIELD-DELIMITER       TO SDRD173-DELIMITER-5.        01791111
           MOVE PV-CURRENT-DATE          TO SDRD173-CURRENT-DATE.       01792011
           MOVE PC-FIELD-DELIMITER       TO SDRD173-DELIMITER-6.        01792117
           MOVE SDT00019-TRIP-QTY        TO SDRD173-TRIP-QTY.           01793017
                                                                        01810000
      ******************************************************************01820000
      * 2200-WRITE-EXTRACT-RECORD.                                      01830000
      ******************************************************************01840000
       2200-WRITE-EXTRACT-RECORD.                                       01850000
                                                                        01860000
           WRITE EXTRACT-RECORD                                         01870000
               FROM SDRD173-EXTRACT-RECORD.                             01880011
                                                                        01890000
           ADD +1 TO PA-WRITE-COUNT.                                    01900000
                                                                        01910000
      ******************************************************************01920000
      * 2300-OPEN-CNCL-TRK-PROJ-CSR.                                    01930001
      ******************************************************************01940000
       2300-OPEN-CNCL-TRK-PROJ-CSR.                                     01950001
                                                                        01960000
           EXEC SQL                                                     01970000
             OPEN CNCL-TRK-PROJ-CSR                                     01980001
           END-EXEC.                                                    01990000
                                                                        02000000
           EVALUATE TRUE                                                02010000
             WHEN SQLCODE = +0                                          02020000
               CONTINUE                                                 02030000
                                                                        02040000
             WHEN OTHER                                                 02050000
               MOVE 'OPENING SQL CURSOR    ' TO PV-DB2-OPERATION        02060000
               MOVE '2300-OPEN-WKLY-STR-ADJ' TO PV-PARAGRAPH-NAME       02070000
               PERFORM 9020-SQL-ABEND-ROUTINE                           02080000
           END-EVALUATE.                                                02090000
                                                                        02100000
      ******************************************************************02110000
      * 2400-FETCH-CNCL-TRK-PROJ-CSR                                    02120001
      ******************************************************************02130000
       2400-FETCH-CNCL-TRK-PROJ-CSR.                                    02140001
                                                                        02150000
           INITIALIZE DCLSDT00019.                                      02160001
                                                                        02170000
           EXEC SQL                                                     02180000
             FETCH CNCL-TRK-PROJ-CSR                                    02190001
               INTO :SDT00019-STR-NBR                                   02200001
                  , :SDT00019-TRLR-ARRVL-DTE                            02210001
                  , :SDT00019-BGTD-TYP-CDE                              02220001
                  , :SDT00019-LAST-UPD-USR-ID                           02230001
                  , :SDT00019-LAST-UPD-TMST                             02231001
                  , :PV-CURRENT-DATE                                    02232001
                  , :SDT00019-TRIP-QTY                                  02233017
           END-EXEC                                                     02240000
                                                                        02250000
           EVALUATE TRUE                                                02260000
             WHEN SQLCODE = +0                                          02270000
               ADD +1 TO PA-FETCH-COUNT                                 02280000
               SET PS-NOT-END-OF-CSR TO TRUE                            02290000
                                                                        02300000
             WHEN SQLCODE = +100                                        02310000
               SET PS-END-OF-CSR TO TRUE                                02320000
                                                                        02330000
             WHEN OTHER                                                 02340000
               MOVE 'FETCHING CSR         ' TO PV-DB2-OPERATION         02350000
               MOVE '2400-                ' TO PV-PARAGRAPH-NAME        02360000
               PERFORM 9020-SQL-ABEND-ROUTINE                           02370000
           END-EVALUATE.                                                02380000
                                                                        02390000
      ******************************************************************02400000
      * 3000-WRAP-UP                                                    02410000
      ******************************************************************02420000
       3000-WRAP-UP.                                                    02430000
                                                                        02440000
           CLOSE EXTRACT-FILE.                                          02450000
           PERFORM 3100-CLOSE-CNCL-TRK-PROJ-CSR.                        02460001
                                                                        02470000
           OPEN OUTPUT COUNT-FILE.                                      02480000
           WRITE COUNT-RECORD FROM PA-WRITE-COUNT.                      02490000
           CLOSE COUNT-FILE.                                            02500000
                                                                        02510000
           DISPLAY '* - CNCL PROJ TRUCK EXTR COMPLETED'.                02520001
           DISPLAY '* - COUNT FILE WRITTEN AS: ' PA-WRITE-COUNT.        02530000
                                                                        02540000
      ******************************************************************02550000
      * 3100-CLOSE-CNCL-TRK-PROJ-CSR.                                   02560001
      ******************************************************************02570000
       3100-CLOSE-CNCL-TRK-PROJ-CSR.                                    02580001
                                                                        02590000
           EXEC SQL                                                     02600000
             CLOSE CNCL-TRK-PROJ-CSR                                    02610001
           END-EXEC.                                                    02620000
                                                                        02630000
           EVALUATE TRUE                                                02640000
             WHEN SQLCODE = +0                                          02650000
               CONTINUE                                                 02660000
                                                                        02670000
             WHEN OTHER                                                 02680000
               MOVE 'CLOSING CSR           '  TO PV-DB2-OPERATION       02690000
               MOVE '3100-CLOSE-CSR         ' TO PV-PARAGRAPH-NAME      02700000
               PERFORM 9020-SQL-ABEND-ROUTINE                           02710000
           END-EVALUATE.                                                02720000
                                                                        02720106
      *----------------------------------------------------------       02721012
      * 9000-SELECT-DB2-TIMESTAMP.                                      02722012
      *     RETRIEVE TIMESTAMP FROM DB2.                                02723012
      *----------------------------------------------------------       02724012
       9000-SELECT-DB2-TIMESTAMP.                                       02725012
                                                                        02726012
           EXEC SQL                                                     02727012
                SELECT CURRENT TIMESTAMP                                02728012
                INTO  :PV-TIMESTAMP                                     02729012
                FROM   SYSIBM.SYSDUMMY1                                 02729112
           END-EXEC.                                                    02729212
                                                                        02729312
           DISPLAY 'PV-CURR-SYS-TMST = ' PV-TIMESTAMP.                  02729410
                                                                        02729710
           IF SQLCODE NOT EQUAL ZERO                                    02729812
              MOVE '9000-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME          02729912
              MOVE 'CANNOT GET CURRENT TIMESTAMP'                       02730012
                                            TO PV-DB2-OPERATION         02730112
              PERFORM 9020-SQL-ABEND-ROUTINE                            02730212
           END-IF.                                                      02730312
                                                                        02730412
      *----------------------------------------------------------       02730512
      * 9010-UPDATE-CNTL-CARD.                                          02730621
      *     UPDATES CONTROL CARD SDCC173.                               02730712
      *----------------------------------------------------------       02730812
       9010-UPDATE-CNTL-CARD.                                           02730921
                                                                        02731012
           MOVE PV-TIMESTAMP TO CC-LAST-RUN-TIMESTAMP.                  02731112
                                                                        02731206
           OPEN OUTPUT LAST-RUN-TMST-FILE.                              02731312
           WRITE LAST-RUN-TMST-RECORD FROM CC-LAST-RUN-TMST-RECORD.     02731412
           CLOSE LAST-RUN-TMST-FILE.                                    02731512
                                                                        02732000
      ******************************************************************02740000
      * 9020-SQL-ABEND-ROUTINE                                          02750000
      ******************************************************************02760000
       9020-SQL-ABEND-ROUTINE.                                          02770000
                                                                        02780000
           DISPLAY '*'.                                                 02790000
           DISPLAY '** ABEND     **'.                                   02800000
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                02810000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              02820000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               02830000
                                                                        02840000
           COPY DPPD004.                                                02850000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            02860000
           CALL 'ILBOABN0' USING ABEND-CODE.                            02870000
                                                                        02870104
      ******************************************************************02871004
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *02872004
      ******************************************************************02873004
       Z910-ABEND.                                                      02874004
                                                                        02875004
           DISPLAY '** ABEND           **'.                             02876004
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         02877004
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.       02878004
                                                                        02879004
           CALL 'ILBOABN0' USING ABEND-CODE.                            02879104
                                                                        02880000
      ******************************************************************02890000
      * 9999-SUMMARY.                                                   02900000
      ******************************************************************02910000
       9999-SUMMARY.                                                    02920000
                                                                        02921006
      *    UPDATE THE LAST UPDATE TIMESTAMP CONTROL CARD                02921106
           PERFORM 9010-UPDATE-CNTL-CARD.                               02923021
                                                                        02930000
           DISPLAY '*'.                                                 02940000
           DISPLAY '*************************************'.             02950000
           DISPLAY '* PROGRAM SUMMARY - SDKUT173        *'.             02960012
           DISPLAY '*************************************'.             02970000
           DISPLAY '* - ROWS EXTRACTED      : ' PA-FETCH-COUNT '   *'.  02980000
           DISPLAY '* - ROWS WRITTEN TO FILE: ' PA-WRITE-COUNT '   *'.  02990000
           DISPLAY '*************************************'.             03000000
                                                                        03010000
      ******************************************************************03020000
