000100 TITLE 'SUMMARIZE CARTON DATA'.                                           
000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    SUKUT062.                                                 
000400 AUTHOR.        DENNIS STEFFEN.                                           
000500 DATE-WRITTEN.  WINTER 2002.                                              
000600 ENVIRONMENT DIVISION.                                                    
000700 INPUT-OUTPUT SECTION.                                                    
000800                                                                          
000900 FILE-CONTROL.                                                            
001000     SELECT CARTON-EXTRACT-FILE     ASSIGN TO INP01.                      
001100     SELECT SUMMARY-FILE            ASSIGN TO OUT01.                      
001200                                                                          
001300 DATA DIVISION.                                                           
001400 FILE SECTION.                                                            
001500                                                                          
001600 FD  CARTON-EXTRACT-FILE                                                  
001700     RECORDING MODE IS F                                                  
001800     LABEL RECORDS ARE STANDARD                                           
001900     RECORD CONTAINS 100                                                  
002000     BLOCK CONTAINS 0 RECORDS                                             
002100     DATA RECORD IS CARTON-EXTRACT-RECORD.                                
002200 01  CARTON-EXTRACT-RECORD            PIC X(100).                         
002300                                                                          
002400 FD  SUMMARY-FILE                                                         
002500     RECORDING MODE IS F                                                  
002600     LABEL RECORDS ARE STANDARD                                           
002700     RECORD CONTAINS 150                                                  
002800     BLOCK CONTAINS 0 RECORDS                                             
002900     DATA RECORD IS CARTON-EXTRACT-RECORD.                                
003000 01  SUMMARY-RECORD                   PIC X(150).                         
003100                                                                          
003200 WORKING-STORAGE SECTION.                                                 
003300                                                                          
003400 01  PV-ABEND-CODE                    PIC S9(04)   VALUE ZERO             
003500                                                   COMP SYNC.             
003600                                                                          
003700 01  PS-PROGRAM-SWITCHES.                                                 
003800     05  PS-END-EXTRACT-SW            PIC X(1)   VALUE 'N'.               
003900         88  PS-END-EXTRACT                      VALUE 'Y'.               
004000                                                                          
004100 01  PV-CONTROL-VARIABLES.                                                
004200     05  PVC-CURR-KEY.                                                    
004300         10  PVC-CURR-OPER-UNT-ID     PIC 9(04) VALUE ZERO.               
004400         10  PVC-CURR-PKGNG-TYPE      PIC X(02) VALUE SPACE.              
004500         10  PVC-CURR-VENDOR-RANK     PIC X(06) VALUE SPACE.              
004600     05  PVC-PREV-KEY.                                                    
004700         10  PVC-PREV-OPER-UNT-ID     PIC 9(04) VALUE ZERO.               
004800         10  PVC-PREV-PKGNG-TYPE      PIC X(02) VALUE SPACE.              
004900         10  PVC-PREV-VENDOR-RANK     PIC X(06) VALUE SPACE.              
005000                                                                          
005100                                                                          
005200 01  PV-PROGRAM-VARIABLES.                                                
005300     05  PV-RETRY-COUNT               PIC S9(04)  VALUE ZERO              
005400                                                  COMP SYNC.              
005500     05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
005600     05  PV-ERROR-MESSAGE-1           PIC X(60)   VALUE SPACES.           
005700     05  PV-ERROR-MESSAGE-2           PIC X(60)   VALUE SPACES.           
005800     05  PV-SQLCODE                   PIC 9(9)    VALUE ZERO.             
005900     05  PV-READ-COUNT                PIC S9(9) COMP-3                    
006000                                                   VALUE ZERO.            
006100     05  PV-WRITE-COUNT               PIC S9(9) COMP-3                    
006200                                                   VALUE ZERO.            
006300     05  PV-DISP-NBR                  PIC Z(8)9.                          
006400                                                                          
006500 01  PC-PROGRAM-CONSTANTS.                                                
006600     05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'SUKUT062'.        
006700     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
006800                                                 COMP SYNC.               
006900 01  PV-ABEND-FIELDS.                                                     
007000     05  PV-ABEND-PROGRAM             PIC X(8)   VALUE 'SUKUT062'.        
007100     05  PV-ABEND-PARAGRAPH           PIC X(50)  VALUE SPACES.            
007200     05  PV-ABEND-OPERATION           PIC X(50)  VALUE SPACES.            
007300     05  PV-ABEND-STRUCTURE-1         PIC X(8)   VALUE SPACES.            
007400     05  PV-ABEND-STRUCTURE-2         PIC X(8)   VALUE SPACES.            
007500     05  PV-ABEND-STRUCTURE-3         PIC X(8)   VALUE SPACES.            
007600     05  PV-ABEND-STATUS              PIC XX     VALUE SPACES.            
007700     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
007800     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
007900     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
008000     05  PV-DB2-OPERATION-MESSAGE-4   PIC  X(79) VALUE SPACES.            
008100                                                                          
008200     COPY SURD028.                                                        
008300                                                                          
008400     COPY SURD030.                                                        
008500/                                                                         
008600*    DATE ATTRIBUTES                                                      
008700     EXEC SQL                                                             
008800         INCLUDE TDTATTR                                                  
008900     END-EXEC                                                             
009000/                                                                         
009100*  USED FOR DB2 ERROR MESSAGE BUILDING                                    
009200     COPY DPWS004.                                                        
009300                                                                          
009400*   DB2 SQL COMMUNICATION AREA                                            
009500     EXEC SQL                                                             
009600          INCLUDE SQLCA                                                   
009700     END-EXEC.                                                            
009800/                                                                         
009900 PROCEDURE DIVISION.                                                      
010000                                                                          
010100     PERFORM 1000-INIT.                                                   
010200                                                                          
010300     PERFORM 2000-PROCESS                                                 
010400        UNTIL PS-END-EXTRACT.                                             
010500                                                                          
010600     PERFORM 9000-TERMINATE.                                              
010700                                                                          
010800     GOBACK.                                                              
010900                                                                          
011000 1000-INIT.                                                               
011100                                                                          
011200     OPEN INPUT  CARTON-EXTRACT-FILE                                      
011300     OPEN OUTPUT SUMMARY-FILE                                             
011400                                                                          
011500     PERFORM 8000-READ-EXTRACT-REC.                                       
011600     IF PV-READ-COUNT = 1                                                 
011700         MOVE PVC-CURR-KEY            TO PVC-PREV-KEY                     
011800         INITIALIZE SU030-RECORD                                          
011900                    SU030-PROMPT-EXTRACT-REC                              
012000                    SU030-CARTON-EXTRACT-REC                              
012100         MOVE SU028-PROCESS-DATE      TO SU030-WEEK-END-DATE              
012200         MOVE SU028-OPER-UNT-ID       TO SU030-OPER-UNT-ID                
012300         MOVE SU028-PKGNG-TYPE        TO SU030-PKGNG-TYPE                 
012400         MOVE SU028-VENDOR-RANK       TO SU030-VENDOR-RANK                
012500         MOVE 'C'                     TO SU030-RECORD-TYPE                
012600         PERFORM 7000-GET-FISCAL-INFO                                     
012700         MOVE DTATTR-FSCL-YR-NBR      TO SU030-FISCAL-YEAR                
012800         MOVE DTATTR-FSCL-MN-NBR      TO SU030-FISCAL-MONTH               
012900         MOVE DTATTR-FSCL-WK-NBR      TO SU030-FISCAL-WEEK                
013000         MOVE DTATTR-SEA-DESC         TO SU030-FISCAL-SEASON              
013100     END-IF.                                                              
013200                                                                          
013300 2000-PROCESS.                                                            
013400                                                                          
013500                                                                          
013600     EVALUATE TRUE                                                        
013700         WHEN PVC-CURR-KEY = PVC-PREV-KEY                                 
013800             ADD 1                    TO SU030-CRTN-PO-CNT                
013900             ADD SU028-TOTAL-RCVR-UNITS                                   
014000                                      TO SU030-CRTN-UNIT-QTY              
014100             ADD SU028-TOTAL-RCVR-CTNS                                    
014200                                      TO SU030-CRTN-CRTN-QTY              
014300         WHEN OTHER                                                       
014400             PERFORM 8100-WRITE-EXTRACT-REC                               
014500             MOVE 1                   TO SU030-CRTN-PO-CNT                
014600             MOVE SU028-TOTAL-RCVR-UNITS                                  
014700                                      TO SU030-CRTN-UNIT-QTY              
014800             MOVE SU028-TOTAL-RCVR-CTNS                                   
014900                                      TO SU030-CRTN-CRTN-QTY              
015000             MOVE PVC-CURR-KEY        TO PVC-PREV-KEY                     
015100             MOVE SU028-PROCESS-DATE  TO SU030-WEEK-END-DATE              
015200             MOVE SU028-OPER-UNT-ID   TO SU030-OPER-UNT-ID                
015300             MOVE SU028-PKGNG-TYPE    TO SU030-PKGNG-TYPE                 
015400             MOVE SU028-VENDOR-RANK   TO SU030-VENDOR-RANK                
015500         END-EVALUATE.                                                    
015600                                                                          
015700     PERFORM 8000-READ-EXTRACT-REC.                                       
015800/                                                                         
015900 7000-GET-FISCAL-INFO.                                                    
016000     MOVE SU028-PROCESS-DATE          TO DTATTR-KY-DTE                    
016100     EXEC SQL                                                             
016200         SELECT                                                           
016300             FSCL_YR_NBR                                                  
016400         ,   FSCL_MN_NBR                                                  
016500         ,   FSCL_WK_NBR                                                  
016600         ,   SEA_DESC                                                     
016700         INTO                                                             
016800             :DTATTR-FSCL-YR-NBR                                          
016900         ,   :DTATTR-FSCL-MN-NBR                                          
017000         ,   :DTATTR-FSCL-WK-NBR                                          
017100         ,   :DTATTR-SEA-DESC                                             
017200         FROM                                                             
017300             TDTATTR                                                      
017400         WHERE                                                            
017500             KY_DTE = :DTATTR-KY-DTE                                      
017600         WITH UR                                                          
017700     END-EXEC.                                                            
017800                                                                          
017900     EVALUATE TRUE                                                        
018000         WHEN SQLCODE = ZERO                                              
018100         WHEN SQLCODE = -904                                              
018200         WHEN SQLCODE = -911                                              
018300         WHEN SQLCODE = -913                                              
018400             CONTINUE                                                     
018500         WHEN SQLWARN0 NOT = SPACE                                        
018600         WHEN SQLCODE  NOT = ZERO                                         
018700             MOVE '7000-GET-FISCAL-INFO'                                  
018800                                       TO PV-ABEND-PARAGRAPH              
018900             MOVE 'SELECT'             TO PV-ABEND-OPERATION              
019000             MOVE 'TDTATTR' TO PV-ABEND-STRUCTURE-1                       
019100             STRING 'DATE = ' DTATTR-KY-DTE DELIMITED BY SIZE             
019200                               INTO PV-ABEND-STRUCTURE-2                  
019300             PERFORM 9900-DB2-ABEND                                       
019400     END-EVALUATE.                                                        
019500                                                                          
019600 8000-READ-EXTRACT-REC.                                                   
019700                                                                          
019800     READ CARTON-EXTRACT-FILE INTO                                        
019900           SU028-PO-CTN-EXTRACT-RECORD                                    
020000     AT END                                                               
020100         SET PS-END-EXTRACT           TO TRUE                             
020200     NOT AT END                                                           
020300         ADD 1                        TO PV-READ-COUNT                    
020400         MOVE SU028-OPER-UNT-ID       TO PVC-CURR-OPER-UNT-ID             
020500         MOVE SU028-PKGNG-TYPE        TO PVC-CURR-PKGNG-TYPE              
020600         MOVE SU028-VENDOR-RANK       TO PVC-CURR-VENDOR-RANK             
020700     END-READ.                                                            
020800                                                                          
020900 8100-WRITE-EXTRACT-REC.                                                  
021000                                                                          
021100     ADD 1                         TO PV-WRITE-COUNT                      
021200     WRITE SUMMARY-RECORD FROM                                            
021300           SU030-RECORD.                                                  
021400/                                                                         
021500 9000-TERMINATE.                                                          
021600                                                                          
021700     IF PV-READ-COUNT > ZERO                                              
021800         PERFORM 8100-WRITE-EXTRACT-REC                                   
021900     END-IF                                                               
022000                                                                          
022100     CLOSE CARTON-EXTRACT-FILE                                            
022200           SUMMARY-FILE                                                   
022300                                                                          
022400     DISPLAY '************************'.                                  
022500     DISPLAY '******* SUKUT062 *******'.                                  
022600     DISPLAY '************************'.                                  
022700     DISPLAY SPACES                                                       
022800     MOVE PV-READ-COUNT               TO PV-DISP-NBR                      
022900     DISPLAY 'CARTONS FETCHED            = ' PV-DISP-NBR                  
023000                                                                          
023100     MOVE PV-WRITE-COUNT              TO PV-DISP-NBR                      
023200     DISPLAY 'EXTRACTS WRITTEN           = ' PV-DISP-NBR                  
023300     DISPLAY SPACES                                                       
023400     DISPLAY '************************'.                                  
023500                                                                          
023600 9900-DB2-ABEND.                                                          
023700                                                                          
023800     MOVE SQLCODE TO PV-SQLCODE.                                          
023900     DISPLAY '*** DB2 ERROR '.                                            
024000     DISPLAY '*** SQLCODE   = ' PV-SQLCODE.                               
024100     DISPLAY '*** PROGRAM   = ' PV-ABEND-PROGRAM.                         
024200     DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.                       
024300     DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.                       
024400     DISPLAY '*** MESSAGE   = ' PV-DB2-OPERATION-MESSAGE-1.               
024500     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-2.               
024600     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-3.               
024700     DISPLAY '*** TABLE 1   = ' PV-ABEND-STRUCTURE-1.                     
024800     DISPLAY '*** TABLE 2   = ' PV-ABEND-STRUCTURE-2.                     
024900                                                                          
025000                                                                          
025100     COPY DPPD004.                                                        
025200                                                                          
025300     MOVE +4013                  TO PV-ABEND-CODE.                        
025400     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
025500                                                                          
025600*9200-ABEND.                                                              
025700******************************************************************        
025800*  PERFORMS A NON-SQL ABEND FOR THE PROGRAM                               
025900******************************************************************        
026000*    DISPLAY '***************************'.                               
026100*    DISPLAY '**       ABEND           **'.                               
026200*    DISPLAY '**  IN PROGRAM SUKUT062  **'.                               
026300*    DISPLAY '***************************'.                               
026400*    DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
026500*    DISPLAY '**                 ** = ' PV-ERROR-MESSAGE-1.               
026600*    DISPLAY '**                 ** = ' PV-ERROR-MESSAGE-2.               
026700*                                                                         
026800*    MOVE +4014                  TO PV-ABEND-CODE.                        
026900*    CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
027000*                                                                         
