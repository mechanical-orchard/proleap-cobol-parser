       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT125.                                       
       AUTHOR.                  JULIE SEIDLER.                                  
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/23/99.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *  REFORMAT SUMMARIZED VARIANCE DATA FOR THE DATA WAREHOUSE               
      *                                                                         
      *  . PROGRAM WILL REFORMAT INPUT FILE.                                    
      *  . PROGRAM WILL DETERMINE THE ITEM STATUS IF IT DOES NOT                
      *    EXIST.  THE STATUS CODE WILL NOT EXIST FOR AFTER-BOOKING             
      *    ADJUSTMENT TRANSACTIONS.  DEPENDING ON THE SORTSUM OF THE            
      *    FILE, THE STATUS MAY OR MAY BE EXIST.                                
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES (MADE BY)          *        
      * -------  ----------  ----------------------------------------  *        
      * XXXX     02/04/2000  CHANGED 8020- ROUTINE TO NOT ABEND, IF AN *        
      *                      ITEM IS NOT FOUND ON TUPCPLS.  INSTEAD    *        
      *                      A STATUS OF '20' WILL BE ASSIGNED TO THE  *        
      *                      ITEM AND WRITTEN OUT.  (J. SEIDLER)       *        
      * 21015    09/26/2000  ADDED A VARIANCE AUDIT FILE THAT WILL BE  *        
      *                      USED BY INKRP110.      (A. LAWSON)        *        
R11902* R11902   01/20/2002  INCREASE TABLE FROM 400 TO 500            *        
R11902*                                             (R.BUTZ)           *        
      * 24521    04/05/2003  ADD TRUE-CUTOFF-QTY TO INPUT AND OUTPUT.  *        
      *                      REMOVE LOGIC TO OBTAIN INVENTORY DATE AND *        
      *                      SKU/ITEM CLEARANCE STATUS FOR AFTER UNIT  *        
      *                      BOOKING ADJUSTMENT TRANSACTIONS.  THIS    *        
      *                      SCENARIO SHOULD NOT BE PRESENT BECAUSE    *        
      *                      THE NEW AFTER UNIT BOOKING EXTRACTION     *        
      *                      FILL IN THE APPROPRIATE VALUE.            *        
      *                                             (V. LEE YANG)      *        
W26600* W26600  07-22-2004 JIM DRAWS INVENTORY STORE EXPANSION                  
W28545* W28545  08-10-2006   B. GRAHAM - STRATAGEM                              
W28545* W28545               DEPARTMENT INVENTORY PROJECT                       
W28545* W28545               TINVPAR/TINCNTL JOIN                               
W33304* W33304  08-11-2008   D. THEEL - HILO PROJECT                   *        
W33304* W33304              -CHANGED VARIANCE OUTPUT LRECL TO 100.     *        
W33304* W33304              -REMOVED ALL REFERENCES TO DATE CONTROL    *        
W33304* W33304               FILE.                                     *        
W33304* W33304              -INCREASE AUDIT OUTPUT LRECL TO 100.       *        
W33304* W33304              -MODIFY 2000-PROCESS-INPUT                 *        
W33304* W33304              -CREATE NEW 2020-PROCESS-INPUT-CONT        *        
W33304* W33304              -MODIFY CURRENT 2000-PROCESS-INPUT         *        
W33304* W33304              -MODIFY 2010-GET-STR-INV-DTE               *        
W33304* W33304              -MODIFY 8010-GET-DATE-ATTRIBUTES           *        
W33304* W33304              -COMMENT OUT OPERCO NBR IN TUPCPLS WHERE   *        
W33304* W33304               CLAUSE                                    *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT VARIANCE-INPUT                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT VARIANCE-REFMT                                                
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT AUDIT-FILE                                                    
               ASSIGN TO OUT03.                                                 
                                                                                
24521      SELECT RECORD-COUNT-CONTROL                                          
24521          ASSIGN TO OUT04.                                                 
24521                                                                           
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  VARIANCE-INPUT                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 60 CHARACTERS                                        
           DATA RECORD IS VARIANCE-INPUT-REC.                                   
       01  VARIANCE-INPUT-REC PIC X(60).                                        
                                                                                
       FD  VARIANCE-REFMT                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           BLOCK CONTAINS 0 RECORDS                                             
W33304     RECORD CONTAINS 100 CHARACTERS                                       
           DATA RECORD IS VARIANCE-REFMT-REC.                                   
W33304 01  VARIANCE-REFMT-REC     PIC X(100).                                   
                                                                                
       FD  AUDIT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           BLOCK CONTAINS 0 RECORDS                                             
W33304     RECORD CONTAINS 100 CHARACTERS                                       
           DATA RECORD IS AUDIT-REC.                                            
W33304 01  AUDIT-RECORD       PIC X(100).                                       
                                                                                
24521  FD  RECORD-COUNT-CONTROL                                                 
24521      RECORDING MODE IS F                                                  
24521      LABEL RECORDS ARE OMITTED                                            
24521      BLOCK CONTAINS 0 RECORDS                                             
24521      RECORD CONTAINS 80 CHARACTERS                                        
24521      DATA RECORD IS RECORD-COUNT-CONTROL-REC.                             
24521  01  RECORD-COUNT-CONTROL-REC        PIC X(80).                           
24521                                                                           
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
24521      05  PV-PARAGRAPH-NAME           PIC X(25)       VALUE SPACE.         
24521      05  PV-DB2-OPER-ATTEMPTED       PIC X(30)       VALUE SPACE.         
24521      05  PV-UNIT-DIFFERENCE          PIC S9(9)       VALUE ZEROES         
24521                          COMP-3.                                          
24521      05  PV-TRUE-UNIT-DIFF           PIC S9(9)       VALUE ZEROES         
24521                          COMP-3.                                          
W26600     05  PV-STR-NBR-COMP         PIC S9(4)           VALUE ZEROES         
24521                          COMP.                                            
24521      05  PV-5-DIGIT-STORE-NBR        PIC 9(05)       VALUE ZEROES.        
24521      05  FILLER REDEFINES PV-5-DIGIT-STORE-NBR.                           
W26600         10  FILLER                  PIC X(01).                           
W26600         10  PV-4-DIGIT-STORE-NBR    PIC X(04).                           
           05  PV-PLND-INV-TMST.                                                
               10  PV-DB2-PLND-INV-DTE     PIC X(10)       VALUE SPACES.        
               10  FILLER                  PIC X(16)                            
                   VALUE '-23.59.59.999999'.                                    
           05  FILLER REDEFINES PV-PLND-INV-TMST.                               
               10  PV-PLND-INV-TMST-REDEF  PIC X(26).                           
24521      05  PV-RECORD-COUNT             PIC 9(11)       VALUE ZEROES.        
24521      05  PV-STAT-CDE-IS-SPACES       PIC 9(09)       VALUE ZEROES.        
24521      05  PV-BAD-ITM-NBR              PIC 9(09)       VALUE ZEROES.        
24521      05  PV-MAX-BAD-DATA             PIC 9(09)       VALUE 200.           
W33304     05  PV-PREV-STR-NBR             PIC S9(05)      COMP-3               
                                                           VALUE ZERO.          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
24521      05  PC-03                       PIC X(02)       VALUE '03'.     CL**1
24521      05  PC-REGULAR-CLEAR            PIC X(02)       VALUE '20'.     CL**1
24521      05  PC-REGIONAL-CLEAR           PIC X(02)       VALUE '25'.     CL**1
24521      05  PC-ZERO-STORE               PIC S9(4)       VALUE ZEROES    CL**1
24521                          COMP.                                            
24521      05  PC-NINES-DATE               PIC X(10)        VALUE               
24521          '9999-09-09'.                                                    
W33304     05  PC-RGIS                     PIC S9(09) VALUE +8428          CL**1
W33304                         COMP.                                            
W33304     05  PC-WIS                      PIC S9(09) VALUE +9203          CL**1
W33304                         COMP.                                            
W33304     05  PC-NO                       PIC S9(09) VALUE +117902        CL**1
W33304                         COMP.                                            
                                                                                
       01  PS-SWITCHES.                                                         
           05  END-OF-VAR-INPUT-INV-SW     PIC X       VALUE 'N'.               
               88  END-OF-VAR-INPUT                    VALUE 'Y'.               
                                                                                
       01  ABEND-CODE                          PIC S9(04)  VALUE +4003          
                                                           COMP SYNC.           
                                                                                
      *****************************************************************         
      *  INPUT VARIANCE DATA                                                    
      *****************************************************************         
24521      COPY INRD009.                                                        
                                                                                
      *****************************************************************         
      *  VARIANCE SUM RECORD LAYOUT                                             
      *****************************************************************         
24521      COPY INRD138.                                                        
                                                                                
      *****************************************************************         
      *  AUDIT FILE LAYOUT                                                      
      *****************************************************************         
W26600     COPY INRD136.                                                        
                                                                                
24521 *****************************************************************         
24521 *  OUTPUT CONTROL DATE (BOM) PASSED TO DATA WAREHOUSE                     
24521 *****************************************************************         
24521  01  WS-REC-COUNT-CONTROL-REC.                                            
24521      05  FILLER                      PIC X(01)       VALUE SPACES.        
24521          88  WS-COMMENT-RECORD                       VALUE '*'.           
24521          88  WS-NOT-COMMENT-RECORD                   VALUE SPACES.        
24521      05  WS-REC-COUNT                PIC 9(11)       VALUE ZEROES.        
24521      05  FILLER                      PIC X(68)       VALUE SPACES.        
24521  01  FILLER REDEFINES WS-REC-COUNT-CONTROL-REC.                           
24521      05  FILLER                      PIC X(01).                           
24521      05  WS-REC-COUNT-COMMENT        PIC X(79).                           
24521          88  WS-REC-COUNT-COMMENT-1                  VALUE                
24521              ' THIS FILE IS BEING GENERATED OUT OF INKUT125.'.            
24521          88  WS-REC-COUNT-COMMENT-2                  VALUE                
24521              ' COL 01-01 IS A RECORD INDICATOR; * = COMMENT.'.            
24521          88  WS-REC-COUNT-COMMENT-3                  VALUE                
24521              ' COL 02-12 IS NUMBER OF RECORD GENERATED.'.                 
24521                                                                           
      ****************************************************************          
      * DATE ATTRIBUTE TABLE                                                    
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
28545 *                                                                 01380000
28545 ******************************************************************01320000
28545 *  DCLGEN FOR THE INVENTORY CONTROL TABLE.                        01330000
28545 ******************************************************************01340000
28545      EXEC SQL                                                     01350000
28545          INCLUDE TINCNTL                                          01360000
28545      END-EXEC.                                                    01370000
28545 *                                                                 01380000
                                                                                
      ****************************************************************          
      * INVENTORY PARAMETER TABLE                                               
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * UPC PRICE LISTING TABLE                                                 
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TUPCPLS                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *  COMMUNICATION AREAS FOR DB2                                            
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *  ERROR MESSAGE AREA FOR DB2                                             
      ****************************************************************          
                                                                                
           COPY DPWS004.                                                        
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT VARIANCE-INPUT                                            
               OUTPUT VARIANCE-REFMT                                            
24521                 AUDIT-FILE                                                
24521                 RECORD-COUNT-CONTROL.                                     
                                                                                
           PERFORM 7000-READ-VARIANCE-INPUT.                                    
                                                                                
           IF END-OF-VAR-INPUT                                                  
               DISPLAY '****************************'                           
               DISPLAY 'INPUT VARIANCE FILE IS EMPTY'                           
               DISPLAY '****************************'                           
           ELSE                                                                 
               PERFORM 1000-HOUSEKEEPING                                        
               PERFORM 2000-PROCESS-INPUT                                       
                   UNTIL END-OF-VAR-INPUT                                       
           END-IF.                                                              
                                                                                
           PERFORM 3000-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       1000-HOUSEKEEPING.                                                       
                                                                                
      *    *************************************************************        
      *    *  USE INVENTORY DATE FOR FIRST STORE IN VARIANCE FILE               
      *    *************************************************************        
           IF IN009-INV-DTE = PC-NINES-DATE                                     
24521          DISPLAY '****************************************'               
24521          DISPLAY '  RECORDS FOUND WITH ALL 9S INVENTORY'                  
24521                  ' DATE.'                                                 
24521          DISPLAY '  CHECK AFTER UNIT BOOKING ADJUSTMENT'                  
24521                  ' TRANSACTIONS EXTRACT PROCESS'                          
24521          DISPLAY '****************************************'               
24521          MOVE +4003 TO ABEND-CODE                                         
24521          PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
24521      SET WS-COMMENT-RECORD TO TRUE.                                       
24521      SET WS-REC-COUNT-COMMENT-1 TO TRUE.                                  
24521      PERFORM 7520-WRITE-REC-COUNT.                                        
                                                                                
24521      SET WS-REC-COUNT-COMMENT-2 TO TRUE.                                  
24521      PERFORM 7520-WRITE-REC-COUNT.                                        
                                                                                
24521      SET WS-REC-COUNT-COMMENT-3 TO TRUE.                                  
24521      PERFORM 7520-WRITE-REC-COUNT.                                        
                                                                                
24521      MOVE SPACES TO WS-REC-COUNT-COMMENT.                                 
24521      PERFORM 7520-WRITE-REC-COUNT.                                        
                                                                                
                                                                                
W33304 2000-PROCESS-INPUT.                                                      
W33304     IF IN009-STR-NBR = PV-PREV-STR-NBR                                   
W33304         IF INVPAR-LOC-INV-STAT-CDE = 'IN'                                
W33304             PERFORM 2020-PROCESS-INPUT-CONT                              
W33304         END-IF                                                           
W33304     ELSE                                                                 
W33304         PERFORM 2010-GET-STR-INV-DTE                                     
W33304         IF INVPAR-LOC-INV-STAT-CDE = 'IN'                                
W33304             PERFORM 8010-GET-DATE-ATTRIBUTES                             
W33304             PERFORM 2020-PROCESS-INPUT-CONT                              
W33304         END-IF                                                           
W33304     END-IF.                                                              
W33304     MOVE IN009-STR-NBR TO PV-PREV-STR-NBR.                               
W33304     PERFORM 7000-READ-VARIANCE-INPUT.                                    
                                                                                
      ******************************************************************        
24521 *    GET STORE'S PLANNED INVENTORY DATE FROM INVENTORY PARAMETER          
      ******************************************************************        
       2010-GET-STR-INV-DTE.                                                    
W33304     MOVE IN009-STR-NBR TO INVPAR-LOC-NBR.                                
                                                                                
24521      EXEC SQL                                                             
W33304         SELECT  A.PLND_INV_DTE                                           
W33304                ,A.INV_ID                                                 
W33304                ,A.INV_SRVC_CDE                                           
W33304                ,A.FINCL_INV_STAT_CDE                                     
W33304                ,A.LOC_INV_STAT_CDE                                       
W33304                ,B.PLND_FIN_BKG_DTE                                       
24521              INTO  :INVPAR-PLND-INV-DTE                                   
W33304                  ,:INVPAR-INV-ID                                         
W33304                  ,:INVPAR-INV-SRVC-CDE                                   
W33304                  ,:INVPAR-FINCL-INV-STAT-CDE                             
W33304                  ,:INVPAR-LOC-INV-STAT-CDE                               
W33304                  ,:INCNTL-PLND-FIN-BKG-DTE                               
24521              FROM  TINVPAR A                                              
28545                   ,TINCNTL B                                              
W26600             WHERE LOC_NBR = :INVPAR-LOC-NBR                              
28545                AND A.INV_ID = B.INV_ID                                    
28545                AND B.ACTV_IND = 'Y'                                       
W33304               AND A.ACTL_FIN_BK_DTE = '9999-09-09'                       
W33304               AND A.LOC_INV_STAT_CDE = 'IN'                              
24521      END-EXEC.                                                            
24521                                                                           
24521      EVALUATE TRUE                                                        
24521          WHEN SQLCODE = +0                                                
                   MOVE INVPAR-INV-ID   TO IN136-INV-ID                         
                   MOVE INVPAR-FINCL-INV-STAT-CDE TO                            
                                           IN136-FINCL-INV-STAT-CDE             
24521          WHEN SQLCODE = +100                                              
W33304             MOVE SPACES TO INVPAR-LOC-INV-STAT-CDE                       
24521          WHEN OTHER                                                       
24521              DISPLAY 'STORE NUMBER: '                                     
W26600                      PV-5-DIGIT-STORE-NBR                                
24521              MOVE    '2010-GET-STR-INV-DTE'                               
24521                                       TO PV-PARAGRAPH-NAME                
24521              MOVE    'SELECT FROM TINVPAR'                                
24521                                       TO PV-DB2-OPER-ATTEMPTED            
24521              PERFORM 9999-SQL-ABEND                                       
24521      END-EVALUATE.                                                        
24521                                                                           
24521                                                                           
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       2020-PROCESS-INPUT-CONT.                                                 
W33304     MOVE INVPAR-INV-ID               TO IN136-INV-ID                     
W33304                                         IN138-INV-ID.                    
W33304     MOVE INVPAR-FINCL-INV-STAT-CDE   TO IN136-FINCL-INV-STAT-CDE.        
W33304     MOVE INCNTL-PLND-FIN-BKG-DTE TO                                      
W33304                                   IN136-CYCLE-PLND-FIN-BKG-DTE.          
W33304     IF INVPAR-INV-SRVC-CDE = 'RG'                                        
W33304         MOVE PC-RGIS    TO IN138-INV-SRVC-ENT-ID                         
W33304     END-IF.                                                              
W33304     IF INVPAR-INV-SRVC-CDE = 'WI'                                        
W33304         MOVE PC-WIS     TO IN138-INV-SRVC-ENT-ID                         
W33304     END-IF.                                                              
W33304     IF INVPAR-INV-SRVC-CDE = 'NO'                                        
W33304         MOVE PC-NO      TO IN138-INV-SRVC-ENT-ID                         
W33304     END-IF.                                                              
      ****************************************************************          
      *    ON THE ADJ RECORDS THE STATUS CODE WILL NOT BE SUPPLIED.             
      *    THE STATUS CODE WILL BE OBTAINED FOR THE STORE AT THIS TIME          
      *    -------                                                              
      *    ON TUPCPLS,                                                          
      *    IF THE 000 ROW HAS A REGIONAL CLEARANCE STATUS (25) THEN             
      *    THE STORE ROW MUST BE RETRIEVED.  IF THE STORE ROW IS NOT            
      *    FOUND, THE STORE'S STATUS WILL BE REGUALAR STATUS (20)               
      ****************************************************************          
                                                                                
           IF IN009-STAT-CDE = SPACES                                           
              ADD +1 TO PV-STAT-CDE-IS-SPACES                                   
              IF IN009-INV-DTE = PC-NINES-DATE                                  
24521             MOVE INVPAR-PLND-INV-DTE      TO PV-DB2-PLND-INV-DTE          
24521                                              IN009-INV-DTE                
              ELSE                                                              
                  MOVE IN009-INV-DTE            TO PV-DB2-PLND-INV-DTE          
              END-IF                                                            
W26600        MOVE PC-ZERO-STORE       TO PV-STR-NBR-COMP                       
              PERFORM 8020-GET-STATUS                                           
24521         IF UPCPLS-UPC-STAT-CDE = PC-REGIONAL-CLEAR                        
W26600            MOVE IN009-STR-NBR   TO PV-STR-NBR-COMP                       
                  PERFORM 8020-GET-STATUS                                       
              END-IF                                                            
              MOVE UPCPLS-UPC-STAT-CDE TO IN138-STAT-CDE                        
                                          IN136-STAT-CDE                        
           ELSE                                                                 
              MOVE IN009-STAT-CDE      TO IN138-STAT-CDE                        
                                          IN136-STAT-CDE                        
           END-IF.                                                              
                                                                                
           MOVE ZERO             TO IN138-UPC-ID                                
                                    IN136-UPC-ID.                               
           MOVE IN009-ITM-NBR    TO IN138-ITM-NBR                               
                                    IN136-ITM-NBR.                              
           IF IN009-ITM-NBR = ZERO                                              
               ADD +1 TO PV-BAD-ITM-NBR                                         
               IF PV-BAD-ITM-NBR > PV-MAX-BAD-DATA                              
                   DISPLAY '***-------------------------------------'           
                   DISPLAY PV-BAD-ITM-NBR ' RECORDS FOUND WITH ZERO'            
                           ' AS THE ITEM NUMBER. '                              
                   DISPLAY 'CHECK INK393 AND FSK025 PROCESSES'                  
                   DISPLAY '***-------------------------------------'           
                   MOVE +4003 TO ABEND-CODE                                     
                   PERFORM 9999-ABEND                                           
           END-IF.                                                              
           MOVE IN009-STR-NBR    TO IN138-STR-NBR.                              
                                                                                
W26600     MOVE IN009-STR-NBR    TO PV-5-DIGIT-STORE-NBR                        
W26600                              IN136-STR-NBR-9.                            
                                                                                
           MOVE IN009-UNT-CTOFF  TO IN138-UNT-CTOFF                             
                                    IN136-UNT-CTOFF.                            
                                                                                
24521      MOVE ZEROES           TO IN138-UNTS-INCR                             
24521                               IN136-UNTS-INCR                             
24521                               IN138-TRUE-UNTS-INCR                        
24521                               IN136-TRUE-UNTS-INCR                        
24521                               IN138-UNTS-DECR                             
24521                               IN136-UNTS-DECR                             
24521                               IN138-TRUE-UNTS-DECR                        
24521                               IN136-TRUE-UNTS-DECR.                       
                                                                                
           COMPUTE PV-UNIT-DIFFERENCE = IN009-PHY-CNT - IN009-UNT-CTOFF.        
           IF PV-UNIT-DIFFERENCE < +0                                           
               MOVE PV-UNIT-DIFFERENCE TO IN138-UNTS-DECR                       
                                          IN136-UNTS-DECR                       
           ELSE                                                                 
               MOVE PV-UNIT-DIFFERENCE TO IN138-UNTS-INCR                       
                                          IN136-UNTS-INCR                       
           END-IF.                                                              
                                                                                
24521      MOVE IN009-TRUE-CUTOFF-QTY  TO IN138-TRUE-CUTOFF-QTY                 
24521                                     IN136-TRUE-CUTOFF-QTY.                
24521                                                                           
24521      COMPUTE PV-TRUE-UNIT-DIFF = IN009-PHY-CNT                            
24521                                - IN009-TRUE-CUTOFF-QTY.                   
24521      IF PV-TRUE-UNIT-DIFF < +0                                            
24521          MOVE PV-TRUE-UNIT-DIFF TO IN138-TRUE-UNTS-DECR                   
24521                                    IN136-TRUE-UNTS-DECR                   
24521      ELSE                                                                 
24521          MOVE PV-TRUE-UNIT-DIFF TO IN138-TRUE-UNTS-INCR                   
24521                                    IN136-TRUE-UNTS-INCR                   
24521      END-IF.                                                              
24521                                                                           
           PERFORM 7500-WRITE-VARIANCE-REFMT.                                   
                                                                                
           PERFORM 7600-WRITE-AUDIT-RECORD.                                     
                                                                                
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
       3000-EOJ.                                                                
                                                                                
           DISPLAY 'NBR OF RECORDS WHERE STAT-CDE WAS SPACES ==> '              
               PV-STAT-CDE-IS-SPACES.                                           
                                                                                
           DISPLAY 'NBR OF RECORDS WHERE ITEM NBR WAS ZEROES ==> '              
               PV-BAD-ITM-NBR.                                                  
                                                                                
           DISPLAY 'NBR OF RECORDS WRITTEN                   ==> '              
               PV-RECORD-COUNT.                                                 
24521      SET WS-NOT-COMMENT-RECORD TO TRUE.                                   
24521      MOVE PV-RECORD-COUNT TO WS-REC-COUNT.                                
24521      PERFORM 7520-WRITE-REC-COUNT.                                        
                                                                                
           CLOSE VARIANCE-INPUT                                                 
                 VARIANCE-REFMT                                                 
                 AUDIT-FILE                                                     
24521            RECORD-COUNT-CONTROL.                                          
                                                                                
      ******************************************************************        
      *    READ VARIANCE INPUT                                                  
      ******************************************************************        
       7000-READ-VARIANCE-INPUT.                                                
           READ VARIANCE-INPUT                                                  
               INTO IN009-VARIANCE-RECORD                                       
               AT END SET END-OF-VAR-INPUT TO TRUE.                             
      ******************************************************************        
      *    WRITE VARIANCE SUM RECORD                                            
      ******************************************************************        
                                                                                
       7500-WRITE-VARIANCE-REFMT.                                               
                                                                                
24521      ADD +1               TO PV-RECORD-COUNT.                             
                                                                                
           WRITE VARIANCE-REFMT-REC                                             
               FROM IN138-VARIANCE-SUMM-RECORD.                                 
                                                                                
24521 ******************************************************************        
24521 *    WRITE RECORD COUNT CONTROL RECORD                                    
24521 ******************************************************************        
24521  7520-WRITE-REC-COUNT.                                                    
24521                                                                           
24521      WRITE RECORD-COUNT-CONTROL-REC                                       
24521          FROM WS-REC-COUNT-CONTROL-REC.                                   
24521                                                                           
      ******************************************************************        
      *    WRITE AUDIT RECORD                                                   
      ******************************************************************        
                                                                                
       7600-WRITE-AUDIT-RECORD.                                                 
                                                                                
           WRITE AUDIT-RECORD                                                   
               FROM IN136-AUDIT-RECORD.                                         
                                                                                
      ******************************************************************        
      *    GET THE BEGINNING MONTH DATE FOR OPEN FISCAL MONTH                   
      ******************************************************************        
       8010-GET-DATE-ATTRIBUTES.                                                
                                                                                
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                        FSCL_MN_BEG_DTE                                         
                    INTO                                                        
                        :DTATTR-FSCL-MN-BEG-DTE                                 
                    FROM                                                        
                        TDTATTR                                                 
                    WHERE                                                       
W33304                  KY_DTE = :INVPAR-PLND-INV-DTE                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +000                                          
W33304                MOVE DTATTR-FSCL-MN-BEG-DTE TO IN138-BEG-WK-DTE           
W33304                                               IN136-BEG-WK-DTE           
                   WHEN OTHER                                                   
24521                  DISPLAY 'STORE INVENTORY DATE: '                         
W33304                          INVPAR-PLND-INV-DTE                             
                       MOVE    '8010-GET-DATE-ATTRIBUTES'                       
                                                TO PV-PARAGRAPH-NAME            
                       MOVE    'SELECT ROW FROM TDTATTR'                        
                                                TO PV-DB2-OPER-ATTEMPTED        
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE.                                                    
                                                                                
                                                                                
      ******************************************************************        
24521 *    GET THE STATUS OF AN ITEM AS OF INVENTORY DATE                       
      ******************************************************************        
       8020-GET-STATUS.                                                         
                                                                                
           INITIALIZE DCLTUPCPLS.                                               
                                                                                
           EXEC SQL                                                             
             SELECT   UPC_STAT_CDE                                              
               INTO  :UPCPLS-UPC-STAT-CDE                                       
               FROM   TUPCPLS                                                   
              WHERE   ITM_NBR = :IN009-ITM-NBR                                  
W33304*         AND   OPERCO_NBR = :PC-03                                       
W26600          AND   STR_NBR = :PV-STR-NBR-COMP                                
                AND   EFF_BEG_TMST <= :PV-PLND-INV-TMST-REDEF                   
                AND   (EFF_END_TMST >= :PV-PLND-INV-TMST-REDEF                  
                 OR    EFF_END_TMST IS NULL)                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
24521              MOVE PC-REGULAR-CLEAR TO UPCPLS-UPC-STAT-CDE                 
W26600             IF PV-STR-NBR-COMP = ZERO                                    
                       DISPLAY 'UNABLE TO FIND CUTOFF PRICE ROW'                
                               ' ON TUPCPLS.  USING DEFAULTS '                  
                       DISPLAY 'FOR ITEM NUMBER ' IN009-ITM-NBR                 
W26600                     ' TUPCPLS STR  ' PV-STR-NBR-COMP                     
                           ' PV-PLND-INV-TMST ' PV-PLND-INV-TMST-REDEF          
                           ' STORE NUMBER ' IN009-STR-NBR                       
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE    '8020-GET-STAT-CDE       '                           
                                            TO PV-PARAGRAPH-NAME                
                   MOVE    'SELECT ROW FROM TUPCPLS'                            
                                            TO PV-DB2-OPER-ATTEMPTED            
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** = INKUT125'.                               
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *                                                                         
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 RELATED PROBLEM.             
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
24521      DISPLAY '** PROGRAM          ** = INKUT125'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
