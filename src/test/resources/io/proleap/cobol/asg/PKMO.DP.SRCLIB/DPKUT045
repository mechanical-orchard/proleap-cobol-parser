00001 ******************************************************************02/15/91
00002  IDENTIFICATION DIVISION.                                         DPKUT045
00003 ******************************************************************   LV002
00004                                                                   DPKUT045
00005  PROGRAM-ID.                DPKUT045.                             DPKUT045
00006  AUTHOR.                    RICK TULLOCH.                         DPKUT045
00007  INSTALLATION.              KOHLS DEPARTMENT STORES.              DPKUT045
00008  DATE-WRITTEN.              8-08-90.                              DPKUT045
00009  DATE-COMPILED.                                                   DPKUT045
00010                                                                   DPKUT045
00011 ******************************************************************DPKUT045
00012 *       THIS SUBROUTINE WILL TRANSLATE THE EIBAID CHARACTER TO   *DPKUT045
00013 *  A NUMERIC VALUE, AND INTERROGATE THE NAVIGATION COMMAND       *DPKUT045
00014 *  FOR LITERALS THAT CAN BE CONVERTED TO FUNCTION KEY VALUES.    *DPKUT045
00015 *  USE COPY MEMBER DPWS045 FOR THE PARAMETER LIST.               *DPKUT045
00016 *                                                                *DPKUT045
00017 *       THE SUBROUTINE BEGINS BY VALIDATING THE PARAMETERS       *DPKUT045
00018 *                                                                *DPKUT045
00019 *       **  NOTE  **                                             *DPKUT045
00020 *                                                                *DPKUT045
00021 *       WHENEVER THIS ROUTINE IS MOVED TO PRODUCTION, THE LOAD   *DPKUT045
00022 *  MODULE MUST BE COPIED INTO THE CICS LOADLIB, TO MAKE IT       *DPKUT045
00023 *  AVAILABLE TO CICS.                                            *DPKUT045
00024 ******************************************************************DPKUT045
00025                                                                   DPKUT045
00026 ******************************************************************DPKUT045
00027  ENVIRONMENT DIVISION.                                            DPKUT045
00028 ******************************************************************DPKUT045
00029                                                                   DPKUT045
00030  CONFIGURATION SECTION.                                           DPKUT045
00031                                                                   DPKUT045
00032  SOURCE-COMPUTER.        IBM-3090.                                DPKUT045
00033  OBJECT-COMPUTER.        IBM-3090.                                DPKUT045
00034                                                                   DPKUT045
00035  INPUT-OUTPUT SECTION.                                            DPKUT045
00036                                                                   DPKUT045
00037  FILE-CONTROL.                                                    DPKUT045
00038                                                                   DPKUT045
00039 ******************************************************************DPKUT045
00040  DATA DIVISION.                                                   DPKUT045
00041 ******************************************************************DPKUT045
00042                                                                   DPKUT045
00043  WORKING-STORAGE SECTION.                                         DPKUT045
00044                                                                   DPKUT045
00045 *                                                                 DPKUT045
00046 *  INTERROGATE EIBAID AND COMMAND ROUTINE PARAMETER LIST LAYOUT.  DPKUT045
00047 *                                                                 DPKUT045
00048                                                                   DPKUT045
00049 *    COPY DPWS045.                                                   CL**2
00050      COPY DPWS045.                                                   CL**2
00051                                                                   DPKUT045
00052 *                                                                    CL**2
00053 *  STANDARD ATTENTION IDENTIFIER LIST - NUMERIC                      CL**2
00054 *                                                                    CL**2
00055                                                                      CL**2
00056 *    COPY DPWS016.                                                   CL**2
00057      COPY DPWS016.                                                   CL**2
00058                                                                   DPKUT045
00059  01  PC-PROGRAM-CONSTANTS.                                        DPKUT045
00060      05  PC-MAXIMUM-FUNCTION-KEYS                                    CL**5
00061                                  PIC S9(4)    VALUE +24              CL**5
00062                                  COMP SYNC.                       DPKUT045
00063                                                                   DPKUT045
00064  01  PV-PROGRAM-VARIABLES.                                        DPKUT045
00065      05  PV-EIBAID               PIC X(1)     VALUE SPACES.          CL**5
00066          88  PV-EIBAID-ENTER                  VALUE QUOTE.           CL**6
00067          88  PV-EIBAID-CLEAR                  VALUE '_'.             CL**6
00068          88  PV-EIBAID-PA1                    VALUE '%'.             CL**6
00069          88  PV-EIBAID-PA2                    VALUE '>'.             CL**6
00070          88  PV-EIBAID-PA3                    VALUE ','.             CL**6
00071          88  PV-EIBAID-PF1                    VALUE '1'.             CL**6
00072          88  PV-EIBAID-PF2                    VALUE '2'.             CL**6
00073          88  PV-EIBAID-PF3                    VALUE '3'.             CL**6
00074          88  PV-EIBAID-PF4                    VALUE '4'.             CL**6
00075          88  PV-EIBAID-PF5                    VALUE '5'.             CL**6
00076          88  PV-EIBAID-PF6                    VALUE '6'.             CL**6
00077          88  PV-EIBAID-PF7                    VALUE '7'.             CL**6
00078          88  PV-EIBAID-PF8                    VALUE '8'.             CL**6
00079          88  PV-EIBAID-PF9                    VALUE '9'.             CL**6
00080          88  PV-EIBAID-PF10                   VALUE ':'.             CL**6
00081          88  PV-EIBAID-PF11                   VALUE '#'.             CL**6
00082          88  PV-EIBAID-PF12                   VALUE '@'.             CL**6
00083          88  PV-EIBAID-PF13                   VALUE 'A'.             CL**6
00084          88  PV-EIBAID-PF14                   VALUE 'B'.             CL**6
00085          88  PV-EIBAID-PF15                   VALUE 'C'.             CL**6
00086          88  PV-EIBAID-PF16                   VALUE 'D'.             CL**6
00087          88  PV-EIBAID-PF17                   VALUE 'E'.             CL**6
00088          88  PV-EIBAID-PF18                   VALUE 'F'.             CL**6
00089          88  PV-EIBAID-PF19                   VALUE 'G'.             CL**6
00090          88  PV-EIBAID-PF20                   VALUE 'H'.             CL**6
00091          88  PV-EIBAID-PF21                   VALUE 'I'.             CL**6
00092          88  PV-EIBAID-PF22                   VALUE '›'.             CL**6
00093          88  PV-EIBAID-PF23                   VALUE '.'.             CL**6
00094          88  PV-EIBAID-PF24                   VALUE '<'.             CL**6
00095      05  PV-NAVIGATION-MNEMONIC  PIC X(8)     VALUE SPACES.          CL**5
00096          88  PV-CLEAR-MNEMONIC                VALUE 'CLR     ',      CL**2
00097                                                     'CLEAR   '.      CL**2
00098          88  PV-PF8-MNEMONIC                  VALUE 'F       ',      CL**2
00099                                                     'FO      ',      CL**2
00100                                                     'FOR     ',      CL**2
00101                                                     'FORWARD ',      CL**2
00102                                                     'D       ',      CL**2
00103                                                     'DO      ',      CL**2
00104                                                     'DOWN    ',      CL**2
00105                                                     'N       ',      CL**2
00106                                                     'NEXT    ',      CL**7
00107                                                     'NEXTPAGE'.      CL**7
00108          88  PV-PF7-MNEMONIC                  VALUE 'B       ',      CL**2
00109                                                     'BA      ',      CL**2
00110                                                     'BACK    ',      CL**2
00111                                                     'BACKWARD',      CL**2
00112                                                     'U       ',      CL**2
00113                                                     'UP      ',      CL**2
00114                                                     'LAST    ',      CL**2
00115                                                     'PREV    ',      CL**7
00116                                                     'PREVPAGE'.      CL**7
00117          88  PV-PF6-MNEMONIC                  VALUE '1ST-PAGE'.      CL**7
00118          88  PV-PF5-MNEMONIC                  VALUE 'REFRESH '.      CL**7
00119          88  PV-PF3-MNEMONIC                  VALUE 'END     ',      CL**7
00120                                                     'X       ',      CL**2
00121                                                     'EX      ',      CL**2
00122                                                     'EXIT    ',      CL**2
00123                                                     'RET     ',      CL**2
00124                                                     'RETURN  '.      CL**2
00125          88  PV-PA2-MNEMONIC                  VALUE 'PA2     '.      CL**2
00126      05  FILLER                  REDEFINES PV-NAVIGATION-MNEMONIC.   CL**4
00127          10  PV-NAVIGATION-MNEMONIC-1-2.                             CL**5
00128              88  PV-FUNCTION-KEY-PF-LITERAL   VALUE 'PF'.            CL**5
00129              15  PV-NAVIGATION-MNEMONIC-1                            CL**6
00130                                  PIC X(1).                           CL**5
00131                  88  PV-FUNCTION-KEY-F-LITERAL                       CL**5
00132                                               VALUE 'F'.             CL**5
00133              15  PV-NAVIGATION-MNEMONIC-2                            CL**6
00134                                  PIC X(1).                           CL**6
00135          10  PV-NAVIGATION-MNEMONIC-3-8.                             CL**5
00136              15  PV-NAVIGATION-MNEMONIC-3                            CL**6
00137                                  PIC X(1).                           CL**6
00138              15  PV-NAVIGATION-MNEMONIC-4-8.                         CL**5
00139                  20  FILLER      PIC X(1).                           CL**5
00140                  20  PV-NAVIGATION-MNEMONIC-5-8                      CL**5
00141                                  PIC X(4).                           CL**5
00142      05  FILLER                  REDEFINES PV-NAVIGATION-MNEMONIC.   CL**5
00143          10  PV-FK-NUMBER-1      PIC 9(1).                           CL**5
00144          10  PV-NAVIGATION-MNEMONIC-2-8.                             CL**5
00145              15  PV-NAVIGATION-MNEMONIC-2-3.                         CL**5
00146                  20  PV-FK-NUMBER-2                                  CL**5
00147                                  PIC 9(1).                           CL**5
00148                  20  PV-FK-NUMBER-3                                  CL**5
00149                                  PIC 9(1).                           CL**5
00150              15  FILLER          PIC X(5).                           CL**5
00151      05  FILLER                  REDEFINES PV-NAVIGATION-MNEMONIC.   CL**5
00152          10  FILLER              PIC X(1).                           CL**5
00153          10  PV-FK-NUMBER-2-3    PIC 9(2).                           CL**5
00154          10  FILLER              PIC X(5).                           CL**5
00155      05  FILLER                  REDEFINES PV-NAVIGATION-MNEMONIC.   CL**5
00156          10  PV-FK-NUMBER-1-2    PIC 9(2).                           CL**6
00157          10  PV-NAVIGATION-MNEMONIC-3-4.                             CL**6
00158              15  PV-FK-NUMBER-3-4                                    CL**5
00159                                  PIC 9(2).                           CL**5
00160          10  FILLER              PIC X(4).                           CL**5
00161                                                                      CL**2
00162  LINKAGE SECTION.                                                 DPKUT045
00163                                                                   DPKUT045
00164  01  LS-INTERR-AID-COMMAND-PARMS                                     CL**6
00165                                  PIC X(12).                          CL**4
00166                                                                   DPKUT045
00167 ******************************************************************DPKUT045
00168  PROCEDURE DIVISION USING LS-INTERR-AID-COMMAND-PARMS.               CL**7
00169 ******************************************************************DPKUT045
00170                                                                   DPKUT045
00171  0000-MAINLINE.                                                   DPKUT045
00172      MOVE LS-INTERR-AID-COMMAND-PARMS TO                             CL**6
00173                DP045-INTERR-AID-COMMAND-PARMS.                       CL**6
00174                                                                   DPKUT045
00175      INITIALIZE DP045-RETURN-CODE.                                   CL**2
00176                                                                      CL**3
00177      PERFORM 1000-TRANSLATE-EIBAID.                                  CL**3
00178                                                                      CL**3
00179      SET DP045-AID-KEY-NOT-CONVERTED TO TRUE.                        CL**3
00180                                                                      CL**3
00181      IF ((DP045-SUCCESSFUL-COMPLETION)                               CL**2
00182              AND (DP045-SOURCE-AID-KEY = DP016-ENTER))               CL**3
00183          PERFORM 2000-PARSE-NAVIGATION-COMMAND                       CL**3
00184      END-IF.                                                         CL**3
00185                                                                   DPKUT045
00186 *                                                                 DPKUT045
00187 *  MOVE THE PARAMETERS LIST, WITH RESPONSES INCLUDED, TO THE      DPKUT045
00188 *  LINKAGE SECTION, FOR PASSING BACK TO THE CALLING PROGRAM.      DPKUT045
00189 *                                                                 DPKUT045
00190                                                                   DPKUT045
00191      MOVE DP045-INTERR-AID-COMMAND-PARMS TO                          CL**6
00192                LS-INTERR-AID-COMMAND-PARMS.                          CL**6
00193                                                                   DPKUT045
00194  0000-EXIT-PROGRAM.                                                  CL**3
00195      EXIT PROGRAM.                                                DPKUT045
00196                                                                   DPKUT045
00197 ******************************************************************   CL**3
00198 *  TRANSLATE THE PASSED EIBAID INTO A NUMERIC VALUE.  SET A      *   CL**3
00199 *  RETURN CODE IF AN INVALID VALUE IS DETECTED.                  *   CL**3
00200 ******************************************************************   CL**3
00201                                                                      CL**3
00202  1000-TRANSLATE-EIBAID.                                              CL**3
00203      MOVE DP045-EIBAID TO PV-EIBAID.                                 CL**6
00204                                                                      CL**6
00205      EVALUATE TRUE                                                   CL**6
00206          WHEN PV-EIBAID-PF1                                          CL**6
00207              MOVE DP016-PF1   TO DP045-SOURCE-AID-KEY                CL**3
00208          WHEN PV-EIBAID-PF2                                          CL**6
00209              MOVE DP016-PF2   TO DP045-SOURCE-AID-KEY                CL**3
00210          WHEN PV-EIBAID-PF3                                          CL**6
00211              MOVE DP016-PF3   TO DP045-SOURCE-AID-KEY                CL**3
00212          WHEN PV-EIBAID-PF4                                          CL**6
00213              MOVE DP016-PF4   TO DP045-SOURCE-AID-KEY                CL**3
00214          WHEN PV-EIBAID-PF5                                          CL**6
00215              MOVE DP016-PF5   TO DP045-SOURCE-AID-KEY                CL**3
00216          WHEN PV-EIBAID-PF6                                          CL**6
00217              MOVE DP016-PF6   TO DP045-SOURCE-AID-KEY                CL**3
00218          WHEN PV-EIBAID-PF7                                          CL**6
00219              MOVE DP016-PF7   TO DP045-SOURCE-AID-KEY                CL**3
00220          WHEN PV-EIBAID-PF8                                          CL**6
00221              MOVE DP016-PF8   TO DP045-SOURCE-AID-KEY                CL**3
00222          WHEN PV-EIBAID-PF9                                          CL**6
00223              MOVE DP016-PF9   TO DP045-SOURCE-AID-KEY                CL**3
00224          WHEN PV-EIBAID-PF10                                         CL**6
00225              MOVE DP016-PF10  TO DP045-SOURCE-AID-KEY                CL**3
00226          WHEN PV-EIBAID-PF11                                         CL**6
00227              MOVE DP016-PF11  TO DP045-SOURCE-AID-KEY                CL**3
00228          WHEN PV-EIBAID-PF12                                         CL**6
00229              MOVE DP016-PF12  TO DP045-SOURCE-AID-KEY                CL**3
00230          WHEN PV-EIBAID-PF13                                         CL**6
00231              MOVE DP016-PF13  TO DP045-SOURCE-AID-KEY                CL**3
00232          WHEN PV-EIBAID-PF14                                         CL**6
00233              MOVE DP016-PF14  TO DP045-SOURCE-AID-KEY                CL**3
00234          WHEN PV-EIBAID-PF15                                         CL**6
00235              MOVE DP016-PF15  TO DP045-SOURCE-AID-KEY                CL**3
00236          WHEN PV-EIBAID-PF16                                         CL**6
00237              MOVE DP016-PF16  TO DP045-SOURCE-AID-KEY                CL**3
00238          WHEN PV-EIBAID-PF17                                         CL**6
00239              MOVE DP016-PF17  TO DP045-SOURCE-AID-KEY                CL**3
00240          WHEN PV-EIBAID-PF18                                         CL**6
00241              MOVE DP016-PF18  TO DP045-SOURCE-AID-KEY                CL**3
00242          WHEN PV-EIBAID-PF19                                         CL**6
00243              MOVE DP016-PF19  TO DP045-SOURCE-AID-KEY                CL**3
00244          WHEN PV-EIBAID-PF20                                         CL**6
00245              MOVE DP016-PF20  TO DP045-SOURCE-AID-KEY                CL**3
00246          WHEN PV-EIBAID-PF21                                         CL**6
00247              MOVE DP016-PF21  TO DP045-SOURCE-AID-KEY                CL**3
00248          WHEN PV-EIBAID-PF22                                         CL**6
00249              MOVE DP016-PF22  TO DP045-SOURCE-AID-KEY                CL**3
00250          WHEN PV-EIBAID-PF23                                         CL**6
00251              MOVE DP016-PF23  TO DP045-SOURCE-AID-KEY                CL**3
00252          WHEN PV-EIBAID-PF24                                         CL**6
00253              MOVE DP016-PF24  TO DP045-SOURCE-AID-KEY                CL**3
00254          WHEN PV-EIBAID-PA1                                          CL**6
00255              MOVE DP016-PA1   TO DP045-SOURCE-AID-KEY                CL**3
00256          WHEN PV-EIBAID-PA2                                          CL**6
00257              MOVE DP016-PA2   TO DP045-SOURCE-AID-KEY                CL**3
00258          WHEN PV-EIBAID-CLEAR                                        CL**6
00259              MOVE DP016-CLEAR TO DP045-SOURCE-AID-KEY                CL**3
00260          WHEN PV-EIBAID-ENTER                                        CL**6
00261              MOVE DP016-ENTER TO DP045-SOURCE-AID-KEY                CL**3
00262          WHEN OTHER                                                  CL**3
00263              INITIALIZE DP045-SOURCE-AID-KEY                         CL**2
00264              SET DP045-INVALID-EIBAID TO TRUE                        CL**2
00265      END-EVALUATE.                                                   CL**3
00266 *1000-EXIT.                                                          CL**3
00267                                                                      CL**3
00268 ******************************************************************   CL**3
00269 *  PARSE THE NAVIGATION COMMAND.  INTERROGATE FOR LITERALS THAT  *   CL**3
00270 *  REPRESENT FUNCTION KEYS.  IF SUCH A LITERAL IS FOUND,         *   CL**3
00271 *  CONVERT THE NUMERIC REPRESENTATION OF THE FUNCTION KEY TO     *   CL**3
00272 *  THAT OF THE FUNCTION KEY IMPLIED BY THE NAVIGATION COMMAND.   *   CL**3
00273 *  SET A SWITCH TO INDICATE THAT A CONVERSION HAS TAKEN PLACE.   *   CL**3
00274 ******************************************************************   CL**3
00275                                                                      CL**3
00276  2000-PARSE-NAVIGATION-COMMAND.                                      CL**3
00277      MOVE DP045-NAVIGATION-MNEMONIC TO PV-NAVIGATION-MNEMONIC.       CL**6
00278                                                                      CL**6
00279      EVALUATE TRUE                                                   CL**3
00280          WHEN ((PV-NAVIGATION-MNEMONIC-1-2 NUMERIC)                  CL**3
00281                  AND (PV-FK-NUMBER-1-2 > ZERO)                       CL**5
00282                  AND (PV-FK-NUMBER-1-2 NOT >                         CL**5
00283                                       PC-MAXIMUM-FUNCTION-KEYS)      CL**5
00284                  AND ((PV-NAVIGATION-MNEMONIC-3-8 = SPACES)          CL**3
00285                  OR (PV-NAVIGATION-MNEMONIC-3-8 = LOW-VALUES)))      CL**3
00286              MOVE PV-FK-NUMBER-1-2 TO DP045-SOURCE-AID-KEY           CL**3
00287              SET DP045-AID-KEY-CONVERTED TO TRUE                     CL**3
00288          WHEN ((PV-NAVIGATION-MNEMONIC-1 NUMERIC)                    CL**3
00289                  AND (PV-FK-NUMBER-1 > ZERO)                         CL**5
00290                  AND ((PV-NAVIGATION-MNEMONIC-2-8 = SPACES)          CL**3
00291                  OR (PV-NAVIGATION-MNEMONIC-2-8 = LOW-VALUES)))      CL**3
00292              MOVE PV-FK-NUMBER-1 TO DP045-SOURCE-AID-KEY             CL**3
00293              SET DP045-AID-KEY-CONVERTED TO TRUE                     CL**3
00294          WHEN PV-FUNCTION-KEY-F-LITERAL                              CL**3
00295              IF ((PV-NAVIGATION-MNEMONIC-2-3 NUMERIC)                CL**3
00296                      AND (PV-FK-NUMBER-2-3 > ZERO)                   CL**5
00297                      AND (PV-FK-NUMBER-2-3 NOT >                     CL**5
00298                                       PC-MAXIMUM-FUNCTION-KEYS)      CL**5
00299                      AND ((PV-NAVIGATION-MNEMONIC-4-8 = SPACES)      CL**3
00300                      OR (PV-NAVIGATION-MNEMONIC-4-8 =                CL**3
00301                                       LOW-VALUES)))                  CL**5
00302                  MOVE PV-FK-NUMBER-2-3 TO DP045-SOURCE-AID-KEY       CL**3
00303                  SET DP045-AID-KEY-CONVERTED TO TRUE                 CL**3
00304              ELSE                                                    CL**3
00305                  IF ((PV-NAVIGATION-MNEMONIC-2 NUMERIC)              CL**3
00306                          AND (PV-FK-NUMBER-2 > ZERO)                 CL**5
00307                          AND ((PV-NAVIGATION-MNEMONIC-3-8 =          CL**3
00308                                                     SPACES)          CL**3
00309                          OR (PV-NAVIGATION-MNEMONIC-3-8 =            CL**3
00310                                                     LOW-VALUES)))    CL**3
00311                      MOVE PV-FK-NUMBER-2 TO DP045-SOURCE-AID-KEY     CL**3
00312                      SET DP045-AID-KEY-CONVERTED TO TRUE             CL**3
00313                  ELSE                                                CL**8
00314                      IF PV-PF8-MNEMONIC                              CL**8
00315                          MOVE DP016-PF8   TO DP045-SOURCE-AID-KEY    CL**8
00316                          SET DP045-AID-KEY-CONVERTED TO TRUE         CL**8
00317                      END-IF                                          CL**8
00318                  END-IF                                              CL**3
00319              END-IF                                                  CL**3
00320          WHEN PV-FUNCTION-KEY-PF-LITERAL                             CL**3
00321              IF ((PV-NAVIGATION-MNEMONIC-3-4 NUMERIC)                CL**3
00322                      AND (PV-FK-NUMBER-3-4 > ZERO)                   CL**5
00323                      AND (PV-FK-NUMBER-3-4 NOT >                     CL**5
00324                                       PC-MAXIMUM-FUNCTION-KEYS)      CL**5
00325                      AND ((PV-NAVIGATION-MNEMONIC-5-8 = SPACES)      CL**5
00326                      OR (PV-NAVIGATION-MNEMONIC-5-8 =                CL**5
00327                                       LOW-VALUES)))                  CL**5
00328                  MOVE PV-FK-NUMBER-3-4 TO DP045-SOURCE-AID-KEY       CL**3
00329                  SET DP045-AID-KEY-CONVERTED TO TRUE                 CL**3
00330              ELSE                                                    CL**3
00331                  IF ((PV-NAVIGATION-MNEMONIC-3 NUMERIC)              CL**3
00332                          AND (PV-FK-NUMBER-3 > ZERO)                 CL**5
00333                          AND ((PV-NAVIGATION-MNEMONIC-4-8 =          CL**3
00334                                                     SPACES)          CL**3
00335                          OR (PV-NAVIGATION-MNEMONIC-4-8 =            CL**3
00336                                                     LOW-VALUES)))    CL**3
00337                      MOVE PV-FK-NUMBER-3 TO DP045-SOURCE-AID-KEY     CL**3
00338                      SET DP045-AID-KEY-CONVERTED TO TRUE             CL**3
00339                  END-IF                                              CL**3
00340              END-IF                                                  CL**3
00341          WHEN PV-CLEAR-MNEMONIC                                      CL**3
00342              MOVE DP016-CLEAR TO DP045-SOURCE-AID-KEY                CL**3
00343              SET DP045-AID-KEY-CONVERTED TO TRUE                     CL**3
00344          WHEN PV-PF8-MNEMONIC                                        CL**3
00345              MOVE DP016-PF8   TO DP045-SOURCE-AID-KEY                CL**3
00346              SET DP045-AID-KEY-CONVERTED TO TRUE                     CL**3
00347          WHEN PV-PF7-MNEMONIC                                        CL**3
00348              MOVE DP016-PF7   TO DP045-SOURCE-AID-KEY                CL**3
00349              SET DP045-AID-KEY-CONVERTED TO TRUE                     CL**3
00350          WHEN PV-PF6-MNEMONIC                                        CL**7
00351              MOVE DP016-PF6   TO DP045-SOURCE-AID-KEY                CL**7
00352              SET DP045-AID-KEY-CONVERTED TO TRUE                     CL**7
00353          WHEN PV-PF5-MNEMONIC                                        CL**7
00354              MOVE DP016-PF5   TO DP045-SOURCE-AID-KEY                CL**7
00355              SET DP045-AID-KEY-CONVERTED TO TRUE                     CL**7
00356          WHEN PV-PF3-MNEMONIC                                        CL**3
00357              MOVE DP016-PF3   TO DP045-SOURCE-AID-KEY                CL**3
00358              SET DP045-AID-KEY-CONVERTED TO TRUE                     CL**3
00359          WHEN PV-PA2-MNEMONIC                                        CL**3
00360              MOVE DP016-PA2   TO DP045-SOURCE-AID-KEY                CL**3
00361              SET DP045-AID-KEY-CONVERTED TO TRUE                     CL**3
00362      END-EVALUATE.                                                   CL**3
00363 *2000-EXIT.                                                          CL**3
