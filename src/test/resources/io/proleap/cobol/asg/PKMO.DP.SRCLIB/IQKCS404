000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    IQKCS404.                                                 
000500 AUTHOR.        GREG SHEFF.                                               
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  02-19-03.                                                 
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*---------------------------------------------------------------*         
001100*    I404 - DEPARTMENT LIST.                                    *         
001200*                                                               *         
001300*    THERE ARE TWO WAYS THIS SCREEN WILL DISPLAY THE            *         
001400*        DEPARTMENT LIST.  THIS DEPENDS ON WHETHER YOU USE      *         
001500*        F19 OR F20 ON IQSVEND (IQKCS401) TO BE VENDOR          *         
001600*        SPECIFIC OR NOT.                                       *         
001700*                                                               *         
001800*        1 - DISPLAY A LIST FOR A SPECIFIC VENDOR LABEL /       *         
001900*            VENDOR NAME AND BUSINESS GROUP THAT WAS SELECTED   *         
002000*            ON IQKCS402 (IQLVEND).                             *         
002100*                                                               *         
002200*        2 - DISPLAY A LIST FOR ALL VENDORS.                    *         
002300*                                                               *         
002400*    ALLOWS THE USER TO SELECT ONE DEPARTMENT AND PASS IT,      *         
002500*    ALONG WITH THE VENDOR NAME (IF SUPPLIED) AND BUSINESS      *         
002600*    GROUP TO ONE OF FOLLOWING SCREENS.                         *         
002700*                                                               *         
002800*        F19 - IQLCLASS (IQKCS405) - FORMAT CODE IS SPACE       *         
002900*        F20 - IQLCOLOR (IQKCS406) - FORMAT CODE IS SPACE       *         
003000*        F21 - IQLSTYLE (IQKCS409) - FORMAT CODE IS SPACE       *         
003100*                                                               *         
003200*---------------------------------------------------------------*         
003210* DATE:        09/14/2021                                                 
003220* WR/IR#:      CHG0261678                                                 
003230* PROGRAMMER:  GERMAN BANDA                                               
003240* DESCRIPTION: CHANGE CALL OF DPKUT100 TO                                 
003250*              CALL DP010I-NUMERIC-EDIT-ROUTINE                           
003260*              MAKING THE CALL DYNAMIC VS STATIC                          
003270*---------------------------------------------------------------*         
CICS  * CHANGED 10/31/2022    BY JIM HUNTER      WR: CHG0277565                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  ******************************************************************        
003300                                                                          
003400 ENVIRONMENT DIVISION.                                                    
003500                                                                          
003600 DATA DIVISION.                                                           
003700                                                                          
003800 WORKING-STORAGE SECTION.                                                 
003900 01  DK-DB2-KEYS.                                                         
004000     15  DK-GP-NBR                      PIC S9(03) COMP-3.                
004100     15  DK-LBL-NM                      PIC  X(30)  VALUE SPACE.          
004200                                                                          
004300 01  PC-PROGRAM-CONSTANTS.                                                
004400     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
004500                                                    'IQ404A  '.           
004600     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
004700                                                    'IQKM404 '.           
004800     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
004900                                                    'IQKCS404'.           
005000     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
005100     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
005200     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE ' '.            
005300     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +13             
005400                                                    COMP SYNC.            
005500     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
005600                                                    COMP SYNC.            
005700     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
005800                                                    +1000000              
005900                                                    COMP-3.               
006000     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
006100                                                    COMP SYNC.            
006200     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
006300                                                    COMP-3.               
006400*                                                                         
006500     05  PC-TSYMSG-NUMBERS.                                               
006600         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
006700         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
006800         10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
006900         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
007000         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
007100         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
007200         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
007300         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
007400         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
007500         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
007600         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
007700         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
007800         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
007900         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
008000*                                                                         
008100 01  PS-PROGRAM-SWITCHES.                                                 
008200     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
008300         88  PS-NO-ERROR                            VALUE 'Y'.            
008400         88  PS-ERROR                               VALUE 'N'.            
008500     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
008600         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
008700         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
008800     05  PS-DATA-CURSOR-SW              PIC  X(01)  VALUE ' '.            
008900         88  PS-CSR-VENDOR-ALL                      VALUE '1'.            
009000         88  PS-CSR-VENDOR-SPECIFIC                 VALUE '2'.            
009100*                                                                         
009200 01  PV-PROGRAM-VARIABLES.                                                
009300     05  PV-CURSOR-NAME                 PIC X(18)   VALUE SPACES.         
009400     05  PV-SUB                         PIC S9(04)  VALUE +0              
009500                                                    COMP SYNC.            
009600     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
009700                                                    COMP SYNC.            
009800     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
009900                                                    COMP-3.               
010000     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
010100                                                    COMP-3.               
010200     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
010300                                                    COMP-3.               
010400     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
010500                                                    COMP-3.               
010600     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
010700                                                    COMP-3.               
010800     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
010900                                                    COMP-3.               
011000     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
011100                                                    COMP SYNC.            
011200     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
011300                                                    COMP SYNC.            
011400     05  PV-ITEM                        PIC S9(04)  VALUE +0              
011500                                                    COMP SYNC.            
011600     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
011700                                                    COMP SYNC.            
011800     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
011900                                                    COMP SYNC.            
012000     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
012100                                                    COMP SYNC.            
012200     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
012300                                                    COMP SYNC.            
012400     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
012500                                                    COMP SYNC.            
012600     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
012700                                                    COMP SYNC.            
012800     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
012900                                                    COMP SYNC.            
013000     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
013100                                                    COMP SYNC.            
013200     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
013300                                                    COMP SYNC.            
013400     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
013500                                                    COMP SYNC.            
013600     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
013700                                                    COMP SYNC.            
013800     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
013900                                                    COMP SYNC.            
014000******************************************************************        
014100**  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
014200**  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
014300**  APPLICATION-SPECIFIC COMMAREA.                              **        
014400******************************************************************        
014500 01  LQW-LIST-QUEUE-WORK-AREA.                                            
014600     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
014700     05  LQW-ITEM-ENTRIES               OCCURS 13 TIMES                   
014800                                        INDEXED BY LQW-IDX.               
014900         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
015000         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
015100         10  LQW-SELECTED-SW            PIC  X(01).                       
015200             88  LQW-SELECT                         VALUE 'S'.            
015300             88  LQW-DESELECT                       VALUE ' '.            
015400         10  LQW-DEPT-NBR               PIC  9(04).                       
015500         10  LQW-DEPT-DESC              PIC  X(20).                       
015600                                                                          
015700     EJECT                                                                
015800*---------------------------------------------------------------*         
015900*  RECORD LAYOUT FOR THE SELECTION TEMP STORAGE QUEUE.          *         
016000*---------------------------------------------------------------*         
016100     COPY IQWS014.                                                        
016200                                                                          
016300     EJECT                                                                
016400*---------------------------------------------------------------*         
016500*    MAP LAYOUT                                                 *         
016600*---------------------------------------------------------------*         
016700                                                                          
016800     COPY IQKM404.                                                        
016900*                                                                         
017000 01  MR-OCCURS-LAYOUT                   REDEFINES  IQ404AO.               
017100     05  FILLER                         PIC X(115).                       
017200     05  MR-SCROLL-AREA                 OCCURS 13 TIMES.                  
017300         10  MR-SELECTL                 PIC S9(04) COMP.                  
017400         10  MR-SELECTA                 PIC  X(01).                       
017500         10  MR-SELECTC                 PIC  X(01).                       
017600         10  MR-SELECTH                 PIC  X(01).                       
017700         10  MR-SELECTI                 PIC  X(01).                       
017800                                                                          
017900         10  MR-DEPT-NBRL               PIC S9(04) COMP.                  
018000         10  MR-DEPT-NBRA               PIC  X(01).                       
018100         10  MR-DEPT-NBRC               PIC  X(01).                       
018200         10  MR-DEPT-NBRH               PIC  X(01).                       
018300         10  MR-DEPT-NBR.                                                 
018400             15  MR-DEPT-NBR9           PIC  9(04).                       
018500                                                                          
018600         10  MR-DEPT-DESCL              PIC S9(04) COMP.                  
018700         10  MR-DEPT-DESCA              PIC  X(01).                       
018800         10  MR-DEPT-DESCC              PIC  X(01).                       
018900         10  MR-DEPT-DESCH              PIC  X(01).                       
019000         10  MR-DEPT-DESC               PIC  X(20).                       
019100     EJECT                                                                
019200*---------------------------------------------------------------*         
019300*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
019400*---------------------------------------------------------------*         
019500                                                                          
019600     COPY DPWS015.                                                        
019700                                                                          
019800*---------------------------------------------------------------*         
019900*    FUNCTION KEYS COPYBOOK.                                    *         
020000*---------------------------------------------------------------*         
020100                                                                          
020200     COPY DPWS016.                                                        
020300                                                                          
020400*---------------------------------------------------------------*         
020500*    NUMERIC EDIT LAYOUT.                                       *         
020600*---------------------------------------------------------------*         
020700                                                                          
020800     COPY DPWS010I.                                                       
020900 EJECT                                                                    
021000*---------------------------------------------------------------*         
021100*    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
021200*---------------------------------------------------------------*         
021300                                                                          
021400     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
021500 EJECT                                                                    
021600*---------------------------------------------------------------*         
021700*    STANDARD COMMAREA.                                         *         
021800*---------------------------------------------------------------*         
021900                                                                          
022000     COPY DPWS020.                                                        
022100     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
022200*---------------------------------------------------------------*         
022300* INTER-APPLICATION COMMAREA.                                   *         
022400*   IQWS016 - IS THE INPUT LAYOUT WHEN COMING FROM THE PREVIOUS *         
022500*             PROGARM.                                          *         
022600*   IQWS016 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     *         
022700*             THIS LIST PROGRAM.                                *         
022800*---------------------------------------------------------------*         
022900                                                                          
023000         10  INTER-APPL-COMM-AREA       PIC X(200).                       
023100     COPY IQWS016.                                                        
023200*---------------------------------------------------------------*         
023300*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
023400*        CURRENT LENGTH IS 1000 FOR IQLDEPT MNEMONIC.                     
023500*        YOU MUST CHECK THE COMMAREA LENGTH FOR THE MNEMONIC              
023600*            IF YOU ADD ANY MORE FIELDS.                                  
023700*---------------------------------------------------------------*         
023800         10  ASC-SPECIFIC-COMMAREA.                                       
023900             15  ASC-LIST-KEYS.                                           
024000                 20  ASC-LBL-NM                      PIC  X(30).          
024100                 20  ASC-GP-NBR                      PIC  9(03).          
024200                 20  ASC-GP-DESC                     PIC  X(25).          
024300             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
024400                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
024500                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
024600                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
024700                                                                          
024800                 20  ASC-ITEM-ARRAY.                                      
024900                     25  ASC-ITEM-ENTRIES OCCURS 13 TIMES.                
025000                         30  ASC-LIST-ITEM-NUMBER                         
025100                                                     PIC S9(09)           
025200                                                     COMP-3.              
025300                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
025400                             88  ASC-VALID-SELECT    VALUE ' '            
025500                                                           'S'            
025600                                                           'B'            
025700                                                           'E'.           
025800                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
025900                             88  ASC-TEMP-SELECT     VALUE 'S'.           
026000                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
026100                             88  ASC-TEMP-END        VALUE 'E'.           
026200                         30  ASC-SELECTED-SW         PIC  X(01).          
026300                             88  ASC-NO-SELECT       VALUE ' '.           
026400                             88  ASC-SELECT          VALUE 'S'.           
026500                             88  ASC-BEGIN           VALUE 'B'.           
026600                             88  ASC-END             VALUE 'E'.           
026700                         30  ASC-DEPT-NBR            PIC  9(04).          
026800                         30  ASC-DEPT-DESC           PIC  X(20).          
026900                                                                          
027000             15  ASC-BLK-SUB                         PIC S9(04)           
027100                                                          COMP            
027200                                                          SYNC.           
027300             15  ASC-ITEM-SUB                        PIC S9(04)           
027400                                                          COMP            
027500                                                          SYNC.           
027600             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
027700                                                          COMP-3.         
027800             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
027900                                                          COMP-3.         
028000             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
028100                                                          COMP-3.         
028200             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
028300                                                          COMP-3.         
028400             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
028500                                                          COMP-3.         
028600             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
028700                                                          COMP            
028800                                                          SYNC.           
028900             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
029000                                                          COMP-3.         
029100             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
029200                                                          COMP-3.         
029300             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
029400                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
029500                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
029600             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
029700                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
029800                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
029900             15  ASC-LIST-QUEUE-NAME.                                     
030000                 20  ASC-LIST-TERM-ID                PIC  X(04).          
030100                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
030200                                                          COMP-3.         
030300                 20  ASC-LIST-SEQ                    PIC  9(01).          
030400             15  ASC-SEL-QUEUE-NAME.                                      
030500                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
030600                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
030700                                                          COMP-3.         
030800                 20  ASC-SEL-SEQ                     PIC  9(01).          
030900             15  ASC-START-ITEM-REQUEST.                                  
031000                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
031100             15  ASC-FILLER                          PIC  X(72).          
031200         10  FILLER                                  PIC  X(1865).        
031300     EJECT                                                                
031400*---------------------------------------------------------------*         
031500*    ABEND PROCESSING COPYBOOK.                                 *         
031600*---------------------------------------------------------------*         
031700                                                                          
031800     COPY DPWS013.                                                        
031900                                                                          
032000 EJECT                                                                    
032100*---------------------------------------------------------------*         
032200*    DB2 AREA FOR XST_VND_STYL                                  *         
032300*---------------------------------------------------------------*         
032400     EXEC SQL                                                             
032500          INCLUDE XST00013                                                
032600     END-EXEC.                                                            
032700*---------------------------------------------------------------*         
032800*    DB2 AREA FOR XST_DEPT                                      *         
032900*---------------------------------------------------------------*         
033000     EXEC SQL                                                             
033100          INCLUDE XST00015                                                
033200     END-EXEC.                                                            
033300*---------------------------------------------------------------*         
033400*    DB2 AREA FOR XST_BUS_GP                                    *         
033500*---------------------------------------------------------------*         
033600     EXEC SQL                                                             
033700          INCLUDE XST00016                                                
033800     END-EXEC.                                                            
033900*---------------------------------------------------------------*         
034000*    DB2 AREA FOR XST_VND                                       *         
034100*---------------------------------------------------------------*         
034200     EXEC SQL                                                             
034300          INCLUDE XST00018                                                
034400     END-EXEC.                                                            
034500*---------------------------------------------------------------*         
034600*    DB2 AREA FOR COMMUNICATIONS                                *         
034700*---------------------------------------------------------------*         
034800     EXEC SQL                                                             
034900          INCLUDE SQLCA                                                   
035000     END-EXEC.                                                            
035100*---------------------------------------------------------------*         
035200*  FETCH THE DEPARTMENT NAME AND NUMBER.                                  
035300*---------------------------------------------------------------*         
035400     EXEC SQL                                                             
035500          DECLARE DEPT-CSR CURSOR FOR                                     
035600            SELECT                                                        
035700                 B.DEPT_NBR,                                              
035800                 B.DEPT_NM                                                
035900              FROM  XST_BUS_GP    A,                                      
036000                    XST_DEPT      B,                                      
036100                    XST_VND_STYL  D,                                      
036200                    XST_VND       E                                       
036300                WHERE  E.ENT_NM_DESC  = :DK-LBL-NM                        
036400                  AND  A.BUS_GP_NBR   = :DK-GP-NBR                        
036500                  AND  D.ENT_ID       = E.ENT_ID                          
036600                  AND  B.DEPT_NBR     = D.DEPT_NBR                        
036700                  AND  A.BUS_GP_NBR   = B.BUS_GP_NBR                      
036800            UNION                                                         
036900            SELECT                                                        
037000                 B.DEPT_NBR,                                              
037100                 B.DEPT_NM                                                
037200              FROM  XST_BUS_GP    A,                                      
037300                    XST_DEPT      B,                                      
037400                    XST_VND_STYL  D                                       
037500                WHERE  D.LBL_NM     = :DK-LBL-NM                          
037600                  AND  A.BUS_GP_NBR = :DK-GP-NBR                          
037700                  AND  D.DEPT_NBR   = B.DEPT_NBR                          
037800                  AND  B.BUS_GP_NBR = A.BUS_GP_NBR                        
037900                    ORDER BY 1                                            
038000     END-EXEC.                                                            
038100                                                                          
038200*---------------------------------------------------------------*         
038300*  FETCH THE DEPARTMENT NAME AND NUMBER.                                  
038400*---------------------------------------------------------------*         
038500     EXEC SQL                                                             
038600          DECLARE DEPT-NO-VENDOR CURSOR FOR                               
038700            SELECT                                                        
038800                 B.DEPT_NBR,                                              
038900                 B.DEPT_NM                                                
039000              FROM  XST_BUS_GP    A                                       
039100                   ,XST_DEPT      B                                       
039200                WHERE  A.BUS_GP_NBR   = :DK-GP-NBR                        
039300                  AND  A.BUS_GP_NBR   = B.BUS_GP_NBR                      
039400     END-EXEC.                                                            
039500                                                                          
039600 LINKAGE SECTION.                                                         
039700                                                                          
039800 01  DFHCOMMAREA.                                                         
039900     05  FILLER                         OCCURS  1 TO 4072 TIMES           
040000                                        DEPENDING ON EIBCALEN.            
040100         10  FILLER                     PIC X.                            
040200                                                                          
040300 EJECT                                                                    
040400 PROCEDURE DIVISION.                                                      
040500******************************************************************        
040600**  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
040700**  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
040800**  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
040900******************************************************************        
041000 0000-MAIN-MODULE.                                                        
041100     INITIALIZE DP030-CICS-API-FIELDS.                                    
041200     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
041300     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
041400     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
041500     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
041600     MOVE LENGTH OF IQ404AI        TO DP030-MAP-LENGTH (1).               
041700     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
041800     PERFORM 0001-CALL-CICS-ARCH-API.                                     
041900*                                                                         
042000     PERFORM 1000-CONTROL-PROCESSING                                      
042100*                                                                         
042200     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
042300     PERFORM 0001-CALL-CICS-ARCH-API.                                     
042400                                                                          
042500******************************************************************        
042600**  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
042700**  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
042800******************************************************************        
042900 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
043100                                    DFHCOMMAREA                           
043200                                    DP030-CICS-API-FIELDS                 
043300                                    DP020-STANDARD-COMMAREA               
043400                                    IQ404AI.                              
043500*                                                                         
043600     IF  DP030-RC-CALL-SUCCESSFUL                                         
043700         CONTINUE                                                         
043800     ELSE                                                                 
043900         SET DP013-NO-ROLLBACK                                            
044000             DP013-XCTL-DISPLAY-RESTART                                   
044100             DP013-CICS-ABEND      TO TRUE                                
044200         MOVE '0001-CALL-CICS-ARCH-API'                                   
044300                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
044600         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
044700         PERFORM DP013-0000-PROCESS-ABEND                                 
044800     END-IF.                                                              
044900*----------------------------------------------------------------*        
045000* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
045100* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
045200*                                                                *        
045300* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
045400*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
045500*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
045600*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
045700*----------------------------------------------------------------*        
045800                                                                          
045900 1000-CONTROL-PROCESSING.                                                 
046000                                                                          
046100     EVALUATE TRUE                                                        
046200         WHEN DP020-NEXT-ACT-INITIAL                                      
046300             INITIALIZE ASC-SPECIFIC-COMMAREA                             
046400             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
046500                                      ASC-SEL-TERM-ID                     
046600             MOVE DP020-SRC-TASK-NUMBER                                   
046700                                   TO ASC-LIST-TASK-NUMBER                
046800                                      ASC-SEL-TASK-NUMBER                 
046900             MOVE PC-LIST-QUEUE-SEQ                                       
047000                                   TO ASC-LIST-SEQ                        
047100             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
047200             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
047300***-----$TEMPLATE NOTE$-------------------------------------------        
047400*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
047500*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
047600*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
047700***---------------------------------------------------------------        
047800             IF  PS-COMMAREA-NOT-VALID                                    
047900                 SET DP020-NEXT-ACT-APPL-ERROR                            
048000                                   TO TRUE                                
048100             ELSE                                                         
048200                 PERFORM 4000-BUILD-INITIAL-PANEL                         
048300             END-IF                                                       
048400                                                                          
048500         WHEN DP020-NEXT-ACT-READ-MAP                                     
048600             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
048700                                   TO PV-TOP-ITEM                         
048800             PERFORM 2000-PROCESS-PANEL                                   
048900                                                                          
049000         WHEN DP020-NEXT-ACT-RETURN                                       
049100             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
049200                                   TO PV-TOP-ITEM                         
049300             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
049400             PERFORM 6500-REINIT-ASC-FLDS                                 
049500             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
049700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
049800                                                                          
049900         WHEN OTHER                                                       
050000             SET DP013-LOGIC-ABEND TO TRUE                                
050100             SET DP013-NO-ROLLBACK TO TRUE                                
050200             MOVE '1000-CONTROL-PROCESSING'                               
050300                                   TO DP013-PARAGRAPH                     
050400             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
050500                                   TO DP013-MESSAGE-TEXT(1)               
050600             PERFORM DP013-0000-PROCESS-ABEND                             
050700     END-EVALUATE.                                                        
050800                                                                          
050900 EJECT                                                                    
051000*----------------------------------------------------------------*        
051100* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
051200* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
051300*----------------------------------------------------------------*        
051400                                                                          
051500 1100-PROCESS-INTER-APPL-COMM.                                            
051600     SET PS-COMMAREA-VALID         TO TRUE.                               
051700     MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
051800                                                                          
051900     IF  DP020-INT-APPL-FORMAT-IND = PC-CRIT-SPEC-FORMAT                  
052000         MOVE IQ016-GP-NBR       TO ASC-GP-NBR                            
052100                                     DK-GP-NBR                            
052200         MOVE IQ016-GP-DESC      TO ASC-GP-DESC                           
052300         MOVE IQ016-LBL-NM       TO ASC-LBL-NM                            
052400                                     DK-LBL-NM                            
052500     ELSE                                                                 
052600         IF  DP020-NAV-KEY-DATA > SPACE                                   
052700             MOVE DP020-NAV-KEY-DATA                                      
052800                                   TO ASC-GP-NBR                          
052900                                      DK-GP-NBR                           
053000                                      ASC-LBL-NM                          
053100                                      DK-LBL-NM                           
053200         END-IF                                                           
053300     END-IF.                                                              
053400                                                                          
053500 EJECT                                                                    
053600*----------------------------------------------------------------*        
053700* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
053800* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
053900* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
054000* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
054100* FUNCTION KEYS CAN GET THIS FAR.                                *        
054200*----------------------------------------------------------------*        
054300                                                                          
054400 2000-PROCESS-PANEL.                                                      
054500     EVALUATE TRUE                                                        
054600         WHEN DP020-SRC-AID = DP016-CLEAR                                 
054700             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
054800             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
054900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
055000                                                                          
055100         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
055200             MOVE ASC-GP-NBR TO DK-GP-NBR                                 
055300             MOVE ASC-LBL-NM TO DK-LBL-NM                                 
055400             PERFORM 2050-DELETE-LIST-QUEUE                               
055500             PERFORM 4000-BUILD-INITIAL-PANEL                             
055600                                                                          
055700         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
055800             PERFORM 2140-DESELECT-ALL-ITEMS                              
055900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
056000                                                                          
056100         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
056200             PERFORM 2150-SELECT-ALL-ITEMS                                
056300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
056400                                                                          
056500         WHEN OTHER                                                       
056600             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
056700             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
056800             IF  PS-NO-ERROR                                              
056900                 SET DP030-CONTINUE-GO                                    
057000                                 TO TRUE                                  
057100                 PERFORM 2100-CHECK-FUNCTION-KEY                          
057200             ELSE                                                         
057300                 SET DP030-OVERRIDE-GO                                    
057400                                 TO TRUE                                  
057500             END-IF                                                       
057600     END-EVALUATE.                                                        
057700 EJECT                                                                    
057800*----------------------------------------------------------------*        
057900*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
058000*----------------------------------------------------------------*        
058100 2050-DELETE-LIST-QUEUE.                                                  
058200                                                                          
058300     EXEC CICS                                                            
058400         DELETEQ TS                                                       
058500             QUEUE (ASC-LIST-QUEUE-NAME)                                  
058600             RESP  (PV-CICS-RESP)                                         
058700     END-EXEC.                                                            
058800                                                                          
058900     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
059000        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
059100         CONTINUE                                                         
059200     ELSE                                                                 
059300         SET DP013-CICS-ABEND      TO TRUE                                
059400         SET DP013-NO-ROLLBACK     TO TRUE                                
059500                                                                          
059600         MOVE '2050-DELETE-LIST-QUEUE'                                    
059700                                   TO DP013-PARAGRAPH                     
059800         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
059900                                   TO DP013-MESSAGE-TEXT (1)              
060000         PERFORM DP013-0000-PROCESS-ABEND                                 
060100     END-IF.                                                              
060200                                                                          
060300     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
060400 EJECT                                                                    
060500*----------------------------------------------------------------*        
060600* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
060700* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
060800* CICS ARCHITECTURE API.                                         *        
060900*----------------------------------------------------------------*        
061000 2100-CHECK-FUNCTION-KEY.                                                 
061100     EVALUATE TRUE                                                        
061200*        LOCAL-01   CLASSLST     IQLCLASS                                 
061300*        LOCAL-02   COLORLST     IQLCOLOR                                 
061400         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
061500         WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
061600         WHEN DP020-FK-LOCAL-FUNC-03(DP020-SRC-AID)                       
061700             IF  ASC-NUMBER-OF-SELECTED-ITEMS = ZERO                      
061800*                --  A LIST ITEM MUST BE SELECTED --                      
061900                 MOVE PC-TSYMSG-00057                                     
062000                                   TO DP020-MSG-NUMBER                    
062100                 SET DP020-MSG-WARNING                                    
062200                     DP030-OVERRIDE-GO                                    
062300                                   TO TRUE                                
062400             ELSE                                                         
062500               IF  ASC-NUMBER-OF-SELECTED-ITEMS > 1                       
062600*                --  ONLY ONE LIST ITEM MAY BE SELECTED --                
062700                   MOVE PC-TSYMSG-00047                                   
062800                                   TO DP020-MSG-NUMBER                    
062900                 SET DP020-MSG-WARNING                                    
063000                     DP030-OVERRIDE-GO                                    
063100                                   TO TRUE                                
063200               ELSE                                                       
063300                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
063400                                TO IQ016-NUMBER-OF-SELECTED-ITEMS         
063500                 PERFORM 5000-PREPARE-SEL-QUEUE                           
063600               END-IF                                                     
063700             END-IF                                                       
063800                                                                          
063900         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
064000             MOVE +1               TO PV-TOP-ITEM                         
064100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
064200                                                                          
064300         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
064400             PERFORM 2110-BROWSE-BACKWARD                                 
064500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
064600                                                                          
064700         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
064800             PERFORM 2120-BROWSE-FORWARD                                  
064900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
065000                                                                          
065100         WHEN DP020-SRC-AID = DP016-ENTER                                 
065200             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
065300             IF  PS-ERROR                                                 
065400                 CONTINUE                                                 
065500             ELSE                                                         
065600                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
065700             END-IF                                                       
065800                                                                          
065900         WHEN OTHER                                                       
066000             SET DP013-NO-ROLLBACK                                        
066100                 DP013-XCTL-DISPLAY-RESTART                               
066200                 DP013-CICS-ABEND  TO TRUE                                
066300             MOVE '2100-CHECK-FUNCTION-KEY'                               
066400                                   TO DP013-PARAGRAPH                     
066500             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
066600                                   TO DP013-MESSAGE-TEXT (1)              
066700             PERFORM DP013-0000-PROCESS-ABEND                             
066800     END-EVALUATE.                                                        
066900*                                                                         
067000*----------------------------------------------------------------*        
067100*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
067200*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
067300*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
067400*----------------------------------------------------------------*        
067500                                                                          
067600 2110-BROWSE-BACKWARD.                                                    
067700     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
067800         GIVING PV-TOP-ITEM.                                              
067900*                                                                         
068000     IF  ((PV-TOP-ITEM < +1)                                              
068100       OR (PV-TOP-ITEM = +1))                                             
068200         MOVE +1                   TO PV-TOP-ITEM                         
068300*        -- TOP OF LIST --                                                
068400         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
068500         SET DP020-MSG-INFORMATIONAL TO TRUE                              
068600     END-IF.                                                              
068700                                                                          
068800 EJECT                                                                    
068900*----------------------------------------------------------------*        
069000*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
069100*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
069200*    PAST THE END, ISSUE THE BOTTOM OF LIST                      *        
069300*----------------------------------------------------------------*        
069400                                                                          
069500 2120-BROWSE-FORWARD.                                                     
069600                                                                          
069700     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
069800         GIVING PV-TOP-ITEM.                                              
069900     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
070000         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
070100                                   TO PV-TOP-ITEM                         
070200*        -- BOTTOM OF LIST --                                             
070300         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
070400         SET DP020-MSG-INFORMATIONAL                                      
070500                                   TO TRUE                                
070600     ELSE                                                                 
070700         COMPUTE PV-BOTTOM-ITEM =                                         
070800             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
070900         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
071000*            -- BOTTOM OF LIST --                                         
071100             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
071200             SET DP020-MSG-INFORMATIONAL                                  
071300                                   TO TRUE                                
071400         END-IF                                                           
071500     END-IF.                                                              
071600 EJECT                                                                    
071700*----------------------------------------------------------------*        
071800*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
071900*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
072000*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
072100*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
072200*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
072300*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
072400*----------------------------------------------------------------*        
072500                                                                          
072600 2130-CHECK-FOR-JUMP-BROWSE.                                              
072700     SET PS-NO-ERROR               TO TRUE.                               
072800     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
072900         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
073000                                   TO PV-TOP-ITEM                         
073100     ELSE                                                                 
073200         MOVE ASC-START-ITEM-REQUEST                                      
073300                                   TO DP010I-UNEDITED-FIELD               
073400         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
073500         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
073600         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
073700                                   TO TRUE                                
073810         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
073820                         USING DP010I-NUMERIC-EDIT-AREA                   
073900                                                                          
074000         IF  NOT DP010I-ERROR-DETECTED                                    
074100             MOVE DP010I-NUMERIC-FIELD                                    
074200                                   TO  ASC-START-ITEM-REQUEST-N           
074300             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
074400                 IF  ASC-START-ITEM-REQUEST-N >                           
074500                                          ASC-NUMBER-OF-ITEMS-READ        
074600                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
074700                     COMPUTE PV-TOP-ITEM =                                
074800                             ASC-NUMBER-OF-ITEMS-READ                     
074900                             - PC-ITEMS-PER-PANEL + 1                     
075000                     IF  PV-TOP-ITEM < +1                                 
075100                         MOVE +1   TO PV-TOP-ITEM                         
075200                         MOVE PC-TSYMSG-00070                             
075300                                   TO DP020-MSG-NUMBER                    
075400                         SET DP020-MSG-INFORMATIONAL                      
075500                                   TO TRUE                                
075600                     END-IF                                               
075700                 ELSE                                                     
075800                     MOVE ASC-START-ITEM-REQUEST-N                        
075900                                   TO PV-TOP-ITEM                         
076000                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
076100                     COMPUTE PV-BOTTOM-ITEM =                             
076200                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
076300                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
076400*                        -- BOTTOM OF LIST --                             
076500                         MOVE PC-TSYMSG-00070                             
076600                                   TO DP020-MSG-NUMBER                    
076700                         SET DP020-MSG-INFORMATIONAL                      
076800                                   TO TRUE                                
076900                     END-IF                                               
077000                 END-IF                                                   
077100             ELSE                                                         
077200*                --- VALUE OUT OF RANGE ----                              
077300                 MOVE PC-TSYMSG-00101                                     
077400                                   TO DP020-MSG-NUMBER                    
077500                 MOVE -1           TO ASTRTITL                            
077600                 SET DP030-SET-CURSOR-APPL-1                              
077700                                   TO TRUE                                
077800                 MOVE DP015-REVERSE                                       
077900                                   TO ASTRTITH                            
078000                 MOVE DP015-RED    TO ASTRTITC                            
078100                 MOVE DP015-UNP-NUM-BRT-MDT                               
078200                                   TO ASTRTITA                            
078300                 SET DP020-MSG-FATAL                                      
078400                                   TO TRUE                                
078500                 SET PS-ERROR      TO TRUE                                
078600             END-IF                                                       
078700                                                                          
078800         ELSE                                                             
078900             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
079000             MOVE -1               TO ASTRTITL                            
079100             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
079200             MOVE DP015-REVERSE    TO ASTRTITH                            
079300             MOVE DP015-RED        TO ASTRTITC                            
079400             MOVE DP015-UNP-NUM-BRT-MDT                                   
079500                                   TO ASTRTITA                            
079600             SET DP020-MSG-FATAL   TO TRUE                                
079700             SET PS-ERROR          TO TRUE                                
079800         END-IF                                                           
079900     END-IF.                                                              
080000 EJECT                                                                    
080100*----------------------------------------------------------------*        
080200*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
080300*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
080400*----------------------------------------------------------------*        
080500 2140-DESELECT-ALL-ITEMS.                                                 
080600     MOVE +1                       TO ASC-BLK-SUB.                        
080700                                                                          
080800     PERFORM 2141-CLEAR-SELECTED-INDS                                     
080900         VARYING ASC-ITEM-SUB                                             
081000         FROM 1 BY 1                                                      
081100         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
081200                                                                          
081300     ADD +1                        TO ASC-BLK-SUB.                        
081400                                                                          
081500     PERFORM 2141-CLEAR-SELECTED-INDS                                     
081600         VARYING ASC-ITEM-SUB                                             
081700         FROM 1 BY 1                                                      
081800         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
081900                                                                          
082000     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
082100                                   TO PV-LIST-ITEM-NUMBER.                
082200                                                                          
082300     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
082400         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
082500                                                                          
082600     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
082700        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
082800         MOVE +1                   TO ASC-BLK-SUB                         
082900                                                                          
083000         PERFORM 5100-WRITE-TEMP-STORAGE                                  
083100                                                                          
083200         MOVE +1                   TO PV-BLOCK-NUMBER                     
083300                                                                          
083400         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
083500             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
083600                                                                          
083700         PERFORM                                                          
083800             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
083900                                                                          
084000             PERFORM 4410-READ-TEMP-STORAGE                               
084100                                                                          
084200             PERFORM 2141-CLEAR-SELECTED-INDS                             
084300                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
084400                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
084500                                                                          
084600             PERFORM 5100-WRITE-TEMP-STORAGE                              
084700                                                                          
084800             ADD +1                TO PV-BLOCK-NUMBER                     
084900                                                                          
085000         END-PERFORM                                                      
085100                                                                          
085200         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
085300             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
085400                                                                          
085500         PERFORM                                                          
085600             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
085700                   ASC-HIGHEST-BLOCK-WRITTEN                              
085800                                                                          
085900             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
086000                                   TO PV-BLOCK-NUMBER                     
086100                                                                          
086200             PERFORM 4410-READ-TEMP-STORAGE                               
086300                                                                          
086400             PERFORM 2141-CLEAR-SELECTED-INDS                             
086500                 VARYING ASC-ITEM-SUB                                     
086600                 FROM 1 BY 1                                              
086700                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
086800                                                                          
086900             PERFORM 5100-WRITE-TEMP-STORAGE                              
087000                                                                          
087100             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
087200                                                                          
087300         END-PERFORM                                                      
087400                                                                          
087500         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
087600                                   TO PV-BLOCK-NUMBER                     
087700                                                                          
087800         PERFORM 4410-READ-TEMP-STORAGE                                   
087900     END-IF.                                                              
088000                                                                          
088100                                                                          
088200     MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
088300     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.        
088400     MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.          
088500                                                                          
088600     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
088700                                                                          
088800 EJECT                                                                    
088900*----------------------------------------------------------------*        
089000*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
089100*----------------------------------------------------------------*        
089200                                                                          
089300 2141-CLEAR-SELECTED-INDS.                                                
089400                                                                          
089500     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
089600             NOT EQUAL ZERO                                               
089700         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
089800             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
089900                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
090000                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
090100                                   TO TRUE                                
090200         END-IF                                                           
090300     END-IF.                                                              
090400                                                                          
090500 EJECT                                                                    
090600*----------------------------------------------------------------*        
090700*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
090800*  FOR ALL ITEMS.                                                *        
090900*----------------------------------------------------------------*        
091000                                                                          
091100 2150-SELECT-ALL-ITEMS.                                                   
091200                                                                          
091300     MOVE +1                       TO ASC-BLK-SUB.                        
091400                                                                          
091500     PERFORM 2151-SET-SELECTED-INDS                                       
091600         VARYING ASC-ITEM-SUB                                             
091700         FROM 1 BY 1                                                      
091800         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
091900                                                                          
092000     ADD +1                        TO ASC-BLK-SUB.                        
092100                                                                          
092200     PERFORM 2151-SET-SELECTED-INDS                                       
092300         VARYING ASC-ITEM-SUB                                             
092400         FROM 1 BY 1                                                      
092500         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
092600                                                                          
092700     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
092800                                   TO PV-LIST-ITEM-NUMBER.                
092900                                                                          
093000     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
093100         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
093200                                                                          
093300     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
093400        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
093500         MOVE +1                   TO ASC-BLK-SUB                         
093600                                                                          
093700         PERFORM 5100-WRITE-TEMP-STORAGE                                  
093800                                                                          
093900         MOVE +1                   TO PV-BLOCK-NUMBER                     
094000                                                                          
094100         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
094200             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
094300                                                                          
094400         PERFORM                                                          
094500             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
094600                                                                          
094700             PERFORM 4410-READ-TEMP-STORAGE                               
094800                                                                          
094900             PERFORM 2151-SET-SELECTED-INDS                               
095000                 VARYING ASC-ITEM-SUB                                     
095100                 FROM 1 BY 1                                              
095200                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
095300                                                                          
095400             PERFORM 5100-WRITE-TEMP-STORAGE                              
095500                                                                          
095600             ADD +1                TO PV-BLOCK-NUMBER                     
095700                                                                          
095800         END-PERFORM                                                      
095900                                                                          
096000         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
096100             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
096200                                                                          
096300         PERFORM                                                          
096400             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
096500                   ASC-HIGHEST-BLOCK-WRITTEN                              
096600                                                                          
096700             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
096800                                   TO PV-BLOCK-NUMBER                     
096900                                                                          
097000             PERFORM 4410-READ-TEMP-STORAGE                               
097100                                                                          
097200             PERFORM 2151-SET-SELECTED-INDS                               
097300                 VARYING ASC-ITEM-SUB                                     
097400                 FROM 1 BY 1                                              
097500                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
097600                                                                          
097700             PERFORM 5100-WRITE-TEMP-STORAGE                              
097800                                                                          
097900             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
098000                                                                          
098100         END-PERFORM                                                      
098200                                                                          
098300         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
098400                                   TO PV-BLOCK-NUMBER                     
098500                                                                          
098600         PERFORM 4410-READ-TEMP-STORAGE                                   
098700     END-IF.                                                              
098800                                                                          
098900                                                                          
099000     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
099100                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
099200                                                                          
099300     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
099400                                                                          
099500 EJECT                                                                    
099600*----------------------------------------------------------------*        
099700*    MARK EACH ITEM AS BEING SELECTED.                           *        
099800*----------------------------------------------------------------*        
099900                                                                          
100000 2151-SET-SELECTED-INDS.                                                  
100100                                                                          
100200     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
100300             NOT EQUAL ZERO                                               
100400         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
100500             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
100600                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
100700                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
100800                               TO  TRUE                                   
100900         END-IF                                                           
101000     END-IF.                                                              
101100******************************************************************        
101200** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
101300** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
101400******************************************************************        
101500 2200-MOVE-SCREEN-TO-COMMAREA.                                            
101600                                                                          
101700     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
101800     MOVE +1                       TO PV-SUB.                             
101900*                                                                         
102000     PERFORM 3100-SET-SUBSCRIPTS.                                         
102100     IF  ASTRTITL > ZERO                                                  
102200         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
102300     ELSE                                                                 
102400         IF  ASTRTITF = DP015-ERASE-EOF                                   
102500             INITIALIZE ASC-START-ITEM-REQUEST                            
102600         END-IF                                                           
102700     END-IF.                                                              
102800     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
102900         VARYING PV-SUB                                                   
103000         FROM 1 BY 1                                                      
103100         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
103200******************************************************************        
103300** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
103400******************************************************************        
103500 2210-MOVE-LINES-TO-COMMAREA.                                             
103600     IF  MR-SELECTL (PV-SUB) > ZERO                                       
103700         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
103800         MOVE MR-SELECTI (PV-SUB)                                         
103900                                   TO ASC-TEMP-SELECTED-SW                
104000                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
104100     ELSE                                                                 
104200         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
104300                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
104400                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
104500                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
104600         END-IF                                                           
104700     END-IF.                                                              
104800     ADD +1                        TO ASC-ITEM-SUB.                       
104900     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
105000         ADD +1                    TO ASC-BLK-SUB                         
105100         MOVE +1                   TO ASC-ITEM-SUB                        
105200     END-IF.                                                              
105300******************************************************************        
105400** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
105500** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
105600******************************************************************        
105700 3000-EDIT-DATA-IN-COMMAREA.                                              
105800     SET PS-NO-ERROR               TO TRUE.                               
105900     PERFORM 3200-RESET-ATTRIBUTES.                                       
106000     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
106100**                                                                        
106200* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
106300* EDITTING IS DONE FROM THE BOTTOM UP.                                    
106400**                                                                        
106500     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
106600         ASC-ITEMS-DISPLAYED - 1.                                         
106700     PERFORM 3100-SET-SUBSCRIPTS.                                         
106800     PERFORM 3300-EDIT-SELECTIONS                                         
106900         VARYING PV-SUB                                                   
107000         FROM PV-SUB BY -1                                                
107100         UNTIL PV-SUB < +1                                                
107200*                                                                         
107300     IF  PS-NO-ERROR                                                      
107400         PERFORM 3400-CHECK-RANGE-SEQUENCE                                
107500     END-IF.                                                              
107600*                                                                         
107700     IF  ((PS-NO-ERROR)                                                   
107800       AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
107900       AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
108000             PERFORM 3500-MARK-RANGE-AS-SELECTED                          
108100     END-IF.                                                              
108200 EJECT                                                                    
108300*----------------------------------------------------------------*        
108400*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
108500*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
108600*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
108700*                                                                *        
108800*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
108900*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
109000*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
109100*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
109200*----------------------------------------------------------------*        
109300                                                                          
109400 3100-SET-SUBSCRIPTS.                                                     
109500                                                                          
109600     COMPUTE ASC-ITEM-SUB =                                               
109700         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
109800                                                                          
109900     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
110000         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
110100                                                                          
110200         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
110300     ELSE                                                                 
110400         MOVE +1                     TO ASC-BLK-SUB                       
110500     END-IF.                                                              
110600******************************************************************        
110700** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
110800******************************************************************        
110900                                                                          
111000 3200-RESET-ATTRIBUTES.                                                   
111100     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
111200     MOVE DP015-GREEN              TO ASTRTITC.                           
111300     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
111400*                                                                         
111500     PERFORM                                                              
111600         VARYING PV-SUB                                                   
111700         FROM 1 BY 1                                                      
111800         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
111900         MOVE DP015-UNP-ALP-NOR-OFF                                       
112000                                   TO MR-SELECTA (PV-SUB)                 
112100         MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
112200         MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
112300     END-PERFORM.                                                         
112400******************************************************************        
112500** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
112600** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
112700** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
112800** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
112900******************************************************************        
113000 3300-EDIT-SELECTIONS.                                                    
113100     EVALUATE TRUE                                                        
113200         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
113300             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
113400                 CONTINUE                                                 
113500         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
113600             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
113700                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
113800                 MOVE ASC-TEMP-SELECTED-SW                                
113900                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
114000                                TO ASC-SELECTED-SW                        
114100                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
114200             END-IF                                                       
114300         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
114400                 = SPACE                                                  
114500             EVALUATE TRUE                                                
114600                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
114700                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
114800                     MOVE SPACE TO ASC-SELECTED-SW                        
114900                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
115000                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
115100                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
115200                     MOVE SPACE TO ASC-SELECTED-SW                        
115300                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
115400                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
115500                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
115600                     MOVE SPACE TO ASC-SELECTED-SW                        
115700                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
115800             END-EVALUATE                                                 
115900         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
116000             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
116100                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
116200             END-IF                                                       
116300             PERFORM 3310-EDIT-RANGE                                      
116400         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
116500             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
116600                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
116700             END-IF                                                       
116800             PERFORM 3310-EDIT-RANGE                                      
116900         WHEN OTHER                                                       
117000             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
117100             SET DP020-MSG-FATAL    TO TRUE                               
117200             MOVE -1                TO MR-SELECTL (PV-SUB)                
117300             SET DP030-SET-CURSOR-APPL-1                                  
117400                                    TO TRUE                               
117500             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
117600             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
117700             SET PS-ERROR TO TRUE                                         
117800             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
117900                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
118000                 MOVE SPACE         TO ASC-SELECTED-SW                    
118100                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
118200             END-IF                                                       
118300     END-EVALUATE.                                                        
118400                                                                          
118500     SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
118600     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
118700     IF  ASC-ITEM-SUB < 1                                                 
118800         SUBTRACT +1 FROM ASC-BLK-SUB                                     
118900         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
119000     END-IF.                                                              
119100******************************************************************        
119200** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
119300** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
119400** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
119500** THE ENDING.                                                  **        
119600******************************************************************        
119700 3310-EDIT-RANGE.                                                         
119800     IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
119900         IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
120000             MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
120100                                   TO ASC-SELECTED-SW                     
120200                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
120300             MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
120400         ELSE                                                             
120500             IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
120600                 CONTINUE                                                 
120700             ELSE                                                         
120800*                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
120900                 MOVE PC-TSYMSG-00054                                     
121000                                   TO DP020-MSG-NUMBER                    
121100                 SET DP020-MSG-FATAL                                      
121200                                   TO TRUE                                
121300                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
121400                 SET DP030-SET-CURSOR-APPL-1                              
121500                                   TO TRUE                                
121600                 MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
121700                 MOVE DP015-UNDERLINE                                     
121800                                   TO MR-SELECTH (PV-SUB)                 
121900                 SET PS-ERROR      TO TRUE                                
122000             END-IF                                                       
122100         END-IF                                                           
122200     ELSE                                                                 
122300         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
122400             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
122500                                   TO ASC-SELECTED-SW                     
122600                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
122700             MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
122800         ELSE                                                             
122900             IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
123000                 CONTINUE                                                 
123100             ELSE                                                         
123200*                -- ONLY ONE END-OF-RANGE ALLOWED --                      
123300                 MOVE PC-TSYMSG-00055                                     
123400                                   TO DP020-MSG-NUMBER                    
123500                 SET DP020-MSG-FATAL                                      
123600                                   TO TRUE                                
123700                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
123800                 SET DP030-SET-CURSOR-APPL-1                              
123900                                   TO TRUE                                
124000                 SET PS-ERROR      TO TRUE                                
124100             END-IF                                                       
124200         END-IF                                                           
124300     END-IF.                                                              
124400*                                                                         
124500******************************************************************        
124600** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
124700** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
124800******************************************************************        
124900 3400-CHECK-RANGE-SEQUENCE.                                               
125000     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
125100       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
125200       AND  ASC-START-OF-SELECTED-RANGE >                                 
125300                                         ASC-END-OF-SELECTED-RANGE        
125400*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
125500         MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
125600         SET DP020-MSG-FATAL TO TRUE                                      
125700         SET PS-ERROR TO TRUE                                             
125800         PERFORM 3410-POSITION-AT-ERROR                                   
125900     END-IF.                                                              
126000******************************************************************        
126100** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
126200** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
126300** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
126400******************************************************************        
126500 3410-POSITION-AT-ERROR.                                                  
126600     SET DP030-SET-CURSOR-APPL-1                                          
126700                                   TO TRUE.                               
126800     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
126900                    - PV-TOP-ITEM + 1.                                    
127000     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
127100         CONTINUE                                                         
127200     ELSE                                                                 
127300         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
127400         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
127500         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
127600     END-IF.                                                              
127700*                                                                         
127800     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
127900                    - PV-TOP-ITEM + 1.                                    
128000     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
128100         CONTINUE                                                         
128200     ELSE                                                                 
128300         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
128400         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
128500         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
128600     END-IF.                                                              
128700 EJECT                                                                    
128800*----------------------------------------------------------------*        
128900*    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
129000*    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
129100*    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
129200*----------------------------------------------------------------*        
129300                                                                          
129400 3500-MARK-RANGE-AS-SELECTED.                                             
129500     MOVE +1                       TO  ASC-BLK-SUB.                       
129600     PERFORM 3510-MARK-SELECTED-INDS                                      
129700         VARYING ASC-ITEM-SUB                                             
129800         FROM 1 BY 1                                                      
129900         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
130000                                                                          
130100     ADD +1                        TO ASC-BLK-SUB.                        
130200     PERFORM 3510-MARK-SELECTED-INDS                                      
130300         VARYING ASC-ITEM-SUB                                             
130400         FROM 1 BY 1                                                      
130500         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
130600                                                                          
130700     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
130800                                   TO  PV-LIST-ITEM-NUMBER.               
130900     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
131000         1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
131100               PC-ITEMS-PER-PANEL).                                       
131200                                                                          
131300     MOVE ASC-START-OF-SELECTED-RANGE                                     
131400                                   TO PV-START-OF-SELECTED-RANGE.         
131500     COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
131600         1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
131700               PC-ITEMS-PER-PANEL).                                       
131800                                                                          
131900     MOVE ASC-END-OF-SELECTED-RANGE                                       
132000                                   TO  PV-END-OF-SELECTED-RANGE.          
132100     COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
132200         1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
132300               PC-ITEMS-PER-PANEL).                                       
132400     COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
132500         PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
132600                                                                          
132700     IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
132800        (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
132900         MOVE +1                   TO ASC-BLK-SUB                         
133000         PERFORM 5100-WRITE-TEMP-STORAGE                                  
133100         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
133200             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
133300                                                                          
133400         PERFORM                                                          
133500             UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
133600                   PV-PREV-BLOCK-IN-ARRAY                                 
133700             MOVE PV-FIRST-BLOCK-IN-RANGE                                 
133800                                   TO PV-BLOCK-NUMBER                     
133900             PERFORM 4410-READ-TEMP-STORAGE                               
134000             PERFORM 3510-MARK-SELECTED-INDS                              
134100                 VARYING ASC-ITEM-SUB                                     
134200                 FROM 1 BY 1                                              
134300                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
134400             PERFORM 5100-WRITE-TEMP-STORAGE                              
134500             ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
134600         END-PERFORM                                                      
134700                                                                          
134800         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
134900             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
135000         PERFORM                                                          
135100             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
135200                   PV-LAST-BLOCK-IN-RANGE                                 
135300             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
135400                                   TO PV-BLOCK-NUMBER                     
135500             PERFORM 4410-READ-TEMP-STORAGE                               
135600             PERFORM 3510-MARK-SELECTED-INDS                              
135700                 VARYING ASC-ITEM-SUB                                     
135800                 FROM 1 BY 1                                              
135900                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
136000             PERFORM 5100-WRITE-TEMP-STORAGE                              
136100             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
136200         END-PERFORM                                                      
136300                                                                          
136400         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
136500                                   TO PV-BLOCK-NUMBER                     
136600         PERFORM 4410-READ-TEMP-STORAGE                                   
136700     END-IF.                                                              
136800                                                                          
136900     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
137000                                      ASC-END-OF-SELECTED-RANGE.          
137100                                                                          
137200 EJECT                                                                    
137300*----------------------------------------------------------------*        
137400* SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
137500*----------------------------------------------------------------*        
137600                                                                          
137700 3510-MARK-SELECTED-INDS.                                                 
137800                                                                          
137900     IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
138000                                ASC-ITEM-SUB) >                           
138100         ASC-START-OF-SELECTED-RANGE)                                     
138200     OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
138300                                ASC-ITEM-SUB) =                           
138400         ASC-START-OF-SELECTED-RANGE)                                     
138500                                                                          
138600         IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
138700                                    ASC-ITEM-SUB) <                       
138800             ASC-END-OF-SELECTED-RANGE)                                   
138900         OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
139000                                    ASC-ITEM-SUB) =                       
139100             ASC-END-OF-SELECTED-RANGE)                                   
139200                                                                          
139300             IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
139400                 SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
139500                     ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
139600                               TO TRUE                                    
139700                 SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
139800                               TO TRUE                                    
139900                 ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
140000             END-IF                                                       
140100         END-IF                                                           
140200     END-IF.                                                              
140300                                                                          
140400 EJECT                                                                    
140500*----------------------------------------------------------------*        
140600*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
140700*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
140800*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
140900*    DISPLAY THE PANEL TO USER.                                  *        
141000*----------------------------------------------------------------*        
141100                                                                          
141200 4000-BUILD-INITIAL-PANEL.                                                
141300                                                                          
141400     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
141500                                 ASC-BLOCK-ENTRIES (2).                   
141600     MOVE +1                 TO ASC-BLK-SUB.                              
141700     SET ASC-ITEMS-LEFT-ON-DB                                             
141800                             TO TRUE.                                     
141900                                                                          
142000     MOVE +0                 TO ASC-ITEM-SUB                              
142100                                ASC-NUMBER-OF-ITEMS-READ                  
142200                                ASC-NUMBER-OF-SELECTED-ITEMS              
142300                                ASC-HIGHEST-BLOCK-WRITTEN                 
142400                                ASC-START-OF-SELECTED-RANGE               
142500                                ASC-END-OF-SELECTED-RANGE                 
142600                                ASC-ITEM-AT-TOP-OF-PANEL.                 
142700                                                                          
142800     EVALUATE TRUE                                                        
142900         WHEN DK-LBL-NM = SPACES                                          
143000              SET PS-CSR-VENDOR-ALL TO TRUE                               
143100              MOVE 'DEPT-NO-VENDOR    ' TO PV-CURSOR-NAME                 
143200                                                                          
143300         WHEN OTHER                                                       
143400              SET PS-CSR-VENDOR-SPECIFIC TO TRUE                          
143500              MOVE 'DEPT-CSR          ' TO PV-CURSOR-NAME                 
143600                                                                          
143700     END-EVALUATE.                                                        
143800                                                                          
143900     PERFORM 4010-OPEN-CURSOR.                                            
144000                                                                          
144100     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
144200                                                                          
144300     PERFORM 4020-CLOSE-CURSOR.                                           
144400                                                                          
144500     MOVE +1                   TO  PV-TOP-ITEM.                           
144600     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
144700         SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
144800         SET DP020-MSG-FATAL   TO  TRUE                                   
144900         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
145000     ELSE                                                                 
145100         MOVE -1               TO MR-SELECTL (1)                          
145200         SET DP030-SET-CURSOR-APPL-1                                      
145300                               TO TRUE                                    
145400         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
145500     END-IF.                                                              
145600 EJECT                                                                    
145700*----------------------------------------------------------------*        
145800* OPEN ONE OF THE DEPARTMENT CURSORS.                            *        
145900*----------------------------------------------------------------*        
146000                                                                          
146100 4010-OPEN-CURSOR.                                                        
146200                                                                          
146300     MOVE '4010-OPEN-CURSOR'   TO DP013-PARAGRAPH.                        
146400                                                                          
146500     PERFORM                                                              
146600         WITH TEST AFTER                                                  
146700         VARYING PV-RETRY-COUNTER                                         
146800         FROM 1 BY 1                                                      
146900         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
147000                   OR (SQLCODE = ZERO))                                   
147100                                                                          
147200         EVALUATE TRUE                                                    
147300             WHEN PS-CSR-VENDOR-ALL                                       
147400                  EXEC SQL                                                
147500                       OPEN DEPT-NO-VENDOR                                
147600                  END-EXEC                                                
147700                                                                          
147800             WHEN PS-CSR-VENDOR-SPECIFIC                                  
147900                  EXEC SQL                                                
148000                       OPEN DEPT-CSR                                      
148100                  END-EXEC                                                
148200                                                                          
148300         END-EVALUATE                                                     
148400                                                                          
148500         EVALUATE TRUE                                                    
148600             WHEN SQLCODE = ZERO                                          
148700             WHEN SQLCODE = -904                                          
148800             WHEN SQLCODE = -913                                          
148900                 CONTINUE                                                 
149000             WHEN SQLWARN0 NOT = SPACES                                   
149100             WHEN SQLCODE < ZERO                                          
149200             WHEN SQLCODE > ZERO                                          
149300                 STRING 'OPEN '                                           
149400                         PV-CURSOR-NAME                                   
149500                        ' CURSOR'                                         
149600                         DELIMITED BY SIZE                                
149700                         INTO DP013-MESSAGE-TEXT (1)                      
149800                 MOVE SQLCA        TO DP013-SQLCA                         
149900                 MOVE 'BUS GP, DEPT'                                      
150000                                   TO DP013-DB2-TABLE-NAME (1)            
150100                 MOVE 'VND, VND_STYL'                                     
150200                                   TO DP013-DB2-TABLE-NAME (2)            
150300                 SET DP013-DB2-ABEND                                      
150400                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
150500                 PERFORM DP013-0000-PROCESS-ABEND                         
150600         END-EVALUATE                                                     
150700     END-PERFORM.                                                         
150800*                                                                         
150900     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
151000         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN '            
151100                 PV-CURSOR-NAME                                           
151200                ' CURSOR'                                                 
151300                 DELIMITED BY SIZE                                        
151400                 INTO DP013-MESSAGE-TEXT (1)                              
151500         MOVE SQLCA                TO DP013-SQLCA                         
151600         MOVE 'BUS GP, DEPT'  TO DP013-DB2-TABLE-NAME (1)                 
151700         MOVE 'VND, VND_STYL' TO DP013-DB2-TABLE-NAME (2)                 
151800         SET DP013-DB2-ABEND                                              
151900             DP013-XCTL-DISPLAY-RESTART                                   
152000                                   TO TRUE                                
152100         PERFORM DP013-0000-PROCESS-ABEND                                 
152200     END-IF.                                                              
152300                                                                          
152400 EJECT                                                                    
152500*----------------------------------------------------------------*        
152600* CLOSE ONE OF THE DEPARTMENT CURSORS.                           *        
152700*----------------------------------------------------------------*        
152800                                                                          
152900 4020-CLOSE-CURSOR.                                                       
153000                                                                          
153100     MOVE '4020-CLOSE-CURSOR'   TO DP013-PARAGRAPH.                       
153200                                                                          
153300     EVALUATE TRUE                                                        
153400         WHEN PS-CSR-VENDOR-ALL                                           
153500              EXEC SQL                                                    
153600                   CLOSE DEPT-NO-VENDOR                                   
153700              END-EXEC                                                    
153800                                                                          
153900         WHEN PS-CSR-VENDOR-SPECIFIC                                      
154000              EXEC SQL                                                    
154100                   CLOSE DEPT-CSR                                         
154200              END-EXEC                                                    
154300                                                                          
154400     END-EVALUATE.                                                        
154500                                                                          
154600     EVALUATE TRUE                                                        
154700         WHEN SQLCODE = ZERO                                              
154800             CONTINUE                                                     
154900         WHEN SQLWARN0 NOT = SPACES                                       
155000         WHEN SQLCODE < ZERO                                              
155100         WHEN SQLCODE > ZERO                                              
155200             STRING 'CLOSE '                                              
155300                     PV-CURSOR-NAME                                       
155400                    ' CURSOR'                                             
155500                     DELIMITED BY SIZE                                    
155600                     INTO DP013-MESSAGE-TEXT (1)                          
155700             MOVE SQLCA        TO DP013-SQLCA                             
155800             MOVE 'BUS GP, DEPT'  TO DP013-DB2-TABLE-NAME (1)             
155900             MOVE 'VND, VND_STYL' TO DP013-DB2-TABLE-NAME (2)             
156000             SET DP013-DB2-ABEND                                          
156100                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
156200             PERFORM DP013-0000-PROCESS-ABEND                             
156300     END-EVALUATE.                                                        
156400                                                                          
156500*----------------------------------------------------------------*        
156600*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
156700*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
156800*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
156900*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
157000*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
157100*                                                                *        
157200*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
157300*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
157400*    UPON INITIALIZATION.                                        *        
157500*----------------------------------------------------------------*        
157600                                                                          
157700 4300-READ-ITEMS-FROM-DB.                                                 
157800                                                                          
157900     PERFORM                                                              
158000         VARYING PV-READ-COUNT                                            
158100         FROM 1 BY 1                                                      
158200         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
158300            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
158400                                                                          
158500         PERFORM 4310-FETCH                                               
158600                                                                          
158700         IF  ASC-ITEMS-LEFT-ON-DB                                         
158800             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
158900                 ADD +1            TO ASC-ITEM-SUB                        
159000             ELSE                                                         
159100                 ADD +1            TO ASC-BLK-SUB                         
159200                 MOVE +1           TO ASC-ITEM-SUB                        
159300                                                                          
159400                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
159500                     MOVE +1       TO ASC-BLK-SUB                         
159600                     SET ASC-BLOCK-MODIFIED (1)                           
159700                                   TO TRUE                                
159800                     PERFORM 5100-WRITE-TEMP-STORAGE                      
159900                     MOVE ASC-BLOCK-ENTRIES                               
160000                               (PC-MAX-BLOCKS-IN-ARRAY)                   
160100                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
160200                     ADD +1        TO ASC-BLK-SUB                         
160300                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
160400                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
160500                                   TO TRUE                                
160600                 END-IF                                                   
160700             END-IF                                                       
160800             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
160900             MOVE ASC-NUMBER-OF-ITEMS-READ                                
161000                                   TO ASC-LIST-ITEM-NUMBER                
161100                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
161200             MOVE XST00015-DEPT-NBR    TO ASC-DEPT-NBR                    
161300                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
161400             MOVE XST00015-DEPT-NM TO ASC-DEPT-DESC                       
161500                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
161600         END-IF                                                           
161700     END-PERFORM.                                                         
161800                                                                          
161900     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
162000         MOVE +1               TO ASC-BLK-SUB                             
162100         PERFORM 5100-WRITE-TEMP-STORAGE                                  
162200         ADD +1                TO ASC-BLK-SUB                             
162300         PERFORM 5100-WRITE-TEMP-STORAGE                                  
162400                                                                          
162500         MOVE 1                TO ASC-BLK-SUB                             
162600                                  PV-BLOCK-NUMBER                         
162700         PERFORM 4410-READ-TEMP-STORAGE                                   
162800         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
162900             MOVE 2            TO ASC-BLK-SUB                             
163000                                  PV-BLOCK-NUMBER                         
163100             PERFORM 4410-READ-TEMP-STORAGE                               
163200         END-IF                                                           
163300     END-IF.                                                              
163400                                                                          
163500*----------------------------------------------------------------*        
163600*    FETCH ONE OF THE DEPARTMENT CURSORS.                        *        
163700*----------------------------------------------------------------*        
163800                                                                          
163900 4310-FETCH.                                                              
164000                                                                          
164100     MOVE '4310-FETCH' TO DP013-PARAGRAPH.                                
164200                                                                          
164300     PERFORM                                                              
164400         WITH TEST AFTER                                                  
164500         VARYING PV-RETRY-COUNTER                                         
164600         FROM 1 BY 1                                                      
164700         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
164800                   OR  SQLCODE = ZERO                                     
164810                   OR  SQLCODE = +100)                                    
164900                                                                          
165000         EVALUATE TRUE                                                    
165100             WHEN PS-CSR-VENDOR-ALL                                       
165200                  EXEC SQL                                                
165300                       FETCH DEPT-NO-VENDOR                               
165400                         INTO                                             
165500                              :XST00015-DEPT-NBR                          
165600                             ,:XST00015-DEPT-NM                           
165700                  END-EXEC                                                
165800                                                                          
165900             WHEN PS-CSR-VENDOR-SPECIFIC                                  
166000                  EXEC SQL                                                
166100                       FETCH DEPT-CSR                                     
166200                             INTO                                         
166300                              :XST00015-DEPT-NBR,                         
166400                              :XST00015-DEPT-NM                           
166500                  END-EXEC                                                
166600                                                                          
166700         END-EVALUATE                                                     
166800                                                                          
166900         EVALUATE TRUE                                                    
167000             WHEN SQLCODE = ZERO                                          
167100             WHEN SQLCODE = +100                                          
167200                  CONTINUE                                                
167300             WHEN SQLWARN0 NOT = SPACE                                    
167400             WHEN SQLCODE NOT = ZERO                                      
167500                  STRING 'FETCH '                                         
167600                          PV-CURSOR-NAME                                  
167700                         ' CURSOR'                                        
167800                          DELIMITED BY SIZE                               
167900                          INTO DP013-MESSAGE-TEXT (1)                     
168000                  MOVE SQLCA       TO DP013-SQLCA                         
168100                  SET DP013-DB2-ABEND                                     
168110                      DP013-XCTL-DISPLAY-RESTART                          
168200                                   TO TRUE                                
168300                  PERFORM DP013-0000-PROCESS-ABEND                        
168400         END-EVALUATE                                                     
168500                                                                          
168600     END-PERFORM.                                                         
168700                                                                          
168800     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
168900         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO FETCH '           
169000                 PV-CURSOR-NAME                                           
169100                ' CURSOR'                                                 
169200                 DELIMITED BY SIZE                                        
169300                 INTO DP013-MESSAGE-TEXT (1)                              
169400         MOVE SQLCA           TO DP013-SQLCA                              
169500         MOVE 'BUS GP, DEPT'  TO DP013-DB2-TABLE-NAME (1)                 
169600         MOVE 'VND, VND_STYL' TO DP013-DB2-TABLE-NAME (2)                 
169700         SET DP013-DB2-ABEND                                              
169800             DP013-XCTL-DISPLAY-RESTART                                   
169900                                   TO TRUE                                
170000         PERFORM DP013-0000-PROCESS-ABEND                                 
170100     END-IF.                                                              
170200                                                                          
170300     IF  SQLCODE EQUAL +100                                               
170400         SET ASC-NO-ITEMS-LEFT-ON-DB                                      
170500                                   TO TRUE                                
170600     END-IF.                                                              
170700                                                                          
170800                                                                          
170900*----------------------------------------------------------------*        
171000* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
171100*----------------------------------------------------------------*        
171200                                                                          
171300 4400-MOVE-COMMAREA-TO-SCREEN.                                            
171400                                                                          
171500     IF ASC-LBL-NM = SPACES                                               
171600        MOVE 'ALL' TO AVENDNMO                                            
171700     ELSE                                                                 
171800        MOVE ASC-LBL-NM TO AVENDNMO                                       
171900     END-IF.                                                              
172000                                                                          
172100     MOVE ASC-GP-NBR          TO ABUSGRPO.                                
172200     MOVE ASC-GP-DESC         TO ABUSDESO.                                
172300                                                                          
172400     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
172500         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
172600         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
172700             MOVE +1    TO ASC-BLK-SUB                                    
172800             PERFORM 5100-WRITE-TEMP-STORAGE                              
172900             ADD +1     TO ASC-BLK-SUB                                    
173000             PERFORM 5100-WRITE-TEMP-STORAGE                              
173100             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
173200                 GIVING PV-BLOCK-NUMBER                                   
173300                 REMAINDER PV-REMAINDER                                   
173400             IF  PV-REMAINDER > ZERO                                      
173500                 ADD +1 TO PV-BLOCK-NUMBER                                
173600             END-IF                                                       
173700             MOVE +1    TO ASC-BLK-SUB                                    
173800             PERFORM 4410-READ-TEMP-STORAGE                               
173900             ADD +1     TO ASC-BLK-SUB                                    
174000                           PV-BLOCK-NUMBER                                
174100             PERFORM 4410-READ-TEMP-STORAGE                               
174200     END-IF.                                                              
174300     MOVE +1            TO ASC-BLK-SUB.                                   
174400     COMPUTE ASC-ITEM-SUB =                                               
174500         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
174600         + PC-ITEMS-PER-PANEL).                                           
174700*                                                                         
174800     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
174900         ADD +1         TO ASC-BLK-SUB                                    
175000         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
175100     END-IF.                                                              
175200*                                                                         
175300     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
175400         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
175500     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
175600     PERFORM 4420-FORMAT-LIST-LINES                                       
175700         VARYING PV-SUB                                                   
175800         FROM PC-ITEMS-PER-PANEL BY -1                                    
175900         UNTIL PV-SUB < +1.                                               
176000*                                                                         
176100     COMPUTE ASC-ITEMS-DISPLAYED =                                        
176200         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
176300     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
176400                                      ASTRTITO.                           
176500     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
176600     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
176700     IF  DP020-MSG-NUMBER-X = SPACE                                       
176800         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
176900*            --- MORE ITEMS TO DISPLAY ----                               
177000             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
177100             SET DP020-MSG-INFORMATIONAL TO TRUE                          
177200         END-IF                                                           
177300     END-IF.                                                              
177400*----------------------------------------------------------------*        
177500*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
177600*----------------------------------------------------------------*        
177700                                                                          
177800 4410-READ-TEMP-STORAGE.                                                  
177900                                                                          
178000     EXEC CICS                                                            
178100         READQ TS                                                         
178200             QUEUE (ASC-LIST-QUEUE-NAME)                                  
178300             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
178400             ITEM  (PV-BLOCK-NUMBER)                                      
178500             RESP  (PV-CICS-RESP)                                         
178600     END-EXEC.                                                            
178700                                                                          
178800     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
178900       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
179000         CONTINUE                                                         
179100     ELSE                                                                 
179200         SET DP013-CICS-ABEND     TO TRUE                                 
179300         SET DP013-NO-ROLLBACK    TO TRUE                                 
179400         MOVE '4410-READ-TEMP-STORAGE'                                    
179500                                  TO DP013-PARAGRAPH                      
179600         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
179700                                  TO DP013-MESSAGE-TEXT(1)                
179800         PERFORM DP013-0000-PROCESS-ABEND                                 
179900     END-IF.                                                              
180000                                                                          
180100     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
180200*                                                                         
180300     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
180400         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
180500     END-IF.                                                              
180600 EJECT                                                                    
180700*----------------------------------------------------------------*        
180800*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
180900*----------------------------------------------------------------*        
181000                                                                          
181100 4420-FORMAT-LIST-LINES.                                                  
181200     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
181300         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
181400         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
181500         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
181600         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
181700                                    MR-DEPT-NBR (PV-SUB)                  
181800                                    MR-DEPT-DESC (PV-SUB)                 
181900     ELSE                                                                 
182000         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
182100                                                   > SPACE                
182200             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
182300                                 TO MR-SELECTI (PV-SUB)                   
182400         ELSE                                                             
182500             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
182600                                 TO MR-SELECTI (PV-SUB)                   
182700         END-IF                                                           
182800*                                                                         
182900         IF  DP030-SET-CURSOR-TRAILER                                     
183000             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
183100                 > SPACE)                                                 
183200               OR (PV-SUB = 1))                                           
183300                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
183400                MOVE -1          TO MR-SELECTL (PV-SUB)                   
183500             END-IF                                                       
183600         END-IF                                                           
183700*                                                                         
183800         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
183900                                                                          
184000             MOVE ASC-DEPT-NBR (ASC-BLK-SUB ASC-ITEM-SUB)                 
184100                                 TO MR-DEPT-NBR (PV-SUB)                  
184200             MOVE ASC-DEPT-DESC (ASC-BLK-SUB ASC-ITEM-SUB)                
184300                                 TO MR-DEPT-DESC (PV-SUB)                 
184400         END-IF                                                           
184500     END-IF.                                                              
184600     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
184700     IF  ASC-ITEM-SUB < + 1                                               
184800         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
184900         MOVE +1                 TO ASC-BLK-SUB                           
185000     END-IF.                                                              
185100******************************************************************        
185200** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
185300** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
185400** ISSUE A WARNING.                                             **        
185500******************************************************************        
185600 4430-CHECK-FOR-BEGIN-AND-END.                                            
185700     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
185800         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
185900*            -- NO END-OF-RANGE SPECFIED ---                              
186000             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
186100             SET DP020-MSG-WARNING TO TRUE                                
186200             PERFORM 4431-POSITION-AT-WARNING                             
186300         END-IF                                                           
186400     ELSE                                                                 
186500         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
186600*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
186700             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
186800             SET DP020-MSG-WARNING TO TRUE                                
186900             PERFORM 4431-POSITION-AT-WARNING                             
187000         END-IF                                                           
187100     END-IF.                                                              
187200******************************************************************        
187300** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
187400** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
187500** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
187600** POSITION THE CURSOR THERE.                                   **        
187700******************************************************************        
187800 4431-POSITION-AT-WARNING.                                                
187900     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
188000                    - PV-TOP-ITEM + 1.                                    
188100     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
188200         CONTINUE                                                         
188300     ELSE                                                                 
188400         SET DP030-SET-CURSOR-APPL-1                                      
188500                                   TO TRUE                                
188600         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
188700         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
188800         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
188900     END-IF.                                                              
189000*                                                                         
189100     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
189200                    - PV-TOP-ITEM + 1.                                    
189300     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
189400         CONTINUE                                                         
189500     ELSE                                                                 
189600         SET DP030-SET-CURSOR-APPL-1                                      
189700                                   TO TRUE                                
189800         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
189900         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
190000         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
190100     END-IF.                                                              
190200 EJECT                                                                    
190300*----------------------------------------------------------------*        
190400* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
190500* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
190600* FOR THE SELECTED ITEMS QUEUE.                                  *        
190700*                                                                *        
190800* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
190900* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
191000* THE ARRAY IS FULL.                                             *        
191100*----------------------------------------------------------------*        
191200                                                                          
191300 5000-PREPARE-SEL-QUEUE.                                                  
191400                                                                          
191500     MOVE +1                   TO  ASC-BLK-SUB.                           
191600     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
191700     ADD +1                    TO  ASC-BLK-SUB.                           
191800     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
191900                                                                          
192000     MOVE ASC-SEL-QUEUE-NAME   TO  IQ016-SEL-QUEUE-NAME.                  
192100     PERFORM 6010-DELETE-SEL-QUEUE.                                       
192200                                                                          
192300     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
192400                                                                          
192500     SET IQ014-IDX             TO  1.                                     
192600     MOVE ZERO                 TO IQ016-NUMBER-OF-TS-ITEMS.               
192700     INITIALIZE IQ014-SELECTED-ITEM-ARRAY.                                
192800                                                                          
192900     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
193000             VARYING PV-ITEM                                              
193100             FROM 1 BY 1                                                  
193200             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
193300                                                                          
193400     IF  IQ014-IDX > 1                                                    
193500         PERFORM 5205-WRITE-SEL-QUEUE                                     
193600         ADD +1                TO IQ016-NUMBER-OF-TS-ITEMS                
193700     END-IF.                                                              
193800 EJECT                                                                    
193900*----------------------------------------------------------------*        
194000*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
194100*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
194200*----------------------------------------------------------------*        
194300                                                                          
194400 5100-WRITE-TEMP-STORAGE.                                                 
194500     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
194600                                   TO PV-LIST-ITEM-NUMBER.                
194700     COMPUTE PV-BLOCK-NUMBER =                                            
194800         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
194900                                                                          
195000     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
195100     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
195200         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
195300             PERFORM 5105-REWRITE-ASC-LISTQ                               
195400         END-IF                                                           
195500     ELSE                                                                 
195600         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
195700         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
195800         PERFORM 5110-WRITE-ASC-LISTQ                                     
195900         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
196000     END-IF.                                                              
196100                                                                          
196200     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
196300******************************************************************        
196400** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
196500** SPECIFIC COMMAREA.                                           **        
196600******************************************************************        
196700 5105-REWRITE-ASC-LISTQ.                                                  
196800     EXEC CICS                                                            
196900         WRITEQ TS                                                        
197000             QUEUE (ASC-LIST-QUEUE-NAME)                                  
197100             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
197200             ITEM  (PV-BLOCK-NUMBER)                                      
197300             REWRITE                                                      
197400             RESP  (PV-CICS-RESP)                                         
197500     END-EXEC.                                                            
197600                                                                          
197700     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
197800         CONTINUE                                                         
197900     ELSE                                                                 
198000         SET DP013-CICS-ABEND            TO TRUE                          
198100         SET DP013-NO-ROLLBACK           TO TRUE                          
198200         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
198300         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
198400                                         TO DP013-MESSAGE-TEXT(1)         
198500         PERFORM DP013-0000-PROCESS-ABEND                                 
198600     END-IF.                                                              
198700                                                                          
198800******************************************************************        
198900** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
199000** SPECIFIC COMMAREA.                                           **        
199100******************************************************************        
199200 5110-WRITE-ASC-LISTQ.                                                    
199300     EXEC CICS                                                            
199400         WRITEQ TS                                                        
199500             QUEUE (ASC-LIST-QUEUE-NAME)                                  
199600             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
199700             ITEM  (PV-BLOCK-NUMBER)                                      
199800             RESP  (PV-CICS-RESP)                                         
199900     END-EXEC.                                                            
200000     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
200100         SET DP013-CICS-ABEND      TO TRUE                                
200200         SET DP013-NO-ROLLBACK     TO TRUE                                
200300         MOVE '5110-WRITE-ASC-LISTQ'                                      
200400                                   TO DP013-PARAGRAPH                     
200500         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
200600                                   TO DP013-MESSAGE-TEXT(1)               
200700         PERFORM DP013-0000-PROCESS-ABEND                                 
200800     END-IF.                                                              
200900 EJECT                                                                    
201000*----------------------------------------------------------------*        
201100* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
201200* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
201300*----------------------------------------------------------------*        
201400                                                                          
201500 5200-FILL-SEL-QUEUE-RECORD.                                              
201600                                                                          
201700     PERFORM 6112-READ-LIST-QUEUE.                                        
201800                                                                          
201900     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
202000         PERFORM                                                          
202100         VARYING LQW-IDX                                                  
202200         FROM 1 BY 1                                                      
202300         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
202400           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
202500                                                                          
202600             IF  LQW-SELECT (LQW-IDX)                                     
202700                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
202800                                   TO IQ014-ITEM (IQ014-IDX)              
202900                 MOVE LQW-DEPT-NBR (LQW-IDX)                              
203000                             TO IQ014-DEPT-NBR (IQ014-IDX)                
203100                                IQ016-DEPT-NBR                            
203200                 MOVE LQW-DEPT-DESC (LQW-IDX)                             
203300                             TO IQ014-DEPT-DESC (IQ014-IDX)               
203400                                IQ016-DEPT-DESC                           
203500                 SET IQ014-IDX UP BY 1                                    
203600                 IF  IQ014-IDX > IQ014-MAX-ITEMS                          
203700                     PERFORM 5205-WRITE-SEL-QUEUE                         
203800                     ADD +1        TO IQ016-NUMBER-OF-TS-ITEMS            
203900                     INITIALIZE IQ014-SELECTED-ITEM-ARRAY                 
204000                     SET IQ014-IDX TO  1                                  
204100                 END-IF                                                   
204200             END-IF                                                       
204300         END-PERFORM                                                      
204400     END-IF.                                                              
204500                                                                          
204600*----------------------------------------------------------------*        
204700*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
204800*----------------------------------------------------------------*        
204900                                                                          
205000 5205-WRITE-SEL-QUEUE.                                                    
205100                                                                          
205200     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
205300                                   TO IQ014-SELECTED-ITEMS.               
205400                                                                          
205500     EXEC CICS                                                            
205600          WRITEQ TS                                                       
205700              QUEUE (ASC-SEL-QUEUE-NAME)                                  
205800              FROM  (IQ014-MESSAGE-SEL-REC)                               
205900              ITEM  (PV-SEL-QUEUE-ITEM)                                   
206000              RESP  (PV-CICS-RESP)                                        
206100     END-EXEC.                                                            
206200                                                                          
206300     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
206400         CONTINUE                                                         
206500     ELSE                                                                 
206600         SET DP013-CICS-ABEND      TO TRUE                                
206700         SET DP013-NO-ROLLBACK     TO TRUE                                
206800         MOVE '5205-WRITE-SEL-QUEUE'                                      
206900                                   TO DP013-PARAGRAPH                     
207000         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
207100                                   TO DP013-MESSAGE-TEXT(1)               
207200         PERFORM DP013-0000-PROCESS-ABEND                                 
207300     END-IF.                                                              
207400 EJECT                                                                    
207500******************************************************************        
207600** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
207700** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
207800** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
207900** THE USER.                                                    **        
208000******************************************************************        
208100 6000-REFRESH-PANEL-FROM-TEMP.                                            
208200     IF  ASC-UPDATE-TSQ                                                   
208300         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
208400         MOVE ZERO                 TO PV-SAVE-ITEM                        
208500                                      PV-SEL-QUEUE-ITEM                   
208600         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
208700                                   TO PV-HOLD-SELECTED-ITEMS              
208800         MOVE +1                   TO PV-ITEMS-PROCESSED                  
208900         PERFORM 6100-UPDATE-TSQ                                          
209000             UNTIL PV-ITEMS-PROCESSED >                                   
209100                                     PV-HOLD-SELECTED-ITEMS               
209200         PERFORM 6010-DELETE-SEL-QUEUE                                    
209300         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
209400                    ASC-BLOCK-ENTRIES (2)                                 
209500     END-IF.                                                              
209600                                                                          
209700*----------------------------------------------------------------*        
209800*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
209900*----------------------------------------------------------------*        
210000 6010-DELETE-SEL-QUEUE.                                                   
210100                                                                          
210200     EXEC CICS                                                            
210300         DELETEQ TS                                                       
210400             QUEUE (ASC-SEL-QUEUE-NAME)                                   
210500             RESP  (PV-CICS-RESP)                                         
210600     END-EXEC.                                                            
210700*                                                                         
210800     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
210900        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
211000         CONTINUE                                                         
211100     ELSE                                                                 
211200         SET DP013-CICS-ABEND TO TRUE                                     
211300         SET DP013-NO-ROLLBACK TO TRUE                                    
211400         MOVE '6010-DELETE-SEL-QUEUE'                                     
211500                        TO DP013-PARAGRAPH                                
211600         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
211700                        TO DP013-MESSAGE-TEXT (1)                         
211800         PERFORM DP013-0000-PROCESS-ABEND                                 
211900     END-IF.                                                              
212000                                                                          
212100*----------------------------------------------------------------*        
212200*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
212300*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
212400*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
212500*    THE TEMP QUEUE.                                             *        
212600*----------------------------------------------------------------*        
212700                                                                          
212800 6100-UPDATE-TSQ.                                                         
212900                                                                          
213000     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
213100     PERFORM 6105-READ-SEL-QUEUE.                                         
213200     PERFORM                                                              
213300         VARYING IQ014-IDX                                                
213400         FROM 1 BY 1                                                      
213500         UNTIL ((PV-ITEMS-PROCESSED >                                     
213600                              PV-HOLD-SELECTED-ITEMS)                     
213700         OR (IQ014-IDX GREATER THAN IQ014-MAX-ITEMS))                     
213800         PERFORM 6110-PROCESS-ITEMS                                       
213900         ADD +1                     TO PV-ITEMS-PROCESSED                 
214000     END-PERFORM.                                                         
214100     PERFORM 6111-WRITE-LIST-QUEUE.                                       
214200*----------------------------------------------------------------*        
214300*    READ THE LIST QUEUE RECORD.                                 *        
214400*----------------------------------------------------------------*        
214500                                                                          
214600 6105-READ-SEL-QUEUE.                                                     
214700                                                                          
214800     EXEC CICS                                                            
214900          READQ TS                                                        
215000              QUEUE (ASC-SEL-QUEUE-NAME)                                  
215100              INTO  (IQ014-MESSAGE-SEL-REC)                               
215200              ITEM  (PV-SEL-QUEUE-ITEM)                                   
215300              RESP  (PV-CICS-RESP)                                        
215400     END-EXEC.                                                            
215500*                                                                         
215600     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
215700         CONTINUE                                                         
215800     ELSE                                                                 
215900         SET DP013-CICS-ABEND      TO  TRUE                               
216000         SET DP013-NO-ROLLBACK     TO  TRUE                               
216100         MOVE '6105-READ-SEL-QUEUE'                                       
216200                                   TO  DP013-PARAGRAPH                    
216300         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
216400                                   TO  DP013-MESSAGE-TEXT(1)              
216500         PERFORM DP013-0000-PROCESS-ABEND                                 
216600     END-IF.                                                              
216700 EJECT                                                                    
216800*----------------------------------------------------------------*        
216900*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
217000*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
217100*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
217200*----------------------------------------------------------------*        
217300                                                                          
217400 6110-PROCESS-ITEMS.                                                      
217500                                                                          
217600     COMPUTE PV-ITEM = 1 + ((IQ014-ITEM (IQ014-IDX) - 1)                  
217700                     / PC-ITEMS-PER-PANEL).                               
217800                                                                          
217900     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
218000         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
218100             PERFORM 6111-WRITE-LIST-QUEUE                                
218200         END-IF                                                           
218300         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
218400                                                                          
218500         PERFORM 6112-READ-LIST-QUEUE                                     
218600         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
218700             CONTINUE                                                     
218800         END-IF                                                           
218900     END-IF.                                                              
219000                                                                          
219100     COMPUTE PV-SUB = (IQ014-ITEM (IQ014-IDX)                             
219200                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
219300     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
219400     MOVE LQW-SELECTED-SW (PV-SUB)                                        
219500                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
219600                                                                          
219700     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
219800                                                                          
219900*----------------------------------------------------------------*        
220000*    WRITE THE TS QUEUE RECORD JUST UPDATED.                     *        
220100*----------------------------------------------------------------*        
220200                                                                          
220300 6111-WRITE-LIST-QUEUE.                                                   
220400                                                                          
220500     EXEC CICS                                                            
220600         WRITEQ TS                                                        
220700             QUEUE (ASC-LIST-QUEUE-NAME)                                  
220800             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
220900             ITEM  (PV-SAVE-ITEM)                                         
221000             REWRITE                                                      
221100             RESP  (PV-CICS-RESP)                                         
221200     END-EXEC.                                                            
221300*                                                                         
221400     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
221500         CONTINUE                                                         
221600     ELSE                                                                 
221700         SET DP013-CICS-ABEND  TO  TRUE                                   
221800         SET DP013-NO-ROLLBACK TO  TRUE                                   
221900         MOVE '6111-WRITE-LIST-QUEUE'                                     
222000                               TO  DP013-PARAGRAPH                        
222100         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
222200                               TO  DP013-MESSAGE-TEXT(1)                  
222300         PERFORM DP013-0000-PROCESS-ABEND                                 
222400     END-IF.                                                              
222500                                                                          
222600 EJECT                                                                    
222700*----------------------------------------------------------------*        
222800*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
222900*----------------------------------------------------------------*        
223000                                                                          
223100 6112-READ-LIST-QUEUE.                                                    
223200                                                                          
223300     EXEC CICS                                                            
223400         READQ TS                                                         
223500             QUEUE (ASC-LIST-QUEUE-NAME)                                  
223600             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
223700             ITEM  (PV-ITEM)                                              
223800             RESP  (PV-CICS-RESP)                                         
223900     END-EXEC.                                                            
224000*                                                                         
224100     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
224200         CONTINUE                                                         
224300     ELSE                                                                 
224400         SET DP013-CICS-ABEND        TO TRUE                              
224500         SET DP013-NO-ROLLBACK       TO TRUE                              
224600         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
224700         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
224800                                     TO DP013-MESSAGE-TEXT(1)             
224900         PERFORM DP013-0000-PROCESS-ABEND                                 
225000     END-IF.                                                              
225100                                                                          
225200 EJECT                                                                    
225300*----------------------------------------------------------------*        
225400*    RE-INITIALIZE ASC- FIELDS AFTER F3 RETURN                   *        
225500*----------------------------------------------------------------*        
225600 6500-REINIT-ASC-FLDS.                                                    
225700                                                                          
225800     MOVE ASC-LBL-NM       TO   IQ016-LBL-NM.                             
225900     MOVE ASC-GP-NBR       TO   IQ016-GP-NBR.                             
226000     MOVE ASC-GP-DESC      TO   IQ016-GP-DESC.                            
226100                                                                          
226200*----------------------------------------------------------------*        
226300*    ABEND PROCESSOR MODULE                                      *        
226400*----------------------------------------------------------------*        
226500                                                                          
226600     COPY DPPD013.                                                        
