       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKRP022.                                                 
       AUTHOR.        V. LEE YANG.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  01/25/2002.                                               
       DATE-COMPILED.                                                           
      ******************************************************************        
      *        INKRP022 - CREATE SALES BASE COMMA DELIMITED EXTRACTS   *        
      *                                                                *        
      * THIS PROGRAM EXTRACT THE CURRENT SALES BASE AND ITS ASSOCIATED *        
      * SHRINK BY DEPARTMENT.  THE CONTROL CARD FILE DETERMINED        *        
      * WHETHER A STORE SHOULD BE PROCESS OR NOT FOR EACH TYPE OF      *        
      * EXTRACTS.                                                      *        
      *                                                                *        
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
476571* 476571   6/23/2002   SALES BASE CUM PROBLEM - ABEND 4003                
476571*                      - ROD JANSSEN                                      
W26600* W26600  04-05-2004 ROD JANSSEN INV LEDGER STORE EXPANSION               
W20445* W20445  09-01-2004 ROD JANSSEN DETAILED INVNENTORY ADJUSTMENT           
W33304* W33304  08-07-2009 INFOSYS/    HILO CHANGES : CHANGED 1 BYTE            
W33304*                    JIMMY       SCHD-INV-TYPE-CD TO 2-BYTES.             
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SALES-BASE-CNTL-FILE                                          
               ASSIGN TO  INP01.                                                
                                                                                
           SELECT SALES-BASE-CUM-FILE                                           
               ASSIGN TO  INP02.                                                
                                                                                
           SELECT SALES-BASE-ALL-FILE                                           
               ASSIGN TO  OUT01.                                                
                                                                                
           SELECT SALES-BASE-PHY-INV-FILE                                       
               ASSIGN TO  OUT02.                                                
                                                                                
           SELECT SALES-BASE-NO-INV-FILE                                        
               ASSIGN TO  OUT03.                                                
                                                                                
           SELECT SALES-BASE-ALL-PHY-INV-FILE                                   
               ASSIGN TO  OUT04.                                                
                                                                                
           SELECT SALES-BASE-FUT-PHY-INV-FILE                                   
               ASSIGN TO  OUT05.                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  SALES-BASE-CNTL-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 CHARACTERS.                                         
       01  SALES-BASE-CNTL-REC             PIC X(50).                           
                                                                                
       FD  SALES-BASE-CUM-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 CHARACTERS.                                         
W20445 01  SALES-BASE-CUM-REC              PIC X(80).                           
                                                                                
       FD  SALES-BASE-ALL-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 CHARACTERS.                                         
       01  SALES-BASE-ALL-LINE             PIC X(160).                          
                                                                                
       FD  SALES-BASE-PHY-INV-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 CHARACTERS.                                         
       01  SALES-BASE-PHY-INV-LINE         PIC X(160).                          
                                                                                
       FD  SALES-BASE-NO-INV-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 CHARACTERS.                                         
       01  SALES-BASE-NO-INV-LINE          PIC X(160).                          
                                                                                
       FD  SALES-BASE-ALL-PHY-INV-FILE                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 CHARACTERS.                                         
       01  SALES-BASE-ALL-PHY-INV-LINE     PIC X(160).                          
                                                                                
       FD  SALES-BASE-FUT-PHY-INV-FILE                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 CHARACTERS.                                         
       01  SALES-BASE-FUT-PHY-INV-LINE     PIC X(160).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ****************************************************************          
      *  ERROR MESSAGE AREA FOR DB2                                             
      ****************************************************************          
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * RECORD LAYOUT FOR SALES BASE CONTROL FILE                               
      ******************************************************************        
           COPY INRD022.                                                        
                                                                                
      ******************************************************************        
      * RECORD LAYOUT FOR SALES BASE CUM FILE                                   
      ******************************************************************        
W20445     COPY INRD023.                                                        
                                                                                
       01  RP-HEADING-3-I.                                                      
           05  FILLER                      PIC X(40)       VALUE ',,,,'.        
           05  FILLER                      PIC X(40)       VALUE                
               ',,,     SALES BASE FOR ALL STORES     ,,'.                      
           05  FILLER                      PIC X(52)       VALUE ',,,,'.        
                                                                                
       01  RP-HEADING-3-II.                                                     
           05  FILLER                      PIC X(35)       VALUE ',,,,'.        
           05  FILLER                      PIC X(53)       VALUE                
               ',,,SALES BASE FOR STORES DOING A PHYSICAL INVENTORY,,'.         
           05  FILLER                      PIC X(47)       VALUE ',,,,'.        
                                                                                
       01  RP-HEADING-3-III.                                                    
           05  FILLER                      PIC X(31)       VALUE ',,,,'.        
           05  FILLER                      PIC X(36)       VALUE                
               ',,,SALES BASE FOR STORES DOING AN ES'.                          
           05  FILLER                      PIC X(35)       VALUE                
               'TIMATE INVENTORY OR NO INVENTORY ,,'.                           
           05  FILLER                      PIC X(31)       VALUE ',,,,'.        
                                                                                
       01  RP-HEADING-3-IV.                                                     
           05  FILLER                      PIC X(35)       VALUE ',,,,'.        
           05  FILLER                      PIC X(50)       VALUE                
               ',,,SALES BASE FOR PHYSICAL INVENTORYING STORES  ,,'.            
           05  FILLER                      PIC X(47)       VALUE ',,,,'.        
                                                                                
       01  RP-HEADING-3-V.                                                      
           05  FILLER                      PIC X(35)       VALUE ',,,,'.        
           05  FILLER                      PIC X(53)       VALUE                
               ',,,SALES BASE FOR FUTURE PHYSICAL INVENTORY STORES,,'.          
           05  FILLER                      PIC X(47)       VALUE ',,,,'.        
                                                                                
       01  RP-HEADING-4.                                                        
           05  FILLER                      PIC X(11)       VALUE                
               ',SALES BASE'.                                                   
           05  FILLER                      PIC X(11)       VALUE                
               ',SALES BASE'.                                                   
           05  FILLER                      PIC X(11)       VALUE                
               ',,--------,'.                                                   
           05  RP-MONTH-NAME               PIC X(09)       VALUE SPACE.         
           05  FILLER                      PIC X(09)       VALUE                
               ',--------'.                                                     
           05  FILLER                      PIC X(11)       VALUE                
               ',,---------'.                                                   
           05  FILLER                      PIC X(12)       VALUE                
               ',CUMMULATIVE'.                                                  
           05  FILLER                      PIC X(09)       VALUE                
               ',--------'.                                                     
           05  FILLER                      PIC X(11)       VALUE                
               ',,---------'.                                                   
           05  FILLER                      PIC X(16)       VALUE                
               ',NEXT SALES BASE'.                                              
           05  FILLER                      PIC X(09)       VALUE                
               ',--------'.                                                     
           05  FILLER                      PIC X(01)       VALUE ','.           
                                                                                
       01  H5-HEADING5.                                                         
           05  FILLER                      PIC X(08)       VALUE 'DEPT'.        
           05  FILLER                      PIC X(11)       VALUE                
               ',BEGIN DATE'.                                                   
           05  FILLER                      PIC X(11)       VALUE                
               ',END DATE'.                                                     
           05  FILLER                      PIC X(01)       VALUE ','.           
           05  FILLER                      PIC X(11)       VALUE                
               ',SALES'.                                                        
           05  FILLER                      PIC X(01)       VALUE ','.           
           05  FILLER                      PIC X(11)       VALUE                
               ',SHRINK'.                                                       
           05  FILLER                      PIC X(01)       VALUE ','.           
           05  FILLER                      PIC X(11)       VALUE                
               ',SALES'.                                                        
           05  FILLER                      PIC X(01)       VALUE ','.           
           05  FILLER                      PIC X(11)       VALUE                
               ',SHRINK'.                                                       
           05  FILLER                      PIC X(01)       VALUE ','.           
           05  FILLER                      PIC X(11)       VALUE                
               ',SALES'.                                                        
           05  FILLER                      PIC X(01)       VALUE ','.           
           05  FILLER                      PIC X(11)       VALUE                
               ',SHRINK'.                                                       
           05  FILLER                      PIC X(01)       VALUE ','.           
                                                                                
       01  RP-DETAIL-LINE.                                                      
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-DEPT-NBR                 PIC X(03)       VALUE SPACES.        
           05  FILLER                      PIC X(01)       VALUE ','.           
           05  RP-SLS-BSE-BEG-DTE          PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(01)       VALUE ','.           
           05  RP-SLS-BSE-END-DTE          PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE ',,'.          
W26600     05  RP-OPN-SLS-RTL-AMT          PIC -9(10).99   VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE ','.           
           05  FILLER                      PIC X(01)       VALUE ','.           
W26600     05  RP-OPN-SHNK-RTL-AMT         PIC -9(10).99   VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE ',,'.          
W26600     05  RP-CUM-SLS-RTL-AMT          PIC -9(10).99   VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE ',,'.          
W26600     05  RP-CUM-SHNK-RTL-AMT         PIC -9(10).99   VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE ',,'.          
W26600     05  RP-NXT-SLS-RTL-AMT          PIC -9(10).99   VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE ','.           
           05  FILLER                      PIC X(01)       VALUE ','.           
W26600     05  RP-NXT-SHNK-RTL-AMT         PIC -9(10).99   VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE ','.           
                                                                                
       01  RP-GRAND-TOTAL-LINE.                                                 
           05  FILLER                      PIC X(19)       VALUE                
               '** GRAND TOTAL **'.                                             
           05  FILLER                      PIC X(04)       VALUE ',,,,'.        
W26600     05  RP-GRAND-CUR-SALES          PIC -9(11).99   VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE ',,'.          
W26600     05  RP-GRAND-CUR-SHRINK         PIC -9(11).99   VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE ',,'.          
W26600     05  RP-GRAND-CUM-SALES          PIC -9(11).99   VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE ',,'.          
W26600     05  RP-GRAND-CUM-SHRINK         PIC -9(11).99   VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE ',,'.          
W26600     05  RP-GRAND-NEXT-SALES         PIC -9(11).99   VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE ',,'.          
W26600     05  RP-GRAND-NEXT-SHRINK        PIC -9(11).99   VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE ','.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES              PIC S9(3)       VALUE +3.            
24029      05  PC-FUTURE-DATE              PIC X(10)       VALUE                
               '9999-09-09'.                                                    
           05  PC-MAX-DATE                 PIC X(10)       VALUE                
               '9999-99-99'.                                                    
           05  PC-MIN-DATE                 PIC X(10)       VALUE                
               '0000-00-00'.                                                    
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-PROCESS-STR-SW           PIC X(01)       VALUE SPACES.        
               88  PS-PROCESS-STR                          VALUE 'Y'.           
               88  PS-NOT-PROCESS-STR                      VALUE 'N'.           
           05  PS-END-OF-CUM-FILE-SW       PIC X(01)       VALUE 'N'.           
               88  PS-END-OF-CUM-FILE                      VALUE 'Y'.           
           05  PS-END-OF-CNTL-FILE-SW      PIC X(01)       VALUE 'N'.           
               88  PS-END-OF-CNTL-FILE                     VALUE 'Y'.           
           05  PS-FIRST-TIME-THRU-SW       PIC X(01)       VALUE SPACES.        
               88  PS-FIRST-TIME-THRU                      VALUE 'Y'.           
               88  PS-NOT-FIRST-TIME-THRU                  VALUE 'N'.           
           05  PS-REPORT-IDX               PIC S9(4)       VALUE ZEROES         
                               COMP.                                            
               88  PS-OUT01                                VALUE +1.            
               88  PS-OUT02                                VALUE +2.            
               88  PS-OUT03                                VALUE +3.            
               88  PS-OUT04                                VALUE +4.            
               88  PS-OUT05                                VALUE +5.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SQL-SUB                 PIC S9(4)        VALUE ZEROES         
                               COMP SYNC.                                       
           05  PV-ABEND-CODE              PIC S9(04)       VALUE ZEROES         
                               COMP SYNC.                                       
           05  PV-MAX-STR-NBR             PIC S9(4)        VALUE ZEROES         
                               COMP.                                            
W26600     05  PV-STR-NBR-9               PIC 9(05)        VALUE ZEROES.        
           05  PV-OPN-FSCL-MN-BEG-DTE     PIC X(10)        VALUE SPACES.        
           05  PV-PARAGRAPH-NAME          PIC X(30)        VALUE SPACES.        
           05  PV-TOTALS.                                                       
W26600         10  PV-TL-OPN-SLS-RTL-AMT  PIC S9(11)V99    VALUE ZEROES         
                               COMP-3.                                          
W26600         10  PV-TL-OPN-SHNK-RTL-AMT PIC S9(11)V99    VALUE ZEROES         
                               COMP-3.                                          
W26600         10  PV-TL-CUM-SLS-RTL-AMT  PIC S9(11)V99    VALUE ZEROES         
                               COMP-3.                                          
W26600         10  PV-TL-CUM-SHNK-RTL-AMT PIC S9(11)V99    VALUE ZEROES         
                               COMP-3.                                          
W26600         10  PV-TL-NXT-SLS-RTL-AMT  PIC S9(11)V99    VALUE ZEROES         
                               COMP-3.                                          
W26600         10  PV-TL-NXT-SHNK-RTL-AMT PIC S9(11)V99    VALUE ZEROES         
                               COMP-3.                                          
           05  PV-GRAND-TOTALS OCCURS 5 TIMES.                                  
W26600         10  PV-GRD-OPN-SLS-RTL-AMT PIC S9(11)V99    VALUE ZEROES         
                               COMP-3.                                          
W26600         10  PV-GRD-OPN-SHNK-RTL-AMT                                      
W26600                                    PIC S9(11)V99    VALUE ZEROES         
                               COMP-3.                                          
W26600         10  PV-GRD-CUM-SLS-RTL-AMT PIC S9(11)V99    VALUE ZEROES         
                               COMP-3.                                          
               10  PV-GRD-CUM-SHNK-RTL-AMT                                      
W26600                                    PIC S9(11)V99    VALUE ZEROES         
                               COMP-3.                                          
W26600         10  PV-GRD-NXT-SLS-RTL-AMT PIC S9(11)V99    VALUE ZEROES         
                               COMP-3.                                          
               10  PV-GRD-NXT-SHNK-RTL-AMT                                      
W26600                                    PIC S9(11)V99    VALUE ZEROES         
                               COMP-3.                                          
                                                                                
      ******************************************************************        
      *    STORE TABLE                                                          
      ******************************************************************        
       01  TB-STORE-TABLE-AREA.                                                 
           05  TB-ST-SUB                   PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  TB-ST-LOADED                PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
W26600     05  TB-ST-MAX-ENTRIES           PIC S9(04)      VALUE +9999          
                               COMP.                                            
           05  TB-STORE-TABLE.                                                  
W26600         10  TB-STORE-TABLE-ENTRY OCCURS 9999 TIMES.                      
W26600             15  TB-ST-STR-NBR       PIC 9(05)       VALUE ZEROES.        
                   15  TB-SLS-BSE-BEG-DTE  PIC X(10)       VALUE SPACES.        
                   15  TB-SLS-BSE-END-DTE  PIC X(10)       VALUE SPACES.        
W33304*            15  TB-SCHD-INV-TYPE-CD PIC X(01)       VALUE SPACES.        
W33304             15  TB-SCHD-INV-TYPE-CD PIC X(02)       VALUE SPACES.        
                                                                                
      ******************************************************************        
      *    DEPARTMENT TABLE                                                     
      ******************************************************************        
       01  TB-DEPT-TABLE-AREA.                                                  
           05  TB-DT-SUB                   PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
476571     05  TB-DT-LOADED            PIC S9(13) COMP-3 VALUE ZEROES.          
W26600     05  TB-DT-MAX-ENTRIES           PIC S9(04)      VALUE +9999          
                               COMP.                                            
           05  TB-DEPT-TABLE.                                                   
W26600         10  TB-DEPT-TABLE-ENTRY OCCURS 9999 TIMES.                       
                   15  TB-DEPT-NBR         PIC X(03)       VALUE SPACES.        
W26600             15  TB-DT-STR-NBR       PIC 9(05)       VALUE ZEROES.        
W26600             15  TB-OPN-SLS-RTL-AMT  PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
W26600             15  TB-OPN-SHNK-RTL-AMT PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
W26600             15  TB-CUM-SLS-RTL-AMT  PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
W26600             15  TB-CUM-SHNK-RTL-AMT PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
W26600             15  TB-NXT-SLS-RTL-AMT  PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
W26600             15  TB-NXT-SHNK-RTL-AMT PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
                                                                                
      ******************************************************************        
      * MERCHANDISE LEDGER CONTROL TABLE                                        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * INVENTORY PARAMETERS TABLE                                              
      ******************************************************************        
W20445*    EXEC SQL                                                             
W20445*        INCLUDE TINVPAR                                                  
W20445*    END-EXEC.                                                            
W20445*                                                                         
      ******************************************************************        
      * MERCHANDISE LEDGER DATE ATTRIBUTE TABLE                                 
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 COMMUNICATION AREA                                                  
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * SECOND DB2 COMMUNICATION AREA                                           
      ******************************************************************        
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 7100-READ-SALES-BASE-CNTL-FILE.                              
           PERFORM 2000-LOAD-STORE-TABLE                                        
               UNTIL PS-END-OF-CNTL-FILE.                                       
                                                                                
           IF TB-ST-LOADED = ZEROES                                             
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PGM = INKRP022 **'                                   
               DISPLAY '** SALES BASE CONTROL FILE IS EMPTY.'                   
               MOVE +4003 TO PV-ABEND-CODE                                      
               CALL 'ILBOABN0'  USING PV-ABEND-CODE                             
           END-IF.                                                              
                                                                                
           PERFORM 7200-READ-SALES-BASE-CUM-FILE.                               
           PERFORM 2500-PROCESS-SALES-BASE                                      
               UNTIL PS-END-OF-CUM-FILE.                                        
                                                                                
           PERFORM                                                              
               VARYING PS-REPORT-IDX FROM 1 BY 1                                
               UNTIL PS-REPORT-IDX > 5                                          
               MOVE PV-GRD-OPN-SLS-RTL-AMT (PS-REPORT-IDX)                      
                   TO RP-GRAND-CUR-SALES                                        
               MOVE PV-GRD-OPN-SHNK-RTL-AMT(PS-REPORT-IDX)                      
                   TO RP-GRAND-CUR-SHRINK                                       
               MOVE PV-GRD-CUM-SLS-RTL-AMT (PS-REPORT-IDX)                      
                   TO RP-GRAND-CUM-SALES                                        
               MOVE PV-GRD-CUM-SHNK-RTL-AMT(PS-REPORT-IDX)                      
                   TO RP-GRAND-CUM-SHRINK                                       
               MOVE PV-GRD-NXT-SLS-RTL-AMT (PS-REPORT-IDX)                      
                   TO RP-GRAND-NEXT-SALES                                       
               MOVE PV-GRD-NXT-SHNK-RTL-AMT(PS-REPORT-IDX)                      
                   TO RP-GRAND-NEXT-SHRINK                                      
               EVALUATE TRUE                                                    
                   WHEN PS-OUT01                                                
                       WRITE SALES-BASE-ALL-LINE                                
                           FROM RP-GRAND-TOTAL-LINE                             
                   WHEN PS-OUT02                                                
                       WRITE SALES-BASE-PHY-INV-LINE                            
                           FROM RP-GRAND-TOTAL-LINE                             
                   WHEN PS-OUT03                                                
                       WRITE SALES-BASE-NO-INV-LINE                             
                           FROM RP-GRAND-TOTAL-LINE                             
                   WHEN PS-OUT04                                                
                       WRITE SALES-BASE-ALL-PHY-INV-LINE                        
                           FROM RP-GRAND-TOTAL-LINE                             
                   WHEN PS-OUT05                                                
                       WRITE SALES-BASE-FUT-PHY-INV-LINE                        
                           FROM RP-GRAND-TOTAL-LINE                             
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           CLOSE SALES-BASE-CNTL-FILE                                           
                 SALES-BASE-CUM-FILE                                            
                 SALES-BASE-ALL-FILE                                            
                 SALES-BASE-PHY-INV-FILE                                        
                 SALES-BASE-NO-INV-FILE                                         
                 SALES-BASE-ALL-PHY-INV-FILE                                    
                 SALES-BASE-FUT-PHY-INV-FILE.                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  SALES-BASE-CNTL-FILE                                     
                       SALES-BASE-CUM-FILE.                                     
           OPEN OUTPUT SALES-BASE-ALL-FILE                                      
                       SALES-BASE-PHY-INV-FILE                                  
                       SALES-BASE-NO-INV-FILE                                   
                       SALES-BASE-ALL-PHY-INV-FILE                              
                       SALES-BASE-FUT-PHY-INV-FILE.                             
                                                                                
           PERFORM 8000-GET-OPN-FSCL-MN-DTE.                                    
           PERFORM 8050-GET-OPN-FSCL-MN-INFO.                                   
           MOVE DTATTR-FSCL-MN-NM TO RP-MONTH-NAME.                             
                                                                                
           INITIALIZE TB-STORE-TABLE.                                           
           SET PS-FIRST-TIME-THRU     TO TRUE.                                  
                                                                                
      ******************************************************************        
      * LOAD THE STORES TABLE FROM THE CONTROL FILE.                            
      ******************************************************************        
       2000-LOAD-STORE-TABLE.                                                   
                                                                                
           MOVE IN022-STR-NBR TO TB-ST-SUB.                                     
           IF TB-ST-SUB > TB-ST-MAX-ENTRIES OR TB-ST-SUB <= 0                   
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PGM = INKRP022 **'                                   
               DISPLAY '** TABLE OVERFLOW ON TB-STORE-TABLE'                    
                       ' WITH INDEX OF ' TB-ST-SUB                              
               MOVE +4004 TO PV-ABEND-CODE                                      
               CALL 'ILBOABN0'  USING PV-ABEND-CODE                             
           ELSE                                                                 
               MOVE IN022-STR-NBR TO TB-ST-STR-NBR (TB-ST-SUB)                  
               MOVE IN022-SLS-BSE-BEG-DTE                                       
                   TO TB-SLS-BSE-BEG-DTE (TB-ST-SUB)                            
               MOVE IN022-SLS-BSE-END-DTE                                       
                   TO TB-SLS-BSE-END-DTE (TB-ST-SUB)                            
               MOVE IN022-SCHEDULED-INV-TYPE-CDE                                
                   TO TB-SCHD-INV-TYPE-CD (TB-ST-SUB)                           
               ADD +1 TO TB-ST-LOADED                                           
               IF TB-ST-SUB > PV-MAX-STR-NBR                                    
                   MOVE TB-ST-SUB TO PV-MAX-STR-NBR                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 7100-READ-SALES-BASE-CNTL-FILE.                              
                                                                                
      ******************************************************************        
      * LOAD THE SALES BASE INTO A WORKING-STORAGE TABLE.                       
      ******************************************************************        
       2100-LOAD-DEPT-TABLE.                                                    
                                                                                
           MOVE IN023-STR-NBR TO TB-DT-SUB                                      
                                 TB-ST-SUB.                                     
           IF TB-DT-SUB > TB-DT-MAX-ENTRIES OR TB-DT-SUB <= 0                   
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PGM = INKRP022 **'                                   
               DISPLAY '** TABLE OVERFLOW ON TB-DEPT-TABLE'                     
                       ' WITH INDEX OF ' TB-DT-SUB                              
               MOVE +4004 TO PV-ABEND-CODE                                      
               CALL 'ILBOABN0'  USING PV-ABEND-CODE                             
           ELSE                                                                 
             IF TB-ST-STR-NBR (TB-ST-SUB) = ZEROES                              
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PGM = INKRP022 **'                               
                   DISPLAY '** STORE DOES NOT EXIST IN CONTROL'                 
                           ' TABLE. STORE: ' TB-ST-SUB                          
                   MOVE +4004 TO PV-ABEND-CODE                                  
                   CALL 'ILBOABN0'  USING PV-ABEND-CODE                         
476571       ELSE                                                               
               MOVE IN023-DEPT-NBR                                              
                   TO TB-DEPT-NBR (TB-DT-SUB)                                   
               MOVE IN023-STR-NBR                                               
                   TO TB-DT-STR-NBR (TB-DT-SUB)                                 
               MOVE IN023-OPN-SLS-RTL-AMT                                       
                   TO TB-OPN-SLS-RTL-AMT (TB-DT-SUB)                            
               MOVE IN023-OPN-SHNK-RTL-AMT                                      
                   TO TB-OPN-SHNK-RTL-AMT(TB-DT-SUB)                            
               MOVE IN023-CUM-SLS-RTL-AMT                                       
                   TO TB-CUM-SLS-RTL-AMT(TB-DT-SUB)                             
               MOVE IN023-CUM-SHNK-RTL-AMT                                      
                   TO TB-CUM-SHNK-RTL-AMT (TB-DT-SUB)                           
               MOVE IN023-NXT-SLS-RTL-AMT                                       
                   TO TB-NXT-SLS-RTL-AMT (TB-DT-SUB)                            
               MOVE IN023-NXT-SHNK-RTL-AMT                                      
                   TO TB-NXT-SHNK-RTL-AMT(TB-DT-SUB)                            
               ADD +1 TO TB-DT-LOADED                                           
           END-IF.                                                              
                                                                                
           PERFORM 7200-READ-SALES-BASE-CUM-FILE.                               
                                                                                
      ******************************************************************        
      * PROCESS SALES BASE FILE                                                 
      ******************************************************************        
       2500-PROCESS-SALES-BASE.                                                 
                                                                                
           INITIALIZE TB-DEPT-TABLE.                                            
                                                                                
476571     MOVE ZEROS           TO TB-DT-LOADED.                                
           MOVE IN023-DEPT-NBR  TO RP-DEPT-NBR.                                 
           PERFORM 2100-LOAD-DEPT-TABLE                                         
               UNTIL PS-END-OF-CUM-FILE                                         
                   OR IN023-DEPT-NBR NOT = RP-DEPT-NBR.                         
                                                                                
           IF TB-DT-LOADED = ZEROES                                             
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PGM = INKRP022 **'                                   
               DISPLAY '** SALES BASE CUM FILE IS EMPTY.'                       
               MOVE +4003 TO PV-ABEND-CODE                                      
               CALL 'ILBOABN0'  USING PV-ABEND-CODE                             
           END-IF.                                                              
                                                                                
      *    PROCESS OUT01 - SALES BASE ALL STORES                                
           SET  PS-OUT01              TO TRUE.                                  
           PERFORM 3000-INITIALIZE-EXTRACTION.                                  
           SET PS-PROCESS-STR         TO TRUE.                                  
           PERFORM 3100-PRODUCE-DETAIL-LINES                                    
               VARYING TB-DT-SUB FROM 1 BY 1                                    
                 UNTIL TB-DT-SUB > PV-MAX-STR-NBR.                              
           PERFORM 4000-PRODUCE-TOTAL-LINE.                                     
                                                                                
      *    PROCESS OUT02 - SALES BASE PHYSICAL STORES, ALREADY INVENTORY        
           SET  PS-OUT02              TO TRUE.                                  
           PERFORM 3000-INITIALIZE-EXTRACTION.                                  
           PERFORM UNTIL TB-DT-SUB > PV-MAX-STR-NBR                             
W33304*        IF (TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'Y' OR                      
W33304*            TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'C')                        
W33304         IF (TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'PI' OR                     
W33304             TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'NS' OR                     
W33304             TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'CL')                       
                   AND TB-SLS-BSE-END-DTE(TB-DT-SUB) NOT = SPACES               
                   SET PS-PROCESS-STR TO TRUE                                   
                   PERFORM 3100-PRODUCE-DETAIL-LINES                            
               END-IF                                                           
               ADD +1                 TO TB-DT-SUB                              
           END-PERFORM.                                                         
           PERFORM 4000-PRODUCE-TOTAL-LINE.                                     
                                                                                
      *    PROCESS OUT03 - SALES BASE ESTIMATED/NON-PHYSICAL STORES             
           SET  PS-OUT03              TO TRUE.                                  
           PERFORM 3000-INITIALIZE-EXTRACTION.                                  
           PERFORM UNTIL TB-DT-SUB > PV-MAX-STR-NBR                             
W33304*        IF TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'N' OR                       
W33304*           TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = SPACE                        
W33304         IF (TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'EI' OR                     
W33304             TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = SPACE )                     
                   SET PS-PROCESS-STR TO TRUE                                   
                   PERFORM 3100-PRODUCE-DETAIL-LINES                            
               END-IF                                                           
               ADD +1 TO TB-DT-SUB                                              
           END-PERFORM.                                                         
           PERFORM 4000-PRODUCE-TOTAL-LINE.                                     
                                                                                
      *    PROCESS OUT04 - SALES BASE ALL PHYSICAL STORES                       
           SET  PS-OUT04              TO TRUE.                                  
           PERFORM 3000-INITIALIZE-EXTRACTION.                                  
           PERFORM UNTIL TB-DT-SUB > PV-MAX-STR-NBR                             
W33304*        IF ((TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'Y' OR                     
W33304*            TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'C')                        
W33304         IF ((TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'PI' OR                    
W33304              TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'NS' OR                    
W33304              TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'CL')                      
24029              AND TB-SLS-BSE-END-DTE(TB-DT-SUB) NOT =                      
24029                                                  PC-FUTURE-DATE)          
                   SET PS-PROCESS-STR TO TRUE                                   
                   PERFORM 3100-PRODUCE-DETAIL-LINES                            
               END-IF                                                           
               ADD +1                 TO TB-DT-SUB                              
           END-PERFORM.                                                         
           PERFORM 4000-PRODUCE-TOTAL-LINE.                                     
                                                                                
      *    PROCESS OUT05 - SALES BASE ALL PHYSICAL STORES, YET TO BE INV        
           SET  PS-OUT05              TO TRUE.                                  
           PERFORM 3000-INITIALIZE-EXTRACTION.                                  
           PERFORM UNTIL TB-DT-SUB > PV-MAX-STR-NBR                             
W33304*        IF ((TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'Y' OR                     
W33304*            TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'C')                        
W33304         IF ((TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'PI' OR                    
W33304              TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'NS' OR                    
W33304              TB-SCHD-INV-TYPE-CD(TB-DT-SUB) = 'CL')                      
24029 **           AND TB-SLS-BSE-END-DTE(TB-DT-SUB) = SPACES                   
24029              AND TB-SLS-BSE-END-DTE(TB-DT-SUB) = PC-FUTURE-DATE)          
                   SET PS-PROCESS-STR TO TRUE                                   
                   PERFORM 3100-PRODUCE-DETAIL-LINES                            
               END-IF                                                           
               ADD +1                 TO TB-DT-SUB                              
           END-PERFORM.                                                         
           PERFORM 4000-PRODUCE-TOTAL-LINE.                                     
                                                                                
           IF PS-FIRST-TIME-THRU                                                
               SET PS-NOT-FIRST-TIME-THRU TO TRUE                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * INITIALIZE SALES BASE EXTRACT PROCESS                                   
      ******************************************************************        
       3000-INITIALIZE-EXTRACTION.                                              
                                                                                
           MOVE PC-MAX-DATE TO RP-SLS-BSE-BEG-DTE.                              
           MOVE PC-MIN-DATE TO RP-SLS-BSE-END-DTE.                              
                                                                                
           INITIALIZE  PV-TOTALS.                                               
           MOVE +1                    TO TB-DT-SUB.                             
                                                                                
           SET PS-NOT-PROCESS-STR     TO TRUE.                                  
                                                                                
      ******************************************************************        
      * CREATE THE SALES BASE REPORT DETAIL LINES, USING THE WS STORE           
      * TABLE AS A DRIVER.                                                      
      *                                                                         
      * THERE IS A SWITCH THAT CONTROLS WHETHER THE STORE GETS PRINTED.         
      * THIS PARAGRAPH IS EXECUTED FOR THREE REPORTS, AND ALL REPORTS           
      * GET PRODUCED.                                                           
      *                                                                         
      ******************************************************************        
       3100-PRODUCE-DETAIL-LINES.                                               
                                                                                
           IF TB-DT-STR-NBR (TB-DT-SUB) NOT = ZERO                              
               IF PS-PROCESS-STR                                                
                   IF TB-SLS-BSE-BEG-DTE(TB-DT-SUB) < RP-SLS-BSE-BEG-DTE        
                       AND TB-SLS-BSE-BEG-DTE(TB-DT-SUB) NOT = SPACES           
                       MOVE TB-SLS-BSE-BEG-DTE  (TB-DT-SUB)                     
                           TO RP-SLS-BSE-BEG-DTE                                
                   END-IF                                                       
                   IF TB-SLS-BSE-END-DTE(TB-DT-SUB) > RP-SLS-BSE-END-DTE        
                       AND TB-SLS-BSE-END-DTE(TB-DT-SUB) NOT = SPACES           
                       MOVE TB-SLS-BSE-END-DTE  (TB-DT-SUB)                     
                           TO RP-SLS-BSE-END-DTE                                
                   END-IF                                                       
                   PERFORM 3200-ACCUM-TOTALS                                    
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * ACCUMULATE THE STORE TOTALS FOR A GRAND TOTAL AT THE END OF THE         
      * REPORT.                                                                 
      ******************************************************************        
       3200-ACCUM-TOTALS.                                                       
                                                                                
           COMPUTE PV-TL-OPN-SLS-RTL-AMT                                        
                 = PV-TL-OPN-SLS-RTL-AMT                                        
                 + TB-OPN-SLS-RTL-AMT (TB-DT-SUB).                              
           COMPUTE PV-TL-OPN-SHNK-RTL-AMT                                       
                 = PV-TL-OPN-SHNK-RTL-AMT                                       
                 + TB-OPN-SHNK-RTL-AMT (TB-DT-SUB).                             
           COMPUTE PV-TL-CUM-SLS-RTL-AMT                                        
                 = PV-TL-CUM-SLS-RTL-AMT                                        
                 + TB-CUM-SLS-RTL-AMT (TB-DT-SUB).                              
           COMPUTE PV-TL-CUM-SHNK-RTL-AMT                                       
                 = PV-TL-CUM-SHNK-RTL-AMT                                       
                 + TB-CUM-SHNK-RTL-AMT (TB-DT-SUB).                             
           COMPUTE PV-TL-NXT-SLS-RTL-AMT                                        
                 = PV-TL-NXT-SLS-RTL-AMT                                        
                 + TB-NXT-SLS-RTL-AMT (TB-DT-SUB).                              
           COMPUTE PV-TL-NXT-SHNK-RTL-AMT                                       
                 = PV-TL-NXT-SHNK-RTL-AMT                                       
                 + TB-NXT-SHNK-RTL-AMT (TB-DT-SUB).                             
                                                                                
      ******************************************************************        
      * BUILD AND WRITE THE GRAND TOTAL LINE.                                   
      ******************************************************************        
       4000-PRODUCE-TOTAL-LINE.                                                 
                                                                                
           IF PS-PROCESS-STR                                                    
               IF PS-FIRST-TIME-THRU                                            
                   PERFORM Z200-PRODUCE-HEADINGS                                
               END-IF                                                           
               ADD PV-TL-OPN-SLS-RTL-AMT                                        
                   TO PV-GRD-OPN-SLS-RTL-AMT (PS-REPORT-IDX)                    
               ADD PV-TL-OPN-SHNK-RTL-AMT                                       
                   TO PV-GRD-OPN-SHNK-RTL-AMT (PS-REPORT-IDX)                   
               ADD PV-TL-CUM-SLS-RTL-AMT                                        
                   TO PV-GRD-CUM-SLS-RTL-AMT (PS-REPORT-IDX)                    
               ADD PV-TL-CUM-SHNK-RTL-AMT                                       
                   TO PV-GRD-CUM-SHNK-RTL-AMT (PS-REPORT-IDX)                   
               ADD PV-TL-NXT-SLS-RTL-AMT                                        
                   TO PV-GRD-NXT-SLS-RTL-AMT (PS-REPORT-IDX)                    
               ADD PV-TL-NXT-SHNK-RTL-AMT                                       
                   TO PV-GRD-NXT-SHNK-RTL-AMT (PS-REPORT-IDX)                   
                                                                                
               IF RP-SLS-BSE-BEG-DTE = PC-MAX-DATE                              
                   MOVE SPACES TO RP-SLS-BSE-BEG-DTE                            
               END-IF                                                           
               IF RP-SLS-BSE-END-DTE = PC-MIN-DATE                              
                   MOVE SPACES TO RP-SLS-BSE-END-DTE                            
               END-IF                                                           
               MOVE PV-TL-OPN-SLS-RTL-AMT  TO RP-OPN-SLS-RTL-AMT                
               MOVE PV-TL-OPN-SHNK-RTL-AMT TO RP-OPN-SHNK-RTL-AMT               
               MOVE PV-TL-CUM-SLS-RTL-AMT  TO RP-CUM-SLS-RTL-AMT                
               MOVE PV-TL-CUM-SHNK-RTL-AMT TO RP-CUM-SHNK-RTL-AMT               
               MOVE PV-TL-NXT-SLS-RTL-AMT  TO RP-NXT-SLS-RTL-AMT                
               MOVE PV-TL-NXT-SHNK-RTL-AMT TO RP-NXT-SHNK-RTL-AMT               
                                                                                
               EVALUATE TRUE                                                    
                   WHEN PS-OUT01                                                
                       WRITE SALES-BASE-ALL-LINE                                
                           FROM RP-DETAIL-LINE                                  
                   WHEN PS-OUT02                                                
                       WRITE SALES-BASE-PHY-INV-LINE                            
                           FROM RP-DETAIL-LINE                                  
                   WHEN PS-OUT03                                                
                       WRITE SALES-BASE-NO-INV-LINE                             
                           FROM RP-DETAIL-LINE                                  
                   WHEN PS-OUT04                                                
                       WRITE SALES-BASE-ALL-PHY-INV-LINE                        
                           FROM RP-DETAIL-LINE                                  
                   WHEN PS-OUT05                                                
                       WRITE SALES-BASE-FUT-PHY-INV-LINE                        
                           FROM RP-DETAIL-LINE                                  
               END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
       7100-READ-SALES-BASE-CNTL-FILE.                                          
                                                                                
           READ SALES-BASE-CNTL-FILE INTO IN022-SALES-BASE-CNTL-REC             
             AT END SET PS-END-OF-CNTL-FILE TO TRUE                             
           END-READ.                                                            
                                                                                
                                                                                
       7200-READ-SALES-BASE-CUM-FILE.                                           
                                                                                
           READ SALES-BASE-CUM-FILE INTO IN023-SALES-BASE-CUM-REC               
             AT END SET PS-END-OF-CUM-FILE TO TRUE                              
           END-READ.                                                            
                                                                                
                                                                                
       8000-GET-OPN-FSCL-MN-DTE.                                                
                                                                                
           EXEC SQL                                                             
               SELECT   OPN_FSCL_MN_DTE                                         
               INTO     :MLCNTL-OPN-FSCL-MN-DTE                                 
               FROM     TMLCNTL                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLWARN NOT = SPACES                                        
               WHEN SQLCODE NOT = ZERO                                          
                   DISPLAY '** READ FOR TMLCNTL'                                
                   MOVE '8000-GET-OPN-FSCL-MN-DTE' TO                           
                         PV-PARAGRAPH-NAME                                      
                   PERFORM Z900-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
*********  DISPLAY '** MLCNTL-OPN-FSCL-MN-DTE:' MLCNTL-OPN-FSCL-MN-DTE.         
                                                                                
                                                                                
       8050-GET-OPN-FSCL-MN-INFO.                                               
                                                                                
           EXEC SQL                                                             
               SELECT   FSCL_MN_BEG_DTE                                         
                       ,FSCL_MN_NM                                              
               INTO    :PV-OPN-FSCL-MN-BEG-DTE                                  
                      ,:DTATTR-FSCL-MN-NM                                       
               FROM     TDTATTR                                                 
               WHERE  KY_DTE = :MLCNTL-OPN-FSCL-MN-DTE                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLWARN NOT = SPACES                                        
               WHEN SQLCODE NOT = ZERO                                          
                   DISPLAY '** READ FOR TDTATTR'                                
                   MOVE '8050-GET-OPN-FSCL-MN-INFO    ' TO                      
                         PV-PARAGRAPH-NAME                                      
                   PERFORM Z900-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       Z200-PRODUCE-HEADINGS.                                                   
                                                                                
           EVALUATE TRUE                                                        
               WHEN PS-OUT01                                                    
                   WRITE SALES-BASE-ALL-LINE                                    
                       FROM RP-HEADING-3-I                                      
                   WRITE SALES-BASE-ALL-LINE                                    
                       FROM RP-HEADING-4                                        
                   WRITE SALES-BASE-ALL-LINE                                    
                       FROM H5-HEADING5                                         
               WHEN PS-OUT02                                                    
                   WRITE SALES-BASE-PHY-INV-LINE                                
                       FROM RP-HEADING-3-II                                     
                   WRITE SALES-BASE-PHY-INV-LINE                                
                       FROM RP-HEADING-4                                        
                   WRITE SALES-BASE-PHY-INV-LINE                                
                       FROM H5-HEADING5                                         
               WHEN PS-OUT03                                                    
                   WRITE SALES-BASE-NO-INV-LINE                                 
                       FROM RP-HEADING-3-III                                    
                   WRITE SALES-BASE-NO-INV-LINE                                 
                       FROM RP-HEADING-4                                        
                   WRITE SALES-BASE-NO-INV-LINE                                 
                       FROM H5-HEADING5                                         
               WHEN PS-OUT04                                                    
                   WRITE SALES-BASE-ALL-PHY-INV-LINE                            
                       FROM RP-HEADING-3-IV                                     
                   WRITE SALES-BASE-ALL-PHY-INV-LINE                            
                       FROM RP-HEADING-4                                        
                   WRITE SALES-BASE-ALL-PHY-INV-LINE                            
                       FROM H5-HEADING5                                         
               WHEN PS-OUT05                                                    
                   WRITE SALES-BASE-FUT-PHY-INV-LINE                            
                       FROM RP-HEADING-3-V                                      
                   WRITE SALES-BASE-FUT-PHY-INV-LINE                            
                       FROM RP-HEADING-4                                        
                   WRITE SALES-BASE-FUT-PHY-INV-LINE                            
                       FROM H5-HEADING5                                         
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * Z900-SQL-ERROR - PERFORMS THE ABEND FOR THE PROGRAM IN THE     *        
      *  EVENT THAT AN SQL ERROR CODE OCCURS.                          *        
      ******************************************************************        
       Z900-SQL-ERROR.                                                          
                                                                                
           MOVE +4013 TO PV-ABEND-CODE.                                         
           MOVE SQLCODE TO SQLCODE2.                                            
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = INKRP022'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
           DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
           DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
