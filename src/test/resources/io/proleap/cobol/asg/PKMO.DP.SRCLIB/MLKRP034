       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKRP034.                                                 
       AUTHOR.        PAUL JORGENSEN.                                           
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  AUGUST 2006.                                              
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                         
      * MLKRP034 - DESCRIPTION.............                                     
      *                                                                         
W30273* THIS PROGRAM WILL TAKE THE DISCREPANCY FILE OF ML'S FRONT END           
      * OF PRICE CHANGES COMPARED TO PERM PRICE AND PRODUCE A REPORT            
      * OF THE DISCREPANCIES.                                                   
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      *                                                                *        
      *                                                                *        
      ******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                00200000
      *                                                                 00210000
       INPUT-OUTPUT SECTION.                                            00220000
       FILE-CONTROL.                                                    00230000
                                                                        00240000
           SELECT DISCREPANCY-INPUT-FILE                                00250000
               ASSIGN TO INP01.                                         00260000
                                                                        00270000
           SELECT DISCREPANCY-REPORT-FILE                               00310000
               ASSIGN TO RPT01.                                         00320000
                                                                        00330000
      ******************************************************************00340000
       DATA DIVISION.                                                   00350000
      ******************************************************************00360000
                                                                        00370000
       FILE SECTION.                                                    00380000
                                                                        00390000
       FD  DISCREPANCY-INPUT-FILE                                       00400000
           LABEL RECORDS ARE STANDARD                                   00410000
           RECORDING MODE IS F                                          00420000
           BLOCK CONTAINS 0 RECORDS.                                    00430000
       01  DISCREPANCY-INPUT-REC       PIC X(198).                      00440000
                                                                        00510000
       FD  DISCREPANCY-REPORT-FILE                                      00520000
           LABEL RECORDS ARE OMITTED                                    00530000
           RECORDING MODE IS F                                          00540000
           BLOCK CONTAINS 0 RECORDS.                                    00550000
       01  DISCREPANCY-REPORT-REC      PIC X(132).                      00560001
                                                                        00570000
      *                                                                 00580000
      ******************************************************************00590000
       WORKING-STORAGE SECTION.                                         00600000
      ******************************************************************00610000
                                                                        00620000
       01  PS-PROGRAM-SWITCHES.                                         00630000
           05  PS-MORE-DESCREP-RECS-SW PIC X       VALUE 'Y'.           00640014
               88  PS-MORE-DESCREP-RECS            VALUE 'Y'.           00650000
               88  PS-NO-MORE-DESCREP-RECS         VALUE 'N'.           00660000
           05  PS-TDTATTR-FOUND        PIC X       VALUE 'N'.           00670001
               88  PS-TDTATTR-IS-FOUND             VALUE 'Y'.           00680001
               88  PS-TDTATTR-IS-NOT-FOUND         VALUE 'N'.           00690001
                                                                        00700000
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.              00710000
           05  PA-DESCREP-RECORDS-READ PIC S9(13)  VALUE ZERO.          00720000
                                                                        00730000
       01  CD-COUNTER-DISPLAY.                                          00740000
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           00750000
                                                                        00760000
       01  PC-PROGRAM-CONSTANTS.                                        00770000
           05  PC-MAX-LINE-COUNT       PIC S9(3) COMP-3 VALUE +60.      00780023
           05  PC-C                    PIC X     VALUE 'C'.             00790033
           05  PC-D                    PIC X     VALUE 'D'.             00791033
           05  PC-N                    PIC X     VALUE 'N'.             00792033
           05  PC-BLANK-LINE           PIC X(132) VALUE SPACES.         02650000
                                                                        00800000
       01  PV-PROGRAM-VARIABLES.                                        00810000
           05  PV-LINE-COUNT           PIC S9(3) COMP-3 VALUE +0.       00820000
           05  PV-PAGE-COUNT           PIC S9(7) COMP-3 VALUE +0.       00830000
                                                                        00840000
           05  PV-DATE-YYMMDD.                                          00850000
               10  PV-DATE-YYMMDD-YY   PIC XX.                          00860000
               10  PV-DATE-YYMMDD-MM   PIC XX.                          00870000
               10  PV-DATE-YYMMDD-DD   PIC XX.                          00880000
           05  PV-TIME-HHMMSS.                                          00890000
               10  PV-TIME-HHMMSS-HH   PIC XX.                          00900000
               10  PV-TIME-HHMMSS-MM   PIC XX.                          00910000
               10  PV-TIME-HHMMSS-SS   PIC XX.                          00920000
                                                                        00930000
           05  PV-SPLIT-DATE.                                           00940000
               10  PV-SPLIT-DATE-CCYY  PIC X(4).                        00950000
               10  PV-HYPHEN-1         PIC X.                           00960000
               10  PV-SPLIT-DATE-MM    PIC X(2).                        00970000
               10  PV-HYPHEN-2         PIC X.                           00980000
               10  PV-SPLIT-DATE-DD    PIC X(2).                        00990000
           05  PV-FRMT-DATE.                                            01000000
               10  PV-FRMT-DATE-CCYY   PIC X(4).                        01050000
               10  PV-HYPHEN-1         PIC X.                           01040000
               10  PV-FRMT-DATE-MM-DD.                                          
                   15  PV-FRMT-DATE-MM PIC X(2).                        01010000
                   15  PV-HYPHEN-2     PIC X.                           01020000
                   15  PV-FRMT-DATE-DD PIC X(2).                        01030000
                                                                        01060001
           05  PV-REPORT-TOTALS.                                        01090001
               10  PV-RPT-BYR-RTL-AMT  PIC S9(12)V99.                   01100013
               10  PV-RPT-BYR-COST-AMT PIC S9(12)V99.                   01110013
                                                                        01120001
               10  PV-RPT-DEPT-RTL-AMT PIC S9(12)V99.                   01130013
               10  PV-RPT-DEPT-COST-AMT                                 01140001
                                       PIC S9(12)V99.                   01150013
                                                                        01160001
               10  PV-RPT-MAJ-CL-RTL-AMT                                01170013
                                       PIC S9(12)V99.                   01180013
               10  PV-RPT-MAJ-CL-COST-AMT                               01190013
                                       PIC S9(12)V99.                   01200013
                                                                        01210013
                                                                        01580000
       01 ABEND-AREAS.                                                  01610000
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           01620000
                                                   COMP SYNC.           01630000
               88  AC-BAD-DATA                     VALUE +4003.         01640000
               88  AC-DB2-ERROR                    VALUE +4013.         01650000
           05  AA-ABEND-LIT            PIC X(40)   VALUE                01660000
                 '*****       ABEND'.                                   01670000
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                01680000
                   '*****   PROGRAM: MLKRP034'.                         01690000
           05  AA-PARAGRAPH-LIT.                                        01700000
               10  FILLER              PIC X(17)   VALUE                01710000
                   '***** PARAGRAPH: '.                                 01720000
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        01730000
           05  AA-MESSAGE-LINE.                                         01740000
               10  FILLER              PIC X(06)   VALUE '*****'.       01750000
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        01760000
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                01770000
                   '*****    DB2 ERROR'.                                01780000
           05  AA-DB2-OPERATION-LIT.                                    01790000
               10  FILLER              PIC X(17)   VALUE                01800000
                   '***** OPERATION: '.                                 01810000
               10  AA-DB2-OPERATION.                                    01820000
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        01830000
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        01840000
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        01850000
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        01860000
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        01870000
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        01880000
                                                                        01890000
      *** INPUT FILE                                                    01980000
           COPY MLRD034.                                                01990023
                                                                        02000000
022700******************************************************************        
022800*  WORK FIELDS FOR PRINTING A 132 CHARACTER REPORT.                       
022900******************************************************************        
023000     COPY DPWS132O.                                                       
                                                                        02080000
      ******************************************************************02090000
      ***  REPORT TITLE LINES                                           02100000
      ******************************************************************02110000
       01  HD-REPORT-TITLE-2.                                           02120000
           05  FILLER                  PIC X       VALUE SPACE.         02130000
           05  FILLER                  PIC X(48)   VALUE SPACES.        02140001
           05  FILLER                  PIC X(39)                        02150000
               VALUE ' ML/PERM PRICE CHANGE DISCREPANCIES   '.          02160000
           05  FILLER                  PIC X(62)   VALUE SPACES.        02170001
                                                                        02180000
                                                                        02280000
      ******************************************************************02290000
      ***  REPORT COLUMN HEADING LINES                                  02300000
      ******************************************************************02310000
       01  HD-COLUMN-HEADING-1.                                         02320000
           05  FILLER                  PIC X(10)   VALUE '  PRCHG ID'.  02330014
           05  FILLER                  PIC XXX     VALUE SPACE.         02360014
           05  FILLER                  PIC X(09)   VALUE 'PRCHG EFF'.   02370014
           05  FILLER                  PIC XXX     VALUE SPACE.         02360014
           05  FILLER                  PIC X(10)   VALUE 'PRCHG TYPE'.  02370014
           05  FILLER                  PIC XXX     VALUE SPACE.         02340014
           05  FILLER                  PIC X(07)   VALUE 'SKU NBR'.     02350014
           05  FILLER                  PIC X(04)   VALUE SPACE.         02380014
           05  FILLER                  PIC X(08)   VALUE 'PERM QTY'.    02390014
           05  FILLER                  PIC X(04)   VALUE SPACE.         02400014
           05  FILLER                  PIC X(11)   VALUE 'PERM RETAIL'. 02410001
           05  FILLER                  PIC X(07)   VALUE SPACE.         02420014
           05  FILLER                  PIC X(06)   VALUE 'ML QTY'.      02430001
           05  FILLER                  PIC X(06)   VALUE SPACE.         02440001
           05  FILLER                  PIC X(09)   VALUE 'ML RETAIL'.   02430001
           05  FILLER                  PIC X(05)   VALUE SPACE.         02380014
           05  FILLER                  PIC X(08)   VALUE 'DIFF QTY'.    02390014
           05  FILLER                  PIC X(04)   VALUE SPACE.         02400014
           05  FILLER                  PIC X(11)   VALUE 'DIFF RETAIL'. 02410001
           05  FILLER                  PIC X(04)   VALUE SPACE.         02440001
       01  HD-COLUMN-HEADING-2.                                         02320000
           05  FILLER                  PIC X(13)   VALUE SPACE.         02360014
           05  FILLER                  PIC X(09)   VALUE '  DATE   '.   02370014
           05  FILLER                  PIC XXX     VALUE SPACE.         02360014
           05  FILLER                  PIC X(10)   VALUE '   CODE   '.  02370014
           05  FILLER                  PIC X(97)   VALUE SPACE.         02340014
                                                                        02450000
      ******************************************************************02460000
      ***  REPORT DETAIL LINES                                          02470000
      ******************************************************************02480000
       01  DL-LINE-1.                                                   02490000
           05  FILLER                  PIC X       VALUE SPACE.         02510014
           05  DL-PRCHG-ID             PIC ZZZZZZZZ9.                   02500014
           05  FILLER                  PIC X(05)   VALUE SPACE.         02510014
           05  DL-PRCHG-EFF-MM-DD      PIC X(05).                       02520014
           05  FILLER                  PIC X(08)   VALUE SPACE.         02530014
           05  DL-PRCHG-TYP-CDE        PIC X(03).                       02520014
           05  FILLER                  PIC X(06)   VALUE SPACE.         02530014
           05  DL-SKU-NBR              PIC 9(08).                       02540014
           05  FILLER                  PIC X       VALUE SPACE.         02550014
           05  DL-PERM-QTY             PIC ZZZ,ZZZ,ZZ9-.                02560014
           05  FILLER                  PIC X       VALUE SPACE.         02570014
           05  DL-PERM-RETAIL          PIC ZZ,ZZZ,ZZ9.99-.              02580013
           05  FILLER                  PIC X       VALUE SPACE.         02550014
           05  DL-ML-QTY               PIC ZZZ,ZZZ,ZZ9-.                02560014
           05  FILLER                  PIC X       VALUE SPACE.         02570014
           05  DL-ML-RETAIL            PIC ZZ,ZZZ,ZZ9.99-.              02580013
           05  FILLER                  PIC X       VALUE SPACE.         02550014
           05  DL-DIFF-QTY             PIC ZZZ,ZZZ,ZZ9-.                02560014
           05  FILLER                  PIC X       VALUE SPACE.         02570014
           05  DL-DIFF-RETAIL          PIC ZZ,ZZZ,ZZ9.99-.              02580013
           05  FILLER                  PIC X(03)   VALUE SPACE.         02590014
                                                                        02610000
      ******************************************************************02620000
      ***  REPORT END OF REPORT LINE                                    02630000
      ******************************************************************02640000
       01  ER-END-OF-REPORT-LINE.                                       02650000
           05  FILLER                  PIC X(21)   VALUE                02660000
                                          '*** END OF REPORT ***'.      02670000
           05  FILLER                  PIC X(112)  VALUE SPACES.        02660000
      ******************************************************************02620000
      ***  REPORT NO DATA LINE                                          02630000
      ******************************************************************02640000
       01  ER-REPORT-NO-DATA-LINE.                                      02650000
           05  FILLER                  PIC X(34)   VALUE                02660000
                                  '*** NO DATA ON WHICH TO REPORT ***'. 02670000
                                                                        02680000
                                                                        02700000
      ******************************************************************02710005
      *  DB2 COMMUNICATION AREA                                         02720005
      ******************************************************************02730005
           EXEC SQL                                                     02740005
                INCLUDE SQLCA                                           02750005
           END-EXEC.                                                    02760005
                                                                        02770005
      ******************************************************************02780002
      *  DB2 ERROR MESSAGES FORMAT AREA.                                02790002
      ******************************************************************02800002
           COPY DPWS004.                                                02810002
                                                                        02820002
      ******************************************************************02830005
      *  DB2 DTATTR TABLE LAYOUT                                        02840005
      ******************************************************************02850005
           EXEC SQL                                                     02860005
                INCLUDE TDTATTR                                         02870005
           END-EXEC.                                                    02880005
                                                                        02890005
                                                                        02910002
      ******************************************************************02920000
       PROCEDURE DIVISION.                                              02930000
      ******************************************************************02940000
                                                                        02950000
      ******************************************************************02960000
       0000-MAINLINE-MLKRP034.                                          02970029
      ******************************************************************02980000
      *                                                                 02990000
           PERFORM 1000-INITIALIZATION.                                 03000029
                                                                        03010000
           PERFORM 2000-MAIN-PROCESS.                                   03020029
                                                                        03030000
           PERFORM 8000-EOJ.                                            03040029
                                                                        03050000
           STOP RUN.                                                    03060000
                                                                        03070000
                                                                        03090000
      ******************************************************************03100000
       1000-INITIALIZATION.                                             03110029
      ******************************************************************03120000
      *                                                                 03130000
           OPEN INPUT  DISCREPANCY-INPUT-FILE                           03140000
                OUTPUT DISCREPANCY-REPORT-FILE.                         03160000
                                                                        03170000
      *    PERFORM 4000-READ-CONTROL-FILE.                              03180029
                                                                        03190000
      *    DISPLAY ' '.                                                 03200000
      *    DISPLAY '*** CONTROL CARD VALUES FOR MLKRP034 FOLLOW ***'.   03210001
      *    DISPLAY CC-CONTROL-CARD.                                     03220001
                                                                        03230000
      *    PERFORM 1100-EDIT-CONTROL-CARD.                              03240029
                                                                        03250001
           INITIALIZE PV-PROGRAM-VARIABLES.                             03260006
                                                                        03270006
           PERFORM 2100-BUILD-REPORT-TITLE.                             03800029
                                                                        03810013
           PERFORM 6000-WRITE-REPORT-TITLE.                             03820029
                                                                        03340000
      ******************************************************************03350001
      *1100-EDIT-CONTROL-CARD.                                          03360029
      ******************************************************************03370001
      *                                                                 03380001
      *    MOVE PC-N                   TO PS-TDTATTR-FOUND.             03390001
      *    MOVE CC-EXTR-START-CCYY-MM-DD                                03400001
      *                                TO PV-KY-DTE.                    03410001
                                                                        03420001
      *    PERFORM 7000-SELECT-TDTATTR-FOR-EDIT.                        03430029
                                                                        03440001
      *    IF PS-TDTATTR-IS-NOT-FOUND                                   03450001
      *        MOVE '1100-EDIT-CONTROL-CARD'                            03460029
      *                                TO AA-PARAGRAPH-NAME             03470001
      *        MOVE 'CTL CARD START DATE IS INVALID'                    03480001
      *                                TO AA-MESSAGE                    03490001
      *        MOVE +4002              TO ABEND-CODE                    03500001
      *        PERFORM 9900-COMMON-ABEND-ROUTINE                        03510029
      *    END-IF.                                                      03520001
                                                                        03530001
                                                                        03550001
      ******************************************************************03560000
       2000-MAIN-PROCESS.                                               03570029
      ******************************************************************03580000
      *                                                                 03590000
           PERFORM 4100-READ-DISCREPANCY-FILE.                          03600029
                                                                        03610000
           IF PS-MORE-DESCREP-RECS                                      03620001
               PERFORM                                                  03840000
                 UNTIL PS-NO-MORE-DESCREP-RECS                          03850000
                   PERFORM 3000-CREATE-REPORT                           03860029
                   PERFORM 4100-READ-DISCREPANCY-FILE                   03870029
               END-PERFORM                                              03880000
               WRITE DISCREPANCY-REPORT-REC FROM ER-END-OF-REPORT-LINE          
                   AFTER ADVANCING 1 LINES                                      
           ELSE                                                         03630001
               WRITE DISCREPANCY-REPORT-REC FROM ER-REPORT-NO-DATA-LINE 03640001
                   AFTER ADVANCING 2 LINES                              03650015
           END-IF.                                                      03780001
                                                                        03790001
                                                                        03910000
      ******************************************************************03920013
       2100-BUILD-REPORT-TITLE.                                         03930029
      ******************************************************************03940013
      *                                                                 03950013
           ACCEPT PV-DATE-YYMMDD FROM DATE.                             03960013
           MOVE PV-DATE-YYMMDD-YY      TO DP132O-RUN-YEAR.              03970013
           MOVE PV-DATE-YYMMDD-MM      TO DP132O-RUN-MONTH.             03980013
           MOVE PV-DATE-YYMMDD-DD      TO DP132O-RUN-DAY.               03990013
           ACCEPT PV-TIME-HHMMSS FROM TIME.                             04000013
           MOVE PV-TIME-HHMMSS-HH      TO DP132O-RUN-HOUR.              04010013
           MOVE PV-TIME-HHMMSS-MM      TO DP132O-RUN-MINUTE.            04020013
           MOVE 'MLKRP034'             TO DP132O-PROGRAM-NAME.          04030013
           MOVE 1                      TO DP132O-REPORT-NUMBER.         04040013
                                                                        04050013
                                                                        04220013
      ******************************************************************04230000
       3000-CREATE-REPORT.                                              04240029
      ******************************************************************04250000
      *                                                                 04260000
           IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                         05220029
               PERFORM 6000-WRITE-REPORT-TITLE                          05230029
           END-IF.                                                      04270016
           MOVE ML034-PRCHG-ID             TO DL-PRCHG-ID               04280016
           MOVE ML034-PRCHG-EFF-DTE        TO PV-FRMT-DATE              04280016
           MOVE PV-FRMT-DATE-MM-DD         TO DL-PRCHG-EFF-MM-DD        04280016
           MOVE ML034-PRCHG-TYP-CDE        TO DL-PRCHG-TYP-CDE          04280016
           MOVE ML034-SKU-NBR              TO DL-SKU-NBR                04280016
           MOVE ML034-PE-QTY               TO DL-PERM-QTY               04280016
           MOVE ML034-PE-RETAIL            TO DL-PERM-RETAIL            04280016
           MOVE ML034-ML-QTY               TO DL-ML-QTY                 04280016
           MOVE ML034-ML-RETAIL            TO DL-ML-RETAIL              04280016
           MOVE ML034-DIFF-QTY             TO DL-DIFF-QTY               04280016
           MOVE ML034-DIFF-RETAIL          TO DL-DIFF-RETAIL            04280016
           WRITE DISCREPANCY-REPORT-REC FROM DL-LINE-1                  05110000
               AFTER ADVANCING 1 LINES.                                 05120000
           ADD +1                          TO PV-LINE-COUNT.            05140001
                                                                        04600000
      ******************************************************************04610029
      *4000-READ-CONTROL-FILE.                                          04620029
      ******************************************************************04630029
      *                                                                 04640029
      *    READ CONTROL-FILE                                            04650029
      *      INTO CC-CONTROL-CARD                                       04660029
      *        AT END                                                   04670029
      *          MOVE '4000-READ-CONTROL-FILE'                          04680029
      *                                TO AA-PARAGRAPH-NAME             04690029
      *          MOVE 'CONTROL CARD IS MISSING'                         04700029
      *                                TO AA-MESSAGE                    04710029
      *          MOVE +4001            TO ABEND-CODE                    04720029
      *          PERFORM 9900-COMMON-ABEND-ROUTINE                      04730029
      *    END-READ.                                                    04740029
                                                                        04750029
                                                                        04770029
      ******************************************************************04780029
       4100-READ-DISCREPANCY-FILE.                                      04790029
      ******************************************************************04800029
      *                                                                 04810029
           READ DISCREPANCY-INPUT-FILE                                  04820029
             INTO ML034-RECORD                                          04830029
               AT END                                                   04840029
                   SET PS-NO-MORE-DESCREP-RECS                          04850029
                                       TO TRUE                          04860029
           END-READ.                                                    04870029
                                                                        04880029
           IF PS-MORE-DESCREP-RECS                                      04890029
               ADD 1                   TO PA-DESCREP-RECORDS-READ       04900029
           END-IF.                                                      04910029
                                                                        04920029
                                                                        04940029
      ******************************************************************04950000
       6000-WRITE-REPORT-TITLE.                                         04960029
      ******************************************************************04970000
      *                                                                 04980000
           ADD 1                       TO PV-PAGE-COUNT.                04990000
           MOVE PV-PAGE-COUNT          TO DP132O-PAGE-NUMBER.           05000001
                                                                        05010000
           WRITE DISCREPANCY-REPORT-REC FROM                            05020001
                                          DP132O-STANDRD-HEADER-132-COLS05020001
               AFTER ADVANCING PAGE.                                    05030000
                                                                        05040000
           WRITE DISCREPANCY-REPORT-REC FROM HD-REPORT-TITLE-2          05050000
               AFTER ADVANCING 1 LINE.                                  05060000
                                                                        05070000
           WRITE DISCREPANCY-REPORT-REC FROM HD-COLUMN-HEADING-1        05110000
               AFTER ADVANCING 2 LINES.                                 05120000
           WRITE DISCREPANCY-REPORT-REC FROM HD-COLUMN-HEADING-2        05110000
               AFTER ADVANCING 1 LINES.                                 05120000
                                                                        05130000
           WRITE DISCREPANCY-REPORT-REC FROM PC-BLANK-LINE              05110000
               AFTER ADVANCING 1 LINES.                                 05120000
                                                                        05130000
           MOVE +6                     TO PV-LINE-COUNT.                05140001
                                                                        05150000
                                                                        07510029
      ******************************************************************07520029
      *7000-SELECT-TDTATTR-FOR-EDIT.                                    07530029
      ******************************************************************07540029
      *                                                                 07550029
      *    EXEC SQL                                                     07560029
      *        SELECT KY_DTE                                            07570029
      *          INTO :DTATTR-KY-DTE                                    07580029
      *          FROM TDTATTR                                           07590029
      *         WHERE (KY_DTE = :PV-KY-DTE)                             07600029
      *    END-EXEC.                                                    07610029
                                                                        07620029
      *    EVALUATE TRUE                                                07630029
      *        WHEN SQLCODE = 0                                         07640029
      *            MOVE 'Y'            TO PS-TDTATTR-FOUND              07650029
      *        WHEN SQLCODE = +100                                      07660029
      *            CONTINUE                                             07670029
      *        WHEN OTHER                                               07680029
      *            MOVE '7000-SELECT-TDTATTR-FOR-EDIT'                  07690029
      *                                TO AA-PARAGRAPH-NAME             07700029
      *            MOVE 'TDTATTR TABLE ERROR'                           07710029
      *                                TO AA-MESSAGE                    07720029
      *            MOVE 'SELECT'       TO AA-DB2-OPERATION              07730029
      *            PERFORM 9901-SQL-ERROR                               07740029
      *    END-EVALUATE.                                                07750029
                                                                        07760029
                                                                        07780029
      ******************************************************************07790000
       8000-EOJ.                                                        07800029
      ******************************************************************07810000
      *                                                                 07820000
           PERFORM 8100-DISPLAY-PROCESS-SUMMARY.                        07890029
                                                                        07900000
           CLOSE DISCREPANCY-INPUT-FILE                                 03140000
                 DISCREPANCY-REPORT-FILE.                               03160000
                                                                        07940000
                                                                        07960000
      ******************************************************************07970015
       8100-DISPLAY-PROCESS-SUMMARY.                                    07980029
      ******************************************************************07990015
      *                                                                 08000015
           DISPLAY ' '.                                                 08010015
           DISPLAY '  PROGRAM MLKRP034 PROCESSING SUMMARY'.             08020015
           DISPLAY '  -----------------------------------'.             08030015
           DISPLAY ' '.                                                 08040015
                                                                        08050015
           MOVE PA-DESCREP-RECORDS-READ TO CD-COUNT-HOLD1.              08060015
           DISPLAY 'DISCREPANCY RECORDS READ = ' CD-COUNT-HOLD1.        08070015
                                                                        08080015
           DISPLAY ' '.                                                 08090015
                                                                        08100015
                                                                        08120015
      ******************************************************************08130000
       9900-COMMON-ABEND-ROUTINE.                                       08140029
      ******************************************************************08150000
      *                                                                 08160000
           DISPLAY AA-ABEND-LIT.                                        08170000
           DISPLAY AA-PROGRAM-LIT.                                      08180000
           DISPLAY AA-PARAGRAPH-LIT.                                    08190000
           DISPLAY AA-MESSAGE-LINE.                                     08200000
           DISPLAY ABEND-CODE.                                          08210000
                                                                        08220000
           CALL 'ILBOABN0' USING ABEND-CODE.                            08230000
                                                                        08240000
                                                                        08260001
      ******************************************************************08270001
       9901-SQL-ERROR.                                                  08280029
      ******************************************************************08290001
      *                                                                 08300001
           CLOSE DISCREPANCY-INPUT-FILE                                 03140000
                 DISCREPANCY-REPORT-FILE.                               03160000
                                                                        08340001
           DISPLAY AA-ABEND-LIT.                                        08350001
           DISPLAY AA-DB2-ERROR-LIT.                                    08360001
           DISPLAY AA-PROGRAM-LIT.                                      08370001
           DISPLAY AA-PARAGRAPH-LIT.                                    08380001
           DISPLAY AA-MESSAGE-LINE.                                     08390001
           DISPLAY AA-DB2-OPERATION-LIT.                                08400001
           SET AC-DB2-ERROR TO TRUE.                                    08410001
           MOVE +4013                  TO ABEND-CODE.                   08420001
                                                                        08430001
           COPY DPPD004.                                                08440001
           CALL 'ILBOABN0' USING ABEND-CODE.                            08450001
                                                                        08460001
