      *-----------------------*                                                 
00002  IDENTIFICATION DIVISION.                                                 
00003 *-----------------------*                                                 
00004  PROGRAM-ID.    INKCS034.                                                 
00005  AUTHOR.        TESS BESTE.                                               
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
00007  DATE-WRITTEN.  10-28-99.                                                 
00008  DATE-COMPILED.                                                           
00009                                                                           
00010 *----------------------------------------------------------------*        
00011 *    I034 - SHEET INVENTORY INQUIRY PROGRAM                               
00012 *                                                                         
00013 * THIS PROGRAM DISPLAYS THE SKU, QUANTITY, UNIT RETAIL, AND               
00014 * EXTENDED RETAIL FOR EACH LINE ON A SHEET.  THE TOTAL UNIT               
00015 * RETAIL, TOTAL QUANTITY, AND TOTAL EXTENDED RETAIL ARE ALSO              
00015 * DISPLAYED.                                                              
      *                                                                         
00015 * THIS IS A SINGLE-PAGE SCREEN.  THE MAXIMUM NUMBER OF LINES THAT         
00015 * CAN BE ENTERED ON A SHEET IS 25, AND ALL 25 LINES ARE DISPLAYED         
00015 * ON ONE SCREEN.                                                          
00016 *----------------------------------------------------------------*        
W21732*---------------------------------------------------------------*         
W21732* 10/28/2002 IGSI                CHG ID:W21732                  *         
W21732* ACCEPT NON-INTELLIGENT SKU IN PLACE OF THE CURRENT DEPT/CLASS/*         
W21732* SEQ NUMBER.                                                   *         
W21732*****************************************************************         
W26600*  WR26600  07/  /2004  PHYSICAL INVENTORY STORE EXPANSION                
W26600*                       ALSO DISPLAYED STORE NAME WITH THE                
W26600*                       LOCATION ID (STORE NUMBER)                        
W26600*                       M JOHNSON                                         
W28545*  W28545   08-22-2005  ROD JANSSEN THE NEW TRANSFER PROCESS              
W26600*                                                                         
W28545*---------------------------------------------------------------*         
W28545*  W28545  02/06/2006  CAPTURING AND REPORTING UPC FOR INVENTORY.         
W28545*                      JOHN SELWITSCHKA                                   
W28545*                      REFERENCE TKMA.IN.CYCLE.SRCLIB FOR CHANGES         
SR3403*  SR3403  03-29-2012  AMT FIELD EXPANSIONS FOR LARGE RETAIL              
SR3403*                      PROJECT                                            
SR3403*                      -JALAJ KUMAR                                       
W26600*****************************************************************         
1122RM* CHANGED 11/03/2022    BY BOB MCASEY      WR: CHG0277188                 
1122RM* ADDED THE DPKCS930 COPYBOOK. CHANGED THE PARAMETERS FOR CALLING         
1122RM* THE CICS ARCHITECTURE API.                                              
1122RM*---------------------------------------------------------------*         
00017                                                                           
00018  ENVIRONMENT DIVISION.                                                    
00019                                                                           
00020  DATA DIVISION.                                                           
00021                                                                           
00022  WORKING-STORAGE SECTION.                                                 
00023                                                                           
00024  01  PC-PROGRAM-CONSTANTS.                                                
00025      05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
00026                                                     'IN034A  '.           
00027      05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
00028                                                     'INKM034 '.           
00029      05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
00030                                                     'INKCS034'.           
00031      05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
00032      05  PC-LIST-FORMAT                 PIC  X(01)  VALUE '2'.            
00033      05  PC-LINES-PER-PANEL             PIC S9(04)  VALUE +25             
00034                                                     COMP SYNC.            
           05  PC-SHEET-TYP-CDE-KEYED         PIC  X      VALUE 'K'.            
00035                                                                           
00036  01  PC-TSYMSG-NUMBERS.                                                   
00038      05  PC-TSYMSG-00069                PIC 9(5)    VALUE 00069.          
00039      05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.          
00040      05  PC-TSYMSG-00137                PIC 9(5)    VALUE 00137.          
00041      05  PC-TSYMSG-00148                PIC 9(5)    VALUE 00148.          
00043                                                                           
00044  01  PS-PROGRAM-SUBSCRIPTS.                                               
00045      05  PS-SUB                         PIC S9(04)   VALUE +0             
00046                                                      COMP SYNC.           
00047                                                                           
00048  01  PS-PROGRAM-SWITCHES.                                                 
00052      05  PS-LIST-END-SW                 PIC X(01)    VALUE 'N'.           
00053          88  PS-LIST-END                             VALUE 'Y'.           
00054          88  PS-NO-LIST-END                          VALUE 'N'.           
00055                                                                           
00056  01  PV-PROGRAM-VARIABLES.                                                
00057      05  PV-CICS-RESP           PIC S9(04)           VALUE +0             
00058                                               COMP SYNC.                  
W28545     05  PV-UPC-POS             PIC S9(04)    COMP   VALUE ZEROS.         
W26600     05  PV-STR-NBR             PIC 9(04)            VALUE ZEROS.         
002900     05  PV-DB2-STR-NBR         PIC S9(04)    COMP.                       
003000     05  PV-DB2-SHEET-NBR       PIC S9(09)    COMP.                       
W28545     05  PV-DB2-TYPE-CDE        PIC X(01).                                
           05  PV-DSPLY-SHEET-NBR     PIC 9(06).                                
           05  PV-TOT-UNT-RTL-AMT     PIC S9(11)V99 COMP-3 VALUE 0.             
           05  PV-TOT-INV-QTY         PIC S9(11)    COMP-3 VALUE 0.             
           05  PV-TOT-EXTD-RTL-AMT    PIC S9(11)V99 COMP-3 VALUE 0.             
00064                                                                           
00066 *---------------------------------------------------------------*         
00067 *    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   *         
00068 *---------------------------------------------------------------*         
00069                                                                           
00070      COPY DPWS030.                                                        
1122RM     COPY DPWS930.                                                        
00071                                                                           
00072 *---------------------------------------------------------------*         
00073 *    STANDARD COMMAREA.                                         *         
00074 *---------------------------------------------------------------*         
00075                                                                           
00076      COPY DPWS020.                                                        
00077      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
00078                                                                           
00079 *---------------------------------------------------------------*         
00080 *    INTER-APPLICATION COMMAREA.                                *         
00081 *---------------------------------------------------------------*         
00082                                                                           
00083          10  INTER-APPL-COMM-AREA       PIC X(200).                       
00084      COPY INWS030.                                                        
00085 *---------------------------------------------------------------*         
00086 *    SPECIFIC COMMMAREA FOR INKCS034.                           *         
00087 *---------------------------------------------------------------*         
00088          10  ASC-SPECIFIC-COMMAREA.                                       
00089              15  ASC-LIST-KEYS.                                           
023900                 20  ASC-KEY-STR-NBR                 PIC S9(04)           
                                                               COMP.            
024000                 20  ASC-KEY-SHEET-NBR               PIC S9(09)           
024100                                                         COMP.            
W28545                 20  ASC-KEY-TYPE-CDE           PIC X(01).                
W28545                     88  ASC-TYPE-ALL-SHEETS    VALUE '*'.                
W28545                     88  ASC-TYPE-TFR-SHEETS    VALUE 'T'.                
W28545                     88  ASC-TYPE-MANI-SHEETS   VALUE 'M'.                
W28545                     88  ASC-TYPE-ENTRD-SHEETS  VALUE 'K'.                
W28545             15  ASC-TYPE-CDE-DESC              PIC X(25).                
W28545                 88  ASC-DESC-ALL-SHEETS                                  
W28545                     VALUE 'ALL SHEETS'.                                  
W28545                 88  ASC-DESC-TFR-SHEETS                                  
W28545                     VALUE 'TRANSFER STORE SHEETS'.                       
W28545                 88  ASC-DESC-MANI-SHEETS                                 
W28545                     VALUE 'MANIFEST TRAILER SHEETS'.                     
W28545                 88  ASC-DESC-ENTRD-SHEETS                                
W28545                     VALUE 'USER ENTERED SHEETS'.                         
00094              15  ASC-STORE-NAME                      PIC  X(25).          
00095              15  ASC-ITEM-ARRAY.                                          
00096                  20  ASC-ITEM-ENTRIES OCCURS 25 TIMES.                    
W21732                     25  ASC-SKU-SEQ-NBR         PIC  X(08).              
W28545                     25  ASC-UPC-NBR-X           PIC  X(15).              
W28545                     25  ASC-UPC-NBR             REDEFINES                
W28545                         ASC-UPC-NBR-X           PIC  9(15).              
00102                      25  ASC-INV-QTY             PIC S9(09)               
00103                                                      COMP.                
00104                      25  ASC-UNT-RTL-AMT         PIC S9(5)V99             
00105                                                      COMP-3.              
00104                      25  ASC-EXTD-RTL-AMT        PIC S9(9)V99             
00105                                                      COMP-3.              
00106                                                                           
00107              15  ASC-NUMBER-OF-SHEET-LINES           PIC S9(04)           
00108                                                      COMP SYNC.           
00109              15  ASC-SEL-SUB                         PIC S9(04)           
00110                                                          COMP.            
00111              15  ASC-SEL-ITEM                        PIC S9(04)           
00112                                                          COMP.            
00113              15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(05)           
00114                                                          COMP-3.          
00115              15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
00116                  88  ASC-ITEMS-LEFT-ON-DB              VALUE 'Y'.         
00117                  88  ASC-NO-ITEMS-LEFT-ON-DB           VALUE 'N'.         
00118              15  ASC-USE-SELECTION-QUEUE-IND         PIC  X(01).          
00119                  88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.         
00120              15  ASC-SEL-QUEUE-NAME.                                      
00121                  20  ASC-SEL-TERM-ID                 PIC  X(04).          
00122                  20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
00123                                                           COMP-3.         
00124                  20  ASC-SEL-SEQ                     PIC  9(01).          
W28545*        10  FILLER                                  PIC X(2239).         
W28545         10  FILLER                                  PIC X(1863).         
00125                                                                           
015200*---------------------------------------------------------------*         
015300*  RECORD LAYOUT FOR THE SHEET INVENTORY TEMP STORAGE SELECTION *         
015400*  QUEUE.                                                       *         
015500*---------------------------------------------------------------*         
00129                                                                           
00130      COPY INWS031.                                                        
00131                                                                           
00132                                                                           
00133 *---------------------------------------------------------------*         
00134 *    ABEND PROCESSING COPYBOOK.                                 *         
00135 *---------------------------------------------------------------*         
00136                                                                           
00137      COPY DPWS013.                                                        
00138                                                                           
00139                                                                           
00140 *---------------------------------------------------------------*         
00141 *    ATTRIBUTE SETTINGS COPYBOOK.                               *         
00142 *---------------------------------------------------------------*         
00143                                                                           
00144      COPY DPWS015.                                                        
00145                                                                           
00146 *---------------------------------------------------------------*         
00147 *    FUNCTION KEYS COPYBOOK.                                    *         
00148 *---------------------------------------------------------------*         
00149                                                                           
00150      COPY DPWS016.                                                        
00151                                                                           
00152 *---------------------------------------------------------------*         
00153 *    MAP LAYOUT                                                 *         
00154 *---------------------------------------------------------------*         
00155                                                                           
00156      COPY INKM034.                                                        
00157                                                                           
00158  01  MR-OCCURS-LAYOUT          REDEFINES  IN034AO.                        
W28545     05  FILLER                PIC X(90).                                 
00160      05  MR-DETAIL-LINE OCCURS 25 TIMES.                                  
W21732         10  MR-SKUL           PIC S9(04) COMP.                           
W21732         10  MR-SKUA           PIC  X(01).                                
W21732         10  MR-SKUC           PIC  X(01).                                
W21732         10  MR-SKUH           PIC  X(01).                                
W28545*        10  MR-SKU            PIC  X(08).                                
W28545         10  MR-SKU.                                                      
W28545             15                PIC  X(07).                                
W28545             15  MR-SKU-8      PIC  X(08).                                
W28545         10  MR-UPC            REDEFINES                                  
W28545             MR-SKU            PIC  X(15).                                
00182          10  MR-QTYL           PIC S9(04) COMP.                           
00183          10  MR-QTYA           PIC  X(01).                                
00184          10  MR-QTYC           PIC  X(01).                                
00185          10  MR-QTYH           PIC  X(01).                                
00186          10  MR-QTY-X.                                                    
SR3403             15  MR-QTY        PIC  Z(2)9.                                
00188          10  MR-PRCEL          PIC S9(04) COMP.                           
00189          10  MR-PRCEA          PIC  X(01).                                
00190          10  MR-PRCEC          PIC  X(01).                                
00191          10  MR-PRCEH          PIC  X(01).                                
00192          10  MR-PRCE-X.                                                   
SR3403             15  MR-PRCE       PIC  Z(05).99.                             
00188          10  MR-EXTDL          PIC S9(04) COMP.                           
00189          10  MR-EXTDA          PIC  X(01).                                
00190          10  MR-EXTDC          PIC  X(01).                                
00191          10  MR-EXTDH          PIC  X(01).                                
00192          10  MR-EXTD-X.                                                   
W28545*            15  MR-EXTD       PIC  ZZ,ZZZ,ZZZ.99.                        
W28545             15  MR-EXTD       PIC  ZZZZZ.99.                             
00194                                                                           
00195                                                                           
00196 *---------------------------------------------------------------*         
00197 *    DB2 AREA FOR INVENTORY SHEET ENTRY TABLE                   *         
00198 *---------------------------------------------------------------*         
00199      EXEC SQL                                                             
00200           INCLUDE TINENTY                                                 
00201      END-EXEC.                                                            
                                                                                
00202 *---------------------------------------------------------------*         
00203 *    DB2 AREA FOR COMMUNICATIONS                                *         
00204 *---------------------------------------------------------------*         
00205      EXEC SQL                                                             
00206           INCLUDE SQLCA                                                   
00207      END-EXEC.                                                            
00208                                                                           
00209 *---------------------------------------------------------------*         
00210 *  CURSOR TO OBTAIN ALL ROWS FOR A SHEET INVENTORY SHEET.                 
00211 *---------------------------------------------------------------*         
00212                                                                           
00213      EXEC SQL                                                             
00214           DECLARE SHEET-DTL-CSR CURSOR FOR                                
00215           SELECT LNE_NBR                                                  
W21732               , SKU_NBR                                                  
W28545               , UPC_NBR                                                  
00220                , INV_QTY                                                  
00221                , UNT_RTL_AMT                                              
00221                , EXTD_RTL_AMT                                             
00222           FROM   TINENTY                                                  
W21732          WHERE  STR_NBR       = :INENTY-STR-NBR                          
W21732            AND  SHEET_NBR     = :INENTY-SHEET-NBR                        
W21732            AND  SHEET_TYP_CDE = :INENTY-SHEET-TYP-CDE                    
00225           ORDER BY                                                        
00215                  LNE_NBR                                                  
00227      END-EXEC.                                                            
00228                                                                           
00230  LINKAGE SECTION.                                                         
00231                                                                           
00232  01  DFHCOMMAREA.                                                         
00233      05  FILLER                         OCCURS  1 TO 4072 TIMES           
00234                                         DEPENDING ON EIBCALEN.            
00235          10  FILLER                     PIC X.                            
00236                                                                           
00237                                                                           
00238  PROCEDURE DIVISION.                                                      
00239                                                                           
00240  0000-MAIN-MODULE.                                                        
00241                                                                           
00242      INITIALIZE DP030-CICS-API-FIELDS.                                    
00243      MOVE +1                      TO DP030-NUMBER-OF-MAPS.                
00244      MOVE PC-CURRENT-MAPSET-NAME  TO DP030-MAPSET-NAME.                   
00245      MOVE PC-CURRENT-MAP-NAME     TO DP030-MAP-NAME (1).                  
00246      SET DP030-RECEIVE-APPL-MAP   TO TRUE.                                
00247      MOVE LENGTH OF IN034AI       TO DP030-MAP-LENGTH (1).                
00248      MOVE 'PROGRAM ENTRY CALL'    TO DP013-MESSAGE-TEXT (1).              
00249      PERFORM 0001-CALL-CICS-ARCH-API.                                     
00250                                                                           
00251      PERFORM 1000-CONTROL-PROCESSING.                                     
00252                                                                           
00253      MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).              
00254      PERFORM 0001-CALL-CICS-ARCH-API.                                     
00255                                                                           
00255                                                                           
00256 *----------------------------------------------------------------*        
00257 *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *        
00258 *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *        
00259 *----------------------------------------------------------------*        
00261  0001-CALL-CICS-ARCH-API.                                                 
00262                                                                           
1122RM*    CALL 'DPKCS030' USING DFHEIBLK                                       
1122RM     CALL DP930-CICS-ARCH-API                                             
1122RM                     USING DFHEIBLK                                       
00264                            DFHCOMMAREA                                    
00265                            DP030-CICS-API-FIELDS                          
00266                            DP020-STANDARD-COMMAREA                        
00267                            IN034AI.                                       
00268                                                                           
00269      IF  DP030-RC-CALL-SUCCESSFUL                                         
00270          CONTINUE                                                         
00271      ELSE                                                                 
00272          SET DP013-NO-ROLLBACK TO TRUE                                    
00273          SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
00274          SET DP013-CICS-ABEND TO TRUE                                     
00275          MOVE 'BEFORE 0000-MAIN-MODULE' TO DP013-PARAGRAPH                
1122RM*        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEX        
1122RM*             'T LINE'         TO DP013-MESSAGE-TEXT (2)                  
1122RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1122RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00278          MOVE DP030-RETURN-CODE                                           
00279                                TO DP013-MESSAGE-TEXT (3)                  
00280          PERFORM DP013-0000-PROCESS-ABEND                                 
00281      END-IF.                                                              
00282                                                                           
00282                                                                           
00283 *----------------------------------------------------------------*        
00284 * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
00285 * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
00286 *----------------------------------------------------------------*        
00288  1000-CONTROL-PROCESSING.                                                 
00289                                                                           
00290      EVALUATE TRUE                                                        
00291          WHEN DP020-NEXT-ACT-INITIAL                                      
00292              PERFORM 1100-PROCESS-INTER-APPL-COMM                         
00293              IF  DP020-NEXT-ACT-APPL-ERROR                                
00294                  CONTINUE                                                 
00295              ELSE                                                         
00296                  PERFORM 4000-BUILD-INITIAL-PANEL                         
00297              END-IF                                                       
00298                                                                           
00299          WHEN DP020-NEXT-ACT-READ-MAP                                     
00300              PERFORM 2000-PROCESS-PANEL                                   
00301                                                                           
00302          WHEN DP020-NEXT-ACT-RETURN                                       
00303              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
00304                                                                           
00305          WHEN OTHER                                                       
00306              SET DP013-LOGIC-ABEND TO TRUE                                
00307              SET DP013-NO-ROLLBACK TO TRUE                                
00308              MOVE '1000-CONTROL-PROCESSING'                               
00309                                TO  DP013-PARAGRAPH                        
00310              MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
00311                                TO  DP013-MESSAGE-TEXT(1)                  
00312              PERFORM DP013-0000-PROCESS-ABEND                             
00313      END-EVALUATE.                                                        
00314                                                                           
00315                                                                           
00316 *----------------------------------------------------------------*        
00317 * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA.               
00318 *----------------------------------------------------------------*        
00320  1100-PROCESS-INTER-APPL-COMM.                                            
00321                                                                           
00322      INITIALIZE ASC-SPECIFIC-COMMAREA.                                    
W28545                                                                          
W28545     IF IN030-TYPE-TFR-SHEET                                              
W28545         SET IN030-DESC-TFR-SHEET  TO TRUE                                
W28545         MOVE IN030-TYPE-CDE-DESC  TO ASC-TYPE-CDE-DESC                   
W28545     ELSE                                                                 
W28545     IF IN030-TYPE-MANI-SHEET                                             
W28545         SET IN030-DESC-MANI-SHEET TO TRUE                                
W28545         MOVE IN030-TYPE-CDE-DESC  TO ASC-TYPE-CDE-DESC                   
W28545     ELSE                                                                 
W28545     IF IN030-TYPE-ENTRD-SHEET                                            
W28545         SET IN030-DESC-ENTRD-SHEET TO TRUE                               
W28545         MOVE IN030-TYPE-CDE-DESC   TO ASC-TYPE-CDE-DESC                  
W28545     ELSE                                                                 
W28545         MOVE IN030-TYPE-CDE        TO ASC-TYPE-CDE-DESC                  
W28545     END-IF.                                                              
W28545                                                                          
00323      MOVE IN030-STORE-NAME     TO ASC-STORE-NAME.                         
00324      MOVE IN030-STR-NBR        TO ASC-KEY-STR-NBR                         
00325                                   PV-DB2-STR-NBR.                         
00326      EVALUATE DP020-INT-APPL-FORMAT-IND                                   
00327          WHEN PC-CRIT-SPEC-FORMAT                                         
      *            THE CRITERIA SPEC PASSES IN A SHEET NUMBER OF ZERO           
      *            WHEN ASTERISKS ARE ENTERED.                                  
00328              IF IN030-SHEET-NBR = ZERO                                    
00329                  SET DP020-NEXT-ACT-APPL-ERROR                            
00330                      DP020-MSG-FATAL           TO TRUE                    
00331 *                ---- ASTERISK NOT PERMITTED FOR SELECTED ----            
00332 *                ---- FUNCTION                            ----            
00333                  MOVE PC-TSYMSG-00137 TO DP020-MSG-NUMBER                 
00334              ELSE                                                         
00335                  MOVE IN030-SHEET-NBR TO ASC-KEY-SHEET-NBR                
00336                                          PV-DB2-SHEET-NBR                 
00337              END-IF                                                       
W28545             IF IN030-TYPE-CDE = "*"                                      
W28545                 SET DP020-NEXT-ACT-APPL-ERROR                            
W28545                     DP020-MSG-FATAL           TO TRUE                    
W28545*                ---- ASTERISK NOT PERMITTED FOR SELECTED ----            
W28545*                ---- FUNCTION                            ----            
W28545                 MOVE PC-TSYMSG-00137 TO DP020-MSG-NUMBER                 
W28545             ELSE                                                         
W28545                 MOVE IN030-TYPE-CDE TO ASC-KEY-TYPE-CDE                  
W28545                                        PV-DB2-TYPE-CDE                   
W28545             END-IF                                                       
00338          WHEN PC-LIST-FORMAT                                              
00339              SET ASC-USE-SELECTION-QUEUE TO TRUE                          
00340              MOVE IN030-NUMBER-OF-TS-ITEMS                                
00341                                TO ASC-NUMBER-OF-TS-ITEMS                  
00342              MOVE IN030-SEL-QUEUE-NAME                                    
00343                                TO ASC-SEL-QUEUE-NAME                      
00344              PERFORM 1120-PREPARE-LIST-ENTRY                              
00345          WHEN OTHER                                                       
00346 *            ---- DATA DOES NOT EXIST ----                                
00347              MOVE PC-TSYMSG-00148          TO DP020-MSG-NUMBER            
00349              SET DP020-MSG-FATAL           TO TRUE                        
00350              SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                        
00351      END-EVALUATE.                                                        
00352                                                                           
00352                                                                           
00353 *----------------------------------------------------------------*        
00354 *    GET READY TO START AT THE FIRST ITEM ON THE LIST.           *        
00355 *----------------------------------------------------------------*        
00357  1120-PREPARE-LIST-ENTRY.                                                 
00358                                                                           
00359      MOVE IN031-MAX-ITEMS TO ASC-SEL-SUB.                                 
00360      MOVE ZERO            TO ASC-SEL-ITEM.                                
00361      PERFORM 2140-GET-NEXT-SHEET.                                         
00362                                                                           
00362                                                                           
00363 *----------------------------------------------------------------*        
00364 *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *        
00365 *    ON WHAT FUNCTION KEY WAS SELECTED.                          *        
00366 *----------------------------------------------------------------*        
00368  2000-PROCESS-PANEL.                                                      
00369                                                                           
00370      EVALUATE TRUE                                                        
00371          WHEN DP020-SRC-AID = DP016-CLEAR                                 
00372              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
00373                                                                           
00374          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
00375              MOVE ASC-KEY-STR-NBR     TO PV-DB2-STR-NBR                   
00376              MOVE ASC-KEY-SHEET-NBR   TO PV-DB2-SHEET-NBR                 
W28545             MOVE ASC-KEY-TYPE-CDE    TO PV-DB2-TYPE-CDE                  
00377              PERFORM 4000-BUILD-INITIAL-PANEL                             
00378                                                                           
00379          WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
00380              IF  ASC-USE-SELECTION-QUEUE                                  
00381                  PERFORM 2140-GET-NEXT-SHEET                              
00382                  IF PS-LIST-END                                           
00383                      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
00384                      SET PS-NO-LIST-END TO TRUE                           
00385                  ELSE                                                     
00386                      MOVE ASC-KEY-STR-NBR    TO PV-DB2-STR-NBR            
00388                      PERFORM 4000-BUILD-INITIAL-PANEL                     
00389                  END-IF                                                   
00390              ELSE                                                         
00391 *                ---- BOTTOM OF LIST ----                                 
00392                  MOVE PC-TSYMSG-00070        TO DP020-MSG-NUMBER          
00394                  SET DP020-MSG-INFORMATIONAL TO TRUE                      
00395              END-IF                                                       
00396                                                                           
00397          WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                     
00398              IF  ASC-USE-SELECTION-QUEUE                                  
00399                  PERFORM 2150-GET-PREV-SHEET                              
00400                  IF PS-LIST-END                                           
00401                      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
00402                      SET PS-NO-LIST-END TO TRUE                           
00403                  ELSE                                                     
00404                      MOVE ASC-KEY-STR-NBR  TO PV-DB2-STR-NBR              
00406                      PERFORM 4000-BUILD-INITIAL-PANEL                     
00407                  END-IF                                                   
00408              ELSE                                                         
00409 *                ---- TOP OF LIST ----                                    
00410                  MOVE PC-TSYMSG-00069        TO DP020-MSG-NUMBER          
00412                  SET DP020-MSG-INFORMATIONAL TO TRUE                      
00413              END-IF                                                       
00414                                                                           
00415          WHEN DP020-SRC-AID = DP016-ENTER                                 
00416              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
00417                                                                           
00418          WHEN OTHER                                                       
00419              SET DP013-NO-ROLLBACK          TO TRUE                       
00420              SET DP013-XCTL-DISPLAY-RESTART TO TRUE                       
00421              SET DP013-CICS-ABEND           TO TRUE                       
00422              MOVE '2000-PROCESS-PANEL' TO DP013-PARAGRAPH                 
00423              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
00424                                    TO DP013-MESSAGE-TEXT (1)              
00425              PERFORM DP013-0000-PROCESS-ABEND                             
00426                                                                           
00427      END-EVALUATE.                                                        
00428                                                                           
00428                                                                           
00429 *----------------------------------------------------------------*        
00430 *    GET THE NEXT SHEET IF A LIST OF SHEETS IS BEING PROCESSED.  *        
00431 *----------------------------------------------------------------*        
00433  2140-GET-NEXT-SHEET.                                                     
00434                                                                           
00435      ADD +1                       TO ASC-SEL-SUB.                         
00436      IF  ASC-SEL-SUB > IN031-MAX-ITEMS                                    
00437          ADD +1                   TO ASC-SEL-ITEM                         
00438          MOVE +1                  TO ASC-SEL-SUB                          
00439      END-IF.                                                              
00440      IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
00441          PERFORM 9100-READ-SEL-QUEUE                                      
00442          IF  IN031-ITEM (ASC-SEL-SUB) > ZERO                              
00443              MOVE IN031-SHEET-NBR (ASC-SEL-SUB)                           
00444                                    TO ASC-KEY-SHEET-NBR                   
00445                                       PV-DB2-SHEET-NBR                    
W28545             MOVE IN031-TYPE-CDE (ASC-SEL-SUB)                            
W28545                                   TO ASC-KEY-TYPE-CDE                    
W28545                                      PV-DB2-TYPE-CDE                     
00446              SET IN031-INQ (ASC-SEL-SUB) TO TRUE                          
00447              PERFORM 9200-REWRITE-SEL-QUEUE                               
00448          ELSE                                                             
00449              SUBTRACT +1 FROM ASC-SEL-SUB                                 
00450 *            ---- BOTTOM OF LIST ----                                     
00451              MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER                     
00452              SET DP020-MSG-INFORMATIONAL TO TRUE                          
00453              SET PS-LIST-END             TO TRUE                          
00454          END-IF                                                           
00455      ELSE                                                                 
00456          SUBTRACT +1 FROM ASC-SEL-ITEM                                    
00457          MOVE IN031-MAX-ITEMS     TO ASC-SEL-SUB                          
00458 *        ---- BOTTOM OF LIST ----                                         
00459          MOVE PC-TSYMSG-00070     TO DP020-MSG-NUMBER                     
00460          SET DP020-MSG-INFORMATIONAL TO TRUE                              
00461          SET PS-LIST-END             TO TRUE                              
00462      END-IF.                                                              
00463                                                                           
00463                                                                           
00464 *----------------------------------------------------------------*        
00465 *    GET THE PREVIOUS SHEET IF A LIST OF SHEETS IS BEING         *        
00466 *    PROCESSED.                                                  *        
00467 *----------------------------------------------------------------*        
00469  2150-GET-PREV-SHEET.                                                     
00470                                                                           
00471      SUBTRACT +1 FROM ASC-SEL-SUB.                                        
00472      IF  ASC-SEL-SUB < +1                                                 
00473          SUBTRACT +1 FROM ASC-SEL-ITEM                                    
00474          MOVE IN031-MAX-ITEMS     TO ASC-SEL-SUB                          
00475      END-IF.                                                              
00476                                                                           
00477      IF  ASC-SEL-ITEM > ZERO                                              
00478          PERFORM 9100-READ-SEL-QUEUE                                      
00479          MOVE IN031-SHEET-NBR (ASC-SEL-SUB) TO ASC-KEY-SHEET-NBR          
00481                                                PV-DB2-SHEET-NBR           
W28545         MOVE IN031-TYPE-CDE (ASC-SEL-SUB) TO ASC-KEY-TYPE-CDE            
W28545                                               PV-DB2-TYPE-CDE            
00482      ELSE                                                                 
00483          MOVE +1 TO ASC-SEL-ITEM                                          
00484                     ASC-SEL-SUB                                           
00485 *        ---- TOP OF LIST ----                                            
00486          MOVE PC-TSYMSG-00069        TO DP020-MSG-NUMBER                  
00487          SET DP020-MSG-INFORMATIONAL TO TRUE                              
00488          SET PS-LIST-END             TO TRUE                              
00489      END-IF.                                                              
00490                                                                           
00491                                                                           
00492 *----------------------------------------------------------------*        
00493 *    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
00494 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
00495 *    DISPLAY THE PANEL TO USER.                                  *        
00496 *----------------------------------------------------------------*        
00498  4000-BUILD-INITIAL-PANEL.                                                
00499                                                                           
00500      INITIALIZE ASC-ITEM-ARRAY.                                           
00501      SET ASC-ITEMS-LEFT-ON-DB TO  TRUE.                                   
00502                                                                           
00503      PERFORM 4010-OPEN-CURSOR.                                            
00504                                                                           
00505      PERFORM 4300-READ-ITEMS-FROM-DB.                                     
00506                                                                           
00507      PERFORM 4020-CLOSE-CURSOR.                                           
00508                                                                           
00509      IF ASC-NUMBER-OF-SHEET-LINES = ZERO                                  
00510          SET DP020-NEXT-ACT-APPL-ERROR                                    
00511              DP020-MSG-FATAL           TO TRUE                            
00512 *        ---- DATA DOES NOT EXIST ----                                    
00513          MOVE PC-TSYMSG-00148 TO DP020-MSG-NUMBER                         
00514      ELSE                                                                 
00515          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
00516      END-IF.                                                              
00517                                                                           
00517                                                                           
00518 *----------------------------------------------------------------*        
00519 * OPEN THE CURSOR.                                               *        
00520 *----------------------------------------------------------------*        
00522  4010-OPEN-CURSOR.                                                        
00523                                                                           
W21732     MOVE PV-DB2-STR-NBR         TO   INENTY-STR-NBR                      
W21732     MOVE PV-DB2-SHEET-NBR       TO   INENTY-SHEET-NBR                    
W28545     MOVE PV-DB2-TYPE-CDE        TO   INENTY-SHEET-TYP-CDE                
W28545***  MOVE PC-SHEET-TYP-CDE-KEYED TO   INENTY-SHEET-TYP-CDE                
W28545                                                                          
00524      EXEC SQL                                                             
00525           OPEN SHEET-DTL-CSR                                              
00526      END-EXEC.                                                            
00527                                                                           
00528      EVALUATE TRUE                                                        
00529          WHEN SQLCODE = ZERO                                              
00530               CONTINUE                                                    
00531          WHEN SQLWARN0 NOT = SPACE                                        
00532          WHEN SQLCODE  NOT = ZERO                                         
00533               MOVE '4010-OPEN-CURSOR'  TO DP013-PARAGRAPH                 
00535               MOVE 'OPEN SHEET-DTL-CSR'                                   
00536                                        TO DP013-MESSAGE-TEXT   (1)        
00049               MOVE 'TINENTY'           TO DP013-DB2-TABLE-NAME (1)   CL*20
00537               MOVE SQLCA               TO DP013-SQLCA                     
00538               SET DP013-DB2-ABEND      TO  TRUE                           
00540               PERFORM DP013-0000-PROCESS-ABEND                            
00541      END-EVALUATE.                                                        
00542                                                                           
00543                                                                           
00544 *----------------------------------------------------------------*        
00545 * CLOSE THE CURSOR.                                              *        
00546 *----------------------------------------------------------------*        
00548  4020-CLOSE-CURSOR.                                                       
00549                                                                           
00550      EXEC SQL                                                             
00551          CLOSE SHEET-DTL-CSR                                              
00552      END-EXEC.                                                            
00553                                                                           
00554      EVALUATE TRUE                                                        
00555          WHEN SQLCODE = ZERO                                              
00556               CONTINUE                                                    
00557          WHEN SQLWARN0 NOT = SPACE                                        
00558          WHEN SQLCODE  NOT = ZERO                                         
00559               MOVE '4020-CLOSE-CURSOR' TO DP013-PARAGRAPH                 
00561               MOVE 'CLOSE SHEET-DTL-CSR'                                  
00562                                        TO DP013-MESSAGE-TEXT   (1)        
00049               MOVE 'TINENTY'           TO DP013-DB2-TABLE-NAME (1)   CL*20
00563               MOVE SQLCA               TO DP013-SQLCA                     
00564               SET DP013-DB2-ABEND      TO TRUE                            
00566               PERFORM DP013-0000-PROCESS-ABEND                            
00567      END-EVALUATE.                                                        
00568                                                                           
00569                                                                           
00570 *----------------------------------------------------------------*        
00571 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
00572 *    ARRAY.                                                               
00573 *----------------------------------------------------------------*        
00575  4300-READ-ITEMS-FROM-DB.                                                 
00576                                                                           
00577      MOVE ZERO TO ASC-NUMBER-OF-SHEET-LINES.                              
                                                                                
00578      PERFORM VARYING PS-SUB FROM 1 BY 1                                   
00581        UNTIL (PS-SUB > PC-LINES-PER-PANEL)                                
00582           OR  ASC-NO-ITEMS-LEFT-ON-DB                                     
00583                                                                           
00584          PERFORM 4310-FETCH                                               
00585                                                                           
00586          IF  ASC-ITEMS-LEFT-ON-DB                                         
W21732             MOVE INENTY-SKU-NBR      TO ASC-SKU-SEQ-NBR  (PS-SUB)        
W28545             MOVE INENTY-UPC-NBR      TO ASC-UPC-NBR      (PS-SUB)        
00593              MOVE INENTY-INV-QTY      TO ASC-INV-QTY      (PS-SUB)        
00594              MOVE INENTY-UNT-RTL-AMT  TO ASC-UNT-RTL-AMT  (PS-SUB)        
00594              MOVE INENTY-EXTD-RTL-AMT TO ASC-EXTD-RTL-AMT (PS-SUB)        
00596              MOVE PS-SUB              TO ASC-NUMBER-OF-SHEET-LINES        
00597          END-IF                                                           
00598      END-PERFORM.                                                         
00599                                                                           
00600                                                                           
00601 *----------------------------------------------------------------*        
00602 *    FETCH THE SHEET DETAIL CURSOR.                              *        
00603 *----------------------------------------------------------------*        
00605  4310-FETCH.                                                              
00606                                                                           
00607      EXEC SQL                                                             
00608           FETCH SHEET-DTL-CSR                                             
00609           INTO  :INENTY-LNE-NBR                                           
W21732              , :INENTY-SKU-NBR                                           
W28545              , :INENTY-UPC-NBR                                           
00614               , :INENTY-INV-QTY                                           
00615               , :INENTY-UNT-RTL-AMT                                       
00615               , :INENTY-EXTD-RTL-AMT                                      
00616      END-EXEC.                                                            
00617                                                                           
00618      EVALUATE TRUE                                                        
00619          WHEN SQLCODE = ZERO                                              
00620               CONTINUE                                                    
00621          WHEN SQLCODE = +100                                              
00622              SET ASC-NO-ITEMS-LEFT-ON-DB TO TRUE                          
00623          WHEN SQLWARN0 NOT = SPACE                                        
00624          WHEN SQLCODE  NOT = ZERO                                         
00625              MOVE '4310-FETCH-CURSOR'  TO DP013-PARAGRAPH                 
00627              MOVE 'FETCH SHEET-DTL-CSR'                                   
00628                                        TO DP013-MESSAGE-TEXT   (1)        
00049              MOVE 'TINENTY'            TO DP013-DB2-TABLE-NAME (1)   CL*20
00629              MOVE SQLCA                TO DP013-SQLCA                     
00630              SET DP013-DB2-ABEND       TO  TRUE                           
00632              PERFORM DP013-0000-PROCESS-ABEND                             
00633      END-EVALUATE.                                                        
00634                                                                           
00635                                                                           
00637 *----------------------------------------------------------------*        
00638 *    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.           *        
00639 *----------------------------------------------------------------*        
00641  4400-MOVE-COMMAREA-TO-SCREEN.                                            
00642                                                                           
W26600     MOVE ASC-KEY-STR-NBR     TO PV-STR-NBR.                              
00643 *    MOVE ASC-STORE-NAME      TO ASTNAMEO.                                
W26600     STRING                                                               
W26600       PV-STR-NBR SPACE '-' SPACE                                         
W26600       ASC-STORE-NAME                                                     
W26600       DELIMITED BY SIZE      INTO ASTNAMEO.                              
W28545                                                                          
00644      MOVE ASC-KEY-SHEET-NBR   TO PV-DSPLY-SHEET-NBR.                      
00644      MOVE PV-DSPLY-SHEET-NBR  TO ASHEETO.                                 
00645                                                                           
W28545                                                                          
W28545     IF ASC-TYPE-TFR-SHEETS                                               
W28545         SET ASC-DESC-TFR-SHEETS  TO TRUE                                 
W28545         MOVE ASC-TYPE-CDE-DESC   TO ATYPDSCO                             
W28545     ELSE                                                                 
W28545     IF ASC-TYPE-MANI-SHEETS                                              
W28545         SET ASC-DESC-MANI-SHEETS TO TRUE                                 
W28545         MOVE ASC-TYPE-CDE-DESC   TO ATYPDSCO                             
W28545     ELSE                                                                 
W28545     IF ASC-TYPE-ENTRD-SHEETS                                             
W28545         SET ASC-DESC-ENTRD-SHEETS TO TRUE                                
W28545         MOVE ASC-TYPE-CDE-DESC    TO ATYPDSCO                            
W28545     ELSE                                                                 
W28545         MOVE ASC-KEY-TYPE-CDE     TO ATYPDSCO                            
W28545     END-IF.                                                              
W28545                                                                          
00646      PERFORM VARYING PS-SUB FROM 1 BY 1                                   
00647        UNTIL PS-SUB > PC-LINES-PER-PANEL                                  
00648          IF  PS-SUB > ASC-NUMBER-OF-SHEET-LINES                           
W21732             MOVE SPACE TO MR-SKU    (PS-SUB)                             
00653                            MR-QTY-X  (PS-SUB)                             
00654                            MR-PRCE-X (PS-SUB)                             
00654                            MR-EXTD-X (PS-SUB)                             
00655          ELSE                                                             
W28545*            MOVE ASC-SKU-SEQ-NBR  (PS-SUB) TO MR-SKU  (PS-SUB)           
W28545             IF ASC-UPC-NBR  (PS-SUB) > ZEROES                            
W28545                 MOVE ZERO                     TO PV-UPC-POS              
W28545                 INSPECT ASC-UPC-NBR-X (PS-SUB) TALLYING                  
W28545                     PV-UPC-POS FOR LEADING ZEROS                         
W28545                 ADD 1                         TO PV-UPC-POS              
W28545                 MOVE ASC-UPC-NBR-X   (PS-SUB) (PV-UPC-POS:)              
W28545                                               TO MR-UPC  (PS-SUB)        
W28545             ELSE                                                         
W28545                 MOVE ASC-SKU-SEQ-NBR (PS-SUB) TO MR-SKU  (PS-SUB)        
W28545             END-IF                                                       
00669              MOVE ASC-INV-QTY      (PS-SUB) TO MR-QTY  (PS-SUB)           
00670              MOVE ASC-UNT-RTL-AMT  (PS-SUB) TO MR-PRCE (PS-SUB)           
00670              MOVE ASC-EXTD-RTL-AMT (PS-SUB) TO MR-EXTD (PS-SUB)           
                   ADD  ASC-UNT-RTL-AMT  (PS-SUB) TO PV-TOT-UNT-RTL-AMT         
                   ADD  ASC-INV-QTY      (PS-SUB) TO PV-TOT-INV-QTY             
                   ADD  ASC-EXTD-RTL-AMT (PS-SUB) TO PV-TOT-EXTD-RTL-AMT        
00672          END-IF                                                           
00673      END-PERFORM.                                                         
00674                                                                           
           MOVE PV-TOT-UNT-RTL-AMT  TO  ATRTL01O.                               
           MOVE PV-TOT-INV-QTY      TO  ATQTY01O.                               
           MOVE PV-TOT-EXTD-RTL-AMT TO  ATEXT01O.                               
00675                                                                           
00676 *----------------------------------------------------------------*        
00677 * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
00678 *----------------------------------------------------------------*        
00680  9100-READ-SEL-QUEUE.                                                     
                                                                                
00681      EXEC CICS                                                            
00682          READQ TS                                                         
00683              QUEUE (ASC-SEL-QUEUE-NAME)                                   
00684              INTO  (IN031-SHEET-SEL-REC)                                  
00685              ITEM  (ASC-SEL-ITEM)                                         
00686              RESP  (PV-CICS-RESP)                                         
00687      END-EXEC.                                                            
00688                                                                           
00689      IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
00690          CONTINUE                                                         
00691      ELSE                                                                 
00692          SET DP013-LOGIC-ABEND                                            
00693              DP013-NO-ROLLBACK      TO TRUE                               
00694          MOVE '9100-READ-SEL-QUEUE' TO DP013-PARAGRAPH                    
00696          MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
00697                                     TO DP013-MESSAGE-TEXT (1)             
00698          PERFORM DP013-0000-PROCESS-ABEND                                 
00699      END-IF.                                                              
                                                                                
                                                                                
00700 *----------------------------------------------------------------*        
00701 * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
00702 *----------------------------------------------------------------*        
00703  9200-REWRITE-SEL-QUEUE.                                                  
                                                                                
00704      EXEC CICS                                                            
00705           WRITEQ TS                                                       
00706               QUEUE  (ASC-SEL-QUEUE-NAME)                                 
00707               FROM   (IN031-SHEET-SEL-REC)                                
00708               ITEM   (ASC-SEL-ITEM)                                       
00709               RESP   (PV-CICS-RESP)                                       
00710               REWRITE                                                     
00711      END-EXEC.                                                            
00712                                                                           
00713      IF  PV-CICS-RESP NOT = DFHRESP(NORMAL)                               
00714          SET DP013-CICS-ABEND                                             
00715              DP013-NO-ROLLBACK         TO TRUE                            
00716          MOVE '9200-REWRITE-SEL-QUEUE' TO DP013-PARAGRAPH                 
00718          MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
00719                                        TO DP013-MESSAGE-TEXT (1)          
00720          PERFORM DP013-0000-PROCESS-ABEND                                 
00721      END-IF.                                                              
00722                                                                           
00723                                                                           
00724 *----------------------------------------------------------------*        
00725 *    ABEND PROCESSOR MODULE                                      *        
00726 *----------------------------------------------------------------*        
00727                                                                           
00728      COPY DPPD013.                                                        
