000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400*                                                                         
000500 PROGRAM-ID.    MHKUP007.                                                 
000600 AUTHOR.        BOB FORD.                                                 
000700 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000800 DATE-WRITTEN.  06-29-2004.                                               
000900 DATE-COMPILED.                                                           
001000*                                                                         
001100******************************************************************        
001200*       MHKUP007 - LOAD UPDATES TO THE DAILY SALES TABLE         *        
001300******************************************************************        
001700*                                                                *        
001800*   THIS PROGRAM UPDATES THE FOLLOWING MHT_DLY_SLS TABLE.  IT    *        
001900*   READS A CONTROL-CARD CONTAINING PROCESSING-DATE(PRCG_DTE),   *        
002000*   AND READS THE FSTT_OH_SLS_AUD TABLE USING THE PRCG_DTE AND   *        
002100*   TRN_STAT_CDE = 'C', RETURNING LOC_NBR, SKU, SKU_STAT_CD, AND *        
002200*   THE SUM OF SLD_QTY AND NET_CHRGD_AMT, AND INSERTS IT INTO THE*        
002300*   DAILY SALES TABLE.  NO UPDATES OR DELETES ARE PERFORMED.     *        
003600*                                                                *        
007700*    AT EOF, THE COUNT OF DB2 I/O (READS AND INSERTS) ON         *        
007800*    EACH OF THE TABLES WILL BE DISPLAYED.  ALSO, A TOTAL        *        
007900*    UNIT AND TOTAL DOLLAR AMT WILL BE DISPLAYED.                *        
008000*                                                                *        
003700*   INPUT:     CONTROL-FILE                                      *        
003800*              SALES AUDIT TABLE                                 *        
003900*                                                                *        
004000*   OUTPUT:    DAILY SALES TABLE                                *         
004100*                                                                *        
004200*   I/O:       NONE.                                             *        
004400*                                                                *        
008100******************************************************************        
008200*  CHANGE MADE: NEW PROGRAM                                      *        
008400*      MADE BY:  BOB FORD                                        *        
008500*         DATE:  06/29/04                 WR# 26355              *        
008100******************************************************************        
008200*  CHANGE MADE: REMOVED DISPLAYS.                                *        
008400*      MADE BY:  BOB FORD                                        *        
008500*         DATE:  07/21/04                 WR# 26355              *        
008600******************************************************************        
008700 ENVIRONMENT DIVISION.                                                    
008800******************************************************************        
008900 CONFIGURATION SECTION.                                                   
009000******************************************************************        
009100 SOURCE-COMPUTER.    IBM-3090.                                            
009200 OBJECT-COMPUTER.    IBM-3090.                                            
009300******************************************************************        
009400 INPUT-OUTPUT SECTION.                                                    
009500******************************************************************        
009600 FILE-CONTROL.                                                            
009700******************************************************************        
010101 DATA DIVISION.                                                           
010102*****************************************************************         
010104 FILE SECTION.                                                            
010130******************************************************************        
010140 WORKING-STORAGE SECTION.                                                 
010150*                                                                         
00121 ******************************************************************        
00122 *  CONTROL CARD LAYOUT FOR PROCESSING-DATE                                
00123 ******************************************************************        
017200 01  CC-DATES.                                                            
017300     05  CC-PRCG-DTE                   PIC X(10).                         
013600*                                                                         
013700 01  ABEND-CODE                    PIC S9(04)    VALUE ZEROES             
013800                                                 COMP                     
013900                                                 SYNC.                    
014000*                                                                         
014100******************************************************************        
014200*  THIS DETAIL LINE IS PRINTED OUT FOR AN ABEND.                 *        
014300******************************************************************        
014400 01  DL-DETAIL-LINES.                                                     
014500     05  DL-ERROR-LINE.                                                   
014600         10  FILLER                PIC X(08)     VALUE SPACES.            
014700         10  FILLER                PIC X(12)     VALUE                    
014800             'ERROR MSG = '.                                              
014900         10  DL-ERR-MSG            PIC X(30)     VALUE SPACES.            
015000         10  FILLER                PIC X(05)     VALUE SPACES.            
015100         10  FILLER                PIC X(13)     VALUE                    
015200             'RECORD-KEY = ' .                                            
015300         10  DL-ERR-KEY.                                                  
015400             15  DL-ERR-SKU        PIC 9(08)     VALUE ZERO.              
015500             15  FILLER            PIC X(01)     VALUE SPACES.            
015600             15  DL-ERR-STORE      PIC 9(05)     VALUE ZERO.              
015700             15  FILLER            PIC X(01)     VALUE SPACES.            
016100         10  FILLER                PIC X(05)     VALUE SPACES.            
016200         10  FILLER                PIC X(11)     VALUE                    
016300             'SQLCODE = '.                                                
016400         10  DL-ERR-SQLCODE        PIC S9(04)    VALUE ZEROES.            
016500         10  FILLER                PIC X(05)     VALUE SPACES.            
016600*                                                                         
016700 01  PC-PROGRAM-CONSTANTS.                                                
016800     05  PC-JOB-NAME               PIC X(08)     VALUE 'MHK025  '.        
016900     05  PC-PROGRAM-NAME           PIC X(08)     VALUE 'MHKUP007'.        
017100     05  PC-DUP-DB2-REC            PIC S9(09)    VALUE -803               
017200                                                 COMP-4 SYNC.             
017300     05  PC-DB2-CONTENTION         PIC S9(09)    VALUE -904               
017400                                                 COMP-4 SYNC.             
017500     05  PC-DB2-DEADLOCK           PIC S9(09)    VALUE -911               
017600                                                 COMP-4 SYNC.             
017700     05  PC-DB2-CONTENTION-LIMIT   PIC S9(02)    VALUE +3.                
017800     05  PC-ERROR-CODES.                                                  
017900         10  PC-SQL-ERROR          PIC S9(04)    VALUE +4013.             
018000*                                                                         
018100****  ERROR LINES - INSERT/UPDATE ERROR DISPLAYS                          
018200     05  PE-ERROR-DB2-PARMS-L1     PIC X(60)    VALUE SPACES.             
018300     05  PE-ERROR-DB2-PARMS-L2     PIC X(60)    VALUE SPACES.             
018400     05  PE-ERROR-MSG-L1.                                                 
018500         10 FILLER                 PIC X(08)     VALUE                    
018600             'PRCG-DT='.                                                  
018700         10 PE-PROCESS-DATE-10     PIC X(10).                             
018800         10 FILLER                 PIC X(01)     VALUE SPACES.            
018900         10 FILLER                 PIC X(08)     VALUE                    
019000             'SKU='.                                                      
019100         10 PE-SKU                 PIC 9(08).                             
019200         10 FILLER                 PIC X(01)     VALUE SPACES.            
019300         10 FILLER                 PIC X(08)     VALUE                    
019400             'STORENO='.                                                  
019500         10 PE-STORENO             PIC 9(05).                             
019600         10 FILLER                 PIC X(11)     VALUE SPACES.            
019700     05  PE-ERROR-MSG-L2.                                                 
019800         10 FILLER                 PIC X(10)     VALUE                    
019900             'STAT-CDE='.                                                 
020000         10 PE-SKU-STAT-CD         PIC X(02).                             
020100         10 FILLER                 PIC X(01)     VALUE SPACES.            
020200         10 FILLER                 PIC X(10)     VALUE                    
020300             'SALE-QTY ='.                                                
020400         10 PE-SALE-QTY            PIC 9(09).                             
020500         10 FILLER                 PIC X(01)     VALUE SPACES.            
020600         10 FILLER                 PIC X(10)     VALUE                    
020700             'SALE-AMT ='.                                                
020800         10 PE-SALE-AMT            PIC 9(09).99.                          
020900         10 FILLER                 PIC X(10)     VALUE SPACES.            
021000*                                                                         
021100 01  PA-DB2-ERROR-ACCUMULATORS                   COMP-3.                  
021200     05  PA-DB2-CONTENTION-CTR     PIC S9(3)     VALUE 0.                 
021400*                                                                         
028500 01  PROGRAM-SWITCHES.                                                    
028600     05  PS-EOF-CONTROL-FILE         PIC  X(01)  VALUE 'N'.               
028700         88  EOF-CONTROL-FILE                    VALUE 'Y'.               
028800     05  PS-EOF-SALES-CURSOR         PIC  X(01)  VALUE 'N'.               
028900         88  EOF-SALES-CURSOR                    VALUE 'Y'.               
029000     05  PS-FIRST-COMMIT-SW          PIC  X(01)  VALUE 'N'.               
029100         88  PS-FIRST-COMMIT                     VALUE 'Y'.               
029200*                                                                         
029300 01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
029400     05  PA-ST-INPUT           PIC S9(7)     VALUE 0.                     
029400     05  PA-ST-INSERTS         PIC S9(7)     VALUE 0.                     
029500     05  PA-INPUT-AMT          PIC S9(9)V99  VALUE 0.                     
029600     05  PA-INPUT-QTY          PIC S9(7)     VALUE 0.                     
029700     05  PA-COMMIT-CNT         PIC  9(7)     VALUE 0.                     
029800*                                                                         
033700 01  PI-PROGRAM-INDICATORS.                                               
033800     05  PI-EOF-PROCESSED-IND  PIC X(01)     VALUE 'N'.                   
033900         88  PI-EOF-PROCESSED                VALUE 'Y'.                   
034000     05  PI-DB2-CALL-COMPLETE-IND                                         
034100                               PIC X(01)     VALUE 'N'.                   
034200         88  PI-DB2-CALL-COMPLETE            VALUE 'Y'.                   
       01 FST00005-SLD-UPD           PIC  S9(7)    COMP.                        
034300*                                                                         
034400 01  PV-PROGRAM-VARIABLES.                                                
035300     05  PV-PARAGRAPH-NAME     PIC X(30)     VALUE SPACES.                
035400     05  PV-DB2-SALES-TYPE         PIC X(02)     VALUE SPACES.            
035500     05  PV-ITEM-NBR               PIC S9(15)V COMP-3                     
035600                                                 VALUE ZERO.              
035500     05  PV-HOLD-SKU               PIC S9(8)V COMP-3                      
035600                                                 VALUE ZERO.              
035500     05  PV-SKU                    PIC S9(8)V COMP-3                      
035600                                                 VALUE ZERO.              
035700     05  PV-COMMIT-TIMESTAMP       PIC X(26)     VALUE SPACES.            
035800     05  PV-CURRENT-TIMESTAMP      PIC X(26)     VALUE SPACES.            
036300     05  PV-NULL-IND-UNT           PIC S9(04)    COMP.                    
036400     05  PV-NULL-IND-AMT           PIC S9(04)    COMP.                    
036500     05  PV-SALES-QTY              PIC S9(7)V COMP-3.                     
           05  PV-PROCESS-DATE-10        PIC X(10)     VALUE SPACES.            
036600                                                                          
088500*****************************************************************         
088600*     FORMATTED FIELDS FOR END OF JOB DISPLAY COUNTS.           *         
088700*****************************************************************         
088800     05  PV-DIS-SALES-AMT      PIC ZZZ,ZZZ,ZZ9.99-                        
088900                                         VALUE ZEROES.                    
089000     05  PV-DIS-SALES-QTY      PIC Z,ZZZ,ZZ9-                             
089100                                         VALUE ZEROES.                    
089200     05  PV-DIS-RECORDS-READ   PIC Z,ZZZ,ZZ9 VALUE ZEROES.                
089400     05  PV-DIS-DLY-SLS-INS    PIC Z,ZZZ,ZZ9 VALUE ZEROES.                
089700     05  PV-DIS-COMMITS        PIC Z,ZZZ,ZZ9 VALUE ZEROES.                
089800*                                                                         
089900*****************************************************************         
090000*  HOLD AREA FOR SKU/SALES INFORMATION                          *         
090100*****************************************************************         
090200     05  SK-SAVE-KEYS.                                                    
090300         10  SK-SKU                PIC S9(8)V   COMP-3                    
090400                                                 VALUE ZEROES.            
091400*                                                                         
092500*                                                                         
092600 01  CR-CHECKPOINT-RESTART-AREA.                                          
092700     05  CR-AREA-LEN                 PIC S9(04) VALUE +47  COMP.          
092800                                                                          
092900     05  CR-CHECKPOINT-RESTART-VAR.                                       
093000         10  CR-SKU                 PIC S9(8)V  VALUE ZERO.               
029400         10  CR-ST-INPUT            PIC S9(7)     VALUE 0.                
029400         10  CR-ST-INSERTS          PIC S9(7)     VALUE 0.                
029500         10  CR-INPUT-AMT           PIC S9(9)V99  VALUE 0.                
029600         10  CR-INPUT-QTY           PIC S9(7)     VALUE 0.                
029700         10  CR-COMMIT-CNT          PIC  9(7)     VALUE 0.                
093100*                                                                         
093200*                                                                         
093300****************************************************************          
093400* CALENDAR ROUTINE WORK AREA                                              
093500****************************************************************          
093600     COPY DPWS500.                                                        
093700*                                                                         
093800******************************************************************        
093900*  DCLGEN MEMBER FOR THE FSTT_OH_SLS_AUD TABLE                   *        
094000******************************************************************        
094100     EXEC SQL                                                             
094200         INCLUDE FST00005                                                 
094300     END-EXEC.                                                            
094400*                                                                         
094500******************************************************************        
094600*  DCLGEN MEMBER FOR THE MHT_DLS_SLS TABLE                       *        
094700******************************************************************        
094800     EXEC SQL                                                             
094900         INCLUDE MHT00000                                                 
095000     END-EXEC.                                                            
095100*                                                                         
094500******************************************************************        
094600*  DCLGEN MEMBER FOR THE TSKXREF TABLE                       *            
094700******************************************************************        
094800     EXEC SQL                                                             
094900         INCLUDE TSKXREF                                                  
095000     END-EXEC.                                                            
095100*                                                                         
095900******************************************************************        
096000*  DCLGEN FOR THE CHECKPOINT RESTART CONTROL TABLE                        
096100******************************************************************        
096200     EXEC SQL                                                             
096300         INCLUDE TCKRSTCN                                                 
096400     END-EXEC.                                                            
096500*                                                                         
096600******************************************************************        
096700*  COBOL DECLARATIONS FOR THE CHECKPOINT RESTART INFO TABLE               
096800******************************************************************        
096900     EXEC SQL                                                             
097000         INCLUDE TCKRSTIN                                                 
097100     END-EXEC.                                                            
097200*                                                                         
097300******************************************************************        
097400*  COMMUNICATIONS AREA FOR DB2                                   *        
097500******************************************************************        
097600     EXEC SQL                                                             
097700         INCLUDE SQLCA                                                    
097800     END-EXEC.                                                            
097900*                                                                         
098000******************************************************************        
098100*  SQL ERROR AREA                                                *        
098200******************************************************************        
098300     COPY SQLCA2.                                                         
098400*                                                                         
098500******************************************************************        
098600*  SALES TRANSACTION CURSOR                                      *        
098700******************************************************************        
098800*                                                                         
098900     EXEC SQL                                                             
099000       DECLARE SALES_CSR CURSOR WITH HOLD FOR                             
099100         SELECT B.SKU                                                     
099200               ,A.LOC_NBR                                                 
099300               ,A.SKU_STAT_CDE                                            
099400               ,SUM(A.SLD_QTY)                                            
099500               ,SUM(A.NET_CHRGD_AMT)                                      
099600           FROM  FSTT_OH_SLS_AUD A                                        
099700               , TSKXREF B                                                
099800          WHERE A.ITM_NBR = B.ITM_NBR                                     
099900            AND A.PRCG_DTE = :CC-PRCG-DTE                                 
100000            AND A.TRN_STAT_CDE = 'C'                                      
100100            AND B.SKU > :PV-SKU                                           
100200          GROUP BY B.SKU                                                  
100300                  ,A.LOC_NBR                                              
100400                  ,A.SKU_STAT_CDE                                         
100500           WITH UR                                                        
100600           FOR FETCH ONLY                                                 
101000     END-EXEC.                                                            
101200*                                                                         
101300******************************************************************        
101400 PROCEDURE DIVISION.                                                      
101500******************************************************************        
101600*  THIS IS THE MAIN MODULE.                                      *        
101700*  PERFORMS:   1.  HOUSEKEEPING                                  *        
101800*              2.  MAIN ROUTINE UNTIL EOF                        *        
101900*              3.  EOF PROCESSING                                *        
102000******************************************************************        
102100*                                                                         
102200     PERFORM 1000-HOUSEKEEPING.                                           
102300*                                                                         
102400     PERFORM 2000-MAIN-ROUTINE                                            
102500         UNTIL EOF-SALES-CURSOR.                                          
102600*                                                                         
102700     PERFORM 9000-EOF-ROUTINE.                                            
102800     STOP RUN.                                                            
102900                                                                          
103000*                                                                         
103100******************************************************************        
103200*  DOES THE FOLLOWING:                                           *        
103300*     1.  ACCEPT PROCESSING-DATE CARD                            *        
103400*     2.  CHECK IF IN RESTART STATUS                             *        
103500*     3.  OPEN CURSOR & FETCH FIRST ROW                          *        
103600*     4.  SET UP SAVE-KEYS BASED ON FIRST ROW READ               *        
103700******************************************************************        
103800 1000-HOUSEKEEPING.                                                       
103900*                                                                         
104000     DISPLAY '*************************************************'.         
104100     DISPLAY '*** START OF MHKUP007 - SUMMARY TABLE UPDATE  ***'.         
104200     DISPLAY '*************************************************'.         
104300     DISPLAY ' '.                                                         
104400*                                                                         
      *  ACCEPT CONTROL CARD TO GET PROCESSING-DATE FOR CURSOR                  
           ACCEPT CC-DATES.                                                     
105000*                                                                         
           DISPLAY '** MHKUP007 CONTROL-CARD: ' CC-PRCG-DTE.                    
                                                                                
           PERFORM 1110-VALIDATE-CC-DATE.                                       
                                                                                
105100     PERFORM 8100-INIT-CHECKPOINT-SELECT.                                 
105200                                                                          
105300**   IF PROGRAM IS IN A RESTART STATUS, SELECT SAVED DATA FROM            
105400**   THE RESTART INFO TABLE.                                              
105500*                                                                         
105600     IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
               DISPLAY 'RESTARTING FROM FAILED STEP'                            
               DISPLAY 'SKU:' CR-SKU                                            
105700         PERFORM 8200-SELECT-RESTART-INFO                                 
105800         MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                            
105900                            CR-CHECKPOINT-RESTART-VAR                     
106000         MOVE CR-SKU             TO PV-SKU                                
106000         MOVE CR-ST-INPUT        TO PA-ST-INPUT                           
106000         MOVE CR-ST-INSERTS      TO PA-ST-INSERTS                         
106000         MOVE CR-INPUT-AMT       TO PA-INPUT-AMT                          
106000         MOVE CR-INPUT-QTY       TO PA-INPUT-QTY                          
106000         MOVE CR-COMMIT-CNT      TO PA-COMMIT-CNT                         
106100     ELSE                                                                 
106200         SET PS-FIRST-COMMIT TO TRUE                                      
106200         MOVE ZERO               TO PV-SKU                                
106300     END-IF.                                                              
106400*                                                                         
106500     PERFORM 7600-OPEN-SALES-CURSOR.                                      
106600*                                                                         
106700     PERFORM 7000-FETCH-SALES-CSR.                                        
106800*                                                                         
106900     IF EOF-SALES-CURSOR                                                  
107000        SET PI-EOF-PROCESSED TO TRUE                                      
107100     ELSE                                                                 
107200        MOVE SKXREF-SKU            TO SK-SKU                              
107700     END-IF.                                                              
107800                                                                          
107900*                                                                         
      ******************************************************************        
      *  CHECK THAT THE CONTROL CARD DATE IS A VALID DATE                       
      ******************************************************************        
       1110-VALIDATE-CC-DATE.                                                   
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-JAN-DEC-F2                                          
               DPG51-DO-NOT-INCR-DECR-DATE                                      
               DPG52-LK-DTE-ISO-FMT        TO TRUE.                             
                                                                                
           MOVE CC-PRCG-DTE TO DPG52-LK-DATE-INPUT                              
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
           IF DPG54-DATE-INVALID                                                
               DISPLAY '** ABEND ** IN MHKUP007'                                
               DISPLAY '** PARAGRAPH: 1110-VALIDATE-CC-DATE.'                   
               DISPLAY '** INVALID PROCESS DATE ON CONTROL CARD.'               
               DISPLAY '** CALENDAR ROUTINE ERROR MESSAGE = '                   
                           DPG54-ERROR-MESSAGE                                  
               DISPLAY '** DPG51 = ' DPG51                                      
               DISPLAY '** DPG52 = ' DPG52                                      
               DISPLAY '** DPG53 = ' DPG53                                      
               DISPLAY '** DPG54 = ' DPG54                                      
               DISPLAY '** DPG55 = ' DPG55                                      
               DISPLAY '** DPG56 = ' DPG56                                      
               MOVE +4003 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           ELSE                                                                 
               IF DPG54-SEVERE-ERROR                                            
                   DISPLAY '** ABEND ** IN MHKUP007'                            
                   DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE.'           
                   DISPLAY '** CALLED BY 1110-VALIDATE-CC-DATE.'                
                   DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE            
                   DISPLAY '** DPG51 = ' DPG51                                  
                   DISPLAY '** DPG52 = ' DPG52                                  
                   DISPLAY '** DPG53 = ' DPG53                                  
                   DISPLAY '** DPG54 = ' DPG54                                  
                   DISPLAY '** DPG55 = ' DPG55                                  
                   DISPLAY '** DPG56 = ' DPG56                                  
                   MOVE +4003 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               ELSE                                                             
                   IF DPG54-WARNING-ERROR                                       
                       DISPLAY                                                  
                             '** WARNING RETURNED BY CALENDAR ROUTINE.'         
                       DISPLAY                                                  
                             '** CALLED BY 1110-VALIDATE-CC-DATE.'              
                       DISPLAY                                                  
                             '** WARNING MESSAGE = ' DPG54-ERROR-MESSAGE        
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE DPG55-DB2-ISO-DATE-FMT TO PV-PROCESS-DATE-10.                   
                                                                                
112100                                                                          
112200*                                                                         
112300******************************************************************        
112400*  THIS PARAGRAPH DOES THE FOLLOWING:                            *        
112500*     1.  CHECK FOR ANY BREAK IN KEYS.  PERFORM TO UPDATE        *        
112600*         STORE 000 RECORD OF CORRESPONDING DB2 TABLES           *        
112700*         DEPENDING ON WHICH KEYS CHANGED.  STORE-000 IS THE     *        
112800*         SUMMARY RECORD.                                        *        
112900*     2.  PERFORM TO UPDATE DB2 TABLES BASED ON RECORD JUST READ *        
113000*     3.  PERFORM TO ACCUMULATE QTY AND AMT.  THESE              *        
113100*         ACCUMULATED VALUES WILL BE USED TO UPDATE THE STORE 000*        
113200*         RECORD WHEN KEY CHANGES (SEE #1 ABOVE).                *        
113300*     4.  FETCHES THE NEXT ROW FROM THE CURSOR                   *        
113400******************************************************************        
113500 2000-MAIN-ROUTINE.                                                       
117400*                                                                         
117500*    SAVE SKU FOR NEW ITEM                                                
117600*                                                                         
117700     MOVE SKXREF-SKU            TO SK-SKU.                                
118200*                                                                         
118300*    INSERT SPECIFIC SKU/STORE ROWS FOR MHT_DLY_SLS                       
118400*                                                                         
118900     PERFORM 2300-INSERT-MHT-DLY-SLS                                      
119200     PERFORM 7000-FETCH-SALES-CSR                                         
113900*                                                                         
113700*    CHECK FOR COMMIT WHEN ITEM-NUMBER CHANGES                            
113900*                                                                         
114000     IF EOF-SALES-CURSOR OR                                               
114100        (SKXREF-SKU NOT = SK-SKU)                                         
115000         MOVE SK-SKU TO CR-SKU                                            
115100         PERFORM 8500-COMMIT-PROCESSING                                   
115400     END-IF.                                                              
115500*                                                                         
119600******************************************************************        
119700*  ADDS A ROW TO THE MHT_DLY_SLS TABLE                                    
120000******************************************************************        
120100 2300-INSERT-MHT-DLY-SLS.                                                 
120200*                                                                         
120700     MOVE 'N'      TO PI-DB2-CALL-COMPLETE-IND                            
120800     MOVE ZERO TO PA-DB2-CONTENTION-CTR                                   
120900                                                                          
121800     PERFORM 2400-INSERT-DLY-SLS                                          
121900         UNTIL PI-DB2-CALL-COMPLETE.                                      
122000                                                                          
115500*                                                                         
119600******************************************************************        
119700*  ADDS A ROW TO THE MHT_DLY_SLS TABLE                                    
120000******************************************************************        
120100 2400-INSERT-DLY-SLS.                                                     
129800     EXEC SQL                                                             
129900         INSERT INTO MHT_DLY_SLS                                          
130000                     (PRCG_DTE,                                           
130100                      SKU_NBR,                                            
130200                      STR_NBR,                                            
130300                      SKU_STAT_CDE,                                       
130400                      SLS_UNT_QTY,                                        
130500                      SLS_AMT                                             
130600                      )                                                   
131300             VALUES  (:PV-PROCESS-DATE-10                                 
131400                     ,:SKXREF-SKU                                         
131500                     ,:FST00005-LOC-NBR                                   
131600                     ,:FST00005-SKU-STAT-CDE                              
131700                     ,:FST00005-SLD-UPD                                   
131800                     ,:FST00005-NET-CHRGD-AMT                             
130600                      )                                                   
132600     END-EXEC.                                                            
132700*                                                                         
132800     IF SQLCODE = ZEROES                                                  
132900         ADD 1 TO PA-ST-INSERTS                                           
159115         ADD 1            TO PA-ST-INPUT                                  
160500         ADD FST00005-SLD-UPD TO PA-INPUT-QTY                             
160600         ADD FST00005-NET-CHRGD-AMT TO PA-INPUT-AMT                       
133000         MOVE 'Y' TO PI-DB2-CALL-COMPLETE-IND                             
133100     ELSE                                                                 
133200         IF SQLCODE NOT = PC-DUP-DB2-REC                                  
133300             MOVE '2400-INSERT-DLY-SLS        '                           
133400                                 TO PV-PARAGRAPH-NAME                     
133500             MOVE 'INSERT ERROR ON MHT_DLY_SLS   '                        
133600                                 TO DL-ERR-MSG                            
133700             MOVE PV-PROCESS-DATE-10 TO PE-PROCESS-DATE-10                
133700             MOVE SKXREF-SKU         TO PE-SKU                            
133800             MOVE FST00005-LOC-NBR   TO PE-STORENO                        
134100             MOVE FST00005-SLD-UPD   TO PE-SALE-QTY                       
134200             MOVE FST00005-SKU-STAT-CDE TO PE-SKU-STAT-CD                 
134300             MOVE PE-ERROR-MSG-L1    TO PE-ERROR-DB2-PARMS-L1             
134400             MOVE PE-ERROR-MSG-L2    TO PE-ERROR-DB2-PARMS-L2             
134500             PERFORM 9500-ERROR-ROUTINE                                   
134600         ELSE                                                             
134700             MOVE 'Y' TO PI-DB2-CALL-COMPLETE-IND                         
134800         END-IF                                                           
134900     END-IF.                                                              
135000                                                                          
120200*                                                                         
158700*                                                                         
158800******************************************************************        
158900*  FETCH A ROW FROM THE SALES CURSOR                                      
159000******************************************************************        
159100 7000-FETCH-SALES-CSR.                                                    
159101*                                                                         
159102     EXEC SQL                                                             
159103         FETCH SALES_CSR                                                  
159104          INTO :SKXREF-SKU                                                
159105             , :FST00005-LOC-NBR                                          
159106             , :FST00005-SKU-STAT-CDE                                     
159108             , :FST00005-SLD-UPD        :PV-NULL-IND-UNT                  
159109             , :FST00005-NET-CHRGD-AMT  :PV-NULL-IND-AMT                  
159112     END-EXEC                                                             
159113     EVALUATE TRUE                                                        
159114         WHEN SQLCODE = ZERO                                              
159115             IF (PV-NULL-IND-UNT = -1)                                    
159116                MOVE ZERO TO FST00005-SLD-UPD                             
159117             END-IF                                                       
159118             IF (PV-NULL-IND-AMT = -1)                                    
159119                MOVE ZERO TO FST00005-NET-CHRGD-AMT                       
159120             END-IF                                                       
159121                                                                          
159122         WHEN SQLCODE = +100                                              
159123             SET EOF-SALES-CURSOR TO TRUE                                 
159124         WHEN SQLWARN0 NOT = SPACES                                       
159125         WHEN SQLCODE  NOT = ZERO                                         
159126             MOVE '7000-FETCH-SALES-CSR          '                        
159127                                 TO PV-PARAGRAPH-NAME                     
159128             MOVE 'FETCH ERROR ON SALES_CSR      '                        
159129                                 TO DL-ERR-MSG                            
159130             MOVE SPACES TO PE-ERROR-DB2-PARMS-L1                         
159140                            PE-ERROR-DB2-PARMS-L2                         
159150             PERFORM 9500-ERROR-ROUTINE                                   
159160     END-EVALUATE                                                         
159170*                                                                         
159180     IF NOT EOF-SALES-CURSOR                                              
159190        PERFORM 7100-PREPARE-INPUT-RECORD                                 
159200     END-IF.                                                              
159300                                                                          
159400*                                                                         
159500******************************************************************        
159600*  PREPARATION OF FIELDS BASED ON ROW JUST READ                  *        
159700*     1.  INITIALIZE WS FIELDS & INCREMENT COUNTERS              *        
159800*     2.  MOVE KEYS TO WORK-AREA FOR LATER COMPARISON            *        
159900******************************************************************        
160000 7100-PREPARE-INPUT-RECORD.                                               
160100*                                                                         
160200     INITIALIZE DCLMHT00000.                                              
160700*                                                                         
161300                                                                          
222500******************************************************************        
222600*     OPEN CURSOR                                                *        
222700******************************************************************        
222800 7600-OPEN-SALES-CURSOR.                                                  
222801*                                                                         
222802*                                                                         
222803     EXEC SQL                                                             
222804         OPEN SALES_CSR                                                   
222805     END-EXEC                                                             
222806                                                                          
222807     EVALUATE TRUE                                                        
222808         WHEN SQLCODE = ZERO                                              
222809         WHEN SQLCODE = +100                                              
222810             CONTINUE                                                     
222820         WHEN OTHER                                                       
222830             MOVE '7600-OPEN-SALES-CURSOR        '                        
222840                                 TO PV-PARAGRAPH-NAME                     
222850             MOVE 'OPEN ERROR ON SALES_CSR       '                        
222860                                 TO DL-ERR-MSG                            
222870             MOVE SPACES TO PE-ERROR-DB2-PARMS-L1                         
222880                            PE-ERROR-DB2-PARMS-L2                         
222881             PERFORM 9500-ERROR-ROUTINE                                   
222882     END-EVALUATE.                                                        
222883*                                                                         
222884******************************************************************        
222885*     CLOSE CURSOR                                               *        
222886******************************************************************        
222887 7700-CLOSE-SALES-CURSOR.                                                 
222888*                                                                         
222889     EXEC SQL                                                             
222890         CLOSE SALES_CSR                                                  
222891     END-EXEC                                                             
222892                                                                          
222893     EVALUATE TRUE                                                        
222894         WHEN SQLCODE = ZERO                                              
222895             CONTINUE                                                     
222896         WHEN OTHER                                                       
222897             MOVE '7700-CLOSE-SALES-CURSOR       '                        
222898                                 TO PV-PARAGRAPH-NAME                     
222899             MOVE 'CLOSE ERROR ON SALES_CSR      '                        
222900                                 TO DL-ERR-MSG                            
223000             MOVE SPACES TO PE-ERROR-DB2-PARMS-L1                         
223100                            PE-ERROR-DB2-PARMS-L2                         
223101             PERFORM 9500-ERROR-ROUTINE                                   
223102       END-EVALUATE.                                                      
223103*                                                                         
223104******************************************************************        
223105*  ==> PROCESSES:                                                *        
223106*    - SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE TO SEE  *        
223107*      IF WE ARE IN A RESTART CONDITION.                         *        
223108******************************************************************        
223109 8100-INIT-CHECKPOINT-SELECT.                                             
223110                                                                          
223120     EXEC SQL                                                             
223130       SELECT                                                             
223140            CKPT_STAT_CODE                                                
223150         INTO                                                             
223160           :TCKRSTCNTL-CKPT-STAT-CODE                                     
223170         FROM  TCKRSTCNTL                                                 
223180        WHERE  JOB_NAME  = :PC-JOB-NAME                                   
223190          AND  PLAN_NAME = :PC-PROGRAM-NAME                               
223200     END-EXEC.                                                            
223300                                                                          
223400     EVALUATE TRUE                                                        
223500         WHEN SQLCODE = ZERO                                              
223600             CONTINUE                                                     
223700         WHEN OTHER                                                       
223800             MOVE '8100-INIT-CHECKPOINT-SELECT '                          
223900                             TO PV-PARAGRAPH-NAME                         
224000             MOVE 'ERROR ON SELECT OF TCKRSTCNTL '                        
224100                             TO DL-ERR-MSG                                
224200             PERFORM 9500-ERROR-ROUTINE                                   
224300     END-EVALUATE.                                                        
224400                                                                          
224500******************************************************************        
224600*  ==> PROCESSES:                                                *        
224700*    - SELECTS FROM THE CHECKPOINT RESTART INFORMATION TABLE TO  *        
224800*      OBTAIN THE PROGRAM'S SAVED VARIABLES.                     *        
224900******************************************************************        
225000 8200-SELECT-RESTART-INFO.                                                
225100                                                                          
225200     EXEC SQL                                                             
225300         SELECT                                                           
225400            WORK_STRG_DESC                                                
225500         INTO                                                             
225600           :TCKRSTINF-WORK-STRG-DESC                                      
225700         FROM  TCKRSTINF                                                  
225800        WHERE  JOB_NAME  = :PC-JOB-NAME                                   
225900          AND  PLAN_NAME = :PC-PROGRAM-NAME                               
226000     END-EXEC.                                                            
226100                                                                          
226200     EVALUATE TRUE                                                        
226300         WHEN SQLCODE = ZERO                                              
226400             CONTINUE                                                     
226500         WHEN OTHER                                                       
226600             MOVE '8200-SELECT-RESTART-INFO '                             
226700                             TO PV-PARAGRAPH-NAME                         
226800             MOVE 'ERROR ON SELECT OF TCKRSTINF '                         
226900                             TO DL-ERR-MSG                                
227000             PERFORM 9500-ERROR-ROUTINE                                   
227100     END-EVALUATE.                                                        
227200                                                                          
227300     EJECT                                                                
227400******************************************************************        
227500*  ==> PROCESSES:                                                *        
227600*    - CHECK IF WE NEED TO TAKE A COMMIT.                        *        
227700*    - COMMIT THIS UNIT OF WORK.                                 *        
227800******************************************************************        
227900 8500-COMMIT-PROCESSING.                                                  
228000                                                                          
228100     PERFORM 8800-SET-CURRENT-TIMESTAMP.                                  
228200                                                                          
228300     IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                        
228400         IF PS-FIRST-COMMIT                                               
228500             MOVE '9'        TO TCKRSTCNTL-CKPT-STAT-CODE                 
228600             PERFORM 8600-UPDATE-CHECKPOINT-STATUS                        
228700             MOVE 'N'        TO PS-FIRST-COMMIT-SW                        
228800         END-IF                                                           
228900         PERFORM 8900-COMMIT                                              
229000         PERFORM 8700-GET-COMMIT-TIMESTAMP                                
229100     END-IF.                                                              
229200                                                                          
229300******************************************************************        
229400*  ==> PROCESSES:                                                *        
229500*    - UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE        *        
229600******************************************************************        
229700 8600-UPDATE-CHECKPOINT-STATUS.                                           
229800                                                                          
229900     EXEC SQL                                                             
230000       UPDATE  TCKRSTCNTL                                                 
230100          SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
230200        WHERE  JOB_NAME  = :PC-JOB-NAME                                   
230300          AND  PLAN_NAME = :PC-PROGRAM-NAME                               
230400     END-EXEC.                                                            
230500                                                                          
230600     EVALUATE TRUE                                                        
230700         WHEN SQLCODE = ZERO                                              
230800             CONTINUE                                                     
230900         WHEN OTHER                                                       
231000             MOVE '8600-UPDATE-CHECKPOINT-STATUS '                        
231100                             TO PV-PARAGRAPH-NAME                         
231200             MOVE 'ERROR ON UPDATE OF TCKRSTCNTL '                        
231300                             TO DL-ERR-MSG                                
231400             PERFORM 9500-ERROR-ROUTINE                                   
231500     END-EVALUATE.                                                        
231600                                                                          
231700******************************************************************        
231800*  ==> PROCESSES:                                                *        
231900*    - GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT CONTROL      *        
232000*      TABLE.                                                    *        
232100******************************************************************        
232200 8700-GET-COMMIT-TIMESTAMP.                                               
232300                                                                          
232400     EXEC SQL                                                             
232500       SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)               
232600         INTO :PV-COMMIT-TIMESTAMP                                        
232700         FROM TCKRSTCNTL                                                  
232800        WHERE JOB_NAME  = :PC-JOB-NAME                                    
232900          AND PLAN_NAME = :PC-PROGRAM-NAME                                
233000     END-EXEC.                                                            
233100                                                                          
233200     IF SQLCODE = 0                                                       
233300         CONTINUE                                                         
233400     ELSE                                                                 
233500         MOVE '8700-GET-COMMIT-TIMESTAMP       '                          
233600                             TO PV-PARAGRAPH-NAME                         
233700         MOVE 'ERROR ON SELECT OF TIMESTAMP  '                            
233800                             TO DL-ERR-MSG                                
233900         PERFORM 9500-ERROR-ROUTINE                                       
234000     END-IF.                                                              
234100                                                                          
234200******************************************************************        
234300*  ==> PROCESSES:                                                *        
234400*    - GET THE CURRENT TIMESTAMP FROM DB2.                       *        
234500******************************************************************        
234600 8800-SET-CURRENT-TIMESTAMP.                                              
234700                                                                          
234800     EXEC SQL                                                             
234900          SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
235000     END-EXEC.                                                            
235100                                                                          
235200     IF SQLCODE = 0                                                       
235300         CONTINUE                                                         
235400     ELSE                                                                 
235500         MOVE '8800-SET-CURRENT-TIMESTAMP      '                          
235600                             TO PV-PARAGRAPH-NAME                         
235700         MOVE 'ERROR ON SET OF TIMESTAMP     '                            
235800                             TO DL-ERR-MSG                                
235900         PERFORM 9500-ERROR-ROUTINE                                       
236000     END-IF.                                                              
236100                                                                          
236200******************************************************************        
236300*  ==> PROCESSES:                                                *        
236400*    - PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.           *        
236500*    - TAKE A COMMIT.                                            *        
236600******************************************************************        
236700 8900-COMMIT.                                                             
236800                                                                          
106000     MOVE SK-SKU               TO CR-SKU                                  
106000     MOVE PA-ST-INPUT          TO CR-ST-INPUT                             
106000     MOVE PA-ST-INSERTS        TO CR-ST-INSERTS                           
106000     MOVE PA-INPUT-AMT         TO CR-INPUT-AMT                            
106000     MOVE PA-INPUT-QTY         TO CR-INPUT-QTY                            
106000     MOVE PA-COMMIT-CNT        TO CR-COMMIT-CNT                           
236900     MOVE CR-CHECKPOINT-RESTART-AREA TO TCKRSTINF-WORK-STRG-DESC.         
237100     EXEC SQL                                                             
237200       UPDATE TCKRSTINF                                                   
237300          SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
237400        WHERE JOB_NAME  = :PC-JOB-NAME                                    
237500          AND PLAN_NAME = :PC-PROGRAM-NAME                                
237600     END-EXEC.                                                            
237700                                                                          
237800     IF SQLCODE = 0                                                       
236900*        DISPLAY 'PV-CURRENT-TIMESTAMP = '                                
237000*                 PV-CURRENT-TIMESTAMP                                    
236900*        DISPLAY 'CR-CHECKPOINT-RESTART-AREA'                             
236900*        DISPLAY 'CR-SKU       -' CR-SKU                                  
236900*        DISPLAY 'CR-ST-INPUT  -' CR-ST-INPUT                             
236900*        DISPLAY 'CR-ST-INSERTS-' CR-ST-INSERTS                           
236900*        DISPLAY 'CR-INPUT-AMT -' CR-INPUT-AMT                            
236900*        DISPLAY 'CR-INPUT-QTY -' CR-INPUT-QTY                            
236900*        DISPLAY 'CR-COMMIT-CNT-' CR-COMMIT-CNT                           
236900*        DISPLAY ' '                                                      
237900         CONTINUE                                                         
238000     ELSE                                                                 
238100         MOVE '8900-COMMIT                     '                          
238200                             TO PV-PARAGRAPH-NAME                         
238300         MOVE 'ERROR ON UPDATE OF TCKRSTINF  '                            
238400                             TO DL-ERR-MSG                                
238500         PERFORM 9500-ERROR-ROUTINE                                       
238600     END-IF.                                                              
238700                                                                          
238800     EXEC SQL                                                             
238900         COMMIT                                                           
239000     END-EXEC.                                                            
239100                                                                          
239200     IF SQLCODE = 0                                                       
239300         ADD 1  TO PA-COMMIT-CNT                                          
239400     ELSE                                                                 
239500         MOVE '8900-COMMIT                     '                          
239600                             TO PV-PARAGRAPH-NAME                         
239700         MOVE 'ERROR ON COMMIT               '                            
239800                             TO DL-ERR-MSG                                
239900         PERFORM 9500-ERROR-ROUTINE                                       
240000     END-IF.                                                              
240100                                                                          
240200*                                                                         
240300******************************************************************        
240400*     1.  CLOSE SALES CURSOR                                     *        
240500*     2.  UPDATE STATUS ON CHECKPOINT TABLE TO INDICATE          *        
240600*         SUCCESSFUL COMPLETION                                  *        
240700*     3.  DISPLAY CONTROL TOTALS                                 *        
240800******************************************************************        
240900 9000-EOF-ROUTINE.                                                        
241000*                                                                         
241100     PERFORM 7700-CLOSE-SALES-CURSOR.                                     
241200*                                                                         
241300     MOVE '0'  TO TCKRSTCNTL-CKPT-STAT-CODE.                              
241400     PERFORM 8600-UPDATE-CHECKPOINT-STATUS.                               
241500*                                                                         
241600     MOVE PA-ST-INPUT         TO PV-DIS-RECORDS-READ.                     
241700     MOVE PA-INPUT-QTY        TO PV-DIS-SALES-QTY.                        
241800     MOVE PA-INPUT-AMT        TO PV-DIS-SALES-AMT.                        
242000     MOVE PA-ST-INSERTS       TO PV-DIS-DLY-SLS-INS.                      
242300     MOVE PA-COMMIT-CNT       TO PV-DIS-COMMITS.                          
242400*                                                                         
242500     DISPLAY '*************************************************'.         
242600     DISPLAY '*** MHKUP007 - CONTROL TOTALS                 ***'.         
242700     DISPLAY '*************************************************'.         
242800     DISPLAY 'TOTAL RECORDS READ  - ' PV-DIS-RECORDS-READ.                
242900     DISPLAY '*************************************************'.         
243000     DISPLAY 'TOTAL SALES UNITS   - ' PV-DIS-SALES-QTY.                   
243100     DISPLAY '*************************************************'.         
243200     DISPLAY 'TOTAL SALES DOLLARS - ' PV-DIS-SALES-AMT.                   
243300     DISPLAY '*************************************************'.         
243600     DISPLAY 'DLY-SLS  DB2 INSERT - ' PV-DIS-DLY-SLS-INS.                 
244100     DISPLAY '*************************************************'.         
244200     DISPLAY 'TOTAL DB2 COMMITS   - ' PV-DIS-COMMITS.                     
244300     DISPLAY '*************************************************'.         
244400     DISPLAY '*************************************************'.         
244500     DISPLAY '*** MHKUP007 SALES SUMMARY TABLE UPDT COMPLETE  *'.         
244600     DISPLAY '*************************************************'.         
244800*                                                                         
244900******************************************************************        
245000*     SQL EXIT WHEN SQL ERRORS ARE ENCOUNTERED                   *        
245100******************************************************************        
245200 9100-SQL-ERROR.                                                          
245300     MOVE SQLCODE     TO SQLCODE2.                                        
245400     MOVE SQLERRD(3)  TO SQLERRD3.                                        
245500     DISPLAY '** ABEND **    = SQL ERROR'.                                
245600     DISPLAY 'SQL ERROR MSG  = ' SQLERRMC.                                
245700     DISPLAY 'PROGRAM        = ' PC-PROGRAM-NAME.                         
245800     DISPLAY 'PARAGRAPH      = ' PV-PARAGRAPH-NAME.                       
245900     DISPLAY 'SQLCODE        = ' SQLCODE2.                                
246000     IF PE-ERROR-DB2-PARMS-L1 = SPACES                                    
246100        CONTINUE                                                          
246200     ELSE                                                                 
246300        DISPLAY 'PARMS IN DB2 CALL:'                                      
246400        DISPLAY PE-ERROR-DB2-PARMS-L1                                     
246500        IF PE-ERROR-DB2-PARMS-L2 = SPACES                                 
246600           CONTINUE                                                       
246700        ELSE                                                              
246800           DISPLAY PE-ERROR-DB2-PARMS-L2                                  
246900        END-IF                                                            
247000     END-IF.                                                              
247100     DISPLAY 'ROWS PROCESSED = ' SQLERRD3.                                
247200     DISPLAY 'S/T  PROCESSED = ' PA-ST-INPUT.                             
247300*                                                                         
247400     IF SQLCODE = -911                                                    
247500         CONTINUE                                                         
247600     ELSE                                                                 
247700         EXEC SQL                                                         
247800             ROLLBACK                                                     
247900         END-EXEC                                                         
248000     END-IF.                                                              
248100                                                                          
248200     MOVE PC-SQL-ERROR TO ABEND-CODE.                                     
248300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
248400*                                                                         
248500 9200-SQL-WARNING.                                                        
248600******************************************************************        
248700*     SQL EXIT WHEN SQL WARNINGS ARE ENCOUNTERED                 *        
248800******************************************************************        
248900     MOVE SQLCODE     TO SQLCODE2.                                        
249000     MOVE SQLERRD(3)  TO SQLERRD3.                                        
249100     DISPLAY 'SQLCODE = ' SQLCODE2.                                       
249200     DISPLAY '** ABEND **    = SQL WARNING'.                              
249300     DISPLAY 'PROGRAM        = ' PC-PROGRAM-NAME.                         
249400     DISPLAY 'PARAGRAPH      = ' PV-PARAGRAPH-NAME.                       
249500     DISPLAY 'ROWS PROCESSED = ' SQLERRD3.                                
249600     IF PE-ERROR-DB2-PARMS-L1 = SPACES                                    
249700        CONTINUE                                                          
249800     ELSE                                                                 
249900        DISPLAY 'PARMS IN DB2 CALL:'                                      
250000        DISPLAY PE-ERROR-DB2-PARMS-L1                                     
250100        IF PE-ERROR-DB2-PARMS-L2 = SPACES                                 
250200           CONTINUE                                                       
250300        ELSE                                                              
250400           DISPLAY PE-ERROR-DB2-PARMS-L2                                  
250500        END-IF                                                            
250600     END-IF.                                                              
250700     DISPLAY 'S/T  PROCESSED = ' PA-ST-INPUT.                             
250800     DISPLAY '      SQLWARN0 = ' SQLWARN0.                                
250900     DISPLAY '      SQLWARN1 = ' SQLWARN1.                                
251000     DISPLAY '      SQLWARN2 = ' SQLWARN2.                                
251100     DISPLAY '      SQLWARN3 = ' SQLWARN3.                                
251200     DISPLAY '      SQLWARN4 = ' SQLWARN4.                                
251300     DISPLAY '      SQLWARN5 = ' SQLWARN5.                                
251400     DISPLAY '      SQLWARN6 = ' SQLWARN6.                                
251500     DISPLAY '      SQLWARN7 = ' SQLWARN7.                                
251600*                                                                         
251700     EXEC SQL                                                             
251800         ROLLBACK                                                         
251900     END-EXEC.                                                            
252000                                                                          
252100     MOVE PC-SQL-ERROR TO ABEND-CODE.                                     
252200     CALL 'ILBOABN0' USING ABEND-CODE.                                    
252300                                                                          
252400*                                                                         
252500******************************************************************        
252600*   AN ERROR LINE IS WRITTEN AND THE DB2 SQL ERROR HANDLING      *        
252700*   PARAGRAPH IS CALLED IN THIS MODULE.                          *        
252800******************************************************************        
252900 9500-ERROR-ROUTINE.                                                      
253000*                                                                         
253100     IF SQLCODE = PC-DB2-DEADLOCK                                         
253200         MOVE SKXREF-SKU              TO DL-ERR-SKU                       
253300         MOVE FST00005-LOC-NBR        TO DL-ERR-STORE                     
253600         MOVE SQLCODE TO DL-ERR-SQLCODE                                   
253700         DISPLAY DL-ERROR-LINE                                            
253800         MOVE SPACES TO DL-ERR-MSG                                        
253900                        DL-ERR-KEY                                        
254000         PERFORM 9100-SQL-ERROR                                           
254100     ELSE                                                                 
254200         IF SQLCODE = PC-DB2-CONTENTION                                   
254300             ADD +1 TO PA-DB2-CONTENTION-CTR                              
254400             IF PA-DB2-CONTENTION-CTR >= PC-DB2-CONTENTION-LIMIT          
254500                 MOVE SKXREF-SKU              TO DL-ERR-SKU               
254600                 MOVE FST00005-LOC-NBR        TO DL-ERR-STORE             
254900                 MOVE SQLCODE TO DL-ERR-SQLCODE                           
255000                 DISPLAY DL-ERROR-LINE                                    
255100                 MOVE SPACES TO DL-ERR-MSG                                
255200                                DL-ERR-KEY                                
255300                 PERFORM 9100-SQL-ERROR                                   
255400             END-IF                                                       
255500         ELSE                                                             
255600             MOVE SKXREF-SKU              TO DL-ERR-SKU                   
255700             MOVE FST00005-LOC-NBR        TO DL-ERR-STORE                 
256000             MOVE SQLCODE                 TO DL-ERR-SQLCODE               
256100             DISPLAY DL-ERROR-LINE                                        
256200             MOVE SPACES                  TO DL-ERR-MSG                   
256300                                             DL-ERR-KEY                   
256400             IF ((SQLCODE > ZERO AND SQLCODE NOT = +100)                  
256500               OR SQLWARN0 = 'W')                                         
256600                 PERFORM 9200-SQL-WARNING                                 
256700             ELSE                                                         
256800                 PERFORM 9100-SQL-ERROR                                   
256900             END-IF                                                       
257000     END-IF.                                                              
