       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKRP012                                                  
       AUTHOR.        DORSEY JACKSON                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  06-04-2002.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * MLKRP012 - M/L CREATE THE MARKDOWN BY STORE REPORT                      
      ******************************************************************        
      *  THIS PROGRAM READ THE PKMO.CONTROL(MLCC019) FILE TO OBTAIN THE*        
      *  MONTH NAME. ALSO, THE PROGRAM READS THE SORTED MARKDOWN FILE  *        
      *  AND CREATE THE MARKDOWN REPORT BY STORE.                      *        
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ    DATE       PGMR        DESCRIPTION OF CHANGES                
      * -------    ---------- ----------- -----------------------------         
W26603* W26603     03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION          
207210* CHG0207210 08-22-2018 JIM HUNTER  RENAME KDM2810 TO MLWS051             
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INP-MARKDOWN-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CONTROL-FILE                                                  
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT OUT-MARKDOWN-REPORT                                           
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INP-MARKDOWN-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 100 CHARACTERS                                       
           DATA RECORD IS INP-MARKDOWN-RECORD.                                  
                                                                                
       01  INP-MARKDOWN-RECORD            PIC X(100).                           
                                                                                
       FD  CONTROL-FILE                                                         
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS CONTROL-RECORD.                                       
                                                                                
       01  CONTROL-RECORD                 PIC X(80).                            
                                                                                
       FD  OUT-MARKDOWN-REPORT                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS OUT-MARKDOWN-LIST-RECORD.                             
                                                                                
       01  OUT-MARKDOWN-REPORT-RECORD     PIC X(132).                           
                                                                                
       EJECT                                                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      *****************************************************************         
      *   MARKDOWN INPUT RECORD                                                 
      *****************************************************************         
207210     COPY MLWS051.                                                        
                                                                                
      *****************************************************************         
      *   CONTROL INPUT RECORD                                                  
      *****************************************************************         
       01  CD-CONTROL-DATA.                                                     
           05  CD-MONTH                      PIC X(09)   VALUE SPACES.          
           05  FILLER                        PIC X(71)   VALUE SPACES.          
                                                                                
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-LINE-MAX                   PIC S9(4)   VALUE +55              
                                                         COMP SYNC.             
           05  PC-PROGRAM-NAME               PIC X(08) VALUE 'MLKRP012'.        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-INPUT-EOF-SW               PIC X(1)     VALUE 'N'.            
               88  PS-INPUT-EOF                           VALUE 'Y'.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PAGE-COUNTER               PIC S9(4)   VALUE ZEROES           
                                                         COMP SYNC.             
           05  PV-LINE-COUNTER               PIC S9(4)   VALUE 99               
                                                         COMP SYNC.             
           05  PV-IN-COUNTER                 PIC S9(4)   VALUE 99               
                                                         COMP SYNC.             
           05  PV-MONTH-NAME                 PIC X(09)   VALUE SPACES.          
           05  PV-WMKDN                      PIC S9(9)V99 VALUE ZEROS.          
W26603     05  PV-SAVE-D090-STORE-40         PIC X(05)   VALUE SPACES.          
           05  PV-CURR-D090-STORE-40X.                                          
W26603****     10  FILLER                    PIC X(01)   VALUE SPACES.          
W26603         10  PV-CURR-D090-STORE-40     PIC X(05)   VALUE SPACES.          
W26603****     10  FILLER                    PIC X(01)   VALUE SPACES.          
      *                                                                         
      *****************************************************************         
      * KOHL'S STANDARD HEADER                                                  
      *****************************************************************         
           COPY DPWS132O.                                                       
                                                                                
       01  HL-HEADING-LINE1.                                                    
           05  FILLER                        PIC X(60)   VALUE SPACES.          
           05  FILLER                        PIC X(09)   VALUE                  
                                             'MARKDOWNS'.                       
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  FILLER                        PIC X(03)   VALUE 'FOR'.           
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  HL-HEADING-LINE1-MONTH-NAME   PIC X(09)   VALUE SPACES.          
                                                                                
       01  HL-HEADING-LINE2.                                                    
W26603     05  FILLER                        PIC X(27)   VALUE SPACES.          
           05  FILLER                        PIC X(05)   VALUE                  
                                             'STORE'.                           
W26603     05  FILLER                        PIC X(07)   VALUE SPACES.          
           05  FILLER                        PIC X(09)   VALUE                  
                                             'OLD PRICE'.                       
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  FILLER                        PIC X(06)   VALUE SPACES.          
           05  FILLER                        PIC X(09)   VALUE                  
                                             'NEW PRICE'.                       
           05  FILLER                        PIC X(15)   VALUE SPACES.          
           05  FILLER                        PIC X(03)   VALUE                  
                                             'QTY'.                             
           05  FILLER                        PIC X(11)   VALUE SPACES.          
           05  FILLER                        PIC X(08)   VALUE                  
                                             'MARKDOWN'.                        
       01  DL-DETAIL-LINE.                                                      
           05  FILLER                        PIC X(10)   VALUE SPACES.          
           05  FILLER                        PIC X(05)   VALUE                  
                                                         'STORE'.               
           05  FILLER                        PIC X(01)   VALUE SPACE.           
           05  FILLER                        PIC X(06)   VALUE                  
                                                         'TOTALS'.              
W26603     05  FILLER                        PIC X(05)   VALUE SPACES.          
           05  DL-STORE                      PIC X(05)   VALUE SPACES.          
W26603     05  FILLER                        PIC X(05)   VALUE SPACES.          
           05  DL-OLD-PRICE                  PIC Z,ZZZ,ZZZ.99- VALUE            
                                                 ZEROES.                        
           05  FILLER                        PIC X(04)   VALUE SPACES.          
           05  DL-NEW-PRICE                  PIC Z,ZZZ,ZZZ.99- VALUE            
                                                 ZEROES.                        
           05  FILLER                        PIC X(07)   VALUE SPACES.          
           05  DL-QTY                        PIC ZZZ,ZZZ,ZZZ VALUE              
                                                 ZEROES.                        
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  DL-MARKDOWN                   PIC Z(14).99- VALUE                
                                                 ZEROES.                        
           05  FILLER                        PIC X(04)   VALUE SPACES.          
           05  DL-FINAL-TOTALS-MESSAGE       PIC X(12)   VALUE SPACES.          
                                                                                
       01  STORE-TOTALS.                                                        
           05  STORE-OPRICE-TOTAL            PIC S9(11)V99 VALUE ZEROES         
                                                          COMP-3.               
           05  STORE-NPRICE-TOTAL            PIC S9(11)V99 VALUE ZEROES         
                                                          COMP-3.               
           05  STORE-QTY-TOTAL               PIC S9(11) VALUE ZEROES            
                                                          COMP-3.               
           05  STORE-MARKDOWN-TOTAL          PIC S9(11)V99 VALUE ZEROES         
                                                          COMP-3.               
       01  FINAL-TOTALS.                                                        
           05  FINAL-OPRICE-TOTAL            PIC S9(11)V99 VALUE ZEROES         
                                                          COMP-3.               
           05  FINAL-NPRICE-TOTAL            PIC S9(11)V99 VALUE ZEROES         
                                                          COMP-3.               
           05  FINAL-QTY-TOTAL               PIC S9(11) VALUE ZEROES            
                                                          COMP-3.               
           05  FINAL-MARKDOWN-TOTAL          PIC S9(11)V99 VALUE ZEROES         
                                                          COMP-3.               
       01  RUN-TIME.                                                            
           05  R-HR                          PIC 99.                            
           05  R-MIN                         PIC 99.                            
           05  R-SEC                         PIC 99.                            
           05  R-TH                          PIC 99.                            
                                                                                
       01  DATE-IN.                                                             
           05 DATE-IN-YY                     PIC XX.                            
           05 DATE-IN-MM                     PIC XX.                            
           05 DATE-IN-DD                     PIC XX.                            
                                                                                
       EJECT                                                                    
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MLKRP012.                                                           
                                                                                
      ****************************************************************          
      *    OPEN ALL FILES.                                                      
      *    INITIALIZE PAGE HEADINGS                                             
      *    PROCESS AND PRINT RECORDS ON PURCHASE JOURNAL FILE                   
      *    PRINT THE VENDOR TOTALS ON CHANGE IN VENDOR NUMBER                   
      *    PRINT THE BATCH TOTALS ON CHANGE IN BATCH NUMBER                     
      *    PRINT THE GRAND TOTALS WHICH INCLUDE ALL BATCH TOTALS                
      *    AT END-OF-JOB.                                                       
      *    CLOSE ALL FILES.                                                     
      ****************************************************************          
                                                                                
           OPEN INPUT  INP-MARKDOWN-FILE                                        
                       CONTROL-FILE                                             
                OUTPUT OUT-MARKDOWN-REPORT.                                     
                                                                                
           PERFORM 1000-INITIALIZE-HEADER.                                      
                                                                                
           PERFORM 2000-PROCESS                                                 
               UNTIL PS-INPUT-EOF.                                              
                                                                                
           IF PV-PAGE-COUNTER > +0                                              
               PERFORM B100-PRINT-STORE-TOTALS                                  
               PERFORM B200-PRINT-FINAL-TOTALS                                  
           END-IF.                                                              
                                                                                
           CLOSE INP-MARKDOWN-FILE                                              
                 CONTROL-FILE                                                   
                 OUT-MARKDOWN-REPORT.                                           
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-INITIALIZE-HEADER.                                                  
                                                                                
      ****************************************************************          
      *    SET UP THE STANDARD PAGE HEADER.                                     
      *    ACCEPT THE RUN DATE AND RUN TIME FROM THE SYSTEM.                    
      *    PERFORMED BY 0000-MLKRP012.                                          
      ****************************************************************          
           ACCEPT DATE-IN  FROM DATE.                                           
           ACCEPT RUN-TIME FROM TIME.                                           
           MOVE DATE-IN-MM TO DP132O-RUN-MONTH.                                 
           MOVE DATE-IN-DD TO DP132O-RUN-DAY.                                   
           MOVE DATE-IN-YY TO DP132O-RUN-YEAR.                                  
           MOVE R-HR       TO DP132O-RUN-HOUR.                                  
           MOVE R-MIN      TO DP132O-RUN-MINUTE.                                
           MOVE PC-PROGRAM-NAME TO DP132O-PROGRAM-NAME.                         
           MOVE 1               TO DP132O-REPORT-NUMBER.                        
           PERFORM 3000-READ-CONTROL-DATA.                                      
           PERFORM 4000-READ-MARKDOWN-DATA.                                     
           MOVE PV-CURR-D090-STORE-40 TO PV-SAVE-D090-STORE-40.                 
                                                                                
       EJECT                                                                    
                                                                                
       2000-PROCESS.                                                            
                                                                                
      ****************************************************************          
      *    ACCUMULATE STORE TOTALS AND READ THE NEXT INPUT RECORD.              
      *                                                                         
      *    PERFORMED BY 0000-MLKRP012.                                          
      ****************************************************************          
                                                                                
           IF D090-CODE-40 = 40                                                 
               IF PV-CURR-D090-STORE-40 = PV-SAVE-D090-STORE-40                 
                   ADD D090-MKDWN-OPRICE TO STORE-OPRICE-TOTAL                  
                   ADD D090-MKDWN-NPRICE TO STORE-NPRICE-TOTAL                  
                   ADD D090-MKDWN-QTY TO STORE-QTY-TOTAL                        
                   COMPUTE PV-WMKDN = (D090-MKDWN-OPRICE -                      
                                       D090-MKDWN-NPRICE) *                     
                       D090-MKDWN-QTY                                           
                   ADD PV-WMKDN    TO STORE-MARKDOWN-TOTAL                      
               ELSE                                                             
                   PERFORM B100-PRINT-STORE-TOTALS                              
                   MOVE PV-CURR-D090-STORE-40 TO PV-SAVE-D090-STORE-40          
                   ADD D090-MKDWN-OPRICE TO STORE-OPRICE-TOTAL                  
                   ADD D090-MKDWN-NPRICE TO STORE-NPRICE-TOTAL                  
                   ADD D090-MKDWN-QTY TO STORE-QTY-TOTAL                        
                   COMPUTE PV-WMKDN = (D090-MKDWN-OPRICE -                      
                                       D090-MKDWN-NPRICE) *                     
                       D090-MKDWN-QTY                                           
                   ADD PV-WMKDN    TO STORE-MARKDOWN-TOTAL                      
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-MARKDOWN-DATA.                                     
                                                                                
           EJECT                                                                
                                                                                
       B100-PRINT-STORE-TOTALS.                                                 
                                                                                
      ****************************************************************          
      *    FORMAT THE STORE TOTAL PRINT LINE AND ADD STORE TOTALS               
      *    TO FINAL TOTALS.                                                     
      *                                                                         
      *    PERFORMED BY 0000-MLKRP012.                                          
      *    PERFORMED BY 2000-PROCESS.                                           
      ****************************************************************          
                                                                                
           MOVE PV-SAVE-D090-STORE-40  TO DL-STORE.                             
           MOVE STORE-OPRICE-TOTAL     TO DL-OLD-PRICE.                         
           MOVE STORE-NPRICE-TOTAL     TO DL-NEW-PRICE.                         
           MOVE STORE-QTY-TOTAL        TO DL-QTY.                               
           MOVE STORE-MARKDOWN-TOTAL   TO DL-MARKDOWN.                          
                                                                                
           PERFORM Z300-PRINT-TOTALS.                                           
                                                                                
           ADD STORE-OPRICE-TOTAL  TO FINAL-OPRICE-TOTAL.                       
           ADD STORE-NPRICE-TOTAL  TO FINAL-NPRICE-TOTAL.                       
           ADD STORE-QTY-TOTAL     TO FINAL-QTY-TOTAL.                          
           ADD STORE-MARKDOWN-TOTAL TO FINAL-MARKDOWN-TOTAL.                    
           MOVE ZEROES             TO STORE-OPRICE-TOTAL                        
                                      STORE-NPRICE-TOTAL                        
                                      STORE-QTY-TOTAL                           
                                      STORE-MARKDOWN-TOTAL.                     
       B200-PRINT-FINAL-TOTALS.                                                 
                                                                                
      ****************************************************************          
      *    FORMAT THE FINAL TOTAL PRINT LINE                                    
      *                                                                         
      *    PERFORMED BY 0000-MLKRP012.                                          
      ****************************************************************          
                                                                                
           MOVE SPACES                 TO DL-DETAIL-LINE.                       
           MOVE FINAL-OPRICE-TOTAL     TO DL-OLD-PRICE.                         
           MOVE FINAL-NPRICE-TOTAL     TO DL-NEW-PRICE.                         
           MOVE FINAL-QTY-TOTAL        TO DL-QTY.                               
           MOVE FINAL-MARKDOWN-TOTAL   TO DL-MARKDOWN.                          
           MOVE 'FINAL TOTALS'         TO DL-FINAL-TOTALS-MESSAGE.              
                                                                                
           PERFORM Z300-PRINT-TOTALS.                                           
                                                                                
       3000-READ-CONTROL-DATA.                                                  
                                                                                
      ****************************************************************          
      *    READ THE CONTROL DATA FILE.                                          
      *                                                                         
      *    PERFORMED BY 1000-INITIALIZE-HEADER.                                 
      ****************************************************************          
           READ CONTROL-FILE INTO CD-CONTROL-DATA                               
               AT END                                                           
                   PERFORM Z400-ERROR-ROUTINE                                   
                   SET PS-INPUT-EOF TO TRUE                                     
               NOT AT END                                                       
                   MOVE CD-MONTH TO HL-HEADING-LINE1-MONTH-NAME.                
                                                                                
       4000-READ-MARKDOWN-DATA.                                                 
                                                                                
      ****************************************************************          
      *    READ THE MARKDOWN DATA FILE.                                         
      *                                                                         
      *    PERFORMED BY 1000-INITIALIZE-HEADER.                                 
      *    PERFORMED BY 2000-PROCESS.                                           
      ****************************************************************          
           READ INP-MARKDOWN-FILE INTO D090-MKDWN-REC                           
               AT END                                                           
                   SET PS-INPUT-EOF TO TRUE                                     
               NOT AT END                                                       
                   ADD 1 TO PV-IN-COUNTER                                       
                   MOVE D090-STORE-40 TO PV-CURR-D090-STORE-40X.                
                                                                                
       6000-PRINT-HEADERS.                                                      
                                                                                
      ****************************************************************          
      *    PRINT THE PAGE HEADERS.                                              
      *                                                                         
      *    PERFORMED BY B100-PRINT-STORE-TOTALS                                 
      ****************************************************************          
                                                                                
           ADD  1               TO PV-PAGE-COUNTER.                             
                                                                                
           MOVE PV-PAGE-COUNTER TO DP132O-PAGE-NUMBER.                          
                                                                                
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM DP132O-STANDRD-HEADER-132-COLS                              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM HL-HEADING-LINE1                                            
               AFTER ADVANCING 2 LINE.                                          
                                                                                
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM HL-HEADING-LINE2                                            
               AFTER ADVANCING 3 LINE.                                          
                                                                                
           MOVE 08 TO PV-LINE-COUNTER.                                          
                                                                                
           EJECT                                                                
                                                                                
                                                                                
       Z300-PRINT-TOTALS.                                                       
                                                                                
      ****************************************************************          
      *    PRINT TOTAL LINE                                                     
      *                                                                         
      *    PERFORMED BY B100-PRINT-STORE-TOTALS                                 
      *    PERFORMED BY B200-PRINT-FINAL-TOTALS                                 
      ****************************************************************          
                                                                                
           IF PV-LINE-COUNTER >= PC-LINE-MAX                                    
              PERFORM 6000-PRINT-HEADERS                                        
           END-IF.                                                              
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM DL-DETAIL-LINE                                              
               AFTER ADVANCING 2 LINE.                                          
           ADD 2 TO PV-LINE-COUNTER.                                            
                                                                                
           EJECT                                                                
                                                                                
       Z400-ERROR-ROUTINE.                                                      
           DISPLAY '**** NO CONTROL RECORD ON CONTROL FILE ****'.               
                                                                                
