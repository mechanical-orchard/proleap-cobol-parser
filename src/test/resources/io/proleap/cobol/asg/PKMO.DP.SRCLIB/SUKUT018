000010*****************************************************************         
000020 IDENTIFICATION DIVISION.                                                 
000030*****************************************************************         
000040                                                                          
000050 PROGRAM-ID.             SUKUT018.                                        
000060 AUTHOR.                 MICHAEL WALTERS.                                 
000070 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000080 DATE-WRITTEN            OCTOBER, 1997.                                   
000090 DATE-COMPILED.                                                           
000091*****************************************************************         
000092*****************************************************************         
000094*  THIS PROGRAM BUMPS THE RECEIVER/AUDIT FILE AND THE CARTON/  *          
000095*  AUDIT FILE TOGETHER -- WHEN THERE ARE MATCHES, IT WILL CHECK *         
000095*  THE COMBO ERROR FLAGS. IF BOTH ARE ON, IT WILL TURN THE FLAG *         
000095*  ON THE RECEIVER FILE OFF AND WRITE IT OUT.  IF THERE ARE NO            
000095*  MATCHES, JUST RE-WRITE THE RECEIVER FILE.                              
000097*****************************************************************         
000120*****************************************************************         
000130 ENVIRONMENT DIVISION.                                                    
000140*****************************************************************         
000150                                                                          
000160 CONFIGURATION SECTION.                                                   
000170                                                                          
000180 SOURCE-COMPUTER.        IBM-3090.                                        
000190 OBJECT-COMPUTER.        IBM-3090.                                        
000191                                                                          
000192 INPUT-OUTPUT SECTION.                                                    
000193                                                                          
000194 FILE-CONTROL.                                                            
000198*                                                                         
000199     SELECT RECEIVER-AUDIT-INPUT-FILE                                     
000200         ASSIGN TO INP01.                                                 
000201                                                                          
000202     SELECT CARTON-AUDIT-INPUT-FILE                                       
000203         ASSIGN TO INP02.                                                 
000201                                                                          
000202     SELECT RECEIVER-AUDIT-OUTPUT-FILE                                    
000203         ASSIGN TO OUT01.                                                 
000210*                                                                         
000220*****************************************************************         
000230 DATA DIVISION.                                                           
000240*****************************************************************         
000250*                                                                         
000260 FILE SECTION.                                                            
000270*                                                                         
000296 FD  RECEIVER-AUDIT-INPUT-FILE                                            
000297     RECORDING MODE IS F                                                  
000298     BLOCK CONTAINS 0  RECORDS.                                           
000299 01  RECEIVER-AUDIT-INPUT-RECORD     PIC  X(200).                         
000300                                                                          
000301 FD  CARTON-AUDIT-INPUT-FILE                                              
000302     RECORDING MODE IS F                                                  
000303     BLOCK CONTAINS 0  RECORDS.                                           
000304 01  CARTON-AUDIT-INPUT-RECORD       PIC  X(200).                         
000300                                                                          
000301 FD  RECEIVER-AUDIT-OUTPUT-FILE                                           
000302     RECORDING MODE IS F                                                  
000303     BLOCK CONTAINS 0  RECORDS.                                           
000304 01  RECEIVER-AUDIT-OUTPUT-RECORD    PIC  X(200).                         
000305*                                                                         
000310                                                                          
000320 WORKING-STORAGE SECTION.                                                 
000321*                                                                         
000322 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
000323                                                   COMP SYNC.             
000331*                                                                         
000340 01  PROGRAM-SWITCHES.                                                    
000362     05  PS-EOF-RECEIVER-AUDIT-SW    PIC  X(01)    VALUE 'N'.             
000363         88  PS-EOF-RECEIVER-AUDIT                 VALUE 'Y'.             
000364         88  PS-NOT-EOF-RECEIVER-AUDIT            VALUE 'N'.              
000362     05  PS-EOF-CARTON-AUDIT-SW      PIC  X(01)    VALUE 'N'.             
000363         88  PS-EOF-CARTON-AUDIT                   VALUE 'Y'.             
000364         88  PS-NOT-EOF-CARTON-AUDIT               VALUE 'N'.             
000362     05  PS-PROCESS-SW               PIC  X(01)    VALUE ' '.             
000363         88  PS-PROCESS-RECEIVER-AUDIT             VALUE 'V'.             
000364         88  PS-PROCESS-CARTON-AUDIT               VALUE 'A'.             
000364         88  PS-PROCESS-BOTH                       VALUE 'B'.             
000409*                                                                         
000410 01  PC-CONSTANTS.                                                        
000431     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
000432         'SUKUT018'.                                                      
000440*                                                                         
000555******************************************************************        
000556*  RECORD LAYOUT FOR THE CARTON AUDIT EXTRACT FILE                        
000557******************************************************************        
044800 01  CARTON-AUDIT.                                                        
000530     COPY SURD003.                                                        
000511                                                                          
000554*                                                                         
000555******************************************************************        
000556*  RECORD LAYOUT FOR THE RECEIVER AUDIT EXTRACT FILE                      
000557******************************************************************        
044800 01  SU004-COPYBOOK.                                                      
000530     COPY SURD004.                                                        
000511                                                                          
001196 EJECT                                                                    
001499*                                                                         
001500 PROCEDURE DIVISION.                                                      
001510*                                                                         
001520 0000-MAINLINE-PROCESSING.                                                
001530******************************************************************        
001540**  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                              
001550******************************************************************        
001580*                                                                         
001590     PERFORM 1000-INITIALIZATION.                                         
001591*                                                                         
001592     PERFORM 2000-BUMP-FILES                                              
00             UNTIL PS-EOF-CARTON-AUDIT                                        
00               AND PS-EOF-RECEIVER-AUDIT.                                     
001596*                                                                         
001590     PERFORM 9000-EOJ-ROUTINE.                                            
001596*                                                                         
001612     GOBACK.                                                              
001613*                                                                         
001614*                                                                         
001615 1000-INITIALIZATION.                                                     
001620******************************************************************        
001630**  PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.                       
001640******************************************************************        
001650                                                                          
001652     OPEN INPUT  RECEIVER-AUDIT-INPUT-FILE                                
                       CARTON-AUDIT-INPUT-FILE                                  
001653          OUTPUT RECEIVER-AUDIT-OUTPUT-FILE                               
001690                                                                          
           SET PS-NOT-EOF-RECEIVER-AUDIT                                        
               PS-NOT-EOF-CARTON-AUDIT      TO TRUE                             
                                                                                
001691     PERFORM 7000-READ-RECEIVER-AUDIT-FILE.                               
001691     PERFORM 7001-READ-CARTON-AUDIT-FILE.                                 
001692                                                                          
001693     IF PS-EOF-RECEIVER-AUDIT                                             
001695         DISPLAY '** RECEIVER AUDIT EXTRACT IS EMPTY **'                  
001698     END-IF.                                                              
001692                                                                          
001693     IF PS-EOF-CARTON-AUDIT                                               
001695         DISPLAY '** CARTON AUDIT EXTRACT IS EMPTY **'                    
001698     END-IF.                                                              
002150*                                                                         
002150****************************************************************          
002160*  2000-BUMP-FILES.                                                       
002160*  MATCH THE RECORDS OF THE VENDOR OVER/SHORT FILE AND THE                
002160*  RECEIVER OVER/SHORT FILE.                                              
002160*  MATCH THE RECORDS AT THE AUDIT/PO/RECEIVER/SKU LEVEL.                  
002150****************************************************************          
002160 2000-BUMP-FILES.                                                         
                                                                                
           PERFORM 2001-COMPARE-KEYS.                                           
                                                                                
      *    IF BOTH FILES ARE ON MATCHING RECORDS..                              
000364     IF  PS-PROCESS-BOTH                                                  
      *        IF THE FILE HAS AN ERROR IND ON BOTH RECORDS, RESET              
      *        ONE (THE RECEIVER) SO THAT WHEN THIS FIELD IS SUMMED             
      *        UP, THIS PARTICULAR UNIT IS NOT COUNTED TWICE.                   
               IF  SU003-UNT-ERR-CNT-COMBO > 0                                  
               AND SU004-UNT-ERR-CNT-COMBO > 0                                  
                   MOVE +0             TO SU004-UNT-ERR-CNT-COMBO               
               END-IF                                                           
                                                                                
               IF  SU003-PICK-ERR-CNT      > 0                                  
               AND SU004-PICK-ERR-CNT      > 0                                  
                   MOVE +0             TO SU004-PICK-ERR-CNT                    
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
      *    IF THE RECEIVER IS BEHIND THE AUDIT, OR IF THEY ARE MATCHED,         
      *    RE-WRITE THE RECEIVER FILE (WITH ANY CHANGES FROM THE                
      *    SECTION ABOVE) AND READ THE NEXT RECEIVER FILE                       
           IF PS-PROCESS-RECEIVER-AUDIT                                         
000364     OR PS-PROCESS-BOTH                                                   
               PERFORM 8000-WRITE-RECEIVER-AUDIT-FILE                           
001691         PERFORM 7000-READ-RECEIVER-AUDIT-FILE                            
           END-IF.                                                              
                                                                                
      *    READ THE NEXT CARTON AUDIT FILE IF THE CARTON IS BEHIND THE          
      *    RECEIVER FILE, OR IF THE MATCH                                       
           IF PS-PROCESS-CARTON-AUDIT                                           
000364     OR PS-PROCESS-BOTH                                                   
001691         PERFORM 7001-READ-CARTON-AUDIT-FILE                              
           END-IF.                                                              
001692                                                                          
                                                                                
002150****************************************************************          
002160*  2001-COMPARE-KEYS.                                                     
002160*  DETERMINE IF THE FILES MATCH.   IF THEY DON'T, DETERMINE WHICH         
002160*  FILE TO PROCESS                                                        
002150****************************************************************          
002160 2001-COMPARE-KEYS.                                                       
                                                                                
           EVALUATE TRUE                                                        
      *        WHEN THE AUDIT NUMBER IS DIFFERENT                               
002161         WHEN SU003-AUD-CNTL-NBR < SU004-AUD-CNTL-NBR                     
                   SET PS-PROCESS-CARTON-AUDIT       TO TRUE                    
002161         WHEN SU003-AUD-CNTL-NBR > SU004-AUD-CNTL-NBR                     
                   SET PS-PROCESS-RECEIVER-AUDIT     TO TRUE                    
                                                                                
      *        PO NUMBER                                                        
               WHEN SU003-PO-NBR < SU004-PO-NBR                                 
                   SET PS-PROCESS-CARTON-AUDIT       TO TRUE                    
               WHEN SU003-PO-NBR > SU004-PO-NBR                                 
                   SET PS-PROCESS-RECEIVER-AUDIT     TO TRUE                    
                                                                                
      *        RECEIVER NUMBER                                                  
               WHEN SU003-REC-SEQ-NBR < SU004-REC-SEQ-NBR                       
                   SET PS-PROCESS-CARTON-AUDIT       TO TRUE                    
               WHEN SU003-REC-SEQ-NBR > SU004-REC-SEQ-NBR                       
                   SET PS-PROCESS-RECEIVER-AUDIT     TO TRUE                    
                                                                                
      *        SKU NUMBER                                                       
               WHEN SU003-SKU-NBR < SU004-SKU-NBR                               
                   SET PS-PROCESS-CARTON-AUDIT       TO TRUE                    
               WHEN SU003-SKU-NBR > SU004-SKU-NBR                               
                   SET PS-PROCESS-RECEIVER-AUDIT     TO TRUE                    
                                                                                
      *        IF IT IS THE SAME ITEM, PROCESS BOTH RECORDS                     
               WHEN OTHER                                                       
                   SET PS-PROCESS-BOTH               TO TRUE                    
           END-EVALUATE.                                                        
068513                                                                          
068514                                                                          
068514****************************************************************          
068514* 7000-READ-RECEIVER-AUDIT-FILE                                *          
068514* READ THE NEXT RECORD OF THE RECEIVER AUDIT EXTRACT FILE      *          
068514****************************************************************          
068515 7000-READ-RECEIVER-AUDIT-FILE.                                           
068517                                                                          
068518     READ RECEIVER-AUDIT-INPUT-FILE                                       
           INTO SU004-EXTRACT-RECORD                                            
068519        AT END                                                            
068520           SET PS-EOF-RECEIVER-AUDIT TO TRUE                              
068520           MOVE +999999999 TO SU004-AUD-CNTL-NBR                          
068521     END-READ.                                                            
068522                                                                          
068514****************************************************************          
068514* 7001-READ-CARTON-AUDIT-FILE                                  *          
068514* READ THE NEXT RECORD OF THE CARTON AUDIT FILE                *          
068514****************************************************************          
068515 7001-READ-CARTON-AUDIT-FILE.                                             
068517                                                                          
068518     READ CARTON-AUDIT-INPUT-FILE                                         
           INTO SU003-EXTRACT-RECORD                                            
068519        AT END                                                            
068520           SET PS-EOF-CARTON-AUDIT TO TRUE                                
068520           MOVE +999999999 TO SU003-AUD-CNTL-NBR                          
068521     END-READ.                                                            
068523                                                                          
068524*                                                                         
068514****************************************************************          
068514* 8000-WRITE-RECEIVER-AUDIT-FILE                               *          
068514* WRITE THE NEXT RECORD OF THE RECEIVER AUDIT FILE             *          
068514****************************************************************          
068525 8000-WRITE-RECEIVER-AUDIT-FILE.                                          
068526     WRITE RECEIVER-AUDIT-OUTPUT-RECORD                                   
068527       FROM SU004-EXTRACT-RECORD.                                         
068528*                                                                         
068529 9000-EOJ-ROUTINE.                                                        
068531                                                                          
001652     CLOSE       RECEIVER-AUDIT-INPUT-FILE                                
                       CARTON-AUDIT-INPUT-FILE                                  
001653                 RECEIVER-AUDIT-OUTPUT-FILE.                              
001690                                                                          
068534                                                                          
