       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.     INKUT309.                                                
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   12/10/2012.                                              
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      ******************************************************************        
      *PURPOSE: THE PROGRAM TAKES IN A FILE FTP'D FROM INV CONTROL.             
      *         INPUT FILE CONTAINS LOCATION, SKU, QTY AND PRICE                
      *         THIS WAS CREATED FOR RTV BECAUSE THE PRICE CANNOT               
      *         BE LOOKED UP.                                                   
      *         IT WILL DO SOME VALIDATIONS AND THE VALID RECORDS               
      *         WILL BE WRITTEN IN INWS309 FORMAT.                              
      ******************************************************************        
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
      * 24521    12-10-2012  INITIAL CREATION                                   
      ******************************************************************        
P47829******************************************************************        
P47829*    PK47829                 ADDED CHECK TO PREVENT PROCESSING            
P47829*                            BEFORE PLANNED INVENTORY DATE                
P47829*                             IN PARA C200-PERFORM-LOOKUP                 
P47829* INC0729760/INC0729269                                                   
P47829*    PK47829                 FIX FOR THE CHANGE P47829                    
P47829*                                                                         
P47829*                                                                         
P47829* INC0729760/INC0729269                                                   
P47829******************************************************************        
P53347* P53347  02-25-2015 COGNIZANT ADDED CONDITION TO FETCH SINGLE            
P53347*                              ROW FOR CANCELLED LOCATION                 
P53347******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INVENTORY-ADJUSTMENTS-FILE                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUT-EDITED-PHYS-INV-FILE                                      
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT OUT-INVALID-FILE                                              
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INVENTORY-ADJUSTMENTS-FILE                                           
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INVENTORY-ADJUSTMENTS-RECORD  PIC X(80).                             
                                                                                
                                                                                
       FD  OUT-EDITED-PHYS-INV-FILE                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  OUT-EDITED-PHYS-INV-RECORD    PIC X(100).                            
                                                                                
       FD  OUT-INVALID-FILE                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  OUT-INVALID-RECORD            PIC X(80).                             
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
                                                                                
      ******************************************************************   CL**1
      *              P R O G R A M     S W I T C H E S                 *   CL**1
      ******************************************************************   CL**1
        01 PS-PROGRAM-SWITCHES.                                                 
           05  PS-DATA-SW                PIC  X(01)   VALUE 'N'.                
               88  PS-END-OF-DATA                   VALUE 'Y'.                  
               88  PS-NOT-END-OF-DATA               VALUE 'N'.                  
           05  PS-CONTROL-SW             PIC  X(01)   VALUE 'N'.                
               88  PS-END-OF-CONTROL                VALUE 'Y'.                  
               88  PS-NOT-END-OF-CONTROL            VALUE 'N'.                  
           05  PS-VALID-LOC-SW           PIC  X(01).                            
               88  PS-VALID-LOCATION                VALUE 'Y'.                  
               88  PS-INVALID-LOCATION              VALUE 'N'.                  
                                                                                
      ******************************************************************   CL**1
      *              P R O G R A M     V A R I A B L E S               *   CL**1
      ******************************************************************   CL**1
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DISPLAY                PIC  9(11)  VALUE ZERO.                
           05  PV-ADD-NUM                PIC S9(04)  COMP VALUE ZERO.           
           05  PV-ADJ-QTY                PIC S9(09)  COMP VALUE ZERO.           
           05  PV-WRITE-COUNT            PIC S9(09)  COMP-3 VALUE ZERO.         
           05  PV-READ-COUNT             PIC S9(09)  COMP-3 VALUE ZERO.         
           05  PV-INVALID-SKU-STR-COUNT  PIC S9(09)  COMP-3 VALUE ZERO.         
           05  PV-WRITE-NEG-COUNT        PIC S9(09)  COMP-3 VALUE ZERO.         
           05  PV-RETRY-COUNT            PIC S9(04)  COMP VALUE +0.             
           05  PV-PROGRAM-NAME           PIC  X(08)  VALUE 'INKUT309'.          
           05  PV-PARAGRAPH-NAME         PIC  X(30)  VALUE SPACE.               
           05  PV-DB2-OPERATION-ATTEMPTED                                       
                                         PIC  X(30)  VALUE SPACE.               
           05  PV-DB2-OPER-ATTEMPTED     PIC  X(30)  VALUE SPACE.               
           05  PV-SKU-NUMBER             PIC S9(08)V COMP-3.                    
           05  PV-SKU-NBR                PIC  9(08).                            
           05  PV-SKU-NBR-ALPH REDEFINES PV-SKU-NBR.                            
               10  PV-SKU-DEPT           PIC  X(03).                            
               10  PV-SKU-SCL            PIC  X(02).                            
               10  DUMMY                 PIC  X(03).                            
                   88  PV-DUMMY-SKU                  VALUES '000' '999'.        
                                                                                
           05  PV-SKU-SCL-CHAR3.                                                
               10  PV-SKU-SCL-P1         PIC  X(01)  VALUE ZERO.                
               10  PV-SKU-SCL-P2         PIC  X(02)  VALUE ZERO.                
           05  PV-INPUT-LOC              PIC  9(05)  VALUE ZERO.                
           05  PV-HOLD-LOC-NBR           PIC S9(04)  VALUE ZERO COMP.           
           05  PV-PREV-LOC-NBR           PIC S9(04)  VALUE ZERO COMP.           
           05  PV-HOLD-KEY-DATE          PIC  X(10)  VALUE SPACES.              
P47829     05  PV-CURRENT-DATE-DB2.                                             
P47829         10  PV-CD-CCYY.                                                  
P47829             15  PC-CD-CC        PIC 9(02)    VALUE ZERO.                 
P47829             15  PV-CD-YY        PIC 9(02)    VALUE ZERO.                 
P47829         10  PV-CD-MM            PIC 9(02)    VALUE ZERO.                 
P47829         10  PV-CD-DD            PIC 9(02)    VALUE ZERO.                 
P47829     05  PV-CURRENT-DATE.                                                 
P47829         10  PV-CD-DB2-CCYY.                                              
P47829             15  PV-CD-DB2-CC    PIC 9(02)    VALUE ZERO.                 
P47829             15  PV-CD-DB2-YY    PIC 9(02)    VALUE ZERO.                 
P47829         10  FILLER              PIC X(01)    VALUE '-'.                  
P47829         10  PV-CD-DB2-MM        PIC 9(02)    VALUE ZERO.                 
P47829         10  FILLER              PIC X(01)    VALUE '-'.                  
P47829         10  PV-CD-DB2-DD        PIC 9(02)    VALUE ZERO.                 
           05  PV-DOLLARS                PIC  9(04)  VALUE ZEROES.              
           05  PV-CENTS                  PIC  9(02)  VALUE ZEROES.              
           05  PV-PRICE                  PIC  9(05)V99 VALUE ZEROES.            
                                                                                
      ******************************************************************   CL**1
      *          P R O G R A M     A B E N D     C O D E               *   CL**1
      ******************************************************************   CL**1
       01  ABEND-CODE                    PIC S9(4)  COMP VALUE ZERO.            
                                                                                
      ******************************************************************   CL**1
      *              P R O G R A M     L A Y OU T S                    *   CL**1
      ******************************************************************   CL**1
       01  CC-INPUT-LAYOUT.                                                     
           05  CC-REC-NBR                PIC 9(02)  VALUE ZEROES.               
           05  FILLER                    PIC X(01)  VALUE SPACES.               
           05  CC-LOC-NBR                PIC 9(04)  VALUE ZEROES.               
           05  FILLER                    PIC X(01)  VALUE SPACES.               
           05  CC-SKU-NBR                PIC 9(08)  VALUE ZEROES.               
           05  FILLER                    PIC X(01)  VALUE SPACES.               
           05  CC-QTY                    PIC 9(04)  VALUE ZEROES.               
           05  FILLER                    PIC X(01)  VALUE SPACES.               
           05  CC-DOLLARS                PIC 9(05)  VALUE ZEROES.               
           05  CC-DECIMAL                PIC X(01)  VALUE '.'.                  
           05  CC-CENTS                  PIC 9(02)  VALUE ZEROES.               
           05  FILLER                    PIC X(52)  VALUE SPACES.               
      ******************************************************************   CL**1
      *              E R R O R  L A Y O U T S                    *         CL**1
      ******************************************************************   CL**1
       01  CC-ERROR-LAYOUT.                                                     
           05  CC-ERR-REC                PIC 9(02)  VALUE ZEROES.               
           05  FILLER                    PIC X(01)  VALUE SPACES.               
           05  CC-ERR-LOC                PIC 9(04)  VALUE ZEROES.               
           05  FILLER                    PIC X(01)  VALUE SPACES.               
           05  CC-ERR-SKU                PIC 9(08)  VALUE ZEROES.               
           05  FILLER                    PIC X(01)  VALUE SPACES.               
           05  CC-ERR-QTY                PIC 9(04)  VALUE ZEROES.               
           05  FILLER                    PIC X(01)  VALUE SPACES.               
           05  CC-ERR-DOLLARS            PIC 9(05)  VALUE ZEROES.               
           05  CC-ERR-DECIMAL            PIC X(01)  VALUE '.'.                  
           05  CC-ERR-CENTS              PIC 9(02)  VALUE ZEROES.               
           05  FILLER                    PIC X(01)  VALUE SPACES.               
           05  CC-ERR-DESCRIPTION        PIC X(22)  VALUE SPACES.               
           05  FILLER                    PIC X(29)  VALUE SPACES.               
      *                                                                         
      /*****************************************************************        
      *01  IN309-INVENTORY-INPUT-RECORD                                         
           COPY INWS309.                                                        
                                                                                
      ******************************************************************        
      *01  DP004-DB2-ERROR-FIELDS                                               
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TINVPAR                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TMDSEAR                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TAPLAVL                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TAPLAVL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TSKXREF                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TINCNTL                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE XMT00002                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       A100-MAINLINE-DRIVER-SECTION.                                            
                                                                                
           PERFORM B100-INITIALIZATION.                                         
                                                                                
           PERFORM B200-PROCESS-INPUT                                           
                   UNTIL PS-END-OF-DATA.                                        
                                                                                
           PERFORM B300-EOP-PROCESS.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      /****************************************************************         
      * B100-INITIALIZATION.                                                    
      *****************************************************************         
       B100-INITIALIZATION.                                                     
                                                                                
           INITIALIZE DCLTINVPAR.                                               
                                                                                
           OPEN INPUT  INVENTORY-ADJUSTMENTS-FILE                               
                OUTPUT OUT-INVALID-FILE                                         
                OUTPUT OUT-EDITED-PHYS-INV-FILE.                                
                                                                                
           MOVE ZEROS                  TO PV-WRITE-COUNT.                       
           MOVE ZEROS                  TO PV-READ-COUNT.                        
           MOVE 'N'                    TO PS-DATA-SW.                           
                                                                                
           PERFORM Z100-READ-INVENTORY.                                         
P47829*****************************************************************         
P47829     ACCEPT PV-CURRENT-DATE-DB2 FROM DATE YYYYMMDD.                       
P47829     MOVE PV-CD-CCYY TO PV-CD-DB2-CCYY.                                   
P47829     MOVE PV-CD-MM TO PV-CD-DB2-MM.                                       
P47829     MOVE PV-CD-DD TO PV-CD-DB2-DD.                                       
                                                                                
      /****************************************************************         
      * B200-PROCESS-INPUT.                                                     
      *                                                                         
      *****************************************************************         
       B200-PROCESS-INPUT.                                                      
                                                                                
           INITIALIZE IN309-INVENTORY-INPUT-RECORD.                             
                                                                                
           ADD 1                       TO  PV-READ-COUNT.                       
                                                                                
           IF CC-LOC-NBR NOT = INVPAR-LOC-NBR                                   
               MOVE CC-LOC-NBR         TO PV-HOLD-LOC-NBR                       
               MOVE PV-HOLD-LOC-NBR    TO INVPAR-LOC-NBR                        
               PERFORM C100-SELECT-TINVPAR                                      
           END-IF.                                                              
                                                                                
           IF CC-DOLLARS = ZERO AND CC-CENTS =   ZERO                           
               DISPLAY 'PRICE IS ZERO '                                         
                       'LOC      : ' CC-LOC-NBR                                 
                       'SKU      : ' PV-SKU-NUMBER                              
                       'QUANTITY : ' CC-QTY                                     
                       'PRICE    : ' CC-DOLLARS CC-DECIMAL CC-CENTS             
           ELSE                                                                 
               IF CC-QTY = ZERO                                                 
                   DISPLAY 'QTY IS ZERO '                                       
                           'LOC      : ' CC-LOC-NBR                             
                           'SKU      : ' PV-SKU-NUMBER                          
                           'QUANTITY : ' CC-QTY                                 
                           'PRICE    : ' CC-DOLLARS CC-DECIMAL CC-CENTS         
               ELSE                                                             
     **    FORMAT THE PRICE                                                     
                   MOVE  CC-DOLLARS    TO PV-PRICE                              
                   IF CC-CENTS > 0                                              
                       COMPUTE PV-PRICE   = PV-PRICE  + (CC-CENTS / 100)        
                   END-IF                                                       
                   PERFORM C200-PERFORM-LOOKUP                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF CC-LOC-NBR = ZERO                                                 
               DISPLAY 'LOCATION IS ZERO '                                      
                       'LOC:     : ' CC-LOC-NBR                                 
                       'SKU:     : ' PV-SKU-NUMBER                              
                       'QUANTITY : ' CC-QTY                                     
                       'PRICE    : ' CC-DOLLARS CC-DECIMAL CC-CENTS             
           END-IF.                                                              
                                                                                
           MOVE PV-HOLD-LOC-NBR        TO PV-PREV-LOC-NBR.                      
           PERFORM Z100-READ-INVENTORY.                                         
                                                                                
      *                                                                         
      /****************************************************************         
      * B300-EOP-PROCESS.                                                       
      *****************************************************************         
       B300-EOP-PROCESS.                                                        
                                                                                
           CLOSE INVENTORY-ADJUSTMENTS-FILE                                     
                 OUT-INVALID-FILE                                               
                 OUT-EDITED-PHYS-INV-FILE.                                      
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '* * * * * * * * * * * * * * * * * * *'.                     
           DISPLAY '*INKUT309 SUMMARY REPORT:           *'.                     
           DISPLAY '*                                   *'.                     
           DISPLAY '*INVENTORY ADJUSTMENTS              *'.                     
           DISPLAY '*  INVENTORY READ * ', PV-READ-COUNT                        
           DISPLAY '*  INWS009 WRITE  * ', PV-WRITE-COUNT                       
           DISPLAY '*  ERROR RECORDS  * ',PV-INVALID-SKU-STR-COUNT              
           DISPLAY '*                                   *'.                     
           DISPLAY '* * * * * * * * * * * * * * * * * * *'.                     
                                                                                
      *                                                                         
      /****************************************************************         
      * C100-SELECT-TINVPAR.                                                    
      *****************************************************************         
       C100-SELECT-TINVPAR.                                                     
                                                                                
           EXEC SQL                                                             
               SELECT ACTL_INV_DTE                                              
                     ,MAX(PLND_INV_DTE)                                         
               INTO :INVPAR-ACTL-INV-DTE                                        
                   ,:INVPAR-PLND-INV-DTE                                        
               FROM TINVPAR A,                                                  
                    TINCNTL B                                                   
               WHERE B.ACTV_IND = 'Y'                                           
                 AND A.INV_ID = B.INV_ID                                        
                 AND LOC_NBR = :INVPAR-LOC-NBR                                  
                 AND ACTL_FIN_BK_DTE = '9999-09-09'                             
P53347           AND LOC_INV_STAT_CDE = 'IN'                                    
               GROUP BY ACTL_INV_DTE                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    SET PS-VALID-LOCATION         TO TRUE                       
               WHEN SQLCODE = +100                                              
                    SET PS-INVALID-LOCATION       TO TRUE                       
      *****       IF NOT FOUND THEN THE LOCATION IS BAD                         
      *****       DISPLAY BAD LOCATION PROGRAM WILL ABEND LATER                 
                    MOVE 'INVALID INVENTORY LOC'                                
                                                 TO CC-ERR-DESCRIPTION          
                    PERFORM Z300-WRITE-INVALID-SKU-STR                          
               WHEN OTHER                                                       
                    MOVE 'C100-SELECT-TINVPAR'   TO PV-PARAGRAPH-NAME           
                    MOVE 'SELECT TINVPAR'        TO                             
                         PV-DB2-OPERATION-ATTEMPTED                             
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
P47829     IF PS-VALID-LOCATION                                                 
P47829        IF PV-CURRENT-DATE <= INVPAR-PLND-INV-DTE                         
P47829           SET PS-INVALID-LOCATION TO TRUE                                
P47829           MOVE 'PLN_INV NOT COMPLETE' TO CC-ERR-DESCRIPTION              
P47829           PERFORM Z300-WRITE-INVALID-SKU-STR                             
P47829        END-IF                                                            
P47829     END-IF.                                                              
                                                                                
      *                                                                         
      *****************************************************************         
      * C200-PERFORM-LOOKUP.                                                    
      *****************************************************************         
       C200-PERFORM-LOOKUP.                                                     
                                                                                
           MOVE CC-LOC-NBR             TO PV-INPUT-LOC                          
           MOVE PV-INPUT-LOC           TO PV-HOLD-LOC-NBR                       
           MOVE INVPAR-PLND-INV-DTE    TO PV-HOLD-KEY-DATE                      
           MOVE CC-SKU-NBR             TO PV-SKU-NBR                            
           MOVE PV-SKU-NBR             TO PV-SKU-NUMBER                         
                                                                                
                                                                                
                                                                                
           IF PV-HOLD-LOC-NBR = PV-PREV-LOC-NBR                                 
P47829*    OR PV-HOLD-KEY-DATE = SPACES                                         
P47829*       OR PV-CURRENT-DATE <= INVPAR-PLND-INV-DTE                         
P47829*       SET PS-INVALID-LOCATION TO TRUE                                   
P47829*        MOVE 'PLN_INV NOT COMPLETE' TO CC-ERR-DESCRIPTION                
              IF PS-INVALID-LOCATION                                            
                 PERFORM Z300-WRITE-INVALID-SKU-STR                             
              ELSE                                                              
                 CONTINUE                                                       
              END-IF                                                            
           ELSE                                                                 
               IF PS-VALID-LOCATION                                             
                   PERFORM D100-SELECT-XMTLOC                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF PS-VALID-LOCATION                                                 
              IF PV-DUMMY-SKU                                                   
                 PERFORM D200-VALIDATE-DEPT-SCL                                 
              ELSE                                                              
                 PERFORM D300-VALIDATE-SKU-EXIST                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *                                                                         
      ******************************************************************        
      * D100-SELECT-XMTLOC                                                      
      ******************************************************************        
       D100-SELECT-XMTLOC.                                                      
                                                                                
                                                                                
           EXEC SQL                                                             
                                                                                
               SELECT A.LOC_NBR                                                 
                  INTO :XMT00001-LOC-NBR                                        
                  FROM XMT_LOC               A                                  
                 WHERE A.LOC_NBR     = :PV-HOLD-LOC-NBR                         
                   AND A.OPN_DTE    <= :PV-HOLD-KEY-DATE                        
                   AND A.CLO_DTE    >= :PV-HOLD-KEY-DATE                        
               WITH UR                                                          
                                                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    SET PS-VALID-LOCATION        TO TRUE                        
               WHEN SQLCODE = +100                                              
                    SET PS-INVALID-LOCATION      TO TRUE                        
                    MOVE  'LOC INVALID IN XMT_LOC'                              
                                                 TO CC-ERR-DESCRIPTION          
                    PERFORM Z300-WRITE-INVALID-SKU-STR                          
               WHEN OTHER                                                       
                   MOVE 'D100-SELECT-XMT00001'   TO PV-PARAGRAPH-NAME           
                   MOVE 'SELECT ROW FROM XMT00001'                              
                                                 TO                             
                        PV-DB2-OPER-ATTEMPTED                                   
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *****************************************************************         
      * D200-VALIDATE-DEPT-SCL.                                                 
      *****************************************************************         
       D200-VALIDATE-DEPT-SCL.                                                  
                                                                                
           MOVE PV-SKU-DEPT                      TO MDSEAR-DEPT-NBR.            
           MOVE PV-SKU-SCL                       TO PV-SKU-SCL-P2.              
           MOVE PV-SKU-SCL-CHAR3                 TO MDSEAR-SUB-CL-NBR.          
                                                                                
     ****  DEPT AND SUBCLASS                                                    
           EXEC SQL                                                             
               SELECT A.DEPT_NBR                                                
                     ,A.SUB_CL_NBR                                              
               INTO :MDSEAR-DEPT-NBR                                            
                   ,:MDSEAR-SUB-CL-NBR                                          
               FROM TMDSEAR A                                                   
                   ,TAPLAVL B                                                   
               WHERE (A.DEPT_NBR    = :MDSEAR-DEPT-NBR)                         
                 AND (A.SUB_CL_NBR  = :MDSEAR-SUB-CL-NBR)                       
                 AND (A.MDSELV_CDE  = 'SCL')                                    
                 AND (A.OPERCO_NBR  = '03')                                     
                 AND (B.APLSYS_CDE  = 'IN')                                     
                 AND (A.MDSEAR_DKEY =  B.MDSEAR_DKEY)                           
                 AND (:INVPAR-ACTL-INV-DTE BETWEEN                              
                      CHAR(B.MDSEAR_STRT_DTE,ISO)  AND                          
                      CHAR(B.MDSEAR_END_DTE,ISO))                               
                 AND (:INVPAR-ACTL-INV-DTE BETWEEN                              
                      CHAR(A.STRT_DTE,ISO)         AND                          
                      CHAR(A.END_DTE,ISO))                                      
                WITH UR                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                    MOVE PV-SKU-NBR              TO IN309-SKU-NBR               
                    MOVE ZERO                    TO IN309-UPC-CODE              
                    MOVE PV-HOLD-LOC-NBR         TO IN309-LOC-NBR               
      **CHANGES BECAUSE OF THIS WORK REQUEST W38216                             
      *             MOVE CC-QTY                  TO IN309-QTY-COMP              
                    MOVE CC-QTY                  TO IN309-QTY                   
                    MOVE PV-PRICE                TO IN309-PRICE                 
                    PERFORM Z200-WRITE-INWS009                                  
               WHEN SQLCODE = +100                                              
                    MOVE  'INVALID DUMMY SKU'                                   
                                                 TO CC-ERR-DESCRIPTION          
                    PERFORM Z300-WRITE-INVALID-SKU-STR                          
               WHEN OTHER                                                       
                    MOVE 'D200-VALIDATE-DEPT-SCL '                              
                                                 TO PV-PARAGRAPH-NAME           
                    MOVE 'SELECT MBS '           TO                             
                         PV-DB2-OPERATION-ATTEMPTED                             
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      *****************************************************************         
      * D300-VALIDATE-SKU-EXIST.                                                
      *****************************************************************         
       D300-VALIDATE-SKU-EXIST.                                                 
     ****  VALIDATE SKU EXISTS                                                  
                                                                                
           EXEC SQL                                                             
               SELECT SKU                                                       
                 INTO :SKXREF-SKU                                               
                 FROM TSKXREF                                                   
               WHERE SKU = :PV-SKU-NUMBER                                       
               WITH UR                                                          
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
     **** MOVE TO OUTPUT                                                        
                    MOVE PV-SKU-NBR              TO IN309-SKU-NBR               
                    MOVE ZERO                    TO IN309-UPC-CODE              
                    MOVE PV-HOLD-LOC-NBR         TO IN309-LOC-NBR               
      *CHANGES BECAUSE OF THIS WORK REQUEST W38216                              
      *            MOVE CC-QTY                   TO IN309-QTY-COMP              
                    MOVE CC-QTY                  TO IN309-QTY                   
                    MOVE PV-PRICE                TO IN309-PRICE                 
                    PERFORM Z200-WRITE-INWS009                                  
               WHEN SQLCODE = +100                                              
                    MOVE 'INVALID SKU NOT FOUND' TO CC-ERR-DESCRIPTION          
                    PERFORM Z300-WRITE-INVALID-SKU-STR                          
               WHEN OTHER                                                       
                     MOVE 'D300-VALIDATE-SKU-EXIST '                            
                                                 TO PV-PARAGRAPH-NAME           
                     MOVE 'SELECT SKU'           TO                             
                        PV-DB2-OPERATION-ATTEMPTED                              
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *                                                                         
      /****************************************************************         
      * Z100-READ-INVENTORY.                                                    
      *    PROCESS THE INVENTORY ADJUSTMENTS FLAT FILE.                         
      *****************************************************************         
       Z100-READ-INVENTORY.                                                     
                                                                                
           READ INVENTORY-ADJUSTMENTS-FILE                                      
               INTO CC-INPUT-LAYOUT                                             
                   AT END SET PS-END-OF-DATA TO TRUE                            
           END-READ.                                                            
                                                                                
      /****************************************************************         
      * Z200-WRITE-INWS009.                                                     
      *    PROCESS THE INVENTORY ADJUSTMENTS FLAT FILE.                         
      *****************************************************************         
       Z200-WRITE-INWS009.                                                      
           WRITE OUT-EDITED-PHYS-INV-RECORD                                     
                FROM IN309-INVENTORY-INPUT-RECORD.                              
                                                                                
           ADD 1                 TO  PV-WRITE-COUNT.                            
                                                                                
      *****************************************************************         
      * Z300-WRITE-INVALID-SKU-STR                                              
      * WRITE THE RECORDS WITH INVALID SKU/STR TO ERROR FILE.                   
      *****************************************************************         
XXXXX  Z300-WRITE-INVALID-SKU-STR.                                              
           MOVE CC-REC-NBR       TO CC-ERR-REC.                                 
           MOVE CC-LOC-NBR       TO CC-ERR-LOC.                                 
           MOVE CC-SKU-NBR       TO CC-ERR-SKU.                                 
           MOVE CC-DOLLARS       TO CC-ERR-DOLLARS.                             
           MOVE CC-DECIMAL       TO CC-ERR-DECIMAL.                             
           MOVE CC-CENTS         TO CC-ERR-CENTS.                               
           MOVE CC-QTY           TO CC-ERR-QTY.                                 
XXXXX      WRITE OUT-INVALID-RECORD                                             
XXXXX                            FROM CC-ERROR-LAYOUT                           
XXXXX      ADD 1                 TO  PV-INVALID-SKU-STR-COUNT.                  
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE 4013      TO ABEND-CODE.                                        
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
