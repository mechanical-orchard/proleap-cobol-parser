000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400 PROGRAM-ID.    XLKUT057.                                                 
000500 AUTHOR.        PAUL OMAN.                                                
000600 INSTALLATION.  KOHLS.                                                    
000700 DATE-WRITTEN   APRIL 2003.                                               
000800 DATE-COMPILED.                                                           
000900                                                                          
001000****************************************************************          
001100*    PROGRAM READS RMS INPUT INTO IMRD076 COPYBOOK SELECTING   *          
001200*    ONLY THOSE FIELDS NEEDED TO FORMAT THE OUTPUT COPYBOOK    *          
001300*    XLRD031 (PACK UPC DATA).                                  *          
001400*                                                              *          
001500*****************************************************************         
001600                                                                          
001700 ENVIRONMENT DIVISION.                                                    
001800 INPUT-OUTPUT SECTION.                                                    
001900                                                                          
002000 FILE-CONTROL.                                                            
002100                                                                          
002200     SELECT RMS-PACK-UPC-FILE                                             
002300         ASSIGN TO INP01.                                                 
002400                                                                          
002500     SELECT PACK-UPC-FILE                                                 
002600         ASSIGN TO OUT01.                                                 
002700                                                                          
002800*****************************************************************         
002900 DATA DIVISION.                                                           
003000*****************************************************************         
003100 FILE SECTION.                                                            
003200                                                                          
003300 FD  RMS-PACK-UPC-FILE                                                    
003400     RECORDING MODE IS F                                                  
003500     BLOCK CONTAINS 0 RECORDS                                             
003600     LABEL RECORDS ARE STANDARD.                                          
003700                                                                          
003800 01  RMS-PACK-UPC-RECORD          PIC X(070).                             
003900                                                                          
004000 FD  PACK-UPC-FILE                                                        
004100     RECORDING MODE IS F                                                  
004200     BLOCK CONTAINS 0 RECORDS                                             
004300     LABEL RECORDS ARE STANDARD.                                          
004400                                                                          
004500 01  PACK-UPC-RECORD              PIC X(144).                             
004600                                                                          
004700                                                                          
004800*****************************************************************         
004900 WORKING-STORAGE SECTION.                                                 
005000                                                                          
005100 01  PS-PROGRAM-SWITCHES.                                                 
005200     05  PS-EOF-INPUT-SW              PIC X(01)  VALUE 'N'.               
005300         88  PS-EOF-INPUT-YES                    VALUE 'Y'.               
005400         88  PS-EOF-INPUT-NO                     VALUE 'N'.               
005500                                                                          
005600 01  PC-PROGRAM-CONSTANTS.                                                
005700     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XLKUT057'.        
005800                                                                          
005900 01  PV-PROGRAM-VARIABLES.                                                
006000     05  PV-RECORDS-READ              PIC  9(09) VALUE  0.                
006100     05  PV-RECORDS-WRITTEN           PIC  9(09) VALUE  0.                
006200                                                                          
006300 01  PV-ABEND-FIELDS.                                                     
006400     05  PV-ABEND-CODE                PIC  S9(4) VALUE +0                 
006500                                                 COMP SYNC.               
006600     05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'XLKUT057'.        
006700     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
006800     05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.            
006900     05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.            
007000     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
007100     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
007200     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
007300                                                                          
007400                                                                          
007500***********************************************************               
007600*  COPYBOOK USED FOR THE RMS PACK HEADER INPUT FILE       *               
007700***********************************************************               
007800     COPY IMRD076.                                                        
007900                                                                          
008000***********************************************************               
008100*    PACK UPC OUTPUT FILE                                 *               
008200***********************************************************               
008300 01  PV-PACK-UPC-RECORD.                                                  
008400     COPY XLRD031.                                                        
008500                                                                          
008600*****************************************************************         
008700 PROCEDURE DIVISION.                                                      
008800*****************************************************************         
008900     DISPLAY ' '.                                                         
009000     DISPLAY '**** BEGINNING XLKUT057 ****'.                              
009100     DISPLAY ' '.                                                         
009200                                                                          
009300     OPEN INPUT  RMS-PACK-UPC-FILE                                        
009400          OUTPUT PACK-UPC-FILE.                                           
009401                                                                          
009410     SET PS-EOF-INPUT-NO  TO TRUE.                                        
009500                                                                          
009600     PERFORM 1000-READ-INPUT-FILE.                                        
009700                                                                          
009800     PERFORM 2000-PROCESS-INPUT-FILE                                      
009900       UNTIL PS-EOF-INPUT-YES.                                            
010000                                                                          
010100     CLOSE RMS-PACK-UPC-FILE                                              
010200           PACK-UPC-FILE.                                                 
010300                                                                          
010400     DISPLAY '   INPUT RMS PACK RECS   = '                                
010500              PV-RECORDS-READ.                                            
010600     DISPLAY '   OUTPUT PACK DATA RECS = '                                
010700              PV-RECORDS-WRITTEN.                                         
010800     DISPLAY ' '.                                                         
010900     DISPLAY '****  END OF  XLKUT057  ****'.                              
011000     DISPLAY ' '.                                                         
011100                                                                          
011200     GOBACK.                                                              
011300                                                                          
011400                                                                          
011500*****************************************************************         
011600*  READ A RMS SKU RECORD                                                  
011700*****************************************************************         
011800 1000-READ-INPUT-FILE.                                                    
011900     INITIALIZE IM076-PACK-UPC-DATA.                                      
012000                                                                          
012100     READ RMS-PACK-UPC-FILE                                               
012200          INTO IM076-PACK-UPC-DATA                                        
012300            AT END                                                        
012400               SET PS-EOF-INPUT-YES TO TRUE                               
012500     END-READ.                                                            
012600                                                                          
012700     IF PS-EOF-INPUT-YES                                                  
012800        CONTINUE                                                          
012900     ELSE                                                                 
013000        ADD +1 TO PV-RECORDS-READ                                         
013100     END-IF.                                                              
013200                                                                          
013300*****************************************************************         
013400*  REFORMAT THE RMS PACK HEADER FILE                            *         
013500*****************************************************************         
013600 2000-PROCESS-INPUT-FILE.                                                 
013700                                                                          
013710     INITIALIZE PV-PACK-UPC-RECORD.                                       
013800     MOVE IM076-UPC-PK-SKU-NBR      TO XL031-UPC-PK-SKU-NBR.              
013900     MOVE IM076-UPC-UPC-NBR         TO XL031-UPC-UPC-NBR.                 
014000     MOVE IM076-UPC-EDI-ORD-IND     TO XL031-UPC-EDI-ORD-IND.             
014000     MOVE SPACES                    TO XL031-UPC-ERROR-MESSAGE.           
014100                                                                          
014200     PERFORM 2100-WRITE-OUTPUT.                                           
014300                                                                          
014400     PERFORM 1000-READ-INPUT-FILE.                                        
014500                                                                          
014600                                                                          
014700*****************************************************************         
014800*  WRITE A RECORD TO THE PACK DATA OUTPUT FILE                  *         
014900*****************************************************************         
015000 2100-WRITE-OUTPUT.                                                       
015100     WRITE PACK-UPC-RECORD FROM                                           
015200           PV-PACK-UPC-RECORD                                             
015300     END-WRITE.                                                           
015400     ADD +1                 TO PV-RECORDS-WRITTEN.                        
015500                                                                          
