       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKRP032                                                  
       AUTHOR.        DORSEY JACKSON                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  06-06-2002.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * MLKRP032 - M/L CREATE THE MARKDOWN TRANSACTION LIST BY STORE,           
      *           DOCUMENT NO, BATCH NO, AND GRAND TOTAL.                       
      ******************************************************************        
      *     THIS PROGRAM SEQUENTIALLY PROCESSES MARKDOWN RECORDS TO     00290000
      *  PRODUCE A MARKDOWN TRANSACTION LISTING REPORT, FOR ALL DATA    00330000
      *  KEYED BY DATA ENTRY.                                           00330000
      ******************************************************************        
      ******************************************************************        
      *                        CHANGE LOG                              *        
21731 *  CHNGE DTE   PERSON     CHG ID     REASON FOR CHANGE           *        
21731 *  ----------  ---------- ---------  -------------------------   *        
21731 *  04/12/2002  RONNA BUTZ   21731    WR#21731  - NEW SKU         *        
21731 *                                   -REPLACE OLD SKU WITH        *        
21731 *                                    INTELLIGENT SKU AND ADD     *        
21731 *                                    DEPT/CL/SUB                 *        
21731 *                                                                         
21731 *                                   -EXPAND REPORT WIDTH TO 200  *        
21731 *                                                                *        
21731 *                                   -EXPAND INPUT AND OUTPUT     *        
21731 *                                    FILETO 100                  *        
21731 *                                                                         
21731 *                                   -REPLACE COPYBOOK MLRD012O   *        
21731 *                                    AND MLRD012I WITH MLRD017   *        
21731 *                                                                         
W21732*  04/24/2003 ROD JANSSEN  W21732   MLRD017 ENHANCEMENTS FOR THE          
W21732*                                   ML017-PC-LINE-NBR.                    
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION             
W28545* W28545  08-18-2005 ROD JANSSEN ADD ML017-BN-UNEARNED-RWDS               
W29145* W29145  11-02-2005 S. JACKSON  SAS MARKDOWN OPT - MLRD017 EXPAND        
SR3403* SR3403  03-01-2012 TEJA K      LARGE RETAIL AMOUNT CHANGES              
W39372* W39372  08-13-2012 RONNA       ADD ML017-BN-UU-GFT-RCPT-RTRN            
205464* CHG0205464 08-16-2018 JIM ARENDT  ADD ML017-BN-NON-RCPT-RETURN          
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT INP-MARKDOWN-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUT-MARKDOWN-FILE                                             
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT OUT-MARKDOWN-TRAN-LIST                                        
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INP-MARKDOWN-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS   0 RECORDS                                          
W26603*    RECORD CONTAINS 120 CHARACTERS                                       
W29145     RECORD CONTAINS 200 CHARACTERS                                       
           DATA RECORD IS INP-MARKDOWN-RECORD.                                  
                                                                                
W26603*01  INP-MARKDOWN-RECORD            PIC X(120).                           
W29145 01  INP-MARKDOWN-RECORD            PIC X(200).                           
                                                                                
       FD  OUT-MARKDOWN-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS   0 RECORDS                                          
W26603*    RECORD CONTAINS 120 CHARACTERS                                       
W26603     RECORD CONTAINS 200 CHARACTERS                                       
           DATA RECORD IS INP-MARKDOWN-RECORD.                                  
                                                                                
W26603*01  OUT-MARKDOWN-RECORD            PIC X(120).                           
W26603 01  OUT-MARKDOWN-RECORD            PIC X(200).                           
                                                                                
       FD  OUT-MARKDOWN-TRAN-LIST                                               
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
21731      RECORD CONTAINS 200 CHARACTERS                                       
           DATA RECORD IS OUT-MARKDOWN-LIST-RECORD.                             
                                                                                
21731  01  OUT-MARKDOWN-REPORT-RECORD     PIC X(200).                           
                                                                                
       EJECT                                                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      *****************************************************************         
      ***************************************************************** 00120000
21731 *  MERCHANDISE LEDGER MARKDOWN/MARKON RECORD LAYOUT - INPUT AND   00130000
21731 *                                                     OUTPUT.     00130000
      ***************************************************************** 00140000
W26603     COPY MLRD017.                                                00000400
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-LINE-MAX                   PIC S9(4)   VALUE +55              
                                                         COMP SYNC.             
           05  PC-PROGRAM-NAME               PIC X(08) VALUE 'MLKRP032'.        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-INPUT-EOF-SW               PIC X(1)     VALUE 'N'.            
               88  PS-INPUT-EOF                           VALUE 'Y'.            
           05  PS-MARKDOWN-REC-FOUND-SW      PIC X(01)    VALUE 'N'.            
               88  PS-MARKDOWN-REC-FOUND                  VALUE 'Y'.            
           05  PS-FIRST-RECORD-SW            PIC X(01)    VALUE 'Y'.            
               88  PS-FIRST-RECORD                        VALUE 'Y'.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PAGE-COUNTER               PIC S9(4)   VALUE ZEROES           
                                                         COMP SYNC.             
           05  PV-LINE-COUNTER               PIC S9(4)   VALUE 99               
                                                         COMP SYNC.             
           05  PV-IN-COUNTER                 PIC S9(4)   VALUE ZEROES           
                                                         COMP SYNC.             
           05  PV-TEMP-DOC.                                                     
               10  PV-TEMP-DOC-POS1       PIC X(01) VALUE SPACE.                
               10  FILLER                 PIC X(06) VALUE SPACES.               
           05  PV-ERRMESG                 PIC X(10) VALUE SPACES.               
SR3403     05  PV-PGMDIFF                 PIC S9(5)V99 VALUE ZEROES.            
           05  PV-EXTAMT                  PIC S9(8)V99 VALUE ZEROES.            
           05  PV-TYPE                    PIC X(04) VALUE SPACE.                
           05  PV-SAVEDOCNO               PIC 9(07) VALUE ZEROES.               
W26603     05  PV-SAVESTORE               PIC X(05) VALUE SPACES.               
           05  PV-DOCDIFF                 PIC S9(07) VALUE ZEROES.              
           05  PV-SAVE-DOCQTY             PIC S9(04) VALUE ZEROES.              
           05  PV-XDATE                   PIC X(05) VALUE SPACES.               
           05  FILLER REDEFINES PV-XDATE.                                       
               10  PV-XDATE1              PIC X(02).                            
               10  PV-DDASH1              PIC X(01).                            
               10  PV-XDATE2              PIC X(02).                            
           05  PV-PDATE                   PIC X(05).                            
           05  PV-XSKU                    PIC X(10) VALUE SPACES.               
           05  FILLER REDEFINES PV-XSKU.                                        
               10  PV-XSKU1               PIC X(03).                            
               10  PV-XDASH1              PIC X(01).                            
               10  PV-XSKU2               PIC X(02).                            
               10  PV-XDASH2              PIC X(01).                            
               10  PV-XSKU3               PIC X(03).                            
           05  PV-PSKU                    PIC X(10) VALUE SPACES.               
21731      05  PV-CLASS.                                                        
21731          10  PV-CLASS-PART1         PIC X(01) VALUE SPACES.               
21731          10  PV-CLASS-PART2         PIC X(01) VALUE SPACES.               
21731      05  PV-MAJOR-CLASS.                                                  
21731          10  PV-MAJOR-CLASS-PART1   PIC X(01) VALUE SPACES.               
21731          10  PV-MAJOR-CLASS-PART2   PIC X(01) VALUE SPACES.               
W26603     05  PV-SAVE-STORE              PIC X(05) VALUE SPACES.               
           05  PV-SAVE-DOCNO              PIC X(07) VALUE SPACES.               
           05  PV-SAVE-BATCH              PIC X(03) VALUE SPACES.               
      ***************************************************************** 00120000
      *   STANDARD REPORT HEADER FOR A 150 COLUMN REPORT.               00220000
      ***************************************************************** 00230000
21731      COPY DPWS200O.                                                       
                                                                                
       01  HL-HEADING-LINE1.                                                    
21731      05  FILLER                        PIC X(87)   VALUE SPACES.          
21731      05  FILLER                        PIC X(25)   VALUE                  
21731          'MARKDOWN TRANSACTION LIST'.                                     
21731      05  FILLER                        PIC X(88)   VALUE SPACES.          
21731  01  HL-HEADING-LINE2.                                                    
21731      05  FILLER                        PIC X(04)   VALUE SPACES.          
21731      05  FILLER                        PIC X(50)   VALUE                  
21731          'BATCH   DOCNO     IN    LINE   ST    DATE    KM   '.            
21731      05  FILLER                        PIC X(50)   VALUE                  
21731          '  SKU      DEPT/CLS/SUB     OLD        NEW        '.            
21731      05  FILLER                        PIC X(50)   VALUE                  
21731          'KEY       PGM        QTY        DOC      DOC      '.            
21731      05  FILLER                        PIC X(46)   VALUE                  
21731          '   EXTENDED       TYPE     ERROR     '.                         
21731  01  HL-HEADING-LINE3.                                                    
21731      05  FILLER                        PIC X(04)   VALUE SPACES.          
21731      05  FILLER                        PIC X(50)   VALUE                  
21731          '                 HOUSE   NO                       '.            
21731      05  FILLER                        PIC X(50)   VALUE                  
21731          '                           PRICE      PRICE       '.            
21731      05  FILLER                        PIC X(50)   VALUE                  
21731          'DIFF      DIFF                  QTY      DIFF     '.            
21731      05  FILLER                        PIC X(46)   VALUE                  
21731          '    AMOUNT                MESSAGE    '.                         
21731                                                                           
21731  01  SL-SPACE-LINE                     PIC X(200)  VALUE SPACES.          
                                                                                
       01  DL-DETAIL-LINE.                                                      
           05  FILLER                        PIC X(05)   VALUE SPACES.          
           05  DL-BATCH                      PIC X(03)   VALUE SPACES.          
21731      05  FILLER                        PIC X(03)   VALUE SPACE.           
           05  DL-DOC                        PIC Z(07)   VALUE ZEROES.          
21731      05  FILLER                        PIC X(04)   VALUE SPACES.          
           05  DL-IN-HOUSE                   PIC X(03)   VALUE SPACES.          
W21732     05  FILLER                        PIC X(02)   VALUE SPACES.          
W21732     05  DL-LINE-NO                    PIC Z(04)   VALUE ZEROES.          
W26603     05  FILLER                        PIC X(02)   VALUE SPACES.          
W26603     05  DL-STORE                      PIC X(05)   VALUE SPACES.          
21731      05  FILLER                        PIC X(03)   VALUE SPACES.          
           05  DL-DATE-MM                    PIC X(02)   VALUE SPACES.          
21731      05  FILLER                        PIC X(01)   VALUE '-'.             
           05  DL-DATE-DD                    PIC X(02)   VALUE SPACES.          
21731      05  FILLER                        PIC X(03)   VALUE SPACES.          
           05  DL-KM                         PIC X(02)   VALUE SPACES.          
21731      05  FILLER                        PIC X(03)   VALUE SPACES.          
21731      05  DL-SKU                        PIC X(08)   VALUE SPACES.          
21731 *    05  FILLER                        PIC X(01)   VALUE '-'.             
21731 *    05  DL-SKU2                       PIC X(02)   VALUE SPACES.          
21731 *    05  FILLER                        PIC X(01)   VALUE '-'.             
21731 *    05  DL-SKU3                       PIC X(03)   VALUE SPACES.          
21731      05  FILLER                        PIC X(04)   VALUE SPACES.          
21731      05  DL-DEPT PIC X(03)   VALUE SPACES.                                
21731      05  FILLER                        PIC X(01)   VALUE '-'.             
21731      05  DL-CLASS                      PIC X(02)   VALUE SPACES.          
21731      05  FILLER                        PIC X(01)   VALUE '-'.             
21731      05  DL-SUB                        PIC X(02)   VALUE SPACES.          
SR3403     05  FILLER                        PIC X(04)   VALUE SPACES.          
SR3403     05  DL-OLD-PRICE                  PIC ZZ,ZZZ.99 VALUE ZEROS.         
SR3403     05  FILLER                        PIC X(02)   VALUE SPACES.          
SR3403     05  DL-NEW-PRICE                  PIC ZZ,ZZZ.99 VALUE ZEROS.         
SR3403     05  FILLER                        PIC X(02)   VALUE SPACES.          
SR3403     05  DL-KEY-DIFF                   PIC ZZ,ZZZ.99 VALUE ZEROS.         
SR3403     05  FILLER                        PIC X(02)   VALUE SPACES.          
SR3403     05  DL-PGM-DIFF                   PIC ZZ,ZZZ.99 VALUE ZEROS.         
RWJ        05  FILLER                        PIC X(03)   VALUE SPACES.          
RWJ        05  DL-QTY                        PIC ZZZZZZZ  VALUE ZEROS.          
RWJ        05  FILLER                        PIC X(03)   VALUE SPACES.          
RWJ        05  DL-DOC-QTY                    PIC ZZZZZZZ  VALUE ZEROS.          
RWJ        05  FILLER                        PIC X(01)   VALUE SPACES.          
RWJ        05  DL-DOC-DIFF                   PIC ZZZZZZZ- VALUE ZEROS.          
21731      05  FILLER                        PIC X(03)   VALUE SPACES.          
           05  DL-EXTENDED-AMT               PIC Z,ZZZ,ZZZ.99-                  
                                                         VALUE ZEROES.          
21731      05  FILLER                        PIC X(05)   VALUE SPACES.          
           05  DL-TYPE                       PIC X(04)   VALUE SPACES.          
21731      05  FILLER                        PIC X(03)   VALUE SPACES.          
           05  DL-MESSAGE                    PIC X(10)   VALUE SPACES.          
                                                                                
       01  DLST-STORE-TOTALS-PRINT-LINE.                                        
           05  FILLER                        PIC X(04)   VALUE SPACES.          
           05  FILLER                        PIC X(11)   VALUE                  
                                             'STORE TOTAL'.                     
RWJ        05  FILLER                        PIC X(109)  VALUE SPACES.          
RWJ        05  DLST-QTY                      PIC ZZZZZZZ VALUE ZEROES.          
RWJ        05  FILLER                        PIC X(03)   VALUE SPACES.          
RWJ        05  DLST-DOC-QTY                  PIC ZZZZZZZ VALUE ZEROES.          
RWJ        05  FILLER                        PIC X(01)   VALUE SPACES.          
RWJ        05  DLST-DOC-DIFF                 PIC ZZZZZZZ- VALUE ZEROES.         
21731      05  FILLER                        PIC X(03)   VALUE SPACES.          
           05  DLST-EXTENDED-AMT             PIC Z,ZZZ,ZZZ.99-                  
                                                 VALUE ZEROES.                  
       01  DLDT-DOCNO-TOTALS-PRINT-LINE.                                        
           05  FILLER                        PIC X(04)   VALUE SPACES.          
           05  FILLER                        PIC X(11)   VALUE                  
                                             'DOCNO TOTAL'.                     
RWJ        05  FILLER                        PIC X(109)  VALUE SPACES.          
RWJ        05  DLDT-QTY                      PIC ZZZZZZZ VALUE ZEROES.          
RWJ        05  FILLER                        PIC X(03)   VALUE SPACES.          
RWJ        05  DLDT-DOC-QTY                  PIC ZZZZZZZ VALUE ZEROES.          
RWJ        05  FILLER                        PIC X(01)   VALUE SPACES.          
RWJ        05  DLDT-DOC-DIFF                 PIC ZZZZZZZ- VALUE ZEROES.         
21731      05  FILLER                        PIC X(03)   VALUE SPACES.          
           05  DLDT-EXTENDED-AMT             PIC Z,ZZZ,ZZZ.99-                  
                                                 VALUE ZEROES.                  
       01  DLBT-BATCH-TOTALS-PRINT-LINE.                                        
           05  FILLER                        PIC X(04)   VALUE SPACES.          
           05  FILLER                        PIC X(11)   VALUE                  
                                             'BATCH TOTAL'.                     
RWJ        05  FILLER                        PIC X(109)  VALUE SPACES.          
RWJ        05  DLBT-QTY                      PIC ZZZZZZZ VALUE ZEROES.          
RWJ        05  FILLER                        PIC X(03)   VALUE SPACES.          
RWJ        05  DLBT-DOC-QTY                  PIC ZZZZZZZ VALUE ZEROES.          
RWJ        05  FILLER                        PIC X(01)   VALUE SPACES.          
RWJ        05  DLBT-DOC-DIFF                 PIC ZZZZZZZ- VALUE ZEROES.         
21731      05  FILLER                        PIC X(03)   VALUE SPACES.          
           05  DLBT-EXTENDED-AMT             PIC Z,ZZZ,ZZZ.99-                  
                                                 VALUE ZEROES.                  
       01  DLGT-GRAND-TOTALS-PRINT-LINE.                                        
           05  FILLER                        PIC X(04)   VALUE SPACES.          
           05  FILLER                        PIC X(11)   VALUE                  
                                             'GRAND TOTAL'.                     
RWJ        05  FILLER                        PIC X(109)  VALUE SPACES.          
RWJ        05  DLGT-QTY                      PIC ZZZZZZZ VALUE ZEROES.          
RWJ        05  FILLER                        PIC X(03)   VALUE SPACES.          
RWJ        05  DLGT-DOC-QTY                  PIC ZZZZZZZ VALUE ZEROES.          
RWJ        05  FILLER                        PIC X(01)   VALUE SPACES.          
RWJ        05  DLGT-DOC-DIFF                 PIC ZZZZZZZ- VALUE ZEROES.         
21731      05  FILLER                        PIC X(03)   VALUE SPACES.          
           05  DLGT-EXTENDED-AMT             PIC Z,ZZZ,ZZZ.99-                  
                                                 VALUE ZEROES.                  
       01  ST-STORE-TOTALS.                                                     
           05  ST-TOT-QTY                    PIC S9(07) VALUE ZEROES            
                                                          COMP-3.               
           05  ST-TOT-DOC-QTY                PIC S9(07) VALUE ZEROES            
                                                          COMP-3.               
           05  ST-TOT-EXTENDED-AMT           PIC S9(11)V99 VALUE ZEROES         
                                                          COMP-3.               
       01  DT-DOCNO-TOTALS.                                                     
           05  DT-TOT-QTY                    PIC S9(07) VALUE ZEROES            
                                                          COMP-3.               
           05  DT-TOT-DOC-QTY                PIC S9(07) VALUE ZEROES            
                                                          COMP-3.               
           05  DT-TOT-EXTENDED-AMT           PIC S9(11)V99 VALUE ZEROES         
                                                          COMP-3.               
       01  BT-BATCH-TOTALS.                                                     
           05  BT-TOT-QTY                    PIC S9(07) VALUE ZEROES            
                                                          COMP-3.               
           05  BT-TOT-DOC-QTY                PIC S9(07) VALUE ZEROES            
                                                          COMP-3.               
           05  BT-TOT-EXTENDED-AMT           PIC S9(11)V99 VALUE ZEROES         
                                                          COMP-3.               
       01  GT-GRAND-TOTALS.                                                     
           05  GT-TOT-QTY                    PIC S9(07) VALUE ZEROES            
                                                          COMP-3.               
           05  GT-TOT-DOC-QTY                PIC S9(07) VALUE ZEROES            
                                                          COMP-3.               
           05  GT-TOT-EXTENDED-AMT           PIC S9(11)V99 VALUE ZEROES         
                                                          COMP-3.               
       01  RUN-TIME.                                                            
           05  R-HR                    PIC 99.                                  
           05  R-MIN                   PIC 99.                                  
           05  R-SEC                   PIC 99.                                  
           05  R-TH                    PIC 99.                                  
                                                                                
       01  DATE-IN.                                                             
           05 DATE-IN-YY               PIC XX.                                  
           05 DATE-IN-MM               PIC XX.                                  
           05 DATE-IN-DD               PIC XX.                                  
                                                                                
       EJECT                                                                    
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MLKRP032.                                                           
                                                                                
      ****************************************************************          
      *    OPEN ALL FILES.                                                      
      *    INITIALIZE PAGE HEADINGS                                             
      *    PROCESS AND PRINT RECORDS ON MARKDOWN FILE                           
      *    PRINT THE STORE TOTALS ON CHANGE IN STORE NUMBER                     
      *    PRINT THE DOCNO TOTALS ON CHANGE IN DOCNO NUMBER                     
      *    PRINT THE BATCH TOTALS ON CHANGE IN BATCH NUMBER                     
      *    PRINT THE GRAND TOTALS WHICH INCLUDE ALL BATCH TOTALS                
      *    AT END-OF-JOB.                                                       
      *    CLOSE ALL FILES.                                                     
      ****************************************************************          
                                                                                
           OPEN INPUT  INP-MARKDOWN-FILE                                        
                OUTPUT OUT-MARKDOWN-FILE                                        
                       OUT-MARKDOWN-TRAN-LIST.                                  
                                                                                
           PERFORM 1000-INITIALIZE-HEADER.                                      
                                                                                
           PERFORM 2000-PROCESS                                                 
               UNTIL PS-INPUT-EOF.                                              
                                                                                
           IF PV-PAGE-COUNTER = +0                                              
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM B100-PRINT-STORE-TOTALS                                  
               PERFORM B200-PRINT-DOCNO-TOTALS                                  
               PERFORM B300-PRINT-BATCH-TOTALS                                  
               PERFORM B400-PRINT-GRAND-TOTALS                                  
           END-IF.                                                              
                                                                                
           CLOSE INP-MARKDOWN-FILE                                              
                 OUT-MARKDOWN-FILE                                              
                 OUT-MARKDOWN-TRAN-LIST.                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-INITIALIZE-HEADER.                                                  
                                                                                
      ****************************************************************          
      *    SET UP THE STANDARD PAGE HEADER.                                     
      *    ACCEPT THE RUN DATE AND RUN TIME FROM THE SYSTEM.                    
      *    READ FIRST MARKDOWN RECORD.                                          
      *    PERFORMED BY 0000-MLKRP032.                                          
      ****************************************************************          
           ACCEPT DATE-IN  FROM DATE.                                           
           ACCEPT RUN-TIME FROM TIME.                                           
21731      MOVE DATE-IN-MM TO DP200O-RUN-MONTH.                                 
21731      MOVE DATE-IN-DD TO DP200O-RUN-DAY.                                   
21731      MOVE DATE-IN-YY TO DP200O-RUN-YEAR.                                  
21731      MOVE R-HR       TO DP200O-RUN-HOUR.                                  
21731      MOVE R-MIN      TO DP200O-RUN-MINUTE.                                
21731      MOVE PC-PROGRAM-NAME TO DP200O-PROGRAM-NAME.                         
21731      MOVE 1               TO DP200O-REPORT-NUMBER.                        
           MOVE 'N'             TO PS-MARKDOWN-REC-FOUND-SW                     
           PERFORM 4000-READ-MARKDOWN-DATA                                      
             UNTIL PS-MARKDOWN-REC-FOUND OR PS-INPUT-EOF.                       
           IF PS-MARKDOWN-REC-FOUND                                             
21731          MOVE ML017-PC-STORE-NBR   TO PV-SAVE-STORE                       
21731          MOVE ML017-PC-DOC-7-NBR   TO PV-SAVE-DOCNO                       
21731          MOVE ML017-PC-BATCH-NBR   TO PV-SAVE-BATCH                       
               MOVE 'Y'                  TO PS-FIRST-RECORD-SW                  
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
       2000-PROCESS.                                                            
                                                                                
                                                                                
      ****************************************************************          
      *    PRINT DETAIL FOR THE CURRENT MARKDOWN RECORD AND                     
      *    READ THE NEXT INPUT RECORD.                                          
      *                                                                         
      *    PERFORMED BY 0000-MLKRP032.                                          
      ****************************************************************          
                                                                                
           IF PS-FIRST-RECORD                                                   
               MOVE 'N' TO PS-FIRST-RECORD-SW                                   
           ELSE                                                                 
21731      IF ((ML017-PC-STORE-NBR = PV-SAVE-STORE) AND                 00004900
21731                (ML017-PC-DOC-7-NBR = PV-SAVE-DOCNO))              00005000
21731          MOVE ZEROES TO ML017-PC-TOT-DOC-QTY                      00005100
           END-IF                                                       00005200
           END-IF.                                                      00005200
                                                                                
21731      IF ML017-PC-BATCH-NBR = PV-SAVE-BATCH                                
21731          IF ML017-PC-DOC-7-NBR = PV-SAVE-DOCNO                            
21731              IF ML017-PC-STORE-NBR = PV-SAVE-STORE                        
                       CONTINUE                                                 
                   ELSE                                                         
                       PERFORM B100-PRINT-STORE-TOTALS                          
21731                  MOVE ML017-PC-STORE-NBR TO PV-SAVE-STORE                 
                   END-IF                                                       
               ELSE                                                             
                   PERFORM B100-PRINT-STORE-TOTALS                              
                   PERFORM B200-PRINT-DOCNO-TOTALS                              
21731              MOVE ML017-PC-STORE-NBR       TO PV-SAVE-STORE               
21731              MOVE ML017-PC-DOC-7-NBR TO PV-SAVE-DOCNO                     
               END-IF                                                           
           ELSE                                                                 
               PERFORM B100-PRINT-STORE-TOTALS                                  
               PERFORM B200-PRINT-DOCNO-TOTALS                                  
               PERFORM B300-PRINT-BATCH-TOTALS                                  
21731          MOVE ML017-PC-STORE-NBR   TO PV-SAVE-STORE                       
21731          MOVE ML017-PC-DOC-7-NBR TO PV-SAVE-DOCNO                         
21731          MOVE ML017-PC-BATCH-NBR   TO PV-SAVE-BATCH                       
           END-IF.                                                              
                                                                                
21731      MOVE ML017-PC-DOC-7-NBR TO PV-TEMP-DOC                               
21731      MOVE ML017-PC-DOC-NBR   TO ML017-PC-DOC-5-NBR                        
                                                                                
21731      IF ((ML017-PC-DOC-7-NBR NUMERIC)                                     
21731             AND(ML017-PLU-MKDN-MKON                                       
21731              OR ML017-REGISTER-MKDN-MKON                                  
21731              OR ML017-UNMATCHED-MKDN-MKON                                 
21731              OR ML017-BUYER-MKDN-MKON                                     
21731              OR ML017-STORE-MKDN-MKON))                                   
21731 *           AND(ML017-PC-CATEG-CDE = '1' OR '2' OR '3' OR                 
21731 *                                           '4' OR '5'))                  
               CONTINUE                                                         
           ELSE                                                                 
21731          IF ((ML017-PC-DOC-DEPT = ML017-PC-DEPT-NBR)              00003600
21731                  AND (ML017-PC-CATEGORY-IND = '00'))                  0000
21731              IF ML017-PC-RSN-CDE = '1'                                    
21731                  MOVE '1' TO ML017-PC-CATEG-CDE                           
                   ELSE                                                         
21731                  IF (ML017-BN-SENIOR-CITZ-DISC                            
21731                   OR ML017-BN-KOHLS-COUPON                                
21731                   OR ML017-BN-GOLDSTAR                                    
21731                   OR ML017-BN-FWP-VOID-CRCT                               
21731                   OR ML017-BN-OWN-ADJ                                     
21731                   OR ML017-BN-OWN-ADJ-CRCT                                
21731                   OR ML017-BN-MANUAL-LID                                  
21731                   OR ML017-BN-MANUAL-LID-CRCT                             
W28545                  OR ML017-BN-UNEARNED-RWDS                               
W39372                  OR ML017-BN-UU-GFT-RCPT-RTRN                            
205464                  OR ML017-BN-NON-RCPT-RETURN                             
21731                   OR ML017-BN-OWN-RTN-ADJ )                               
21731 *                IF ML017-PC-BATCH-NBR = '728' OR '733' OR                
205464*                                        '710' OR                         
21731 *                                       '753' OR '771' OR                 
21731 *                                       '796' OR '797' OR                 
21731 *                                       '798' OR '799' OR                 
21731 *                                       '792'                             
21731                      MOVE '2' TO ML017-PC-CATEG-CDE                       
                       ELSE                                                     
21731                      MOVE '1' TO ML017-PC-CATEG-CDE                       
                       END-IF                                                   
                   END-IF                                                       
               ELSE                                                             
21731              IF ML017-PC-DOC-DEPT = '999'                                 
21731                  MOVE '3' TO ML017-PC-CATEG-CDE                           
                   ELSE                                                         
21731                  IF ((ML017-PC-CATEGORY-IND = '00')                       
                               OR (PV-TEMP-DOC-POS1 = '8'))                     
21731                      MOVE '4' TO ML017-PC-CATEG-CDE                       
                       ELSE                                                     
21731                      MOVE '5' TO ML017-PC-CATEG-CDE                       
                       END-IF                                           00003800
                   END-IF                                               00003800
               END-IF                                                   00003800
           END-IF.                                                      00003800
                                                                                
      ****************************************************************          
21731 *        IF ((ML017-PC-DOC-7-NBR GE 90000)                                
      * CHANGED RELATIONAL OPERATOR TO > AND VALUE TO 89999 DLJ                 
21731 *                AND (ML017-PC-DOC-7-NBR LE 99899))                       
      * CHANGED RELATIONAL OPERATOR TO < AND VALUE TO 99900 DLJ                 
      *            IF ((ML012O_7_DIGIT_DOCUMENT_NMBR GE 5000000)                
      * CHANGED RELATIONAL OPERATOR TO > AND VALUE TO 4999999 DLJ               
      *             AND (ML012O_7_DIGIT_DOCUMENT_NMBR LE 5999999))              
      * CHANGED RELATIONAL OPERATOR TO < AND VALUE TO 6000000 DLJ               
      *                IF ((ML012O_7_DIGIT_DOCUMENT_NMBR GE 2000000)            
      * CHANGED RELATIONAL OPERATOR TO > AND VALUE TO 1999999 DLJ               
      *                 AND (ML012O_7_DIGIT_DOCUMENT_NMBR LE 2999999))          
      * CHANGED RELATIONAL OPERATOR TO < AND VALUE TO 3000000 DLJ               
      ****************************************************************          
21731      IF ML017-PC-DOC-7-NBR NUMERIC                                        
21731          IF ((ML017-PC-DOC-7-NBR > 89999)                                 
21731                  AND (ML017-PC-DOC-7-NBR < 99900))                        
21731              MOVE '999' TO ML017-PC-MKDN-MKUP-RSN-CDE                     
               ELSE                                                             
21731              IF ((ML017-PC-DOC-7-NBR > 4999999)                           
21731               AND (ML017-PC-DOC-7-NBR < 6000000))                         
21731                  MOVE '501' TO ML017-PC-MKDN-MKUP-RSN-CDE                 
                   ELSE                                                         
21731                  IF ((ML017-PC-DOC-7-NBR > 1999999)                       
21731                   AND (ML017-PC-DOC-7-NBR < 3000000))                     
21731                      MOVE '502' TO ML017-PC-MKDN-MKUP-RSN-CDE             
                       ELSE                                                     
21731                      MOVE SPACES TO ML017-PC-MKDN-MKUP-RSN-CDE            
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
21731          MOVE SPACES TO ML017-PC-MKDN-MKUP-RSN-CDE                        
           END-IF                                                               
                                                                        00005300
      ****************************************************************          
21731      IF ML017-PC-DOC-7-NBR NOT EQUAL PV-SAVE-DOCNO                00005400
21731          MOVE ML017-PC-DOC-7-NBR TO PV-SAVE-DOCNO                 00005500
21731          MOVE ML017-PC-STORE-NBR          TO PV-SAVE-STORE        00005600
21731          MOVE ML017-PC-TOT-DOC-QTY        TO PV-SAVE-DOCQTY       00005700
           END-IF                                                       00005800
      ****************************************************************          
                                                                                
21731      IF ML017-PC-STORE-NBR NOT EQUAL PV-SAVE-STORE                00006000
21731          MOVE ML017-PC-STORE-NBR TO PV-SAVE-STORE                 00006010
21731          MOVE ML017-PC-TOT-DOC-QTY TO PV-SAVE-DOCQTY              00006020
           END-IF                                                       00006300
                                                                        00006400
           COMPUTE PV-EXTAMT =                                          00006510
21731        (ML017-PC-OLD-PR-AMT - ML017-PC-NEW-PR-AMT)                00006510
21731            * ML017-PC-QTY.                                        00006520
                                                                        00006530
           COMPUTE PV-PGMDIFF =                                         00006610
21731        ML017-PC-OLD-PR-AMT - ML017-PC-NEW-PR-AMT.                 00006610
                                                                        00006700
21731      IF ML017-PC-TYP-CDE = '1'                                    00006800
               MOVE 'MKDN' TO PV-TYPE                                   00006900
           ELSE                                                         00007000
               MOVE 'MCNL' TO PV-TYPE                                   00007100
           END-IF.                                                      00007200
                                                                        00007300
           IF PV-PGMDIFF < 0                                            00007400
               COMPUTE PV-PGMDIFF = PV-PGMDIFF * -1                     00007500
           END-IF.                                                      00007600
                                                                        00007700
21731      IF ML017-PC-DIFFER-AMT NOT EQUAL PV-PGMDIFF                  00007800
               MOVE 'DIFF ERROR' TO PV-ERRMESG                          00007900
           ELSE                                                         00008000
               MOVE SPACES TO PV-ERRMESG                                00008100
           END-IF.                                                      00008200
                                                                                
      ****************************************************************          
      *  ADDED THE FOLLOWING CODE FOR INVALID                           00008300
      *  VENDOR ALLOWANCE TRANSACTIONS                                  00008300
                                                                                
21731      IF ML017-PC-MKDN-MKUP-RSN-CDE = '999'                                
21731          IF ML017-PC-VENDOR-INHSE-NMBR = '000'                            
                   MOVE 'INHSE ERR' TO PV-ERRMESG                               
               ELSE                                                             
                   WRITE OUT-MARKDOWN-RECORD FROM                               
21731                    ML017-PRICE-CHANGE-RECORD                              
               END-IF                                                           
           ELSE                                                                 
               WRITE OUT-MARKDOWN-RECORD FROM                                   
21731                ML017-PRICE-CHANGE-RECORD                                  
           END-IF.                                                              
21731      MOVE ML017-PC-BATCH-NBR       TO DL-BATCH.                           
           MOVE PV-SAVE-DOCNO            TO DL-DOC.                             
21731      MOVE ML017-PC-VENDOR-INHSE-NMBR TO DL-IN-HOUSE.                      
21731      MOVE ML017-PC-LINE-NBR        TO DL-LINE-NO.                         
21731      MOVE ML017-PC-STORE-NBR       TO DL-STORE.                           
21731      MOVE ML017-PC-DTE-MM          TO DL-DATE-MM.                         
21731      MOVE ML017-PC-DTE-DD          TO DL-DATE-DD.                         
21731      MOVE ML017-PC-KOHLS-MM        TO DL-KM.                              
21731      MOVE ML017-PC-KOHLS-MM        TO DL-KM.                              
21731      MOVE ML017-PC-SKU             TO DL-SKU.                             
21731      MOVE ML017-PC-DEPT-NBR        TO DL-DEPT.                            
21731      MOVE ML017-PC-CLASS-NBR       TO DL-SUB                              
21731                                       PV-CLASS.                           
21731      MOVE PV-CLASS-PART1           TO PV-MAJOR-CLASS-PART1.               
21731      MOVE '0'                      TO PV-MAJOR-CLASS-PART2.               
21731      MOVE PV-MAJOR-CLASS           TO DL-CLASS                            
21731                                       ML017-PC-MAJ-CLASS.                 
21731 *    MOVE ML012O-SEQUENCE-NMBR     TO DL-SKU3.                            
21731      MOVE ML017-PC-OLD-PR-AMT      TO DL-OLD-PRICE.                       
21731      MOVE ML017-PC-NEW-PR-AMT      TO DL-NEW-PRICE.                       
21731      MOVE ML017-PC-DIFFER-AMT      TO DL-KEY-DIFF.                        
           MOVE PV-PGMDIFF               TO DL-PGM-DIFF.                        
21731      MOVE ML017-PC-QTY             TO DL-QTY.                             
21731      MOVE ML017-PC-TOT-DOC-QTY     TO DL-DOC-QTY.                         
           MOVE PV-DOCDIFF               TO DL-DOC-DIFF.                        
           MOVE PV-EXTAMT                TO DL-EXTENDED-AMT.                    
           MOVE PV-TYPE                  TO DL-TYPE.                            
           MOVE PV-ERRMESG               TO DL-MESSAGE.                         
           PERFORM Z100-PRINT-DETAIL.                                           
21731      ADD ML017-PC-QTY              TO ST-TOT-QTY.                         
21731      ADD ML017-PC-TOT-DOC-QTY      TO ST-TOT-DOC-QTY.                     
           ADD PV-EXTAMT                 TO ST-TOT-EXTENDED-AMT.                
           MOVE 'N'                      TO PS-MARKDOWN-REC-FOUND-SW            
           PERFORM 4000-READ-MARKDOWN-DATA                                      
             UNTIL PS-MARKDOWN-REC-FOUND OR PS-INPUT-EOF.                       
                                                                                
       4000-READ-MARKDOWN-DATA.                                                 
                                                                                
      ****************************************************************          
      *    READ THE MARKDOWN DATA FILE.                                         
      *                                                                         
      *    PERFORMED BY 1000-INITIALIZE-HEADER.                                 
      *    PERFORMED BY 2000-PROCESS.                                           
      ****************************************************************          
21731      READ INP-MARKDOWN-FILE INTO ML017-PRICE-CHANGE-RECORD                
               AT END                                                           
                   SET PS-INPUT-EOF TO TRUE.                                    
21731      IF ML017-MKDOWN                                                      
               MOVE 'Y' TO PS-MARKDOWN-REC-FOUND-SW                             
21731 *        MOVE ML017-PRICE-CHANGE-RECORD TO                                
21731 *             ML017-PRICE-CHANGE-RECORD                                   
               ADD 1 TO PV-IN-COUNTER                                           
           END-IF.                                                              
                                                                                
       6000-PRINT-HEADERS.                                                      
                                                                                
      ****************************************************************          
      *    PRINT THE PAGE HEADERS.                                              
      *                                                                         
      *    PERFORMED BY Z100-DETAIL-PRINT-LINE                                  
      *    PERFORMED BY Z200-PRINT-STORE-TOTALS.                                
      *    PERFORMED BY Z300-PRINT-DOCNO-TOTALS.                                
      *    PERFORMED BY Z400-PRINT-BATCH-TOTALS.                                
      *    PERFORMED BY Z500-PRINT-GRAND-TOTALS.                                
      ****************************************************************          
                                                                                
           ADD  1               TO PV-PAGE-COUNTER.                             
                                                                                
21731      MOVE PV-PAGE-COUNTER TO DP200O-PAGE-NUMBER.                          
                                                                                
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
21731          FROM DP200O-STANDRD-HEADER-200-COLS                              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM HL-HEADING-LINE1                                            
               AFTER ADVANCING 2 LINE.                                          
                                                                                
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM HL-HEADING-LINE2                                            
               AFTER ADVANCING 2 LINE.                                          
                                                                                
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM HL-HEADING-LINE3                                            
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM SL-SPACE-LINE                                               
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE 07 TO PV-LINE-COUNTER.                                          
                                                                                
           EJECT                                                                
                                                                                
       B100-PRINT-STORE-TOTALS.                                                 
                                                                                
      ****************************************************************          
      *    FORMAT STORE TOTAL PRINT LINE AND ADD STORE TOTALS                   
      *    TO DOCNO TOTALS.                                                     
      *                                                                         
      *    PERFORMED BY 0000-MLKRP032.                                          
      *    PERFORMED BY 2000-PROCESS.                                           
      ****************************************************************          
                                                                                
           MOVE ST-TOT-QTY             TO DLST-QTY.                             
           MOVE ST-TOT-DOC-QTY         TO DLST-DOC-QTY.                         
           MOVE ST-TOT-EXTENDED-AMT    TO DLST-EXTENDED-AMT.                    
           COMPUTE PV-DOCDIFF = ST-TOT-DOC-QTY - ST-TOT-QTY.                    
           MOVE PV-DOCDIFF             TO DLST-DOC-DIFF.                        
                                                                                
           PERFORM Z200-PRINT-STORE-TOTALS.                                     
                                                                                
           ADD ST-TOT-QTY              TO DT-TOT-QTY.                           
           ADD ST-TOT-DOC-QTY          TO DT-TOT-DOC-QTY.                       
           ADD ST-TOT-EXTENDED-AMT     TO DT-TOT-EXTENDED-AMT.                  
           MOVE ZEROES                 TO ST-TOT-QTY                            
                                          ST-TOT-DOC-QTY                        
                                          ST-TOT-EXTENDED-AMT.                  
       B200-PRINT-DOCNO-TOTALS.                                                 
                                                                                
      ****************************************************************          
      *    FORMAT DOCNO TOTAL PRINT LINE AND ADD DOCNO TOTALS                   
      *    TO BATCH TOTALS.                                                     
      *                                                                         
      *    PERFORMED BY 0000-MLKRP032.                                          
      *    PERFORMED BY 2000-PROCESS.                                           
      ****************************************************************          
                                                                                
           MOVE DT-TOT-QTY             TO DLDT-QTY.                             
           MOVE DT-TOT-DOC-QTY         TO DLDT-DOC-QTY.                         
           MOVE DT-TOT-EXTENDED-AMT    TO DLDT-EXTENDED-AMT.                    
           COMPUTE PV-DOCDIFF = DT-TOT-DOC-QTY - DT-TOT-QTY.                    
           MOVE PV-DOCDIFF             TO DLDT-DOC-DIFF.                        
                                                                                
           PERFORM Z300-PRINT-DOCNO-TOTALS.                                     
                                                                                
           ADD DT-TOT-QTY              TO BT-TOT-QTY.                           
           ADD DT-TOT-DOC-QTY          TO BT-TOT-DOC-QTY.                       
           ADD DT-TOT-EXTENDED-AMT     TO BT-TOT-EXTENDED-AMT.                  
           MOVE ZEROES                 TO DT-TOT-QTY                            
                                          DT-TOT-DOC-QTY                        
                                          DT-TOT-EXTENDED-AMT.                  
       B300-PRINT-BATCH-TOTALS.                                                 
                                                                                
      ****************************************************************          
      *    FORMAT BATCH TOTAL PRINT LINE AND ADD BATCH TOTALS                   
      *    TO GRAND TOTALS.                                                     
      *                                                                         
      *    PERFORMED BY 0000-MLKRP032.                                          
      *    PERFORMED BY 2000-PROCESS.                                           
      ****************************************************************          
                                                                                
           MOVE BT-TOT-QTY             TO DLBT-QTY.                             
           MOVE BT-TOT-DOC-QTY         TO DLBT-DOC-QTY.                         
           MOVE BT-TOT-EXTENDED-AMT    TO DLBT-EXTENDED-AMT.                    
           COMPUTE PV-DOCDIFF = BT-TOT-DOC-QTY - BT-TOT-QTY.                    
           MOVE PV-DOCDIFF             TO DLBT-DOC-DIFF.                        
                                                                                
           PERFORM Z400-PRINT-BATCH-TOTALS.                                     
                                                                                
           ADD BT-TOT-QTY              TO GT-TOT-QTY.                           
           ADD BT-TOT-DOC-QTY          TO GT-TOT-DOC-QTY.                       
           ADD BT-TOT-EXTENDED-AMT     TO GT-TOT-EXTENDED-AMT.                  
           MOVE ZEROES                 TO BT-TOT-QTY                            
                                          BT-TOT-DOC-QTY                        
                                          BT-TOT-EXTENDED-AMT.                  
           PERFORM 6000-PRINT-HEADERS.                                          
                                                                                
       B400-PRINT-GRAND-TOTALS.                                                 
      ****************************************************************          
      *    FORMAT GRAND TOTAL PRINT LINE                                        
      *                                                                         
      *    PERFORMED BY 0000-MLKRP032.                                          
      *    PERFORMED BY 2000-PROCESS.                                           
      ****************************************************************          
                                                                                
           MOVE GT-TOT-QTY             TO DLGT-QTY.                             
           MOVE GT-TOT-DOC-QTY         TO DLGT-DOC-QTY.                         
           MOVE GT-TOT-EXTENDED-AMT    TO DLGT-EXTENDED-AMT.                    
           COMPUTE PV-DOCDIFF = GT-TOT-DOC-QTY - GT-TOT-QTY.                    
           MOVE PV-DOCDIFF             TO DLGT-DOC-DIFF.                        
                                                                                
           PERFORM Z500-PRINT-GRAND-TOTALS.                                     
                                                                                
       Z100-PRINT-DETAIL.                                                       
                                                                                
      ****************************************************************          
      *    PRINT DETAIL LINE.                                                   
      *                                                                         
      *    PERFORMED BY 2000-PROCESS.                                           
      ****************************************************************          
                                                                                
           IF PV-LINE-COUNTER >= PC-LINE-MAX                                    
              PERFORM 6000-PRINT-HEADERS                                        
           END-IF.                                                              
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM DL-DETAIL-LINE                                              
               AFTER ADVANCING 1 LINE.                                          
           ADD 1 TO PV-LINE-COUNTER.                                            
                                                                                
           EJECT                                                                
                                                                                
       Z200-PRINT-STORE-TOTALS.                                                 
                                                                                
      ****************************************************************          
      *    PRINT TOTAL LINE                                                     
      *                                                                         
      *    PERFORMED BY B100-PRINT-STORE-TOTALS.                                
      ****************************************************************          
                                                                                
           IF PV-LINE-COUNTER >= PC-LINE-MAX                                    
              PERFORM 6000-PRINT-HEADERS                                        
           END-IF.                                                              
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM DLST-STORE-TOTALS-PRINT-LINE                                
               AFTER ADVANCING 1 LINE.                                          
           ADD 2 TO PV-LINE-COUNTER.                                            
                                                                                
           EJECT                                                                
                                                                                
       Z300-PRINT-DOCNO-TOTALS.                                                 
                                                                                
      ****************************************************************          
      *    PRINT DOCNO TOTALS                                                   
      *                                                                         
      *    PERFORMED BY B200-PRINT-DOCNO-TOTALS.                                
      ****************************************************************          
                                                                                
           IF PV-LINE-COUNTER >= PC-LINE-MAX                                    
              PERFORM 6000-PRINT-HEADERS                                        
           END-IF.                                                              
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM DLDT-DOCNO-TOTALS-PRINT-LINE                                
               AFTER ADVANCING 2 LINE.                                          
           ADD 2 TO PV-LINE-COUNTER.                                            
                                                                                
           EJECT                                                                
                                                                                
       Z400-PRINT-BATCH-TOTALS.                                                 
                                                                                
      ****************************************************************          
      *    PRINT BATCH TOTALS                                                   
      *                                                                         
      *    PERFORMED BY B300-PRINT-BATCH-TOTALS.                                
      ****************************************************************          
                                                                                
           IF PV-LINE-COUNTER >= PC-LINE-MAX                                    
              PERFORM 6000-PRINT-HEADERS                                        
           END-IF.                                                              
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM DLBT-BATCH-TOTALS-PRINT-LINE                                
               AFTER ADVANCING 2 LINE.                                          
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM SL-SPACE-LINE                                               
               AFTER ADVANCING 1 LINE.                                          
           ADD 2 TO PV-LINE-COUNTER.                                            
                                                                                
           EJECT                                                                
                                                                                
       Z500-PRINT-GRAND-TOTALS.                                                 
                                                                                
      ****************************************************************          
      *    PRINT GRAND TOTALS                                                   
      *                                                                         
      *    PERFORMED BY B300-PRINT-BATCH-TOTALS.                                
      ****************************************************************          
                                                                                
           IF PV-LINE-COUNTER >= PC-LINE-MAX                                    
              PERFORM 6000-PRINT-HEADERS                                        
           END-IF.                                                              
           WRITE OUT-MARKDOWN-REPORT-RECORD                                     
               FROM DLGT-GRAND-TOTALS-PRINT-LINE                                
               AFTER ADVANCING 1 LINE.                                          
           ADD 2 TO PV-LINE-COUNTER.                                            
                                                                                
           EJECT                                                                
                                                                                
