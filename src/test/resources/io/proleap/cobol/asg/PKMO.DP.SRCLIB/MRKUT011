00001 ***************************************************************** 06/20/95
00002  IDENTIFICATION DIVISION.                                         MRKUT011
00003 *****************************************************************    LV002
00004                                                                   MRKUT011
00005  PROGRAM-ID.             MRKUT011.                                   CL**2
00006  AUTHOR.                 CHRISTINE FOLEY.                         MRKUT011
00007  INSTALLATION.           KOHLS DEPARTMENT STORES.                 MRKUT011
00008  DATE-WRITTEN            10-XX-92.                                MRKUT011
00009  DATE-COMPILED.                                                   MRKUT011
00010                                                                   MRKUT011
00011 ***************************************************************** MRKUT011
00012 *     EXTRACT OF TMRITST FOR DEMAND EXECPTION REPORTING         * MRKUT011
00013 *                                                               * MRKUT011
00014 *  THIS PROGRAM WILL EXTRACT THE RECORDS OFF OF THE TMRITST     * MRKUT011
00015 *  TABLE TO BE USED FOR PROCESSING IN MRKRP010                  * MRKUT011
00016 ***************************************************************** MRKUT011
00017 *  CHANGE MADE:                                                 * MRKUT011
00018 *                                                               * MRKUT011
00019 ***************************************************************** MRKUT011
00020 * CHANGED XX/XX/XX BY XXXXXXXXXXXXXXXXXXXXXXXXXX                  MRKUT011
00021 *   WR# XXXXXXXXXXXX                                              MRKUT011
00022 *            XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  MRKUT011
00023 *            XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  MRKUT011
00024 *            XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  MRKUT011
00025 *            XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  MRKUT011
00026 ***************************************************************** MRKUT011
002410* 07/30/2004 - ANNE KINTOPF STORE NUMBER EXPANSION  WR#26363    *         
002410*              MOVED TO PICK UP DCLGEN CHANGES FOR TMRISTS AND  *         
002410*              COPYBOOK CHANGES FOR MRRD010                     *         
002490*****************************************************************         
002490*****************************************************************         
00027  ENVIRONMENT DIVISION.                                            MRKUT011
00028 ***************************************************************** MRKUT011
00029                                                                   MRKUT011
00030  CONFIGURATION SECTION.                                           MRKUT011
00031                                                                   MRKUT011
00032  SOURCE-COMPUTER.        IBM-3090.                                MRKUT011
00033  OBJECT-COMPUTER.        IBM-3090.                                MRKUT011
00034                                                                   MRKUT011
00035  INPUT-OUTPUT SECTION.                                            MRKUT011
00036                                                                   MRKUT011
00037  FILE-CONTROL.                                                    MRKUT011
00038 *                                                                 MRKUT011
00039      SELECT PTAG-FILE           ASSIGN        TO OUT01.           MRKUT011
00040 *                                                                 MRKUT011
00041 ***************************************************************** MRKUT011
00042  DATA DIVISION.                                                   MRKUT011
00043 ***************************************************************** MRKUT011
00044 *                                                                 MRKUT011
00045  FILE SECTION.                                                    MRKUT011
00046 *                                                                 MRKUT011
00047  FD  PTAG-FILE                                                    MRKUT011
00048      RECORDING MODE F                                             MRKUT011
00049      BLOCK CONTAINS 0 RECORDS                                     MRKUT011
00050      RECORD CONTAINS 35 CHARACTERS                                MRKUT011
00051      LABEL RECORDS ARE OMITTED                                    MRKUT011
00052      DATA RECORD IS PTAG-RECS.                                    MRKUT011
00053 *                                                                 MRKUT011
00054  01  PTAG-RECS                                       PIC X(35).   MRKUT011
00055                                                                   MRKUT011
00056 *                                                                 MRKUT011
00057  WORKING-STORAGE SECTION.                                         MRKUT011
00058 *                                                                 MRKUT011
00059  01  FILLER.                                                      MRKUT011
00060      05  FILLER                      PIC  X(36)    VALUE          MRKUT011
00061          ' ***** WORKING STORAGE PGM=MRKUT001 '.                  MRKUT011
00062      05  PGM-COMPILED                PIC  X(20)    VALUE SPACE.   MRKUT011
00063      05  FILLER                      PIC  X(10)    VALUE          MRKUT011
00064          ' LOCATION='.                                            MRKUT011
00065      05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.   MRKUT011
00066      05  FILLER                      PIC  X(06)    VALUE          MRKUT011
00067          ' *****'.                                                MRKUT011
00068  EJECT                                                            MRKUT011
00069  01  PROGRAM-SWITCHES.                                            MRKUT011
00070      05  PS-END-OF-PTAG-CURSOR       PIC  X(01)    VALUE 'N'.     MRKUT011
00071          88  EOC-PTAG-CURSOR                       VALUE 'Y'.     MRKUT011
00072 *                                                                 MRKUT011
00073 /                                                                 MRKUT011
00074  01  PC-CONSTANTS.                                                MRKUT011
00075      05  PC-PLUS-1                   PIC S9(04)    VALUE +1       MRKUT011
00076                                                    COMP SYNC.     MRKUT011
00077      05  PC-PLUS-2                   PIC S9(04)    VALUE +2       MRKUT011
00078                                                    COMP SYNC.     MRKUT011
00079      05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100     MRKUT011
00080                                                    COMP SYNC.     MRKUT011
00081      05  PC-1                        PIC S9(03)    VALUE +1       MRKUT011
00082                                                    COMP-3.        MRKUT011
00083      05  PC-N                        PIC  X(01)    VALUE 'N'.     MRKUT011
00084      05  PC-Y                        PIC  X(01)    VALUE 'Y'.     MRKUT011
00085      05  PC-MRKUT011                 PIC  X(08)    VALUE          MRKUT011
00086                                                    'MRKUT011'.    MRKUT011
00087      05  PC-ASTERISK                 PIC  X(30)    VALUE          MRKUT011
00088          '*****************************'.                         MRKUT011
00089  EJECT                                                            MRKUT011
00090 *                                                                 MRKUT011
00091  01  PV-MISC-FIELDS.                                              MRKUT011
00092      05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO     MRKUT011
00093                                                    COMP SYNC.     MRKUT011
00094      05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.  MRKUT011
00095 *                                                                 MRKUT011
00096      COPY MRRD010.                                                MRKUT011
00097 *                                                                 MRKUT011
00098 * MERCHANDISE REPLENISHMENT ITEM/STORE TABLE                      MRKUT011
00099      EXEC SQL                                                     MRKUT011
00100          INCLUDE TMRITST                                          MRKUT011
00101      END-EXEC.                                                    MRKUT011
00102  EJECT                                                            MRKUT011
00103 * DB2 COMMUNICATION AREA                                          MRKUT011
00104 *                                                                 MRKUT011
00105      EXEC SQL                                                     MRKUT011
00106          INCLUDE SQLCA                                            MRKUT011
00107      END-EXEC.                                                    MRKUT011
00108  EJECT                                                            MRKUT011
00109 * DB2 COMMUNICATION AREA2                                         MRKUT011
00110 *                                                                 MRKUT011
00111      COPY SQLCA2.                                                 MRKUT011
00112  EJECT                                                            MRKUT011
00113 *                                                                 MRKUT011
00114 *                                                                 MRKUT011
00115 * CURSOR FOR MR ITEM STORE TABLE RETRIEVING ALL PTAGS             MRKUT011
00116 *                                                                 MRKUT011
00117      EXEC SQL                                                     MRKUT011
00118        DECLARE PTAG_CSR CURSOR FOR                                MRKUT011
00119          SELECT PTAG_ID,                                          MRKUT011
00120                 ITEM_NBR,                                         MRKUT011
00121                 STR_NBR,                                          MRKUT011
00122                 DFLREG_CDE                                        MRKUT011
00123            FROM TMRITST                                           MRKUT011
00124           WHERE PTAG_ID > '       '                               MRKUT011
00125      END-EXEC.                                                    MRKUT011
00126 *                                                                 MRKUT011
00127 ***************************************************************** MRKUT011
00128 *PROCEDURE DIVISION.                                              MRKUT011
00129 ***************************************************************** MRKUT011
00130  PROCEDURE DIVISION.                                              MRKUT011
00131 *                                                                 MRKUT011
00132  A000-MAINLINE-PROCESSING.                                        MRKUT011
00133 *                                                                 MRKUT011
00134      PERFORM A100-INITIALIZATION.                                 MRKUT011
00135 *                                                                 MRKUT011
00136      PERFORM B000-PROCESS-PTAG-CURSOR                             MRKUT011
00137          UNTIL EOC-PTAG-CURSOR.                                   MRKUT011
00138 *                                                                 MRKUT011
00139      PERFORM Y100-END-ROUTINE.                                    MRKUT011
00140 *                                                                 MRKUT011
00141      GOBACK.                                                      MRKUT011
00142 *                                                                 MRKUT011
00143  EJECT                                                            MRKUT011
00144 ******************************************************************MRKUT011
00145 ** OPEN THE SYSTEM FILE AND ACCEPT CONTROL CARDS.                 MRKUT011
00146 ******************************************************************MRKUT011
00147  A100-INITIALIZATION.                                             MRKUT011
00148 *                                                                 MRKUT011
00149      OPEN  OUTPUT PTAG-FILE.                                      MRKUT011
00150 *                                                                 MRKUT011
00151      PERFORM S000-OPEN-PTAG-CURSOR.                               MRKUT011
00152 *                                                                 MRKUT011
00153  EJECT                                                            MRKUT011
00154 ******************************************************************MRKUT011
00155 *  THIS IS THE MAIN CONTROLLING PARAGRAPH FOR PROCESSING THE     *MRKUT011
00156 *  PTAG CURSOR.                                                  *MRKUT011
00157 ******************************************************************MRKUT011
00158  B000-PROCESS-PTAG-CURSOR.                                        MRKUT011
00159 *                                                                 MRKUT011
00160      PERFORM S100-FETCH-PTAG-CURSOR.                              MRKUT011
00161 *                                                                 MRKUT011
00162      MOVE MRITST-PTAG-ID    TO   MR010-PTAG-ID.                   MRKUT011
00163      MOVE MRITST-ITEM-NBR   TO   MR010-ITEM-NBR.                  MRKUT011
00164      MOVE MRITST-STR-NBR    TO   MR010-STR-NBR.                   MRKUT011
00165      MOVE MRITST-DFLREG-CDE TO   MR010-DFLREG-CDE.                MRKUT011
00166 *                                                                 MRKUT011
00167      PERFORM W000-WRITE-PTAG-FILE.                                MRKUT011
00168 *                                                                 MRKUT011
00169  EJECT                                                            MRKUT011
00170 ***************************************************************** MRKUT011
00171 * S000-OPEN-PTAG-CURSOR                                         * MRKUT011
00172 *                                                               * MRKUT011
00173 *                                                               * MRKUT011
00174 ***************************************************************** MRKUT011
00175                                                                   MRKUT011
00176  S000-OPEN-PTAG-CURSOR.                                           MRKUT011
00177                                                                   MRKUT011
00178 *    DISPLAY 'S000-OPEN-PTAG-CURSOR'.                             MRKUT011
00179                                                                   MRKUT011
00180      MOVE 'S000-OPEN-PTAG-CURSOR'                                 MRKUT011
00181                                  TO PV-ABEND-PARAGRAPH.           MRKUT011
00182                                                                   MRKUT011
00183      EXEC SQL                                                     MRKUT011
00184          OPEN PTAG_CSR                                            MRKUT011
00185      END-EXEC.                                                    MRKUT011
00186                                                                   MRKUT011
00187      EVALUATE TRUE                                                MRKUT011
00188        WHEN SQLCODE = ZEROS                                       MRKUT011
00189        WHEN SQLCODE = PC-ROW-NOT-FOUND                            MRKUT011
00190          CONTINUE                                                 MRKUT011
00191        WHEN OTHER                                                 MRKUT011
00192          DISPLAY PV-ABEND-PARAGRAPH                               MRKUT011
00193          PERFORM Z999-SQL-ERROR                                   MRKUT011
00194      END-EVALUATE.                                                MRKUT011
00195                                                                   MRKUT011
00196      EJECT                                                        MRKUT011
00197 *                                                                 MRKUT011
00198 ***************************************************************** MRKUT011
00199 * S100-FETCH-PTAG-CURSOR                                        * MRKUT011
00200 *                                                               * MRKUT011
00201 *                                                               * MRKUT011
00202 ***************************************************************** MRKUT011
00203                                                                   MRKUT011
00204  S100-FETCH-PTAG-CURSOR.                                          MRKUT011
00205                                                                   MRKUT011
00206 *    DISPLAY 'S100-FETCH-PTAG-CURSOR'.                            MRKUT011
00207                                                                   MRKUT011
00208      MOVE 'S100-FETCH-PTAG-CURSOR'                                MRKUT011
00209                                  TO PV-ABEND-PARAGRAPH.           MRKUT011
00210                                                                   MRKUT011
00211      EXEC SQL                                                     MRKUT011
00212           FETCH PTAG_CSR                                          MRKUT011
00213            INTO :MRITST-PTAG-ID,                                  MRKUT011
00214                 :MRITST-ITEM-NBR,                                 MRKUT011
00215                 :MRITST-STR-NBR,                                  MRKUT011
00216                 :MRITST-DFLREG-CDE                                MRKUT011
00217      END-EXEC.                                                    MRKUT011
00218                                                                   MRKUT011
00219      EVALUATE TRUE                                                MRKUT011
00220        WHEN SQLCODE = ZEROS                                       MRKUT011
00221          CONTINUE                                                 MRKUT011
00222        WHEN SQLCODE = PC-ROW-NOT-FOUND                            MRKUT011
00223          MOVE PC-Y TO  PS-END-OF-PTAG-CURSOR                      MRKUT011
00224        WHEN OTHER                                                 MRKUT011
00225          DISPLAY PV-ABEND-PARAGRAPH                               MRKUT011
00226          PERFORM Z999-SQL-ERROR                                   MRKUT011
00227      END-EVALUATE.                                                MRKUT011
00228                                                                   MRKUT011
00229      EJECT                                                        MRKUT011
00230 *                                                                 MRKUT011
00231 ***************************************************************** MRKUT011
00232 * S300-CLOSE-PTAG-CURSOR                                        * MRKUT011
00233 *                                                               * MRKUT011
00234 *                                                               * MRKUT011
00235 ***************************************************************** MRKUT011
00236                                                                   MRKUT011
00237  S300-CLOSE-PTAG-CURSOR.                                          MRKUT011
00238                                                                   MRKUT011
00239 *    DISPLAY 'S300-CLOSE-PTAG-CURSOR'.                            MRKUT011
00240                                                                   MRKUT011
00241      MOVE 'S300-CLOSE-PTAG-CURSOR'                                MRKUT011
00242                                  TO PV-ABEND-PARAGRAPH.           MRKUT011
00243                                                                   MRKUT011
00244      EXEC SQL                                                     MRKUT011
00245           CLOSE PTAG_CSR                                          MRKUT011
00246      END-EXEC.                                                    MRKUT011
00247                                                                   MRKUT011
00248      EVALUATE TRUE                                                MRKUT011
00249        WHEN SQLCODE = ZEROS                                       MRKUT011
00250        WHEN SQLCODE = PC-ROW-NOT-FOUND                            MRKUT011
00251          CONTINUE                                                 MRKUT011
00252        WHEN OTHER                                                 MRKUT011
00253          DISPLAY PV-ABEND-PARAGRAPH                               MRKUT011
00254          PERFORM Z999-SQL-ERROR                                   MRKUT011
00255      END-EVALUATE.                                                MRKUT011
00256                                                                   MRKUT011
00257      EJECT                                                        MRKUT011
00258 *                                                                 MRKUT011
00259 ******************************************************************MRKUT011
00260 *     WRITES THE REVISED SYSTEM RECORD.                          *MRKUT011
00261 *                                                                *MRKUT011
00262 ******************************************************************MRKUT011
00263  W000-WRITE-PTAG-FILE.                                            MRKUT011
00264 *                                                                 MRKUT011
00265      MOVE 'W000'                  TO PGM-LOCATION.                MRKUT011
00266      MOVE 'W000-WRITE-PTAG-FILE       '                           MRKUT011
00267                                   TO PV-ABEND-PARAGRAPH.          MRKUT011
00268 *                                                                 MRKUT011
00269      WRITE PTAG-RECS              FROM MR010-PTAG-FILE.           MRKUT011
00270 *                                                                 MRKUT011
00271  EJECT                                                            MRKUT011
00272 ******************************************************************MRKUT011
00273 *   CLOSES FILES AND DISPLAYS MESSAGES.                          *MRKUT011
00274 ******************************************************************MRKUT011
00275  Y100-END-ROUTINE.                                                MRKUT011
00276 *                                                                 MRKUT011
00277      PERFORM S300-CLOSE-PTAG-CURSOR.                              MRKUT011
00278 *                                                                 MRKUT011
00279      CLOSE  PTAG-FILE.                                            MRKUT011
00280 *                                                                 MRKUT011
00281  EJECT                                                            MRKUT011
00282 ******************************************************************MRKUT011
00283 *     SQL WARNING OR ERROR PROCESSING                            *MRKUT011
00284 *                                                                *MRKUT011
00285 ******************************************************************MRKUT011
00286                                                                   MRKUT011
00287  Z999-SQL-ERROR.                                                  MRKUT011
00288 *---------------                                                  MRKUT011
00289                                                                   MRKUT011
00290      MOVE SQLCODE    TO SQLCODE2.                                 MRKUT011
00291      MOVE SQLERRD(3) TO SQLERRD3.                                 MRKUT011
00292      DISPLAY '** ABEND **       ** = SQLERROR'.                   MRKUT011
00293      DISPLAY '** PROGRAM        ** = MRKUT011'.                   MRKUT011
00294      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  MRKUT011
00295      DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  MRKUT011
00296      DISPLAY '** SQLERRM        ** = ' SQLERRM.                   MRKUT011
00297      DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  MRKUT011
00298      MOVE +4013 TO PV-ABEND-CODE.                                 MRKUT011
00299      CALL 'ILBOABN0' USING PV-ABEND-CODE.                         MRKUT011
00300 *                                                                 MRKUT011
00301 ******************************************************************MRKUT011
