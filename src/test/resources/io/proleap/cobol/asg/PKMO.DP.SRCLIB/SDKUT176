       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    SDKUT176.                                         00020000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  11/29/2012.                                       00050000
       DATE-COMPILED.                                                   00060000
                                                                        00061000
      ******************************************************************00062000
      * DELTA PROCESSING FOR TRUCK PROJECTED NON-CANCELLED DELIVERIES   00063005
      *    COMPARES PREVIOUS VS CURRENT EXTRACT FILES AND WRITES        00063105
      *    A CHANGE RECORD FOR ADDS, UPDATES, AND DELETES.              00063205
      *    ONLY WRITE RECORDS FOR TRUCK ARRIVAL DATES >= TODAYS DATE    00065005
      ******************************************************************00066000
                                                                        00069000
       ENVIRONMENT DIVISION.                                            00070000
       CONFIGURATION SECTION.                                           00080000
       SOURCE-COMPUTER.    IBM-3090.                                    00090000
       OBJECT-COMPUTER.    IBM-3090.                                    00100000
       INPUT-OUTPUT SECTION.                                            00110000
       FILE-CONTROL.                                                    00120000
                                                                        00130000
           SELECT CURR-EXTRACT-FILE                                     00140000
                  ASSIGN TO INP01.                                      00150000
           SELECT PREV-EXTRACT-FILE                                     00160000
                  ASSIGN TO INP02.                                      00170000
           SELECT DELTA-FILE                                            00180000
                  ASSIGN TO OUT01.                                      00190000
           SELECT DELTA-COUNT-FILE                                      00200000
                  ASSIGN TO OUT02.                                      00210000
                                                                        00220000
       DATA DIVISION.                                                   00230000
       FILE SECTION.                                                    00240000
                                                                        00250000
       FD  CURR-EXTRACT-FILE                                            00260000
           RECORDING MODE IS F                                          00270000
           LABEL RECORDS OMITTED                                        00280000
           BLOCK CONTAINS 0 RECORDS                                     00290000
           DATA RECORD IS CURR-EXTRACT-RECORD.                          00300000
       01  CURR-EXTRACT-RECORD                 PIC X(80).               00310000
                                                                        00320000
       FD  PREV-EXTRACT-FILE                                            00330000
           RECORDING MODE IS F                                          00340000
           LABEL RECORDS OMITTED                                        00350000
           BLOCK CONTAINS 0 RECORDS                                     00360000
           DATA RECORD IS PREV-EXTRACT-RECORD.                          00370000
       01  PREV-EXTRACT-RECORD                 PIC X(80).               00380000
                                                                        00390000
       FD  DELTA-FILE                                                   00400000
           RECORDING MODE IS F                                          00410000
           LABEL RECORDS OMITTED                                        00420000
           BLOCK CONTAINS 0 RECORDS                                     00430000
           DATA RECORD IS DELTA-RECORD.                                 00440000
       01  DELTA-RECORD                        PIC X(31).               00450008
                                                                        00460000
       FD  DELTA-COUNT-FILE                                             00470000
           RECORDING MODE IS F                                          00480000
           LABEL RECORDS OMITTED                                        00490000
           BLOCK CONTAINS 0 RECORDS                                     00500000
           DATA RECORD IS DELTA-COUNT-RECORD.                           00510000
       01  DELTA-COUNT-RECORD                  PIC 9(07).               00511000
                                                                        00512000
       WORKING-STORAGE SECTION.                                         00513000
      ******************************************************************00514000
      **PROGRAM SWITCHES                                                00515000
      ******************************************************************00516000
       01  PS-SWITCH-AREAS.                                             00517000
           05  PS-END-OF-CUR-FILE-SW           PIC X(01)  VALUE 'N'.    00518000
               88  PS-END-OF-CUR-FILE                     VALUE 'Y'.    00519000
               88  PS-NOT-END-OF-CUR-FILE                 VALUE 'N'.    00520000
           05  PS-END-OF-PRV-FILE-SW           PIC X(01)  VALUE 'N'.    00530000
               88  PS-END-OF-PRV-FILE                     VALUE 'Y'.    00540000
               88  PS-NOT-END-OF-PRV-FILE                 VALUE 'N'.    00550000
                                                                        00551005
      ******************************************************************00560000
      **DATE TIME FIELDS                                                00570000
      ******************************************************************00580000
       01 DF-DATE-TIME-FIELDS.                                          00590000
          05 DF-ACCEPT-DATE                    PIC X(06) VALUE SPACES.  00600000
          05 DF-ACCEPT-TIME                    PIC X(08) VALUE SPACES.  00610000
                                                                        00611005
      ******************************************************************00620000
      **PROGRAM ACCUMULATORS (COUNTERS)                                 00630000
      ******************************************************************00640000
       01  PA-PROGRAM-ACCUMULATORS.                                     00650000
           05  PA-CUR-READ                     PIC 9(07) VALUE ZEROS.   00660000
           05  PA-PRV-READ                     PIC 9(07) VALUE ZEROS.   00670000
           05  PA-MATCH-COUNT                  PIC 9(07) VALUE ZEROS.   00680000
           05  PA-DATE-OOB-COUNT               PIC 9(07) VALUE ZEROS.   00690000
           05  PA-WRITE-ADD-COUNT              PIC 9(07) VALUE ZEROS.   00700000
           05  PA-WRITE-DLT-COUNT              PIC 9(07) VALUE ZEROS.   00720000
           05  PA-TTL-WRITE-COUNT              PIC 9(07) VALUE ZEROS.   00730000
                                                                        00740000
      ******************************************************************00750000
      **PROGRAM VARIABLES                                               00760000
      ******************************************************************00770000
       01  PV-PROGRAM-VARIABLES.                                        00780000
           05  PV-COUNT-RECORD.                                         00781000
               10  PV-RECORDS-WRITTEN-TO-FILE  PIC 9(07) VALUE ZEROES.  00781105
                                                                        00781200
       01  PV-FISCAL-PERIOD-DATES.                                      00781305
           05  PV-CUR-FP-1ST-DTE               PIC X(10) VALUE SPACES.  00781405
           05  PV-CUR-FP-LAST-DTE              PIC X(10) VALUE SPACES.  00781505
           05  PV-PRV-FP-1ST-DTE               PIC X(10) VALUE SPACES.  00781605
           05  PV-PRV-FP-LAST-DTE              PIC X(10) VALUE SPACES.  00781705
           05  PV-NXT-FP-1ST-DTE               PIC X(10) VALUE SPACES.  00781805
           05  PV-NXT-FP-LAST-DTE              PIC X(10) VALUE SPACES.  00781905
                                                                        00782000
      ******************************************************************00782100
      **PRIMARY KEYS USED TO MATCH UP RECORDS                           00782200
      ******************************************************************00782300
       01  PV-PRIMARY-KEYS.                                             00782400
           05  PV-CUR-KEY.                                              00782500
               10  PV-CUR-STR-NBR              PIC 9(04) VALUE ZEROES.  00782605
               10  PV-CUR-TRLR-ARRVL-DTE       PIC X(10) VALUE SPACES.  00782705
           05  PV-PRV-KEY.                                              00782800
               10  PV-PRV-STR-NBR              PIC 9(04) VALUE ZEROES.  00782905
               10  PV-PRV-TRLR-ARRVL-DTE       PIC X(10) VALUE SPACES.  00783005
                                                                        00783100
      ******************************************************************00784000
      *               SYSTEM TIME AND DATE VARIABLES                    00785000
      ******************************************************************00786000
       01  SYS-DATE-TIME.                                               00786100
           05  SYS-DATE                        PIC X(08) VALUE SPACES.  00786205
           05  SYS-TIME                        PIC X(08) VALUE SPACES.  00786305
                                                                        00786400
       01  ST-BEG-TIME.                                                 00786500
           05  ST-BEG-HR                       PIC 9(02) VALUE ZEROS.   00786605
           05  FILLER                          PIC X(01) VALUE ':'.     00786705
           05  ST-BEG-MN                       PIC 9(02) VALUE ZEROS.   00786805
                                                                        00786900
       01  ST-END-TIME.                                                 00787000
           05  ST-END-HR                       PIC 9(02) VALUE ZEROS.   00788005
           05  FILLER                          PIC X(01) VALUE ':'.     00789005
           05  ST-END-MN                       PIC 9(02) VALUE ZEROS.   00789105
                                                                        00789200
       01  PV-TODAYS-DATE.                                              00790102
           05 PV-YEAR                          PIC 9(04) VALUE ZEROES.  00790205
           05 FILLER                           PIC X(01) VALUE '-'.     00790305
           05 PV-MONTH                         PIC 9(02) VALUE ZEROES.  00790405
           05 FILLER                           PIC X(01) VALUE '-'.     00790505
           05 PV-DAY                           PIC 9(02) VALUE ZEROES.  00790605
                                                                        00790700
      ****************************************************************  00791300
      **ABEND INFORMATION                                               00791400
      ****************************************************************  00791500
       01  AC-ABEND-FIELDS.                                             00791600
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    00791700
                                                           COMP SYNC.   00791800
               88  AC-DATE-ROUTINE-ERROR                 VALUE +4019.   00791900
                                                                        00792000
       01  PV-MISC-ABEND-FIELDS.                                        00793000
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   00794000
           05  PV-OPERATION-MSG                PIC X(40) VALUE SPACE.   00795000
                                                                        00795105
      ******************************************************************00796000
      **COPYBOOKS                                                       00797000
      ******************************************************************00798000
      ****BOTH INPUT FILES USE THE SAME COPYBOOK, THIS MAKES THEM UNIQUE00799000
           COPY SDRD175 REPLACING 'SDRD175-' BY 'SD175-CUR-'.           00800001
           COPY SDRD175 REPLACING 'SDRD175-' BY 'SD175-PRV-'.           00810001
      ****OUTPUT RECORD LAYOUT                                          00820000
           COPY SDRD175 REPLACING 'SDRD175-' BY 'SD175-OUT-'.           00830001
                                                                        00840000
       PROCEDURE DIVISION.                                              00910000
      ******************************************************************00920000
       0000-MAINLINE.                                                   00930000
                                                                        00940000
           PERFORM 1000-INITIALIZATION.                                 00950000
                                                                        00960000
           PERFORM 2000-PROCESS-RECORDS                                 00970000
                   UNTIL PS-END-OF-CUR-FILE                             00980000
                      OR PS-END-OF-PRV-FILE.                            00990000
                                                                        01000000
           PERFORM 2010-PROCESS-EXTRA-RECORDS                           01010010
                   UNTIL PS-END-OF-CUR-FILE                             01020000
                     AND PS-END-OF-PRV-FILE.                            01030000
                                                                        01031000
           PERFORM 9000-WRAP-UP.                                        01032009
           GOBACK.                                                      01033000
                                                                        01034000
      ******************************************************************01035000
      **INITIALIZATION ROUTINE                                          01036000
      ******************************************************************01037000
       1000-INITIALIZATION.                                             01038000
                                                                        01039000
           DISPLAY '*************************************'.             01039105
           DISPLAY '* SDKUT176 - TRUCK NON-CANCELLED    *'.             01039205
           DISPLAY '*            PROJECTED DELIVERIES   *'.             01039305
           DISPLAY '*            DELTA FILE PROCESSING  *'.             01039405
           DISPLAY '*************************************'.             01039505
           DISPLAY '*'.                                                 01039805
           DISPLAY '* - PROCESSING...'.                                 01039905
                                                                        01040005
           OPEN INPUT  CURR-EXTRACT-FILE                                01041005
                       PREV-EXTRACT-FILE.                               01050000
           OPEN OUTPUT DELTA-FILE.                                      01060000
                                                                        01070000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01090000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01100000
                                                                        01110000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 01120000
                                                                        01130000
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            01140000
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            01150000
                                                                        01160000
           MOVE SYS-DATE (1:4)         TO PV-YEAR.                      01182201
           MOVE SYS-DATE (5:2)         TO PV-MONTH.                     01182301
           MOVE SYS-DATE (7:2)         TO PV-DAY.                       01182401
           DISPLAY '* SYSTEM DATE: ' PV-TODAYS-DATE.                    01182501
                                                                        01183000
           PERFORM 4000-READ-CUR-EXTR-RECORD.                           01186000
           PERFORM 4100-READ-PRV-EXTR-RECORD.                           01187000
                                                                        01253700
      ******************************************************************01255200
      **COMPARES THE INPUT FILES AND IDENTIFIES ADDS, DELETES, AND CHG  01255300
      ******************************************************************01255400
       2000-PROCESS-RECORDS.                                            01255500
                                                                        01255600
           EVALUATE TRUE                                                01255700
      *--> KEY DATA MATCHES ; NO NON-KEY DATA VALUES TO CHECK HERE      01255800
              WHEN PV-CUR-KEY EQUAL PV-PRV-KEY                          01255900
                   ADD +1 TO PA-MATCH-COUNT                             01281000
                   PERFORM 4000-READ-CUR-EXTR-RECORD                    01283000
                   PERFORM 4100-READ-PRV-EXTR-RECORD                    01284000
      *--> CURRENT FILE HAS AN ADDITIONAL RECORD, SO LIST AS 'A'        01285000
              WHEN PV-CUR-KEY LESS THAN PV-PRV-KEY                      01286000
                   PERFORM 2100-WRITE-CURRENT-ADD                       01287000
                   PERFORM 4000-READ-CUR-EXTR-RECORD                    01288000
      *--> CURRENT FILE IS MISSING A PREV RECORD, SO LIST AS 'D'        01289000
              WHEN PV-CUR-KEY GREATER THAN PV-PRV-KEY                   01290000
                   PERFORM 2300-WRITE-PREVIOUS-DELETE                   01291000
                   PERFORM 4100-READ-PRV-EXTR-RECORD                    01291100
           END-EVALUATE.                                                01291200
                                                                        01291300
      ******************************************************************01291400
      * 2010-PROCESS-EXTRA-RECORDS.                                     01291510
      *     PROCESSES THRU END OF FILE FOR REMAINING RECORDS.           01291600
      ******************************************************************01291700
       2010-PROCESS-EXTRA-RECORDS.                                      01291810
                                                                        01291900
      * WHEN CUR EXTRACT FILE STILL HAS RECORDS... COUNT AS 'A'         01292000
           IF  PS-NOT-END-OF-CUR-FILE                                   01293000
               PERFORM 2100-WRITE-CURRENT-ADD                           01293100
               PERFORM 4000-READ-CUR-EXTR-RECORD                        01293200
           END-IF.                                                      01293300
                                                                        01293400
      * WHEN PRV EXTRACT FILE STILL HAS RECORDS... COUNT AS 'D'         01293500
           IF  PS-NOT-END-OF-PRV-FILE                                   01293600
               PERFORM 2300-WRITE-PREVIOUS-DELETE                       01293700
               PERFORM 4100-READ-PRV-EXTR-RECORD                        01293800
           END-IF.                                                      01293900
                                                                        01294000
      ******************************************************************01295000
      **WRITE CURRENT RECORD - ADD                                      01296000
      ******************************************************************01297000
       2100-WRITE-CURRENT-ADD.                                          01298000
                                                                        01299000
           MOVE SD175-CUR-EXTRACT-RECORD TO SD175-OUT-EXTRACT-RECORD.   01300001
           MOVE 'A' TO SD175-OUT-CHANGE-ACTN-CDE.                       01310001
                                                                        01320000
           IF SD175-OUT-TRLR-ARRVL-DTE >= PV-TODAYS-DATE                01330002
              WRITE DELTA-RECORD FROM SD175-OUT-EXTRACT-RECORD          01350001
              ADD +1 TO PA-WRITE-ADD-COUNT                              01360000
              ADD +1 TO PA-TTL-WRITE-COUNT                              01370000
           ELSE                                                         01380000
              ADD +1 TO PA-DATE-OOB-COUNT                               01390000
              DISPLAY 'SKIPPED; DATE OOB: ' SD175-OUT-EXTRACT-RECORD    01400001
           END-IF.                                                      01410000
                                                                        01420000
                                                                        01546000
      ******************************************************************01547000
      **WRITE PREVIOUS RECORD - DELETE                                  01548000
      ******************************************************************01549000
       2300-WRITE-PREVIOUS-DELETE.                                      01549100
                                                                        01549200
           MOVE SD175-PRV-EXTRACT-RECORD TO SD175-OUT-EXTRACT-RECORD.   01549301
           MOVE 'D' TO SD175-OUT-CHANGE-ACTN-CDE.                       01549401
                                                                        01549500
           IF SD175-OUT-TRLR-ARRVL-DTE >= PV-TODAYS-DATE                01549601
              WRITE DELTA-RECORD FROM SD175-OUT-EXTRACT-RECORD          01549901
              ADD +1 TO PA-WRITE-DLT-COUNT                              01550000
              ADD +1 TO PA-TTL-WRITE-COUNT                              01550100
           ELSE                                                         01551000
              ADD +1 TO PA-DATE-OOB-COUNT                               01551100
              DISPLAY 'SKIPPED; DATE OOB: ' SD175-OUT-EXTRACT-RECORD    01551201
           END-IF.                                                      01551300
                                                                        01551400
      ******************************************************************01551500
      **READ CURR FILE RECORD                                           01551600
      ******************************************************************01551700
       4000-READ-CUR-EXTR-RECORD.                                       01551800
                                                                        01551900
           READ CURR-EXTRACT-FILE                                       01552000
              INTO SD175-CUR-EXTRACT-RECORD                             01553001
                 AT END                                                 01554000
                 SET PS-END-OF-CUR-FILE TO TRUE                         01555000
           END-READ.                                                    01556000
                                                                        01557000
      * MOVE KEYS                                                       01558000
           IF  PS-NOT-END-OF-CUR-FILE                                   01559000
               ADD +1                        TO PA-CUR-READ             01560001
               MOVE SD175-CUR-STR-NBR        TO PV-CUR-STR-NBR          01570001
               MOVE SD175-CUR-TRLR-ARRVL-DTE TO PV-CUR-TRLR-ARRVL-DTE   01580001
           END-IF.                                                      01590000
                                                                        01600000
      ******************************************************************01610000
      **READ PRICE CHANGE RECORD FROM THE PREVIOUS REPORT FILE          01620000
      ******************************************************************01630000
       4100-READ-PRV-EXTR-RECORD.                                       01640000
                                                                        01650000
           READ PREV-EXTRACT-FILE                                       01660000
              INTO SD175-PRV-EXTRACT-RECORD                             01670001
                 AT END                                                 01680000
                 SET PS-END-OF-PRV-FILE TO TRUE                         01690000
           END-READ.                                                    01700000
                                                                        01710000
      * MOVE KEYS                                                       01720000
           IF  PS-NOT-END-OF-PRV-FILE                                   01730000
               ADD +1                        TO PA-PRV-READ             01740001
               MOVE SD175-PRV-STR-NBR        TO PV-PRV-STR-NBR          01750001
               MOVE SD175-PRV-TRLR-ARRVL-DTE TO PV-PRV-TRLR-ARRVL-DTE   01751001
           END-IF.                                                      01770000
                                                                        01780000
      ******************************************************************01786700
      **CLOSE FILES                                                     01786800
      ******************************************************************01786900
       9000-WRAP-UP.                                                    01787009
                                                                        01788000
      *    UPDATE THE DELTA COUNT FILE WITH NUMBER OF RECORDS WRITTEN   01789000
           OPEN OUTPUT DELTA-COUNT-FILE.                                01790000
           WRITE DELTA-COUNT-RECORD FROM PA-TTL-WRITE-COUNT.            01791000
           CLOSE DELTA-COUNT-FILE.                                      01792000
                                                                        01793000
           CLOSE CURR-EXTRACT-FILE                                      01794000
                 PREV-EXTRACT-FILE                                      01795000
                 DELTA-FILE.                                            01796000
                                                                        01797000
           PERFORM 9999-DISPLAY-STATISTICS.                             01798000
                                                                        01799000
      ******************************************************************01800000
      **DISPLAY PROGRAM STATS                                           01810000
      ******************************************************************01820000
       9999-DISPLAY-STATISTICS.                                         01830000
                                                                        01840000
           DISPLAY '*************************************************'. 01850000
           DISPLAY '* PROGRAM...........: SDKUT176'.                    01860000
           DISPLAY '* DATE..............: ' DF-ACCEPT-DATE.             01870000
           DISPLAY '* TIME..............: ' DF-ACCEPT-TIME.             01880000
           DISPLAY '*************************************************'. 01890000
           DISPLAY '* CUR INP RECORDS READ: ' PA-CUR-READ.              01900000
           DISPLAY '* PRV INP RECORDS READ: ' PA-PRV-READ.              01910000
           DISPLAY '*************************************************'. 01920000
           DISPLAY '* DELTA PROCESSING...'.                             01930000
           DISPLAY '* - ADD RECORDS COUNT: ' PA-WRITE-ADD-COUNT.        01940000
           DISPLAY '* - DLT RECORDS COUNT: ' PA-WRITE-DLT-COUNT.        01960000
           DISPLAY '*************************************************'. 01970000
           DISPLAY '* - TOTAL WRITE COUNT: ' PA-TTL-WRITE-COUNT.        01980000
           DISPLAY '*************************************************'. 01990000
           DISPLAY '* MATCHED RECORDS, NOT NEEDED: ' PA-MATCH-COUNT.    02000000
           DISPLAY '* SKIPPED DUE TO DATE OOB....: ' PA-DATE-OOB-COUNT. 02010000
           DISPLAY '*************************************************'. 02020000
                                                                        02030000
      ******************************************************************02040000
