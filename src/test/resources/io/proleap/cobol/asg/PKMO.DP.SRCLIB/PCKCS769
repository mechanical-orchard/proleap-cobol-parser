      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    PCKCS769.                                                 
       AUTHOR.        SUBASH T.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  AUGUST, 2013.                                             
       DATE-COMPILED.                                                           
      *----------------------------------------------------------------*        
      *    E769 - PO-SET LIST REPORT                                   *        
      *                                                                *        
      *    THIS PROGRAM ALLOWS THE USER TO ENTER PO SET CODE (MAX 10   *        
      *    SET CODES) AND YEAR TO GET THE RELEVANT PO-SET LIST REPORT  *        
      *    BY PRESSING F24 KEY                                         *        
      *----------------------------------------------------------------*        
1122RM* DATE  11/07/2022                                              *         
1122RM*            - ADD COPYBOOK DPWS930                             *         
1122RM*              CHANGE CALL TO DPWS030 FROM STATIC TO            *         
1122RM*              DYNAMIC.                                         *         
1122RM*     MADE BY: BOB MCASEY              WR/IR #: CHG0277910      *         
1122RM*-----------------------------------------------------------------        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *----------------------------------------------------------------*        
      *  LAYOUT FOR THE ABEND MODULE COMMUNICATIONS AREA               *        
      *----------------------------------------------------------------*        
           COPY DPWS013.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *  CICS ATTRIBUTES COPYBOOK                                      *        
      *----------------------------------------------------------------*        
           COPY DPWS015.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *  FUNCTION KEYS COPYBOOK                                        *        
      *----------------------------------------------------------------*        
           COPY DPWS016.                                                        
                                                                                
           COPY DPWS017.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *  STANDARD ATTENTION IDENTIFIER LIST                            *        
      *----------------------------------------------------------------*        
           COPY DFHAID.                                                         
                                                                                
      *----------------------------------------------------------------*        
      *  DSECT FOR THE PCKM769 MAPSET                                  *        
      *----------------------------------------------------------------*        
           COPY PCKM769.                                                        
        01  MR-SET-CDE-INP-LAYOUT   REDEFINES  PC769AI.                         
            05 FILLER                    PIC X(21).                             
            05 MR-SET-CODE-DET      OCCURS 10 TIMES.                            
               10 FILLER                 PIC X(8).                              
               10 MR-MCDEL               PIC S9(4) COMP.                        
               10 MR-MCDEF               PIC X.                                 
               10 FILLER REDEFINES MR-MCDEF.                                    
                  15 MR-MCDEA            PIC X.                                 
               10 FILLER                 PIC X(2).                              
               10 MR-MCDEI               PIC X(5).                              
               10 FILLER                 PIC X(30).                             
                                                                                
        01  MR-SET-CDE-OUT-LAYOUT   REDEFINES  PC769AO.                         
            05 FILLER                    PIC X(21).                             
            05 MR-SET-CODE-DET-OUT  OCCURS 10 TIMES.                            
               10 FILLER                 PIC X(11).                             
               10 MR-MCDEC               PIC X(1).                              
               10 MR-MCDEH               PIC X(1).                              
               10 MR-MCDEO               PIC X(5).                              
               10 FILLER                 PIC X(05).                             
               10 MR-MNMEO               PIC X(25).                             
                                                                                
      *----------------------------------------------------------------*        
      * TDQ DECLARATION                                                *        
      *----------------------------------------------------------------*        
       01  TD-TRANSIENT-DATA-AREA.                                              
           05 TD-QUEUE-NAME-LENGTH       PIC S9(4) VALUE +4  COMP SYNC.         
           05 TD-JCL-LINE-LENGTH         PIC S9(4) VALUE +80 COMP SYNC.         
           05 TD-QUEUE-NAME              PIC X(4)  VALUE 'IRTD'.                
           05 TD-JCL-LINE                PIC X(80) VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      * PROGRAM SWITCHES                                               *        
      *----------------------------------------------------------------*        
       01  PS-PROGRAM-SWITCHES.                                                 
           05 PS-EDIT-ERROR-IND          PIC X(1)  VALUE 'N'.                   
              88  PS-NO-EDIT-ERROR                 VALUE 'N'.                   
              88  PS-EDIT-ERROR                    VALUE 'Y'.                   
           05 PS-SET-CDE-IND             PIC X(1)  VALUE 'N'.                   
              88  PS-SET-CDE-NO                    VALUE 'N'.                   
              88  PS-SET-CDE-YES                   VALUE 'Y'.                   
           05 PS-SET-CDE-DUP-IND         PIC X(1)  VALUE 'Y'.                   
              88  PS-SET-CDE-DUP-NO                VALUE 'N'.                   
              88  PS-SET-CDE-DUP-YES               VALUE 'Y'.                   
                                                                                
      *----------------------------------------------------------------*        
      * PROGRAM CONSTANTS                                              *        
      *----------------------------------------------------------------*        
       01  PC-PROGRAM-CONSTANTS.                                                
           05 PC-TEST                    PIC X(01) VALUE 'T'.                   
           05 PC-QA                      PIC X(01) VALUE 'Q'.                   
           05 PC-LEARN                   PIC X(01) VALUE 'L'.                   
           05 PC-CURRENT-PROGRAM-NAME    PIC X(08) VALUE 'PCKCS769'.            
           05 PC-CURRENT-MAPSET-NAME     PIC X(08) VALUE 'PCKM769'.             
           05 PC-CURRENT-MAP-NAME        PIC X(08) VALUE 'PC769A'.              
           05 PC-DPKCS030                PIC X(08) VALUE 'DPKCS030'.            
           05 PC-STATUS-REPORT-DESC      PIC X(13) VALUE                        
               'PO SET REPORT'.                                                 
           05 PC-STATUS-REPORT-PROC      PIC X(07) VALUE 'PCK9411'.             
           05 PC-STATUS-REPORT-JOB       PIC X(07) VALUE 'PCK941 '.             
                                                                                
      *----------------------------------------------------------------*        
      *  SYSTEM MESSAGES                                               *        
      *----------------------------------------------------------------*        
       01  PC-TSYMSG-AREA.                                                      
      *  REQUIRED ENTRY                                                         
           05 PC-TSYMSG-00007            PIC 9(5)  VALUE 00007.                 
      *  MUST BE NUMERIC                                                        
           05 PC-TSYMSG-00008            PIC 9(5)  VALUE 00008.                 
      *  REPORT REQUEST SUCCESSFULLY SUBMITTED                                  
           05 PC-TSYMSG-00458            PIC 9(5)  VALUE 00458.                 
      *  DUPLICATE SET CODE ENTERED - PLEASE RETRY                              
           05 PC-TSYMSG-03735            PIC 9(5)  VALUE 03735.                 
      *  SET CODE ENTERED DOES NOT EXIST - PLEASE RETRY                         
           05 PC-TSYMSG-03736            PIC 9(5)  VALUE 03736.                 
                                                                                
      *----------------------------------------------------------------*        
      * PROGRAM VARIABLES                                              *        
      *----------------------------------------------------------------*        
       01  PV-PROGRAM-VARIABLES.                                                
           05 PV-CICS-RESPONSE           PIC S9(9) VALUE ZERO COMP SYNC.        
           05 PV-USER-ID                 PIC X(8)  VALUE SPACES.                
           05 PV-SET-CDE-NAME            PIC X(25) VALUE SPACES.                
           05 PV-SET-CDE                 PIC X(5)  VALUE SPACES.                
                                                                                
           05 PV-SYSTEM-ID.                                                     
               10 FILLER                 PIC X(04) VALUE SPACE.                 
               10 PV-REGION-IND          PIC X(01).                             
                  88  PV-TEST-CICS                 VALUE 'T'.                   
                  88  PV-QA-CICS                   VALUE 'Q'.                   
                  88  PV-PROD-CICS                 VALUE 'P'.                   
                  88  PV-LEARN-CICS                VALUE 'L'.                   
               10 FILLER                 PIC X(03) VALUE SPACES.                
                                                                                
           05 PV-JOB-DESCRIPTION         PIC X(13) VALUE SPACES.                
           05 PV-JOB-PROC                PIC X(07) VALUE SPACES.                
           05 PV-JOB-NAME                PIC X(07) VALUE SPACES.                
           05 PV-SUB1                    PIC S9(04) COMP SYNC.                  
           05 PV-SUB2                    PIC S9(04) COMP SYNC.                  
      *----------------------------------------------------------------*        
      *  JCL LINES FOR A STATUS REPORTS REQUEST                        *        
      *----------------------------------------------------------------*        
        01  JL-JCL-LINE-AREA.                                                   
            05 JL-JCL-LINE-1             PIC X(80) VALUE SPACE.                 
            05 JL-JOB-LINE-PROD.                                                
               10 FILLER                 PIC X(02) VALUE '//'.                  
               10 JL-PROD-JOB-NAME       PIC X(07) VALUE SPACE.                 
               10 FILLER                 PIC X(12) VALUE                        
                  '  JOB KOHLS,'.                                               
               10 FILLER                 PIC X(1)  VALUE QUOTE.                 
               10 JL-PROD-JOB-DESC       PIC X(13) VALUE SPACES.                
               10 FILLER                 PIC X(1)  VALUE QUOTE.                 
               10 FILLER                 PIC X(29) VALUE                        
                  ',MSGCLASS=P,CLASS=P'.                                        
               10 FILLER                 PIC X(15) VALUE SPACES.                
            05 JL-JOB-LINE-TEST.                                                
               10 FILLER                 PIC X(02) VALUE '//'.                  
               10 JL-TEST-JOB-NAME       PIC X(07) VALUE SPACE.                 
               10 FILLER                 PIC X(01) VALUE 'Q'.                   
               10 FILLER                 PIC X(11) VALUE                        
                  ' JOB KOHLS,'.                                                
               10 FILLER                 PIC X(1)  VALUE QUOTE.                 
               10 JL-TEST-JOB-DESC       PIC X(13) VALUE SPACES.                
               10 FILLER                 PIC X(1)  VALUE QUOTE.                 
               10 FILLER                 PIC X(29) VALUE                        
                  ',MSGCLASS=X,CLASS=E'.                                        
            05 JL-JCL-LINE-2-PROD.                                              
               10 FILLER                 PIC X(40) VALUE                        
                   '//JOBLIB    DD DSN=PKMO.LOADLIB,DISP=SHR'.                  
               10 FILLER                 PIC X(40) VALUE SPACES.                
            05 JL-JCL-LINE-2-TEST.                                              
               10 FILLER                 PIC X(40) VALUE                        
                   '//JOBLIB    DD DSN=TKMA.LOADLIB,DISP=SHR'.                  
               10 FILLER                 PIC X(40) VALUE SPACES.                
            05 JL-JCL-LINE-3.                                                   
               10 FILLER                 PIC X(16) VALUE                        
                  '//PS020    EXEC '.                                           
               10 JL-LINE-3-PROC         PIC X(07) VALUE SPACES.                
               10 JL-LINE-3-PROC-T       PIC X(01) VALUE SPACE.                 
               10 FILLER                 PIC X(56) VALUE SPACES.                
            05 JL-JCL-LINE-4.                                                   
               10 FILLER                 PIC X(18) VALUE                        
                  '//S010.SYSUT1 DD *'.                                         
               10 FILLER                 PIC X(62) VALUE SPACES.                
            05 JL-JCL-LINE-5.                                                   
               10 JL-LINE-5-USER-ID      PIC X(07) VALUE SPACES.                
               10 JL-LINE-5-SET-YEAR     PIC 9(04) VALUE ZEROES.                
               10 JL-LINE-5-SET-CDE      PIC X(50) VALUE SPACES.                
               10 FILLER                 PIC X(19) VALUE SPACES.                
            05 JL-JCL-LINE-EOF.                                                 
               10 FILLER                 PIC X(5)  VALUE '/*EOF'.               
               10 FILLER                 PIC X(75) VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *    STANDARD COMAREA DEFINITION                                 *        
      *----------------------------------------------------------------*        
           COPY DPWS030.                                                        
1122RM     COPY DPWS930.                                                        
           COPY DPWS020.                                                        
          05 FILLER REDEFINES DP020-VARIABLE-COMMAREA.                          
      *----------------------------------------------------------------*        
      *    APPLICATION SPECIFIC COMMAREA.                              *        
      *----------------------------------------------------------------*        
             10 FILLER                   PIC X(200).                            
             10 ASC-PGM-SPECIFIC-COMAREA.                                       
                15 ASC-SET-YEAR          PIC X(04).                             
                15 ASC-SET-YEAR-N    REDEFINES ASC-SET-YEAR                     
                                         PIC 9(04).                             
                15 ASC-SET-CODE-ALL      PIC X(50).                             
                15 ASC-SET-CODE-TABLE REDEFINES ASC-SET-CODE-ALL.               
                   20 ASC-SET-CODE       PIC X(05) OCCURS 10 TIMES.             
                15 FILLER                PIC X(146).                            
             10 FILLER                   PIC X(200).                            
                                                                                
      *----------------------------------------------------------------*        
      * SQL AND COBOL DECLARATIONS FOR THE THE SEASONAL SET CODE TABLE *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE TSEASET                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA               *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
       LINKAGE SECTION.                                                         
      *----------------------------------------------------------------*        
       01  DFHCOMMAREA.                                                         
           05 FILLER                       OCCURS 1 TO 4072 TIMES               
                                            DEPENDING ON EIBCALEN.              
              10 FILLER                  PIC X.                                 
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
       0000-MAIN-MODULE.                                                        
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
           MOVE LENGTH OF PC769AI        TO DP030-MAP-LENGTH (1).               
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
      *                                                                         
           PERFORM 1000-CONTROL-PROCESSING                                      
                                                                                
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
           EJECT                                                                
                                                                                
       0001-CALL-CICS-ARCH-API.                                                 
1122RM*    CALL PC-DPKCS030 USING DFHEIBLK                                      
1122RM     CALL DP930-CICS-ARCH-API                                             
1122RM                      USING DFHEIBLK                                      
                                  DFHCOMMAREA                                   
                                  DP030-CICS-API-FIELDS                         
                                  DP020-STANDARD-COMMAREA                       
                                  PC769AI.                                      
      *                                                                         
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK                                            
                   DP013-XCTL-DISPLAY-RESTART                                   
                   DP013-CICS-ABEND      TO TRUE                                
               MOVE 'BEFORE 0000-MAIN-MODULE'                                   
                                         TO DP013-PARAGRAPH                     
1122RM*        MOVE 'CALL TO PCKCS769 NOT SUCCESSFUL, RETURN-CODE ON NEX        
1122RM*             'T LINE'             TO DP013-MESSAGE-TEXT (2)              
1122rm         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1122RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE                                           
                                         TO DP013-MESSAGE-TEXT (3)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
      *----------------------------------------------------------------*        
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE ASC-PGM-SPECIFIC-COMAREA                          
                   PERFORM 3200-RESET-ATTRIBUTES                                
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   PERFORM 2000-FKEY-PROCESSING                                 
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   PERFORM 4100-MOVE-COMAREA-TO-SCREEN                          
                   PERFORM 3000-EDIT-DATA-IN-COMAREA                            
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND                                        
                       DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                         TO  DP013-PARAGRAPH                    
                   MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
                                         TO  DP013-MESSAGE-TEXT(1)              
                   MOVE DP020-NEXT-APPL-ACTIVITY                                
                                         TO DP013-MESSAGE-TEXT(2)               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
      * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
      * EDITS.                                                         *        
      *----------------------------------------------------------------*        
       2000-FKEY-PROCESSING.                                                    
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   INITIALIZE ASC-PGM-SPECIFIC-COMAREA                          
                   PERFORM 3000-EDIT-DATA-IN-COMAREA                            
                                                                                
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
                   INITIALIZE ASC-PGM-SPECIFIC-COMAREA                          
                   PERFORM 3200-RESET-ATTRIBUTES                                
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                                                                                
               WHEN OTHER                                                       
                   PERFORM 2200-MOVE-SCREEN-TO-COMAREA                          
                   PERFORM 3000-EDIT-DATA-IN-COMAREA                            
                   IF PS-NO-EDIT-ERROR                                          
                      SET DP030-CONTINUE-GO                                     
                                         TO TRUE                                
                      PERFORM 2100-CHECK-FUNCTION-KEY                           
                   ELSE                                                         
                      SET DP030-OVERRIDE-GO                                     
                                         TO TRUE                                
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * PERFORM THIS PARAGRAPH AFTER ALL THE EDITS HAVE BEEN MADE AND  *        
      * THEN CHECK THE INT-APPL-FORMAT-IND TO SEE WHAT PF KEY HAS BEEN *        
      * SELECTED.                                                      *        
      *                                                                *        
      * CURRENTLY PROGRAM IS ONLY SET UP FOR ENTER & F18 KEYS.         *        
      *    ENTER - ONLY EDITS SCREEN                                   *        
      *    F24 --- REQUEST REPORTS TO RDS                              *        
      *----------------------------------------------------------------*        
       2100-CHECK-FUNCTION-KEY.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                    CONTINUE                                                    
               WHEN DP020-SRC-AID = DP016-PF24                                  
                    PERFORM 4200-SUBMIT-JOB                                     
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
      * THE APPLICATION-SPECIFIC COMAREA.   ALL EDITS ARE DONE IN THE  *        
      * APPLICATION-SPECIFIC COMAREA, NOT ON THE SCREEN.               *        
      *----------------------------------------------------------------*        
       2200-MOVE-SCREEN-TO-COMAREA.                                             
                                                                                
           IF  MYEARL > ZERO                                                    
               MOVE MYEARI               TO ASC-SET-YEAR                        
           ELSE                                                                 
               IF  MYEARF  = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-SET-YEAR                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE 1                        TO PV-SUB1.                            
           PERFORM                                                              
                UNTIL  PV-SUB1 > 10                                             
                IF  MR-MCDEL(PV-SUB1) > ZERO                                    
                    MOVE MR-MCDEI(PV-SUB1)                                      
                                         TO ASC-SET-CODE(PV-SUB1)               
                ELSE                                                            
                    IF  MR-MCDEF(PV-SUB1) = DP015-ERASE-EOF                     
                        MOVE SPACE       TO ASC-SET-CODE(PV-SUB1)               
                    END-IF                                                      
                END-IF                                                          
                ADD 1                    TO PV-SUB1                             
           END-PERFORM.                                                         
                                                                                
      *----------------------------------------------------------------*        
      * ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *        
      * CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *        
      * A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*        
      * IN THE DESTINATION PROGRAM, NOT HERE.                          *        
      *----------------------------------------------------------------*        
       3000-EDIT-DATA-IN-COMAREA.                                               
                                                                                
           PERFORM 3200-RESET-ATTRIBUTES.                                       
                                                                                
      *                                                                         
      *** VALIDATE YEAR                                                         
      *                                                                         
           IF ASC-SET-YEAR-N   NOT NUMERIC                                      
              SET PS-EDIT-ERROR                                                 
                  DP020-MSG-FATAL        TO TRUE                                
              MOVE PC-TSYMSG-00008       TO DP020-MSG-NUMBER                    
              MOVE DP015-UNP-ALP-BRT-OFF TO MYEARA                              
              MOVE DP015-RED             TO MYEARC                              
              MOVE DP015-REVERSE         TO MYEARH                              
              MOVE -1                    TO MYEARL                              
              SET DP030-SET-CURSOR-APPL-1                                       
                                         TO TRUE                                
           END-IF.                                                              
                                                                                
      *                                                                         
      *** CHECK FOR EXISTANCE OF SET CODE                                       
      *                                                                         
           IF PS-NO-EDIT-ERROR                                                  
              MOVE 1                     TO PV-SUB1                             
              MOVE 2                     TO PV-SUB2                             
              SET PS-SET-CDE-NO          TO TRUE                                
              PERFORM 3100-VALIDATE-SET-CDE UNTIL PV-SUB1 > 10                  
                                             OR PV-SUB2 > 10                    
                                             OR PS-EDIT-ERROR                   
           END-IF.                                                              
                                                                                
           IF PS-NO-EDIT-ERROR AND PS-SET-CDE-NO                                
              SET DP020-MSG-FATAL                                               
                  PS-EDIT-ERROR          TO TRUE                                
              MOVE PC-TSYMSG-00007       TO DP020-MSG-NUMBER                    
              MOVE DP015-UNP-ALP-BRT-OFF TO MR-MCDEA(1)                         
              MOVE DP015-RED             TO MR-MCDEC(1)                         
              MOVE DP015-REVERSE         TO MR-MCDEH(1)                         
              MOVE -1                    TO MR-MCDEL(1)                         
              SET DP030-SET-CURSOR-APPL-1                                       
                                         TO TRUE                                
           END-IF.                                                              
                                                                                
      *                                                                         
      *** CHECK FOR DUPLICATE SET CODE                                          
      *                                                                         
           IF PS-NO-EDIT-ERROR                                                  
              PERFORM 3200-CHECK-DUPLICATE-SET-CDE                              
           END-IF.                                                              
      *                                                                         
      *** CHECK FOR THE EXISTANCE OF SET CODE AND GET THE SET NAME              
      *                                                                         
           IF PS-NO-EDIT-ERROR                                                  
              PERFORM 3300-VALIDATE-SET-CODE                                    
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *  VALIDATE SET CODE ENTERED                                     *        
      *----------------------------------------------------------------*        
       3100-VALIDATE-SET-CDE.                                                   
                                                                                
           EVALUATE TRUE                                                        
               WHEN ASC-SET-CODE(PV-SUB1) = SPACES AND                          
                    ASC-SET-CODE(PV-SUB2) > SPACES                              
                   SET DP020-MSG-FATAL                                          
                       PS-EDIT-ERROR     TO TRUE                                
                   MOVE PC-TSYMSG-00007  TO DP020-MSG-NUMBER                    
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO MR-MCDEA(PV-SUB1)                   
                   MOVE DP015-RED        TO MR-MCDEC(PV-SUB1)                   
                   MOVE DP015-REVERSE    TO MR-MCDEH(PV-SUB1)                   
                   MOVE -1               TO MR-MCDEL(PV-SUB1)                   
                   SET DP030-SET-CURSOR-APPL-1                                  
                                         TO TRUE                                
                                                                                
               WHEN ASC-SET-CODE(PV-SUB1) = SPACES AND                          
                    ASC-SET-CODE(PV-SUB2) = SPACES                              
                   ADD 1                 TO PV-SUB2                             
                                                                                
               WHEN OTHER                                                       
                   ADD 1                 TO PV-SUB1                             
                                            PV-SUB2                             
                   SET PS-SET-CDE-YES    TO TRUE                                
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *  CHECKS FOR DUPLUCATE SET CODE                                 *        
      *----------------------------------------------------------------*        
       3200-CHECK-DUPLICATE-SET-CDE.                                            
                                                                                
           SET PS-SET-CDE-DUP-YES        TO TRUE.                               
           PERFORM                                                              
            VARYING PV-SUB1 FROM 1 BY 1                                         
             UNTIL PV-SUB1 > 10 OR ASC-SET-CODE(PV-SUB1) = SPACES               
                COMPUTE PV-SUB2 = PV-SUB1 + 1                                   
               PERFORM                                                          
                VARYING PV-SUB2 FROM PV-SUB2 BY 1                               
                 UNTIL PV-SUB2 > 10 OR ASC-SET-CODE(PV-SUB2) = SPACES           
                                                                                
                    IF ASC-SET-CODE(PV-SUB1) = ASC-SET-CODE(PV-SUB2)            
                       SET DP020-MSG-FATAL                                      
                           PS-EDIT-ERROR TO TRUE                                
                       MOVE PC-TSYMSG-03735                                     
                                         TO DP020-MSG-NUMBER                    
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                         TO MR-MCDEA(PV-SUB2)                   
                       MOVE DP015-RED    TO MR-MCDEC(PV-SUB2)                   
                       MOVE DP015-REVERSE                                       
                                         TO MR-MCDEH(PV-SUB2)                   
                       IF PS-SET-CDE-DUP-YES                                    
                          MOVE -1        TO MR-MCDEL(PV-SUB2)                   
                          SET DP030-SET-CURSOR-APPL-1                           
                                         TO TRUE                                
                          SET PS-SET-CDE-DUP-NO                                 
                                         TO TRUE                                
                       END-IF                                                   
                    END-IF                                                      
               END-PERFORM                                                      
           END-PERFORM.                                                         
                                                                                
      *----------------------------------------------------------------*        
      *  CHECKS FOR THE EXISTANCE OF SET CODE AND GETS THE SET NAME    *        
      *----------------------------------------------------------------*        
       3300-VALIDATE-SET-CODE.                                                  
                                                                                
           PERFORM                                                              
            VARYING PV-SUB1 FROM 1 BY 1                                         
             UNTIL PV-SUB1 > 10 OR ASC-SET-CODE(PV-SUB1) = SPACES               
                                                                                
              MOVE ASC-SET-CODE(PV-SUB1) TO PV-SET-CDE                          
                                                                                
              EXEC SQL                                                          
                 SELECT A.SEASET_NM                                             
                 INTO :PV-SET-CDE-NAME                                          
                 FROM TSEASET A                                                 
                WHERE A.SEASET_YR = :ASC-SET-YEAR                               
                  AND A.SEASET_SHRT_NM = :PV-SET-CDE                            
              END-EXEC                                                          
                                                                                
      *                                                                         
              EVALUATE TRUE                                                     
                  WHEN SQLCODE = ZERO                                           
                       MOVE PV-SET-CDE-NAME                                     
                                         TO MR-MNMEO(PV-SUB1)                   
                  WHEN SQLCODE = +100                                           
                       SET DP020-MSG-FATAL                                      
                           PS-EDIT-ERROR TO TRUE                                
                       MOVE PC-TSYMSG-03736                                     
                                         TO DP020-MSG-NUMBER                    
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                         TO MR-MCDEA(PV-SUB1)                   
                       MOVE DP015-RED    TO MR-MCDEC(PV-SUB1)                   
                       MOVE DP015-REVERSE                                       
                                         TO MR-MCDEH(PV-SUB1)                   
                       MOVE -1           TO MR-MCDEL(PV-SUB1)                   
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                  WHEN OTHER                                                    
                       MOVE '3300-VALIDATE-SET-CODE'                            
                                         TO DP013-PARAGRAPH                     
                       MOVE 'CHECKING EXISTANCE OF SET CODE'                    
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TSEASET'    TO DP013-DB2-TABLE-NAME(1)             
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART                           
                                         TO TRUE                                
                       PERFORM DP013-0000-PROCESS-ABEND                         
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      ** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **        
      ** MAP.                                                         **        
      *----------------------------------------------------------------*        
       3200-RESET-ATTRIBUTES.                                                   
                                                                                
           MOVE DP015-UNP-ALP-NOR-OFF    TO MYEARA                              
                                            MCDE01A                             
                                            MCDE02A                             
                                            MCDE03A                             
                                            MCDE04A                             
                                            MCDE05A                             
                                            MCDE06A                             
                                            MCDE07A                             
                                            MCDE08A                             
                                            MCDE09A                             
                                            MCDE10A.                            
           MOVE DP015-UNDERLINE          TO MYEARH                              
                                            MCDE01H                             
                                            MCDE02H                             
                                            MCDE03H                             
                                            MCDE04H                             
                                            MCDE05H                             
                                            MCDE06H                             
                                            MCDE07H                             
                                            MCDE08H                             
                                            MCDE09H                             
                                            MCDE10H.                            
                                                                                
            MOVE DP015-GREEN             TO MYEARC                              
                                            MCDE01C                             
                                            MCDE02C                             
                                            MCDE03C                             
                                            MCDE04C                             
                                            MCDE05C                             
                                            MCDE06C                             
                                            MCDE07C                             
                                            MCDE08C                             
                                            MCDE09C                             
                                            MCDE10C.                            
                                                                                
           MOVE SPACES                   TO MNME01O                             
                                            MNME02O                             
                                            MNME03O                             
                                            MNME04O                             
                                            MNME05O                             
                                            MNME06O                             
                                            MNME07O                             
                                            MNME08O                             
                                            MNME09O                             
                                            MNME10O.                            
                                                                                
      *----------------------------------------------------------------*        
      *  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *        
      *  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *        
      *  IT HERE.                                                      *        
      *----------------------------------------------------------------*        
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           PERFORM DP017-0000-GET-CURR-DATE-TIME.                               
           MOVE DP017-CURRENT-MDY-YYYY   TO MYEARO                              
                                            ASC-SET-YEAR.                       
           SET DP030-SET-CURSOR-APPL-1   TO TRUE.                               
           MOVE -1                       TO MYEARL.                             
                                                                                
      *----------------------------------------------------------------*        
      *  RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.            *        
      *----------------------------------------------------------------*        
       4100-MOVE-COMAREA-TO-SCREEN.                                             
                                                                                
           MOVE ASC-SET-YEAR             TO MYEARO.                             
           MOVE ASC-SET-CODE(1)          TO MCDE01O.                            
           MOVE ASC-SET-CODE(2)          TO MCDE02O.                            
           MOVE ASC-SET-CODE(3)          TO MCDE03O.                            
           MOVE ASC-SET-CODE(4)          TO MCDE04O.                            
           MOVE ASC-SET-CODE(5)          TO MCDE05O.                            
           MOVE ASC-SET-CODE(6)          TO MCDE06O.                            
           MOVE ASC-SET-CODE(7)          TO MCDE07O.                            
           MOVE ASC-SET-CODE(8)          TO MCDE08O.                            
           MOVE ASC-SET-CODE(9)          TO MCDE09O.                            
           MOVE ASC-SET-CODE(10)         TO MCDE10O.                            
                                                                                
      *----------------------------------------------------------------*        
      * BUILDS THE PCK941 JOB BASE ON THE CICS REGION AND SUBMITS IT.  *        
      *----------------------------------------------------------------*        
       4200-SUBMIT-JOB.                                                         
                                                                                
           MOVE  PC-STATUS-REPORT-DESC   TO PV-JOB-DESCRIPTION.                 
           MOVE  PC-STATUS-REPORT-PROC   TO PV-JOB-PROC.                        
           MOVE  PC-STATUS-REPORT-JOB    TO PV-JOB-NAME.                        
                                                                                
            EXEC CICS                                                           
                ASSIGN                                                          
                    APPLID(PV-SYSTEM-ID)                                        
                    USERID (PV-USER-ID)                                         
            END-EXEC.                                                           
                                                                                
            PERFORM 4210-ENQUEUE-INTERNAL-READER.                               
                                                                                
            MOVE '*4200-SUBMIT-JOB            *'                                
                                         TO DP013-PARAGRAPH.                    
                                                                                
      *----------------------------------------------------------------*        
      * PROD/LEARN JCL SETUP                                                    
      *  //PCK941   JOB KOHLS,'PO SET REPORT',MSGCLASS=P,CLASS=P                
      *  //JOBLIB   DD DSN=PKMO.LOADLIB,DISP=SHR                                
      *  //PS020    EXEC PCK9411                                                
      *  //S010.SYSUT1 DD *                                                     
      *  TKXXXXXYYYYSTCD1STCD2STCD3STCD4STCD5STCD6STCD7STCD8STCD9STC10          
      *  /*EOF                                                                  
      *----------------------------------------------------------------*        
      * TEST JCL SETUP                                                          
      *  //XXXXXXXQ JOB KOHLS,'PO SET REPORT',MSGCLASS=X,CLASS=E                
      *  //JOBLIB   DD DSN=TKMA.LOADLIB,DISP=SHR                                
      *  //PS020    EXEC PCK9411T                                               
      *  //S010.SYSUT1 DD *                                                     
      *  TKXXXXXYYYYSTCD1STCD2STCD3STCD4STCD5STCD6STCD7STCD8STCD9STC10          
      *  /*EOF                                                                  
      *---------------------------------------------------------------*         
                                                                                
            EVALUATE TRUE                                                       
                WHEN PV-PROD-CICS                                               
                     MOVE PV-JOB-NAME    TO  JL-PROD-JOB-NAME                   
                     MOVE PV-JOB-DESCRIPTION                                    
                                         TO  JL-PROD-JOB-DESC                   
                     MOVE JL-JOB-LINE-PROD                                      
                                         TO  JL-JCL-LINE-1                      
                     MOVE JL-JCL-LINE-1  TO  TD-JCL-LINE                        
                     PERFORM 4220-WRITE-TD-LINE                                 
                     MOVE JL-JCL-LINE-2-PROD                                    
                                         TO  TD-JCL-LINE                        
                     PERFORM 4220-WRITE-TD-LINE                                 
                     MOVE PV-JOB-PROC    TO  JL-LINE-3-PROC                     
                     MOVE SPACE          TO  JL-LINE-3-PROC-T                   
                     MOVE JL-JCL-LINE-3  TO  TD-JCL-LINE                        
                     PERFORM 4220-WRITE-TD-LINE                                 
                                                                                
                WHEN PV-TEST-CICS                                               
                     MOVE DP020-USERID   TO  JL-TEST-JOB-NAME                   
                     MOVE PV-JOB-DESCRIPTION                                    
                                         TO  JL-TEST-JOB-DESC                   
                     MOVE JL-JOB-LINE-TEST                                      
                                         TO  JL-JCL-LINE-1                      
                     MOVE JL-JCL-LINE-1  TO  TD-JCL-LINE                        
                     PERFORM 4220-WRITE-TD-LINE                                 
                     MOVE JL-JCL-LINE-2-TEST                                    
                                         TO  TD-JCL-LINE                        
                     PERFORM 4220-WRITE-TD-LINE                                 
                     MOVE PV-JOB-PROC    TO  JL-LINE-3-PROC                     
                     MOVE PC-TEST        TO  JL-LINE-3-PROC-T                   
                     MOVE JL-JCL-LINE-3  TO  TD-JCL-LINE                        
                     PERFORM 4220-WRITE-TD-LINE                                 
                                                                                
                WHEN PV-QA-CICS                                                 
                     MOVE DP020-USERID   TO  JL-TEST-JOB-NAME                   
                     MOVE PV-JOB-DESCRIPTION                                    
                                         TO  JL-TEST-JOB-DESC                   
                     MOVE JL-JOB-LINE-TEST                                      
                                         TO  JL-JCL-LINE-1                      
                     MOVE JL-JCL-LINE-1  TO  TD-JCL-LINE                        
                     PERFORM 4220-WRITE-TD-LINE                                 
                     MOVE JL-JCL-LINE-2-TEST                                    
                                         TO  TD-JCL-LINE                        
                     PERFORM 4220-WRITE-TD-LINE                                 
                     MOVE PV-JOB-PROC    TO  JL-LINE-3-PROC                     
                     MOVE PC-QA          TO  JL-LINE-3-PROC-T                   
                     MOVE JL-JCL-LINE-3  TO  TD-JCL-LINE                        
                     PERFORM 4220-WRITE-TD-LINE                                 
                                                                                
                WHEN OTHER                                                      
                     MOVE DP020-USERID   TO  JL-TEST-JOB-NAME                   
                     MOVE PV-JOB-DESCRIPTION                                    
                                         TO  JL-TEST-JOB-DESC                   
                     MOVE JL-JOB-LINE-TEST                                      
                                         TO  JL-JCL-LINE-1                      
                     MOVE JL-JCL-LINE-1  TO  TD-JCL-LINE                        
                     PERFORM 4220-WRITE-TD-LINE                                 
                     MOVE JL-JCL-LINE-2-TEST                                    
                                         TO  TD-JCL-LINE                        
                     PERFORM 4220-WRITE-TD-LINE                                 
                     MOVE PV-JOB-PROC    TO  JL-LINE-3-PROC                     
                     MOVE PC-LEARN       TO  JL-LINE-3-PROC-T                   
                     MOVE JL-JCL-LINE-3  TO  TD-JCL-LINE                        
                     PERFORM 4220-WRITE-TD-LINE                                 
            END-EVALUATE.                                                       
                                                                                
           MOVE JL-JCL-LINE-4            TO  TD-JCL-LINE.                       
           PERFORM 4220-WRITE-TD-LINE.                                          
                                                                                
           MOVE DP020-USERID             TO  JL-LINE-5-USER-ID.                 
           MOVE ASC-SET-YEAR             TO  JL-LINE-5-SET-YEAR.                
           MOVE ASC-SET-CODE-ALL         TO  JL-LINE-5-SET-CDE.                 
           MOVE JL-JCL-LINE-5            TO  TD-JCL-LINE.                       
           PERFORM 4220-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-LINE-EOF          TO  TD-JCL-LINE.                       
           PERFORM 4220-WRITE-TD-LINE.                                          
                                                                                
           PERFORM 9800-DEQUEUE-INTERNAL-READER.                                
                                                                                
           MOVE PC-TSYMSG-00458          TO DP020-MSG-NUMBER.                   
                                                                                
           SET DP020-MSG-INFORMATIONAL   TO TRUE.                               
                                                                                
      *----------------------------------------------------------------*        
      * ENQUEUE THE TRANSIENT DATA QUEUE FOR THE INTERNAL READER.      *        
      *----------------------------------------------------------------*        
        4210-ENQUEUE-INTERNAL-READER.                                           
            MOVE '*4210-ENQUEUE-INTERNAL-READER.   *'                           
                                       TO DP013-PARAGRAPH                       
            EXEC CICS                                                           
                ENQ                                                             
                    RESOURCE(TD-QUEUE-NAME)                                     
                    LENGTH(TD-QUEUE-NAME-LENGTH)                                
                    RESP(PV-CICS-RESPONSE)                                      
            END-EXEC.                                                           
                                                                                
            IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                           
                STRING 'ENQUEUE '            DELIMITED BY SIZE                  
                       TD-QUEUE-NAME         DELIMITED BY SIZE                  
                       ' (INTERNAL READER)'  DELIMITED BY SIZE                  
                    INTO DP013-MESSAGE-TEXT(1)                                  
                PERFORM 9920-CICS-ERROR                                         
            END-IF.                                                             
                                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *  WRITE A JCL LINE TO THE TRANSIENT DATA QUEUE FOR THE INTERNAL *        
      *  READER.  ON ANY ERROR, DEQUEUE THE INTERNAL READER, DELETE    *        
      *  THE TEMPORARY STORAGE QUEUE (TO PREVENT OLD REQUEST           *        
      *  INFORMATION FROM ACCIDENTALLY APPEARING ON A FUTURE REQUEST), *        
      *  FORMAT AND DISPLAY AN ERROR MESSAGE, AND RETURN TO NATIVE     *        
      *  CICS.                                                         *        
      *----------------------------------------------------------------*        
        4220-WRITE-TD-LINE.                                                     
            MOVE '*4220-WRITE-TD-LINE.             *'                           
                                         TO DP013-PARAGRAPH                     
            EXEC CICS                                                           
                WRITEQ TD                                                       
                    QUEUE(TD-QUEUE-NAME)                                        
                    FROM(TD-JCL-LINE)                                           
                    LENGTH(TD-JCL-LINE-LENGTH)                                  
                    RESP(PV-CICS-RESPONSE)                                      
            END-EXEC.                                                           
                                                                                
            IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                           
                STRING 'WRITE TO TRANSIENT DATA QUEUE '                         
                                                       DELIMITED BY SIZE        
                       TD-QUEUE-NAME                   DELIMITED BY SIZE        
                       ' (INTERNAL READER)'            DELIMITED BY SIZE        
                    INTO DP013-MESSAGE-TEXT(1)                                  
                PERFORM 9800-DEQUEUE-INTERNAL-READER                            
                PERFORM 9920-CICS-ERROR                                         
            END-IF.                                                             
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *   ROLL BACK ALL RESOURCES, FOLLOWING A DB2 ERROR.  FORMAT      *        
      *   INFORMATION RELATIVE TO THE ERROR.                           *        
      *----------------------------------------------------------------*        
        9920-CICS-ERROR.                                                        
                                                                                
            SET DP013-ROLLBACK                                                  
                DP013-XCTL-DISPLAY-RESTART                                      
                DP013-CICS-ABEND         TO TRUE                                
                                                                                
            PERFORM DP013-0000-PROCESS-ABEND.                                   
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *   DEQUEUE THE TRANSIENT DATA QUEUE FOR THE INTERNAL READER.    *        
      *   THIS SHOULD BE DONE FOLLOWING A SUCCESSFUL REPORT REQUEST    *        
      *   SUBMISSION, A CANCELLATION OF THE CURRENT REPORT REQUEST, OR *        
      *   ANY DETECTED ABENDS.  THIS WILL FREE UP THE INTERNAL READER  *        
      *----------------------------------------------------------------*        
        9800-DEQUEUE-INTERNAL-READER.                                           
                                                                                
                                                                                
            EXEC CICS                                                           
                DEQ                                                             
                    RESOURCE(TD-QUEUE-NAME)                                     
                    LENGTH(TD-QUEUE-NAME-LENGTH)                                
                    RESP(PV-CICS-RESPONSE)                                      
            END-EXEC.                                                           
                                                                                
            IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                           
                STRING 'DEQUEUE '                DELIMITED BY SIZE              
                       TD-QUEUE-NAME             DELIMITED BY SIZE              
                       ' (INTERNAL READER)'      DELIMITED BY SIZE              
                    INTO DP013-MESSAGE-TEXT(1)                                  
                PERFORM 9920-CICS-ERROR                                         
            END-IF.                                                             
                                                                                
            EJECT                                                               
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
                                                                                
           COPY DPPD017.                                                        
