000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400 PROGRAM-ID.   CEKUP901.                                                  
000500 AUTHOR.       RACHEL GRAMANN.                                            
000600 INSTALLATION. KOHLS DEPARTMENT STORES.                                   
000700 DATE-WRITTEN. MARCH 2011.                                                
000800 DATE-COMPILED.                                                           
000900******************************************************************        
001000*              CENTRAL STOCK DATA FIX                                     
001100*                                                                         
001200* THIS PROGRAM WILL TAKE A FILE THAT WAS CREATED BY A USER & BRING        
001300* THAT FILE INTO THIS PROGRAM TO UPDAT THE TCEPLAU TABLE AND              
001400* INSERT A RECORD ONTO TCEPLDS.  THIS PROGRAM IS BEING CREATED AS         
001500* THE RESULT OF BOLS BEING SENT TWICE FROM WMOS AND ALREADY PICKED        
001510* UP BY FASHION SALES.  WE NEED A WAY TO BE ABLE TO ALTER THE             
001520* TABLES FOR THE NEXT TIME THAT THIS HAPPENS.                             
001560*                                                                         
001600******************************************************************        
001900 ENVIRONMENT DIVISION.                                                    
002000******************************************************************        
002100 CONFIGURATION SECTION.                                                   
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600 FILE-CONTROL.                                                            
002700     SELECT CE-PULL-DATA-FILE                                             
002800         ASSIGN TO INP01.                                                 
003540                                                                          
003550******************************************************************        
003560 DATA DIVISION.                                                           
003570******************************************************************        
003580 FILE SECTION.                                                            
003590                                                                          
003600 FD  CE-PULL-DATA-FILE                                                    
003700     RECORDING MODE IS F                                                  
003800     LABEL RECORDS ARE STANDARD                                           
003900     RECORD CONTAINS 90 CHARACTERS                                        
004000     BLOCK CONTAINS 0 RECORDS                                             
004001     DATA RECORD IS CE-PULL-DATA-FILE-REC.                                
004100 01  CE-PULL-DATA-FILE-REC.                                               
004110     05  CE-USER-ID                    PIC X(07).                         
           05  FILLER                        PIC X(01).                         
004120     05  CE-PULL-NBR                   PIC 9(05).                         
           05  FILLER                        PIC X(01).                         
004140     05  CE-LINE-NBR                   PIC 9(04).                         
           05  FILLER                        PIC X(01).                         
004160     05  CE-STOR-NBR                   PIC 9(04).                         
           05  FILLER                        PIC X(01).                         
004190     05  CE-ADJ-TYPE                   PIC X(01).                         
           05  FILLER                        PIC X(01).                         
004191     05  CE-ADJ-QTY                    PIC 9(05).                         
           05  FILLER                        PIC X(01).                         
004192     05  CE-DESCRIPTION                PIC X(58).                         
006800                                                                          
006900******************************************************************        
007000 WORKING-STORAGE SECTION.                                                 
007100******************************************************************        
007200 01  PC-PROGRAM-CONSTANTS.                                                
007300     05  PC-CURRENT-PROGRAM-NAME       PIC X(08) VALUE 'CEKUP901'.        
007600                                                                          
007700 01  PS-PROGRAM-SWITCHES.                                                 
007800     05  PS-END-OF-FILE-SW             PIC X(01) VALUE 'N'.               
007900         88  PS-END-OF-FILE-YES                  VALUE 'Y'.               
008000         88  PS-END-OF-FILE-NO                   VALUE 'N'.               
008400     05  PS-ERROR-MESSAGE-SW           PIC X(01) VALUE 'N'.               
008500         88  PS-ERROR-MESSAGE-YES                VALUE 'Y'.               
008600         88  PS-ERROR-MESSAGE-NO                 VALUE 'N'.               
012540                                                                          
012550 01  DK-DATABASE-KEYS.                                                    
012570     05  DK-CE-PULL-NBR                PIC S9(9) COMP.                    
012580     05  DK-CE-LINE-NBR                PIC S9(4) COMP.                    
012581     05  DK-CE-STOR-NBR                PIC S9(4) COMP.                    
013000                                                                          
013100 01  PV-ABEND-FIELDS.                                                     
013110     05  PV-SQLCODE                    PIC  9999-.                        
013200     05  PV-ABEND-CODE                 PIC S9(4) VALUE +0                 
013300                                                        COMP SYNC.        
013400     05  PV-ABEND-PROGRAM              PIC X(08) VALUE 'CEKUP901'.        
013500     05  PV-ABEND-PARAGRAPH            PIC X(25) VALUE SPACES.            
013600     05  PV-ABEND-OPERATION            PIC X(20) VALUE SPACES.            
013700     05  PV-ABEND-STRUCTURE-1          PIC X(08) VALUE SPACES.            
013800     05  PV-ABEND-STRUCTURE-2          PIC X(08) VALUE SPACES.            
013900     05  PV-ABEND-STATUS               PIC XX    VALUE SPACES.            
014000     05  PV-DB2-OPERATION-MESSAGE-1    PIC X(79) VALUE SPACES.            
014100     05  PV-DB2-OPERATION-MESSAGE-2    PIC X(79) VALUE SPACES.            
014200     05  PV-DB2-TABLE-NAME             PIC X(08) VALUE SPACES.            
015212                                                                          
015213 01  PV-NULL-INDICATORS.                                                  
015214     05  PV-NULL-IND-1                 PIC S9(4) VALUE +0 COMP.           
015215     05  PV-NULL-IND-2                 PIC S9(4) VALUE +0 COMP.           
015216     05  PV-NULL-IND-3                 PIC S9(4) VALUE +0 COMP.           
015217                                                                          
015218 01  PV-PROGRAM-VARIABLES.                                                
015219     05  FILLER                        PIC X(26) VALUE                    
015220                                   '***PV-PROGRAM-VARIABLES***'.          
015290     05  PV-DISPLAY-REC-3              PIC ZZZ,ZZZ,999-.                  
015300     05  PV-DISPLAY-REC-4              PIC Z,ZZZ,999-.                    
015400     05  PV-DISPLAY-AMT                PIC 999.99.                        
018000     05  PV-ITM-NBR                    PIC S9(15)V COMP-3.                
018100     05  PV-ADJ-QTY                    PIC S9(09)  COMP.                  
030300                                                                          
030400 01  PA-PROGRAM-ACCUMULATORS.                                             
030500     05  PA-RECS-IN                    PIC S9(9) VALUE ZEROES.            
030510     05  PA-TCEPLAU-INSERTED           PIC S9(9) VALUE ZEROES.            
030520     05  PA-TCEPLDS-UPDATED            PIC S9(9) VALUE ZEROES.            
030600     05  PA-PROCESS-QTY                PIC S9(9) VALUE ZEROES.            
034500                                                                          
      *---------------------------------------------------------------*         
      *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
      *---------------------------------------------------------------*         
       01  ASC-SPECIFIC-COMMAREA.                                               
           05  ASC-DS-QTY-CURR         PIC 9(05).                               
           05  ASC-ADJ-QTY-TO-N        PIC 9(05).                               
047200                                                                          
047300***********************************************************               
047400*  USED FOR DB2 ERROR MESSAGE BUILDING                    *               
047500***********************************************************               
047600     COPY DPWS004.                                                        
047700                                                                          
047800***********************************************************               
047900*  DB2 SQL COMMUNICATION AREA                             *               
048000***********************************************************               
048100     EXEC SQL                                                             
048200         INCLUDE SQLCA                                                    
048300     END-EXEC.                                                            
048400                                                                          
050600***********************************************************               
050700*  CENTRAL STOCK - STORE (DISTRIBUTION) TABLE             *               
050800***********************************************************               
050900     EXEC SQL                                                             
051000         INCLUDE TCEPLDS                                                  
051100     END-EXEC.                                                            
051200                                                                          
051300***********************************************************               
051400*  CENTRAL STOCK - MANIFEST TABLE                         *               
051500***********************************************************               
051600     EXEC SQL                                                             
051700         INCLUDE TCEPLAU                                                  
051800     END-EXEC.                                                            
051900                                                                          
023500*---------------------------------------------------------------*         
023600*    ABEND PROCESSING COPYBOOK.                                 *         
023700*---------------------------------------------------------------*         
023800     COPY DPWS013.                                                        
023900                                                                          
053700***********************************************************               
053800***********************************************************               
053900 PROCEDURE DIVISION.                                                      
054000     DISPLAY '*** START OF CEKUP901 ***'.                                 
054100     DISPLAY ' '.                                                         
054200                                                                          
054300*INITIALIZATION.                                                          
054400     PERFORM 0100-INITIALIZATION.                                         
054500                                                                          
054600*PROCESS INPUT FILE.                                                      
054700     IF PS-END-OF-FILE-YES                                                
054800         DISPLAY '**** NO INPUT FILE FOUND FOR CEKUP901 ****'             
054900     ELSE                                                                 
055000         PERFORM 1000-PROCESS-INPUT                                       
055100           UNTIL PS-END-OF-FILE-YES                                       
055200     END-IF.                                                              
055300                                                                          
055400*DISPLAY RECORD COUNTS.                                                   
055500     DISPLAY ' '.                                                         
055600     DISPLAY '****************************************'.                  
055700     MOVE PA-RECS-IN             TO PV-DISPLAY-REC-3.                     
055800     DISPLAY '*** RECORDS READ     ==> ' PV-DISPLAY-REC-3.                
055900     MOVE PA-PROCESS-QTY         TO PV-DISPLAY-REC-3.                     
056000     DISPLAY '*** UNITS PROCESSED  ==> ' PV-DISPLAY-REC-3.                
056100     MOVE PA-TCEPLAU-INSERTED    TO PV-DISPLAY-REC-3.                     
056200     DISPLAY '*** TCEPLAU INSERTED ==> ' PV-DISPLAY-REC-3.                
056210     MOVE PA-TCEPLDS-UPDATED     TO PV-DISPLAY-REC-3.                     
056220     DISPLAY '*** TCEPLDS UPDATED  ==> ' PV-DISPLAY-REC-3.                
056300     DISPLAY '****************************************'.                  
056400     DISPLAY ' '.                                                         
056500                                                                          
056600*CLOSE FILES.                                                             
056700     CLOSE CE-PULL-DATA-FILE.                                             
056900                                                                          
057300     DISPLAY '***  END OF CEKUP901  ***'.                                 
057400     GOBACK.                                                              
057500                                                                          
057600******************************************************************        
057700 0100-INITIALIZATION.                                                     
057800******************************************************************        
057900     MOVE '0100-INITIALIZATION        ' TO PV-ABEND-PARAGRAPH.            
058000                                                                          
058100     OPEN INPUT  CE-PULL-DATA-FILE.                                       
058300                                                                          
059500     READ CE-PULL-DATA-FILE                                               
059600         AT END                                                           
059700            SET PS-END-OF-FILE-YES TO TRUE.                               
059800                                                                          
059900     IF PS-END-OF-FILE-NO                                                 
060000        ADD +1 TO PA-RECS-IN                                              
060100        ADD CE-ADJ-QTY TO PA-PROCESS-QTY                                  
060200     END-IF.                                                              
060300                                                                          
060400******************************************************************        
060500 1000-PROCESS-INPUT.                                                      
060600******************************************************************        
060700     MOVE '1000-PROCESS-INPUT         ' TO PV-ABEND-PARAGRAPH.            
060800                                                                          
                                                                                
           MOVE CE-PULL-NBR            TO DK-CE-PULL-NBR.                       
           MOVE CE-LINE-NBR            TO DK-CE-LINE-NBR.                       
           MOVE CE-STOR-NBR            TO DK-CE-STOR-NBR.                       
061200                                                                          
RGTEMP     PERFORM 1300-GET-STR-DS.                                             
068030     PERFORM 1100-UPDATE-TCEPLDS.                                         
RGTEMP     PERFORM 1400-INSERT-ADJ.                                             
068200                                                                          
060900     DISPLAY 'PULL NUMBER: ' CE-PULL-NBR.                                 
061000     DISPLAY 'LINE NUMBER: ' CE-LINE-NBR.                                 
061100     DISPLAY 'STORE NUMBER: ' CE-STOR-NBR.                                
061110     DISPLAY 'BEFORE QTY: ' CEPLAU-BEF-ADJ-UNT-QTY.                       
061110     DISPLAY 'AFTER QTY: ' CEPLAU-AFT-ADJ-UNT-QTY.                        
061120     DISPLAY ' '.                                                         
068300     READ CE-PULL-DATA-FILE                                               
068400         AT END                                                           
068500            SET PS-END-OF-FILE-YES TO TRUE.                               
068600                                                                          
068700     IF PS-END-OF-FILE-NO                                                 
068800        ADD +1 TO PA-RECS-IN                                              
068900        ADD CE-ADJ-QTY TO PA-PROCESS-QTY                                  
069000     END-IF.                                                              
097200                                                                          
104300******************************************************************        
104400 1100-UPDATE-TCEPLDS.                                                     
104500******************************************************************        
104600     MOVE '1100-UPDATE-TCEPLDS        ' TO PV-ABEND-PARAGRAPH.            
104700                                                                          
104710     MOVE CE-ADJ-QTY TO PV-ADJ-QTY.                                       
RGTEMP     IF CE-ADJ-TYPE = 'A'                                                 
105300         EXEC SQL                                                         
105400              UPDATE TCEPLDS                                              
105500                SET  ACTL_DISTRB_QTY  =                                   
105510                     ACTL_DISTRB_QTY + :PV-ADJ-QTY                        
105520              WHERE  CNSTK_REQ_PL_NBR = :DK-CE-PULL-NBR                   
105530                AND  PL_LNE_NBR = :DK-CE-LINE-NBR                         
105540                AND  STR_NBR = :DK-CE-STOR-NBR                            
105900         END-EXEC                                                         
RGTEMP     ELSE                                                                 
RGTEMP     IF CE-ADJ-TYPE = 'S'                                                 
               IF (ASC-DS-QTY-CURR - PV-ADJ-QTY) < 0                            
               DISPLAY 'CANNOT UPDATE TO NEGATIVE QTY'                          
               ELSE                                                             
105300             EXEC SQL                                                     
105400                  UPDATE TCEPLDS                                          
105500                  SET  ACTL_DISTRB_QTY  =                                 
105510                       ACTL_DISTRB_QTY - :PV-ADJ-QTY                      
105520                  WHERE  CNSTK_REQ_PL_NBR = :DK-CE-PULL-NBR               
105530                  AND  PL_LNE_NBR = :DK-CE-LINE-NBR                       
105540                  AND  STR_NBR = :DK-CE-STOR-NBR                          
105900             END-EXEC                                                     
               END-IF                                                           
RGTEMP     END-IF                                                               
106000                                                                          
106100     EVALUATE TRUE                                                        
106200       WHEN SQLCODE = ZERO                                                
106210           ADD +1 TO PA-TCEPLDS-UPDATED                                   
RGTEMP       WHEN SQLCODE = +100                                                
RGTEMP           DISPLAY 'DOES NOT EXIST'                                       
106500       WHEN OTHER                                                         
106600           MOVE 'TCEPLDS' TO PV-DB2-TABLE-NAME                            
106700           MOVE 'UPDATE DISTRIBUTION RECORD'                              
106800                          TO PV-DB2-OPERATION-MESSAGE-1                   
106810           STRING 'PULL# / LINE / STORE = '                               
106811                  CE-PULL-NBR    ' / '                                    
106812                  CE-LINE-NBR    ' / '                                    
106813                  CE-STOR-NBR                                             
107300             DELIMITED BY SIZE                                            
107400             INTO PV-DB2-OPERATION-MESSAGE-2                              
107500           PERFORM 1500-DB2-ABEND                                         
107600     END-EVALUATE                                                         
107700                                                                          
RGTEMP     PERFORM 1200-COMMIT.                                                 
120500                                                                          
135100******************************************************************        
135200 1200-COMMIT.                                                             
135300******************************************************************        
135400     MOVE '1200-COMMIT                ' TO PV-ABEND-PARAGRAPH.            
135500                                                                          
135600     EXEC SQL                                                             
135700         COMMIT                                                           
135800     END-EXEC.                                                            
135900                                                                          
      ******************************************************************        
116900 1300-GET-STR-DS.                                                         
      ******************************************************************        
117000                                                                          
117100     EXEC SQL                                                             
117200         SELECT ACTL_DISTRB_QTY                                           
117300           INTO :CEPLDS-ACTL-DISTRB-QTY :PV-NULL-IND-1                    
117400           FROM TCEPLDS                                                   
117500          WHERE CNSTK_REQ_PL_NBR  = :DK-CE-PULL-NBR                       
117600            AND PL_LNE_NBR        = :DK-CE-LINE-NBR                       
117700            AND STR_NBR           = :DK-CE-STOR-NBR                       
117800     END-EXEC.                                                            
117900                                                                          
118000     EVALUATE TRUE                                                        
118100         WHEN SQLCODE = ZERO                                              
               IF  PV-NULL-IND-1 < +0                                           
                    MOVE ZEROES  TO ASC-DS-QTY-CURR                             
               ELSE                                                             
                   MOVE CEPLDS-ACTL-DISTRB-QTY TO ASC-DS-QTY-CURR               
               END-IF                                                           
                                                                                
119100         WHEN SQLCODE = +100                                              
119200             CONTINUE                                                     
119400         WHEN SQLCODE = -904                                              
119500         WHEN SQLCODE = -911                                              
119600         WHEN SQLCODE = -913                                              
119700             PERFORM 1500-DB2-ABEND                                       
119900         WHEN SQLWARN0 NOT = SPACE                                        
120000         WHEN SQLCODE < ZERO                                              
120100         WHEN SQLCODE > ZERO                                              
120300             MOVE '1300-GET-STR-DS' TO PV-ABEND-PARAGRAPH                 
120400             MOVE 'TCEPLDS'         TO PV-DB2-TABLE-NAME                  
120500             MOVE 'SELECT STORE DISTRIBUTION'                             
120600                                    TO PV-DB2-OPERATION-MESSAGE-1         
120700             SET DP013-DB2-ABEND    TO TRUE                               
120800             PERFORM 1500-DB2-ABEND                                       
120900     END-EVALUATE.                                                        
121000                                                                          
      ******************************************************************        
125000 1400-INSERT-ADJ.                                                         
      ******************************************************************        
125100                                                                          
125200     MOVE CE-PULL-NBR        TO CEPLAU-CNSTK-REQ-PL-NBR.                  
125300     MOVE CE-LINE-NBR        TO CEPLAU-PL-LNE-NBR.                        
125400     MOVE CE-STOR-NBR        TO CEPLAU-STR-NBR.                           
125500     MOVE ASC-DS-QTY-CURR    TO CEPLAU-BEF-ADJ-UNT-QTY.                   
RGTEMP     IF CE-ADJ-TYPE = 'A'                                                 
RGTEMP        COMPUTE ASC-ADJ-QTY-TO-N = ASC-DS-QTY-CURR + CE-ADJ-QTY           
RGTEMP     ELSE                                                                 
RGTEMP     IF CE-ADJ-TYPE = 'S'                                                 
RGTEMP        COMPUTE ASC-ADJ-QTY-TO-N = ASC-DS-QTY-CURR - CE-ADJ-QTY           
RGTEMP     END-IF.                                                              
125600     MOVE ASC-ADJ-QTY-TO-N   TO CEPLAU-AFT-ADJ-UNT-QTY.                   
125700     MOVE CE-USER-ID         TO CEPLAU-ADJ-BY-USR-ID.                     
125800     MOVE SPACES             TO CEPLAU-EXPRT-PRC-BTCH-ID.                 
125900     MOVE 250                TO CEPLAU-ADJ-RSN-DESC-LEN.                  
126000     MOVE CE-DESCRIPTION     TO CEPLAU-ADJ-RSN-DESC-TEXT.                 
126100                                                                          
126200     EXEC SQL                                                             
126300         INSERT INTO TCEPLAU                                              
126400             (CNSTK_REQ_PL_NBR                                            
126500           ,  PL_LNE_NBR                                                  
126600           ,  STR_NBR                                                     
126700           ,  ADJ_TMST                                                    
126800           ,  BEF_ADJ_UNT_QTY                                             
126900           ,  AFT_ADJ_UNT_QTY                                             
127000           ,  ADJ_BY_USR_ID                                               
127100           ,  EXPRT_PRC_BTCH_ID                                           
127200           ,  ADJ_RSN_DESC)                                               
127300      VALUES (:CEPLAU-CNSTK-REQ-PL-NBR                                    
127400           ,  :CEPLAU-PL-LNE-NBR                                          
127500           ,  :CEPLAU-STR-NBR                                             
127600           ,  CURRENT TIMESTAMP                                           
127700           ,  :CEPLAU-BEF-ADJ-UNT-QTY                                     
127800           ,  :CEPLAU-AFT-ADJ-UNT-QTY                                     
127900           ,  :CEPLAU-ADJ-BY-USR-ID                                       
128000           ,  :CEPLAU-EXPRT-PRC-BTCH-ID                                   
128100           ,  :CEPLAU-ADJ-RSN-DESC)                                       
128200     END-EXEC.                                                            
128300                                                                          
128400     EVALUATE TRUE                                                        
128500         WHEN SQLCODE = ZERO                                              
128600          ADD +1 TO PA-TCEPLAU-INSERTED                                   
128800         WHEN SQLCODE = -530                                              
                    CONTINUE                                                    
128800         WHEN SQLCODE = -904                                              
128900         WHEN SQLCODE = -911                                              
129000         WHEN SQLCODE = -913                                              
129100             PERFORM 1500-DB2-ABEND                                       
129200                                                                          
129300         WHEN OTHER                                                       
129400             MOVE '8100-INSERT-ADJ-AUD'                                   
129500                                    TO PV-ABEND-PARAGRAPH                 
129600             MOVE 'UNSUCCESSFUL INSERT OF ADJUSTMENT AUDIT'               
129700                                    TO PV-DB2-OPERATION-MESSAGE-1         
129900             MOVE 'TCEPLAU '        TO PV-DB2-TABLE-NAME                  
130000             SET DP013-DB2-ABEND                                          
130100                 DP013-NO-ROLLBACK  TO TRUE                               
130200             PERFORM 1500-DB2-ABEND                                       
130300     END-EVALUATE.                                                        
RGTEMP                                                                          
RGTEMP     PERFORM 1200-COMMIT.                                                 
130400                                                                          
142900******************************************************************        
143000 1500-DB2-ABEND.                                                          
143100******************************************************************        
143200     MOVE '1500-DB2-ABEND             ' TO PV-ABEND-PARAGRAPH.            
143300                                                                          
143400     MOVE SQLCODE TO PV-SQLCODE.                                          
143500     DISPLAY '*** DB2 ERROR '.                                            
143600     DISPLAY '*** SQLCODE   = ' PV-SQLCODE.                               
143700     DISPLAY '*** PROGRAM   = ' PV-ABEND-PROGRAM.                         
143800     DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.                       
143900     DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.                       
144000     DISPLAY '*** MESSAGE   = ' PV-DB2-OPERATION-MESSAGE-1.               
144100     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-2.               
144200     DISPLAY '*** TABLE 1   = ' PV-DB2-TABLE-NAME.                        
144300                                                                          
144400     COPY DPPD004.                                                        
144500                                                                          
144600     MOVE +4013 TO PV-ABEND-CODE.                                         
144700     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
144800                                                                          
144900******************************************************************        
