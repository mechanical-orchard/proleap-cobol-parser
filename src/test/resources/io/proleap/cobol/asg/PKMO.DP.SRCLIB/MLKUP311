                                                                        00010092
       IDENTIFICATION DIVISION.                                         00020092
                                                                        00030092
       PROGRAM-ID.    MLKUP311.                                         00040092
       AUTHOR.        BOB MCASEY.                                       00050092
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00060092
       DATE-WRITTEN.  08-06-1998.                                       00070092
       DATE-COMPILED.                                                   00080092
                                                                        00090092
      ******************************************************************00100092
      * MLKUP311 - M/L RECALC MONTH-TO-DATE SUB CLASS STORE TABLE       00110092
      ******************************************************************00120092
      *  THIS PROGRAM WILL RECALCULATE THE FOLLOWING FIELDS ON TMNSCLS *00130092
      *    SHNK_RTL_AMT     - RETAIL SHRINK                            *00140092
      *    END_INV_RTL_AMT  - RETAIL ENDING INVENTORY                  *00150092
      *    CUM_MKUP_PCT     - CUM-TO-DATE MARKUP PERCENT               *00160092
      *    END_INV_COST_AMT - COST ENDING INVENTORY                    *00170092
      *    SLS_COST_AMT     - COST AS A BASIS OF SALES                 *00180092
      *    GRO_MRGN_AMT     - GROSS MARGIN DOLLAR AMOUNT               *00190092
      **                                                              **00200092
      * THE INPUT CONTROL CARD WILL BE USED TO DETERMINE WHICH MONTH'S *00210092
      *    ROW WILL BE UPDATED/RECALCULATED AND USED TO OBTAIN THE     *00220092
      *    PREVIOUS MONTH AND SEASON END DATES.                        *00230092
      ******************************************************************00240092
      *                                                                 00250092
      *     TABLE TMLCNTL CONTAINS DATES RELATIVE TO PROCESSING DATES.  00260092
      *  MLKUP313 USES THE CURRENT 'OPEN' FISCAL MONTH-END-DATE AND THE 00270092
      *  MAX MONTH-END POSTDATE (MXPSTG) THAT WILL BE RECALCED THIS RUN.00280092
      *     TMLCNTL WILL BE READ TO GET THE DATE-RANGE TO PROCESS. THESE00290092
      *  DATES WILL BE MOVED TO A TABLE AND SUBSCRIPTED BASED ON MN-END.00300092
      *  THE FIRST TABLE OCCURENCE WILL BE THE DATES RELATED TO THE     00310092
      *  MONTH PRIOR TO CURRENT OPEN MONTH IN ORDER TO CARRY FORWARD THE00320092
      *  ENDING-INVENTORY.                                              00330092
      *                                                                 00340092
      ******************************************************************00350093
      *    WR/PITS#     DATE        DESCRIPTION                        *00360093
      *    --------   --------     ---------------------------------   *00370093
      *    5701       11/17/1998   MODIFIED WS ACCUM FIELDS TO MIMIC   *00380093
      *                            CHANGES MADE TO DCLGEN.             *00390093
      *                            ALL "AMT" FIELDS WERE CHANGED FROM  *00400093
      *                            PIC S9(07)V99 TO S9(09)V99.         *00410093
      *                            THIS WAS DONE DUE TO AN OVERFLOW ON *00420094
      *                            XFER_IN_RTL_AMT & XFER_OUT_RTL_AMT  *00430094
      *                            THAT OCCURRED WITHIN TMNSCLS ON     *00440095
      *                            DEPT 127; SCLS 80 ON 11/15/1998.    *00450093
      *    10422      04/05/1999   PAUL KEBER - STRATAGEM              *00460099
      *                            CHANGES MADE TO OBTAIN THE SHRINK   *00470099
      *                            RETAIL AMOUNT FROM THE TWKSHNK DB2  *00480099
      *                            TABLE INSTEAD OF CALCULATING THE    *00490099
      *                            AMOUNT.                             *00500099
      *    4095       08/13/1999   SCOTT JACKSON                       *00510099
      *                            ML- CUM MARK-UP PCT MAX = S9(4)V9(7)*00520099
      *                            BP- CALCS CUM MARKUP PCT >S9(4)V9(7)*00530099
      *                            IF ML CUM MARK-UP PCT IS EXCEEDED,  *00540099
      *                            A RETURN CODE OF '4095' IS CREATED  *00550099
      *                            WHICH WILL FORCE A MESSAGE TO BP APPL00560099
      *                            ADDED OUT01 TO CAPTURE THESE ROWS   *00570099
      *    9999       04/06/2000   PAUL KEBER                          *00570199
      *                            ELIMINATED THE SUB_CL_CSR CURSOR BY *00570299
      *                            OBTAINING THE STORE NUMBER FROM THE *00570399
      *                            INPUT FILE.  CHANGED THE SEASON_TO_ *00570499
      *                            DATE_CSR CURSOR TO A SELECT         *00570599
      *                            STATEMENT.                          *00570699
      * WR26603       03/11/2004   D. THEEL                            *00570799
      *                            CHANGED WHERE CLAUSES TO USE HOST   *00570899
      *                            VARIABLES, ADDED UR TO SELECTS,     *00570999
      *                            INCREASED OUTPUT FROM 72 TO 75.     *00571099
      * W31475        06/01/2009   COGNIZANT                           *00570799
      *                            INTRODUCE MULTI-ROW FETCH - CHANGED *00570899
      *                            AS PART OF ML BATCH PERF TUNING     *00570999
      *                            PROJECT                             *00571099
      ******************************************************************00580099
                                                                        00590092
       ENVIRONMENT DIVISION.                                            00600092
                                                                        00610092
       CONFIGURATION SECTION.                                           00620092
       SOURCE-COMPUTER.        IBM-3090.                                00630092
       OBJECT-COMPUTER.        IBM-3090.                                00640092
       INPUT-OUTPUT SECTION.                                            00650092
       FILE-CONTROL.                                                    00660092
      *                                                                 00670093
           SELECT MNSCLS-EXTR-FILE                                      00680092
               ASSIGN TO INP01.                                         00690092
      **                                                                00700099
4095                                                                    00710099
4095       SELECT OUTPUT-FILE                                           00720099
4095           ASSIGN TO OUT01.                                         00730099
4095                                                                    00740099
                                                                        00750092
      *                                                                 00760092
       DATA DIVISION.                                                   00770092
       FILE SECTION.                                                    00780092
      **                                                                00790092
       FD  MNSCLS-EXTR-FILE                                             00800092
           RECORDING MODE IS F                                          00810092
           LABEL RECORDS ARE STANDARD                                   00820092
           BLOCK CONTAINS 0 RECORDS.                                    00830092
                                                                        00840092
       01  MNSCLS-EXTR-REC             PIC X(18).                       00850092
      **                                                                00860099
4095                                                                    00870099
4095   FD  OUTPUT-FILE                                                  00880099
4095       LABEL RECORDS ARE STANDARD                                   00890099
4095       RECORDING MODE IS F                                          00900099
4095       BLOCK CONTAINS 0 RECORDS.                                    00910099
W26603 01  OUTPUT-RECORD               PIC X(75).                       00920099
4095                                                                    00930099
                                                                        00940092
      *                                                                 00950092
       WORKING-STORAGE SECTION.                                         00960092
                                                                        00970092
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00980092
                                                                        00990092
       01  PA-PROGRAM-ACCUMULATORS.                                     01000092
           05  PA-COMMIT-COUNT                 PIC  9(09) VALUE ZEROES. 01010092
           05  PA-DEPT-COUNT                   PIC  9(09) VALUE ZEROES. 01020092
           05  PA-INSERT-COUNT                 PIC  9(09) VALUE ZEROES. 01030092
           05  PA-UPDATE-COUNT                 PIC  9(09) VALUE ZEROES. 01040092
           05  PA-EXTRACT-RECS-READ            PIC  9(09) VALUE ZEROES. 01050092
4095       05  PA-CUM-MKUP-MAX-LIMIT           PIC  9(05) VALUE ZEROES. 01060099
                                                                        01070092
       01  PS-PROGRAM-SWITCHES.                                         01080092
           05  PS-EOF-EXTR-FILE-SW             PIC X(01) VALUE 'N'.     01090092
               88  PS-EOF-EXTR-FILE                      VALUE 'Y'.     01100092
           05  PS-FIRST-TIME-SW                PIC X     VALUE 'N'.     01110092
               88  PS-FIRST-TIME-THRU                    VALUE 'Y'.     01120092
           05  PS-FIRST-COMMIT-SW              PIC X     VALUE 'N'.     01130092
               88  PS-FIRST-COMMIT                       VALUE 'Y'.     01140092
           05  PS-CONTROL-TABLE-EMPTY-SW       PIC X     VALUE 'N'.     01150092
               88  PS-EMPTY-CONTROL-TABLE                VALUE 'Y'.     01160092
           05  PS-LOAD-DATE-TABLE-SW           PIC X     VALUE 'N'.     01170092
               88  PS-DATE-TABLE-LOADED                  VALUE 'Y'.     01180092
           05  PS-DEPT-PROCESSING-SW           PIC X     VALUE 'N'.     01190092
               88  PS-DEPT-PROCESSED                     VALUE 'Y'.     01200092
           05  PS-DEPT-SCLS-CURSOR-SW          PIC X     VALUE 'N'.     01210092
               88  PS-END-OF-DEPT-SCLS-CURSOR            VALUE 'Y'.     01220092
           05  PS-TABLE-PROCESSING-SW          PIC X     VALUE 'N'.     01230092
               88  PS-TABLE-PROCESSED                    VALUE 'Y'.     01240092
PK0400     05  PS-SEA-LOAD-DATE-TABLE-SW       PIC X     VALUE 'N'.     01240199
PK0400         88  PS-SEA-DATE-TABLE-LOADED              VALUE 'Y'.     01240299
PK0400     05  PS-SEASON-DATE-TABLE-SW         PIC X     VALUE 'N'.     01240399
PK0400         88  PS-SEASON-DATE-TABLE-LOADED           VALUE 'Y'.     01240499
PK0400         88  PS-SEASON-DATE-TAB-NOT-LOADED         VALUE 'N'.     01241099
           05  PS-CURR-MONTH-SW                PIC X     VALUE 'N'.     01250092
               88  PS-CURR-MONTH-ROW-NOT-FOUND           VALUE 'Y'.     01260092
           05  PS-PREV-MONTH-ROW-SW            PIC X     VALUE 'N'.     01270092
               88  PS-PREV-MONTH-ROW-NOT-FOUND           VALUE 'Y'.     01280092
           05  PS-DEPT-SHRINK-PCT-SW           PIC X     VALUE 'N'.     01290092
               88  PS-DEPT-SHRINK-RETRIEVED              VALUE 'Y'.     01300092
           05  PS-CARRY-FORWARD-SW             PIC X     VALUE 'N'.     01310092
               88  PS-PFM-INV-DOLLARS-FOUND              VALUE 'Y'.     01320092
           05  PS-PREV-SEASON-INV-SW           PIC X     VALUE 'N'.     01330092
               88  PS-NO-PREV-SEASONAL-INV               VALUE 'Y'.     01340092
           05  PS-CURR-SEASON-MTHS-SW          PIC X     VALUE 'N'.     01340199
               88  PS-END-OF-CURR-SEASON-MTHS            VALUE 'Y'.     01340299
               88  PS-NOT-END-OF-CURR-SEASON-MTHS        VALUE 'N'.     01341099
           05  PS-CURR-SEASON-CURSOR-SW        PIC X     VALUE 'N'.     01350092
               88  PS-END-OF-CURR-SEASON-CSR             VALUE 'Y'.     01360092
           05  PS-CURRENT-PURCH-COST-SW        PIC X     VALUE 'N'.     01370092
               88  PS-NO-CURRENT-PURCH-COSTS             VALUE 'Y'.     01380092
           05  PS-RESULTANT-TABLE-SW           PIC X     VALUE 'N'.     01390092
               88  PS-EMPTY-RESULT-TABLE                 VALUE 'Y'.     01400092
           05  PS-COMMIT-TAKEN-SW              PIC X     VALUE 'N'.     01410092
               88  PS-COMMIT-TAKEN                       VALUE 'Y'.     01420092
           05  PS-SEASON-MTH-SW                PIC X     VALUE 'N'.     01420199
               88  PS-SEASON-MTH-FOUND                   VALUE 'Y'.     01420299
               88  PS-SEASON-MTH-NOT-FOUND               VALUE 'N'.     01421099
                                                                        01430092
       01  PC-PROGRAM-CONSTANTS.                                        01440092
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK220'.    01450092
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP311'.  01460092
           05  PC-MAX-DATE-TABLE       PIC  9(02)    VALUE 24.          01470092
                                                                        01480092
       01  PE-PROGRAM-ERROR-MESSAGES.                                   01490092
           05  PE-MSG-01                       PIC X(30) VALUE          01500092
                'INSPECT WHERE CLAUSE VARIABLE'.                        01510092
           05  PE-MSG-02                       PIC X(30) VALUE          01520092
                'DB2 SELECT ATTEMPTED         '.                        01530092
           05  PE-MSG-03                       PIC X(30) VALUE          01540092
                'ATTEMPTING TO OPEN DB2 CURSOR'.                        01550092
           05  PE-MSG-04                       PIC X(30) VALUE          01560092
                'ATTEMPT TO FETCH CURSOR ROW  '.                        01570092
           05  PE-MSG-05                       PIC X(30) VALUE          01580092
                'ATTEMPT TO CLOSE DB2 CURSOR  '.                        01590092
           05  PE-MSG-06                       PIC X(30) VALUE          01600092
                'ERROR ON UPDATE OF TMNSCLS    '.                       01610092
           05  PE-MSG-07                       PIC X(30) VALUE          01620092
                'ERROR IN SELECT - TCKRSTCNTL '.                        01630092
           05  PE-MSG-08                       PIC X(30) VALUE          01640092
                'UPDATE WORK_STRG - TCKRSTINF '.                        01650092
           05  PE-MSG-09                       PIC X(30) VALUE          01660092
                'PGM ABEND DURING A COMMIT    '.                        01670092
           05  PE-MSG-10                       PIC X(30) VALUE          01680092
                'UPDATE OF TCKRSTCNTL FAILED  '.                        01690092
           05  PE-MSG-11                       PIC X(30) VALUE          01700092
                'ERROR IN SELECT - TCKRSTINF  '.                        01710092
           05  PE-MSG-12                       PIC X(30) VALUE          01720092
                'UNABLE TO OBTAIN TIMESTAMP   '.                        01730092
           05  PE-MSG-13                       PIC X(30) VALUE          01740092
                'ERROR IN SELECT - TWKSHNK    '.                        01750099
           05  PE-MSG-14                       PIC X(30) VALUE          01760092
                'ERROR ON ''INSERT'' TO TMNSCLS'.                       01770092
           05  PE-MSG-15                       PIC X(30) VALUE          01780092
                'MAX DATE TBL ENTRIES EXCEEDED'.                        01790092
           05  PE-MSG-16                       PIC X(30) VALUE          01790199
                'ERROR IN SEA MN NBR - DTATTR.'.                        01790299
           05  PE-MSG-17                       PIC X(30) VALUE          01790399
                'ERROR IN PFM MN NBR - DTATTR.'.                        01790499
           05  PE-MSG-18                       PIC X(30) VALUE          01790599
                'ERR PREV SEA MN NBR - DTATTR.'.                        01790699
           05  PE-MSG-20                       PIC X(30) VALUE          01790999
                'ERR IN OPN SEA MTH - TDTATTR.'.                        01791099
           05  PE-MSG-21                       PIC X(30) VALUE          01791199
                'ERR IN FETCH SEA MTH - DTATTR'.                        01791299
           05  PE-MSG-23                       PIC X(30) VALUE          01791599
                'ERROR IN CLOS SEA MTH - DTAT.'.                        01792099
       01  MISC.                                                        01800092
           05  MISC-HOLD-DEPT                  PIC X(03)  VALUE SPACES. 01810092
      *                                                                 01820099
W31475 01  WS-SHNK-RTL-AMT-NULL-IND     PIC S9(04) COMP VALUE 0.                
  |    01  WS-COMMIT-TIME.                                                      
  |        05  WS-COMMIT-HH             PIC  9(02) VALUE 0.                     
  |        05  WS-COMMIT-MIN            PIC  9(02) VALUE 0.                     
  |        05  WS-COMMIT-SEC            PIC  9(02) VALUE 0.                     
  |        05  WS-COMMIT-MSEC           PIC  9(02) VALUE 0.                     
  |    01  WS-HCOMMIT-TIME.                                                     
  |        05  WS-HCOMMIT-HH            PIC  9(02) VALUE 0.                     
  |        05  WS-HCOMMIT-MIN           PIC  9(02) VALUE 0.                     
  |        05  WS-HCOMMIT-SEC           PIC  9(02) VALUE 0.                     
  |        05  WS-HCOMMIT-MSEC          PIC  9(02) VALUE 0.                     
  |    01  WS-CURRENT-TIME.                                                     
  |        05  WS-CURRENT-HH            PIC  9(02) VALUE 0.                     
  |        05  WS-CURRENT-MIN           PIC  9(02) VALUE 0.                     
  |        05  WS-CURRENT-SEC           PIC  9(02) VALUE 0.                     
W31475     05  WS-CURRENT-MSEC          PIC  9(02) VALUE 0.                     
      *                                                                 01820099
4095   01  OUTPUT-DETAIL-LINE.                                          01830099
4095       05  FILLER                   PIC  X(05) VALUE 'STR: '.       01840099
4095       05  FILLER                   PIC  X(01) VALUE SPACES.        01850099
W26603     05  DL-STORE-NBR             PIC  Z(05) VALUE SPACES.        01860099
4095       05  FILLER                   PIC  X(01) VALUE SPACES.        01870099
4095       05  FILLER                   PIC  X(11) VALUE ' DEPT/SCL: '. 01880099
4095       05  DL-DEPT-NBR              PIC  X(03) VALUE SPACES.        01890099
4095       05  FILLER                   PIC  X(01) VALUE '-'.           01900099
4095       05  DL-SCL-NBR               PIC  X(03) VALUE SPACES.        01910099
4095       05  DL-CUM-MKUP-PCT          PIC  Z,ZZ9,999.9999999-.        01920099
4095       05  FILLER                   PIC  X(03) VALUE SPACES.        01930099
4095       05  FILLER                   PIC  X(09) VALUE                01940099
4095                                               'ML USED: '.         01950099
4095       05  DL-CUM-MKUP-USED         PIC  Z,999.9999999-.            01960099
      *                                                                 01970092
       01  PROGRAM-VARIABLES.                                           01980092
           05  PV-TMNSCLS-UPDATE-VARIABLES.                             01990092
               10  PV-HOLD-DEPT-NBR            PIC  X(03) VALUE SPACES. 02000092
               10  PV-HOLD-SUB-CL-NBR          PIC  X(02) VALUE SPACES. 02010092
               10  PV-HOLD-STR-NBR             PIC S9(04) VALUE +0      02020092
                                                               COMP.    02030092
W26603         10  PV-HOLD-STR-NBR-RED         PIC  9(05) VALUE ZERO.   02040099
     ** THESE VARIABLES WILL UPDATE CURRENT TMNSCLS ROW *************** 02050092
1198RM* CHANGED FIELDS FROM S9(07)V99 TO S9(09)V99                      02060096
W26603         10  PV-SHNK-RTL-AMT             PIC  S9(11)V99  COMP-3.  02070099
W26603         10  PV-END-INV-RTL-AMT          PIC  S9(11)V99  COMP-3.  02080099
     *         10  PV-CUM-MKUP-PCT             PIC  S9(4)V9(7) COMP-3.  02090092
4095           10  PV-CUM-MKUP-PCT-MAX         PIC  S9(9)V9(7) COMP-3.  02100099
W26603         10  PV-END-INV-COST-AMT         PIC  S9(11)V99  COMP-3.  02110099
W26603         10  PV-SLS-COST-AMT             PIC  S9(11)V99  COMP-3.  02120099
W26603         10  PV-GRO-MRGN-AMT             PIC  S9(11)V99  COMP-3.  02130099
     *******************************************************************02140092
1198RM* CHANGED FIELDS FROM S9(09)V99 TO S9(11)V99                      02150097
           05  PV-SEASON-TO-DATE-CSR-VAR.                               02160092
               10  PV-MNSCLS-RCPT-RTL-AMT      PIC S9(11)V99 COMP-3.    02170097
               10  PV-MNSCLS-RCPT-NET-COST-AMT PIC S9(11)V99 COMP-3.    02180097
               10  PV-MNSCLS-PADJ-RTL-AMT      PIC S9(11)V99 COMP-3.    02190097
               10  PV-MNSCLS-PADJ-NET-COST-AMT PIC S9(11)V99 COMP-3.    02200097
               10  PV-MNSCLS-MKUP-RTL-AMT      PIC S9(11)V99 COMP-3.    02210097
               10  PV-MNSCLS-XFER-NET-AMT      PIC S9(11)V99 COMP-3.    02220097
           05  PV-CURR-MONTH-COST-VAR.                                  02230092
               10  PV-CURR-MONTH-RCPT-NET-COST PIC S9(11)V99 COMP-3.    02240097
               10  PV-CURR-MONTH-PADJ-NET-COST PIC S9(11)V99 COMP-3.    02250097
           05  PV-PREV-MONTH-VAR.                                       02260092
               10  PV-PREV-MONTH-END-INV-RTL   PIC S9(11)V99 COMP-3.    02270097
               10  PV-PREV-MONTH-END-INV-COST  PIC S9(11)V99 COMP-3.    02280097
           05  PV-PREV-SEASON-VAR.                                      02290092
               10  PV-PREV-SEASON-PURCH-RETAIL PIC S9(11)V99 COMP-3.    02300097
               10  PV-PREV-SEASON-PURCH-COST   PIC S9(11)V99 COMP-3.    02310097
           05  PV-CUM-TO-DATE-VARIABLES.                                02320092
               10  PV-CUM-TO-DATE-PURCH-RETAIL PIC S9(11)V99 COMP-3.    02330097
               10  PV-CUM-TO-DATE-PURCH-COST   PIC S9(11)V99 COMP-3.    02340097
               10  PV-CUM-SEASON-MKUP          PIC S9(11)V99 COMP-3.    02350097
      *                                                                 02360092
      *                                                                 02370092
       01  PV-VARIABLE-FIELDS.                                          02380092
           05  PV-SUB1                         PIC 9(2)   VALUE ZEROES. 02390092
           05  PV-SEA-SUB                      PIC 9(2)   VALUE ZEROES. 02391099
           05  PV-MAX-DATE-SUB                 PIC 9(2)   VALUE ZEROES. 02400092
           05  PV-HOLD-DATE                    PIC  X(10) VALUE SPACES. 02410092
PK0400     05  PV-HOLD-PFM-MN-NBR              PIC  X(02) VALUE SPACES. 02410199
PK0400     05  PV-TEMP-PFM-MN-NBR              PIC  X(02) VALUE SPACES. 02410299
           05  PV-HOLD-PFM-END-DTE             PIC  X(10) VALUE SPACES. 02410399
PK0400     05  PV-TEMP-PFM-END-DTE             PIC  X(10) VALUE SPACES. 02410499
           05  PV-HOLD-SEA-BEG-DTE             PIC  X(10) VALUE SPACES. 02410599
PK0400     05  PV-TEMP-SEA-BEG-DTE             PIC  X(10) VALUE SPACES. 02410699
PK0400     05  PV-HOLD-SEA-BEG-MN-NBR          PIC  X(02) VALUE SPACES. 02410799
PK0400     05  PV-TEMP-SEA-BEG-MN-NBR          PIC  X(02) VALUE SPACES. 02410899
           05  PV-HOLD-OPN-MN-END-DTE          PIC  X(10) VALUE SPACES. 02410999
PK0400     05  PV-TEMP-OPN-MN-END-DTE          PIC  X(10) VALUE SPACES. 02411099
PK0400     05  PV-HOLD-OPN-MN-NBR              PIC  X(02) VALUE SPACES. 02411199
PK0400     05  PV-TEMP-OPN-MN-NBR              PIC  X(02) VALUE SPACES. 02411299
           05  PV-HOLD-CAL-MN-NBR              PIC  9(02) VALUE ZEROES. 02411399
PK0400     05  PV-HOLD-PREV-SEA-MN-NBR         PIC  X(02) VALUE SPACES. 02411499
PK0400     05  PV-TEMP-PREV-SEA-MN-NBR         PIC  X(02) VALUE SPACES. 02411599
           05  PV-HOLD-PREV-SEA-END-DTE        PIC  X(10) VALUE SPACES. 02411699
PK0400     05  PV-TEMP-PREV-SEA-END-DTE        PIC  X(10) VALUE SPACES. 02411799
           05  PV-HOLD-FSCL-MN-NBR             PIC  X(02) VALUE SPACES. 02411899
PK0400     05  PV-SEA-MTH-DTE-DB2              PIC  X(10) VALUE SPACES. 02411999
PK0400     05  PV-SEA-MTH-NBR-DB2              PIC  X(02) VALUE SPACES. 02412099
PK0400     05  PV-SEASON-MN-DTE                PIC  X(10) VALUE SPACES. 02412199
PK0400     05  PV-SEASON-MN-NBR                PIC  X(02) VALUE SPACES. 02412299
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 02510092
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 02520092
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 02530092
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 02540092
           05  PV-OPERATION-ATTEMPTED          PIC  X(30) VALUE SPACES. 02550092
           05  PV-XFER-NET                     PIC  S9(11)V99  COMP-3.  02560097
                                                                        02570099
      *** TMNSCLS EXTRACT FILE                                          02580092
       01  EXTR-RECORD.                                                 02590092
           05  EXTR-DEPT-NBR                   PIC X(03)  VALUE SPACES. 02600099
           05  EXTR-SUB-CL-NBR                 PIC X(02)  VALUE SPACES. 02610099
           05  EXTR-STR-NBR                    PIC S9(04) VALUE +0 COMP.02611099
           05  FILLER                          PIC X(11)  VALUE SPACES. 02620099
      *                                                                 02630092
      * TABLE TO CONTAIN DATE INFORMATION FOR ALL MONTHS RE-CALCULATED  02640092
       01  DATE-TABLE.                                                  02650092
           05  CONTROL-DATES OCCURS 24 TIMES.                           02660092
               10  DT-OPN-MN-NBR               PIC X(02)  VALUE SPACES. 02661099
               10  DT-OPN-MN-END-DTE           PIC X(10)  VALUE SPACES. 02670099
               10  DT-CAL-MN-NBR               PIC X(02)  VALUE SPACES. 02680099
               10  DT-FSCL-MN-NBR              PIC X(02)  VALUE SPACES. 02680199
               10  DT-PFM-MN-NBR               PIC X(02)  VALUE SPACES. 02681099
               10  DT-PFM-END-DTE              PIC X(10)  VALUE SPACES. 02690099
               10  DT-PREV-SEA-MN-NBR          PIC X(02)  VALUE SPACES. 02690299
               10  DT-PREV-SEA-END-DTE         PIC X(10)  VALUE SPACES. 02690399
               10  DT-SEA-BEG-MN-NBR           PIC X(02)  VALUE SPACES. 02691099
               10  DT-SEA-BEG-DTE              PIC X(10)  VALUE SPACES. 02700099
PK0400         10  DT-SEA-MN1-DTE              PIC X(10)  VALUE SPACES. 02702199
PK0400         10  DT-SEA-MN1-NBR              PIC X(02)  VALUE SPACES. 02702299
PK0400         10  DT-SEA-MN2-DTE              PIC X(10)  VALUE SPACES. 02702399
PK0400         10  DT-SEA-MN2-NBR              PIC X(02)  VALUE SPACES. 02702499
PK0400         10  DT-SEA-MN3-DTE              PIC X(10)  VALUE SPACES. 02702599
PK0400         10  DT-SEA-MN3-NBR              PIC X(02)  VALUE SPACES. 02702699
PK0400         10  DT-SEA-MN4-DTE              PIC X(10)  VALUE SPACES. 02702799
PK0400         10  DT-SEA-MN4-NBR              PIC X(02)  VALUE SPACES. 02702899
PK0400         10  DT-SEA-MN5-DTE              PIC X(10)  VALUE SPACES. 02702999
PK0400         10  DT-SEA-MN5-NBR              PIC X(02)  VALUE SPACES. 02703099
PK0400         10  DT-SEA-MN6-DTE              PIC X(10)  VALUE SPACES. 02703199
PK0400         10  DT-SEA-MN6-NBR              PIC X(02)  VALUE SPACES. 02704099
      *                                                                 02750098
      ****************************************************              02760098
      *  COMMUNICATION AREAS FOR DB2                     *              02770098
      ****************************************************              02780098
           EXEC SQL                                                     02790098
               INCLUDE SQLCA                                            02800098
           END-EXEC.                                                    02810098
      ****************************************************              02820098
      *  M/L WEEKLY SHRINKAGE TABLE                      *              02830098
      ****************************************************              02840098
           EXEC SQL                                                     02850098
               INCLUDE TWKSHNK                                          02860098
           END-EXEC.                                                    02870098
      ****************************************************              02880092
      *  M/L MONTH-TO-DATE DEPARTMENT TABLE              *              02890092
      ****************************************************              02900092
           EXEC SQL                                                     02910092
               INCLUDE TMNSCLS                                          02920092
           END-EXEC.                                                    02930092
      ****************************************************              02940092
      * M/L DATE TABLE AND CURRENT OPEN FISCAL DATE INFO *              02950092
      ****************************************************              02960092
           EXEC SQL                                                     02970092
               INCLUDE TDTATTR                                          02980092
           END-EXEC.                                                    02990092
                                                                        03000092
      ****************************************************              03010092
      * M/L DATE CONTROL INFO                            *              03020092
      ****************************************************              03030092
           EXEC SQL                                                     03040092
               INCLUDE TMLCNTL                                          03050092
           END-EXEC.                                                    03060092
                                                                        03070092
      ****************************************************              03080092
      *CHECKPOINT RESTART CONTROL AND INFORMATION TABLES *              03090092
      ****************************************************              03100092
           EXEC SQL                                                     03110092
               INCLUDE TCKRSTCN                                         03120092
           END-EXEC.                                                    03130092
      *                                                                 03140092
           EXEC SQL                                                     03150092
               INCLUDE TCKRSTIN                                         03160092
           END-EXEC.                                                    03170092
      *                                                                 03190092
      *                                                                 03530092
      * MONTHS TO BE PROCESSED - ALL DATES BETWEEN OPEN AND MAX DATES   03540092
           EXEC SQL                                                     03550092
               DECLARE FISCAL_DATE_CSR CURSOR FOR                       03560092
                SELECT  DISTINCT FSCL_MN_END_DTE                        03570092
                  FROM  TDTATTR                                         03580092
                 WHERE FSCL_MN_END_DTE BETWEEN                          03590092
                                            :MLCNTL-OPN-FSCL-MN-DTE     03600092
                                        AND :MLCNTL-MXPSTG-FSCL-MN-DTE  03610092
                 ORDER BY FSCL_MN_END_DTE                               03620092
                 FOR FETCH ONLY                                         03630092
                 WITH UR                                                03631099
           END-EXEC.                                                    03640092
      *                                                                 03640199
      *************************************************************     03640299
      * MONTHS TO BE PROCESSED - ALL DATES IN THE CURRENT SEASON        03640399
      *                                         ------------------      03640499
      **************************************************************    03640599
           EXEC SQL                                                     03640699
               DECLARE SEASON_DATE_CSR CURSOR FOR                       03640799
                SELECT  DISTINCT FSCL_MN_END_DTE                        03640899
                                ,FSCL_MN_NBR                            03640999
                  FROM  TDTATTR                                         03641099
                 WHERE FSCL_MN_END_DTE BETWEEN                          03641199
                                            :PV-TEMP-SEA-BEG-DTE        03641299
                                        AND :PV-HOLD-DATE               03641399
                 ORDER BY FSCL_MN_END_DTE                               03641499
                 FOR FETCH ONLY                                         03641599
                 WITH UR                                                03641699
           END-EXEC.                                                    03642099
      *                                                                 03650092
      ******************************************************************03660092
      * CHECKPOINT RESTART VARIABLES                                   *03670092
      ******************************************************************03680092
       01  CR-CHECKPOINT-RESTART-AREA.                                  03690092
           05  CR-AREA-LEN                  PIC S9(04) VALUE +76  COMP. 03700092
           05  CR-CHECKPOINT-RESTART-VAR.                               03710092
               10  CR-PREV-DTL-KEY          PIC  X(31) VALUE SPACES.    03720092
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    03730092
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                 03740092
                   15  CR-FISCAL-MN-NBR     PIC  X(02).                 03750092
                   15  CR-FISCAL-MX-END-DTE PIC  X(10).                 03760092
                   15  CR-FISCAL-MX-NBR     PIC  X(02).                 03770092
                   15  CR-DEPT-NBR          PIC  X(03).                 03780092
                   15  CR-SUB-CL-NBR        PIC  X(02).                 03790092
                   15  CR-STR-NBR           PIC  S9(04) COMP.           03800092
               10  CR-TMNSCLS-INPUT-COUNT   PIC  9(09) VALUE 0.         03810092
               10  CR-TMNSCLS-FETCH-COUNT   PIC  9(09) VALUE 0.         03820092
               10  CR-TMNSCLS-UPDATE-COUNT  PIC  9(09) VALUE 0.         03830092
               10  CR-TMNSCLS-INSERT-COUNT  PIC  9(09) VALUE 0.         03840092
               10  CR-TMNSCLS-COMMIT-COUNT  PIC  9(09) VALUE 0.         03850092
      *                                                                 03860092
      ****************************************************              03870092
      *  ERROR MESSAGE AREA FOR DB2                      *              03880092
      ****************************************************              03890092
           COPY DPWS004.                                                03900092
      *                                                                 03910092
           EJECT                                                        03920092
      *                                                                 03930092
      *-------------------*                                             03940092
       PROCEDURE DIVISION.                                              03950092
      *-------------------*                                             03960092
                                                                        03970092
       0000-MLKUP311.                                                   03980092
                                                                        03990092
           PERFORM 1000-INITIALIZATION.                                 04000092
                                                                        04010092
           PERFORM 2000-MAINLINE                                        04020092
                UNTIL PS-EOF-EXTR-FILE.                                 04030092
                                                                        04040092
           PERFORM 8000-EOJ-ROUTINE.                                    04050092
                                                                        04060099
4095       CLOSE OUTPUT-FILE.                                           04070099
                                                                        04080099
4095       IF PA-CUM-MKUP-MAX-LIMIT > 0                                 04090099
4095          MOVE 4095 TO RETURN-CODE                                  04100099
4095       ELSE                                                         04110099
4095          MOVE 0    TO RETURN-CODE                                  04120099
4095       END-IF.                                                      04130099
                                                                        04140092
           STOP RUN.                                                    04150092
      *                                                                 04160092
      *                                                                 04170092
      ******************************************************************04180092
      * LOAD TABLE WITH MONTHS TO BE RECALCULATED                       04190092
      * DETERMINE IF THIS IS A 'RESTART' (TCKRSTINF-CKPT-STAT-CODE = 9) 04200092
      ******************************************************************04210092
       1000-INITIALIZATION.                                             04220092
      *                                                                 04230092
4095       OPEN OUTPUT OUTPUT-FILE.                                     04240099
      *                                                                 04250099
           PERFORM 7000-READ-CONTROL-TABLE.                             04260092
           PERFORM 7025-PROCESS-CNTL-TBL.                               04270092
                                                                        04280092
           PERFORM 7650-INIT-CHECKPOINT-SELECT.                         04290092
      *                                                                 04300092
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           04310092
                PERFORM 7700-SELECT-RESTART-INFO                        04320092
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                   04330092
                                              CR-CHECKPOINT-RESTART-VAR 04340092
PK0400          DISPLAY 'RESTART-LAST DEPT/SCL COMMIT: ',               04350199
PK0400                   CR-DEPT-NBR '-' CR-SUB-CL-NBR                  04350299
PK0100                   ' STR: ' CR-STR-NBR                            04351099
                MOVE CR-DEPT-NBR      TO PV-HOLD-DEPT-NBR               04360092
                MOVE CR-SUB-CL-NBR    TO PV-HOLD-SUB-CL-NBR             04370092
                MOVE CR-STR-NBR       TO PV-HOLD-STR-NBR                04380092
           ELSE                                                         04390092
               SET PS-FIRST-COMMIT TO TRUE                              04400092
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     04410092
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  04420092
                                 TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'04430092
               PERFORM 7600-GET-COMMIT-TIMESTAMP                        04440092
           END-IF.                                                      04450092
      *                                                                 04460092
           SET PS-FIRST-TIME-THRU TO TRUE.                              04470092
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                          04480092
                                                                        04520092
           OPEN INPUT MNSCLS-EXTR-FILE.                                 04530092
                                                                        04540092
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           04550092
               PERFORM 4000-READ-EXTR-FILE                              04560092
                   UNTIL                                                04570092
                   (PA-EXTRACT-RECS-READ = CR-TMNSCLS-INPUT-COUNT)      04580092
                    OR (PS-EOF-EXTR-FILE)                               04590092
           ELSE                                                         04600092
               PERFORM 4000-READ-EXTR-FILE                              04610092
           END-IF.                                                      04620092
                                                                        04630092
           MOVE EXTR-DEPT-NBR TO MISC-HOLD-DEPT.                        04640092
           DISPLAY 'PROCESSING DEPT: ' MISC-HOLD-DEPT.                  04650092
      *                                                                 04660092
           IF  PS-EOF-EXTR-FILE                                         04700099
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       04710092
                   DISPLAY 'NO RECORDS PROCESSED AFTER RESTART'         04720092
               ELSE                                                     04730092
                   IF TCKRSTCNTL-CKPT-STAT-CODE = '0'                   04740092
                       DISPLAY 'NO ROWS SELECTED FOR RECALCULATION'     04750092
                       SET PS-EMPTY-RESULT-TABLE TO TRUE                04760092
                   END-IF                                               04770092
               END-IF                                                   04780092
           END-IF.                                                      04870092
                                                                        04880099
           MOVE ZERO TO PA-CUM-MKUP-MAX-LIMIT.                          04890099
                                                                        04900092
      ******************************************************************04910092
      * MAIN PROCESSING PERFORM UNTIL END OF EXTRACT FILE               04920092
      ******************************************************************04930092
       2000-MAINLINE.                                                   04940092
                                                                        04950092
           ADD 1      TO PA-DEPT-COUNT.                                 04960092
                                                                        04970092
           MOVE 1 TO PV-SUB1.                                           04980092
           PERFORM 2020-PROCESS-DEPT-SCLS                               05000092
               UNTIL PV-SUB1 > PV-MAX-DATE-SUB.                         05010092
                                                                        05020092
           PERFORM 7800-COMMIT-PROCESSING.                              05030092
                                                                        05040092
           PERFORM 4000-READ-EXTR-FILE.                                 05050092
                                                                        05050199
           IF PS-EOF-EXTR-FILE                                          05050299
               DISPLAY 'END OF INPUT FILE'                              05050399
           ELSE                                                         05050599
PK0400         IF EXTR-DEPT-NBR NOT = MISC-HOLD-DEPT                    05050699
PK0400             MOVE EXTR-DEPT-NBR TO MISC-HOLD-DEPT                 05050799
PK0400             DISPLAY 'PROCESSING DEPT: ' MISC-HOLD-DEPT           05050899
PK0400         END-IF                                                   05050999
PK0400         IF PS-COMMIT-TAKEN                                       05051099
PK0400             MOVE 'N' TO PS-COMMIT-TAKEN-SW                       05051199
PK0400         END-IF                                                   05051299
PK0400     END-IF.                                                      05052099
                                                                        05160092
      ******************************************************************05190092
      * PROCESS ALL STORES FOR A SUB-CLASS                              05200092
      ******************************************************************05210092
       2020-PROCESS-DEPT-SCLS.                                          05220092
                                                                        05230092
           PERFORM 2025-PROCESS-DEPT-SCLS.                              05240099
PK0400     ADD 1 TO PV-SUB1.                                            05310099
                                                                        05400092
      ******************************************************************05410092
      * PROCESS STORE FOR A SUB-CLASS                                   05420092
      ******************************************************************05430092
       2025-PROCESS-DEPT-SCLS.                                          05440092
           INITIALIZE  PROGRAM-VARIABLES.                               05450099
           MOVE 'N' TO PS-CURR-MONTH-SW                                 05510092
                       PS-PREV-MONTH-ROW-SW                             05520092
                       PS-DEPT-SHRINK-PCT-SW                            05530092
                       PS-CARRY-FORWARD-SW.                             05540092
                                                                        05550092
PK0400     MOVE EXTR-DEPT-NBR     TO PV-HOLD-DEPT-NBR.                  05560099
PK0400     MOVE EXTR-SUB-CL-NBR   TO PV-HOLD-SUB-CL-NBR.                05570099
PK0400     MOVE EXTR-STR-NBR      TO PV-HOLD-STR-NBR.                   05580099
                                                                        05590092
           PERFORM 2800-FORMAT-DATES.                                   05630099
      *                                                                 05660092
      * DETERMINE IF CURRENT DOLLARS AND/OR PRIOR INVENTORY DOLLARS     05661099
           PERFORM 7350-SELECT-PREV-MONTH-END-INV.                      05670092
           PERFORM 7300-SELECT-CURRENT-DOLLARS.                         05680092
      *                                                                 05690092
      ******************************************************************05700092
      *****                   CARRY FORWARD LOGIC                  *****05710092
      ******************************************************************05720092
      *    CURRENT $ FOUND FOR MONTH       - RECALC ROW                *05730092
      * NO CURRENT $ FOUND/   PREV $ FOUND - CARRY FORWARD INVENTORY $ *05740092
      * NO CURRENT $ FOUND/NO PREV $ FOUND - BYPASS RECALC PROCESSING  *05750092
      *                                      PROCESS ANOTHER ROW       *05760092
      ******************************************************************05770092
           IF PS-CURR-MONTH-ROW-NOT-FOUND                               05780092
               IF PS-PREV-MONTH-ROW-NOT-FOUND                           05790092
                   CONTINUE                                             05800092
               ELSE                                                     05810092
                   IF PS-PFM-INV-DOLLARS-FOUND                          05820092
                       PERFORM 7950-INSERT-CARRY-FWD-ROW                05830092
                       PERFORM 7300-SELECT-CURRENT-DOLLARS              05840092
                       PERFORM 2035-RECALC-PROCESSING                   05850092
                   END-IF                                               05860092
               END-IF                                                   05870092
           ELSE                                                         05880092
               PERFORM 2035-RECALC-PROCESSING                           05890092
           END-IF.                                                      05900092
                                                                        05930092
      ******************************************************************05940092
      *                                                                *05950092
      *              ********* RECALC PROCESSING **********            *05960092
      *                                                                *05970092
      ******************************************************************05980092
       2035-RECALC-PROCESSING.                                          05990092
      *                                                                 06000092
      *                                                                 06010092
           PERFORM 7200-SELECT-SHRINK-AMT.                              06020099
      *                                                                 06030099
      * CALCULATE THE ENDING INVENTORY OF CURRENT MONTH BASED ON THE    06040099
      *       ENDING INVENTORY FROM THE PRIOR MONTH                     06050099
           COMPUTE PV-END-INV-RTL-AMT = (  PV-PREV-MONTH-END-INV-RTL    06060099
                                         + MNSCLS-RCPT-RTL-AMT          06070099
                                         + MNSCLS-PADJ-RTL-AMT          06080099
                                         + MNSCLS-MKUP-RTL-AMT          06090099
                                         - MNSCLS-SLS-RTL-AMT           06100099
                                         + MNSCLS-XFER-IN-RTL-AMT       06110099
                                         - MNSCLS-XFER-OUT-RTL-AMT      06120099
                                         - MNSCLS-MKDN-RTL-AMT          06130099
                                         - MNSCLS-INV-ADJ-RTL-AMT       06140099
                                         - PV-SHNK-RTL-AMT).            06150099
      *                                                                 06160092
           PERFORM 7400-PREV-SEASON-END-INV.                            06170092
           PERFORM 2200-CALC-PREV-SEASN-END-PURCH.                      06180092
      *                                                                 06190092
PK0400     SET PS-NOT-END-OF-CURR-SEASON-MTHS TO TRUE.                  06190199
PK0400     PERFORM 2250-CALC-SEASON-TO-DATE-PURCH                       06190299
               VARYING PV-SEA-SUB FROM 1 BY 1                           06190399
               UNTIL PS-END-OF-CURR-SEASON-MTHS.                        06191099
      *                                                                 06270092
           PERFORM 2300-CALC-CUMM-TO-DATE-PURCH.                        06280092
      * SALES COST AMOUNT                                               06290092
W31475*    PERFORM 7475-CURR-SEASON-COST.                               06300092
 --->      PERFORM 2600-CALC-SALES-COST-AMT.                            06310092
           PERFORM 7500-UPDATE-PROCESSING.                              06320092
      *                                                                 06350092
      *                                                                 06360092
      ******************************************************************06370092
      * CALCULATE CUMULATIVE MARKUP PERCENTAGE FOR UPDATE               06380092
      ******************************************************************06390092
       2200-CALC-PREV-SEASN-END-PURCH.                                  06400092
      *                                                                 06410092
           IF PS-NO-PREV-SEASONAL-INV                                   06420092
                MOVE 0 TO PV-PREV-SEASON-PURCH-RETAIL                   06430092
                MOVE 0 TO PV-PREV-SEASON-PURCH-COST                     06440092
           ELSE                                                         06450092
                MOVE MNSCLS-END-INV-RTL-AMT TO                          06460092
                                         PV-PREV-SEASON-PURCH-RETAIL    06470092
                MOVE MNSCLS-END-INV-COST-AMT TO                         06480092
                                         PV-PREV-SEASON-PURCH-COST      06490092
           END-IF.                                                      06500092
      *                                                                 06510092
      *                                                                 06520092
      ******************************************************************06530092
      * ACCUMULATE CURRENT SEASON RETAIL/COST AMTS FROM SEASON BEGINNING06540092
      ******************************************************************06550092
       2250-CALC-SEASON-TO-DATE-PURCH.                                  06560092
      *                                                                 06570092
           PERFORM 7450-GET-SEASONAL-MTH.                               06570199
                                                                        06570299
           IF PS-SEASON-MTH-FOUND                                       06570399
               COMPUTE PV-XFER-NET =                                    06570499
                  (MNSCLS-XFER-IN-RTL-AMT -                             06570599
                   MNSCLS-XFER-OUT-RTL-AMT)                             06570699
               ADD PV-XFER-NET             TO  PV-MNSCLS-XFER-NET-AMT   06570799
               ADD MNSCLS-RCPT-RTL-AMT     TO  PV-MNSCLS-RCPT-RTL-AMT   06570899
               ADD MNSCLS-RCPT-NET-COST-AMT                             06570999
                   TO PV-MNSCLS-RCPT-NET-COST-AMT                       06571099
               ADD MNSCLS-PADJ-RTL-AMT     TO  PV-MNSCLS-PADJ-RTL-AMT   06571199
               ADD MNSCLS-PADJ-NET-COST-AMT                             06571299
                   TO PV-MNSCLS-PADJ-NET-COST-AMT                       06571399
               ADD MNSCLS-MKUP-RTL-AMT     TO  PV-MNSCLS-MKUP-RTL-AMT   06571499
           END-IF.                                                      06572099
      *                                                                 06690092
      *                                                                 06700092
      ******************************************************************06710092
      * DETERMINE CUMULATIVE RETAIL AND COST                            06720092
      * ADD PRIOR SEASON END INV AMTS TO CURRENT SEASON-TO-DATE AMTS    06730092
      ******************************************************************06740092
       2300-CALC-CUMM-TO-DATE-PURCH.                                    06750092
      *                                                                 06760092
           COMPUTE PV-CUM-TO-DATE-PURCH-COST   =                        06770092
                                     (  PV-PREV-SEASON-PURCH-COST       06780092
                                      + PV-MNSCLS-RCPT-NET-COST-AMT     06790092
                                      + PV-MNSCLS-PADJ-NET-COST-AMT).   06800092
                                                                        06810092
           COMPUTE PV-CUM-TO-DATE-PURCH-RETAIL =                        06820092
                                      (  PV-PREV-SEASON-PURCH-RETAIL    06830092
**NEW*                                 + PV-MNSCLS-XFER-NET-AMT         06840092
                                       + PV-MNSCLS-RCPT-RTL-AMT         06850092
                                       + PV-MNSCLS-PADJ-RTL-AMT         06860092
                                       + PV-MNSCLS-MKUP-RTL-AMT).       06870092
      *                                                                 06880092
      * FINAL CALCULATIONS OF VALUES TO UPDATE TMNSCLS - CUM_MKUP_PCT   06890092
      *                                                                 06900092
           COMPUTE PV-CUM-SEASON-MKUP = (  PV-CUM-TO-DATE-PURCH-RETAIL  06910092
                                         - PV-CUM-TO-DATE-PURCH-COST  ).06920092
                                                                        06930092
      * CUM SEASON RETAIL CANNOT EQUAL ZERO IN DIVISOR ('ON SIZE ERROR')06940092
**NEW**    IF PV-CUM-TO-DATE-PURCH-RETAIL = 0                           06950092
           IF (PV-CUM-TO-DATE-PURCH-RETAIL) = 0                         06960092
                MOVE 100 TO PV-CUM-MKUP-PCT                             06970092
           ELSE                                                         06980092
4095            COMPUTE PV-CUM-MKUP-PCT-MAX ROUNDED =                   06990099
----->*         COMPUTE PV-CUM-MKUP-PCT ROUNDED =                       07000099
               (PV-CUM-SEASON-MKUP * 100) / PV-CUM-TO-DATE-PURCH-RETAIL 07010099
4095            IF ( PV-CUM-MKUP-PCT-MAX > 9999.9999999 OR              07020099
4095                 PV-CUM-MKUP-PCT-MAX < -9999.9999999  )             07030099
4095                MOVE PV-HOLD-DEPT-NBR     TO DL-DEPT-NBR            07040099
4095                MOVE PV-HOLD-SUB-CL-NBR   TO DL-SCL-NBR             07050099
4095                MOVE PV-HOLD-STR-NBR      TO PV-HOLD-STR-NBR-RED    07060099
4095                MOVE PV-HOLD-STR-NBR-RED  TO DL-STORE-NBR           07070099
4095                MOVE PV-CUM-MKUP-PCT-MAX  TO DL-CUM-MKUP-PCT        07080099
4095                                             DL-CUM-MKUP-USED       07090099
4095                PERFORM 5000-WRITE-OUTPUT-RECORD                    07100099
4095                ADD +1 TO PA-CUM-MKUP-MAX-LIMIT                     07110099
4095            END-IF                                                  07120099
4095            MOVE PV-CUM-MKUP-PCT-MAX TO PV-CUM-MKUP-PCT             07130099
           END-IF.                                                      07140092
      *                                                                 07150092
      * FINAL CALC OF VALUES TO UPDATE TMNSCLS - END_INV_COST_AMT       07160092
      *                                                                 07170092
           COMPUTE PV-END-INV-COST-AMT ROUNDED =                        07180092
                        ( (100 - PV-CUM-MKUP-PCT)  *                    07190092
                               ( PV-END-INV-RTL-AMT) ) / 100.           07200092
      *                                                                 07210092
      *                                                                 07220092
      ******************************************************************07230092
      * FINAL CALC OF VALUES TO UPDATE TMNSCLS - SALES_COST_AMT *       07240092
      ******************************************************************07250092
       2600-CALC-SALES-COST-AMT.                                        07260092
      *                                                                 07270092
           IF PS-NO-CURRENT-PURCH-COSTS                                 07280092
               MOVE ZEROES            TO PV-CURR-MONTH-RCPT-NET-COST    07290092
                                         PV-CURR-MONTH-PADJ-NET-COST    07300092
           END-IF.                                                      07310092
      *                                                                 07320092
           COMPUTE PV-SLS-COST-AMT =                                    07330092
                 (  PV-PREV-MONTH-END-INV-COST                          07340092
                  + PV-CURR-MONTH-RCPT-NET-COST                         07350092
                  + PV-CURR-MONTH-PADJ-NET-COST ) - PV-END-INV-COST-AMT.07360092
      *                                                                 07370092
      * FINAL CALCULATIONS OF VALUES TO UPDATE TMNSCLS - GRO_MRGN_AMT  *07380092
      *                                                                 07390092
           IF MNSCLS-SLS-RTL-AMT = 0                                    07400092
               MOVE 0 TO PV-GRO-MRGN-AMT                                07410092
           ELSE                                                         07420092
               COMPUTE PV-GRO-MRGN-AMT =                                07430092
                       MNSCLS-SLS-RTL-AMT - PV-SLS-COST-AMT             07440092
           END-IF.                                                      07450092
      *                                                                 07460092
      *                                                                 07470092
      ******************************************************************07480092
      * MOVE SUBSCRIPTED VARIABLE TO FORMAT USABLE BY DB2               07490092
      ******************************************************************07500092
       2800-FORMAT-DATES.                                               07510092
           MOVE DT-SEA-BEG-DTE(PV-SUB1)    TO PV-HOLD-SEA-BEG-DTE.      07510199
PK0400     MOVE DT-SEA-BEG-MN-NBR(PV-SUB1) TO PV-HOLD-SEA-BEG-MN-NBR.   07510299
           MOVE DT-OPN-MN-END-DTE(PV-SUB1) TO PV-HOLD-OPN-MN-END-DTE.   07510399
PK0400     MOVE DT-OPN-MN-NBR(PV-SUB1)     TO PV-HOLD-OPN-MN-NBR.       07510499
           MOVE DT-PFM-END-DTE(PV-SUB1)    TO PV-HOLD-PFM-END-DTE.      07510599
PK0400     MOVE DT-PFM-MN-NBR(PV-SUB1)     TO PV-HOLD-PFM-MN-NBR.       07511099
           MOVE DT-CAL-MN-NBR(PV-SUB1)     TO PV-HOLD-CAL-MN-NBR.       07550099
           MOVE DT-FSCL-MN-NBR(PV-SUB1)    TO PV-HOLD-FSCL-MN-NBR.      07560099
           MOVE DT-PFM-MN-NBR(PV-SUB1) TO                               07570099
                PV-HOLD-PFM-MN-NBR.                                     07580099
           MOVE DT-PREV-SEA-END-DTE(PV-SUB1) TO                         07590092
                PV-HOLD-PREV-SEA-END-DTE.                               07600092
           MOVE DT-PREV-SEA-MN-NBR(PV-SUB1) TO                          07610099
                PV-HOLD-PREV-SEA-MN-NBR.                                07620099
      *                                                                 07630092
       4000-READ-EXTR-FILE.                                             07640092
                                                                        07650092
           READ MNSCLS-EXTR-FILE INTO EXTR-RECORD                       07660092
               AT END                                                   07670092
                  SET PS-EOF-EXTR-FILE TO TRUE.                         07680092
                                                                        07690092
           IF PS-EOF-EXTR-FILE                                          07700092
               CONTINUE                                                 07710092
           ELSE                                                         07720092
               ADD 1 TO PA-EXTRACT-RECS-READ                            07730092
           END-IF.                                                      07740092
         EJECT                                                          07750092
      *                                                                 07760092
      *                                                                 07770099
4095  ******************************************************************07780099
4095  * WRITE CONTROL CARD FOR OUTPUT MESSAGE TO BP (CUM MKUP PCT VAR)  07790099
4095  ******************************************************************07800099
4095   5000-WRITE-OUTPUT-RECORD.                                        07810099
4095  *                                                                 07820099
4095       WRITE OUTPUT-RECORD FROM OUTPUT-DETAIL-LINE.                 07830099
4095  *                                                                 07840099
      ******************************************************************07850092
      * READ THE DB2 CONTROL TABLE CONTAINING OPEN FISCAL DATE INFO     07860092
      ******************************************************************07870092
       7000-READ-CONTROL-TABLE.                                         07880092
      *                                                                 07890092
            EXEC SQL                                                    07900092
                SELECT OPN_FSCL_MN_DTE                                  07910092
                      ,OPN_FSCL_MN_NBR                                  07920092
                      ,MXPSTG_FSCL_MN_DTE                               07930092
                      ,MXPSTG_FSCL_MN_NBR                               07940092
                 INTO  :MLCNTL-OPN-FSCL-MN-DTE                          07950092
                      ,:MLCNTL-OPN-FSCL-MN-NBR                          07960092
                      ,:MLCNTL-MXPSTG-FSCL-MN-DTE                       07970092
                      ,:MLCNTL-MXPSTG-FSCL-MN-NBR                       07980092
                 FROM  TMLCNTL                                          07990092
W26603          WITH UR                                                 07991099
            END-EXEC.                                                   08000092
      *                                                                 08010092
           EVALUATE SQLCODE                                             08020092
               WHEN 0                                                   08030092
                   MOVE MLCNTL-OPN-FSCL-MN-DTE TO CR-FISCAL-MN-END-DTE  08040092
                   MOVE MLCNTL-OPN-FSCL-MN-NBR TO CR-FISCAL-MN-NBR      08050092
                   MOVE MLCNTL-MXPSTG-FSCL-MN-DTE                       08060092
                                               TO CR-FISCAL-MX-END-DTE  08070092
                   MOVE MLCNTL-MXPSTG-FSCL-MN-NBR                       08080092
                                               TO CR-FISCAL-MX-NBR      08090092
                   DISPLAY 'CONTROL DATES (TMLCNTL)'                    08100092
                   DISPLAY 'FISCAL OPEN END DATE  : ',                  08110092
                                                 MLCNTL-OPN-FSCL-MN-DTE 08120092
                   DISPLAY '       OPEN MONTH NBR : ',                  08130092
                                                 MLCNTL-OPN-FSCL-MN-NBR 08140092
                   DISPLAY 'MAXPSTG  MONTH END DTE: ',                  08150092
                                              MLCNTL-MXPSTG-FSCL-MN-DTE 08160092
                   DISPLAY '     MAX MONTH NUMBER : ',                  08170092
                                              MLCNTL-MXPSTG-FSCL-MN-NBR 08180092
               WHEN OTHER                                               08190092
                  MOVE '7000-READ-CONTROL-TABLE' TO PV-PARAGRAPH-NAME   08200092
                  MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED              08210092
                  PERFORM 9999-SQL-ABEND                                08220092
           END-EVALUATE.                                                08230092
      *                                                                 08240092
      *                                                                 08250092
      ******************************************************************08260092
      * LOAD ALL DATES TO BE PROCESSED TO INTERNAL TABLE                08270092
      ******************************************************************08280092
       7025-PROCESS-CNTL-TBL.                                           08290092
      *                                                                 08300092
            PERFORM 7050-OPEN-DATE-CURSOR.                              08310092
            PERFORM 7075-PROCESS-DATE-CURSOR                            08320092
                UNTIL PS-DATE-TABLE-LOADED.                             08330092
      *                                                                 08340092
      *                                                                 08350092
      ***************************************************************** 08360092
      * OPEN CURSOR FOR UPDATE AT INITIALIZATION & AFTER EACH COMMIT    08370092
      ***************************************************************** 08380092
       7050-OPEN-DATE-CURSOR.                                           08390092
      *                                                                 08400092
           EXEC SQL                                                     08410092
               OPEN FISCAL_DATE_CSR                                     08420092
           END-EXEC                                                     08430092
                                                                        08440092
           EVALUATE SQLCODE                                             08450092
               WHEN 0                                                   08460092
                    CONTINUE                                            08470092
               WHEN OTHER                                               08480092
                  MOVE '7050-OPEN-DATE-CSR' TO PV-PARAGRAPH-NAME        08490092
                  MOVE PE-MSG-03 TO PV-OPERATION-ATTEMPTED              08500092
                  PERFORM 9999-SQL-ABEND                                08510092
           END-EVALUATE.                                                08520092
      *                                                                 08530092
      *                                                                 08540092
      ***************************************************************** 08550092
      * INCREMENT SUBSCRIPT AND CONTINUE LOADING INTERNAL TABLE         08560092
      ***************************************************************** 08570092
       7075-PROCESS-DATE-CURSOR.                                        08580092
      *                                                                 08590092
           EXEC SQL                                                     08600092
                  FETCH FISCAL_DATE_CSR                                 08610092
                   INTO :PV-HOLD-DATE                                   08620092
           END-EXEC                                                     08630092
                                                                        08640092
           EVALUATE SQLCODE                                             08650092
               WHEN +0                                                  08660092
                   ADD 1 TO PV-SUB1                                     08670092
                   IF PV-SUB1 > PC-MAX-DATE-TABLE                       08680092
                       MOVE '7075-PROCESS-DATE-CSR' TO PV-PARAGRAPH-NAME08690092
                       MOVE PE-MSG-15 TO PV-OPERATION-ATTEMPTED         08700092
                       PERFORM 9999-SQL-ABEND                           08710092
                   END-IF                                               08720092
                   DISPLAY 'HOLD-DTE: ', PV-HOLD-DATE                   08730092
                   DISPLAY 'PV-SUB1 : ', PV-SUB1                        08740092
                   PERFORM 7100-SELECT-DB2-DATES                        08750092
                   PERFORM 7125-LOAD-DATE-TABLE                         08760092
               WHEN +100                                                08850092
                   DISPLAY 'NBR OF MONTHS PROCESSED THIS RUN: ', PV-SUB108860092
                   IF PV-SUB1 > 1                                       08870092
                       DISPLAY '   ', MLCNTL-OPN-FSCL-MN-DTE, ' THRU ' ,08880092
                               MLCNTL-MXPSTG-FSCL-MN-DTE                08890092
                   END-IF                                               08900092
                   MOVE PV-SUB1 TO PV-MAX-DATE-SUB                      08910092
                   MOVE ZEROES  TO PV-SUB1                              08920092
                   SET PS-DATE-TABLE-LOADED TO TRUE                     08930092
               WHEN OTHER                                               08940092
                   MOVE '7075-PROCESS-DATE-CSR' TO PV-PARAGRAPH-NAME    08950092
                   MOVE PE-MSG-04 TO PV-OPERATION-ATTEMPTED             08960092
                   PERFORM 9999-SQL-ABEND                               08970092
           END-EVALUATE.                                                08980092
      *                                                                 08990092
      *                                                                 09000092
      ******************************************************************09010092
      *                                                                 09020092
      ******************************************************************09030092
       7100-SELECT-DB2-DATES.                                           09040092
      *                                                                 09050092
           EXEC SQL                                                     09060092
               SELECT  CAL_MN_NBR                                       09070092
                      ,PFM_END_DTE                                      09080092
                      ,SEA_BEG_DTE                                      09090092
                      ,PREV_SEA_END_DTE                                 09100092
                      ,FSCL_MN_NBR                                      09110092
                INTO  :DTATTR-CAL-MN-NBR                                09120092
                     ,:DTATTR-PFM-END-DTE                               09130092
                     ,:DTATTR-SEA-BEG-DTE                               09140092
                     ,:DTATTR-PREV-SEA-END-DTE                          09150092
                     ,:DTATTR-FSCL-MN-NBR                               09160092
                FROM TDTATTR                                            09170092
                WHERE KY_DTE  = :PV-HOLD-DATE                           09180092
W26603          WITH UR                                                 09181099
           END-EXEC.                                                    09190092
      *                                                                 09200092
           EVALUATE SQLCODE                                             09210092
               WHEN 0                                                   09220092
                   DISPLAY 'DTATTR-CAL-MN-NBR ', DTATTR-CAL-MN-NBR      09230092
                   DISPLAY 'DTATTR-PFM-END-DTE ', DTATTR-PFM-END-DTE    09240092
                   DISPLAY 'DTATTR-SEA-BEG-DTE ', DTATTR-SEA-BEG-DTE    09250092
                   DISPLAY 'DTATTR-PREV-SEA-END-DTE ',                  09260092
                                                 DTATTR-PREV-SEA-END-DTE09270092
                   DISPLAY 'DTATTR-FSCL-MN-NBR ', DTATTR-FSCL-MN-NBR    09280092
               WHEN +100                                                09290092
                   DISPLAY 'DATE NOT FOUND ON DTATTR ' PV-HOLD-DATE     09300092
               WHEN OTHER                                               09310092
                  MOVE '7100-SELECT-DB2-DATES' TO PV-PARAGRAPH-NAME     09320092
                  MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED              09330092
                  PERFORM 9999-SQL-ABEND                                09340092
           END-EVALUATE.                                                09350092
      *                                                                 09350199
      *                                                                 09350299
      ******************************************************************09350399
      *                                                                 09350499
      ******************************************************************09350599
       7110-SELECT-SEA-MN-NBR.                                          09350799
      *                                                                 09350899
           EXEC SQL                                                     09350999
               SELECT  FSCL_MN_NBR                                      09351099
                INTO  :PV-TEMP-SEA-BEG-MN-NBR                           09351199
                FROM TDTATTR                                            09351299
                WHERE KY_DTE  = :PV-TEMP-SEA-BEG-DTE                    09351399
W26603         WITH UR                                                  09351499
           END-EXEC.                                                    09351599
      *                                                                 09351699
           EVALUATE SQLCODE                                             09351799
               WHEN 0                                                   09351899
                   CONTINUE                                             09351999
               WHEN OTHER                                               09352099
                  DISPLAY '****************************************'    09352199
                  DISPLAY 'SEASON BEGIN DATE:  ' PV-TEMP-SEA-BEG-DTE    09352299
                  DISPLAY ' '                                           09352399
                  MOVE '7110-SELECT-SEA-MN-NBR' TO PV-PARAGRAPH-NAME    09352499
                  MOVE PE-MSG-16 TO PV-OPERATION-ATTEMPTED              09352599
                  PERFORM 9999-SQL-ABEND                                09352699
           END-EVALUATE.                                                09352799
      *                                                                 09352899
      *                                                                 09352999
      ******************************************************************09353099
      *                                                                 09353199
      ******************************************************************09353299
       7112-SELECT-PREV-MN-NBR.                                         09353499
      *                                                                 09353599
           EXEC SQL                                                     09353699
               SELECT  FSCL_MN_NBR                                      09353799
                INTO  :PV-TEMP-PFM-MN-NBR                               09353899
                FROM TDTATTR                                            09353999
                WHERE KY_DTE  = :PV-TEMP-PFM-END-DTE                    09354099
W26603         WITH UR                                                  09354199
           END-EXEC.                                                    09354299
      *                                                                 09354399
           EVALUATE SQLCODE                                             09354499
               WHEN 0                                                   09354599
                   CONTINUE                                             09354699
               WHEN OTHER                                               09354799
                  DISPLAY '******************************************'  09354899
                  DISPLAY 'PREV FSCL MN DTE :  ' PV-TEMP-PFM-END-DTE    09354999
                  DISPLAY ' '                                           09355099
                  MOVE '7112-SELECT-PREV-MN-NBR' TO PV-PARAGRAPH-NAME   09355199
                  MOVE PE-MSG-17 TO PV-OPERATION-ATTEMPTED              09355299
                  PERFORM 9999-SQL-ABEND                                09355399
           END-EVALUATE.                                                09355499
      *                                                                 09355599
      *                                                                 09355699
      ******************************************************************09355799
      *                                                                 09355899
      ******************************************************************09355999
       7114-SELECT-PREV-SEA-MN-NBR.                                     09356099
      *                                                                 09356199
           EXEC SQL                                                     09356299
               SELECT  FSCL_MN_NBR                                      09356399
                INTO  :PV-TEMP-PREV-SEA-MN-NBR                          09356499
                FROM TDTATTR                                            09356599
                WHERE KY_DTE  = :PV-TEMP-PREV-SEA-END-DTE               09356699
W26603         WITH UR                                                  09356799
           END-EXEC.                                                    09356899
      *                                                                 09356999
           EVALUATE SQLCODE                                             09357099
               WHEN 0                                                   09357199
                   CONTINUE                                             09357299
               WHEN OTHER                                               09357399
                  DISPLAY '********************************************'09357499
                  DISPLAY 'PREV SEA END DTE:  ' PV-TEMP-PREV-SEA-END-DTE09357599
                  DISPLAY ' '                                           09357699
                  MOVE '7114-SELECT-PREV-SEA-MN-NBR'                    09357799
                        TO PV-PARAGRAPH-NAME                            09357899
                  MOVE PE-MSG-18 TO PV-OPERATION-ATTEMPTED              09357999
                  PERFORM 9999-SQL-ABEND                                09358099
           END-EVALUATE.                                                09359099
      *                                                                 09600092
      *                                                                 09610092
      ***************************************************************** 09620092
      * POPULATE INTERNAL TABLE WITH DATES RELATED TO MONTH-END-DATE    09630092
      ***************************************************************** 09640092
       7125-LOAD-DATE-TABLE.                                            09650092
           MOVE PV-HOLD-DATE            TO DT-OPN-MN-END-DTE(PV-SUB1).  09660092
           MOVE DTATTR-FSCL-MN-NBR      TO DT-OPN-MN-NBR(PV-SUB1).      09661099
           MOVE DTATTR-CAL-MN-NBR       TO DT-CAL-MN-NBR(PV-SUB1).      09670092
           MOVE DTATTR-PFM-END-DTE      TO DT-PFM-END-DTE(PV-SUB1).     09680099
           MOVE DTATTR-SEA-BEG-DTE      TO DT-SEA-BEG-DTE(PV-SUB1).     09700092
           MOVE DTATTR-PREV-SEA-END-DTE TO DT-PREV-SEA-END-DTE(PV-SUB1).09710099
           MOVE DTATTR-FSCL-MN-NBR      TO DT-FSCL-MN-NBR(PV-SUB1).     09730092
                                                                        09730199
           MOVE DTATTR-SEA-BEG-DTE      TO PV-TEMP-SEA-BEG-DTE.         09730299
PK0400     PERFORM 7110-SELECT-SEA-MN-NBR.                              09730399
           MOVE PV-TEMP-SEA-BEG-MN-NBR TO DT-SEA-BEG-MN-NBR(PV-SUB1)    09730499
                                                                        09730599
           MOVE DTATTR-PFM-END-DTE      TO PV-TEMP-PFM-END-DTE.         09730699
PK0400     PERFORM 7112-SELECT-PREV-MN-NBR.                             09730799
           MOVE PV-TEMP-PFM-MN-NBR      TO DT-PFM-MN-NBR(PV-SUB1).      09730899
                                                                        09730999
           MOVE DTATTR-PREV-SEA-END-DTE TO PV-TEMP-PREV-SEA-END-DTE.    09731099
PK0400     PERFORM 7114-SELECT-PREV-SEA-MN-NBR.                         09731199
           MOVE PV-TEMP-PREV-SEA-MN-NBR TO DT-PREV-SEA-MN-NBR(PV-SUB1). 09731299
                                                                        09731399
PK0400     PERFORM 7150-GET-SEASON-DATE-RANGE.                          09731499
                                                                        09732199
      *    ********************                                         09732299
      *    **  DISPLAY FIELDS                                           09732399
      *    ********************                                         09732499
                                                                        09732599
           DISPLAY '--------------------------------------------'       09732699
           DISPLAY '    OPN MN END DTE    ' DT-OPN-MN-END-DTE(PV-SUB1). 09732799
PK0400     DISPLAY '    OPN MN END NBR    ' DT-OPN-MN-NBR(PV-SUB1).     09732899
                                                                        09732999
           DISPLAY '    CAL MN NBR        ' DT-CAL-MN-NBR(PV-SUB1).     09733099
                                                                        09733199
           DISPLAY '    PREV FSCL END DTE ' DT-PFM-END-DTE(PV-SUB1)     09733299
           DISPLAY '    PREV FSCL MN NBR  ' DT-PFM-MN-NBR(PV-SUB1).     09733399
                                                                        09733499
           DISPLAY '    SEA BEG DTE       ' DT-SEA-BEG-DTE(PV-SUB1).    09733599
           DISPLAY '    SEA BEG MN NBR    ' DT-SEA-BEG-MN-NBR(PV-SUB1)  09733699
                                                                        09733799
           DISPLAY '    PREV SEA END DTE  '                             09733899
                   DT-PREV-SEA-END-DTE(PV-SUB1).                        09733999
           DISPLAY '    PREV SEA MN NBR   '                             09734099
                   DT-PREV-SEA-MN-NBR(PV-SUB1).                         09734199
                                                                        09734299
           DISPLAY '    FSCL MN NBR       ' DT-FSCL-MN-NBR(PV-SUB1).    09734399
                                                                        09734699
           IF PS-SEASON-DATE-TABLE-LOADED                               09734799
               SUBTRACT 2 FROM PV-SEA-SUB                               09734899
           ELSE                                                         09734999
               SUBTRACT 1 FROM PV-SEA-SUB                               09735099
           END-IF.                                                      09735199
                                                                        09735299
           DISPLAY ' '                                                  09735399
           DISPLAY '    NBR OF SEA MTHS PROCESSED FOR THIS DATE: ',     09735499
                            PV-SEA-SUB                                  09735599
           IF PV-SEA-SUB > 0                                            09735699
               DISPLAY '        ' PV-TEMP-SEA-BEG-DTE ' THRU ' ,        09735799
                             PV-HOLD-DATE                               09735899
               DISPLAY ' '                                              09735999
               DISPLAY '       1ST MTH DTE '  DT-SEA-MN1-DTE(PV-SUB1)   09736099
                       '   MTH NBR '      DT-SEA-MN1-NBR(PV-SUB1)       09736199
               DISPLAY '       2ND MTH DTE '  DT-SEA-MN2-DTE(PV-SUB1)   09736299
                       '   MTH NBR '      DT-SEA-MN2-NBR(PV-SUB1)       09736399
               DISPLAY '       3RD MTH DTE '  DT-SEA-MN3-DTE(PV-SUB1)   09736499
                       '   MTH NBR '      DT-SEA-MN3-NBR(PV-SUB1)       09736599
               DISPLAY '       4TH MTH DTE '  DT-SEA-MN4-DTE(PV-SUB1)   09736699
                       '   MTH NBR '      DT-SEA-MN4-NBR(PV-SUB1)       09736799
               DISPLAY '       5TH MTH DTE '  DT-SEA-MN5-DTE(PV-SUB1)   09736899
                       '   MTH NBR '      DT-SEA-MN5-NBR(PV-SUB1)       09736999
               DISPLAY '       6TH MTH DTE '  DT-SEA-MN6-DTE(PV-SUB1)   09737099
                       '   MTH NBR '      DT-SEA-MN6-NBR(PV-SUB1)       09737199
           ELSE                                                         09737299
               DISPLAY ' ************************'                      09737399
               DISPLAY '   NO SEASONAL DATA EXISTS '                    09737499
               MOVE '7125-LOAD-DATE-TABLE' TO PV-PARAGRAPH-NAME         09737599
               PERFORM 9999-ABEND                                       09737699
           END-IF.                                                      09737799
                                                                        09738099
      *                                                                 09738199
      *                                                                 09738299
      ***************************************************************** 09738399
      *                                                                 09738499
      ***************************************************************** 09738599
                                                                        09738699
       7150-GET-SEASON-DATE-RANGE.                                      09738799
                                                                        09738899
           SET PS-SEASON-DATE-TAB-NOT-LOADED TO TRUE.                   09738999
           PERFORM 7152-OPN-SEA-DTE-RANGE-CUR.                          09739099
           PERFORM 7154-PROCESS-SEA-DATE-CURSOR                         09739199
               VARYING PV-SEA-SUB FROM 1 BY 1 UNTIL PV-SEA-SUB > 6      09739299
               OR                                                       09739399
               PS-SEASON-DATE-TABLE-LOADED.                             09739499
           PERFORM 7158-CLOSE-SEA-DTE-RANGE-CUR.                        09739599
                                                                        09739699
      *                                                                 09739799
      ***************************************************************** 09739899
      * OPEN CURSOR FOR UPDATE AT INITIALIZATION & AFTER EACH COMMIT    09739999
      ***************************************************************** 09740099
       7152-OPN-SEA-DTE-RANGE-CUR.                                      09740199
      *                                                                 09740299
           EXEC SQL                                                     09740399
               OPEN SEASON_DATE_CSR                                     09740499
           END-EXEC                                                     09740599
                                                                        09740699
           EVALUATE SQLCODE                                             09740799
               WHEN 0                                                   09740899
                    CONTINUE                                            09740999
               WHEN OTHER                                               09741099
                  MOVE '7152-OPEN-SEASON-CSR' TO PV-PARAGRAPH-NAME      09741199
                  MOVE PE-MSG-20 TO PV-OPERATION-ATTEMPTED              09741299
                  PERFORM 9999-SQL-ABEND                                09741399
           END-EVALUATE.                                                09741499
      *                                                                 09741599
      *                                                                 09741699
      ***************************************************************** 09741799
      *                                                                 09741899
      ***************************************************************** 09741999
       7154-PROCESS-SEA-DATE-CURSOR.                                    09742099
      *                                                                 09742199
           EXEC SQL                                                     09742299
                  FETCH SEASON_DATE_CSR                                 09742399
                   INTO :PV-SEASON-MN-DTE                               09742499
                       ,:PV-SEASON-MN-NBR                               09742599
           END-EXEC                                                     09742699
                                                                        09742799
           EVALUATE SQLCODE                                             09742899
               WHEN +0                                                  09742999
                   PERFORM 7156-LOAD-SEA-DATE-RANGE-TABLE               09743099
               WHEN +100                                                09743199
                   SET PS-SEASON-DATE-TABLE-LOADED TO TRUE              09743299
               WHEN OTHER                                               09743399
                   MOVE '7154-PROCESS-SEA-DATE-CSR'                     09743499
                        TO PV-PARAGRAPH-NAME                            09743599
                   MOVE PE-MSG-21 TO PV-OPERATION-ATTEMPTED             09743699
                   PERFORM 9999-SQL-ABEND                               09743799
           END-EVALUATE.                                                09743899
      *                                                                 09743999
      *                                                                 09744099
      ***************************************************************** 09744199
      *                                                                 09744299
      ***************************************************************** 09744399
       7156-LOAD-SEA-DATE-RANGE-TABLE.                                  09744499
                                                                        09744599
           EVALUATE PV-SEA-SUB                                          09744699
           WHEN 1                                                       09744799
PK0400         MOVE PV-SEASON-MN-DTE     TO DT-SEA-MN1-DTE(PV-SUB1)     09744899
PK0400         MOVE PV-SEASON-MN-NBR     TO DT-SEA-MN1-NBR(PV-SUB1)     09744999
           WHEN 2                                                       09745099
PK0400         MOVE PV-SEASON-MN-DTE     TO DT-SEA-MN2-DTE(PV-SUB1)     09745199
PK0400         MOVE PV-SEASON-MN-NBR     TO DT-SEA-MN2-NBR(PV-SUB1)     09745299
           WHEN 3                                                       09745399
PK0400         MOVE PV-SEASON-MN-DTE     TO DT-SEA-MN3-DTE(PV-SUB1)     09745499
PK0400         MOVE PV-SEASON-MN-NBR     TO DT-SEA-MN3-NBR(PV-SUB1)     09745599
           WHEN 4                                                       09745699
PK0400         MOVE PV-SEASON-MN-DTE     TO DT-SEA-MN4-DTE(PV-SUB1)     09745799
PK0400         MOVE PV-SEASON-MN-NBR     TO DT-SEA-MN4-NBR(PV-SUB1)     09745899
           WHEN 5                                                       09745999
PK0400         MOVE PV-SEASON-MN-DTE     TO DT-SEA-MN5-DTE(PV-SUB1)     09746099
PK0400         MOVE PV-SEASON-MN-NBR     TO DT-SEA-MN5-NBR(PV-SUB1)     09746199
           WHEN 6                                                       09746299
PK0400         MOVE PV-SEASON-MN-DTE     TO DT-SEA-MN6-DTE(PV-SUB1)     09746399
PK0400         MOVE PV-SEASON-MN-NBR     TO DT-SEA-MN6-NBR(PV-SUB1)     09746499
           WHEN OTHER                                                   09746599
               DISPLAY '**********************************************' 09746699
               DISPLAY 'SEASONS PROCESSED > 6'                          09746799
               DISPLAY 'SEASONS PROCESSED = ' PV-SEA-SUB                09746899
               MOVE '7156-LOAD-SEA-DATE-RANGE-TABLE'                    09746999
                   TO PV-PARAGRAPH-NAME                                 09747099
               PERFORM 9999-ABEND                                       09747199
           END-EVALUATE.                                                09747299
                                                                        09747599
      *                                                                 09747699
      *                                                                 09747799
      ***************************************************************** 09747899
      * CLOSE CURSOR FOR UPDATE AT INITIALIZATION & AFTER EACH COMMIT   09747999
      ***************************************************************** 09748099
       7158-CLOSE-SEA-DTE-RANGE-CUR.                                    09748199
      *                                                                 09748299
           EXEC SQL                                                     09748399
               CLOSE SEASON_DATE_CSR                                    09748499
           END-EXEC                                                     09748599
                                                                        09748699
           EVALUATE SQLCODE                                             09748799
               WHEN 0                                                   09748899
                    CONTINUE                                            09748999
               WHEN OTHER                                               09749099
                  MOVE '7158-CLOSE-SEASON-CSR' TO PV-PARAGRAPH-NAME     09749199
                  MOVE PE-MSG-23 TO PV-OPERATION-ATTEMPTED              09749299
                  PERFORM 9999-SQL-ABEND                                09749399
           END-EVALUATE.                                                09749499
      *                                                                 09749592
      *                                                                 09750092
      ***************************************************************** 10180092
      * SELECTS THE SHRINK RETAIL AMOUNT SUM FROM TWKSHNK               10190099
      ***************************************************************** 10200092
       7200-SELECT-SHRINK-AMT.                                          10210099
      *                                                                 10220099
W26603     MOVE PV-HOLD-OPN-MN-END-DTE     TO WKSHNK-FSCL-MN-END-DTE.   10221099
W26603     MOVE PV-HOLD-DEPT-NBR           TO WKSHNK-DEPT-NBR.          10222099
W26603     MOVE PV-HOLD-SUB-CL-NBR         TO WKSHNK-SUB-CL-NBR.        10223099
W26603     MOVE PV-HOLD-STR-NBR            TO WKSHNK-STR-NBR.           10224099
      *                                                                 10225099
           EXEC SQL                                                     10230099
                SELECT  SUM (SHNK_RTL_AMT)                              10240099
                  INTO :PV-SHNK-RTL-AMT                                 10250099
W31475                 :WS-SHNK-RTL-AMT-NULL-IND                        10250099
                  FROM TWKSHNK                                          10260099
W26603           WHERE FSCL_MN_END_DTE = :WKSHNK-FSCL-MN-END-DTE        10270099
W26603             AND DEPT_NBR        = :WKSHNK-DEPT-NBR               10280099
W26603             AND SUB_CL_NBR      = :WKSHNK-SUB-CL-NBR             10290099
W26603             AND STR_NBR         = :WKSHNK-STR-NBR                10300099
W26603           WITH UR                                                10301099
           END-EXEC                                                     10310099
      *                                                                 10320099
           EVALUATE SQLCODE                                             10330099
               WHEN +0                                                  10340099
W31475             IF WS-SHNK-RTL-AMT-NULL-IND  = -1                            
  |                   MOVE ZERO TO PV-SHNK-RTL-AMT                      10370099
  |                END-IF                                               10350099
  |   *            CONTINUE                                             10360099
  |   *        WHEN -305                                                10360099
W31475*            MOVE ZERO TO PV-SHNK-RTL-AMT                         10370099
               WHEN OTHER                                               10380099
                   DISPLAY '                                     '      10390099
                   DISPLAY 'DEPT IN PROCESS : '  PV-HOLD-DEPT-NBR       10400099
                   DISPLAY '    OPEN MONTH  : '  PV-HOLD-OPN-MN-END-DTE 10410099
                   DISPLAY '    SUB CLASS   : '  PV-HOLD-SUB-CL-NBR     10420099
                   DISPLAY '    STORE       : '  PV-HOLD-STR-NBR        10430099
                   MOVE '7200-SELECT-SHRINK-AMT' TO PV-PARAGRAPH-NAME   10440099
                   MOVE PE-MSG-13              TO PV-OPERATION-ATTEMPTED10450099
                   PERFORM 9999-SQL-ABEND                               10460099
             END-EVALUATE.                                              10470099
      *                                                                 10480092
      ***************************************************************** 10490092
      * SELECT A ROW FROM DB2 TO BE RECALCED & UPDATED (OR INSERT/UPD)  10500092
      ***************************************************************** 10510092
       7300-SELECT-CURRENT-DOLLARS.                                     10520092
      *                                                                 10530092
W31475     MOVE 'N'                        TO PS-CURRENT-PURCH-COST-SW. 12190092
W26603     MOVE PV-HOLD-OPN-MN-END-DTE     TO MNSCLS-FSCL-MN-END-DTE.   10531099
W26603     MOVE PV-HOLD-FSCL-MN-NBR        TO MNSCLS-FSCL-MN-NBR.       10531199
W26603     MOVE PV-HOLD-DEPT-NBR           TO MNSCLS-DEPT-NBR.          10532099
W26603     MOVE PV-HOLD-SUB-CL-NBR         TO MNSCLS-SUB-CL-NBR.        10533099
W26603     MOVE PV-HOLD-STR-NBR            TO MNSCLS-STR-NBR.           10534099
      *                                                                 10535099
           EXEC SQL                                                     10540092
                SELECT  SLS_RTL_AMT                                     10550092
                       ,MKDN_RTL_AMT                                    10560092
                       ,MKUP_RTL_AMT                                    10570092
                       ,XFER_IN_RTL_AMT                                 10580092
                       ,XFER_OUT_RTL_AMT                                10590092
                       ,RCPT_RTL_AMT                                    10600092
                       ,PADJ_RTL_AMT                                    10610092
                       ,INV_ADJ_RTL_AMT                                 10620092
W31475                 ,RCPT_NET_COST_AMT                               10620092
W31475                 ,PADJ_NET_COST_AMT                               10620092
                  INTO :MNSCLS-SLS-RTL-AMT                              10630092
                      ,:MNSCLS-MKDN-RTL-AMT                             10640092
                      ,:MNSCLS-MKUP-RTL-AMT                             10650092
                      ,:MNSCLS-XFER-IN-RTL-AMT                          10660092
                      ,:MNSCLS-XFER-OUT-RTL-AMT                         10670092
                      ,:MNSCLS-RCPT-RTL-AMT                             10680092
                      ,:MNSCLS-PADJ-RTL-AMT                             10690092
                      ,:MNSCLS-INV-ADJ-RTL-AMT                          10700092
W31475                ,:PV-CURR-MONTH-RCPT-NET-COST                     10700092
W31475                ,:PV-CURR-MONTH-PADJ-NET-COST                     10700092
                  FROM TMNSCLS                                          10710092
W26603           WHERE FSCL_MN_NBR     = :MNSCLS-FSCL-MN-NBR            10720099
W26603             AND FSCL_MN_END_DTE = :MNSCLS-FSCL-MN-END-DTE        10730099
W26603             AND DEPT_NBR        = :MNSCLS-DEPT-NBR               10740099
W26603             AND SUB_CL_NBR      = :MNSCLS-SUB-CL-NBR             10750099
W26603             AND STR_NBR         = :MNSCLS-STR-NBR                10760099
W26603           WITH UR                                                10761099
           END-EXEC                                                     10770092
      *                                                                 10780092
           EVALUATE SQLCODE                                             10790092
               WHEN +0                                                  10800092
                   CONTINUE                                             10810092
               WHEN +100                                                10820092
                 SET PS-CURR-MONTH-ROW-NOT-FOUND   TO TRUE              10830092
W31475           SET PS-NO-CURRENT-PURCH-COSTS     TO TRUE              12380092
               WHEN OTHER                                               10840092
                   MOVE '7300-SELECT-CURRENT'  TO PV-PARAGRAPH-NAME     10850092
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED10860092
                   PERFORM 9999-SQL-ABEND                               10870092
             END-EVALUATE.                                              10880092
      *                                                                 10890092
      *                                                                 10900092
      ******************************************************************10910092
      * RETRIEVE PRIOR MONTH'S ENDING INVENTORY RETAIL/COST FOR A DEPT, 10920092
      *     SUB-CLASS, STORE.                                           10930092
      *     THIS IS ALSO THE BEGINNING INVENTORY FOR THE CURRENT MONTH  10940092
      ******************************************************************10950092
       7350-SELECT-PREV-MONTH-END-INV.                                  10960092
      *                                                                 10970092
W26603     MOVE PV-HOLD-PFM-MN-NBR         TO MNSCLS-FSCL-MN-NBR.       10971099
W26603     MOVE PV-HOLD-PFM-END-DTE        TO MNSCLS-FSCL-MN-END-DTE.   10972099
W26603     MOVE PV-HOLD-DEPT-NBR           TO MNSCLS-DEPT-NBR.          10973099
W26603     MOVE PV-HOLD-SUB-CL-NBR         TO MNSCLS-SUB-CL-NBR.        10974099
W26603     MOVE PV-HOLD-STR-NBR            TO MNSCLS-STR-NBR.           10975099
      *                                                                 10976099
           EXEC SQL                                                     10980092
                SELECT  END_INV_RTL_AMT                                 10990092
                      , END_INV_COST_AMT                                11000092
                  INTO  :PV-PREV-MONTH-END-INV-RTL                      11010092
                       ,:PV-PREV-MONTH-END-INV-COST                     11020092
                  FROM  TMNSCLS                                         11030092
W26603           WHERE FSCL_MN_NBR     = :MNSCLS-FSCL-MN-NBR            11031099
W26603             AND FSCL_MN_END_DTE = :MNSCLS-FSCL-MN-END-DTE        11032099
W26603             AND DEPT_NBR        = :MNSCLS-DEPT-NBR               11033099
W26603             AND SUB_CL_NBR      = :MNSCLS-SUB-CL-NBR             11034099
W26603             AND STR_NBR         = :MNSCLS-STR-NBR                11035099
W26603          WITH UR                                                 11081099
           END-EXEC.                                                    11090092
      *                                                                 11100092
           EVALUATE SQLCODE                                             11110092
               WHEN 0                                                   11120092
                   IF ((PV-PREV-MONTH-END-INV-RTL  NOT = 0) OR          11130092
                       (PV-PREV-MONTH-END-INV-COST NOT = 0))            11140092
                       SET PS-PFM-INV-DOLLARS-FOUND TO TRUE             11150092
                   END-IF                                               11160092
               WHEN +100                                                11170092
                   SET PS-PREV-MONTH-ROW-NOT-FOUND TO TRUE              11180092
                   MOVE ZEROES TO PV-PREV-MONTH-END-INV-RTL             11190092
                                  PV-PREV-MONTH-END-INV-COST            11200092
               WHEN OTHER                                               11210092
                   MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED  11220092
                   MOVE  '7350-SELECT-PREV-MONTH-END-INV'               11230092
                                             TO PV-PARAGRAPH-NAME       11240092
                   PERFORM 9999-SQL-ABEND                               11250092
           END-EVALUATE.                                                11260092
      *                                                                 11270092
      *                                                                 11280092
      ******************************************************************11290092
      * 7400-PREV-SEASON-END-INV SELECTS THE ENDING INVENTORY COST &   *11300092
      *      RETAIL FROM THE LAST DAY OF THE PREVIOUS FISCAL SEASON    *11310092
      ******************************************************************11320092
       7400-PREV-SEASON-END-INV.                                        11330092
      *                                                                 11340092
           MOVE 'N'    TO PS-PREV-SEASON-INV-SW.                        11350092
W26603     MOVE PV-HOLD-PREV-SEA-MN-NBR   TO MNSCLS-FSCL-MN-NBR.        11351099
W26603     MOVE PV-HOLD-PREV-SEA-END-DTE  TO MNSCLS-FSCL-MN-END-DTE.    11352099
W26603     MOVE PV-HOLD-DEPT-NBR          TO MNSCLS-DEPT-NBR.           11353099
W26603     MOVE PV-HOLD-SUB-CL-NBR        TO MNSCLS-SUB-CL-NBR.         11354099
W26603     MOVE PV-HOLD-STR-NBR           TO MNSCLS-STR-NBR.            11355099
      *                                                                 11360092
           EXEC SQL                                                     11370092
                SELECT  END_INV_RTL_AMT                                 11380092
                       ,END_INV_COST_AMT                                11390092
                  INTO  :MNSCLS-END-INV-RTL-AMT                         11400092
                       ,:MNSCLS-END-INV-COST-AMT                        11410092
                  FROM  TMNSCLS                                         11420092
W26603           WHERE FSCL_MN_NBR     = :MNSCLS-FSCL-MN-NBR            11421099
W26603             AND FSCL_MN_END_DTE = :MNSCLS-FSCL-MN-END-DTE        11422099
W26603             AND DEPT_NBR        = :MNSCLS-DEPT-NBR               11423099
W26603             AND SUB_CL_NBR      = :MNSCLS-SUB-CL-NBR             11424099
W26603             AND STR_NBR         = :MNSCLS-STR-NBR                11425099
W26603           WITH UR                                                11471099
           END-EXEC.                                                    11480092
      *                                                                 11490092
           EVALUATE SQLCODE                                             11500092
               WHEN 0                                                   11510092
                   CONTINUE                                             11520092
               WHEN +100                                                11530092
                   SET PS-NO-PREV-SEASONAL-INV       TO TRUE            11540092
               WHEN OTHER                                               11550092
                   MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED  11560092
                   MOVE  '7400-PREV-SEASON-END-INV'                     11570092
                                             TO PV-PARAGRAPH-NAME       11580092
                   PERFORM 9999-SQL-ABEND                               11590092
           END-EVALUATE.                                                11600092
      *                                                                 11790092
      *                                                                 11800092
      ******************************************************************11810092
      * 7450-GET-SEASONAL-MTH WILL RETRIEVE ALL ROWS WITH AN ENDING     11820099
      * INVENTORY AMOUNT FOR EACH MONTH END DATE FOR THE CURRENT SEASON.11830092
      ******************************************************************11840092
       7450-GET-SEASONAL-MTH.                                           11840199
                                                                        11840299
           IF PV-SEA-SUB = 1                                            11840399
               AND DT-SEA-MN1-DTE (PV-SUB1) NOT EQUAL SPACES            11840499
               MOVE DT-SEA-MN1-DTE (PV-SUB1) TO PV-SEA-MTH-DTE-DB2      11840599
               MOVE DT-SEA-MN1-NBR (PV-SUB1) TO PV-SEA-MTH-NBR-DB2      11840699
           ELSE                                                         11840799
           IF PV-SEA-SUB = 2                                            11840899
               AND DT-SEA-MN2-DTE (PV-SUB1) NOT EQUAL SPACES            11840999
               MOVE DT-SEA-MN2-DTE (PV-SUB1) TO PV-SEA-MTH-DTE-DB2      11841099
               MOVE DT-SEA-MN2-NBR (PV-SUB1) TO PV-SEA-MTH-NBR-DB2      11841199
           ELSE                                                         11841299
           IF PV-SEA-SUB = 3                                            11841399
               AND DT-SEA-MN3-DTE (PV-SUB1) NOT EQUAL SPACES            11841499
               MOVE DT-SEA-MN3-DTE (PV-SUB1) TO PV-SEA-MTH-DTE-DB2      11841599
               MOVE DT-SEA-MN3-NBR (PV-SUB1) TO PV-SEA-MTH-NBR-DB2      11841699
           ELSE                                                         11841799
           IF PV-SEA-SUB = 4                                            11841899
               AND DT-SEA-MN4-DTE (PV-SUB1) NOT EQUAL SPACES            11841999
               MOVE DT-SEA-MN4-DTE (PV-SUB1) TO PV-SEA-MTH-DTE-DB2      11842099
               MOVE DT-SEA-MN4-NBR (PV-SUB1) TO PV-SEA-MTH-NBR-DB2      11842199
           ELSE                                                         11842299
           IF PV-SEA-SUB = 5                                            11842399
               AND DT-SEA-MN5-DTE (PV-SUB1) NOT EQUAL SPACES            11842499
               MOVE DT-SEA-MN5-DTE (PV-SUB1) TO PV-SEA-MTH-DTE-DB2      11842599
               MOVE DT-SEA-MN5-NBR (PV-SUB1) TO PV-SEA-MTH-NBR-DB2      11842699
           ELSE                                                         11842799
           IF PV-SEA-SUB = 6                                            11842899
               AND DT-SEA-MN6-DTE (PV-SUB1) NOT EQUAL SPACES            11842999
               MOVE DT-SEA-MN6-DTE (PV-SUB1) TO PV-SEA-MTH-DTE-DB2      11843099
               MOVE DT-SEA-MN6-NBR (PV-SUB1) TO PV-SEA-MTH-NBR-DB2      11843199
           ELSE                                                         11843299
               SET PS-SEASON-MTH-NOT-FOUND TO TRUE                      11843399
               SET PS-END-OF-CURR-SEASON-MTHS TO TRUE.                  11843499
                                                                        11843599
                                                                        11843699
           IF PS-NOT-END-OF-CURR-SEASON-MTHS                            11843799
W26603         MOVE PV-SEA-MTH-NBR-DB2    TO MNSCLS-FSCL-MN-NBR         11843899
W26603         MOVE PV-SEA-MTH-DTE-DB2    TO MNSCLS-FSCL-MN-END-DTE     11843999
W26603         MOVE PV-HOLD-DEPT-NBR      TO MNSCLS-DEPT-NBR            11844099
W26603         MOVE PV-HOLD-SUB-CL-NBR    TO MNSCLS-SUB-CL-NBR          11844199
W26603         MOVE PV-HOLD-STR-NBR       TO MNSCLS-STR-NBR             11844299
               EXEC SQL                                                 11844399
               SELECT  XFER_IN_RTL_AMT                                  11844499
                       ,XFER_OUT_RTL_AMT                                11844599
                       ,RCPT_RTL_AMT                                    11844699
                       ,RCPT_NET_COST_AMT                               11844799
                       ,PADJ_RTL_AMT                                    11844899
                       ,PADJ_NET_COST_AMT                               11844999
                       ,MKUP_RTL_AMT                                    11845099
                 INTO  :MNSCLS-XFER-IN-RTL-AMT                          11845199
                      ,:MNSCLS-XFER-OUT-RTL-AMT                         11845299
                      ,:MNSCLS-RCPT-RTL-AMT                             11845399
                      ,:MNSCLS-RCPT-NET-COST-AMT                        11845499
                      ,:MNSCLS-PADJ-RTL-AMT                             11845599
                      ,:MNSCLS-PADJ-NET-COST-AMT                        11845699
                      ,:MNSCLS-MKUP-RTL-AMT                             11845799
                 FROM  TMNSCLS                                          11845899
W26603           WHERE FSCL_MN_NBR     = :MNSCLS-FSCL-MN-NBR            11845999
W26603             AND FSCL_MN_END_DTE = :MNSCLS-FSCL-MN-END-DTE        11846099
W26603             AND DEPT_NBR        = :MNSCLS-DEPT-NBR               11846199
W26603             AND SUB_CL_NBR      = :MNSCLS-SUB-CL-NBR             11846299
W26603             AND STR_NBR         = :MNSCLS-STR-NBR                11846399
W26603           WITH UR                                                11846699
               END-EXEC                                                 11846799
                                                                        11846899
               EVALUATE SQLCODE                                         11846999
                    WHEN 0                                              11847099
                        SET PS-SEASON-MTH-FOUND TO TRUE                 11847199
                    WHEN +100                                           11847299
                        SET PS-SEASON-MTH-NOT-FOUND TO TRUE             11847399
                    WHEN OTHER                                          11847499
                        MOVE PE-MSG-04      TO PV-OPERATION-ATTEMPTED   11847599
                        MOVE '7450-GET-SEASONAL-MTH'                    11847699
                                                   TO PV-PARAGRAPH-NAME 11847799
                        PERFORM 9999-SQL-ABEND                          11847899
               END-EVALUATE                                             11847999
           END-IF.                                                      11848099
      *                                                                 12110092
      *                                                                 12120092
      ******************************************************************12130092
      * 7475-CURR-SEASON-COST SELECTS THE ENDING INVENTORY COST &      *12140092
      *      RETAIL FROM THE LAST DAY OF THE PREVIOUS FISCAL SEASON    *12150092
      ******************************************************************12160092
W31475*7475-CURR-SEASON-COST.                                           12170092
  |   *    MOVE 'N'    TO PS-CURRENT-PURCH-COST-SW.                     12190092
  |   *    MOVE PV-HOLD-FSCL-MN-NBR       TO MNSCLS-FSCL-MN-NBR.        12191099
  |   *    MOVE PV-HOLD-OPN-MN-END-DTE    TO MNSCLS-FSCL-MN-END-DTE.    12192099
  |   *    MOVE PV-HOLD-DEPT-NBR          TO MNSCLS-DEPT-NBR.           12193099
  |   *    MOVE PV-HOLD-SUB-CL-NBR        TO MNSCLS-SUB-CL-NBR.         12194099
  |   *    MOVE PV-HOLD-STR-NBR           TO MNSCLS-STR-NBR.            12195099
  |   *                                                                 12200092
  |   *    EXEC SQL                                                     12210092
  |   *         SELECT  RCPT_NET_COST_AMT                               12220092
  |   *                ,PADJ_NET_COST_AMT                               12230092
  |   *           INTO  :PV-CURR-MONTH-RCPT-NET-COST                    12240092
  |   *                ,:PV-CURR-MONTH-PADJ-NET-COST                    12250092
  |   *           FROM  TMNSCLS                                         12260092
  |   *          WHERE FSCL_MN_NBR     = :MNSCLS-FSCL-MN-NBR            12261099
  |   *            AND FSCL_MN_END_DTE = :MNSCLS-FSCL-MN-END-DTE        12262099
  |   *            AND DEPT_NBR        = :MNSCLS-DEPT-NBR               12263099
  |   *            AND SUB_CL_NBR      = :MNSCLS-SUB-CL-NBR             12264099
  |   *            AND STR_NBR         = :MNSCLS-STR-NBR                12265099
  |   *         WITH UR                                                 12311099
  |   *    END-EXEC.                                                    12320092
  |   *                                                                 12330092
  |   *    EVALUATE SQLCODE                                             12340092
  |   *        WHEN 0                                                   12350092
  |   *            CONTINUE                                             12360092
  |   *        WHEN +100                                                12370092
  |   *            SET PS-NO-CURRENT-PURCH-COSTS     TO TRUE            12380092
  |   *        WHEN OTHER                                               12390092
  |   *            MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED  12400092
  |   *            MOVE  '7475-CURRENT-SEASON-COST'                     12410092
  |   *                                      TO PV-PARAGRAPH-NAME       12420092
  |   *            PERFORM 9999-SQL-ABEND                               12430092
W31475*    END-EVALUATE.                                                12440092
      *                                                                 12450092
      *                                                                 12460092
      ******************************************************************12470092
      * UPDATE COLUMNS WITH VALUES JUST CALCULATED FOR THIS DEPARTMENT  12480092
      ******************************************************************12490092
       7500-UPDATE-PROCESSING.                                          12500092
      *                                                                 12510092
W26603     MOVE PV-HOLD-FSCL-MN-NBR       TO MNSCLS-FSCL-MN-NBR.        12511099
W26603     MOVE PV-HOLD-OPN-MN-END-DTE    TO MNSCLS-FSCL-MN-END-DTE.    12512099
W26603     MOVE PV-HOLD-DEPT-NBR          TO MNSCLS-DEPT-NBR.           12513099
W26603     MOVE PV-HOLD-SUB-CL-NBR        TO MNSCLS-SUB-CL-NBR.         12514099
W26603     MOVE PV-HOLD-STR-NBR           TO MNSCLS-STR-NBR.            12515099
      *                                                                 12516099
           EXEC SQL                                                     12520092
               UPDATE TMNSCLS                                           12530092
                  SET  SHNK_RTL_AMT     = :PV-SHNK-RTL-AMT              12540092
                      ,END_INV_RTL_AMT  = :PV-END-INV-RTL-AMT           12550092
                      ,CUM_MKUP_PCT     = :PV-CUM-MKUP-PCT              12560092
                      ,END_INV_COST_AMT = :PV-END-INV-COST-AMT          12570092
                      ,SLS_COST_AMT     = :PV-SLS-COST-AMT              12580092
                      ,GRO_MRGN_AMT     = :PV-GRO-MRGN-AMT              12590092
W26603            WHERE FSCL_MN_NBR     = :MNSCLS-FSCL-MN-NBR           12641099
W26603              AND FSCL_MN_END_DTE = :MNSCLS-FSCL-MN-END-DTE       12642099
W26603              AND DEPT_NBR        = :MNSCLS-DEPT-NBR              12643099
W26603              AND SUB_CL_NBR      = :MNSCLS-SUB-CL-NBR            12644099
W26603              AND STR_NBR         = :MNSCLS-STR-NBR               12645099
           END-EXEC                                                     12650092
  *                                                                     12660092
           EVALUATE SQLCODE                                             12670092
               WHEN 0                                                   12680092
                    ADD 1                TO PA-UPDATE-COUNT             12690092
               WHEN OTHER                                               12700092
                    DISPLAY '*************************************'     12710092
                    DISPLAY 'DEPT PROCESSING : ', PV-HOLD-DEPT-NBR      12720092
                    DISPLAY 'MONTH-END-DATE  : ',                       12730092
                                              PV-HOLD-OPN-MN-END-DTE    12740092
                    DISPLAY 'FISCAL MONTH NBR: ',                       12750092
                                                 PV-HOLD-FSCL-MN-NBR    12760092
                    MOVE PE-MSG-06        TO PV-OPERATION-ATTEMPTED     12770092
                    MOVE '7500-UPDATE-PROCESSING'                       12780092
                                          TO PV-PARAGRAPH-NAME          12790092
                    PERFORM 9999-SQL-ABEND                              12800092
           END-EVALUATE.                                                12810092
      *                                                                 13230092
      *                                                                 13240092
      ******************************************************************13250092
      * 7600-GET-COMMIT-TIMESTAMP.                                     *13260092
      *   GET THE TIMESTAMP FROM THE CHECKPOINT TABLE FOR NEXT COMMIT  *13270092
      ******************************************************************13280092
       7600-GET-COMMIT-TIMESTAMP.                                       13290092
W31475*                                                                 13300092
  |   *    EXEC SQL                                                     13310092
  |   *      SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       13320092
  |   *        INTO :PV-COMMIT-TIMESTAMP                                13330092
  |   *        FROM TCKRSTCNTL                                          13340092
  |   *       WHERE JOB_NAME  = :PC-JOB-NAME                            13350092
  |   *         AND PLAN_NAME = :PC-PROGRAM-NAME                        13360092
  |   *    END-EXEC.                                                    13370092
  |   *                                                                 13380092
  |   *    EVALUATE TRUE                                                13390092
  |   *        WHEN SQLCODE = 0                                         13400092
  |   *            CONTINUE                                             13410092
  |   *        WHEN SQLCODE NOT EQUAL ZERO                              13420092
  |   *            MOVE '7600-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME13430092
  |   *            MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED 13440092
  |   *            PERFORM 9999-SQL-ABEND                               13450092
  |   *    END-EVALUATE.                                                13460092
  |        ACCEPT WS-COMMIT-TIME FROM TIME.                                     
  |        MOVE WS-COMMIT-TIME   TO WS-HCOMMIT-TIME.                            
  |        ADD TCKRSTCNTL-CKPT-FRQNCY-QTY                                       
W31475                           TO WS-HCOMMIT-SEC.                             
                                                                        13470092
      ******************************************************************13480092
      * 7650-INIT-CHECKPOINT-SELECT                                    *13490092
      * CHECK STATUS CODE TO SEE IF THIS IS A RESTART CONDITION        *13500092
      ******************************************************************13510092
       7650-INIT-CHECKPOINT-SELECT.                                     13520092
                                                                        13530092
           EXEC SQL                                                     13540092
             SELECT  CKPT_STAT_CODE                                     13550092
                    ,CKPT_FRQNCY_QTY                                    13560092
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         13570092
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        13580092
               FROM  TCKRSTCNTL                                         13590092
              WHERE  JOB_NAME  = :PC-JOB-NAME                           13600092
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       13610092
           END-EXEC.                                                    13620092
                                                                        13630092
           IF SQLCODE = 0                                               13640092
               CONTINUE                                                 13650092
           ELSE                                                         13660092
               MOVE '7650-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  13670092
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     13680092
               PERFORM 9999-SQL-ABEND                                   13690092
           END-IF.                                                      13700092
                                                                        13710092
      ******************************************************************13720092
      * 7700-SELECT-RESTART-INFO                                       *13730092
      * OBTAIN INFORMATION REGARDING WHERE TO RESTART TABLE PROCESSING *13740092
      ******************************************************************13750092
       7700-SELECT-RESTART-INFO.                                        13760092
                                                                        13770092
           EXEC SQL                                                     13780092
             SELECT  WORK_STRG_DESC                                     13790092
               INTO  :TCKRSTINF-WORK-STRG-DESC                          13800092
               FROM  TCKRSTINF                                          13810092
              WHERE  JOB_NAME  = :PC-JOB-NAME                           13820092
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       13830092
           END-EXEC.                                                    13840092
                                                                        13850092
           IF SQLCODE = 0                                               13860092
               CONTINUE                                                 13870092
           ELSE                                                         13880092
               MOVE '*7700-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   13890092
               MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED     13900092
               PERFORM 9999-SQL-ABEND                                   13910092
           END-IF.                                                      13920092
                                                                        13930092
      ******************************************************************13940092
      * 7750-SET-CURRENT-TIMESTAMP.                                    *13950092
      * CURRENT TIMESTAMP SET TO BE COMPARED TO COMMIT TIMESTAMP       *13960092
      ******************************************************************13970092
       7750-SET-CURRENT-TIMESTAMP.                                      13980092
W31475*                                                                 13990092
  |   *    EXEC SQL                                                     14000092
  |   *         SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           14010092
  |   *    END-EXEC.                                                    14020092
  |   *                                                                 14030092
  |   *    IF SQLCODE = 0                                               14040092
  |   *        CONTINUE                                                 14050092
  |   *    ELSE                                                         14060092
  |   *        MOVE PE-MSG-12             TO PV-OPERATION-ATTEMPTED     14070092
  |   *        MOVE '*7750-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 14080092
  |   *        PERFORM 9999-SQL-ABEND                                   14090092
  |   *    END-IF.                                                      14100092
W31475     ACCEPT WS-CURRENT-TIME FROM TIME.                                    
                                                                        14110092
      *                                                                 14120092
      ******************************************************************14130092
      * 7800-COMMIT-PROCESSING.                                        *14140092
      * COMPARE COMMIT TIMESTAMP TO CURRENT TO DETERMINE IF COMMIT REQD*14150092
      ******************************************************************14160092
       7800-COMMIT-PROCESSING.                                          14170092
           MOVE '*7800-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     14180092
      *                                                                 14190092
           PERFORM 7750-SET-CURRENT-TIMESTAMP.                          14200092
      *                                                                 14210092
W31475*    IF (PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP)              14220092
W31475     IF ((WS-CURRENT-TIME > WS-HCOMMIT-TIME) OR                           
W31475         (WS-CURRENT-TIME < WS-COMMIT-TIME))                              
      * PREPARE COMMIT RECORD FOR UPDATE                                14230092
               ADD 1                        TO PA-COMMIT-COUNT          14240092
               MOVE PV-HOLD-OPN-MN-END-DTE  TO CR-FISCAL-MN-END-DTE     14250092
               MOVE PV-HOLD-DEPT-NBR        TO CR-DEPT-NBR              14260092
               MOVE PV-HOLD-SUB-CL-NBR      TO CR-SUB-CL-NBR            14270092
               MOVE PV-HOLD-STR-NBR         TO CR-STR-NBR               14280092
               MOVE PA-EXTRACT-RECS-READ    TO CR-TMNSCLS-INPUT-COUNT   14290092
               MOVE PA-DEPT-COUNT           TO CR-TMNSCLS-FETCH-COUNT   14300092
               MOVE PA-UPDATE-COUNT         TO CR-TMNSCLS-UPDATE-COUNT  14310092
               MOVE PA-INSERT-COUNT         TO CR-TMNSCLS-INSERT-COUNT  14320092
               MOVE PA-COMMIT-COUNT         TO CR-TMNSCLS-COMMIT-COUNT  14330092
      * MOVE COMMIT INFO TO WORKING STORAGE ROW                         14340092
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       14350092
                                             TCKRSTINF-WORK-STRG-DESC   14360092
               MOVE 'Y' TO PS-COMMIT-TAKEN-SW                           14370092
               IF PS-FIRST-COMMIT                                       14380092
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                14390092
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                14400092
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       14410092
               END-IF                                                   14420092
               PERFORM 7850-COMMIT-CHANGES                              14430092
               PERFORM 7600-GET-COMMIT-TIMESTAMP                        14440092
           END-IF.                                                      14450092
      *                                                                 14460092
      *                                                                 14470092
      ******************************************************************14480092
      * 7850-COMMIT-CHANGES.                                            14490092
      * UPDATE CHECKPOINT INFORMATION TABLE. TAKE A COMMIT.             14500092
      ******************************************************************14510092
       7850-COMMIT-CHANGES.                                             14520092
      *                                                                 14530092
           EXEC SQL                                                     14540092
             UPDATE TCKRSTINF                                           14550092
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          14560092
              WHERE JOB_NAME       = :PC-JOB-NAME                       14570092
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   14580092
           END-EXEC.                                                    14590092
      *                                                                 14600092
           IF SQLCODE = 0                                               14610092
               CONTINUE                                                 14620092
           ELSE                                                         14630092
               MOVE PE-MSG-08                  TO PV-OPERATION-ATTEMPTED14640092
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     14650092
               PERFORM 9999-SQL-ABEND                                   14660092
           END-IF.                                                      14670092
      *                                                                 14680092
           EXEC SQL                                                     14690092
               COMMIT                                                   14700092
           END-EXEC.                                                    14710092
      *                                                                 14720092
           IF SQLCODE = 0                                               14730092
               CONTINUE                                                 14740092
           ELSE                                                         14750092
               MOVE PE-MSG-09                  TO PV-OPERATION-ATTEMPTED14760092
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     14770092
               PERFORM 9999-SQL-ABEND                                   14780092
           END-IF.                                                      14790092
      *                                                                 14800092
      *                                                                 14810092
      ******************************************************************14820092
      * UPDATE CHECKPOINT STATUS                                        14830092
      ******************************************************************14840092
       7900-UPDATE-CHECKPOINT-STATUS.                                   14850092
      *                                                                 14860092
           EXEC SQL                                                     14870092
             UPDATE  TCKRSTCNTL                                         14880092
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        14890092
              WHERE  JOB_NAME  = :PC-JOB-NAME                           14900092
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       14910092
           END-EXEC.                                                    14920092
      *                                                                 14930092
           IF SQLCODE = 0                                               14940092
               CONTINUE                                                 14950092
           ELSE                                                         14960092
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME14970092
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED14980092
               PERFORM 9999-SQL-ABEND                                   14990092
           END-IF.                                                      15000092
                                                                        15010092
      ******************************************************************15020092
      * 7950-INSERT-CARRY-FWD-ROW                                       15030092
      * PERFORMED IF NO ROW IS FOUND ON DB2 (FOR THIS PROCESSING MONTH) 15040092
      * BUT INVENTORY DOLLARS (COST AND/OR RETAIL) FOUND FOR LAST MONTH 15050092
      ******************************************************************15060092
       7950-INSERT-CARRY-FWD-ROW.                                       15070092
      *                                                                 15080092
      *    DISPLAY 'CUM MKUP: ', PV-CUM-MKUP-PCT.                       15090092
      *    DISPLAY 'DEPT: ', PV-HOLD-DEPT-NBR,  PV-HOLD-OPN-MN-END-DTE  15100092
      *                                                                 15110092
W26603     MOVE PV-HOLD-FSCL-MN-NBR       TO MNSCLS-FSCL-MN-NBR.        15111099
W26603     MOVE PV-HOLD-OPN-MN-END-DTE    TO MNSCLS-FSCL-MN-END-DTE.    15112099
W26603     MOVE PV-HOLD-DEPT-NBR          TO MNSCLS-DEPT-NBR.           15113099
W26603     MOVE PV-HOLD-SUB-CL-NBR        TO MNSCLS-SUB-CL-NBR.         15114099
W26603     MOVE PV-HOLD-STR-NBR           TO MNSCLS-STR-NBR.            15115099
W26603     MOVE PV-PREV-MONTH-END-INV-RTL  TO MNSCLS-END-INV-RTL-AMT.   15115199
W26603     MOVE PV-PREV-MONTH-END-INV-COST TO MNSCLS-END-INV-COST-AMT.  15115299
      *                                                                 15116099
           EXEC SQL                                                     15120092
               INSERT INTO TMNSCLS                                      15130092
                   ( FSCL_MN_NBR                                        15140092
                    ,FSCL_MN_END_DTE                                    15150092
                    ,DEPT_NBR                                           15160092
                    ,SUB_CL_NBR                                         15170092
                    ,STR_NBR                                            15180092
                    ,SLS_RTL_AMT                                        15190092
                    ,MKDN_RTL_AMT                                       15200092
                    ,MKUP_RTL_AMT                                       15210092
                    ,XFER_IN_RTL_AMT                                    15220092
                    ,XFER_OUT_RTL_AMT                                   15230092
                    ,RCPT_RTL_AMT                                       15240092
                    ,RCPT_NET_COST_AMT                                  15250092
                    ,PADJ_RTL_AMT                                       15260092
                    ,PADJ_NET_COST_AMT                                  15270092
                    ,INV_ADJ_RTL_AMT                                    15280092
                    ,SHNK_RTL_AMT                                       15290092
                    ,END_INV_RTL_AMT                                    15300092
                    ,CUM_MKUP_PCT                                       15310092
                    ,END_INV_COST_AMT                                   15320092
                    ,SLS_COST_AMT                                       15330092
                    ,GRO_MRGN_AMT )                                     15340092
               VALUES (:MNSCLS-FSCL-MN-NBR                              15350099
                      ,:MNSCLS-FSCL-MN-END-DTE                          15360099
                      ,:MNSCLS-DEPT-NBR                                 15370099
                      ,:MNSCLS-SUB-CL-NBR                               15380099
                      ,:MNSCLS-STR-NBR                                  15390099
                      ,0.                                               15400099
                      ,0.                                               15410099
                      ,0.                                               15420099
                      ,0.                                               15430099
                      ,0.                                               15440099
                      ,0.                                               15450099
                      ,0.                                               15460099
                      ,0.                                               15470099
                      ,0.                                               15480099
                      ,0.                                               15490099
                      ,0.                                               15500099
                      ,:MNSCLS-END-INV-RTL-AMT                          15510099
                      ,0.                                               15520099
                      ,:MNSCLS-END-INV-COST-AMT                         15530099
                      ,0.                                               15540099
                      ,0. )                                             15550099
           END-EXEC                                                     15560092
      *                                                                 15570092
           IF SQLCODE = 0                                               15580092
               ADD 1                      TO PA-INSERT-COUNT            15590092
      *        CONTINUE                                                 15600092
           ELSE                                                         15610092
               MOVE '7950-INSERT-CARRY-FWD-ROW' TO PV-PARAGRAPH-NAME    15620092
               MOVE PE-MSG-14             TO PV-OPERATION-ATTEMPTED     15630092
               PERFORM 9999-SQL-ABEND                                   15640092
           END-IF.                                                      15650092
      *                                                                 15660092
      *                                                                 15670092
      ******************************************************************15680092
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *15690092
      ******************************************************************15700092
       8000-EOJ-ROUTINE.                                                15710092
      *                                                                 15720092
      *    COMMIT ANY FINAL CHANGES THAT MAY HAVE BEEN MADE             15730092
           IF PS-EMPTY-RESULT-TABLE                                     15740092
               CONTINUE                                                 15750092
           ELSE                                                         15760092
               PERFORM 7800-COMMIT-PROCESSING                           15770092
           END-IF.                                                      15790092
                                                                        15800092
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       15810092
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       15820092
           CLOSE MNSCLS-EXTR-FILE.                                      15830092
      *                                                                 15840092
           DISPLAY 'TMNSCLS ROWS UPDATED  ' PA-UPDATE-COUNT.            15850092
           DISPLAY 'TMNSCLS ROWS INSERTED ' PA-INSERT-COUNT.            15860092
           DISPLAY SPACE.                                               15870092
           DISPLAY 'SUBCLASSES PROCESSED  ' PA-DEPT-COUNT.              15880092
           DISPLAY SPACE.                                               15890092
           DISPLAY 'COMMITS TAKEN         ' PA-COMMIT-COUNT.            15900092
      *                                                                 15900199
      *                                                                 15900299
      ******************************************************************15900399
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM. 15900499
      ******************************************************************15900599
       9999-ABEND.                                                      15900699
                                                                        15900799
           DISPLAY '**  ABEND     **'.                                  15900899
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              15900999
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            15901099
           DISPLAY '**'                                                 15901199
           DISPLAY '**'                                                 15901299
           DISPLAY '**********************************'                 15901399
           CALL 'ILBOABN0'  USING ABEND-CODE.                           15901499
      *                                                                 15910092
      *                                                                 15920092
      ******************************************************************15930092
      *  STANDARD DB2 ERROR ABEND ROUTINE                               15940092
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             15950092
      ******************************************************************15960092
       9999-SQL-ABEND.                                                  15970092
                                                                        15980092
           MOVE +4013                 TO ABEND-CODE.                    15990092
           DISPLAY '**  ABEND     **'.                                  16000092
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              16010092
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            16020092
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       16030092
                                                                        16040092
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         16050092
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        16060092
                                                                        16070092
           COPY DPPD004.                                                16080092
           CALL 'ILBOABN0'  USING ABEND-CODE.                           16090092
      *                                                                 16100092
