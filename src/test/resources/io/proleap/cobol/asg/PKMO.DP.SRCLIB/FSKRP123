000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400*                                                                         
000500 PROGRAM-ID.    FSKRP123.                                                 
000600 AUTHOR.        LINDA FRIESS.                                             
000700 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000800 DATE-WRITTEN.  03-19-2003.                                               
000900 DATE-COMPILED.                                                           
001000*                                                                         
001100******************************************************************        
001200*       FSKRP123 - DAILY SALES ERROR REPORT                               
001300******************************************************************        
001400*                                                                         
001500*   THIS PROGRAM WILL PRINT THE SALES ERROR REPORT FOR A GIVEN            
001600*   DAY.  THE REPORT WILL BREAK ON DEPARTMENT AND MAJOR CLASS.            
001800*                                                                         
001900******************************************************************        
002000*  CHANGE MADE: ADDED PRINTING OF HEADERS & MESSAGE IF NO ERRORS *        
002100*               WERE FOUND                                       *        
002200*      MADE BY:  LINDA FRIESS                                    *        
002300*         DATE:  03/31/03                 WR# 23946              *        
001900******************************************************************        
002000*  CHANGE MADE: STORE NUMBER EXPANSION PROJECT.                  *        
002200*      MADE BY:  MARIA KLAMIK                                    *        
002300*         DATE:  04/22/2004               WR# 26355              *        
002400******************************************************************        
001900******************************************************************        
002000*  CHANGE MADE: TO IDENTIFY THE UNIT QTY ERROR TRANSACTIONS      *        
002200*      MADE BY: COGNIZANT                                        *        
002300*         DATE: 06/01/2016               CHG# CHG0143046         *        
002400******************************************************************        
002500 ENVIRONMENT DIVISION.                                                    
002600******************************************************************        
002700 CONFIGURATION SECTION.                                                   
002800******************************************************************        
002900 SOURCE-COMPUTER.    IBM-3090.                                            
003000 OBJECT-COMPUTER.    IBM-3090.                                            
003100******************************************************************        
003200 INPUT-OUTPUT SECTION.                                                    
003300******************************************************************        
003400 FILE-CONTROL.                                                            
003500******************************************************************        
003600*                                                                         
003700     SELECT CONTROL-FILE    ASSIGN TO INP01.                              
003800*                                                                         
008300     SELECT PRINT-REPORT    ASSIGN TO OUT01.                              
003700     SELECT OUTERROR-FILE   ASSIGN TO OUT02.                              
008400*                                                                         
008500******************************************************************        
008600 DATA DIVISION.                                                           
009200*****************************************************************         
009300*                                                                         
009400 FILE SECTION.                                                            
009500*                                                                         
009600 FD  CONTROL-FILE                                                         
009700     RECORDING MODE IS F                                                  
009800     DATA RECORD IS CONTROL-RECORD.                                       
009900                                                                          
010000 01  CONTROL-RECORD                    PIC X(80).                         
010100*                                                                         
       FD  OUTERROR-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 133 CHARACTERS                                       
           DATA RECORD IS OUTERR-REC.                                           
       01  OUTERR-REC                        PIC X(133).                        
010200*                                                                         
011000 FD  PRINT-REPORT                                                         
011100     LABEL RECORDS ARE STANDARD                                           
011200     RECORDING MODE IS F                                                  
011300     RECORD CONTAINS 132 CHARACTERS                                       
011400     BLOCK CONTAINS 0 RECORDS.                                            
011500                                                                          
011600 01  PRINT-LINE                       PIC X(132).                         
011700*                                                                         
011800******************************************************************        
011900 WORKING-STORAGE SECTION.                                                 
012000*                                                                         
012700 01  CONTROL-DATA-IN.                                                     
012800     05  CTL-PROCESSING-DATE           PIC X(10).                         
013500     05  FILLER                        PIC X(70).                         
013600*                                                                         
      ******************************************************************        
      *  RECORD LAYOUT FROM ERROR FILE - PRORATED FORMAT               *        
      ******************************************************************        
019800 01  OUTPUT-ERR-REC.                                                      
019900     05  FILLER                       PIC X(03)  VALUE SPACES.            
020000     05  TRANS-DATE                   PIC X(08).                          
020100     05  FILLER                       PIC X(03)  VALUE SPACES.            
020200     05  STORE-NBR                    PIC ZZZZ9.                          
020300     05  FILLER                       PIC X(05)  VALUE SPACES.            
020301     05  REGISTER-ID                  PIC ZZZZ9.                          
020502     05  FILLER                       PIC X(06)  VALUE SPACES.            
020600     05  TRANS-NBR                    PIC ZZZZ9.                          
020700     05  FILLER                       PIC X(05)  VALUE SPACES.            
020701     05  TRANS-TYPE                   PIC X(02).                          
020702     05  FILLER                       PIC X(05)  VALUE SPACES.            
020703     05  SKU-NBR                      PIC X(08).                          
020704     05  FILLER                       PIC X(02)  VALUE SPACES.            
020800     05  QTY                          PIC -------9.                       
020900     05  FILLER                       PIC X(02)  VALUE SPACES.            
021000     05  AMOUNT                       PIC -----,--9.99.                   
021100     05  FILLER                       PIC X(08)  VALUE SPACES.            
021710     05  ERROR-MSG                    PIC X(30).                          
021720     05  FILLER                       PIC X(10).                          
022700*                                                                         
014200******************************************************                    
014300** STANDARD HEADER LINE - KOHLS 132 COL                                   
014400******************************************************                    
014500     COPY DPWS132.                                                        
014600*                                                                         
014700 01  HEADING-LINE-2.                                                      
015300     05  FILLER                       PIC X(58)  VALUE SPACES.            
015400     05  FILLER                       PIC X(15)  VALUE                    
015500         'SKU SALES ERROR'.                                               
015600     05  FILLER                       PIC X(59)  VALUE SPACES.            
015700*                                                                         
015800 01  HEADING-LINE-3.                                                      
015900     05  FILLER                       PIC X(02)  VALUE SPACES.            
016000     05  FILLER                       PIC X(06)  VALUE 'DEPT: '.          
016100     05  HL3-DEPT                     PIC ZZZ9.                           
016200     05  FILLER                       PIC X(02)  VALUE SPACES.            
016300     05  FILLER                       PIC X(13)  VALUE                    
016400                                                 'MAJOR CLASS: '.         
016500     05  HL3-MAJOR-CLASS              PIC ZZZ9.                           
016600     05  FILLER                       PIC X(02)  VALUE SPACES.            
016601     05  FILLER                       PIC X(10)  VALUE                    
016602                                                 'SUBCLASS: '.            
016700     05  HL3-SUBCLASS                 PIC ZZZ9.                           
016800     05  FILLER                       PIC X(85)  VALUE SPACES.            
017210*                                                                         
017400 01  HEADING-LINE-4.                                                      
017500     05  FILLER                       PIC X(05)  VALUE SPACES.            
017800     05  FILLER                       PIC X(44)  VALUE                    
017810         'TRANS     STORE    REGISTER   TRANS    TRANS'.                  
017902     05  FILLER                       PIC X(83)  VALUE SPACES.            
018100*                                                                         
018200 01  HEADING-LINE-5.                                                      
018300     05  FILLER                       PIC X(05)  VALUE SPACES.            
018400     05  FILLER                       PIC X(43)  VALUE                    
018500         'DATE      NUMBER      ID      NUMBER   TYPE'.                   
018703     05  FILLER                       PIC X(03)  VALUE SPACES.            
018704     05  FILLER                       PIC X(38)  VALUE                    
018705         '   SKU         UNITS       AMOUNT     '.                        
018706     05  FILLER                       PIC X(18)  VALUE                    
018707         '    ERROR MESSAGE '.                                            
018708     05  FILLER                       PIC X(25)  VALUE SPACES.            
019700*                                                                         
019800 01  DETAIL-LINE-1.                                                       
019900     05  FILLER                       PIC X(03)  VALUE SPACES.            
020000     05  DL1-TRANS-DATE               PIC X(08).                          
020100     05  FILLER                       PIC X(03)  VALUE SPACES.            
020200     05  DL1-STORE-NBR                PIC ZZZZ9.                          
020300     05  FILLER                       PIC X(05)  VALUE SPACES.            
020301     05  DL1-REGISTER-ID              PIC ZZZZ9.                          
020502     05  FILLER                       PIC X(06)  VALUE SPACES.            
020600     05  DL1-TRANS-NBR                PIC ZZZZ9.                          
020700     05  FILLER                       PIC X(05)  VALUE SPACES.            
020701     05  DL1-TRANS-TYPE               PIC X(02).                          
020702     05  FILLER                       PIC X(05)  VALUE SPACES.            
020703     05  DL1-SKU-NBR                  PIC X(08).                          
020704     05  FILLER                       PIC X(02)  VALUE SPACES.            
020800     05  DL1-QTY                      PIC -------9.                       
020900     05  FILLER                       PIC X(02)  VALUE SPACES.            
021000     05  DL1-AMOUNT                   PIC -----,--9.99.                   
021100     05  FILLER                       PIC X(08)  VALUE SPACES.            
021710     05  DL1-ERROR-MSG                PIC X(30).                          
021720     05  FILLER                       PIC X(10).                          
022700*                                                                         
027600*                                                                         
027700 01  BLANK-LINE.                                                          
027800     05  FILLER                       PIC X(132) VALUE SPACES.            
027900*                                                                         
028000 01  NO-ERRORS-LINE.                                                      
028100     05  FILLER                       PIC X(25)      VALUE                
028200         ' ** NO ERRORS FOUND **   '.                                     
027900*                                                                         
028000 01  END-OF-REPORT-LINE.                                                  
028100     05  FILLER                       PIC X(25)      VALUE                
028200         ' **  END OF REPORT  **   '.                                     
028300*                                                                         
028400*                                                                         
028500 01  PROGRAM-SWITCHES.                                                    
028600     05  PS-EOF-CONTROL-FILE         PIC  X(01)    VALUE 'N'.             
028700         88  EOF-CONTROL-FILE                      VALUE 'Y'.             
028800     05  PS-EOF-SLS-AUD-CURSOR       PIC  X(01)    VALUE 'N'.             
028900         88  EOF-SLS-AUD-CURSOR                    VALUE 'Y'.             
028901     05  PS-EOF-INV-ITEM-CURSOR      PIC  X(01)    VALUE 'N'.             
028902         88  EOF-INV-ITEM-CURSOR                   VALUE 'Y'.             
028903     05  PS-FIRST-TIME-THRU          PIC  X(01)    VALUE 'Y'.             
028904         88  FIRST-TIME-THRU                       VALUE 'Y'.             
031200*                                                                         
031300 01  ABEND-CODE                       PIC S9(04)     VALUE +0             
031400                                                     COMP SYNC.           
031500     88  ABEND-4013                                  VALUE +4013.         
031600*                                                                         
031700 01  PC-CONSTANTS.                                                        
031800     05  PC-PROGRAM-NAME             PIC X(08)  VALUE 'FSKRP123'.         
033100     05  PC-1                        PIC S9(03) VALUE +1                  
033200                                                COMP-3.                   
033300     05  PC-2                        PIC S9(03) VALUE +2                  
033400                                                COMP-3.                   
033401     05  PC-MAX-LINES                PIC S9(3)  VALUE +56                 
033402                                                COMP-3.                   
033403*                                                                         
033404 01  PV-PROGRAM-VARIABLES.                                                
046200     05  PV-PREV-DEPT                PIC 9(04).                           
046201     05  PV-PREV-MAJ-CL              PIC 9(04).                           
046202     05  PV-PREV-SUB-CL              PIC 9(04).                           
046203     05  PV-HOLD-CLASS               PIC 9(04).                           
046204     05  PV-EDIT-SKU                 PIC ZZZZZZZ9.                        
046205     05  PV-HOLD-SKU                 PIC X(08).                           
046206     05  PV-RECS-PROCESSED           PIC S9(09) COMP-3 VALUE +0.          
046206     05  PV-RECS-WRITTEN             PIC S9(09) COMP-3 VALUE +0.          
046207     05  PV-DISP-RECS-PROC           PIC ZZZ,ZZZ,ZZ9.                     
046207     05  PV-DISP-RECS-WRITE          PIC ZZZ,ZZZ,ZZ9.                     
084609*                                                                         
084610     05  PV-SALES-DATE.                                                   
084620         10  PV-SALES-CENTURY        PIC X(02).                           
084630         10  PV-SALES-YEAR           PIC X(02).                           
084640         10  FILLER                  PIC X(01).                           
084650         10  PV-SALES-MONTH          PIC X(02).                           
084660         10  FILLER                  PIC X(01).                           
084670         10  PV-SALES-DAY            PIC X(02).                           
084680*                                                                         
084690     05  PV-TRANS-DATE.                                                   
084700         10  PV-TRANS-MONTH          PIC X(02).                           
084800         10  FILLER                  PIC X(01)     VALUE '/'.             
084900         10  PV-TRANS-DAY            PIC X(02).                           
085000         10  FILLER                  PIC X(01)     VALUE '/'.             
085001         10  PV-TRANS-YEAR           PIC X(02).                           
085002*                                                                         
085003     05  PV-PAGE-CNT                 PIC S9(5)     VALUE +0               
085004                                                   COMP-3.                
085005     05  PV-LINE-CNT                 PIC S9(3)     VALUE +0               
085006                                                   COMP-3.                
085007     05  PV-PARAGRAPH-NAME           PIC X(30)     VALUE SPACES.          
085008     05  PV-DB2-OPER-ATTEMPTED       PIC X(30)     VALUE SPACES.          
085009*                                                                         
085010 01  CD-CPU-DATE                      PIC 9(08).                          
085011 01  FILLER REDEFINES CD-CPU-DATE.                                        
085012     05  CD-CPU-YEAR                  PIC 9(04).                          
085013     05  CD-CPU-MON                   PIC 9(02).                          
085014     05  CD-CPU-DAY                   PIC 9(02).                          
085015*                                                                         
085016 01  CT-CPU-TIME.                                                         
085017     05  CT-CPU-HR                    PIC 9(02)     VALUE ZEROS.          
085018     05  CT-CPU-MIN                   PIC 9(02)     VALUE ZEROS.          
085019     05  CT-CPU-SEC                   PIC 9(02)     VALUE ZEROS.          
085020     05  CT-CPU-HSEC                  PIC 9(02)     VALUE ZEROS.          
085021*                                                                         
085022***********************                                                   
085023*  DB2 TABLE LAYOUTS  *                                                   
085024***********************                                                   
085025*                                                                         
085026     EXEC SQL                                                             
085027         INCLUDE TSKXREF                                                  
085028     END-EXEC.                                                            
085029*                                                                         
085030************************************                                      
085031*   ONHAND EDIT ERROR TYPE TABLE                                          
085032************************************                                      
085033     EXEC SQL                                                             
085034         INCLUDE FST00002                                                 
085035     END-EXEC.                                                            
085036*                                                                         
085037*****************************************                                 
085038*   ONHAND SALES AUDIT TEMPORARY TABLE                                    
085039*****************************************                                 
085040     EXEC SQL                                                             
085041         INCLUDE FST00005                                                 
085042     END-EXEC.                                                            
085043*                                                                         
085044*                                                                         
085045* DB2 COMMUNICATION AREA                                                  
085046*                                                                         
085047     EXEC SQL                                                             
085048         INCLUDE SQLCA                                                    
085049     END-EXEC.                                                            
085050*                                                                         
085051* DB2 COMMUNICATION AREA2                                                 
085052*                                                                         
085053     COPY SQLCA2.                                                         
085054 EJECT                                                                    
085055*                                                                         
085056* CURSOR FOR SALES AUDIT ERRORS                                           
085057*                                                                         
085058     EXEC SQL                                                             
085059       DECLARE SALES_AUD_ERR_CSR CURSOR FOR                               
085060         SELECT A.DEPT_NBR                                                
085061               ,B.CLASS                                                   
085062               ,A.SUB_CL_NBR                                              
085063               ,A.SLS_DTE                                                 
085064               ,A.LOC_NBR                                                 
085065               ,A.RGST_ID                                                 
085066               ,A.TRN_NBR                                                 
085067               ,A.POS_TRN_TYP_CDE                                         
085068               ,B.SKU                                                     
085069               ,A.SLD_QTY                                                 
085070               ,A.NET_CHRGD_AMT                                           
085120               ,C.ERR_TYP_DESC                                            
085200           FROM FSTT_OH_SLS_AUD A                                         
085300               ,TSKXREF B                                                 
085310               ,FST_OH_ERR_TYP C                                          
085400          WHERE A.ITM_NBR = B.ITM_NBR                                     
085410            AND A.ERR_TYP_CDE = C.ERR_TYP_CDE                             
085500            AND A.PRCG_DTE = :CTL-PROCESSING-DATE                         
085600            AND A.TRN_STAT_CDE = 'E'                                      
085700            AND B.SKU_STAT_CDE > '00'                                     
087600          ORDER BY A.DEPT_NBR                                             
087610                  ,B.CLASS                                                
087620                  ,A.SUB_CL_NBR                                           
087630                  ,A.SLS_DTE                                              
087640                  ,A.LOC_NBR                                              
087650                  ,B.SKU                                                  
087700       WITH UR                                                            
087800       FOR FETCH ONLY                                                     
087801     END-EXEC.                                                            
087802*                                                                         
087803*                                                                         
087804* CURSOR FOR INVALID ITEM NUMBERS                                         
087805*                                                                         
087806     EXEC SQL                                                             
087807       DECLARE INVALID_ITEM_CSR CURSOR FOR                                
087808         SELECT A.DEPT_NBR                                                
087809               ,A.MAJ_CL_NBR                                              
087810               ,A.SUB_CL_NBR                                              
087811               ,A.SLS_DTE                                                 
087812               ,A.LOC_NBR                                                 
087813               ,A.RGST_ID                                                 
087814               ,A.TRN_NBR                                                 
087815               ,A.POS_TRN_TYP_CDE                                         
087817               ,A.SLD_QTY                                                 
087818               ,A.NET_CHRGD_AMT                                           
087819               ,C.ERR_TYP_DESC                                            
087831           FROM FSTT_OH_SLS_AUD A                                         
087832               ,FST_OH_ERR_TYP C                                          
087833          WHERE A.ITM_NBR = 0                                             
087834            AND A.ERR_TYP_CDE = C.ERR_TYP_CDE                             
087835            AND A.PRCG_DTE = :CTL-PROCESSING-DATE                         
087840            AND A.TRN_STAT_CDE = 'E'                                      
087850          ORDER BY A.DEPT_NBR                                             
087851                  ,A.MAJ_CL_NBR                                           
087852                  ,A.SUB_CL_NBR                                           
087853                  ,A.SLS_DTE                                              
087854                  ,A.LOC_NBR                                              
087856       WITH UR                                                            
087857       FOR FETCH ONLY                                                     
087858     END-EXEC.                                                            
087859*                                                                         
087860*---------------------------------------------------------------*         
087861*   ERROR MESSAGE AREA FOR DB2                                            
087862*---------------------------------------------------------------*         
087863     COPY DPWS004.                                                        
087864*                                                                         
087865 EJECT                                                                    
087866*                                                                         
087867 LINKAGE SECTION.                                                         
087868*****************************************************************         
087869*PROCEDURE DIVISION.                                                      
087870*****************************************************************         
087871 PROCEDURE DIVISION.                                                      
087872*                                                                         
087873 A100-MAINLINE-PROCESSING.                                                
087874*                                                                         
087875     PERFORM B100-INITIALIZATION.                                         
087876*                                                                         
087877     PERFORM B200-PROCESS-SALES-AUD-CURSOR                                
087878             UNTIL EOF-SLS-AUD-CURSOR.                                    
087879*                                                                         
087880     PERFORM Z300-CLOSE-SALES-AUD-CURSOR.                                 
087881     PERFORM Z500-OPEN-INV-ITEM-CURSOR.                                   
087882*                                                                         
087883     PERFORM B300-PROCESS-INV-ITEM-CURSOR                                 
087884             UNTIL EOF-INV-ITEM-CURSOR.                                   
087885*                                                                         
087886     PERFORM B400-END-ROUTINE.                                            
087887*                                                                         
087888     STOP RUN.                                                            
087889*                                                                         
087890 EJECT                                                                    
087891*****************************************************************         
087892*                                                                         
087893*  - GET SYSTEM DATE & TIME AND MOVE TO REPORT HEADER                     
087894*  - READ CONTROL CARD WITH PROCESSING DATE                               
087895*  - OPEN CURSOR                                                          
087896*                                                                         
087897*****************************************************************         
087898 B100-INITIALIZATION.                                                     
087899*                                                                         
087900     ACCEPT CT-CPU-TIME FROM TIME.                                        
087901     ACCEPT CD-CPU-DATE FROM DATE YYYYMMDD.                               
087902*                                                                         
087903     MOVE CT-CPU-HR     TO DP132-RUN-HOUR.                                
087904     MOVE CT-CPU-MIN    TO DP132-RUN-MINUTE.                              
087905*                                                                         
087906     MOVE CD-CPU-MON    TO DP132-RUN-MONTH.                               
087907     MOVE CD-CPU-DAY    TO DP132-RUN-DAY.                                 
087908     MOVE CD-CPU-YEAR   TO DP132-RUN-YEAR.                                
087909*                                                                         
087910     MOVE 1  TO DP132-REPORT-NUMBER                                       
087911                DP132-PAGE-NUMBER.                                        
087912     MOVE PC-PROGRAM-NAME TO DP132-PROGRAM-NAME.                          
087913*                                                                         
087914     OPEN INPUT  CONTROL-FILE                                             
087915          OUTPUT PRINT-REPORT                                             
087915          OUTPUT OUTERROR-FILE.                                           
087916*                                                                         
087917     PERFORM Z100-READ-CONTROL-FILE.                                      
087920*                                                                         
088000     PERFORM Z200-OPEN-SALES-AUD-CURSOR.                                  
088001                                                                          
088002 EJECT                                                                    
088003*****************************************************************         
088004*                                                                         
088005*  - FETCH ROW FROM CURSOR                                                
088006*  - CHECK FOR BREAKS                                                     
088007*  - PRINT DETAIL LINE                                                    
088008*                                                                         
088009*****************************************************************         
088010 B200-PROCESS-SALES-AUD-CURSOR.                                           
088011*                                                                         
088012     PERFORM C100-FETCH-SALES-AUD-CSR.                                    
088013*                                                                         
088014     IF EOF-SLS-AUD-CURSOR                                                
088015        CONTINUE                                                          
088016     ELSE                                                                 
088017        IF FIRST-TIME-THRU                                                
088018           MOVE SKXREF-CLASS TO PV-HOLD-CLASS                             
088019           PERFORM C200-DEPT-MCL-BREAK                                    
088020           MOVE 'N' TO PS-FIRST-TIME-THRU                                 
088021        ELSE                                                              
088030           IF (FST00005-DEPT-NBR NOT = PV-PREV-DEPT) OR                   
088040              (SKXREF-CLASS NOT = PV-PREV-MAJ-CL)                         
088041               MOVE SKXREF-CLASS TO PV-HOLD-CLASS                         
088050               PERFORM C200-DEPT-MCL-BREAK                                
088060           ELSE                                                           
088070              IF (FST00005-SUB-CL-NBR NOT = PV-PREV-SUB-CL)               
088080                 PERFORM C300-SUBCLASS-BREAK                              
088090              END-IF                                                      
088100           END-IF                                                         
088200        END-IF                                                            
088300                                                                          
088310        MOVE SKXREF-SKU  TO PV-EDIT-SKU                                   
088320        MOVE PV-EDIT-SKU TO PV-HOLD-SKU                                   
088330                                                                          
088400        PERFORM C400-FORMAT-DETAIL-LINE                                   
088500     END-IF.                                                              
088501                                                                          
088502 EJECT                                                                    
088503*****************************************************************         
088504*                                                                         
088505*  - FETCH ROW FROM INVALID ITEM CURSOR                                   
088506*  - CHECK FOR BREAKS                                                     
088507*  - PRINT DETAIL LINE                                                    
088508*                                                                         
088509*****************************************************************         
088510 B300-PROCESS-INV-ITEM-CURSOR.                                            
088511*                                                                         
088512     PERFORM C500-FETCH-INV-ITEM-CSR.                                     
088513*                                                                         
088514     IF EOF-INV-ITEM-CURSOR                                               
088515        CONTINUE                                                          
088516     ELSE                                                                 
088517        IF FIRST-TIME-THRU                                                
088518           MOVE FST00005-MAJ-CL-NBR TO PV-HOLD-CLASS                      
088519           PERFORM C200-DEPT-MCL-BREAK                                    
088520           MOVE 'N' TO PS-FIRST-TIME-THRU                                 
088521        ELSE                                                              
088522           IF (FST00005-DEPT-NBR NOT = PV-PREV-DEPT) OR                   
088523              (FST00005-MAJ-CL-NBR NOT = PV-PREV-MAJ-CL)                  
088524               MOVE FST00005-MAJ-CL-NBR TO PV-HOLD-CLASS                  
088525               PERFORM C200-DEPT-MCL-BREAK                                
088526           ELSE                                                           
088527              IF (FST00005-SUB-CL-NBR NOT = PV-PREV-SUB-CL)               
088528                 PERFORM C300-SUBCLASS-BREAK                              
088529              END-IF                                                      
088530           END-IF                                                         
088531        END-IF                                                            
088532                                                                          
088533        MOVE 'INVALID' TO PV-HOLD-SKU                                     
088534                                                                          
088535        PERFORM C400-FORMAT-DETAIL-LINE                                   
088536     END-IF.                                                              
088537                                                                          
088538 EJECT                                                                    
088539*****************************************************************         
088540*                                                                         
088540*  - IF NO ERRORS FOUND, PRINT HEADERS & MESSAGE                          
088540*  - PRINT "END" MESSAGE                                                  
088541*  - PRINT CONTROL TOTALS, CLOSE FILES                                    
088542*                                                                         
088543*****************************************************************         
088544 B400-END-ROUTINE.                                                        
088545*                                                                         
           IF PV-RECS-PROCESSED = ZERO                                          
222261        ADD PC-1 TO PV-PAGE-CNT                                           
222262        MOVE PV-PAGE-CNT TO DP132-PAGE-NUMBER                             
222263*                                                                         
222264        WRITE PRINT-LINE FROM DP132-STANDRD-HEADER-132-COLS               
222265            AFTER ADVANCING PAGE                                          
222266*                                                                         
222267        WRITE PRINT-LINE FROM HEADING-LINE-2                              
222268           AFTER ADVANCING PC-1                                           
222266*                                                                         
222267        WRITE PRINT-LINE FROM NO-ERRORS-LINE                              
222268           AFTER ADVANCING PC-2                                           
           END-IF.                                                              
222269*                                                                         
088546     WRITE PRINT-LINE FROM END-OF-REPORT-LINE                             
088547         AFTER ADVANCING PC-2.                                            
088548*                                                                         
088549     MOVE PV-RECS-PROCESSED TO PV-DISP-RECS-PROC.                         
088549     MOVE PV-RECS-WRITTEN   TO PV-DISP-RECS-WRITE.                        
088550     DISPLAY '*************************************************'.         
088551     DISPLAY 'FSKRP123 - RECORDS PROCESSED: ' PV-DISP-RECS-PROC.          
088551     DISPLAY 'FSKRP123 - RECORDS WRITTEN : ' PV-DISP-RECS-WRITE.          
088552     DISPLAY '*************************************************'.         
088553*                                                                         
088560     CLOSE CONTROL-FILE                                                   
210700           PRINT-REPORT                                                   
                 OUTERROR-FILE.                                                 
210701*                                                                         
210702     PERFORM Z600-CLOSE-INV-ITEM-CURSOR.                                  
210703                                                                          
210704 EJECT                                                                    
211000*****************************************************************         
211100*                                                                         
211200*  - FETCH ROW FROM SALES AUDIT ERROR CURSOR                              
211600*                                                                         
211700*****************************************************************         
211800 C100-FETCH-SALES-AUD-CSR.                                                
211801*                                                                         
211802     EXEC SQL                                                             
211803         FETCH SALES_AUD_ERR_CSR                                          
211804          INTO :FST00005-DEPT-NBR,                                        
211805               :SKXREF-CLASS,                                             
211806               :FST00005-SUB-CL-NBR,                                      
211807               :FST00005-SLS-DTE,                                         
211808               :FST00005-LOC-NBR,                                         
211809               :FST00005-RGST-ID,                                         
211810               :FST00005-TRN-NBR,                                         
211811               :FST00005-POS-TRN-TYP-CDE,                                 
211812               :SKXREF-SKU,                                               
211813               :FST00005-SLD-QTY,                                         
211814               :FST00005-NET-CHRGD-AMT,                                   
211815               :FST00002-ERR-TYP-DESC                                     
211816     END-EXEC.                                                            
211817*                                                                         
211818     EVALUATE TRUE                                                        
211819         WHEN SQLCODE = ZERO                                              
211820             CONTINUE                                                     
211821         WHEN SQLCODE = +100                                              
211822             SET EOF-SLS-AUD-CURSOR TO TRUE                               
211823         WHEN SQLWARN0 NOT = SPACES                                       
211824         WHEN SQLCODE  NOT = ZERO                                         
211825             PERFORM Z999-SQL-ABEND                                       
211826     END-EVALUATE.                                                        
211827                                                                          
211828 EJECT                                                                    
211829*****************************************************************         
211830*                                                                         
211831*  - PAGE BREAK FOR NEW DEPT OR MAJOR CLASS                               
211832*                                                                         
211833*****************************************************************         
211834 C200-DEPT-MCL-BREAK.                                                     
211835*                                                                         
211836     MOVE FST00005-DEPT-NBR   TO HL3-DEPT.                                
211837     MOVE PV-HOLD-CLASS       TO HL3-MAJOR-CLASS.                         
211838     MOVE FST00005-SUB-CL-NBR TO HL3-SUBCLASS.                            
211839*                                                                         
211840     PERFORM Z400-PRINT-REPORT-HEADERS.                                   
211841*                                                                         
211850     MOVE FST00005-DEPT-NBR   TO PV-PREV-DEPT.                            
211860     MOVE PV-HOLD-CLASS       TO PV-PREV-MAJ-CL.                          
211870     MOVE FST00005-SUB-CL-NBR TO PV-PREV-SUB-CL.                          
211871*                                                                         
211872*****************************************************************         
211873*                                                                         
211874*  - PRINT DEPT/MAJOR CLASS/SUBCLASS LINE WHEN SUBCLASS CHANGES           
211875*                                                                         
211876*****************************************************************         
211877 C300-SUBCLASS-BREAK.                                                     
211878*                                                                         
211879     MOVE FST00005-SUB-CL-NBR TO HL3-SUBCLASS.                            
215900*                                                                         
216000     WRITE PRINT-LINE FROM HEADING-LINE-3                                 
216100         AFTER ADVANCING PC-2.                                            
216200*                                                                         
216300     MOVE FST00005-SUB-CL-NBR TO PV-PREV-SUB-CL.                          
216301                                                                          
216302 EJECT                                                                    
216303*****************************************************************         
216304*                                                                         
216305*  - MOVE DATA TO DETAIL LINE & PRINT                                     
216306*                                                                         
216307*****************************************************************         
216308 C400-FORMAT-DETAIL-LINE.                                                 
216309*                                                                         
216310     MOVE FST00005-SLS-DTE         TO PV-SALES-DATE.                      
216320     MOVE PV-SALES-YEAR            TO PV-TRANS-YEAR.                      
216330     MOVE PV-SALES-MONTH           TO PV-TRANS-MONTH.                     
216340     MOVE PV-SALES-DAY             TO PV-TRANS-DAY.                       
216350     MOVE PV-TRANS-DATE            TO DL1-TRANS-DATE.                     
216360*                                                                         
216370     MOVE FST00005-LOC-NBR         TO DL1-STORE-NBR.                      
216380     MOVE FST00005-RGST-ID         TO DL1-REGISTER-ID.                    
216390     MOVE FST00005-TRN-NBR         TO DL1-TRANS-NBR.                      
216400     MOVE FST00005-POS-TRN-TYP-CDE TO DL1-TRANS-TYPE.                     
216500     MOVE PV-HOLD-SKU              TO DL1-SKU-NBR.                        
216600     MOVE FST00005-SLD-QTY         TO DL1-QTY.                            
216601     MOVE FST00005-NET-CHRGD-AMT   TO DL1-AMOUNT.                         
216602     MOVE FST00002-ERR-TYP-DESC    TO DL1-ERROR-MSG.                      
      *                                                                         
216303*****************************************************************         
216305*  -  WRITE THE UNIT QTY ERROR TRANS IN A FILE                            
216307*****************************************************************         
           IF FST00002-ERR-TYP-DESC  = 'TRANSACTION UNIT QTY ERROR'             
 16350     MOVE PV-TRANS-DATE            TO TRANS-DATE                          
216370     MOVE FST00005-LOC-NBR         TO STORE-NBR                           
216380     MOVE FST00005-RGST-ID         TO REGISTER-ID                         
216390     MOVE FST00005-TRN-NBR         TO TRANS-NBR                           
216400     MOVE FST00005-POS-TRN-TYP-CDE TO TRANS-TYPE                          
216500     MOVE PV-HOLD-SKU              TO SKU-NBR                             
216600     MOVE FST00005-SLD-QTY         TO QTY                                 
216601     MOVE FST00005-NET-CHRGD-AMT   TO AMOUNT                              
216602     MOVE FST00002-ERR-TYP-DESC    TO ERROR-MSG                           
221100     ADD +1 TO PV-RECS-WRITTEN                                            
           END-IF.                                                              
216603*                                                                         
221000     PERFORM D100-PRINT-DETAIL-LINE.                                      
221000     PERFORM D110-WRITE-ERROR-LINE.                                       
221100     ADD +1 TO PV-RECS-PROCESSED.                                         
221101                                                                          
221102 EJECT                                                                    
221103*****************************************************************         
221104*                                                                         
221105*  - FETCH ROW FROM INVALID ITEM CURSOR                                   
221106*                                                                         
221107*****************************************************************         
221108 C500-FETCH-INV-ITEM-CSR.                                                 
221109*                                                                         
221110     EXEC SQL                                                             
221111         FETCH INVALID_ITEM_CSR                                           
221112          INTO :FST00005-DEPT-NBR,                                        
221113               :FST00005-MAJ-CL-NBR,                                      
221114               :FST00005-SUB-CL-NBR,                                      
221115               :FST00005-SLS-DTE,                                         
221116               :FST00005-LOC-NBR,                                         
221117               :FST00005-RGST-ID,                                         
221118               :FST00005-TRN-NBR,                                         
221119               :FST00005-POS-TRN-TYP-CDE,                                 
221120               :FST00005-SLD-QTY,                                         
221121               :FST00005-NET-CHRGD-AMT,                                   
221122               :FST00002-ERR-TYP-DESC                                     
221124     END-EXEC.                                                            
221125*                                                                         
221126     EVALUATE TRUE                                                        
221127         WHEN SQLCODE = ZERO                                              
221128             CONTINUE                                                     
221129         WHEN SQLCODE = +100                                              
221130             SET EOF-INV-ITEM-CURSOR TO TRUE                              
221131         WHEN SQLWARN0 NOT = SPACES                                       
221132         WHEN SQLCODE  NOT = ZERO                                         
221133             PERFORM Z999-SQL-ABEND                                       
221134     END-EVALUATE.                                                        
221135                                                                          
221136 EJECT                                                                    
221137*****************************************************************         
221138 D100-PRINT-DETAIL-LINE.                                                  
221139*                                                                         
221140     ADD PC-2 TO PV-LINE-CNT.                                             
221141*                                                                         
221142     IF PV-LINE-CNT > PC-MAX-LINES                                        
221143        PERFORM Z400-PRINT-REPORT-HEADERS                                 
221144        ADD PC-2 TO PV-LINE-CNT                                           
221145     END-IF.                                                              
221146*                                                                         
221147     WRITE PRINT-LINE FROM DETAIL-LINE-1                                  
221148         AFTER ADVANCING PC-2.                                            
221149*                                                                         
221150     MOVE SPACES TO PRINT-LINE.                                           
221151*                                                                         
221152                                                                          
221153 EJECT                                                                    
221154*****************************************************************         
221138 D110-WRITE-ERROR-LINE.                                                   
221139*                                                                         
221147     WRITE OUTERR-REC  FROM OUTPUT-ERR-REC.                               
221149*                                                                         
221152                                                                          
221153 EJECT                                                                    
221154*****************************************************************         
221155*                                                                         
221156*  - READ CONTROL CARD CONTAINING PROCESSING DATE                         
221157*                                                                         
221158*****************************************************************         
221159 Z100-READ-CONTROL-FILE.                                                  
221160*                                                                         
221161     READ CONTROL-FILE INTO CONTROL-DATA-IN                               
221162          AT END SET EOF-CONTROL-FILE TO TRUE.                            
221163*                                                                         
221170     IF EOF-CONTROL-FILE                                                  
221500        DISPLAY '** ABEND **'                                             
221600        DISPLAY '** PROGRAM    = ' PC-PROGRAM-NAME                        
221700        DISPLAY '** PARAGRAPH  = Z100-READ-CONTROL-FILE'                  
221800        DISPLAY '** MESSAGE    = CONTROL FILE DATE NOT FOUND'             
222000        MOVE +4003      TO ABEND-CODE                                     
222100        CALL 'ILBOABN0' USING ABEND-CODE                                  
222200     END-IF.                                                              
222201                                                                          
222202 EJECT                                                                    
222203******************************************************************        
222204 Z200-OPEN-SALES-AUD-CURSOR.                                              
222205*                                                                         
222206     MOVE 'Z200-OPEN-SALES-AUD-CURSOR' TO PV-PARAGRAPH-NAME.              
222207     MOVE 'OPEN CURSOR '               TO PV-DB2-OPER-ATTEMPTED.          
222208*                                                                         
222209     EXEC SQL                                                             
222210         OPEN SALES_AUD_ERR_CSR                                           
222211     END-EXEC.                                                            
222212                                                                          
222213     EVALUATE TRUE                                                        
222214         WHEN SQLCODE = ZERO                                              
222215             CONTINUE                                                     
222216         WHEN OTHER                                                       
222217             PERFORM Z999-SQL-ABEND                                       
222218     END-EVALUATE.                                                        
222219                                                                          
222220*                                                                         
222230******************************************************************        
222240 Z300-CLOSE-SALES-AUD-CURSOR.                                             
222241*                                                                         
222242     MOVE 'Z300-CLOSE-SALES-AUD-CURSOR' TO PV-PARAGRAPH-NAME.             
222243     MOVE 'CLOSE CURSOR '               TO PV-DB2-OPER-ATTEMPTED.         
222244*                                                                         
222245     EXEC SQL                                                             
222246         CLOSE SALES_AUD_ERR_CSR                                          
222247     END-EXEC.                                                            
222248                                                                          
222249     EVALUATE TRUE                                                        
222250         WHEN SQLCODE = ZERO                                              
222251             CONTINUE                                                     
222252         WHEN OTHER                                                       
222253             PERFORM Z999-SQL-ABEND                                       
222254     END-EVALUATE.                                                        
222255                                                                          
222256 EJECT                                                                    
222257*                                                                         
222258*****************************************************************         
222259 Z400-PRINT-REPORT-HEADERS.                                               
222260*                                                                         
222261     ADD PC-1 TO PV-PAGE-CNT.                                             
222262     MOVE PV-PAGE-CNT TO DP132-PAGE-NUMBER.                               
222263*                                                                         
222264     WRITE PRINT-LINE FROM DP132-STANDRD-HEADER-132-COLS                  
222265         AFTER ADVANCING PAGE.                                            
222266*                                                                         
222267     WRITE PRINT-LINE FROM HEADING-LINE-2                                 
222268         AFTER ADVANCING PC-1.                                            
222269*                                                                         
222270     WRITE PRINT-LINE FROM HEADING-LINE-3                                 
222271         AFTER ADVANCING PC-1.                                            
222272*                                                                         
222273     WRITE PRINT-LINE FROM HEADING-LINE-4                                 
222274         AFTER ADVANCING PC-2.                                            
222275*                                                                         
222276     WRITE PRINT-LINE FROM HEADING-LINE-5                                 
222277         AFTER ADVANCING PC-1.                                            
222278*                                                                         
222279     MOVE ZERO TO PV-LINE-CNT.                                            
222280                                                                          
222290 EJECT                                                                    
222291******************************************************************        
222292 Z500-OPEN-INV-ITEM-CURSOR.                                               
222293*                                                                         
222294     MOVE 'Z500-OPEN-INV-ITEM-CURSOR' TO PV-PARAGRAPH-NAME.               
222295     MOVE 'OPEN CURSOR '              TO PV-DB2-OPER-ATTEMPTED.           
222296*                                                                         
222297     EXEC SQL                                                             
222298         OPEN INVALID_ITEM_CSR                                            
222299     END-EXEC.                                                            
222300                                                                          
222301     EVALUATE TRUE                                                        
222302         WHEN SQLCODE = ZERO                                              
222303             CONTINUE                                                     
222304         WHEN OTHER                                                       
222305             PERFORM Z999-SQL-ABEND                                       
222306     END-EVALUATE.                                                        
222307                                                                          
222308*                                                                         
222309******************************************************************        
222310 Z600-CLOSE-INV-ITEM-CURSOR.                                              
222311*                                                                         
222312     MOVE 'Z600-CLOSE-INV-ITEM-CURSOR' TO PV-PARAGRAPH-NAME.              
222313     MOVE 'CLOSE CURSOR '              TO PV-DB2-OPER-ATTEMPTED.          
222314*                                                                         
222315     EXEC SQL                                                             
222316         CLOSE INVALID_ITEM_CSR                                           
222317     END-EXEC.                                                            
222318                                                                          
222319     EVALUATE TRUE                                                        
222320         WHEN SQLCODE = ZERO                                              
222321             CONTINUE                                                     
222322         WHEN OTHER                                                       
222323             PERFORM Z999-SQL-ABEND                                       
222324     END-EVALUATE.                                                        
222325                                                                          
222330*                                                                         
222400*****************************************************************         
222500 Z999-SQL-ABEND.                                                          
222600*                                                                         
222700     SET ABEND-4013            TO TRUE.                                   
222800     DISPLAY '**  ABEND     **'.                                          
222900     DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
223000     DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
223100     DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
223200                                                                          
223300*    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
223400*    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
223500                                                                          
223600     COPY DPPD004.                                                        
223700                                                                          
223800     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
223900*                                                                         
224000*****************************************************************         
