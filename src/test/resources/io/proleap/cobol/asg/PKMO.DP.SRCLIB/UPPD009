      ******************************************************************        
      *                                                                *        
      * UPPD009 - PROCEDURE DIVISION FOR MQ CICS/ONLINE PROGRAM        *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *                                                                *        
      * DESCRIPTION: THIS COPY BOOK IS TO CONN/DISCONN QUEUE MANAGER,  *        
      *              OPEN/CLOSE OF QUEUE, PUT/GET OPERATIONS ON QUEUE  *        
      *                                                                *        
      *----------------------------------------------------------------*        
      * HOW TO USE THIS COPYBOOK :                                     *        
      *                                                                *        
      *  1.  INCLUDE COPY BOOK UPPD009 IN THE MAIN PROGRAM.            *        
      *                                                                *        
      *----------------------------------------------------------------*        
      * CHANGE HISTORY:                                                *        
      * WR NUMBER     PROGRAMMER       DATE                            *        
      * ---------------------------------------------------------------*        
      * ORIGINAL VERSION               02/08/2013                      *        
      *                                                                *        
      ******************************************************************        
                                                                                
      *=================================================================02860000
      * READ CONTROL/CONNECT TO MANAGER/OPEN THE QUEUES.                02870000
      *=================================================================02880000
       M100-ESTABLISH-CONNECTION.                                               
            PERFORM 2300-CONNECT-TO-QMNGR.                                      
            PERFORM 2400-MQOPEN-FOR-PUT.                                        
            PERFORM 2500-MQOPEN-FOR-GET.                                        
                                                                                
      *=================================================================03960000
      * CONNECT TO THE QUEUE MANAGER                                    03970000
      *=================================================================03980000
       2300-CONNECT-TO-QMNGR.                                                   
           EXEC CICS                                                            
                ASSIGN APPLID (UP009-CICS-REGION)                               
                RESP (UP009-RESPONSE)                                           
           END-EXEC                                                             
                                                                                
           IF UP009-RESPONSE = DFHRESP(NORMAL)                                  
               EVALUATE UP009-CICS-REGION(5:1)                                  
                  WHEN 'P'                                                      
                  SET UP009-KOHLS-PROD-SYSTEM TO TRUE                           
                  SET UP009-PROD-QMGR-NAME   TO TRUE                            
                  WHEN 'Q'                                                      
                  SET UP009-KOHLS-QA-SYSTEM   TO TRUE                           
                  SET UP009-QA-QMGR-NAME      TO TRUE                           
                  WHEN 'T'                                                      
                  SET UP009-KOHLS-TEST-SYSTEM TO TRUE                           
                  SET UP009-TEST-QMGR-NAME    TO TRUE                           
               END-EVALUATE                                                     
           ELSE                                                                 
                  SET UP009-CICS-REGION-NOT-FOUND                               
                                              TO TRUE                           
           END-IF.                                                              
           MOVE UP009-QMGR-NAME      TO   UP009-QM-NAME.                04060000
           CALL UP009-MQCONN         USING       UP009-QM-NAME          04070000
                                                 UP009-HCONN            04080000
                                                 UP009-COMPLETION-CODE  04090000
                                                 UP009-REASON.          04100000
                                                                        04110000
           IF (UP009-COMPLETION-CODE NOT = MQCC-OK)                     05170000
              MOVE 'MQCONN ERROR'              TO UP009-ERROR-MESSAGE   04510000
              MOVE '2300-CONNECT-TO-QMNGR'     TO UP009-PARAGRAPH       04520000
              MOVE  UP009-REASON               TO UP009-SAVE-REASON     04530000
              MOVE  UP009-COMPLETION-CODE      TO UP009-SAVE-COMP-CODE  04540000
              PERFORM 3100-DISPLAY-MQ-ERROR                                     
           ELSE                                                                 
              SET  UP009-CONNECTED             TO TRUE                  04110000
           END-IF.                                                              
      *=================================================================03960000
      * OPEN THE STATIC QUEUE FOR PUT REQUEST OPERATION.                03970000
      *=================================================================03980000
       2400-MQOPEN-FOR-PUT.                                                     
           COMPUTE UP009-OPEN-OPTIONS =         MQOO-OUTPUT +           04330000
                                                MQOO-FAIL-IF-QUIESCING. 04350000
           MOVE   UP009-QM-NAME           TO    MQOD-OBJECTQMGRNAME.    04380000
           MOVE   UP009-SQ-NAME           TO    MQOD-OBJECTNAME.                
           CALL   UP009-MQOPEN            USING UP009-HCONN             04400000
                                                MQM-OBJECT-DESCRIPTOR   04410000
                                                UP009-OPEN-OPTIONS      04420000
                                                UP009-PQ-HANDLE         04430000
                                                UP009-COMPLETION-CODE   04440000
                                                UP009-REASON.           04450000
           IF (UP009-COMPLETION-CODE NOT = MQCC-OK) THEN                04500000
              MOVE 'MQOPEN ERROR'              TO UP009-ERROR-MESSAGE   04510000
              MOVE '2400-MQOPEN-FOR-PUT'       TO UP009-PARAGRAPH       04520000
              MOVE  UP009-REASON               TO UP009-SAVE-REASON     04530000
              MOVE  UP009-COMPLETION-CODE      TO UP009-SAVE-COMP-CODE  04540000
              PERFORM 3100-DISPLAY-MQ-ERROR                                     
           ELSE                                                         04570000
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
      *=================================================================03960000
      * OPEN THE DYNAMIC QUEUE FOR GET RESPONSE OPERATION.              03970000
      *=================================================================03980000
       2500-MQOPEN-FOR-GET.                                                     
           COMPUTE UP009-OPEN-OPTIONS    =    MQOO-INPUT-SHARED +       04330000
                                              MQOO-FAIL-IF-QUIESCING +  04350000
                                              MQOO-SAVE-ALL-CONTEXT.    04360000
           MOVE UP009-QM-NAME            TO   MQOD-OBJECTQMGRNAME.      04380000
           MOVE UP009-MODEL-NAME         TO   MQOD-OBJECTNAME.          04370000
           MOVE UP009-DYMODELQ-NAME      TO   MQOD-DYNAMICQNAME.        04370000
           CALL UP009-MQOPEN             USING UP009-HCONN              04400000
                                               MQM-OBJECT-DESCRIPTOR    04410000
                                               UP009-OPEN-OPTIONS       04420000
                                               UP009-GQ-HANDLE          04430000
                                               UP009-COMPLETION-CODE    04440000
                                               UP009-REASON.            04450000
           MOVE MQOD-OBJECTNAME           TO   UP009-REPLYTOQNAME.              
           IF (UP009-COMPLETION-CODE NOT = MQCC-OK) THEN                04500000
              MOVE 'MQOPEN ERROR'         TO   UP009-ERROR-MESSAGE      04510000
              MOVE '2500-MQOPEN-FOR-GET ' TO   UP009-PARAGRAPH          04520000
              MOVE UP009-REASON           TO   UP009-SAVE-REASON        04530000
              MOVE UP009-COMPLETION-CODE  TO   UP009-SAVE-COMP-CODE     04540000
              PERFORM 3100-DISPLAY-MQ-ERROR                             04560000
           ELSE                                                         04570000
              CONTINUE                                                  04600000
           END-IF.                                                              
                                                                                
      *=================================================================03960000
      * PROCESS MESSGES BY PERFORMING MQPUT AND MQGET OPERATIONS        03970000
      *=================================================================03980000
       2600-PROCESS-MESSAGES.                                                   
           IF UP009-REQ-MSGBUFFER = SPACES                                      
             MOVE '2600-PROCESS-MESSAGES :'   TO UP009-PARAGRAPH                
             MOVE 'REQUEST STRING IS EMPTY :' TO UP009-ERROR-MESSAGE            
             PERFORM 3500-EMPTY-REQ-STRING-ABEND                                
           END-IF.                                                              
           PERFORM 2700-PUT-MQ-MESSAGE.                                         
           PERFORM 2800-GET-MQ-MESSAGE.                                         
      *=================================================================03960000
      * PUT THE MESSAGE INTO THE OUTPUT STATIC QUEUE.                   03970000
      *=================================================================03980000
       2700-PUT-MQ-MESSAGE.                                             05190000
           INITIALIZE   UP009-DATA-LENGTH.                              05200000
           MOVE MQMI-NONE                   TO MQMD-MSGID.              05210000
           MOVE MQCI-NONE                   TO MQMD-CORRELID.           05220000
           COMPUTE MQPMO-CONTEXT            =  UP009-PQ-HANDLE.         05240000
           COMPUTE MQPMO-OPTIONS            =  MQPMO-NO-SYNCPOINT.      05250000
           MOVE MQMD-ENCODING               TO UP009-ENCODING.          05280000
           MOVE MQMD-CODEDCHARSETID         TO UP009-CODEDCHARSETID.    05290000
           MOVE UP009-ENCODING2             TO MQMD-ENCODING.           05320000
           MOVE UP009-CODEDCHARSETID2       TO MQMD-CODEDCHARSETID.     05330000
           MOVE UP009-QM-NAME               TO MQOD-OBJECTQMGRNAME.     05350000
           MOVE UP009-SQ-NAME               TO MQOD-OBJECTNAME.         05360000
           MOVE MQFMT-STRING                TO MQMD-FORMAT.             05400000
           MOVE UP009-REPLYTOQNAME          TO MQMD-REPLYTOQ.                   
           MOVE UP009-QM-NAME               TO MQMD-REPLYTOQMGR         05410000
           MOVE MQMT-REQUEST                TO MQMD-MSGTYPE.                    
           INSPECT  UP009-REQ-MSGBUFFER                                 05450000
           TALLYING UP009-DATA-LENGTH                                   05460000
           FOR CHARACTERS BEFORE INITIAL '~@~NULL'.                     05470000
           ADD 7 TO UP009-DATA-LENGTH.                                  05480000
           CALL UP009-MQPUT              USING UP009-HCONN              05510000
                                               UP009-PQ-HANDLE          05520000
                                               MQM-MESSAGE-DESCRIPTOR   05530000
                                               MQM-PUT-MESSAGE-OPTIONS  05540000
                                               UP009-DATA-LENGTH        05550000
                                               UP009-REQ-MSGBUFFER      05560000
                                               UP009-COMPLETION-CODE    05570000
                                               UP009-REASON.            05580000
           IF (UP009-COMPLETION-CODE NOT = MQCC-OK)    THEN             05630000
              IF (UP009-REASON NOT = MQRC-PAGESET-FULL AND              05640000
                  UP009-REASON NOT = MQRC-Q-FULL       AND              05650000
                  UP009-REASON NOT = MQRC-MSG-TOO-BIG-FOR-Q AND         05660000
                  UP009-REASON NOT = MQRC-Q-SPACE-NOT-AVAILABLE )       05670000
                MOVE 'MQPUT ERROR'               TO UP009-ERROR-MESSAGE 05700000
                MOVE '2700-PUT-MQ-MESSAGE.     ' TO UP009-PARAGRAPH     05690000
                MOVE UP009-REASON                TO UP009-SAVE-REASON   05710000
                MOVE UP009-COMPLETION-CODE       TO UP009-SAVE-COMP-CODE05720000
                PERFORM 3100-DISPLAY-MQ-ERROR                           05750000
              ELSE                                                      05760000
                 IF UP009-REASON = MQRC-MSG-TOO-BIG-FOR-Q               05840000
                    CONTINUE                                            05850000
                 END-IF                                                 06080000
              END-IF                                                    06090000
           ELSE                                                         06120000
              SET     UP009-MQPUT-SUCCESSFUL     TO TRUE                06130000
      *       PERFORM 3000-COMMIT-MQ-MESSAGE                            06140000
           END-IF.                                                      06150000
           MOVE UP009-ENCODING                   TO MQMD-ENCODING.      06180000
           MOVE UP009-CODEDCHARSETID             TO MQMD-CODEDCHARSETID.06190000
                                                                        06200000
      *=================================================================03960000
      * GET THE MESSAGE FROM THE INPUT DYNAMIC QUEUE.                   03970000
      *=================================================================03980000
       2800-GET-MQ-MESSAGE.                                             06990000
            INITIALIZE UP009-RES-MSGBUFFER.                             07010000
            MOVE MQMI-NONE                TO MQMD-MSGID.                07040000
            MOVE MQCI-NONE                TO MQMD-CORRELID.             07050000
            COMPUTE MQGMO-OPTIONS         =  MQGMO-CONVERT   +          07080000
                                             MQGMO-SYNCPOINT +          07090000
                                             MQGMO-WAIT.                07100000
            MOVE UP009-WAIT-TIME          TO MQGMO-WAITINTERVAL.        07120000
            MOVE UP009-QM-NAME            TO MQOD-OBJECTQMGRNAME.       04380000
            MOVE UP009-MODEL-NAME         TO MQOD-OBJECTNAME.           07060000
            CALL UP009-MQGET              USING UP009-HCONN             07140000
                                                UP009-GQ-HANDLE         07150000
                                                MQM-MESSAGE-DESCRIPTOR  07160000
                                                MQM-GET-MESSAGE-OPTIONS 07170000
                                                UP009-MSGLENGTH         07180000
                                                UP009-RES-MSGBUFFER     07190000
                                                UP009-DATA-LENGTH       07200000
                                                UP009-COMPLETION-CODE   07210000
                                                UP009-REASON.           07220000
           IF (UP009-COMPLETION-CODE NOT = MQCC-OK)     OR              07270000
                 (UP009-REASON = MQRC-Q-MGR-QUIESCING)  OR              07280000
                 (UP009-REASON = MQRC-Q-MGR-STOPPING )                  07290000
                 MOVE 'MQGET ERROR'         TO UP009-ERROR-MESSAGE      07340000
                 MOVE '2800-GET-MQ-MESSAGE' TO UP009-PARAGRAPH          07350000
                 MOVE UP009-REASON          TO UP009-SAVE-REASON        07360000
                 MOVE UP009-COMPLETION-CODE TO UP009-SAVE-COMP-CODE     07370000
                 PERFORM 3100-DISPLAY-MQ-ERROR                          07390000
           END-IF.                                                      07580000
                                                                                
      ****************************************************************  06570000
      * CLOSE ROUTINE                                                   06580000
      ****************************************************************  06590000
       2900-CLOSE-MQ-ROUTINE.                                           06600000
           CALL UP009-MQCLOSE           USING UP009-HCONN               06620000
                                              UP009-CLOSE-HANDLE        06630000
                                              MQCO-NONE                 06640000
                                              UP009-COMPLETION-CODE     06650000
                                              UP009-REASON.             06660000
           IF (UP009-COMPLETION-CODE NOT = MQCC-OK)                     11040000
             MOVE '2900-CLOSE-MQ-ROUTINE ' TO UP009-PARAGRAPH           H 110500
             MOVE 'MQCLOSE ERROR'          TO UP009-ERROR-MESSAGE       07340000
             MOVE UP009-REASON             TO UP009-SAVE-REASON         07360000
             MOVE UP009-COMPLETION-CODE    TO UP009-SAVE-COMP-CODE      07370000
             PERFORM 3100-DISPLAY-MQ-ERROR                              11070000
           END-IF.                                                      11080000
                                                                        06910000
      ****************************************************************  06210000
      * COMMIT MQ MESSAGES                                              06220000
      ****************************************************************  06230000
       3000-COMMIT-MQ-MESSAGE.                                          06240000
                                                                        06250000
           EXEC CICS                                                    09530000
               SYNCPOINT                                                09540000
           END-EXEC.                                                    09550000
                                                                                
      ****************************************************************  07760000
      * DISPLAY THE VALUES OF A FAILED MQ API COMMAND, AND ABEND PGM    07770000
      ****************************************************************  07780000
       3100-DISPLAY-MQ-ERROR.                                           07790000
                                                                        07800000
           CALL UP009-MQCHECK USING UP009-SAVE-REASON                   11720000
                                    UP009-REASON-TEXT.                          
                                                                        11740000
           MOVE UP009-PARAGRAPH        TO DP013-PARAGRAPH.              11780000
           MOVE UP009-ERROR-MESSAGE    TO DP013-MESSAGE-TEXT (1).       11790000
           MOVE UP009-SAVE-REASON      TO DP013-MESSAGE-TEXT (2).       06820000
           MOVE UP009-SAVE-COMP-CODE   TO DP013-MESSAGE-TEXT (3).       06830000
           MOVE UP009-REASON-TEXT      TO DP013-MESSAGE-TEXT (4).       11810000
W3502S     SET UP009-ERROR-FOUND       TO TRUE.                                 
                                                                                
      ****************************************************************  06570000
      * CHECK WEB SERVICE RESPONSE                                      06580000
      ****************************************************************  06590000
       3200-CHECK-WS-RESPONSE.                                                  
           UNSTRING UP009-RES-MSGBUFFER DELIMITED BY UP009-WS-SIGN              
           INTO     UP009-RESPONSE1,                                            
                    UP009-RESPONSE2,                                            
                    UP009-RESPONSE-MSG.                                         
           IF (UP009-RESPONSE1 NOT = '00000')     OR                            
              (UP009-RESPONSE2 NOT = '00000')                                   
              MOVE UP009-RESPONSE-MSG       TO    UP009-ERROR-MESSAGE           
              MOVE '3200-CHECK-WS-RESPONSE' TO    UP009-PARAGRAPH               
              PERFORM 3300-WSDL-ABEND                                           
           END-IF.                                                              
      ******************************************************************07760000
      * ABEND ROUTINE IF ERROR FROM WEBSERVICE                          07770000
      ******************************************************************07780000
       3300-WSDL-ABEND.                                                         
           MOVE UP009-PARAGRAPH   TO DP013-PARAGRAPH.                   11780000
           MOVE UP009-RESPONSE1   TO DP013-MESSAGE-TEXT (1)             11780000
           MOVE UP009-RESPONSE2   TO DP013-MESSAGE-TEXT (2)             11780000
           MOVE UP009-ERROR-MESSAGE                                     11790000
                                  TO DP013-MESSAGE-TEXT (3).            11800000
           MOVE UP009-REASON-TEXT                                       11810000
                                  TO DP013-MESSAGE-TEXT (4).            11820000
W3502S     SET UP009-ERROR-FOUND  TO TRUE.                                      
      ******************************************************************07760000
      * CLOSE ALL THE OPEN QUEUES                                       07770000
      ******************************************************************07780000
       3400-CLOSE-MQ-CONNECTION.                                                
           MOVE UP009-PQ-HANDLE TO UP009-CLOSE-HANDLE.                          
           PERFORM 2900-CLOSE-MQ-ROUTINE.                                       
           MOVE UP009-GQ-HANDLE TO UP009-CLOSE-HANDLE.                          
           PERFORM 2900-CLOSE-MQ-ROUTINE.                                       
           SET UP009-DISCONNECTED   TO TRUE.                                    
      ******************************************************************07760000
      * EMPTY REQ STRING ABEND PARAGRAPH                                07770000
      ******************************************************************07780000
       3500-EMPTY-REQ-STRING-ABEND.                                             
           MOVE UP009-PARAGRAPH   TO DP013-PARAGRAPH.                   11780000
           MOVE '** MQSERIES ERROR **'                                  11790000
                                  TO DP013-MESSAGE-TEXT (1).            11800000
           MOVE UP009-ERROR-MESSAGE                                     11810000
                                  TO DP013-MESSAGE-TEXT (2).            11820000
W3502S     SET UP009-ERROR-FOUND  TO TRUE.                                      
      ******************************************************************07760000
      * REQUEST STRING FORMATION.                                       07770000
      ******************************************************************07780000
       3700-CREATE-REQ-STRING.                                                  
           INITIALIZE UP009-MESSAGE-STRING.                                     
           STRING                                                               
           UP009-QUERY-ID                                                       
           DELIMITED BY SPACE                                                   
           UP001-TILDE-SIGN                                                     
           DELIMITED BY SPACE                                                   
           UP009-JOB-NUMBER                                                     
           DELIMITED BY SPACE                                                   
           UP001-TILDE-SIGN                                                     
           DELIMITED BY SPACE                                                   
           UP009-ACTION                                                         
           DELIMITED BY SPACE                                                   
           UP001-TILDE-SIGN                                                     
           DELIMITED BY SPACE                                                   
           INTO                                                                 
           UP009-REQ-MSGBUFFER.                                                 
      *----------------------SELECT CLAUSE------------------------------        
           MOVE 1 TO  UP009-TEMP-CNT.                                           
           PERFORM UNTIL UP009-TEMP-CNT > UP009-SELECT-CNT                      
              STRING                                                            
              UP009-REQ-MSGBUFFER                                               
              DELIMITED BY SPACE                                                
              UP009-SELECT(UP009-TEMP-CNT)                                      
              DELIMITED BY SPACE                                                
              INTO                                                              
              UP009-REQ-MSGBUFFER                                               
                                                                                
               IF UP009-TEMP-CNT < UP009-SELECT-CNT                             
                 STRING                                                         
                 UP009-REQ-MSGBUFFER                                            
                 DELIMITED BY SPACE                                             
                 UP001-AND-SIGN                                                 
                 DELIMITED BY SPACE                                             
                 INTO                                                           
                 UP009-REQ-MSGBUFFER                                            
               ELSE                                                             
                 STRING                                                         
                 UP009-REQ-MSGBUFFER                                            
                 DELIMITED BY SPACE                                             
                 UP001-TILDE-SIGN                                               
                 DELIMITED BY SPACE                                             
                 UP009-WHERE-SIGN                                               
                 DELIMITED BY SIZE                                              
                 INTO                                                           
                 UP009-REQ-MSGBUFFER                                            
               END-IF                                                           
           ADD 1 TO UP009-TEMP-CNT                                              
           END-PERFORM.                                                         
      *----------------------WHERE CLAUSE-------------------------------        
           MOVE 1 TO  UP009-TEMP-CNT.                                           
           PERFORM UNTIL UP009-TEMP-CNT > UP009-WHERE-CNT                       
              STRING                                                            
              UP009-REQ-MSGBUFFER                                               
              DELIMITED BY UP009-WHERE-SIGN                                     
              UP009-WHERE(UP009-TEMP-CNT)                                       
              DELIMITED BY SPACE                                                
              UP001-EQUAL-SIGN                                                  
              DELIMITED BY SPACE                                                
              UP009-WHERE-VALUE(UP009-TEMP-CNT)                                 
              DELIMITED BY SIZE                                                 
              UP009-WHERE-SIGN                                                  
              DELIMITED BY SPACE                                                
              INTO                                                              
              UP009-REQ-MSGBUFFER                                               
                                                                                
              IF UP009-TEMP-CNT < UP009-WHERE-CNT                               
                STRING                                                          
                UP009-REQ-MSGBUFFER                                             
                DELIMITED BY UP009-WHERE-SIGN                                   
                UP001-PIPE-SIGN                                                 
                DELIMITED BY SPACE                                              
                UP009-WHERE-SIGN                                                
                DELIMITED BY SIZE                                               
                INTO                                                            
                UP009-REQ-MSGBUFFER                                             
              ELSE                                                              
                STRING                                                          
                UP009-REQ-MSGBUFFER                                             
                DELIMITED BY UP009-WHERE-SIGN                                   
                UP001-END-OF-QUERY                                              
                DELIMITED BY SPACE                                              
                INTO                                                            
                UP009-REQ-MSGBUFFER                                             
              END-IF                                                            
           ADD 1 TO UP009-TEMP-CNT                                              
           END-PERFORM.                                                         
           PERFORM 2600-PROCESS-MESSAGES.                               02640000
           PERFORM 3200-CHECK-WS-RESPONSE.                                      
           UNSTRING UP009-RES-MSGBUFFER     DELIMITED BY X"00" OR               
                    UP009-WS-SIGN           OR UP001-TILDE-SIGN                 
           INTO     UP009-RESPONSE1,                                            
                    UP009-RESPONSE2,                                            
                    UP009-QUERY-ID,                                             
                    UP009-JOB-NUMBER-X,                                         
                    UP009-STATUS,                                               
                    UP009-ROWS-RETURNED,                                        
                    UP009-MQ-RESPONSE.                                          
           MOVE     UP009-STATUS       TO   UP009-RESPONSE-TYPE.                
           IF UP009-ROWS-RETURNED = 1  AND  UP009-SUCCESS                       
             MOVE 1  TO UP009-STRING-POINTER                                    
             MOVE 1  TO UP009-TEMP-CNT                                          
             PERFORM UNTIL  UP009-TEMP-CNT > UP009-SELECT-CNT                   
               UNSTRING UP009-MQ-RESPONSE   DELIMITED BY                        
                        UP001-AND-SIGN  OR  UP001-PERCENT-SIGN                  
               INTO     UP009-SELECT-VALUE(UP009-TEMP-CNT)                      
               WITH POINTER UP009-STRING-POINTER                                
               ADD 1 TO UP009-TEMP-CNT                                          
             END-PERFORM                                                        
           END-IF.                                                              
