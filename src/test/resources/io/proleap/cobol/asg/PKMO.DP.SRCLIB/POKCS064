00001 *-----------------------*                                         10/06/94
00002  IDENTIFICATION DIVISION.                                         POKCS064
00003 *-----------------------*                                            LV036
00004  PROGRAM-ID.    POKCS064.                                         POKCS064
00005  AUTHOR.        TIM SMITH.                                        POKCS064
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          POKCS064
00007  DATE-WRITTEN.  09-16-94.                                         POKCS064
00008  DATE-COMPILED.                                                   POKCS064
00009 *----------------------------------------------------------------*POKCS064
00010 *    W064 - DC AGING CRITERIA SPEC                               *POKCS064
00011 *                                                                *POKCS064
00012 *    THIS PROGRAM ALLOWS THE USER ENTER DATA TO BE USED BY A     *POKCS064
00013 *    SUBSEQUENT PROGRAM THAT CREATES A LIST OF AGED IN-PROCESS   *   CL*35
00014 *    RECEIPTS.                                                   *   CL*35
00015 *----------------------------------------------------------------*   CL*35
076319* DATE  10/27/2021                                                        
076320*            - CHANGE CALL OF 'DPKUT100' TO                     *         
076321*              CALL DP010I-NUMERIC-EDIT-ROUTINE                 *         
076322*              MAKING THE CALL DYNAMIC VS STATIC                *         
076323*     MADE BY: GERMAN BANDA            WR/IR #: CHG0263126                
076312*                                                               *         
076318*---------------------------------------------------------------*         
CICS  *  CHANGED 11/14/2022    BY BARB SCHULTZ    WR: CHG0278485      *         
CICS  *                                                               *         
CICS  *  ADDED COPYBOOK DPWS930 AND MADE THE CICS ARCHITECTURE CALL   *         
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.    *         
CICS  *---------------------------------------------------------------*         
00016 *                                                                    CL*35
00017  ENVIRONMENT DIVISION.                                            POKCS064
00018 *                                                                    CL*35
00019  DATA DIVISION.                                                   POKCS064
00020 *                                                                    CL*35
00021  WORKING-STORAGE SECTION.                                         POKCS064
00022 *                                                                 POKCS064
00023  01  PC-PROGRAM-CONSTANTS.                                        POKCS064
00024      05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'PO064A  '. POKCS064
00025      05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'POKM064 '. POKCS064
00026      05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'POKCS064'. POKCS064
00027      05  PC-MAX-RETRIES            PIC  9(01)  VALUE  3.             CL**7
00028      05  PC-TSYMSG-NUMBERS.                                       POKCS064
00029          10  PC-TSYMSG-00005       PIC  9(05)  VALUE  00005.         CL*26
00030          10  PC-TSYMSG-00008       PIC  9(05)  VALUE  00008.         CL*26
00031          10  PC-TSYMSG-00956       PIC  9(05)  VALUE  00956.         CL*18
00032          10  PC-TSYMSG-00958       PIC  9(05)  VALUE  00958.         CL*18
00033          10  PC-TSYMSG-00959       PIC  9(05)  VALUE  00959.         CL*27
00034 *                                                                 POKCS064
00035  01  PV-PROGRAM-VARIABLES.                                           CL**7
00036      05  PV-RETRY-COUNTER          PIC  9(03)  VALUE 0               CL**7
00037                                                COMP-3.               CL**7
00038      05  PV-DB2-COUNT              PIC S9(09)  VALUE +0              CL**7
00039                                                COMP-3.               CL**7
00040      05  PV-NUM3                   PIC  9(03)  VALUE 0.              CL*15
00041 *                                                                    CL**7
00042  01  PS-PROGRAM-SWITCHES.                                         POKCS064
00043      05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.        POKCS064
00044          88  PS-ERROR                          VALUE  'Y'.        POKCS064
00045          88  PS-NO-ERROR                       VALUE  'N'.        POKCS064
00046                                                                   POKCS064
00047 *----------------------------------------------------------------*POKCS064
00048 *    MAP LAYOUT                                                  *POKCS064
00049 *----------------------------------------------------------------*POKCS064
00050                                                                   POKCS064
00051      COPY POKM064.                                                POKCS064
00052      EJECT                                                        POKCS064
00053 *----------------------------------------------------------------*POKCS064
00054 *    ATTRIBUTE SETTINGS COPYBOOK.                                *POKCS064
00055 *----------------------------------------------------------------*POKCS064
00056                                                                   POKCS064
00057      COPY DPWS015.                                                POKCS064
00058                                                                   POKCS064
00059 *----------------------------------------------------------------*POKCS064
00060 *    FUNCTION KEYS COPYBOOK                                      *POKCS064
00061 *----------------------------------------------------------------*POKCS064
00062                                                                   POKCS064
00063      COPY DPWS016.                                                POKCS064
00064  EJECT                                                            POKCS064
00065 ******************************************************************POKCS064
00066 ** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **POKCS064
00067 ** SUB-ROUTINE (DPKUT100).                                      **POKCS064
00068 ******************************************************************POKCS064
00069      COPY DPWS010I.                                               POKCS064
00070  EJECT                                                            POKCS064
00071 ******************************************************************POKCS064
CICS  *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          **POKCS064
00074 ******************************************************************POKCS064
00075      COPY DPWS030.                                                POKCS064
CICS       COPY DPWS930.                                                POKCS064
00076  EJECT                                                            POKCS064
00077 ***************************************************************** POKCS064
00078 *    STANDARD COMMAREA.                                           POKCS064
00079 ***************************************************************** POKCS064
00080                                                                   POKCS064
00081      COPY DPWS020.                                                POKCS064
00082      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                POKCS064
00083                                                                   POKCS064
00084 ***************************************************************** POKCS064
00085 *    INTER-APPLICATION COMMAREA.                                  POKCS064
00086 ***************************************************************** POKCS064
00087                                                                   POKCS064
00088          10  INTER-APPL-COMM-AREA PIC X(200).                     POKCS064
00089      COPY POWS064.                                                   CL*36
00090 ***************************************************************** POKCS064
00091 *    SPECIFIC COMMMAREA FOR POKCS064.                             POKCS064
00092 ***************************************************************** POKCS064
00093          10  ASC-SPECIFIC-COMMAREA.                               POKCS064
00094              15  ASC-DMM-LNAME               PIC  X(18).             CL*14
00095              15  ASC-DMM-FNAME               PIC  X(12).             CL*14
00096              15  ASC-BUYER-LNAME             PIC  X(18).             CL*14
00097              15  ASC-BUYER-FNAME             PIC  X(12).             CL*14
00098              15  ASC-DEPT-NBR                PIC  X(03).             CL*18
00099              15  ASC-DAYS1                   PIC  X(03).             CL*24
00100              15  ASC-DAYS1-N REDEFINES ASC-DAYS1                     CL*33
00101                                              PIC  9(03).             CL*33
00102              15  ASC-DAYS2                   PIC  X(03).             CL*24
00103              15  ASC-DAYS2-N REDEFINES ASC-DAYS2                     CL*33
00104                                              PIC  9(03).             CL*33
00105          10  FILLER                          PIC  X(2796).           CL*17
00106      EJECT                                                        POKCS064
00107 *----------------------------------------------------------------*POKCS064
00108 *    ABEND PROCESSING COPYBOOK.                                  *POKCS064
00109 *----------------------------------------------------------------*POKCS064
00110                                                                   POKCS064
00111      COPY DPWS013.                                                POKCS064
00112                                                                   POKCS064
00113                                                                      CL**3
00114  EJECT                                                            POKCS064
00115  LINKAGE SECTION.                                                 POKCS064
00116                                                                   POKCS064
00117  01  DFHCOMMAREA.                                                 POKCS064
00118      05  FILLER                         OCCURS  1 TO 4072 TIMES   POKCS064
00119                                         DEPENDING ON EIBCALEN.    POKCS064
00120          10  FILLER                     PIC X.                    POKCS064
00121                                                                   POKCS064
00122                                                                   POKCS064
00123  PROCEDURE DIVISION.                                              POKCS064
00124 ***************************************************************** POKCS064
00125 ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. ** POKCS064
00126 ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  ** POKCS064
00127 ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            ** POKCS064
00128 **                                                             ** POKCS064
00129 ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  ** POKCS064
00130 ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       ** POKCS064
00131 ***************************************************************** POKCS064
00132  0000-MAIN-MODULE.                                                POKCS064
00133      INITIALIZE DP030-CICS-API-FIELDS.                            POKCS064
00134      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       POKCS064
00135      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          POKCS064
00136      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         POKCS064
00137      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       POKCS064
00138      MOVE LENGTH OF PO064AI        TO DP030-MAP-LENGTH (1).          CL**2
00139      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     POKCS064
00140      PERFORM 0001-CALL-CICS-ARCH-API.                             POKCS064
00141 *                                                                 POKCS064
00142      PERFORM 1000-CONTROL-PROCESSING                              POKCS064
00143 *                                                                 POKCS064
00144      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     POKCS064
00145      PERFORM 0001-CALL-CICS-ARCH-API.                             POKCS064
00146                                                                   POKCS064
00147  0001-CALL-CICS-ARCH-API.                                         POKCS064
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                      POKCS064
00149                                     DFHCOMMAREA                   POKCS064
00150                                     DP030-CICS-API-FIELDS         POKCS064
00151                                     DP020-STANDARD-COMMAREA       POKCS064
00152                                     PO064AI.                         CL**8
00153 *                                                                 POKCS064
00154      IF  DP030-RC-CALL-SUCCESSFUL                                 POKCS064
00155          CONTINUE                                                 POKCS064
00156      ELSE                                                         POKCS064
00157          SET DP013-NO-ROLLBACK                                    POKCS064
00158              DP013-XCTL-DISPLAY-RESTART                           POKCS064
00159              DP013-CICS-ABEND      TO TRUE                        POKCS064
CICS           MOVE '0001-CALL-CICS-ARCH-API'                           POKCS064
00161                                    TO DP013-PARAGRAPH             POKCS064
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE OPOKCS064
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)      POKCS064
00164          MOVE DP030-RETURN-CODE                                   POKCS064
00165                                    TO DP013-MESSAGE-TEXT (3)      POKCS064
00166          PERFORM DP013-0000-PROCESS-ABEND                         POKCS064
00167      END-IF.                                                      POKCS064
00168  EJECT                                                            POKCS064
00169 *----------------------------------------------------------------*POKCS064
00170 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *POKCS064
00171 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *POKCS064
00172 *----------------------------------------------------------------*POKCS064
00173                                                                   POKCS064
00174  1000-CONTROL-PROCESSING.                                         POKCS064
00175                                                                   POKCS064
00176      EVALUATE TRUE                                                POKCS064
00177          WHEN DP020-NEXT-ACT-INITIAL                              POKCS064
00178              INITIALIZE ASC-SPECIFIC-COMMAREA                     POKCS064
00179              PERFORM 4000-BUILD-INITIAL-PANEL                     POKCS064
00180                                                                   POKCS064
00181          WHEN DP020-NEXT-ACT-READ-MAP                             POKCS064
00182              PERFORM 2000-FKEY-PROCESSING                         POKCS064
00183                                                                   POKCS064
00184          WHEN DP020-NEXT-ACT-RETURN                               POKCS064
00185              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 POKCS064
00186              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   POKCS064
00187                                                                   POKCS064
00188          WHEN OTHER                                               POKCS064
00189              SET DP013-LOGIC-ABEND                                POKCS064
00190                  DP013-NO-ROLLBACK TO TRUE                        POKCS064
00191              MOVE '1000-CONTROL-PROCESSING'                       POKCS064
00192                                    TO  DP013-PARAGRAPH            POKCS064
00193              MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   POKCS064
00194                                    TO  DP013-MESSAGE-TEXT(1)      POKCS064
00195              MOVE DP020-NEXT-APPL-ACTIVITY                        POKCS064
00196                                    TO DP013-MESSAGE-TEXT(2)       POKCS064
00197              PERFORM DP013-0000-PROCESS-ABEND                     POKCS064
00198      END-EVALUATE.                                                POKCS064
00199                                                                   POKCS064
00200 *----------------------------------------------------------------*POKCS064
00201 * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*POKCS064
00202 * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*POKCS064
00203 * EDITS.                                                         *POKCS064
00204 *----------------------------------------------------------------*POKCS064
00205                                                                   POKCS064
00206  2000-FKEY-PROCESSING.                                            POKCS064
00207                                                                   POKCS064
00208      EVALUATE TRUE                                                POKCS064
00209                                                                   POKCS064
00210          WHEN DP020-SRC-AID = DP016-CLEAR                         POKCS064
00211              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   POKCS064
00212              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 POKCS064
00213                                                                   POKCS064
00214          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     POKCS064
00215              INITIALIZE ASC-SPECIFIC-COMMAREA                     POKCS064
00216              PERFORM 4000-BUILD-INITIAL-PANEL                     POKCS064
00217              PERFORM 3200-RESET-ATTRIBUTES                        POKCS064
00218                                                                   POKCS064
00219          WHEN OTHER                                               POKCS064
00220              PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 POKCS064
00221              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   POKCS064
00222              IF  PS-NO-ERROR                                      POKCS064
00223                  SET DP030-CONTINUE-GO TO TRUE                    POKCS064
00224                  PERFORM 2100-CHECK-FUNCTION-KEY                  POKCS064
00225              ELSE                                                 POKCS064
00226                  SET DP030-OVERRIDE-GO TO TRUE                    POKCS064
00227              END-IF                                               POKCS064
00228      END-EVALUATE.                                                POKCS064
00229 ******************************************************************POKCS064
00230 ** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **POKCS064
00231 ** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **POKCS064
00232 ** FROM THE CICS ARCHITECTURE API.                              **POKCS064
00233 ******************************************************************POKCS064
00234  2100-CHECK-FUNCTION-KEY.                                         POKCS064
00235      EVALUATE TRUE                                                POKCS064
00236          WHEN DP020-FK-LOCAL-GOTO (DP020-SRC-AID)                 POKCS064
00237              PERFORM 2110-PREPARE-INTER-APPL-COMM                 POKCS064
00238                                                                   POKCS064
00239          WHEN DP020-SRC-AID = DP016-ENTER                         POKCS064
00240              CONTINUE                                             POKCS064
00241                                                                   POKCS064
00242          WHEN OTHER                                               POKCS064
00243              SET DP013-NO-ROLLBACK                                POKCS064
00244                  DP013-XCTL-DISPLAY-RESTART                       POKCS064
00245                  DP013-CICS-ABEND  TO TRUE                        POKCS064
00246              MOVE '2100-CHECK-FUNCTION-KEY'                       POKCS064
00247                                    TO DP013-PARAGRAPH             POKCS064
00248              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      POKCS064
00249                                    TO DP013-MESSAGE-TEXT (1)      POKCS064
00250              PERFORM DP013-0000-PROCESS-ABEND                     POKCS064
00251      END-EVALUATE.                                                POKCS064
00252 ******************************************************************POKCS064
00253 ** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **POKCS064
00254 ** INTER-APPLICATION COMMAREA.                                  **POKCS064
00255 ******************************************************************POKCS064
00256  2110-PREPARE-INTER-APPL-COMM.                                    POKCS064
00257      INITIALIZE PO064-INTER-APPL-COMMAREA.                           CL*10
00258      MOVE ASC-DMM-LNAME            TO PO064-DMM-LNAME.               CL*14
00259      MOVE ASC-DMM-FNAME            TO PO064-DMM-FNAME.               CL*32
00260                                                                      CL*29
00261      MOVE ASC-BUYER-LNAME          TO PO064-BUYER-LNAME.             CL*14
00262      MOVE ASC-BUYER-FNAME          TO PO064-BUYER-FNAME.             CL*14
00263      MOVE ASC-DEPT-NBR             TO PO064-DEPT-NBR.                CL*14
00264      IF ASC-DAYS1 = SPACES                                           CL*34
00265          MOVE 0                    TO PO064-DAYS1                    CL*34
00266      ELSE                                                            CL*34
00267          MOVE ASC-DAYS1-N          TO PO064-DAYS1                    CL*34
00268      END-IF.                                                         CL*34
00269      IF ASC-DAYS2 = SPACES                                           CL*34
00270          MOVE 0                    TO PO064-DAYS2                    CL*34
00271      ELSE                                                            CL*34
00272          MOVE ASC-DAYS2-N          TO PO064-DAYS2                    CL*34
00273      END-IF.                                                         CL*34
00274  EJECT                                                            POKCS064
00275 *----------------------------------------------------------------*POKCS064
00276 * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*POKCS064
00277 * THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *POKCS064
00278 * APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *POKCS064
00279 *----------------------------------------------------------------*POKCS064
00280                                                                   POKCS064
00281  2200-MOVE-SCREEN-TO-COMMAREA.                                    POKCS064
00282      IF  ADMMLL > ZERO                                               CL*14
00283          MOVE ADMMLI               TO ASC-DMM-LNAME                  CL*14
00284      ELSE                                                         POKCS064
00285          IF  ADMMLF = DP015-ERASE-EOF                                CL*14
00286              MOVE SPACE            TO ASC-DMM-LNAME                  CL*14
00287          END-IF                                                   POKCS064
00288      END-IF.                                                      POKCS064
00289                                                                   POKCS064
00290      IF  ADMMFL > ZERO                                               CL*14
00291          MOVE ADMMFI               TO ASC-DMM-FNAME                  CL*20
00292      ELSE                                                            CL*14
00293          IF  ADMMFF = DP015-ERASE-EOF                                CL*14
00294              MOVE SPACE            TO ASC-DMM-FNAME                  CL*14
00295          END-IF                                                      CL*14
00296      END-IF.                                                         CL*14
00297                                                                      CL*14
00298      IF  ABUYLL > ZERO                                               CL*14
00299          MOVE ABUYLI               TO ASC-BUYER-LNAME                CL*14
00300      ELSE                                                            CL*14
00301          IF  ABUYLF = DP015-ERASE-EOF                                CL*23
00302              MOVE SPACE            TO ASC-BUYER-LNAME                CL*14
00303          END-IF                                                      CL*14
00304      END-IF.                                                         CL*14
00305                                                                      CL*14
00306      IF  ABUYFL > ZERO                                               CL*14
00307          MOVE ABUYFI               TO ASC-BUYER-FNAME                CL*14
00308      ELSE                                                            CL*14
00309          IF  ABUYFF = DP015-ERASE-EOF                                CL*23
00310              MOVE SPACE            TO ASC-BUYER-FNAME                CL*14
00311          END-IF                                                      CL*14
00312      END-IF.                                                         CL*14
00313                                                                      CL*14
00314      IF  ADEPTL > ZERO                                               CL*14
00315          MOVE ADEPTI               TO ASC-DEPT-NBR                   CL*14
00316      ELSE                                                         POKCS064
00317          IF  ADEPTF = DP015-ERASE-EOF                                CL*14
00318              MOVE SPACE            TO ASC-DEPT-NBR                   CL*14
00319          END-IF                                                   POKCS064
00320      END-IF.                                                      POKCS064
00321                                                                   POKCS064
00322      IF  ADAYS1L > ZERO                                              CL*16
00323          MOVE ADAYS1I              TO ASC-DAYS1                      CL*16
00324      ELSE                                                         POKCS064
00325          IF  ADAYS1F = DP015-ERASE-EOF                               CL*16
00326              MOVE ZERO             TO ASC-DAYS1                      CL*16
00327          END-IF                                                   POKCS064
00328      END-IF.                                                      POKCS064
00329                                                                      CL*16
00330      IF  ADAYS2L > ZERO                                              CL*16
00331          MOVE ADAYS2I              TO ASC-DAYS2                      CL*16
00332      ELSE                                                            CL*16
00333          IF  ADAYS2F = DP015-ERASE-EOF                               CL*16
00334              MOVE ZERO             TO ASC-DAYS2                      CL*16
00335          END-IF                                                      CL*16
00336      END-IF.                                                         CL*16
00337                                                                   POKCS064
00338 *----------------------------------------------------------------*POKCS064
00339 * ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *POKCS064
00340 * CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *POKCS064
00341 * A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*POKCS064
00342 * IN THE DESTINATION PROGRAM, NOT HERE.                          *POKCS064
00343 *----------------------------------------------------------------*POKCS064
00344                                                                   POKCS064
00345  3000-EDIT-DATA-IN-COMMAREA.                                      POKCS064
00346      PERFORM 3200-RESET-ATTRIBUTES.                               POKCS064
00347 *                                                                    CL**3
00348      IF  ASC-DEPT-NBR > SPACE                                        CL*14
00349          MOVE ASC-DEPT-NBR         TO DP010I-UNEDITED-FIELD          CL*14
00350          MOVE LENGTH OF ASC-DEPT-NBR TO DP010I-MAXIMUM-DIGITS        CL*14
00351          MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS        CL*14
00352          SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                     CL*14
26422          CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
26423                          USING DP010I-NUMERIC-EDIT-AREA                   
00354          IF  DP010I-ERROR-DETECTED                                   CL*14
00355              SET PS-ERROR                                            CL*14
00356                  DP020-MSG-FATAL   TO TRUE                           CL*14
00357              MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER               CL*14
00358              MOVE DP015-UNP-ALP-BRT-OFF                              CL*14
00359                                    TO ADEPTA                         CL*14
00360              MOVE DP015-RED        TO ADEPTC                         CL*14
00361              MOVE DP015-REVERSE    TO ADEPTH                         CL*14
00362              MOVE -1               TO ADEPTL                         CL*14
00363              SET DP030-SET-CURSOR-APPL-1 TO TRUE                     CL*14
00364          ELSE                                                        CL*14
00365              MOVE DP010I-NUMERIC-FIELD                               CL*14
00366                                    TO PV-NUM3                        CL*15
00367              MOVE PV-NUM3          TO ASC-DEPT-NBR                   CL*15
00368                                       ADEPTO                         CL*14
00369          END-IF                                                      CL*14
00370      END-IF.                                                         CL*14
00371 *                                                                    CL*14
00372      IF  ASC-DAYS1 > SPACE                                           CL*16
00373          MOVE ASC-DAYS1            TO DP010I-UNEDITED-FIELD          CL*16
00374          MOVE LENGTH OF ASC-DAYS1  TO DP010I-MAXIMUM-DIGITS          CL*16
00375          MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS        CL**3
00376          SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                     CL**3
26422          CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
26423                          USING DP010I-NUMERIC-EDIT-AREA                   
00378          IF  DP010I-ERROR-DETECTED                                   CL**3
00379              SET PS-ERROR                                            CL**3
00380                  DP020-MSG-FATAL   TO TRUE                           CL**3
00381              MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER               CL**3
00382              MOVE DP015-UNP-ALP-BRT-OFF                              CL**3
00383                                    TO ADAYS1A                        CL*16
00384              MOVE DP015-RED        TO ADAYS1C                        CL*16
00385              MOVE DP015-REVERSE    TO ADAYS1H                        CL*16
00386              MOVE -1               TO ADAYS1L                        CL*16
00387              SET DP030-SET-CURSOR-APPL-1 TO TRUE                     CL**3
00388          ELSE                                                        CL**3
00389              MOVE DP010I-NUMERIC-FIELD                               CL**3
00390                                    TO PV-NUM3                        CL*25
00391              MOVE PV-NUM3          TO ASC-DAYS1-N                    CL*33
00392                                       ADAYS1O                        CL*16
00393          END-IF                                                      CL**3
00394      END-IF.                                                         CL**3
00395 *                                                                    CL**3
00396      IF  ASC-DAYS2 > SPACE                                           CL*16
00397          MOVE ASC-DAYS2            TO DP010I-UNEDITED-FIELD          CL*16
00398          MOVE LENGTH OF ASC-DAYS2  TO DP010I-MAXIMUM-DIGITS          CL*16
00399          MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS        CL*16
00400          SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                     CL*16
26422          CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
26423                          USING DP010I-NUMERIC-EDIT-AREA                   
00402          IF  DP010I-ERROR-DETECTED                                   CL*16
00403              SET PS-ERROR                                            CL*16
00404                  DP020-MSG-FATAL   TO TRUE                           CL*16
00405              MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER               CL*16
00406              MOVE DP015-UNP-ALP-BRT-OFF                              CL*16
00407                                    TO ADAYS2A                        CL*16
00408              MOVE DP015-RED        TO ADAYS2C                        CL*16
00409              MOVE DP015-REVERSE    TO ADAYS2H                        CL*16
00410              MOVE -1               TO ADAYS2L                        CL*16
00411              SET DP030-SET-CURSOR-APPL-1 TO TRUE                     CL*16
00412          ELSE                                                        CL*16
00413              MOVE DP010I-NUMERIC-FIELD                               CL*16
00414                                    TO PV-NUM3                        CL*25
00415              MOVE PV-NUM3          TO ASC-DAYS2-N                    CL*33
00416                                       ADAYS2O                        CL*16
00417          END-IF                                                      CL*16
00418      END-IF.                                                         CL*16
00419 *                                                                    CL*16
00420 * IF BOTH DAY FIELDS ARE ENTERED, DAYS2 MUST BE GREATER THAN         CL*35
00421 * DAYS1.                                                             CL*35
00422 *                                                                    CL*35
00423      IF ASC-DAYS1 = SPACES OR ASC-DAYS2 = SPACES                     CL*31
00424          CONTINUE                                                    CL*31
00425      ELSE                                                            CL*31
00426          IF ASC-DAYS2 > ASC-DAYS1                                    CL*31
00427              CONTINUE                                                CL*31
00428          ELSE                                                        CL*31
00429              SET PS-ERROR                                            CL*31
00430                  DP020-MSG-FATAL   TO TRUE                           CL*31
00431              MOVE PC-TSYMSG-00005  TO DP020-MSG-NUMBER               CL*31
00432              MOVE DP015-UNP-ALP-BRT-OFF                              CL*31
00433                                    TO ADAYS2A                        CL*26
00434              MOVE DP015-RED        TO ADAYS2C                        CL*31
00435              MOVE DP015-REVERSE    TO ADAYS2H                        CL*31
00436              MOVE -1               TO ADAYS2L                        CL*31
00437              SET DP030-SET-CURSOR-APPL-1 TO TRUE                     CL*31
00438          END-IF                                                      CL*31
00439      END-IF.                                                         CL*31
00440 *                                                                    CL*18
00441 * AT LEASE ONE OF THE DAY FIELDS MUST BE ENTERED.                    CL*35
00442 *                                                                    CL*35
00443      IF ASC-DAYS1 NOT > '000' AND                                    CL*28
00444         ASC-DAYS2 NOT > '000'                                        CL*28
00445          SET PS-ERROR                                                CL*28
00446              DP020-MSG-FATAL       TO TRUE                           CL*28
00447          MOVE PC-TSYMSG-00959      TO DP020-MSG-NUMBER               CL*28
00448          MOVE DP015-UNP-ALP-BRT-OFF                                  CL*28
00449                                    TO ADAYS1A                        CL*28
00450          MOVE DP015-RED            TO ADAYS1C                        CL*28
00451          MOVE DP015-REVERSE        TO ADAYS1H                        CL*28
00452          MOVE -1                   TO ADAYS1L                        CL*28
00453          MOVE DP015-UNP-ALP-BRT-OFF                                  CL*28
00454                                    TO ADAYS2A                        CL*28
00455          MOVE DP015-RED            TO ADAYS2C                        CL*28
00456          MOVE DP015-REVERSE        TO ADAYS2H                        CL*28
00457          MOVE -1                   TO ADAYS2L                        CL*28
00458          SET DP030-SET-CURSOR-APPL-1 TO TRUE                         CL*28
00459      END-IF.                                                         CL*28
00460 *                                                                    CL*28
00461 ******************************************************************   CL*18
00462 * VALIDATE THAT MORE THAN ONE FIELD IS NOT ENTERED.              *   CL*18
00463 ******************************************************************   CL*18
00464      IF ASC-DMM-LNAME > SPACES OR ASC-DMM-FNAME > SPACES             CL*18
00465          IF ASC-BUYER-LNAME > SPACES                                 CL*18
00466              SET PS-ERROR                                            CL*18
00467                  DP020-MSG-FATAL   TO TRUE                           CL*18
00468              MOVE PC-TSYMSG-00956  TO DP020-MSG-NUMBER               CL*18
00469              MOVE DP015-UNP-ALP-BRT-OFF                              CL*18
00470                                    TO ABUYLA                         CL*18
00471              MOVE DP015-RED        TO ABUYLC                         CL*18
00472              MOVE DP015-REVERSE    TO ABUYLH                         CL*18
00473              MOVE -1               TO ABUYLL                         CL*18
00474              SET DP030-SET-CURSOR-APPL-1 TO TRUE                     CL*18
00475          ELSE                                                        CL*18
00476              IF ASC-BUYER-FNAME > SPACES                             CL*18
00477                  SET PS-ERROR                                        CL*18
00478                      DP020-MSG-FATAL                                 CL*18
00479                                    TO TRUE                           CL*18
00480                  MOVE PC-TSYMSG-00956  TO DP020-MSG-NUMBER           CL*18
00481                  MOVE DP015-UNP-ALP-BRT-OFF                          CL*18
00482                                    TO ABUYFA                         CL*18
00483                  MOVE DP015-RED    TO ABUYFC                         CL*18
00484                  MOVE DP015-REVERSE TO ABUYFH                        CL*18
00485                  MOVE -1           TO ABUYFL                         CL*18
00486                  SET DP030-SET-CURSOR-APPL-1 TO TRUE                 CL*18
00487              ELSE                                                    CL*18
00488                  IF ASC-DEPT-NBR > SPACES                            CL*19
00489                      SET PS-ERROR                                    CL*18
00490                          DP020-MSG-FATAL                             CL*18
00491                                    TO TRUE                           CL*18
00492                      MOVE PC-TSYMSG-00956 TO DP020-MSG-NUMBER        CL*18
00493                      MOVE DP015-UNP-ALP-BRT-OFF                      CL*18
00494                                    TO ADEPTA                         CL*18
00495                      MOVE DP015-RED                                  CL*18
00496                                    TO ADEPTC                         CL*18
00497                      MOVE DP015-REVERSE TO ADEPTH                    CL*18
00498                      MOVE -1       TO ADEPTL                         CL*18
00499                      SET DP030-SET-CURSOR-APPL-1 TO TRUE             CL*18
00500                  END-IF                                              CL*18
00501              END-IF                                                  CL*18
00502          END-IF                                                      CL*18
00503      ELSE                                                            CL*18
00504      IF ASC-BUYER-LNAME > SPACES OR ASC-BUYER-FNAME > SPACES         CL*18
00505          IF ASC-DMM-LNAME > SPACES                                   CL*18
00506              SET PS-ERROR                                            CL*18
00507                  DP020-MSG-FATAL   TO TRUE                           CL*18
00508              MOVE PC-TSYMSG-00956  TO DP020-MSG-NUMBER               CL*18
00509              MOVE DP015-UNP-ALP-BRT-OFF                              CL*18
00510                                    TO ADMMLA                         CL*18
00511              MOVE DP015-RED        TO ADMMLC                         CL*18
00512              MOVE DP015-REVERSE    TO ADMMLH                         CL*18
00513              MOVE -1               TO ADMMLL                         CL*18
00514              SET DP030-SET-CURSOR-APPL-1 TO TRUE                     CL*18
00515          ELSE                                                        CL*18
00516              IF ASC-DMM-FNAME > SPACES                               CL*18
00517                  SET PS-ERROR                                        CL*18
00518                      DP020-MSG-FATAL                                 CL*18
00519                                    TO TRUE                           CL*18
00520                  MOVE PC-TSYMSG-00956  TO DP020-MSG-NUMBER           CL*18
00521                  MOVE DP015-UNP-ALP-BRT-OFF                          CL*18
00522                                    TO ADMMFA                         CL*18
00523                  MOVE DP015-RED    TO ADMMFC                         CL*18
00524                  MOVE DP015-REVERSE TO ADMMFH                        CL*18
00525                  MOVE -1           TO ADMMFL                         CL*18
00526                  SET DP030-SET-CURSOR-APPL-1 TO TRUE                 CL*18
00527              ELSE                                                    CL*18
00528                  IF ASC-DEPT-NBR > SPACES                            CL*19
00529                      SET PS-ERROR                                    CL*18
00530                          DP020-MSG-FATAL                             CL*18
00531                                    TO TRUE                           CL*18
00532                      MOVE PC-TSYMSG-00956 TO DP020-MSG-NUMBER        CL*18
00533                      MOVE DP015-UNP-ALP-BRT-OFF                      CL*18
00534                                    TO ADEPTA                         CL*18
00535                      MOVE DP015-RED                                  CL*18
00536                                    TO ADEPTC                         CL*18
00537                      MOVE DP015-REVERSE TO ADEPTH                    CL*18
00538                      MOVE -1       TO ADEPTL                         CL*18
00539                      SET DP030-SET-CURSOR-APPL-1 TO TRUE             CL*18
00540                  END-IF                                              CL*18
00541              END-IF                                                  CL*18
00542          END-IF                                                      CL*18
00543      END-IF.                                                         CL*20
00544 *                                                                    CL*18
00545      IF ASC-DEPT-NBR > SPACES                                        CL*22
00546          IF ASC-DMM-LNAME > SPACES                                   CL*22
00547              SET PS-ERROR                                            CL*22
00548                  DP020-MSG-FATAL   TO TRUE                           CL*22
00549              MOVE PC-TSYMSG-00956  TO DP020-MSG-NUMBER               CL*22
00550              MOVE DP015-UNP-ALP-BRT-OFF                              CL*22
00551                                    TO ADMMLA                         CL*22
00552              MOVE DP015-RED        TO ADMMLC                         CL*22
00553              MOVE DP015-REVERSE    TO ADMMLH                         CL*22
00554              MOVE -1               TO ADMMLL                         CL*22
00555              SET DP030-SET-CURSOR-APPL-1 TO TRUE                     CL*22
00556          ELSE                                                        CL*22
00557              IF ASC-DMM-FNAME > SPACES                               CL*22
00558                  SET PS-ERROR                                        CL*22
00559                      DP020-MSG-FATAL                                 CL*22
00560                                    TO TRUE                           CL*22
00561                  MOVE PC-TSYMSG-00956  TO DP020-MSG-NUMBER           CL*22
00562                  MOVE DP015-UNP-ALP-BRT-OFF                          CL*22
00563                                    TO ADMMFA                         CL*22
00564                  MOVE DP015-RED    TO ADMMFC                         CL*22
00565                  MOVE DP015-REVERSE TO ADMMFH                        CL*22
00566                  MOVE -1           TO ADMMFL                         CL*22
00567                  SET DP030-SET-CURSOR-APPL-1 TO TRUE                 CL*22
00568              ELSE                                                    CL*22
00569                  IF ASC-BUYER-LNAME > SPACES                         CL*22
00570                      SET PS-ERROR                                    CL*22
00571                          DP020-MSG-FATAL   TO TRUE                   CL*22
00572                      MOVE PC-TSYMSG-00956  TO DP020-MSG-NUMBER       CL*22
00573                      MOVE DP015-UNP-ALP-BRT-OFF                      CL*22
00574                                    TO ABUYLA                         CL*22
00575                      MOVE DP015-RED        TO ABUYLC                 CL*22
00576                      MOVE DP015-REVERSE    TO ABUYLH                 CL*22
00577                      MOVE -1               TO ABUYLL                 CL*22
00578                      SET DP030-SET-CURSOR-APPL-1 TO TRUE             CL*22
00579                 ELSE                                                 CL*22
00580                     IF ASC-BUYER-FNAME > SPACES                      CL*22
00581                         SET PS-ERROR                                 CL*22
00582                             DP020-MSG-FATAL                          CL*22
00583                                    TO TRUE                           CL*22
00584                         MOVE PC-TSYMSG-00956  TO DP020-MSG-NUMBER    CL*22
00585                         MOVE DP015-UNP-ALP-BRT-OFF                   CL*22
00586                                    TO ABUYFA                         CL*22
00587                         MOVE DP015-RED    TO ABUYFC                  CL*22
00588                         MOVE DP015-REVERSE TO ABUYFH                 CL*22
00589                         MOVE -1           TO ABUYFL                  CL*22
00590                         SET DP030-SET-CURSOR-APPL-1 TO TRUE          CL*22
00591                     END-IF                                           CL*22
00592                 END-IF                                               CL*22
00593              END-IF                                                  CL*22
00594          END-IF                                                      CL*22
00595      END-IF.                                                         CL*22
00596 *                                                                    CL*22
00597 *  FIRST NAME CANNOT BE ENTERED WITHOUT LAST NAME                    CL*35
00598 *                                                                    CL*35
00599      IF ASC-DMM-FNAME > SPACES AND ASC-DMM-LNAME = SPACES            CL*20
00600          SET PS-ERROR                                                CL*20
00601              DP020-MSG-FATAL       TO TRUE                           CL*20
00602          MOVE PC-TSYMSG-00958      TO DP020-MSG-NUMBER               CL*20
00603          MOVE DP015-UNP-ALP-BRT-OFF                                  CL*20
00604                                    TO ADMMLA                         CL*20
00605          MOVE DP015-RED            TO ADMMLC                         CL*20
00606          MOVE DP015-REVERSE        TO ADMMLH                         CL*20
00607          MOVE -1                   TO ADMMLL                         CL*20
00608          SET DP030-SET-CURSOR-APPL-1 TO TRUE                         CL*20
00609      END-IF.                                                         CL*20
00610 *                                                                    CL*35
00611 *  FIRST NAME CANNOT BE ENTERED WITHOUT LAST NAME                    CL*35
00612 *                                                                    CL*35
00613      IF ASC-BUYER-FNAME > SPACES AND ASC-BUYER-LNAME = SPACES        CL*20
00614          SET PS-ERROR                                                CL*20
00615              DP020-MSG-FATAL       TO TRUE                           CL*20
00616          MOVE PC-TSYMSG-00958      TO DP020-MSG-NUMBER               CL*20
00617          MOVE DP015-UNP-ALP-BRT-OFF                                  CL*20
00618                                    TO ABUYLA                         CL*20
00619          MOVE DP015-RED            TO ABUYLC                         CL*20
00620          MOVE DP015-REVERSE        TO ABUYLH                         CL*20
00621          MOVE -1                   TO ABUYLL                         CL*20
00622          SET DP030-SET-CURSOR-APPL-1 TO TRUE                         CL*20
00623      END-IF.                                                         CL*20
00624 *                                                                    CL*20
00625      IF  PS-NO-ERROR                                              POKCS064
00626          MOVE -1                   TO ADMMLL                         CL*14
00627          SET DP030-SET-CURSOR-APPL-1                              POKCS064
00628                                    TO TRUE                        POKCS064
00629      END-IF.                                                      POKCS064
00630 ******************************************************************POKCS064
00631 ** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **POKCS064
00632 ** MAP.                                                         **POKCS064
00633 ******************************************************************POKCS064
00634  3200-RESET-ATTRIBUTES.                                           POKCS064
00635      MOVE DP015-UNP-ALP-NOR-OFF    TO ADMMLA                         CL*15
00636                                       ADMMFA                         CL*15
00637                                       ABUYLA                         CL*15
00638                                       ABUYFA                         CL*15
00639                                       ADEPTA                         CL*15
00640                                       ADAYS1A                        CL*16
00641                                       ADAYS2A.                       CL*16
00642      MOVE DP015-GREEN              TO ADMMLC                         CL*15
00643                                       ADMMFC                         CL*15
00644                                       ABUYLC                         CL*15
00645                                       ABUYFC                         CL*15
00646                                       ADEPTC                         CL*15
00647                                       ADAYS1C                        CL*16
00648                                       ADAYS2C.                       CL*16
00649      MOVE DP015-UNDERLINE          TO ADMMLH                         CL*15
00650                                       ADMMFH                         CL*15
00651                                       ABUYLH                         CL*15
00652                                       ABUYFH                         CL*15
00653                                       ADEPTH                         CL*15
00654                                       ADAYS1H                        CL*16
00655                                       ADAYS2H.                       CL*16
00656                                                                      CL*15
00657 *----------------------------------------------------------------*POKCS064
00658 *  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *POKCS064
00659 *  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *POKCS064
00660 *  IT HERE.                                                      *POKCS064
00661 *----------------------------------------------------------------*POKCS064
00662  4000-BUILD-INITIAL-PANEL.                                        POKCS064
00663      SET DP030-SET-CURSOR-APPL-1   TO TRUE                        POKCS064
00664      MOVE -1                       TO ADMMLL.                        CL*15
00665 *                                                                 POKCS064
00666 *----------------------------------------------------------------*POKCS064
00667 *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *POKCS064
00668 *----------------------------------------------------------------*POKCS064
00669  4400-MOVE-COMMAREA-TO-SCREEN.                                    POKCS064
00670      MOVE ASC-DMM-LNAME            TO ADMMLO.                        CL*14
00671      MOVE ASC-DMM-FNAME            TO ADMMFO.                        CL*14
00672      MOVE ASC-BUYER-LNAME          TO ABUYLO.                        CL*14
00673      MOVE ASC-BUYER-FNAME          TO ABUYFO.                        CL*14
00674      MOVE ASC-DEPT-NBR             TO ADEPTO.                        CL*14
00675      MOVE ASC-DAYS1                TO ADAYS1O.                       CL*16
00676  EJECT                                                            POKCS064
00677 *----------------------------------------------------------------*POKCS064
00678 *    ABEND PROCESSOR MODULE                                      *POKCS064
00679 *----------------------------------------------------------------*POKCS064
00680                                                                   POKCS064
00681      COPY DPPD013.                                                POKCS064
