       IDENTIFICATION DIVISION.                                         00010014
       PROGRAM-ID.    XSKUP008.                                         00020014
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030014
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040014
       DATE-WRITTEN.  09/29/2002.                                       00050014
       DATE-COMPILED.                                                   00060014
      ******************************************************************00070014
      **       VSM ITEM MESSAGE - NUMBER 031 - STYLE-SKU DELETE         00080014
      ******************************************************************00090014
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO DELETE A SKU FROM THE  00100014
      ***     TABLE XST_SKU.  IT IS A CASCADING DELETE THAT CAUSES ALL  00110014
      ***     ROWS FOR THE SKU TO BE DELETED FROM THE XST_SKU_ATTR,     00120014
      ***     XST_UPC, XST_SKU_LOC AND XST_SKU_LOC_ATTR ALSO.           00130014
      ***                                                               00131018
      ***     A ROW IS INSERTED TO THE TEMPORARY DELETED SKU TABLE FOR  00132018
      ***     INTERNAL UPC NUMBER.  ANY VENDOR UPC NUMBERS ARE FETCHED  00133018
      ***     AND A ROW IS INSERTED FOR THESE ALSO.  THIS IS DONE PRIOR 00134023
      ***     TO THE SKU DELETE.                                        00135023
      ******************************************************************00140014
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00150014
      **02/28/2003 CHANGED BY:SANDY POTTER  REMOVE UPC DELETE (CASCADES)00160014
      **01/06/2004 CHANGED BY:SANDY POTTER  ADD DELETED SKU TABLE LOGIC 00161018
      ******************************************************************00170014
       ENVIRONMENT DIVISION.                                            00180014
       CONFIGURATION SECTION.                                           00190014
       SOURCE-COMPUTER.  IBM-3090.                                      00200014
       OBJECT-COMPUTER.  IBM-3090.                                      00210014
       DATA DIVISION.                                                   00220014
                                                                        00230014
       WORKING-STORAGE SECTION.                                         00240014
      ******************************************************************00250014
      **PROGRAM SWITCHES                                                00260014
      ******************************************************************00270014
       01  PS-PROGRAM-SWITCHES.                                         00280014
           05  PS-PROCESS-COMPLETE-SW           PIC X(01) VALUE 'Y'.    00290018
               88  PS-PROCESS-CONTINUE                    VALUE 'Y'.    00300014
               88  PS-PROCESS-COMPLETE                    VALUE 'N'.    00310014
               88  PS-PROCESS-STOP                        VALUE 'S'.    00311018
           05  PS-ACTION-IND                   PIC 9(02)  VALUE 00.     00320018
               88  PS-ACTION-REVERSE                      VALUE 01.     00330018
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00340014
               88  PS-ACTION-NOTHING                      VALUE 02.     00350018
           05  PS-MORE-VEND-UPC-SW              PIC X(01) VALUE 'Y'.    00350118
               88  PS-MORE-VEND-UPCS                      VALUE 'Y'.    00350218
               88  PS-NO-MORE-VEND-UPCS                   VALUE 'N'.    00350322
                                                                        00351015
      ******************************************************************00360014
      **PROGRAM CONSTANTS                                               00370015
      ******************************************************************00380014
       01  PC-PROGRAM-VARIABLES.                                        00390014
           05  PC-MAX-RETRIES                   PIC 9(01) VALUE 3.      00400014
                                                                        00411015
      ******************************************************************00420014
      **PROGRAM VARIABLES                                               00430014
      ******************************************************************00440014
       01  PV-PROGRAM-VARIABLES.                                        00450014
           05  PV-PARAGRAPH-NAME                PIC X(30) VALUE SPACE.  00460014
           05  PV-RETRIES                       PIC 9(01) VALUE ZEROS.  00460118
                                                                        00461015
      ******************************************************************00470014
      **PROGRAM CALCULATIONS                                            00480014
      ******************************************************************00490014
       01  PC-PROGRAM-CALCULATIONS.                                     00500014
           05  PC-10-DIGIT                  PIC 9(10)  VALUE ZEROS.     00510014
           05  PC-18-DIGIT                  PIC S9(18) COMP VALUE ZEROS.00520014
           05  PC-09-DIGIT                  PIC S9(09) COMP VALUE ZEROS.00530014
                                                                        00531015
      ******************************************************************00540014
      **COPYBOOKS                                                       00550014
      ******************************************************************00560014
      ***ABEND COPYBOOK                                                 00570014
           COPY DPWS004.                                                00580018
      ***DB2 MESSAGE AREA                                               00590014
           COPY SQLCA2.                                                 00600018
      ***ITEM MESSAGE COPYBOOK                                          00610018
           COPY IMRD008.                                                00620018
                                                                        00622015
      ******************************************************************00630014
      **TABLE INCLUSIONS                                                00640014
      ******************************************************************00650014
      ***DB2 COMMUNICATIONS AREA                                        00660014
           EXEC SQL                                                     00670014
                INCLUDE SQLCA                                           00680014
           END-EXEC.                                                    00690014
      *---------------------------------------------                    00691018
      *  DB2 TABLE XST00010 - SKU TABLE(XST_SKU)                        00692018
      *---------------------------------------------                    00693018
           EXEC SQL                                                     00710014
               INCLUDE XST00010                                         00720014
           END-EXEC.                                                    00730014
      *---------------------------------------------                    00732018
      *  DB2 TABLE XST00012 - UPC TABLE(XST_UPC)                        00733018
      *---------------------------------------------                    00734018
           EXEC SQL                                                     00735018
                INCLUDE XST00012                                        00736018
           END-EXEC.                                                    00737018
      *---------------------------------------------                    00739018
      *  TEMP DB2 TABLE EPT00016 - DELETED SKUS TABLE(EPTW_DLTD_SKU)    00739118
      *---------------------------------------------                    00739218
           EXEC SQL                                                     00739318
                INCLUDE EPT00016                                        00739418
           END-EXEC.                                                    00739518
                                                                        00739618
      *---------------------------------------------                    00739718
      *  VEND UPC CURSOR WILL FETCH ANY VENDOR UPC NUMBERS FOR THE      00739818
      *  DELETED SKU (EXCEPT THOSE MARKED FOR DELETION - THESE MAY HAVE 00739925
      *  BEEN REUSED, SO WE DON'T WANT TO INCLUDE THEM).                00740025
      *---------------------------------------------                    00740125
           EXEC SQL                                                     00740225
             DECLARE VEND-UPC-CSR CURSOR FOR                            00740325
                  SELECT UPC_NBR                                        00740425
                  FROM   XST_UPC                                        00740525
                  WHERE  INTR_UPC_ID    = :XST00012-INTR-UPC-ID         00740625
                    AND  UPC_TYP_CDE    = 'V'                           00740725
                    AND  LAST_ACTN_CDE <> 'D'                           00740825
                    WITH UR                                             00740925
           END-EXEC.                                                    00741025
                                                                        00742025
       LINKAGE SECTION.                                                 00750014
      ******************************************************************00760014
      **LINKAGE VARIABLES                                               00770014
      ******************************************************************00780014
       01  LV-IMRD008-RECORD               PIC X(450).                  00790014
       01  LV-RETURN-CODE                  PIC S9(04).                  00800014
       01  LV-ACTION-IND                   PIC 9(02).                   00810014
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00820014
                                                                        00830014
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00840014
                                LV-RETURN-CODE,                         00850014
                                LV-ACTION-IND,                          00860014
                                LV-CURRENT-TIMESTAMP.                   00870014
                                                                        00871018
      ******************************************************************00880014
       0000-MAINLINE.                                                   00890014
                                                                        00900014
            PERFORM 1000-INITIALIZATION.                                00910014
                                                                        00911023
            PERFORM 3000-PROCESS-TEMP-TABLE                             00912023
                                                                        00932018
            IF PS-PROCESS-CONTINUE                                      00933023
                INITIALIZE PV-RETRIES                                   00933123
                PERFORM 2000-PROCESS-MESSAGE                            00933223
                    UNTIL PS-PROCESS-COMPLETE                           00933323
                       OR PS-PROCESS-STOP                               00933423
                                                                        00934120
                IF PS-PROCESS-STOP                                      00934223
      *             * PROBLEM WITH DELETE - WILL ROLLBACK *             00934423
                    DISPLAY '** XSKUP008 SKU DELETE FOR '               00934523
                            IM008-ITEM-KEY-INTR-UPC-ID                  00934623
                            ' HAD SQLCODE OF ' SQLCODE                  00934723
                END-IF                                                  00934823
            END-IF.                                                     00934923
                                                                        00936018
            PERFORM 9999-WRAPUP.                                        00940014
                                                                        00950014
      ***************************************************************** 00970014
      **INITIALIZATION AREA                                             00980014
      ***************************************************************** 00990014
       1000-INITIALIZATION.                                             01000014
                                                                        01010014
           MOVE '1000-INITIALIZATION'      TO PV-PARAGRAPH-NAME.        01020015
                                                                        01030014
           MOVE LV-IMRD008-RECORD          TO IMRD008-RECORD.           01070015
                                                                        01080014
      ***SPECIAL PROCESSING DONE BECAUSE DCLGEN VALUE IS DEFINED AS COMP01090014
           MOVE IM008-ITEM-KEY-INTR-UPC-ID TO PC-10-DIGIT.              01100014
           MOVE PC-10-DIGIT                TO PC-18-DIGIT.              01110014
           MOVE PC-18-DIGIT                TO PC-09-DIGIT.              01120014
           MOVE PC-09-DIGIT                TO XST00010-INTR-UPC-ID.     01130014
           MOVE PC-09-DIGIT                TO XST00012-INTR-UPC-ID.     01131024
                                                                        01140014
           SET PS-PROCESS-CONTINUE         TO TRUE.                     01150015
           INITIALIZE PV-RETRIES.                                       01160018
                                                                        01170014
      ***************************************************************** 01190014
      ** CALLS PROCESS TO DELETE SKU FROM TABLES.                       01200014
      ***************************************************************** 01210014
       2000-PROCESS-MESSAGE.                                            01220014
                                                                        01230014
           MOVE '2000-PROCESS-MESSAGE'         TO PV-PARAGRAPH-NAME.    01240014
                                                                        01250014
           PERFORM 2100-DELETE-SKU.                                     01260014
                                                                        01270014
      ***************************************************************** 01290014
      ** DELETES A ROW IN THE XST_SKU TABLE.                            01300014
      ** BECAUSE OF CASCADING DELETES THAT HAVE BEEN DEFINED, IT ALSO   01310014
      ** DELETES ALL ROWS FOR THE SKU FROM THE XST_SKU_ATTR, XST_UPC,   01320014
      ** XST_SKU_LOC AND XST_SKU_LOC_ATTR TABLES.                       01330014
      ***************************************************************** 01340014
       2100-DELETE-SKU.                                                 01350014
                                                                        01360014
           MOVE '2100-DELETE-SKU'       TO PV-PARAGRAPH-NAME.           01370014
                                                                        01380014
           EXEC SQL                                                     01390014
                DELETE FROM XST_SKU                                     01400014
                WHERE INTR_UPC_ID = :XST00010-INTR-UPC-ID               01410014
           END-EXEC.                                                    01420014
                                                                        01430014
           EVALUATE TRUE                                                01440014
      ***SUCCESSFUL PROCESSING                                          01450014
              WHEN SQLCODE = ZERO                                       01460014
                   SET PS-PROCESS-COMPLETE  TO TRUE                     01470018
      ***ROW/TABLE LOCK                                                 01490217
              WHEN SQLCODE = -911                                       01490316
              WHEN SQLCODE = -913                                       01490417
              WHEN SQLCODE = -904                                       01491015
                   IF  PV-RETRIES >= PC-MAX-RETRIES                     01500018
                       SET PS-PROCESS-STOP  TO TRUE                     01512018
                   ELSE                                                 01520014
                       ADD +1               TO PV-RETRIES               01530018
                   END-IF                                               01540014
              WHEN OTHER                                                01550014
                   SET PS-PROCESS-STOP      TO TRUE                     01560018
           END-EVALUATE.                                                01570014
                                                                        01580014
      *----------------------------------------------------------       01581019
      *  INSERTS ROW IN TEMP DELETED SKU TABLE FOR THE SKU WITH         01582019
      *  INTERNAL UPC NUMBER.  FETCHES ANY VENDOR UPC NUMBERS AND ALSO  01582119
      *  INSERTS THEM INTO THE TEMP DELETED SKU TABLE.                  01582219
      *----------------------------------------------------------       01582319
       3000-PROCESS-TEMP-TABLE.                                         01584018
                                                                        01585018
           MOVE '3000-PROCESS-TEMP-TABLE'  TO PV-PARAGRAPH-NAME.        01586018
                                                                        01587018
           SET PS-MORE-VEND-UPCS           TO TRUE.                     01587119
           MOVE PC-09-DIGIT                TO EPT00016-INTR-UPC-ID.     01587218
           MOVE IM008-ITEM-KEY-SKU         TO EPT00016-SKU-NBR.         01587318
           MOVE IM008-ITEM-KEY-UPC-NBR     TO EPT00016-UPC-NBR.         01587418
                                                                        01587518
           PERFORM 8000-INSERT-DLTD-SKU.                                01588019
                                                                        01588118
           IF PS-PROCESS-CONTINUE                                       01588218
               PERFORM 8100-OPEN-VEND-UPC                               01588519
           END-IF.                                                      01588618
                                                                        01588718
           IF PS-PROCESS-CONTINUE                                       01588818
               PERFORM 8200-FETCH-VEND-UPC                              01589019
           END-IF.                                                      01589118
                                                                        01589218
           PERFORM 3100-PROCESS-VEND-UPC                                01589419
               UNTIL PS-NO-MORE-VEND-UPCS                               01589518
                  OR PS-PROCESS-STOP.                                   01589618
                                                                        01589718
           IF PS-PROCESS-CONTINUE                                       01589818
               PERFORM 8300-CLOSE-VEND-UPC                              01590019
           END-IF.                                                      01590118
                                                                        01590219
      *----------------------------------------------------------       01590319
      *  INSERTS ROW IN TEMP DELETED SKU TABLE FOR A VENDOR UPC NUMBER  01590419
      *  OF THE DELETED SKU.  FETCHES NEXT VENDOR UPC NUMBER.           01590519
      *----------------------------------------------------------       01590619
       3100-PROCESS-VEND-UPC.                                           01590819
                                                                        01590919
           MOVE '3100-PROCESS-VEND-UPC'  TO PV-PARAGRAPH-NAME.          01591019
                                                                        01591119
           MOVE XST00012-UPC-NBR         TO EPT00016-UPC-NBR.           01591519
                                                                        01591619
           PERFORM 8000-INSERT-DLTD-SKU.                                01591719
                                                                        01591819
           IF PS-PROCESS-CONTINUE                                       01591919
               PERFORM 8200-FETCH-VEND-UPC                              01592019
           END-IF.                                                      01592119
                                                                        01592219
      *----------------------------------------------------------       01592718
      * 8000-INSERT-DLTD-SKU                                            01592819
      *      INSERT ROW INTO TEMPORARY DELETED SKU TABLE.               01592919
      *----------------------------------------------------------       01593018
       8000-INSERT-DLTD-SKU.                                            01593119
                                                                        01593219
           MOVE '8000-INSERT-DLTD-SKU'    TO PV-PARAGRAPH-NAME.         01593319
                                                                        01593419
           PERFORM                                                      01593519
               WITH TEST AFTER                                          01593619
               VARYING PV-RETRIES                                       01593719
               FROM 1 BY 1                                              01593819
               UNTIL (PV-RETRIES GREATER PC-MAX-RETRIES  OR             01593919
                      PS-PROCESS-STOP                    OR             01594019
                      SQLCODE = 0)                                      01594119
                                                                        01594219
               EXEC SQL                                                 01594319
                  INSERT INTO EPTW_DLTD_SKU ( INTR_UPC_ID               01594419
                                            , UPC_NBR                   01594519
                                            , SKU_NBR                   01594619
                                            , CRE_TMST)                 01594719
                  VALUES ( :EPT00016-INTR-UPC-ID                        01594819
                         , :EPT00016-UPC-NBR                            01594919
                         , :EPT00016-SKU-NBR                            01595019
                         , CURRENT TIMESTAMP)                           01595120
               END-EXEC                                                 01595219
                                                                        01595319
               EVALUATE TRUE                                            01595419
      *           **SUCCESSFUL PROCESSING                               01595519
                  WHEN SQLCODE = ZERO                                   01595619
                       CONTINUE                                         01595719
      *           **REFERENTIAL INTEGRITY ISSUE                         01595819
                  WHEN SQLCODE = -530                                   01595919
                  WHEN SQLCODE = -532                                   01596019
                       SET PS-PROCESS-STOP    TO TRUE                   01596119
      *           **ROW ALREADY EXISTS ON TABLE                         01596219
                  WHEN SQLCODE = -803                                   01596319
                       SET PS-PROCESS-STOP    TO TRUE                   01596419
      *           **ROW/TABLE LOCK                                      01596519
                  WHEN SQLCODE = -904                                   01596620
                  WHEN SQLCODE = -911                                   01596720
                  WHEN SQLCODE = -913                                   01596820
                       CONTINUE                                         01596920
                  WHEN OTHER                                            01597020
                       SET PS-PROCESS-STOP    TO TRUE                   01597120
               END-EVALUATE                                             01597220
           END-PERFORM.                                                 01597320
                                                                        01597420
           IF PV-RETRIES GREATER PC-MAX-RETRIES  OR                     01597520
              PS-PROCESS-STOP                                           01597620
               SET PS-PROCESS-STOP    TO TRUE                           01597720
               DISPLAY '** EPKUP008 INSERT TO EPTW DLTD SKU FOR '       01597820
                       'INTR UPC ID '                                   01597920
                       IM008-ITEM-KEY-INTR-UPC-ID                       01598020
               DISPLAY '   AND UPC NUMBER '                             01598120
                       EPT00016-UPC-NBR                                 01598220
                       ' HAD SQLCODE OF ' SQLCODE                       01598320
           END-IF.                                                      01598420
                                                                        01598520
      *----------------------------------------------------------       01598620
      * 8100-OPEN-VEND-UPC.                                             01598720
      *      OPEN THE CURSOR OF VENDOR UPCS FOR A DELETED SKU.          01598820
      *----------------------------------------------------------       01598920
       8100-OPEN-VEND-UPC.                                              01599020
                                                                        01599120
           MOVE '8100-OPEN-VEND-UPC'  TO PV-PARAGRAPH-NAME.             01599220
                                                                        01599320
           PERFORM                                                      01599420
               WITH TEST AFTER                                          01599520
               VARYING PV-RETRIES                                       01599620
               FROM 1 BY 1                                              01599720
               UNTIL (PV-RETRIES GREATER PC-MAX-RETRIES  OR             01599820
                      PS-PROCESS-STOP                    OR             01599920
                      SQLCODE = 0)                                      01600020
                                                                        01600120
                 EXEC SQL                                               01600220
                    OPEN VEND-UPC-CSR                                   01600320
                 END-EXEC                                               01600420
                                                                        01600520
               EVALUATE TRUE                                            01600620
                   WHEN SQLCODE = -904                                  01600720
                   WHEN SQLCODE = -911                                  01600820
                   WHEN SQLCODE = -913                                  01600920
                       CONTINUE                                         01601021
                   WHEN SQLCODE = 0                                     01601120
                       CONTINUE                                         01601220
                   WHEN OTHER                                           01601323
                       SET PS-PROCESS-STOP TO TRUE                      01601520
               END-EVALUATE                                             01601620
           END-PERFORM.                                                 01601720
                                                                        01601820
           IF PV-RETRIES GREATER PC-MAX-RETRIES  OR                     01601920
              PS-PROCESS-STOP                                           01602020
               SET PS-PROCESS-STOP    TO TRUE                           01602120
               DISPLAY '** EPKUP008 OPEN OF VEND UPC CURSOR FOR '       01602220
                       IM008-ITEM-KEY-INTR-UPC-ID                       01602320
                       ' HAD SQLCODE OF ' SQLCODE                       01602420
           END-IF.                                                      01602520
                                                                        01602620
      *----------------------------------------------------------       01602720
      * 8200-FETCH-VEND-UPC.                                            01602820
      *      FETCH A VENDOR UPC FOR THE DELETED SKU.                    01602920
      *----------------------------------------------------------       01603020
       8200-FETCH-VEND-UPC.                                             01603120
                                                                        01603220
           MOVE '8200-FETCH-VEND-UPC'  TO PV-PARAGRAPH-NAME.            01603320
                                                                        01603420
           PERFORM                                                      01603520
               WITH TEST AFTER                                          01603620
               VARYING PV-RETRIES                                       01603720
               FROM 1 BY 1                                              01603820
               UNTIL (PV-RETRIES GREATER PC-MAX-RETRIES OR              01603920
                      PS-PROCESS-STOP                   OR              01604020
                      SQLCODE = 0                       OR              01604120
                      SQLCODE = +100)                                   01604220
                                                                        01604320
                 EXEC SQL                                               01604420
                    FETCH VEND-UPC-CSR                                  01604520
                    INTO  :XST00012-UPC-NBR                             01604620
                 END-EXEC                                               01604720
                                                                        01604820
                EVALUATE TRUE                                           01604920
                    WHEN SQLCODE = -904                                 01605020
                    WHEN SQLCODE = -911                                 01605120
                    WHEN SQLCODE = -913                                 01605220
                        CONTINUE                                        01605320
                    WHEN SQLCODE = ZERO                                 01605420
                        CONTINUE                                        01605521
                    WHEN SQLCODE = +100                                 01605620
                        SET PS-NO-MORE-VEND-UPCS   TO TRUE              01605720
                    WHEN OTHER                                          01605823
                        SET PS-PROCESS-STOP        TO TRUE              01606020
                END-EVALUATE                                            01606120
           END-PERFORM.                                                 01606220
                                                                        01606320
           IF PV-RETRIES GREATER PC-MAX-RETRIES  OR                     01606420
              PS-PROCESS-STOP                                           01606520
               SET PS-PROCESS-STOP    TO TRUE                           01606620
               DISPLAY '** EPKUP008 FETCH OF VEND UPC CURSOR FOR '      01606720
                       IM008-ITEM-KEY-INTR-UPC-ID                       01606820
                       ' HAD SQLCODE OF ' SQLCODE                       01606920
           END-IF.                                                      01607020
                                                                        01607120
      *-------------------------------------------------------------    01607220
      * 8300-CLOSE-VEND-UPC.                                            01607320
      *      CLOSE THE VENDOR UPCS CURSOR.                              01607420
      *-------------------------------------------------------------    01607520
       8300-CLOSE-VEND-UPC.                                             01607620
                                                                        01607720
           MOVE '8300-CLOSE-VEND-UPC'  TO PV-PARAGRAPH-NAME.            01607820
                                                                        01607920
           PERFORM                                                      01608020
               WITH TEST AFTER                                          01608120
               VARYING PV-RETRIES                                       01608220
               FROM 1 BY 1                                              01608320
               UNTIL (PV-RETRIES GREATER PC-MAX-RETRIES  OR             01608420
                      PS-PROCESS-STOP                    OR             01608520
                      SQLCODE = 0)                                      01608620
                                                                        01608720
                 EXEC SQL                                               01608820
                    CLOSE VEND-UPC-CSR                                  01608920
                 END-EXEC                                               01609020
                                                                        01609120
                EVALUATE TRUE                                           01609220
                   WHEN SQLCODE = -904                                  01609320
                   WHEN SQLCODE = -911                                  01609420
                   WHEN SQLCODE = -913                                  01609520
                       CONTINUE                                         01609621
                   WHEN SQLCODE = 0                                     01609720
                       CONTINUE                                         01609820
                   WHEN OTHER                                           01609923
                       SET PS-PROCESS-STOP TO TRUE                      01610120
                END-EVALUATE                                            01610220
           END-PERFORM.                                                 01610320
                                                                        01610420
           IF PV-RETRIES GREATER PC-MAX-RETRIES OR                      01610520
              PS-PROCESS-STOP                                           01610620
               SET PS-PROCESS-STOP    TO TRUE                           01610720
               DISPLAY '** EPKUP008 CLOSE OF VEND UPC CURSOR FOR '      01610820
                       IM008-ITEM-KEY-INTR-UPC-ID                       01610920
                       ' HAD SQLCODE OF ' SQLCODE                       01611020
           END-IF.                                                      01611120
                                                                        01611220
      ******************************************************************01611320
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01612014
      ******************************************************************01620014
       9999-WRAPUP.                                                     01630014
                                                                        01640014
           MOVE '9999-WRAPUP' TO PV-PARAGRAPH-NAME.                     01650014
                                                                        01660014
           MOVE SQLCODE       TO LV-RETURN-CODE.                        01670014
           MOVE PS-ACTION-IND TO LV-ACTION-IND.                         01680014
                                                                        01690014
           EXIT PROGRAM.                                                01700014
