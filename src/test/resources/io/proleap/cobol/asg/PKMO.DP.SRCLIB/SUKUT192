       IDENTIFICATION DIVISION.                                         00010001
       PROGRAM-ID.    SUKUT192.                                         00020002
       AUTHOR.        JIM ARENDT.                                       00030001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040001
       DATE-WRITTEN.  SEPTEMBER, 2017.                                  00050001
       DATE-COMPILED.                                                   00060001
      ******************************************************************00070001
      *  DB2                                                            00080001
      * EXTRACT THE SML'S THAT WERE SCANNED BY THE STORES FOR THE       00090013
      * STATUS STORE UNLOADED TRAILERS.                                 00100013
      ******************************************************************00110001
      * DATE: 09/2017 MADE BY:JIM ARENDT  INITIAL IMPLEMENTATION        00120001
      ******************************************************************00130001
       ENVIRONMENT DIVISION.                                            00140001
       CONFIGURATION SECTION.                                           00150001
       SOURCE-COMPUTER.    IBM-3090.                                    00160001
       OBJECT-COMPUTER.    IBM-3090.                                    00170001
       INPUT-OUTPUT SECTION.                                            00180001
       FILE-CONTROL.                                                    00190001
                                                                        00200001
           SELECT INPUT-EXTRACT-DS-FILE                                 00210001
                  ASSIGN TO INP01.                                      00220001
                                                                        00230001
           SELECT OUTPUT-SML-FILE                                       00240001
                  ASSIGN TO OUT01.                                      00250001
                                                                        00260001
       DATA DIVISION.                                                   00270001
       FILE SECTION.                                                    00280001
                                                                        00290001
       FD  INPUT-EXTRACT-DS-FILE                                        00300001
           RECORDING MODE IS F                                          00310001
           LABEL RECORDS OMITTED                                        00320001
           BLOCK CONTAINS 0 RECORDS                                     00330001
           DATA RECORD IS INPUT-EXTRACT-DS-RECORD.                      00340001
       01  INPUT-EXTRACT-DS-RECORD             PIC  X(250).             00350008
                                                                        00360001
       FD  OUTPUT-SML-FILE                                              00370001
           RECORDING MODE IS F                                          00380001
           LABEL RECORDS OMITTED                                        00390001
           BLOCK CONTAINS 0 RECORDS                                     00400001
           DATA RECORD IS OUTPUT-SML-RECORD.                            00410001
       01  OUTPUT-SML-RECORD                   PIC  X(250).             00420008
                                                                        00430001
       WORKING-STORAGE SECTION.                                         00440001
                                                                        00450001
      ******************************************************************00460001
      * PROGRAM CONSTANTS                                               00470001
      ******************************************************************00480001
       01  PC-CONSTANTS-FIELDS.                                         00490001
           05  PC-PGM-NAME                     PIC  X(08) VALUE         00500001
                                                          'SUKUT192'.   00510002
           05  PC-MAX-RETRIES                  PIC S9(03) VALUE +3      00520001
                                                          COMP-3.       00530001
                                                                        00540001
      ******************************************************************00550001
      * PROGRAM VARIABLES                                               00560001
      ******************************************************************00570001
       01  PV-VARIABLE-FIELDS.                                          00580001
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACE.  00590001
           05  PV-DB2-OPERATION                PIC  X(30) VALUE SPACE.  00600001
           05  PV-RETRY-COUNTER                PIC S9(04) VALUE ZEROS   00610001
                                                          COMP SYNC.    00620001
           05  PV-EDIT-FIELD                   PIC  ZZZ,ZZZ,ZZ9.        00630012
           05  PV-NULL-IND                     PIC S9(04) VALUE ZERO    00640005
                                                          COMP SYNC.    00650005
           05  PV-TRIP-ID                      PIC S9(09) COMP.         00660001
           05  PV-TIMESTAMP.                                            00670011
               10  PV-DATE-FIELD               PIC  X(10) VALUE SPACES. 00680011
               10  FILLER                      PIC  X(01) VALUE SPACES. 00690011
               10  PV-TIME-FIELD.                                       00700011
                   15 PV-TIME-HH               PIC  X(02) VALUE SPACES. 00710011
                   15 PV-TIME-COLON1           PIC  X(01) VALUE SPACES. 00720011
                   15 PV-TIME-MM               PIC  X(02) VALUE SPACES. 00730011
                   15 PV-TIME-COLON2           PIC  X(01) VALUE SPACES. 00740011
                   15 PV-TIME-MM               PIC  X(02) VALUE SPACES. 00750011
               10  FILLER                      PIC  X(07) VALUE SPACES. 00760011
                                                                        00770001
      ******************************************************************00780001
      * PROGRAM ACCUMULATORS                                            00790001
      ******************************************************************00800001
       01  PA-PROGRAM-ACCUMULATORS.                                     00810001
           05  PA-INPUT-RECORDS-READ           PIC  9(09) VALUE ZEROS.  00820001
           05  PA-DTL-RECORDS-READ             PIC  9(09) VALUE ZEROS.  00830001
           05  PA-OUTPUT-RECS-WROTE            PIC  9(09) VALUE ZEROS.  00840001
                                                                        00850001
      ******************************************************************00860001
      * PROGRAM SWITCHES                                                00870001
      ******************************************************************00880001
       01  PS-SWITCH-AREAS.                                             00890001
           05  PS-FIRST-TIME-SW                PIC  X(01) VALUE 'Y'.    00900013
               88  PS-FIRST-TIME-Y                        VALUE 'Y'.    00910001
               88  PS-FIRST-TIME-N                        VALUE 'N'.    00920001
           05  PS-END-OF-INPUT-FILE-SW         PIC  X(01) VALUE 'N'.    00930001
               88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.    00940001
               88  PS-NOT-END-OF-FILE                     VALUE 'N'.    00950001
           05  PS-SML-CSR-EOF-SW               PIC  X(01) VALUE 'N'.    00960001
               88  PS-SML-CSR-EOF                         VALUE 'Y'.    00970001
               88  PS-SML-CSR-NOT-EOF                     VALUE 'N'.    00980001
                                                                        00990001
      ******************************************************************01000001
      * ABEND INFORMATION                                               01010001
      ******************************************************************01020001
       01  ABEND-CODE                          PIC S9(04) VALUE ZEROS   01030001
                                                          COMP SYNC.    01040001
           88  ABEND-4001-WRITE-ERROR                     VALUE +4001.  01050001
           88  ABEND-4002-READ-ERROR                      VALUE +4002.  01060001
           88  ABEND-4003-CTRL-CARD-ERROR                 VALUE +4003.  01070001
           88  ABEND-4004-TABLE-ERROR                     VALUE +4004.  01080001
           88  ABEND-4005-OPEN-ERROR                      VALUE +4005.  01090001
           88  ABEND-4006-CLOSE-ERROR                     VALUE +4006.  01100001
           88  ABEND-4007-SEQUENCE-ERROR                  VALUE +4007.  01110001
           88  ABEND-4008-DATA-ERROR                      VALUE +4008.  01120001
           88  ABEND-4009-KEY-DATA-ERROR                  VALUE +4009.  01130001
           88  ABEND-4013-DB2-ERROR                       VALUE +4013.  01140001
           88  ABEND-4019-CAL-SUB-ERROR                   VALUE +4019.  01150001
           88  ABEND-4020-MQ-ERROR                        VALUE +4020.  01160001
           88  ABEND-4444-FORCED-ABEND                    VALUE +4444.  01170001
                                                                        01180001
      ******************************************************************01190001
      ****ABEND ROUTINE INFORMATION                                     01200001
           COPY SQLCA2.                                                 01210001
                                                                        01220001
      ****ABEND ROUTINE INFORMATION                                     01230001
           COPY DPWS004.                                                01240001
                                                                        01250001
      ****INPUT/OUTPUT COPYBOOK                                         01260008
           COPY SURD163.                                                01270001
                                                                        01280001
      ******************************************************************01290001
      * TABLE INCLUSIONS                                                01300001
      ******************************************************************01310001
      ** SUT_OTBND_CART TABLE                                           01320001
           EXEC SQL INCLUDE SUT00024 END-EXEC.                          01330001
                                                                        01340001
      ** SUT_OTBND_STR_SCN TABLE                                        01350002
           EXEC SQL INCLUDE SUT00121 END-EXEC.                          01360002
                                                                        01370002
      ** DB2 TABLE INCLUSION                                            01380001
           EXEC SQL INCLUDE SQLCA    END-EXEC.                          01390001
                                                                        01400001
      ******************************************************************01410001
      * CURSOR TO RETRIEVE EXTRACT SML'S THAT WERE SCANNED BY THE STORES01420007
      * FOR THE 'SU'(STORE UNLOADED) TRAILERS                           01430007
      ******************************************************************01440001
           EXEC SQL                                                     01450001
             DECLARE SML_CSR CURSOR FOR                                 01460001
               SELECT C.SHIPT_UNT_ID                                    01470002
                     ,C.ORGN_LOC_NBR                                    01480002
                     ,C.DEST_LOC_NBR                                    01490002
                     ,C.CART_SRC_CDE                                    01500002
                     ,C.CRE_DTE                                         01510002
                     ,C.CRE_TM                                          01520002
                     ,C.ORIG_CART_ID                                    01530002
                     ,C.ACTV_IND                                        01540002
                     ,A.CRE_TMST                                        01550002
                     ,A.SCN_LOC_NBR                                     01560002
                     ,A.SCN_TYP_CDE                                     01570002
                     ,A.ACTV_IND                                        01580002
                     ,A.ORIG_OTBND_TRIP_ID                              01590002
                     ,A.SCN_FR_MSTR_IND                                 01600002
                 FROM SUT_OTBND_STR_SCN A                               01610002
                    , SUT_OTBND_CART    C                               01620012
                WHERE A.SCN_OTBND_TRIP_ID = :PV-TRIP-ID                 01630012
                  AND NOT A.SCN_TYP_CDE IN ('SSAR','SSMS','SSAU')       01640002
                  AND A.SHIPT_UNT_ID      = C.SHIPT_UNT_ID              01650012
               WITH UR                                                  01660001
          END-EXEC.                                                     01670001
                                                                        01680001
       PROCEDURE DIVISION.                                              01690001
      ******************************************************************01700001
       0000-MAINLINE-PROCESSING.                                        01710001
                                                                        01720001
           PERFORM 1000-INITIALIZATION.                                 01730001
                                                                        01740001
           PERFORM 2000-PROCESS-INPUT-FILE                              01750001
             UNTIL PS-END-OF-INPUT-FILE.                                01760001
                                                                        01770001
           PERFORM 9000-WRAPUP.                                         01780001
           GOBACK.                                                      01790001
                                                                        01800001
      ******************************************************************01810001
      * INITIALIZATION ROUTINE                                          01820001
      ******************************************************************01830001
       1000-INITIALIZATION.                                             01840001
                                                                        01850001
           DISPLAY ' '.                                                 01860001
           DISPLAY '*******************************************'.       01870001
           DISPLAY '***  PROGRAM NAME:  '  PC-PGM-NAME.                 01880001
           DISPLAY ' '.                                                 01890001
                                                                        01900001
           OPEN INPUT  INPUT-EXTRACT-DS-FILE                            01910001
                OUTPUT OUTPUT-SML-FILE.                                 01920001
                                                                        01930001
           PERFORM 1100-READ-INPUT-FILE.                                01940001
                                                                        01950001
      ******************************************************************01960001
      * READ THE INPUT 'SU'(STORE UNLOADED) TRAILER FILE                01970013
      ******************************************************************01980001
       1100-READ-INPUT-FILE.                                            01990001
                                                                        02000001
           READ INPUT-EXTRACT-DS-FILE INTO SU163-EXTRACT-RECORD         02010008
              AT END                                                    02020001
                 SET PS-END-OF-INPUT-FILE TO TRUE.                      02030001
                                                                        02040001
           IF PS-END-OF-INPUT-FILE                                      02050001
               IF PS-FIRST-TIME-Y                                       02060001
                  DISPLAY ' '                                           02070001
                  DISPLAY '-- INPUT FILE MISSING --'                    02080001
                  DISPLAY ' '                                           02090001
               END-IF                                                   02100001
           ELSE                                                         02110001
               ADD 1                     TO PA-INPUT-RECORDS-READ       02120001
               MOVE SU163-OTBND-TRIP-ID  TO PV-TRIP-ID                  02130008
               SET PS-FIRST-TIME-N       TO TRUE                        02140001
           END-IF.                                                      02150001
                                                                        02160001
      ******************************************************************02170001
      * OPEN THE SML CURSOR                                             02180001
      ******************************************************************02190001
       1200-OPEN-SML-CSR.                                               02200001
                                                                        02210001
           EXEC SQL                                                     02220001
             OPEN SML_CSR                                               02230001
           END-EXEC.                                                    02240001
                                                                        02250001
           EVALUATE TRUE                                                02260001
               WHEN SQLCODE = +0                                        02270001
               WHEN SQLCODE = -904                                      02280001
               WHEN SQLCODE = -911                                      02290001
               WHEN SQLCODE = -913                                      02300001
                    CONTINUE                                            02310001
                                                                        02320001
               WHEN SQLWARN0 NOT EQUAL SPACE                            02330001
               WHEN SQLCODE NOT EQUAL ZERO                              02340001
                    DISPLAY ' ERROR TRIP-ID = ' PV-TRIP-ID              02350012
                    MOVE '1200-OPEN-SML-CSR'                            02360001
                                           TO PV-PARAGRAPH-NAME         02370001
                    MOVE 'ERROR ON OPEN OF SML_CSR'                     02380001
                                           TO PV-DB2-OPERATION          02390001
                    SET ABEND-4005-OPEN-ERROR TO TRUE                   02400001
                    PERFORM 9200-SQL-ABEND                              02410001
                                                                        02420001
           END-EVALUATE.                                                02430001
                                                                        02440001
      ******************************************************************02450001
      * RETRIEVE THE SML TRAILER DATA                                   02460001
      ******************************************************************02470001
       1300-FETCH-SML-CSR.                                              02480001
                                                                        02490001
           PERFORM                                                      02500001
               WITH TEST AFTER                                          02510001
               VARYING PV-RETRY-COUNTER                                 02520001
               FROM 1 BY 1                                              02530001
               UNTIL (PV-RETRY-COUNTER > PC-MAX-RETRIES)                02540001
                   OR (SQLCODE = +0                                     02550001
                   OR  SQLCODE = +100)                                  02560001
                                                                        02570001
               EXEC SQL                                                 02580001
                  FETCH SML_CSR                                         02590001
                    INTO  :SUT00024-SHIPT-UNT-ID                        02600002
                        , :SUT00024-ORGN-LOC-NBR                        02610002
                        , :SUT00024-DEST-LOC-NBR                        02620002
                        , :SUT00024-CART-SRC-CDE                        02630002
                        , :SUT00024-CRE-DTE                             02640002
                        , :SUT00024-CRE-TM                              02650002
                        , :SUT00024-ORIG-CART-ID                        02660002
                        , :SUT00024-ACTV-IND                            02670002
                        , :SUT00121-CRE-TMST                            02680002
                        , :SUT00121-SCN-LOC-NBR                         02690002
                        , :SUT00121-SCN-TYP-CDE                         02700002
                        , :SUT00121-ACTV-IND                            02710002
                        , :SUT00121-ORIG-OTBND-TRIP-ID                  02720002
                          :PV-NULL-IND                                  02730005
                        , :SUT00121-SCN-FR-MSTR-IND                     02740002
               END-EXEC                                                 02750001
                                                                        02760001
               EVALUATE TRUE                                            02770001
                   WHEN SQLCODE = ZERO                                  02780001
                       ADD 1                     TO PA-DTL-RECORDS-READ 02790001
                       IF PV-NULL-IND = -1                              02800012
                          MOVE ZEROES            TO                     02810012
                                             SUT00121-ORIG-OTBND-TRIP-ID02820012
                       END-IF                                           02830012
                                                                        02840001
                   WHEN SQLCODE = +100                                  02850001
                       SET PS-SML-CSR-EOF        TO TRUE                02860001
                                                                        02870001
                   WHEN SQLCODE = -904                                  02880001
                   WHEN SQLCODE = -911                                  02890001
                   WHEN SQLCODE = -913                                  02900001
                       CONTINUE                                         02910001
                                                                        02920001
                   WHEN SQLWARN0 NOT EQUAL SPACE                        02930001
                   WHEN SQLCODE NOT EQUAL ZERO                          02940001
                       DISPLAY ' ERROR TRIP-ID = '  PV-TRIP-ID          02950012
                       MOVE '1300-FETCH-SML-CSR' TO PV-PARAGRAPH-NAME   02960001
                       MOVE 'ERROR ON FETCHING SML_CSR'                 02970001
                                                 TO PV-DB2-OPERATION    02980001
                       SET ABEND-4002-READ-ERROR TO TRUE                02990001
                       PERFORM 9200-SQL-ABEND                           03000001
                                                                        03010001
               END-EVALUATE                                             03020001
           END-PERFORM.                                                 03030001
                                                                        03040001
           IF (PV-RETRY-COUNTER > PC-MAX-RETRIES)                       03050001
               DISPLAY ' ERROR TRIP-ID = '         PV-TRIP-ID           03060012
               MOVE '1300-FETCH-SML-CSR'        TO PV-PARAGRAPH-NAME    03070001
               MOVE 'MAX RETRIES EXCEEDED'      TO PV-DB2-OPERATION     03080001
               SET ABEND-4002-READ-ERROR        TO TRUE                 03090001
               PERFORM 9200-SQL-ABEND                                   03100001
           END-IF.                                                      03110001
                                                                        03120001
      ******************************************************************03130001
      * PROCESS ALL THE RECORDS IN THE INPUT FILE                       03140001
      ******************************************************************03150001
       2000-PROCESS-INPUT-FILE.                                         03160001
                                                                        03170001
           SET PS-SML-CSR-NOT-EOF  TO TRUE.                             03180001
           PERFORM 1200-OPEN-SML-CSR.                                   03190001
                                                                        03200001
           PERFORM 1300-FETCH-SML-CSR.                                  03210001
           IF PS-SML-CSR-EOF                                            03220001
              CONTINUE                                                  03230001
                                                                        03240001
           ELSE                                                         03250001
              PERFORM 2300-PROCESS-RECORDS                              03260001
                UNTIL PS-SML-CSR-EOF                                    03270001
                                                                        03280001
           END-IF.                                                      03290001
                                                                        03300001
           PERFORM 3000-CLOSE-SML-CSR.                                  03310001
                                                                        03320001
           PERFORM 1100-READ-INPUT-FILE.                                03330001
                                                                        03340001
      ******************************************************************03350001
      * POPULATE THE OUTPUT RECORD                                      03360001
      ******************************************************************03370001
       2300-PROCESS-RECORDS.                                            03380001
                                                                        03390001
           MOVE SUT00024-SHIPT-UNT-ID     TO SU163-SHIPT-UNIT-ID.       03400011
           MOVE SUT00024-ORGN-LOC-NBR     TO SU163-SML-ORGN-LOC-NBR.    03410011
           MOVE SUT00024-DEST-LOC-NBR     TO SU163-SML-DEST-LOC-NBR     03420011
           MOVE SUT00024-CART-SRC-CDE     TO SU163-CART-SRC-CDE.        03430011
           MOVE SUT00024-CRE-DTE          TO SU163-SML-CRE-DTE.         03440011
           IF SUT00024-CRE-TM > SPACES                                  03450010
              MOVE SUT00024-CRE-TM        TO PV-TIME-FIELD              03460010
              MOVE ':'                    TO PV-TIME-COLON1             03470010
                                             PV-TIME-COLON2             03480010
              MOVE PV-TIME-FIELD          TO SU163-SML-CRE-TM           03490011
           END-IF.                                                      03500010
           MOVE SUT00024-ORIG-CART-ID     TO SU163-ORIG-CART-ID.        03510011
           MOVE SUT00024-ACTV-IND         TO SU163-ACTV-IND.            03520011
           MOVE SUT00121-SCN-LOC-NBR      TO SU163-STR-SCAN-LOC.        03530012
           MOVE SUT00121-SCN-TYP-CDE      TO SU163-STR-SCAN-TYPE-CDE.   03540008
           MOVE SUT00121-ORIG-OTBND-TRIP-ID                             03550004
                                          TO SU163-STR-SCAN-ORIG-TRIP-ID03560004
           MOVE SUT00121-SCN-FR-MSTR-IND  TO SU163-STR-SCAN-FR-MSTR-IND.03570004
           MOVE SUT00121-CRE-TMST         TO PV-TIMESTAMP.              03580011
           IF PV-DATE-FIELD > SPACES                                    03590011
               MOVE PV-DATE-FIELD         TO SU163-STR-SCAN-DATE        03600011
               MOVE ':'                   TO PV-TIME-COLON1             03610011
                                             PV-TIME-COLON2             03620011
               MOVE PV-TIME-FIELD         TO SU163-STR-SCAN-TIME        03630011
           END-IF.                                                      03640011
                                                                        03650011
           PERFORM 2400-WRITE-OUTPUT-RECORD.                            03660011
                                                                        03670001
           PERFORM 1300-FETCH-SML-CSR.                                  03680001
                                                                        03690001
      ******************************************************************03700001
      * WRITE THE OUTPUT RECORD                                         03710001
      ******************************************************************03720001
       2400-WRITE-OUTPUT-RECORD.                                        03730001
                                                                        03740001
           WRITE OUTPUT-SML-RECORD FROM SU163-EXTRACT-RECORD.           03750001
                                                                        03760001
           ADD +1              TO PA-OUTPUT-RECS-WROTE.                 03770001
                                                                        03780001
      ******************************************************************03790001
      * CLOSE THE SML CURSOR                                            03800001
      ******************************************************************03810001
       3000-CLOSE-SML-CSR.                                              03820001
                                                                        03830001
           EXEC SQL                                                     03840001
              CLOSE SML_CSR                                             03850001
           END-EXEC.                                                    03860001
                                                                        03870001
           EVALUATE TRUE                                                03880001
               WHEN SQLCODE = +0                                        03890001
               WHEN SQLCODE = -904                                      03900001
               WHEN SQLCODE = -911                                      03910001
               WHEN SQLCODE = -913                                      03920001
                    CONTINUE                                            03930001
                                                                        03940001
               WHEN SQLWARN0 NOT EQUAL SPACE                            03950001
               WHEN SQLCODE NOT EQUAL ZERO                              03960001
                    DISPLAY ' ERROR TRIP-ID = '    PV-TRIP-ID           03970012
                    MOVE '3000-CLOSE-SML-CSR'   TO PV-PARAGRAPH-NAME    03980001
                    MOVE 'ERROR ON CLOSE OF SML_CSR'                    03990001
                                                TO PV-DB2-OPERATION     04000001
                    SET ABEND-4006-CLOSE-ERROR  TO TRUE                 04010001
                    PERFORM 9200-SQL-ABEND                              04020001
                                                                        04030001
           END-EVALUATE.                                                04040001
                                                                        04050001
      ******************************************************************04060001
      * CLOSE INPUT/OUTPUT FILES AND DISPLAY PROGRAM COUNTS             04070001
      ******************************************************************04080001
       9000-WRAPUP.                                                     04090001
                                                                        04100001
           CLOSE INPUT-EXTRACT-DS-FILE                                  04110001
                 OUTPUT-SML-FILE.                                       04120001
                                                                        04130001
           PERFORM 9100-DISPLAY-COUNTS.                                 04140001
                                                                        04150001
      ******************************************************************04160001
      * DISPLAY PROGRAM COUNTS                                          04170001
      ******************************************************************04180001
       9100-DISPLAY-COUNTS.                                             04190001
                                                                        04200001
           MOVE PA-INPUT-RECORDS-READ             TO PV-EDIT-FIELD.     04210001
           DISPLAY ' INPUT RECORDS READ.......: '    PV-EDIT-FIELD.     04220001
                                                                        04230001
           MOVE PA-DTL-RECORDS-READ               TO PV-EDIT-FIELD.     04240001
           DISPLAY ' DETAIL CSR RECORDS READ..: '    PV-EDIT-FIELD.     04250001
                                                                        04260001
           MOVE PA-OUTPUT-RECS-WROTE              TO PV-EDIT-FIELD.     04270001
           DISPLAY ' OUTPUT RECORDS WROTE.....: '    PV-EDIT-FIELD.     04280001
                                                                        04290001
           DISPLAY ' '.                                                 04300001
           DISPLAY '*******************************************'.       04310001
           DISPLAY ' '.                                                 04320001
                                                                        04330001
      ******************************************************************04340001
      * ABEND ROUTINE FOR BAD SQL CALLS                                 04350001
      ******************************************************************04360001
       9200-SQL-ABEND.                                                  04370001
                                                                        04380001
           DISPLAY '** ABEND     **'.                                   04390001
           DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME.                    04400001
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04410001
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               04420001
           DISPLAY '** SQLCODE   ** = ' SQLCODE.                        04430001
                                                                        04440001
      ***************************************************************** 04450001
      *    COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED  04460001
      *    TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.   04470001
      ***************************************************************** 04480001
           COPY DPPD004.                                                04490001
                                                                        04500001
           CALL 'ILBOABN0' USING ABEND-CODE.                            04510001
