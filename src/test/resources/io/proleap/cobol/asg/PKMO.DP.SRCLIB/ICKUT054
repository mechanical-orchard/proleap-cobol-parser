       IDENTIFICATION DIVISION.                                         00010004
       PROGRAM-ID.    ICKUT054.                                         00020004
       AUTHOR.        R. KELLEN.                                        00030004
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040004
       DATE-WRITTEN.  02/01/2008.                                       00050004
       DATE-COMPILED.                                                   00060004
                                                                        00070004
      ***************************************************************** 00080004
      * ICKUT054 - LOOKUP SKU_NBR FOR INTR_UPC_ID ON REPLEN MANIFEST    00090004
      *            FILE.                                                00100004
      ***************************************************************** 00110004
      *        THIS PROGRAM WILL READ A FILE OF TRUCK REPLENISHMENT     00120004
      *        MANIFESTS, LOOKS UP THE SKU_NBR AND THEN WRITES A NEW    00130004
      *        UPDATED FILE.                                            00140004
      *                                                                 00150004
      ***************************************************************** 00160004
      *                                                                 00170004
      *  TABLES USED ARE:                                               00180004
      *      XST_SKU                                                    00190004
      *  FILES READ ARE:                                                00200004
      *      TRUCK MANIFEST FILE                                        00210004
      ***************************************************************** 00220004
      * 02/02/06 - R KELLEN         -  WR NO. WR30388                   00230004
      *    - NEW PROGRAM.                                               00240004
      ***************************************************************** 00250004
                                                                        00260004
       ENVIRONMENT DIVISION.                                            00270004
       CONFIGURATION SECTION.                                           00280004
       SOURCE-COMPUTER. IBM-3090.                                       00290004
       OBJECT-COMPUTER. IBM-3090.                                       00300004
       INPUT-OUTPUT SECTION.                                            00310004
                                                                        00320004
       FILE-CONTROL.                                                    00330004
           SELECT TRUCK-MNFST-FILE           ASSIGN TO INP01.           00340004
                                                                        00350004
           SELECT UPDTD-TRUCK-MNFST-FILE     ASSIGN TO OUT01.           00360004
                                                                        00370004
      ***************************************************************** 00380004
       DATA DIVISION.                                                   00390004
      ***************************************************************** 00400004
       FILE SECTION.                                                    00410004
                                                                        00420004
       FD  TRUCK-MNFST-FILE                                             00430004
           LABEL RECORDS ARE STANDARD                                   00440004
           RECORDING MODE IS F                                          00450004
           BLOCK CONTAINS 0 RECORDS                                     00460004
           DATA RECORD IS TRUCK-MNFST-REC.                              00470004
       01  TRUCK-MNFST-REC            PIC  X(18).                       00480004
                                                                        00490004
       FD  UPDTD-TRUCK-MNFST-FILE                                       00500004
           LABEL RECORDS ARE STANDARD                                   00510004
           RECORDING MODE IS F                                          00520004
           BLOCK CONTAINS 0 RECORDS                                     00530004
           DATA RECORD IS UPDTD-TRUCK-MNFST-REC.                        00540004
       01  UPDTD-TRUCK-MNFST-REC      PIC  X(23).                       00550004
                                                                        00560004
                                                                        00570004
      *                                                                 00580004
       WORKING-STORAGE SECTION.                                         00590004
      ******************************************************************00600004
      * PC PROGRAM CONSTANTS                                           *00610004
      ******************************************************************00620004
       01  PC-PROGRAM-CONSTANTS.                                        00630004
           05  PC-PROGRAM-NAME                PIC X(08)    VALUE        00640004
                                                 'ICKUT054'.            00650004
           05  PC-MAXIMUM-RETRY-COUNT         PIC S9(04)   VALUE +2     00660004
                                                           COMP SYNC.   00670004
                                                                        00680004
      ******************************************************************00690004
      * PV PROGRAM VARIABLES                                           *00700004
      ******************************************************************00710004
       01  PV-WORK-VARIABLES.                                           00720004
           05  PV-RETRIES                     PIC S9(04)   VALUE +0     00730004
                                                           COMP.        00740004
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.00750004
           05  PV-COMPLETED-COUNT-FLG         PIC  X       VALUE SPACES.00760004
           05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.00770004
           05  PV-SQLCODE-DISPLAY             PIC +(8)9    VALUE ZEROES.00780000
           05  DI-RECORDS-READ                PIC  9(9)    VALUE ZEROES.00790000
                                                                        00800004
      ******************************************************************00810004
      * PS PROGRAM SWITCHES                                            *00820004
      ******************************************************************00830004
       01  PS-PROGRAM-SWITCHES.                                         00840004
           05  PS-END-OF-INPUT-FILE-SW        PIC  X(01)   VALUE 'N'.   00850004
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.   00860004
           05  PS-PROCESS-STOP-SW             PIC  X(01)   VALUE 'N'.   00870004
               88  PS-PROCESS-STOP                         VALUE 'Y'.   00880004
                                                                        00890004
       01  ABEND-CODE                         PIC S9(04)   VALUE +0     00900004
                                                           COMP SYNC.   00910004
                                                                        00920004
       01  DISPLAY-EDITS.                                               00930004
           05  DI-ACT-CNTR                  PIC  9(09)  VALUE ZEROS.    00940004
           05  DI-CLR-CNTR                  PIC  9(09)  VALUE ZEROS.    00950004
           05  DI-IMMED-CNTR                PIC  9(09)  VALUE ZEROS.    00960004
           05  DI-COMMIT-CNTR               PIC  9(09)  VALUE ZEROS.    00970004
                                                                        00980004
      *                                                                 00990004
      ***************************************************************** 01000004
      *    INPUT/OUTPUT COPYBOOKS                                       01010004
      ***************************************************************** 01020004
           COPY SFRD045.                                                01030004
                                                                        01040004
           COPY ICRD058.                                                01050004
      ***************************************************************** 01060004
      *    DB2 COMMUNICATION AREA                                       01070004
      ***************************************************************** 01080004
           COPY DPWS004.                                                01090004
                                                                        01100004
      ***************************************************************** 01110004
      *  XST_SKU TABLE                                                  01120004
      ***************************************************************** 01130004
           EXEC SQL                                                     01140004
                INCLUDE XST00010                                        01150004
           END-EXEC.                                                    01160004
                                                                        01170004
           EXEC SQL                                                     01180004
               INCLUDE SQLCA                                            01190004
           END-EXEC.                                                    01200004
                                                                        01210004
           COPY SQLCA2.                                                 01220004
                                                                        01230004
       PROCEDURE DIVISION.                                              01240004
      ******************************************************************01250004
      * 0000-MAIN-MODULE.                                              *01260004
      *  ==> PROCESSES:                                                *01270004
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *01280004
      ******************************************************************01290004
       0000-MAIN-MODULE.                                                01300004
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                01310004
                                                                        01320004
           PERFORM 1000-INITIALIZATION.                                 01330004
                                                                        01340004
           PERFORM 7000-READ-TRUCK-MNFST-FILE.                          01350004
           PERFORM 2000-PROCESS-SKU-LOOKUP                              01360004
             UNTIL PS-END-OF-INPUT-FILE.                                01370004
                                                                        01380004
           PERFORM 3000-EOJ-PROCESSING.                                 01390004
                                                                        01400004
           STOP RUN.                                                    01410004
                                                                        01420004
      ******************************************************************01430004
      * 1000-INITIALIZATION.                                           *01440004
      ******************************************************************01450004
       1000-INITIALIZATION.                                             01460004
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.            01470004
                                                                        01480004
           OPEN INPUT  TRUCK-MNFST-FILE                                 01490004
                OUTPUT UPDTD-TRUCK-MNFST-FILE.                          01500004
                                                                        01510004
      ******************************************************************01520004
      * 2000-PROCESS-SKU-LOOKUP.                                       *01530004
      *  ==> FINDS ALL SKUS FOR EACH COUNT/LOCATION                    *01540004
      ******************************************************************01550004
       2000-PROCESS-SKU-LOOKUP.                                         01560004
           MOVE '2000-PROCESS-SKU-LOOKUP' TO PV-PARAGRAPH-NAME.         01570004
           IF SFRD045-INTR-UPC-ID NOT = XST00010-INTR-UPC-ID            01580004
             MOVE SFRD045-INTR-UPC-ID TO XST00010-INTR-UPC-ID           01590004
             PERFORM 8000-SELECT-SKU-NBR                                01600004
           ELSE                                                         01610004
             MOVE SFRD045-LOC-NBR       TO  ICRD058-LOC-NBR             01620004
             MOVE SFRD045-INTR-UPC-ID   TO  ICRD058-INTR-UPC-ID         01630004
             MOVE SFRD045-DEPT-NBR      TO  ICRD058-DEPT-NBR            01640004
             MOVE SFRD045-MNFST-QTY     TO  ICRD058-MNFST-QTY           01650004
             MOVE SFRD045-BUS-GP-NBR    TO  ICRD058-BUS-GP-NBR          01660004
             MOVE SFRD045-MAJ-CL-NBR    TO  ICRD058-MAJ-CL-NBR          01670004
             MOVE XST00010-SKU-NBR   TO ICRD058-SKU-NBR                 01680004
             PERFORM 7100-WRITE-UPD-TRK-MFST-FL.                        01690004
           PERFORM 7000-READ-TRUCK-MNFST-FILE.                          01700004
                                                                        01710004
                                                                        01720004
      ******************************************************************01730004
      * 3000-EOJ-PROCESSING.                                           *01740004
      *  ==> PROCESSES:                                                *01750004
      *    - CLOSE FILES                                               *01760004
      *    - DISPLAYS PROCESSING TOTALS                                *01770004
      *    - TAKES A FINAL COMMIT                                      *01780004
      ******************************************************************01790004
       3000-EOJ-PROCESSING.                                             01800004
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.             01810004
                                                                        01820004
           CLOSE TRUCK-MNFST-FILE                                       01830004
                 UPDTD-TRUCK-MNFST-FILE.                                01840004
                                                                        01850004
           PERFORM 9990-DISPLAY-CONTROL-INFO.                           01860004
                                                                        01870004
      *----------------------------------------------------------       01880004
      * 7000-READ-CNT-STORE-EXP-FL.                                     01890004
      *----------------------------------------------------------       01900004
       7000-READ-TRUCK-MNFST-FILE.                                      01910004
           MOVE '7000-READ-TRUCK-MNFST-FILE' TO PV-PARAGRAPH-NAME.      01920004
           READ TRUCK-MNFST-FILE                                        01930004
             INTO SFRD045-REFORMAT-MANIFEST-FILE                        01940004
           AT END                                                       01950004
             SET PS-END-OF-INPUT-FILE TO TRUE                           01960004
           NOT AT END                                                   01970004
             ADD 1 TO DI-RECORDS-READ                                   01980004
           END-READ.                                                    01990004
                                                                        02000004
      *----------------------------------------------------------       02010004
      * 7100-WRITE-UPD-TRK-MFST-FL.                                     02020004
      *----------------------------------------------------------       02030004
       7100-WRITE-UPD-TRK-MFST-FL.                                      02040004
           MOVE '7100-WRITE-UPD-TRK-MFST-FL' TO PV-PARAGRAPH-NAME.      02050004
           WRITE UPDTD-TRUCK-MNFST-REC                                  02060004
             FROM ICRD058-REFORMAT-MANIFEST-FILE.                       02070004
                                                                        02080004
                                                                        02090004
      *-------------------------------------------------------------    02100004
      * 8000-SELECT-SKU-NBR.                                            02110004
      *      LOOKUP SKU_NBR FOR AN INTERNAL UPC ID                      02120004
      *-------------------------------------------------------------    02130004
       8000-SELECT-SKU-NBR.                                             02140004
           MOVE '8000-SELECT-SKU-NBR' TO PV-PARAGRAPH-NAME.             02150004
           EXEC SQL                                                     02160004
             SELECT  SKU_NBR                                            02170004
             INTO   :XST00010-SKU-NBR                                   02180004
             FROM   XST_SKU      A                                      02190004
             WHERE A.INTR_UPC_ID = :XST00010-INTR-UPC-ID                02200004
           END-EXEC.                                                    02210004
           EVALUATE TRUE                                                02220004
              WHEN SQLCODE = 0                                          02230004
                MOVE SFRD045-LOC-NBR       TO  ICRD058-LOC-NBR          02240004
                MOVE SFRD045-INTR-UPC-ID   TO  ICRD058-INTR-UPC-ID      02250004
                MOVE SFRD045-DEPT-NBR      TO  ICRD058-DEPT-NBR         02260004
                MOVE SFRD045-MNFST-QTY     TO  ICRD058-MNFST-QTY        02270004
                MOVE SFRD045-BUS-GP-NBR    TO  ICRD058-BUS-GP-NBR       02280004
                MOVE SFRD045-MAJ-CL-NBR    TO  ICRD058-MAJ-CL-NBR       02290004
                MOVE XST00010-SKU-NBR      TO  ICRD058-SKU-NBR          02300004
                PERFORM 7100-WRITE-UPD-TRK-MFST-FL                      02310004
              WHEN SQLCODE = +100                                       02320004
                DISPLAY 'SKU NOT FOUND FOR - ' XST00010-INTR-UPC-ID     02330004
              WHEN OTHER                                                02340004
               SET PS-PROCESS-STOP    TO TRUE                           02350004
               DISPLAY '** ICKUT054 SELECT SKU_NBR'                     02360004
                       ' HAD SQLCODE OF ' SQLCODE                       02370004
               PERFORM 9999-SQL-ABEND                                   02380004
           END-EVALUATE.                                                02390004
                                                                        02400004
      ******************************************************************02410004
      * 9990-DISPLAY-CONTROL-INFO.                                     *02420004
      *  ==> PROCESSES:                                                *02430004
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *02440004
      ******************************************************************02450004
       9990-DISPLAY-CONTROL-INFO.                                       02460004
                                                                        02470004
           DISPLAY '********************************************'.      02480004
           DISPLAY '**** RUN STATISTICS FOR PROGRAM ICKUT054 ***'.      02490004
           DISPLAY '****'.                                              02500004
           DISPLAY 'RECORDS READ               :' DI-RECORDS-READ.      02510004
           DISPLAY '****'.                                              02520004
           DISPLAY '**** RUN STATS END FOR PROGRAM ICKUT054  ***'.      02530004
           DISPLAY '********************************************'.      02540004
                                                                        02550004
      ******************************************************************02560004
      * 9999-SQL-ABEND.                                                *02570004
      *  ==> PROCESSES:                                                *02580004
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *02590004
      ******************************************************************02600004
       9999-SQL-ABEND.                                                  02610004
                                                                        02620004
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                          02630004
                                                                        02640004
           DISPLAY '** ABEND **'.                                       02650004
           DISPLAY 'ICKUT054 - ABEND'.                                  02660004
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                    02670004
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.           02680004
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                   02690004
           PERFORM 9990-DISPLAY-CONTROL-INFO.                           02700004
                                                                        02710004
           COPY DPPD004.                                                02720004
                                                                        02730004
           MOVE +4013 TO ABEND-CODE.                                    02740004
           CALL 'ILBOABN0' USING ABEND-CODE.                            02750004
                                                                        02760004
