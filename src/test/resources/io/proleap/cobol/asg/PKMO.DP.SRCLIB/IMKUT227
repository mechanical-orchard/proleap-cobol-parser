      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     IMKUT227.                                                
       AUTHOR.         MARK A FRAMNESS.                                         
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   02/16/11.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  STORE COMMON EXTRACT PROGRAM                                           
      *  (STORE HISTORY PROGRAM)                                                
      *  THIS PROGRAM COMPARES THE CURRENT SORTED UNLOAD OF TUPCPLS             
      *  WITH THE PREVIOUS UNLOAD OF TUPCPLS AND GENERATES AN INSERT            
      *  AND A DELETE FILE. THIS PROGRAM IS GOING TO BE RUN IN FOUR             
      *  SEPARATE AND PARALLEL JOBS EACH WORKING ON A BLOCK OF 50               
      *  PARTITIONS                                                             
      *                                                                         
      *  THIS PROGRAM WAS CLONED FROM CWKUT010 AND MODIFIED TO                  
      *  FIT INTO A PARTICUILAR INTERFACE ALREADY ESTABLISHED                   
      ******************************************************************        
TD0804*    CHANGED: 08/20/04    BY: THOMAS DUFFY                                
TD0804*      LOCATION DIMENSION - CHANGE LRECL FOR INPUT RECORDS                
TD0804*               FROM 668 TO 687, AND OUTPUT RECORD FROM                   
TD0804*               669 TO 688 TO USE V2 OF LOCATION EXTRACTS.                
TD0804*               ADDRESS OUTPUT DECREASED TO 97.                           
39512 *    CHANGED: 04/21/11    BY: MARK FRAMNESS                               
39512 *      ITEM ATTRIBUTE EXPORT TUPCPLS                                      
39512 *               PROGRAM CLONED FROM CWKT010                               
39512 *               COMPARE THE PREVIOUS SORTED TUPCPLS UNLOAD DS             
39512 *               TO THE CURRENT ONE. WRITE:                                
39512 *                NEW RECORDS TO THE INSERT DS                             
39512 *                UPDATE RECORDS NEW VERSION TO THE INSERT DS              
39512 *                DELETE RECORDS TO THE DELETE DS                          
39512 *                UPDATE RECORDS OLD VERSION TO THE DELETE DS              
39512 *                WRITE THE RECORDS ACCORDING TO ALREADY EST.              
39512 *                   FORAMT                                                
MAF212*    CHANGED: 03/03/12    BY: MARK FRAMNESS                               
MAF212*     WRITE ALL OUTPUT TO ONE FILE APPENDING A IUD INDICATOR              
MAF212*     TO EACH RECORD AS APPROPRIATE                                       
39512 ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CW-CURRENT-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CW-PREVIOUS-FILE                                              
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT CW-DELTA-FILE                                                 
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CW-CURRENT-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-CURRENT-RECORD                  PIC X(122).                       
                                                                                
       FD  CW-PREVIOUS-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-PREVIOUS-RECORD                 PIC X(122).                       
                                                                                
       FD  CW-DELTA-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-DELTA-RECORD                    PIC X(161).                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01 DEBUG-STUFF-HEAD.                                                     
           05  FILLER                         PIC   X(35)                       
           VALUE '*** WORKING-STORAGE STARTS HERE ***'.                         
                                                                                
       01  CURRENT-REC.                                                         
           05  CR-CURR-KEY                    PIC   X(32).                      
           05  CR-CURR-DATA                   PIC   X(90).                      
                                                                                
       01  PREVIOUS-REC.                                                        
           05  PR-PREV-KEY                    PIC   X(32).                      
           05  PR-PREV-DATA                   PIC   X(90).                      
                                                                                
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'IMKUT227'.            
           05  PC-OPERCO                 PIC X(02) VALUE '03'.                  
MAF212     05  PC-DELTA-INSERT           PIC X(01) VALUE 'I'.                   
MAF212     05  PC-DELTA-UPDATE           PIC X(01) VALUE 'U'.                   
MAF212     05  PC-DELTA-DELETE           PIC X(01) VALUE 'D'.                   
                                                                                
                                                                                
       01  PV-NUMERIC-UNPCK-FLDS.                                               
           05  PV-UPC-ID-UNPCK           PIC S9(09).                            
           05  PV-STORE-NBR-UNPCK        PIC S9(04).                            
           05  PV-UNT-CST-AMT-UNPCK      PIC S9(07)V9(3).                       
           05  PV-UNT-RTL-AMT-UNPCK      PIC S9(07)V9(2).                       
           05  PV-MEITGRP-NBR-UNPCK      PIC S9(09).                            
           05  PF-S910-COMP              PIC S9(10) COMP.                       
           05  PF-S910-UNPACK            PIC S9(10).                            
           05  PF-S95-COMP               PIC S9(05) COMP.                       
           05  PF-S95-UNPACK             PIC S9(05).                            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-PRE-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-CUR-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-OUTPUT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-INSERT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-UPDATE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-DELETE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-NO-CHANGE-RECORDS      PIC  9(9)       VALUE ZERO.            
      ******************************************************************        
      * OUTPUT LAYOUT FOR DELTA FILES -- BOTH DELETE & INSERT                   
      ******************************************************************        
       01 DLTA-RECORD.                                                          
           05 DLTA-KEY-FLDS.                                                    
              10 DLTA-INTR-UPC-ID.                                              
                 15 DLTA-INTR-UPC-ID-NBR    PIC 9999999999                      
                                                      VALUE ZEROS.              
                 15 FILLER                  PIC X(01) VALUE '+'.                
              10 FILLER                     PIC X(01) VALUE '|'.                
              10 DLTA-STR-NBR               PIC 99999 VALUE ZEROS.              
              10 FILLER                     PIC X(01) VALUE '+'.                
              10 FILLER                     PIC X(01) VALUE '|'.                
              10 DLTA-EFF-BEG-TMST          PIC X(26) VALUE SPACES.             
              10 FILLER                     PIC X(01) VALUE '|'.                
           05 DLTA-EFF-END-TMST          PIC X(26) VALUE SPACES.                
           05 FILLER                     PIC X(01) VALUE '|'.                   
           05 DLTA-UNT-COST-AMT          PIC 9999999.999 VALUE ZEROS.           
           05 DLTA-UNT-COST-AMT-SGN      PIC X(01) VALUE SPACE.                 
           05 FILLER                     PIC X(01) VALUE '|'.                   
           05 DLTA-UNT-RTL-AMT           PIC 9999999.99  VALUE ZEROS.           
           05 DLTA-UNT-RTL-AMT-SGN       PIC X(01) VALUE SPACE.                 
           05 FILLER                     PIC X(01) VALUE '|'.                   
           05 DLTA-UNT-RTL-CHG-DTE       PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(01) VALUE '|'.                   
           05 DLTA-SKU-STAT-CDE          PIC X(02) VALUE SPACES.                
           05 FILLER                     PIC X(01) VALUE '|'.                   
           05 DLTA-SKU-STAT-CHG-DTE      PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(01) VALUE '|'.                   
           05 DLTA-PLU-IND               PIC X(01) VALUE SPACE.                 
           05 FILLER                     PIC X(01) VALUE '|'.                   
           05 DLTA-MEITGP-NBR            PIC 9999999999 VALUE ZEROS.            
           05 FILLER                     PIC X(01) VALUE '+'.                   
           05 FILLER                     PIC X(01) VALUE '|'.                   
           05 DLTA-CRE-BY-USR-ID         PIC X(08) VALUE SPACES.                
           05 FILLER                     PIC X(01) VALUE '|'.                   
           05 DLTA-ORIG-CLR-DTE          PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(01) VALUE '|'.                   
           05 DLTA-MKDN-CDE              PIC X(01) VALUE SPACE.                 
           05 FILLER                     PIC X(01) VALUE '|'.                   
MAF212     05 DLTA-DELTA-TYPE            PIC X(01) VALUE SPACE.                 
MAF212     05 FILLER                     PIC X(01) VALUE '|'.                   
      ******************************************************************        
      * INPUT LAYOUT FOR TUPCPLS UNLOAD FILE                                    
      ******************************************************************        
           COPY IMRD056.                                                        
                                                                                
       01 DEBUG-STUFF-HEAD.                                                     
           05  FILLER                         PIC   X(35)                       
           VALUE '*** WORKING-STORAGE ENDS   HERE ***'.                         
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL CR-CURR-KEY = HIGH-VALUES                                  
                     AND                                                        
                     PR-PREV-KEY = HIGH-VALUES.                                 
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  CW-CURRENT-FILE                                          
                       CW-PREVIOUS-FILE                                         
                OUTPUT CW-DELTA-FILE.                                           
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 8000-READ-CURRENT.                                           
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
                                                                                
           IF CR-CURR-KEY  =  PR-PREV-KEY                                       
              PERFORM 2100-CHECK-EQUALITY                                       
           ELSE                                                                 
               IF CR-CURR-KEY  <  PR-PREV-KEY                                   
      *            INSERT RECORD                                                
      *            WRITE RECORD FROM NEW FILE                                   
                   INITIALIZE            IM056-ITEM-SKU-LOC-UNLD-REC            
                   MOVE CURRENT-REC   TO IM056-ITEM-SKU-LOC-UNLD-REC            
                   PERFORM 3000-PREPARE-OUTPUT                                  
MAF212             MOVE PC-DELTA-INSERT TO DLTA-DELTA-TYPE                      
                   PERFORM 7100-WRITE-DELTA                                     
                   ADD +1             TO PA-INSERT-RECORDS                      
                   PERFORM 8000-READ-CURRENT                                    
               ELSE                                                             
      *            DELETE RECORD                                                
      *            FORMAT & WRITE PREV RECORD TO DELETE FILE                    
                   INITIALIZE            IM056-ITEM-SKU-LOC-UNLD-REC            
                   MOVE PREVIOUS-REC  TO IM056-ITEM-SKU-LOC-UNLD-REC            
                   PERFORM 3000-PREPARE-OUTPUT                                  
MAF212             MOVE PC-DELTA-DELETE TO DLTA-DELTA-TYPE                      
                   PERFORM 7100-WRITE-DELTA                                     
                   ADD +1             TO PA-DELETE-RECORDS                      
                   PERFORM 8100-READ-PREVIOUS                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS RECORD FOR EQUALITY                                             
      *  IF DATA IS NOT EQUAL, THEN RECORD IS AN UPDATE                         
      ******************************************************************        
       2100-CHECK-EQUALITY.                                                     
           IF CR-CURR-DATA  =  PR-PREV-DATA                                     
      *       RECORDS IDENTICAL BYPASS                                          
               ADD +1                 TO PA-NO-CHANGE-RECORDS                   
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           ELSE                                                                 
      *        UPDATE RECORD                                                    
      *        WRITE OUT UPDATE RECORD FROM CURRENT INPUT                       
               INITIALIZE             IM056-ITEM-SKU-LOC-UNLD-REC               
               MOVE CURRENT-REC    TO IM056-ITEM-SKU-LOC-UNLD-REC               
               PERFORM 3000-PREPARE-OUTPUT                                      
MAF212         MOVE PC-DELTA-UPDATE TO DLTA-DELTA-TYPE                          
               PERFORM 7100-WRITE-DELTA                                         
               ADD +1              TO PA-UPDATE-RECORDS                         
                                                                                
      *        READ IN NEXT CURRENT & NEXT PREVIOUS RECORDS                     
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  UNPACK NUMERICS AND MOVE ALL FIELDS TO OUTPUT RECORD                   
      ******************************************************************        
       3000-PREPARE-OUTPUT.                                                     
      *REQUIRES IMRD056 INPUT RECORD TO BE LOADED                               
      *PROMISES TO UNPACK NUMERICS, PREPARE SIGNS, SCREEN OUT                   
      * NULLS, AND LOAD NULL VALUES WITH SPACES IN RECORD READY                 
      * FOR WRITING TO DELETE & INSERT DATASETS                                 
                                                                                
      * FIRST UNPACK AND READY NUMERIC OUTPUT                                   
      *HIGH-VALUES IN NULL-IND FIELDS INDICATE A NULL VALUE                     
      *LOW-VALUES IN NULL-IND FIELDS INDICATE A POPULATED VALUE                 
      *                                                                         
      *VERY IMPORTANT -- UNPACK ALL COMP FIELDS INTO FIELDS ONE BYTE            
      *                 LARGER THAN THEIR COMP DEF'N IMPLIES                    
                                                                                
           INITIALIZE DLTA-RECORD                                               
                                                                                
           MOVE IM056-INTR-UPC-ID    TO PF-S910-COMP                            
           MOVE PF-S910-COMP         TO PF-S910-UNPACK                          
           MOVE PF-S910-UNPACK       TO DLTA-INTR-UPC-ID-NBR                    
                                                                                
           MOVE IM056-STR-NBR        TO PF-S95-COMP                             
           MOVE PF-S95-COMP          TO PF-S95-UNPACK                           
           MOVE PF-S95-UNPACK        TO DLTA-STR-NBR                            
                                                                                
           IF IM056-UNT-COST-NULL-IND = LOW-VALUES                              
               MOVE IM056-UNT-COST-AMT   TO PV-UNT-CST-AMT-UNPCK                
               MOVE PV-UNT-CST-AMT-UNPCK TO DLTA-UNT-COST-AMT                   
               IF PV-UNT-CST-AMT-UNPCK < 0                                      
                   MOVE '-' TO DLTA-UNT-COST-AMT-SGN                            
               ELSE                                                             
                   MOVE '+' TO DLTA-UNT-COST-AMT-SGN                            
               END-IF                                                           
           ELSE                                                                 
               MOVE ZEROES TO DLTA-UNT-COST-AMT                                 
               MOVE SPACE  TO DLTA-UNT-COST-AMT-SGN                             
           END-IF                                                               
                                                                                
           IF IM056-UNT-RTL-NULL-IND = LOW-VALUES                               
               MOVE IM056-UNT-RTL-AMT    TO PV-UNT-RTL-AMT-UNPCK                
               MOVE PV-UNT-RTL-AMT-UNPCK TO DLTA-UNT-RTL-AMT                    
               IF PV-UNT-RTL-AMT-UNPCK < 0                                      
                   MOVE '-' TO DLTA-UNT-RTL-AMT-SGN                             
               ELSE                                                             
                   MOVE '+' TO DLTA-UNT-RTL-AMT-SGN                             
               END-IF                                                           
           ELSE                                                                 
               MOVE ZEROES  TO DLTA-UNT-RTL-AMT                                 
               MOVE SPACE   TO DLTA-UNT-RTL-AMT-SGN                             
           END-IF                                                               
                                                                                
           IF IM056-MEITGP-NULL-IND = LOW-VALUES                                
               MOVE IM056-MEITGP-NBR      TO PF-S910-COMP                       
               MOVE PF-S910-COMP          TO PF-S910-UNPACK                     
               MOVE PF-S910-UNPACK        TO DLTA-MEITGP-NBR                    
           ELSE                                                                 
               MOVE ZEROES                TO DLTA-MEITGP-NBR                    
           END-IF                                                               
                                                                                
      * NOW PREPARE NON-NUMERIC OUTPUT                                          
           MOVE IM056-EFF-BEG-TMST     TO DLTA-EFF-BEG-TMST                     
                                                                                
           IF IM056-EFF-END-TMST NOT = LOW-VALUES                               
               MOVE IM056-EFF-END-TMST TO DLTA-EFF-END-TMST                     
           ELSE                                                                 
               MOVE SPACES TO DLTA-EFF-END-TMST                                 
           END-IF                                                               
                                                                                
           MOVE IM056-UNT-RTL-CHG-DTE  TO DLTA-UNT-RTL-CHG-DTE                  
                                                                                
           IF IM056-SKU-STAT-CDE NOT = LOW-VALUES                               
               MOVE IM056-SKU-STAT-CDE TO DLTA-SKU-STAT-CDE                     
           ELSE                                                                 
               MOVE SPACES             TO DLTA-SKU-STAT-CDE                     
           END-IF                                                               
                                                                                
           MOVE IM056-SKU-STAT-CHG-DTE TO DLTA-SKU-STAT-CHG-DTE                 
           MOVE IM056-PLU-IND          TO DLTA-PLU-IND                          
           MOVE IM056-CRE-BY-USR-ID    TO DLTA-CRE-BY-USR-ID                    
                                                                                
           IF IM056-ORIG-CLR-DTE NOT = LOW-VALUES                               
               MOVE IM056-ORIG-CLR-DTE TO DLTA-ORIG-CLR-DTE                     
           ELSE                                                                 
               MOVE SPACES             TO DLTA-ORIG-CLR-DTE                     
           END-IF                                                               
                                                                                
      *THE SORT OF THE UNLOAD DATASET CHANGES SPACES TO NULLS                   
      *FOR THIS FIELD, BUT DOES NOT TOUCH THE NI, IF THE FIELD                  
      *IN DB2 IS SPACES THE VALUE WILL GET FLIPPED TO NULLS BUT THE             
      *NI IS UNAFFECTED SO DROP THE NI CHECK IN FAVOR OF                        
      *   IM056-MKDN-CDE NOT = LOWVALUES ONLY                                   
           IF IM056-MKDN-CDE NOT = LOW-VALUES                                   
               MOVE IM056-MKDN-CDE     TO DLTA-MKDN-CDE                         
           ELSE                                                                 
               MOVE SPACES             TO DLTA-MKDN-CDE                         
           END-IF                                                               
           .                                                                    
       3000-EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE THE CHANGE RECORD                                                
      ******************************************************************        
       7100-WRITE-DELTA.                                                        
           WRITE  CW-DELTA-RECORD FROM DLTA-RECORD.                             
           ADD +1 TO PA-OUTPUT-RECORDS.                                         
       7100-EXIT.                                                               
                                                                                
      ******************************************************************        
      *  READ THE CURRENT FILE.                                        *        
      ******************************************************************        
       8000-READ-CURRENT.                                                       
           READ CW-CURRENT-FILE                                                 
               INTO CURRENT-REC                                                 
               AT END                                                           
                   MOVE HIGH-VALUES  TO  CR-CURR-KEY                            
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-CUR-RECORDS                     
           END-READ.                                                            
       8000-EXIT.                                                               
                                                                                
      ******************************************************************        
      *  READ THE PREVIOUS FILE.                                                
      ******************************************************************        
       8100-READ-PREVIOUS.                                                      
           READ CW-PREVIOUS-FILE                                                
               INTO PREVIOUS-REC                                                
               AT END                                                           
                   MOVE HIGH-VALUES  TO  PR-PREV-KEY                            
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-PRE-RECORDS                     
           END-READ.                                                            
       8100-EXIT.                                                               
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
MAF212     DISPLAY '   '.                                                       
MAF212     DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
MAF212     DISPLAY '   '.                                                       
                                                                                
MAF212     DISPLAY 'INPUT SECTION'                                              
MAF212     DISPLAY 'INPUT PREVIOUS RECS  : ' PA-INPUT-PRE-RECORDS.              
MAF212     DISPLAY 'INPUT CURRENT RECS   : ' PA-INPUT-CUR-RECORDS.              
MAF212     DISPLAY '========================================'                   
MAF212     DISPLAY 'TOTAL INPUT RECS READ: ' PA-INPUT-RECORDS.                  
MAF212     DISPLAY '   '.                                                       
MAF212     DISPLAY 'OUTPUT SECTION'                                             
MAF212     DISPLAY 'DELETE RECS WRITTEN  : ' PA-DELETE-RECORDS.                 
MAF212     DISPLAY 'INSERT RECS WRITTEN  : ' PA-INSERT-RECORDS.                 
MAF212     DISPLAY 'UPDATE RECS WRITTEN  : ' PA-UPDATE-RECORDS.                 
MAF212     DISPLAY '========================================'                   
MAF212     DISPLAY 'TOTAL OUTPUT RECORDS : ' PA-OUTPUT-RECORDS.                 
MAF212     DISPLAY '   '.                                                       
MAF212     DISPLAY 'OTHER SECTION'                                              
MAF212     DISPLAY 'MATCHING RECORDS     : ' PA-NO-CHANGE-RECORDS.              
MAF212     DISPLAY '   '.                                                       
                                                                                
           CLOSE CW-CURRENT-FILE                                                
                 CW-PREVIOUS-FILE                                               
                 CW-DELTA-FILE.                                                 
                                                                                
      ******************************************************************        
      ******************************************************************        
