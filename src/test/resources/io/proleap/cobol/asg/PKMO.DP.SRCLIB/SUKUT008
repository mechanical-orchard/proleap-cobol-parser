000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUT008.                                                 
000600 AUTHOR.        STEPHANIE HERON                                           
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   JULY, 1997.                                               
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM WILL INSERT A LOCATION RECORD FOR ALL CARTONS THAT        
001300*  HAVE NOT BEEN CHECKED IN, IN OVER 9 MONTHS.  THIS WILL ALLOW           
001310*  THE REST OF THE JOBSTEAM TO PICK UP THE DATA AS USUAL                  
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900                                                                          
002901     SELECT INSERT-FILE                                                   
002902         ASSIGN TO IN01.                                                  
002910                                                                          
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700                                                                          
003800                                                                          
003801 FD  INSERT-FILE                                                          
003802     RECORDING MODE IS F                                                  
003803     LABEL RECORDS ARE STANDARD                                           
003804     BLOCK CONTAINS 0 RECORDS.                                            
003805                                                                          
003810 01  INSERT-RECORD              PIC  X(100).                              
003900                                                                          
004500*                                                                         
004600*                                                                         
004700******************************************************************        
004800 WORKING-STORAGE SECTION.                                                 
004900******************************************************************        
005000*                                                                         
005100*                                                                         
005600*                                                                         
005700*                                                                         
005800******************************************************************        
005900*  RECORD LAYOUT FOR THE TIMESTAMP FILE.                                  
006000******************************************************************        
006100     COPY SURD002.                                                        
006200*                                                                         
006300*                                                                         
006400*                                                                         
006500******************************************************************        
006600*  DB2 FORMATTED MESSAGE AREA                                             
006700******************************************************************        
006800     COPY DPWS004.                                                        
006900*                                                                         
007000*                                                                         
007100*                                                                         
007200******************************************************************        
007300*  COMMUNICATIONS AREA FOR DB2                                            
007400******************************************************************        
007500     EXEC SQL                                                             
007600          INCLUDE SQLCA                                                   
007700     END-EXEC.                                                            
007800                                                                          
008600******************************************************************        
008700*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
008800*  LOCATION TABLE                                                         
008900******************************************************************        
009000     EXEC SQL                                                             
009100          INCLUDE TSHUNLO                                                 
009200     END-EXEC.                                                            
009300*                                                                         
009400 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
009500                                                   COMP SYNC.             
009600                                                                          
009700 01  PS-PROGRAM-SWITCHES.                                                 
009800     05  PS-END-OF-FILE-SW           PIC  X(01)    VALUE 'N'.             
009900         88  PS-END-OF-FILE                        VALUE 'Y'.             
010000         88  PS-NOT-END-OF-FILE                    VALUE 'N'.             
010100                                                                          
010200                                                                          
010300 01  PC-PROGRAM-CONSTANTS.                                                
010400     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
010500                                                   COMP SYNC.             
010600     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
010700         'SUKUT008'.                                                      
010800     05  PC-COMMIT-HOLD              PIC 9(3)      VALUE 200.             
010801     05  PC-INSERT-TIMESTAMP         PIC  X(26)    VALUE SPACES.          
010810     05  PC-CURRENT-TIMESTAMP        PIC  X(26)    VALUE SPACES.          
010820     05  PC-CURRENT-TIMESTAMP-RDF  REDEFINES PC-CURRENT-TIMESTAMP.        
010830         10  PC-CURRENT-DATE         PIC X(10).                           
010840         10  PC-CURRENT-TIME         PIC X(16).                           
010850     05  PC-EXTRACT-DATE             PIC X(10).                           
010900                                                                          
011000 01  PV-PROGRAM-VARIABLES.                                                
011100     05  PV-ITEM-NBR                 PIC  9(15)    VALUE ZERO.            
011200     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
011300     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
011400     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO             
011500                                                   COMP SYNC.             
011600     05  PV-CLOSE-COUNTER            PIC S9(04)    VALUE ZERO             
011700                                                   COMP SYNC.             
011800     05  PV-FETCH-COUNTER            PIC S9(04)    VALUE ZERO             
011900                                                   COMP SYNC.             
012000     05  PC-INSERT-COUNT             PIC S9(09)    VALUE ZERO             
012100                                                   COMP SYNC.             
012110     05  PC-READ-COUNT               PIC S9(09)    VALUE ZERO             
012120                                                   COMP SYNC.             
012200     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
012300     05  PV-COMMIT-COUNT             PIC S9(09)    VALUE +0               
012400                                                   COMP SYNC.             
012500     05  PV-COMMIT-COUNTER           PIC 9(03)     VALUE ZERO.            
012600                                                                          
012700     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO             
012800                                                   COMP-3.                
012900                                                                          
013000                                                                          
013100 01  SD-SYSTEM-DATE.                                                      
013200     05  SD-SYSTEM-DATE-YY           PIC  9(02)      VALUE ZERO.          
013300     05  SD-SYSTEM-DATE-MM           PIC  9(02)      VALUE ZERO.          
013400     05  SD-SYSTEM-DATE-DD           PIC  9(02)      VALUE ZERO.          
013500                                                                          
013600                                                                          
013630                                                                          
013700******************************************************************        
013800 PROCEDURE DIVISION.                                                      
013900******************************************************************        
014000                                                                          
014100 0000-PROGRAM-MAINLINE.                                                   
014200******************************************************************        
014300* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
014400*      PROGRAM.                                                           
014500******************************************************************        
014600                                                                          
014700     PERFORM 1000-INITIALIZE.                                             
014800     IF PS-NOT-END-OF-FILE                                                
014900         PERFORM 2000-PROCESS-SHIPMENT-LOCS                               
015000            UNTIL PS-END-OF-FILE                                          
015100     END-IF.                                                              
015200     PERFORM 9000-EOJ-ROUTINE.                                            
015300                                                                          
015400     STOP RUN.                                                            
015500                                                                          
015600 1000-INITIALIZE.                                                         
015700******************************************************************        
015800*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
015900*    -- OPEN INPUT FILE                                                   
016000*    -- READ FILE                                                         
016100******************************************************************        
016200                                                                          
016300     OPEN INPUT INSERT-FILE.                                              
016320     PERFORM 7100-READ-INPUT-FILE.                                        
016700                                                                          
016800 2000-PROCESS-SHIPMENT-LOCS.                                              
016900******************************************************************        
017000*  PROCESS THE SHIPPING UNIT RECORDS                                      
017300******************************************************************        
017400                                                                          
017500     PERFORM 8000-INSERT-LOCATION-RECS.                                   
017600     ADD +1 TO PV-COMMIT-COUNTER.                                         
017700                                                                          
017800     IF PV-COMMIT-COUNTER > PC-COMMIT-HOLD                                
017900         EXEC SQL                                                         
018000            COMMIT                                                        
018100         END-EXEC                                                         
018200                                                                          
018300         IF SQLCODE NOT = +0                                              
018400             DISPLAY '### COMMIT FAILED ! ###'                            
018500             PERFORM 9100-SQL-ABEND                                       
018600         END-IF                                                           
018700                                                                          
018800         ADD +1                          TO PV-COMMIT-COUNT               
018900         MOVE +0 TO PV-COMMIT-COUNTER                                     
019000     END-IF.                                                              
019100                                                                          
019110     PERFORM 7100-READ-INPUT-FILE.                                        
019300                                                                          
020000*                                                                         
020040 7100-READ-INPUT-FILE.                                                    
020041******************************************************************        
020042*  FETCH THE ROW FROM THE RESULTS TABLE USING THE FILE.                   
020043******************************************************************        
020044                                                                          
020047     READ INSERT-FILE INTO SU002-EXTRACT-RECORD                           
020048         AT END                                                           
020049           SET PS-END-OF-FILE TO TRUE.                                    
020061                                                                          
020062     IF PS-END-OF-FILE                                                    
020063        CONTINUE                                                          
020064     ELSE                                                                 
020065        ADD +1 TO PC-READ-COUNT                                           
020066     END-IF.                                                              
020080                                                                          
020120 8000-INSERT-LOCATION-RECS.                                               
020200******************************************************************        
020300*  INSERT THE NEW LOCATION RECORD                                         
020400******************************************************************        
020500                                                                          
020510     INITIALIZE  DCLTSHUNLO.                                              
020520                                                                          
020600     EXEC SQL                                                             
020700         INSERT INTO TSHUNLO                                              
020800                (SHIPT_UNT_ID,                                            
020900                 OPER_UNT_ID,                                             
020910                 FCLTY_ID,                                                
020920                 SCN_LOC_ID,                                              
020930                 CRE_USR_ID,                                              
020940                 CRE_TMST,                                                
020950                 TRIP_ID)                                                 
020960          VALUES                                                          
020970                (:SU002-SHIPT-UNT-ID                                      
020980                ,:SU002-OPER-UNT-ID                                       
020990                ,:SU002-FCLTY-ID                                          
020991                ,:SU002-SCN-LOC-ID                                        
020992                ,:PC-CURRENT-PROGRAM-NAME                                 
020993                ,:SU002-CRE-TMST                                          
020994                ,:SU002-TRIP-ID)                                          
021000     END-EXEC.                                                            
021100                                                                          
021200     EVALUATE TRUE                                                        
021300         WHEN SQLCODE = 0                                                 
021400             ADD +1           TO PC-INSERT-COUNT                          
021700         WHEN SQLCODE < 0                                                 
021800         WHEN SQLCODE > 0                                                 
021900         WHEN SQLWARN0 = 'W'                                              
022000             MOVE '8000-INSERT-LOCATION-RECS'                             
022100                         TO PV-PARAGRAPH-NAME                             
022200             MOVE 'INSERT OF TSHUNLO'                                     
022300                         TO PV-DB2-OPERATION-ATTEMPTED                    
022400             PERFORM 9100-SQL-ABEND                                       
022500     END-EVALUATE.                                                        
022600                                                                          
022800 9000-EOJ-ROUTINE.                                                        
022900******************************************************************        
023000*  CLOSE OUTPUT FILE.  CLOSE THE FILE.                                    
023100******************************************************************        
023200     CLOSE INSERT-FILE.                                                   
023300                                                                          
023400     MOVE PC-READ-COUNT  TO PV-DISPLAY-COUNT.                             
023500     DISPLAY 'TOTAL RECORDS READ  ' PV-DISPLAY-COUNT.                     
023600     MOVE PC-INSERT-COUNT  TO PV-DISPLAY-COUNT.                           
023700     DISPLAY 'TOTAL INSERTS MADE  ' PV-DISPLAY-COUNT.                     
023800                                                                          
023900                                                                          
024000 9100-SQL-ABEND.                                                          
024100******************************************************************        
024200*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
024300*  ERROR OR WARNING CODE OCCURS.                                          
024400******************************************************************        
024500     DISPLAY '9100-SQL-ABEND'.                                            
024600     DISPLAY '** ABEND     **'.                                           
024700     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
024800     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
024900     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
025000                                                                          
025100******************************************************************        
025200* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
025300* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
025400* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
025500* FORMAT.                                                                 
025600******************************************************************        
025700     COPY DPPD004.                                                        
025800                                                                          
025900     MOVE +4013                  TO ABEND-CODE.                           
026000     CALL 'ILBOABN0' USING ABEND-CODE.                                    
