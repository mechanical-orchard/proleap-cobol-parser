000100 IDENTIFICATION DIVISION.                                                 
000200 TITLE 'INTRANSIT DELETES'.                                               
000300                                                                          
000400 PROGRAM-ID.             PCKUP010.                                        
000500 AUTHOR.                 DENNIS STEFFEN.                                  
000600 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000700 DATE-WRITTEN            AUTUMN 2004.                                     
000800 DATE-COMPILED.                                                           
000900                                                                          
001000 ENVIRONMENT DIVISION.                                                    
001100 CONFIGURATION SECTION.                                                   
001200                                                                          
001300 SOURCE-COMPUTER.        IBM-3090.                                        
001400 OBJECT-COMPUTER.        IBM-3090.                                        
001500                                                                          
001600 INPUT-OUTPUT SECTION.                                                    
001700                                                                          
001800 FILE-CONTROL.                                                            
001900                                                                          
002000     SELECT IN-TRANSIT-EXTRACT-FILE                                       
002100         ASSIGN TO INP01.                                                 
002200                                                                          
002300     SELECT CUTOFF-DATE-FILE                                              
002400         ASSIGN TO INP02.                                                 
002500                                                                          
002600                                                                          
002700 DATA DIVISION.                                                           
002800 FILE SECTION.                                                            
002900                                                                          
003000 FD  IN-TRANSIT-EXTRACT-FILE                                              
003100     RECORDING MODE IS F                                                  
003200     LABEL RECORDS ARE STANDARD                                           
003300     BLOCK CONTAINS 0 RECORDS.                                            
003400                                                                          
003500 01  IN-TRANSIT-RECORD    PIC  X(022).                                    
003600                                                                          
003700 FD  CUTOFF-DATE-FILE                                                     
003800     RECORDING MODE IS F                                                  
003900     LABEL RECORDS ARE STANDARD                                           
004000     BLOCK CONTAINS 0 RECORDS.                                            
004100 01  CUTOFF-DATE-REC                  PIC X(11).                          
004200                                                                          
004300 WORKING-STORAGE SECTION.                                                 
004400                                                                          
004500                                                                          
004600 01  FILLER.                                                              
004700     05  FILLER                      PIC  X(36)    VALUE                  
004800         ' ***** WORKING STORAGE PGM=PCKUP010 '.                          
004900     05  PGM-COMPILED                PIC  X(20)    VALUE SPACE.           
005000     05  FILLER                      PIC  X(10)    VALUE                  
005100         ' LOCATION='.                                                    
005200     05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.           
005300     05  FILLER                      PIC  X(06)    VALUE                  
005400         ' *****'.                                                        
005500                                                                          
005600 01  PV-CUTOFF-DATE-REC.                                                  
005700     05  PV-CUTOFF-DATE               PIC X(10).                          
005800     05  FILLER                       PIC X(70).                          
005900                                                                          
006000 01  PV-DISP-KEY-MSG.                                                     
006100     05  FILLER                      PIC X(14) VALUE                      
006200         'INTR-UPC-ID = '.                                                
006300     05  PV-KEY-INTR-UPC-ID          PIC 9(09) VALUE ZEROES.              
006400     05  FILLER                      PIC X(12) VALUE                      
006500         '; LOC-NBR = '.                                                  
006600     05  PV-KEY-LOC-NBR              PIC 9(04) VALUE ZEROES.              
006700     05  FILLER                      PIC X(13) VALUE                      
006800         '; CLSN-CDE = '.                                                 
006900     05  PV-KEY-CLSN-CDE             PIC X(02) VALUE ZEROES.              
007000                                                                          
007100 01  PS-SWITCHES.                                                         
007200     05  PS-EOF-SW                    PIC X(01) VALUE 'N'.                
007300         88  PS-EOF-EXTRACT-YES                 VALUE 'Y'.                
007400         88  PS-EOF-EXTRACT-NO                  VALUE 'N'.                
007500*    05  PS-VERIFY-SW                 PIC X(01) VALUE 'N'.                
007600*        88  PS-VERIFY-YES                      VALUE 'Y'.                
007700*        88  PS-VERIFY-NO                       VALUE 'N'.                
007800                                                                          
007900 01  PC-CONSTANTS.                                                        
008000     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
008100                                                   'PCKUP010'.            
008200     05  PC-OPEN-FUNC                PIC X(05)     VALUE 'OPEN '.         
008300     05  PC-TRANS-FUNC               PIC X(05)     VALUE 'TRANS'.         
008400     05  PC-CLOSE-FUNC               PIC X(05)     VALUE 'CLOSE'.         
008500     05  PC-RSTRT-FUNC               PIC X(05)     VALUE 'RSTRT'.         
008600     05  PV-CKPT-JOB-NAME            PIC X(08)   VALUE 'PCK026'.          
008700     05  PC-CKPT-PLAN-NAME           PIC X(08)   VALUE 'PCKUP010'.        
008800     05  PC-ILBOABN0                 PIC  X(08)    VALUE                  
008900                                                   'ILBOABN0'.            
009000     05  PC-MAX-RETRIES              PIC S9(04)    VALUE  +3              
009100                                                   COMP.                  
009200     05  PC-DEFAULT-DATE             PIC  X(10)    VALUE                  
009300                                     '0001-01-01'.                        
009400     05  PC-DEFAULT-TIME             PIC  X(08)    VALUE                  
009500                                     '00.00.00'.                          
009600                                                                          
009700                                                                          
009800 01  PV-MISC-STUFF.                                                       
009900     05  PV-ABEND-PARAGRAPH           PIC  X(30) VALUE SPACES.            
010000     05  PV-DB2-OPERATION-ATTEMPTED   PIC  X(60) VALUE SPACE.             
010100     05  PV-SQL-CODE                  PIC  9(04) VALUE ZERO.              
010200     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
010300                                                   COMP SYNC.             
010400     05  PV-RETRY-COUNTER             PIC S9(04) VALUE ZERO               
010500                                                 COMP SYNC.               
010600     05  PV-READ-COUNTER              PIC S9(09) COMP-3                   
010700                                              VALUE ZERO.                 
010800     05 PV-QTY-DELETE-COUNTER         PIC S9(09) COMP-3                   
010900                                              VALUE ZERO.                 
011000     05 PV-BYPASS-COUNT               PIC S9(09) COMP-3                   
011100                                              VALUE ZERO.                 
011200     05  PV-DISP-NBR                  PIC ZZZ,ZZZ,ZZ9.                    
011300                                                                          
011400 01  ABEND-CODE                       PIC S9(04)     VALUE +0             
011500                                                     COMP SYNC.           
011600     88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
011700     88  ABEND-4002-READ-ERROR                       VALUE +4002.         
011800     88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
011900     88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
012000     88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
012100     88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
012200     88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
012300     88  ABEND-4008-DATS-ERROR                       VALUE +4008.         
012400     88  ABEND-4009-KEY-DATS-ERROR                   VALUE +4009.         
012500     88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
012600                                                                          
012700* TRANSACTION PRESENTATION MODULE PARAMETERS                              
260107     COPY DPWS991.                                                        
012800                                                                          
012800     COPY DPLS001.                                                        
012900         05  WORK-STRG-AREA          PIC X(022).                          
013000                                                                          
013100                                                                          
013200*    IN-TRANSIT EXTRACT RECORD LAYOUT                                     
013300     COPY PCRD024.                                                        
013400                                                                          
013500/                                                                         
013600*  IN-TRANSIT HEADERS                                                     
013700     EXEC SQL                                                             
013800         INCLUDE PCT00014                                                 
013900     END-EXEC.                                                            
014000                                                                          
014100*                                                                         
014200*  IN-TRANSIT DETAIL                                                      
014300*                                                                         
014400*    EXEC SQL                                                             
014500*        INCLUDE PCT00015                                                 
014600*    END-EXEC.                                                            
014700                                                                          
014800*                                                                         
014900*  CHECK POINT RESTART CONTROL                                            
015000*                                                                         
015100*    EXEC SQL                                                             
015200*        INCLUDE TCKRSTCN                                                 
015300*    END-EXEC.                                                            
015400*                                                                         
015500*  COMMUNICATIONS AREA FOR DB2                                            
015600*                                                                         
015700     EXEC SQL                                                             
015800         INCLUDE SQLCA                                                    
015900     END-EXEC.                                                            
016000                                                                          
016100*                                                                         
016200* DB2 COMMUNICATION AREA 2                                                
016300*                                                                         
016400     COPY SQLCA2.                                                         
016500/                                                                         
016600     COPY DPWS004.                                                        
016700/                                                                         
016800 PROCEDURE DIVISION.                                                      
016900                                                                          
017000 0000-MAINLINE-PROCESSING.                                                
017100                                                                          
017200     PERFORM 1000-INITIALIZATION.                                         
017300                                                                          
017400     PERFORM 2000-PROCESS                                                 
017500       UNTIL PS-EOF-EXTRACT-YES                                           
017600                                                                          
017700     PERFORM 9000-QUIT.                                                   
017800                                                                          
017900     GOBACK.                                                              
018000                                                                          
018100******************************************************************        
018200** READ THE DB2 CONTROL TABLE AND READ THE FIRST ASN EXTRACT REC *        
018300******************************************************************        
018400 1000-INITIALIZATION.                                                     
018500     MOVE '1000-INITIALIZATION         '  TO PV-ABEND-PARAGRAPH.          
018600                                                                          
018700*    THE CUTOFF DATE IS PASSED FROM THE EXTRACT PROGRAM                   
018800*    (PCKUT032).  IT MUST BE PRESENT TO VERIFY THAT ALL                   
018900*    THE EXTRACTS ARE STILL VALID.                                        
019000                                                                          
019100     OPEN INPUT CUTOFF-DATE-FILE.                                         
019200     READ CUTOFF-DATE-FILE                                                
019300         INTO                                                             
019400         PV-CUTOFF-DATE-REC                                               
019500     AT END                                                               
019600         DISPLAY ALL '*'                                                  
019700         DISPLAY ' CUTOFF DATE IS MISSING - GOTTA HAVE ONE'               
019800         MOVE +4003                  TO PV-ABEND-CODE                     
019900         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
020000     NOT AT END                                                           
020100         DISPLAY ' CUTOFF DATE IS ' PV-CUTOFF-DATE                        
020200     END-READ.                                                            
020300                                                                          
020400     CLOSE CUTOFF-DATE-FILE.                                              
020500                                                                          
020600     ACCEPT PV-CKPT-JOB-NAME FROM SYSIN.                                  
020700                                                                          
020800*    OPEN THE EXTRACT FILE                                                
020900     PERFORM 8000-OPEN-EXTRACT.                                           
039200     MOVE DP001-TRANS-RECORD  TO PC024-PURGE-EXTRACT                      
039700     ADD 1                            TO PV-READ-COUNTER                  
021000                                                                          
021100*    PERFORM 8100-READ-EXTRACT.                                           
021200                                                                          
021300     IF PS-EOF-EXTRACT-YES                                                
021400         DISPLAY SPACES                                                   
021500         DISPLAY ALL '*'                                                  
021600         DISPLAY '  INPUT FILE IS EMPTY '                                 
021700         DISPLAY ALL '*'                                                  
021800         DISPLAY SPACES                                                   
021900     END-IF.                                                              
022000/                                                                         
022100 2000-PROCESS.                                                            
022200                                                                          
022300     MOVE '2000-PROCESS                '  TO PV-ABEND-PARAGRAPH.          
022400                                                                          
022600*    PERFORM 7100-VERIFY                                                  
022700*    IF PS-VERIFY-YES                                                     
                                                                                
022800     PERFORM 7000-DELETE-INTRANSIT-HEADER                                 
                                                                                
022900*    END-IF                                                               
023100                                                                          
023200                                                                          
023300     PERFORM 8100-READ-EXTRACT.                                           
023400                                                                          
023500 7000-DELETE-INTRANSIT-HEADER.                                            
023600                                                                          
023700     MOVE '7000-DELETE-INTRANSIT-HEADER'  TO PV-ABEND-PARAGRAPH.          
023800     MOVE PC024-INTR-UPC-ID           TO PCT00014-INTR-UPC-ID             
023900     MOVE PC024-LOC-NBR               TO PCT00014-LOC-NBR                 
024000     MOVE PC024-TRN-CLSN-CDE          TO PCT00014-TRN-CLSN-CDE            
024100                                                                          
024200     PERFORM                                                              
024300         WITH TEST AFTER                                                  
024400         VARYING PV-RETRY-COUNTER                                         
024500         FROM 1 BY 1                                                      
024600         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
024700            OR  (SQLCODE = +100)                                          
024800            OR  (SQLCODE = +0))                                           
024900                                                                          
025000         EXEC SQL                                                         
025100             DELETE                                                       
025200              FROM  PCT_TRN_CLSN_BAL                                      
025300             WHERE                                                        
025400                   INTR_UPC_ID  = :PCT00014-INTR-UPC-ID                   
025500               AND LOC_NBR      = :PCT00014-LOC-NBR                       
025600               AND TRN_CLSN_CDE = :PCT00014-TRN-CLSN-CDE                  
032400               AND TRN_CLSN_QTY = 0                                       
032500               AND LAST_UPD_DTE < :PV-CUTOFF-DATE                         
025700         END-EXEC                                                         
025800                                                                          
025900         EVALUATE TRUE                                                    
026000             WHEN SQLCODE = ZERO                                          
026100                 ADD 1                TO PV-QTY-DELETE-COUNTER            
026200             WHEN SQLCODE = +100                                          
026100                 ADD 1                TO PV-BYPASS-COUNT                  
026400             WHEN SQLCODE = -904                                          
026500             WHEN SQLCODE = -911                                          
026600             WHEN SQLCODE = -913                                          
026700                  CONTINUE                                                
026800                                                                          
026900             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
027000                 DISPLAY '**************************************'         
027100                 DISPLAY 'WARNING ISSUED '                                
027200                 DISPLAY 'SELECT PCT_IN_TRNST_QTY               '         
027300                 DISPLAY PV-DISP-KEY-MSG                                  
027400                 DISPLAY '**************************************'         
027500                 PERFORM 9800-SQL-WARNING                                 
027600                                                                          
027700             WHEN SQLCODE < ZERO                                          
027800                 DISPLAY '**************************************'         
027900                 DISPLAY 'ERROR ISSUED ON                       '         
028000                 DISPLAY 'SELECT PCT_IN_TRNST_QTY               '         
028100                 DISPLAY PV-DISP-KEY-MSG                                  
028200                 DISPLAY '**************************************'         
028300                 PERFORM 9900-SQL-ERROR                                   
028400                                                                          
028500         END-EVALUATE                                                     
028600     END-PERFORM.                                                         
028700                                                                          
028800     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
028900         SQLCODE NOT = ZERO                                               
029000         DISPLAY '**************************************'                 
029100         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
029200         DISPLAY 'DELETE PCT_TRN_CLSN_BAL'                                
029300         DISPLAY PV-DISP-KEY-MSG                                          
029400         DISPLAY '**************************************'                 
029500         PERFORM 9900-SQL-ERROR                                           
029600     END-IF.                                                              
029700                                                                          
029800*7100-VERIFY.                                                             
029900*                                                                         
030000*    MOVE '7100-VERIFY'               TO PV-ABEND-PARAGRAPH.              
030100*    MOVE PC024-INTR-UPC-ID           TO PCT00014-INTR-UPC-ID             
030200*    MOVE PC024-LOC-NBR               TO PCT00014-LOC-NBR                 
030300*    MOVE PC024-TRN-CLSN-CDE          TO PCT00014-TRN-CLSN-CDE            
030400*    MOVE 'N'                         TO PS-VERIFY-SW                     
030500*                                                                         
030600*    PERFORM                                                              
030700*        WITH TEST AFTER                                                  
030800*        VARYING PV-RETRY-COUNTER                                         
030900*        FROM 1 BY 1                                                      
031000*        UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
031100*           OR  (SQLCODE = +100)                                          
031200*           OR  (SQLCODE = +0))                                           
031300*                                                                         
031400*        EXEC SQL                                                         
031500*            SELECT                                                       
031600*               'Y'                                                       
031700*            INTO                                                         
031800*                :PS-VERIFY-SW                                            
031900*             FROM  PCT_TRN_CLSN_BAL BAL                                  
032000*            WHERE                                                        
032100*                  BAL.INTR_UPC_ID  = :PCT00014-INTR-UPC-ID               
032200*              AND BAL.LOC_NBR      = :PCT00014-LOC-NBR                   
032300*              AND TRN_CLSN_CDE     = :PCT00014-TRN-CLSN-CDE              
032400*              AND BAL.TRN_CLSN_QTY = 0                                   
032500*              AND BAL.LAST_UPD_DTE < :PV-CUTOFF-DATE                     
032600*        END-EXEC                                                         
032700*                                                                         
032800*        EVALUATE TRUE                                                    
032900*            WHEN SQLCODE = ZERO                                          
033000*                CONTINUE                                                 
033100*            WHEN SQLCODE = +100                                          
033200*                ADD 1                TO PV-BYPASS-COUNT                  
033300*            WHEN SQLCODE = -904                                          
033400*            WHEN SQLCODE = -911                                          
033500*            WHEN SQLCODE = -913                                          
033600*                 CONTINUE                                                
033700*                                                                         
033800*            WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
033900*                DISPLAY '**************************************'         
034000*                DISPLAY 'WARNING ISSUED '                                
034100*                DISPLAY 'SELECT PCT_IN_TRNST_QTY               '         
034200*                DISPLAY PV-DISP-KEY-MSG                                  
034300*                DISPLAY '**************************************'         
034400*                PERFORM 9800-SQL-WARNING                                 
034500*                                                                         
034600*            WHEN SQLCODE < ZERO                                          
034700*                DISPLAY '**************************************'         
034800*                DISPLAY 'ERROR ISSUED ON                       '         
034900*                DISPLAY 'SELECT PCT_IN_TRNST_QTY               '         
035000*                DISPLAY PV-DISP-KEY-MSG                                  
035100*                DISPLAY '**************************************'         
035200*                PERFORM 9900-SQL-ERROR                                   
035300*                                                                         
035400*        END-EVALUATE                                                     
035500*    END-PERFORM.                                                         
035600*                                                                         
035700*    IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
035800*        SQLCODE NOT = ZERO                                               
035900*        DISPLAY '**************************************'                 
036000*        DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
036100*        DISPLAY 'DELETE PCT_TRN_CLSN_BAL'                                
036200*        DISPLAY PV-DISP-KEY-MSG                                          
036300*        DISPLAY '**************************************'                 
036400*        PERFORM 9900-SQL-ERROR                                           
036500*    END-IF.                                                              
036600*                                                                         
036700/                                                                         
036800 8000-OPEN-EXTRACT.                                                       
036900                                                                          
037000     MOVE PC-OPEN-FUNC           TO DP001-FUNC-CODE.                      
037100     MOVE '8000-OPEN-EXTRACT   '  TO PV-ABEND-PARAGRAPH.                  
037200*                                                                         
037300     MOVE LENGTH OF PC024-PURGE-EXTRACT                                   
037400                                     TO DP001-WORK-STRG-LENGTH.           
037500     MOVE PV-CKPT-JOB-NAME           TO DP001-JOB-NAME.                   
037600     MOVE PC-CKPT-PLAN-NAME          TO DP001-PLAN-NAME.                  
037700                                                                          
037800     PERFORM Z900-CALL-TRANS-PRES-MODULE.                                 
037900                                                                          
038000 8100-READ-EXTRACT.                                                       
038100                                                                          
038200     MOVE PC-TRANS-FUNC          TO DP001-FUNC-CODE.                      
038300     MOVE '8100-READ-EXTRACT   '  TO PV-ABEND-PARAGRAPH.                  
038400*                                                                         
038500     MOVE LENGTH OF PC024-PURGE-EXTRACT                                   
038600                                     TO DP001-WORK-STRG-LENGTH.           
038700     MOVE PV-CKPT-JOB-NAME           TO DP001-JOB-NAME.                   
038800     MOVE PC-CKPT-PLAN-NAME          TO DP001-PLAN-NAME.                  
038900                                                                          
039000     PERFORM Z900-CALL-TRANS-PRES-MODULE.                                 
039100                                                                          
039200     MOVE DP001-TRANS-RECORD          TO PC024-PURGE-EXTRACT              
472200                                         WORK-STRG-AREA                   
039300                                                                          
039400     IF DP001-PREP-TRANS-EOF                                              
039500         SET PS-EOF-EXTRACT-YES       TO TRUE                             
039600     ELSE                                                                 
039700         ADD 1                        TO PV-READ-COUNTER                  
039800     END-IF.                                                              
039900                                                                          
040000 8200-CLOSE-EXTRACT.                                                      
040100                                                                          
040200     MOVE PC-CLOSE-FUNC               TO DP001-FUNC-CODE.                 
040300     MOVE '8200-CLOSE-EXTRACT   '     TO PV-ABEND-PARAGRAPH.              
040400*                                                                         
040500     MOVE PV-CKPT-JOB-NAME           TO DP001-JOB-NAME.                   
040600     MOVE PC-CKPT-PLAN-NAME          TO DP001-PLAN-NAME.                  
040700                                                                          
040800     PERFORM Z900-CALL-TRANS-PRES-MODULE.                                 
040900/                                                                         
041000 9000-QUIT.                                                               
041300                                                                          
041400     PERFORM 8200-CLOSE-EXTRACT.                                          
041500                                                                          
041600     MOVE PV-READ-COUNTER             TO PV-DISP-NBR                      
041700     DISPLAY 'RECORDS READ               = ' PV-DISP-NBR                  
041800                                                                          
041900     MOVE PV-QTY-DELETE-COUNTER       TO PV-DISP-NBR                      
042000     DISPLAY 'PCT_TRN_CLSN_BAL DELETES   = '  PV-DISP-NBR.                
042100                                                                          
042200     MOVE PV-BYPASS-COUNT             TO PV-DISP-NBR                      
042300     DISPLAY 'RECORDS BYPASSED           = '  PV-DISP-NBR.                
042400/                                                                         
042500 9100-SQL-ABEND.                                                          
042600******************************************************************        
042700*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
042800*  ERROR OR WARNING CODE OCCURS.                                          
042900******************************************************************        
043000     DISPLAY '9100-SQL-ABEND'.                                            
043100     DISPLAY '** ABEND     **'.                                           
043200     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
043300     DISPLAY '** PARAGRAPH ** = ' PV-ABEND-PARAGRAPH.                     
043400     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
043500     DISPLAY '** INTR-UPC-ID  = ' PC024-INTR-UPC-ID                       
043600     DISPLAY '** LOC-NBR      ** = ' PC024-LOC-NBR                        
043700     DISPLAY '** TRN-CLSN-CDE ** = ' PC024-TRN-CLSN-CDE                   
043800     DISPLAY '**              ** = '                                      
043900     MOVE SQLCODE TO PV-SQL-CODE.                                         
044000     DISPLAY '** SQL CODE  ** = ' PV-SQL-CODE.                            
044100     MOVE +4013 TO PV-ABEND-CODE.                                         
044200     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
044300                                                                          
044400******************************************************************        
044500*  THIS MODULE PERFORMS THE SQL WARNING.                         *        
044600******************************************************************        
044700                                                                          
044800 9800-SQL-WARNING.                                                        
044900                                                                          
045000     MOVE SQLCODE               TO SQLCODE2.                              
045100     MOVE SQLERRD(3)            TO SQLERRD3.                              
045200     DISPLAY '** ABEND **       ** = SQLWARNING'.                         
045300     DISPLAY '** PROGRAM        ** = PCKUP010'.                           
045400     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
045500     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
045600     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
045700                                                                          
045800     COPY DPPD004.                                                        
045900     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
046000     CALL 'ILBOABN0' USING ABEND-CODE.                                    
046100                                                                          
046200 EJECT                                                                    
046300******************************************************************        
046400* THIS MODULE PERFORMS THE SQL ERROR.                            *        
046500******************************************************************        
046600                                                                          
046700 9900-SQL-ERROR.                                                          
046800                                                                          
046900     MOVE SQLCODE               TO SQLCODE2.                              
047000     MOVE SQLERRD(3)            TO SQLERRD3.                              
047100     DISPLAY '** ABEND **       ** = SQLERROR'.                           
047200     DISPLAY '** PROGRAM        ** = PCKUP010'.                           
047300     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
047400                                                                          
047500     COPY DPPD004.                                                        
047600     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
047700     CALL 'ILBOABN0' USING ABEND-CODE.                                    
047800/                                                                         
047900     COPY DPPD001.                                                        
