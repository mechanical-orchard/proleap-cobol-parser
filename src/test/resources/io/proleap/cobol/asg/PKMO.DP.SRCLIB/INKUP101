       ID  DIVISION.                                                    12/13/94
00002  PROGRAM-ID.         INKUP101.                                    INKUP101
00003  AUTHOR.             TROY BIRK.                                      LV006
00004  INSTALLATION.       KOHLS DEPARTMENT STORES.                     INKUP101
00005  DATE-WRITTEN.       08/93.                                       INKUP101
00006  DATE-COMPILED.                                                   INKUP101
00007                                                                   INKUP101
00008 ******************************************************************INKUP101
00009 *   THIS PROGRAM RUNS UNDER THE DB2 CHECKPOINT RESTART SYSTEM.   *INKUP101
00010 ******************************************************************INKUP101
00011 *                     CHANGE LOG                                 *   CL**4
00012 ******************************************************************   CL**4
00013 * 03/94  M BLAZICH                                               *   CL**4
00014 *        CHANGED LRECLS AND INCLUDES TO HAVE SCAN-LINE-NBR       *   CL**4
00015 *        REMOVED REFERENCE TO INV-QTY                            *   CL**5
00019 ******************************************************************   CL**4
00024 * 02/99   MARIA KLAMIK                                               CL**6
00018 * EXPAND COPYBOOK INWS008 TO ACCOMMODATE NEW SCAN DATE AND           CL**3
      * TIMESTAMP FIELDS.                                                       
W21732******************************************************************   CL**4
W21732* 11/02   IGSI                                                   *        
W21732* DEPT/CLASS/SEQ NUMBERS ARE REMOVED AND 8 DIGIT SKU NUMBER      *        
W21732* IS ADDED                                                       *        
W26600******************************************************************   CL**4
W26600*  W26600  06-  -2004  PHYSICAL INVENTORY STORE EXPANSION                 
W26600*                      M JOHNSON                                          
731886* P731886 01-28-2005 ROD JANSSEN S04C ABEND IN INK307, INKUP101.          
P28545*W28545  03-24-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE SETUP        
00016 ******************************************************************   CL**4
00017                                                                   INKUP101
00018  ENVIRONMENT DIVISION.                                            INKUP101
00019 *                                                                 INKUP101
00020  CONFIGURATION SECTION.                                           INKUP101
00021 *                                                                 INKUP101
00022  SOURCE-COMPUTER.        IBM-3090.                                INKUP101
00023  OBJECT-COMPUTER.        IBM-3090.                                INKUP101
00024 *                                                                 INKUP101
00025  INPUT-OUTPUT SECTION.                                            INKUP101
W26600                                                                          
00026  FILE-CONTROL.                                                    INKUP101
W26600                                                                          
W26600     SELECT INVALID-DETAIL-FILE                                           
W26600       ASSIGN TO INP01.                                                   
00027                                                                   INKUP101
P28545     SELECT CC-JOBNAME-CNTL-FILE                                          
P28545         ASSIGN TO INP02.                                                 
P28545                                                                          
00028  DATA DIVISION.                                                   INKUP101
W26600                                                                          
W26600 FILE SECTION.                                                            
W26600                                                                          
W26600 FD  INVALID-DETAIL-FILE                                                  
W26600     LABEL RECORDS ARE STANDARD                                           
W26600     RECORDING MODE IS F                                                  
W26600     BLOCK CONTAINS 0 RECORDS.                                            
W26600                                                                          
W26600 01  INVALID-DETAIL-RCD          PIC X(88).                               
00029                                                                   INKUP101
P28545 FD  CC-JOBNAME-CNTL-FILE                                                 
P28545     RECORDING MODE IS F                                                  
P28545     LABEL RECORDS ARE STANDARD                                           
P28545     BLOCK CONTAINS 0 RECORDS.                                            
P28545 01  CC-JOBNAME-CNTL-REC         PIC X(80).                               
P28545                                                                          
00030  WORKING-STORAGE SECTION.                                         INKUP101
W26600                                                                     CL**4
W26600******************************************************************   CL**4
W26600 01  PS-PROGRAM-SWITCHES.                                         INKUP101
731886     05  PS-FIRST-TIME-SW        PIC X(01)           VALUE 'N'.           
731886         88  PS-FIRST-TIME-THRU                      VALUE 'N'.           
731886         88  PS-CONTINUED-PROCESSING                 VALUE 'Y'.           
W26600     05  PS-INVALID-DTL-EOF-SW   PIC X(01)           VALUE 'N'.           
W26600         88  PS-INVALID-DTL-EOF                      VALUE 'Y'.           
W26600     05  PS-END-OF-INPUT-SW      PIC  X(01)          VALUE 'N'.   INKUP101
W26600         88  PS-END-OF-INPUT                         VALUE 'Y'.   INKUP101
W26600     05  PS-INSERT-ROW-SW        PIC X(01)           VALUE SPACE. INKUP101
W26600         88  PS-INSERT-ROW                           VALUE 'Y'.   INKUP101
W26600         88  PS-UPDATE-ROW                           VALUE 'N'.   INKUP101
W26600     05  PS-TCKRSTCNTL-RCD-FND-SW                                         
W26600                                 PIC X(01)           VALUE 'N'.           
W26600         88  PS-TCKRSTCNTL-RCD-FND                   VALUE 'Y'.           
W26600     05  PS-TCKRSTINF-RCD-FND-SW PIC X(01)           VALUE 'N'.           
W26600         88  PS-TCKRSTINF-RCD-FND                    VALUE 'Y'.           
W26600                                                                     CL**4
00031 *                                                                 INKUP101
00032  01  ABEND-CODE                  PIC S9(04)          VALUE ZEROS  INKUP101
00033                                                      COMP SYNC.   INKUP101
00034 *                                                                 INKUP101
W26600******************************************************************   CL**4
00035  01  PC-PROGRAM-CONSTANTS.                                        INKUP101
00036      05  PC-MAX-RETRIES          PIC S9(03)          VALUE +03    INKUP101
00037                                                      COMP-3.      INKUP101
00038      05  PC-CKPT-JOB-NAME        PIC  X(08)          VALUE        INKUP101
00039          'INK300  '.                                              INKUP101
00040      05  PC-CKPT-PLAN-NAME       PIC  X(08)          VALUE        INKUP101
00041          'INKUP101'.                                              INKUP101
W26600                                                                     CL**4
W26600******************************************************************   CL**4
W26000 01  PT-PROGRAM-TOTALS.                                                   
W26600     05  PT-NBR-INVALID-DTL-RCD-READ                                      
W26600                                 PIC S9(09)          VALUE +0             
W26600                                                     COMP-3.              
W26600                                                                          
W26600******************************************************************   CL**4
W26600 01  PV-PROGRAM-VARIABLES.                                        INKUP101
W26600     05  PV-SQL-SUB              PIC  9(03)          VALUE ZEROS. INKUP101
W26600     05  PV-PARAGRAPH-NAME       PIC  X(30)          VALUE SPACE. INKUP101
W26600     05  PV-DB2-OPER-ATTEMPTED   PIC  X(30)          VALUE SPACE. INKUP101
W26600     05  PV-RETRY-COUNT          PIC S9(03)          VALUE ZEROS  INKUP101
W26600                                                     COMP-3.      INKUP101
W26600     05  PV-REC-COUNTER          PIC  9(07)          VALUE ZEROS. INKUP101
W26600     05  PV-ITEM-COUNTER         PIC  9(07)          VALUE ZEROS. INKUP101
P28545 01  PV-PROCESS-JOB-NAME-CC.                                              
P28545     05  PV-PROCESS-JOB-NAME     PIC X(08)   VALUE SPACES.                
P28545     05  FILLER                  PIC X(72)   VALUE SPACES.                
P28545                                                                     CL**4
W26600 01  SA-SAVE-AREA.                                                INKUP101
W26600     05  SA-NBR-INVALID-DTL-RCD-READ                                      
W26600                                 PIC S9(09)          VALUE +0             
W26600                                                     COMP-3.              
W26600     05  SA-LOC-NBR              PIC  9(04)          VALUE ZEROS. INKUP101
W26600     05  SA-AREA-NBR             PIC  9(06)          VALUE ZEROS. INKUP101
W26600     05  SA-INV-LINE-NBR         PIC  9(06)          VALUE ZEROS.         
W26600                                                                          
W26600******************************************************************   CL**4
W26600 01  CR-CHECKPOINT-RESTART-AREA.                                          
W26600     05  CR-AREA-LEN             PIC S9(04)          VALUE +47            
W26600                                                     COMP.                
W26600     05  CR-CHECKPOINT-RESTART-VAR.                                       
W26600*        10  CR-PREV-DTL-KEY     PIC  X(11) VALUE SPACES.                 
W26600         10  CR-PREV-DTL-KEY.                                             
W26600             15  CR-NBR-INVALID-DTL-RCD-READ                              
W26600                                 PIC S9(09)          VALUE +0             
W26600                                                     COMP-3.              
W26600             15  CR-LOC-NBR      PIC  9(04)          VALUE ZEROS.         
W26600             15  CR-AREA-NBR     PIC  9(06)          VALUE ZEROS.         
W26600             15  CR-INV-LINE-NBR PIC  9(06)          VALUE ZEROS.         
W26600         10  CR-COMMIT-COUNT     PIC  9(09)          VALUE ZEROS.         
00042                                                                   INKUP101
00043 *                                                                 INKUP101
00044 *****                                                             INKUP101
00045 *                                                                 INKUP101
W26600******************************************************************   CL**4
00046      COPY INWS008.                                                   CL**6
00047 *01  PV-INWS008-RDF REDEFINES                                        CL**3
00048 *    IN008-INVENTORY-COUNT-RECORD.                                   CL**3
00049 *    05  FILLER                  PIC X(86).                          CL**3
00050 *    05  PV-IN008-INVALID-REC                                        CL**3
00051 *                                PIC X(01).                          CL**3
00052 *    05  PV-IN008-INVALID-ITEM                                       CL**3
00053 *                                PIC X(01).                          CL**3
00054                                                                   INKUP101
W26600/                                                                    CL**4
W26600******************************************************************   CL**4
00058 *****  SQL ABEND ROUTINE WORKING STORAGE                          INKUP101
00059 *                                                                 INKUP101
00060      COPY DPWS004.                                                INKUP101
00061      EJECT                                                        INKUP101
00062 *                                                                 INKUP101
W26600******************************************************************   CL**4
W26600*  IN-INVALID INVENTORY DETAIL                                       CL**4
W26600******************************************************************   CL**4
00063 *****  DB2 INVALID DETAIL DCLGEN                                  INKUP101
00064 *                                                                 INKUP101
00065      EXEC SQL                                                     INKUP101
00066          INCLUDE TINVDTL                                          INKUP101
00067      END-EXEC.                                                    INKUP101
W26600                                                                          
W26600******************************************************************   CL**4
00069 *****  DB2 INVNETORY PHYSICAL LOCATION DCLGEN                     INKUP101
W26600******************************************************************   CL**4
00070 *                                                                 INKUP101
00071      EXEC SQL                                                     INKUP101
00072          INCLUDE TINVLOC                                          INKUP101
00073      END-EXEC.                                                    INKUP101
00074 *                                                                 INKUP101
W26600                                                                          
W26600*****************************************************************         
W26600*  CHECKPOINT RESTART CONTROL AND INFORMATION TABLES                      
W26600*****************************************************************         
W26600     EXEC SQL                                                             
W26600         INCLUDE TCKRSTCN                                                 
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EXEC SQL                                                             
W26600         INCLUDE TCKRSTIN                                                 
W26600     END-EXEC.                                                            
W26600                                                                          
00075      COPY SQLCA2.                                                 INKUP101
00076                                                                   INKUP101
00077 *                                                                 INKUP101
00078 *  TRANSACTION PRESENTATION MODULE COMMUNICATIONS AREA            INKUP101
00079 *                                                                 INKUP101
00080 *    COPY DPLS001.                                                INKUP101
00081 *    05  WORKING-STORAGE-PASSED.                                  INKUP101
00085 *                                                                 INKUP101
00094                                                                   INKUP101
00102 ******************************************************************INKUP101
00103 *  COMMUNICATIONS AREA FOR DB2                                   *INKUP101
00104 ******************************************************************INKUP101
00105                                                                   INKUP101
00106      EXEC SQL                                                     INKUP101
00107          INCLUDE SQLCA                                            INKUP101
00108      END-EXEC.                                                    INKUP101
00109                                                                   INKUP101
W26600*****************************************************************         
00111  PROCEDURE DIVISION.                                              INKUP101
00112 *                                                                 INKUP101
00113 ******************************************************************INKUP101
00114 *                                                                 INKUP101
00115 ******************************************************************INKUP101
W26600 0000-MAIN-MODULE.                                                INKUP101
00117                                                                   INKUP101
W26600     PERFORM 8000-OPEN-FILES.                                             
W26600                                                                          
W26600     PERFORM 0100-INITIALIZE.                                             
00120                                                                   INKUP101
W26600     IF NOT PS-INVALID-DTL-EOF                                            
W26600         PERFORM 7900-COMMIT-WORK                                         
W26600     END-IF                                                               
W26600                                                                          
W26600     PERFORM 1000-PRCSS-INVDTL-INPUT                              INKUP101
W26600         UNTIL PS-INVALID-DTL-EOF.                                        
00125                                                                   INKUP101
00126 *    PERFORM D100-UPDATE-LOCATION-TABLE.                          INKUP101
W26600     IF SA-LOC-NBR > 0                                                    
W26600     AND SA-AREA-NBR > 0                                                  
W26600         PERFORM 2000-FMT-UPDT-TINVLOC                            INKUP101
W26600         PERFORM 2500-FMT-UPDT-RSRT-INFO                                  
W26600         PERFORM 7900-COMMIT-WORK                                         
W26600     END-IF.                                                      INKUP101
00130                                                                   INKUP101
W26600     GOBACK.                                                      INKUP101
W26600                                                                  INKUP101
W26600/                                                                 INKUP101
W26600******************************************************************INKUP101
W26600 0100-INITIALIZE.                                                         
W26600                                                                  INKUP101
731886     MOVE 'N'            TO PS-FIRST-TIME-SW.                             
731886                                                                          
W26600     PERFORM 7700-INIT-CHECKPOINT-SELECT.                         INKUP101
W26600     IF PS-TCKRSTCNTL-RCD-FND                                             
W26600         IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       INKUP101
W26600           PERFORM 7800-SELECT-RESTART-INFO                       INKUP101
W26600           IF PS-TCKRSTINF-RCD-FND                                        
W26600             MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                           
W26600                                 TO CR-CHECKPOINT-RESTART-VAR             
W26600             PERFORM 8100-READ-INVALID-DTL-FILE                   INKUP101
W26600               UNTIL PT-NBR-INVALID-DTL-RCD-READ >                        
W26600                 TCKRSTCNTL-TRANS-QTY                                     
W26600               OR PS-INVALID-DTL-EOF                                      
W26600           END-IF                                                         
W26600         ELSE                                                     INKUP101
W26600           PERFORM 0500-INITIALIZE-TCKRSTCNTL-RCD                         
W26600           PERFORM 8100-READ-INVALID-DTL-FILE                     INKUP101
W26600        END-IF                                                    INKUP101
W26600     END-IF.                                                      INKUP101
W26600                                                                          
W26600******************************************************************INKUP101
W26600 0500-INITIALIZE-TCKRSTCNTL-RCD.                                          
W26600                                                                          
W26000     MOVE '0'                    TO TCKRSTCNTL-CKPT-STAT-CODE.            
W26600     MOVE 0                      TO TCKRSTCNTL-TRANS-QTY.                 
W26600     MOVE 0                      TO TCKRSTCNTL-CKPT-TAKEN-QTY.            
W26600     MOVE 0                      TO TCKRSTCNTL-TRANS-PRCSD-QTY.           
W26600     PERFORM 7710-UPDT-TCKRSTCNTL-RCD.                            INKUP101
W26600     PERFORM 7900-COMMIT-WORK.                                            
W26600                                                                  INKUP101
00133 /                                                                 INKUP101
00152 ******************************************************************INKUP101
00153 *                                                                 INKUP101
00154 ******************************************************************INKUP101
00155  1000-PRCSS-INVDTL-INPUT.                                         INKUP101
00156                                                                   INKUP101
W26600     IF (IN008-STORE-NUMBER NOT EQUAL SA-LOC-NBR)                 INKUP101
W26600     OR (IN008-AREA-NUMBER NOT EQUAL SA-AREA-NBR)                 INKUP101
W26600         IF SA-LOC-NBR > 0                                                
W26600         AND SA-AREA-NBR > 0                                              
W26600           PERFORM 2000-FMT-UPDT-TINVLOC                          INKUP101
00160            INITIALIZE            PV-ITEM-COUNTER                  INKUP101
00161                                  PV-REC-COUNTER                   INKUP101
W26600           PERFORM 2500-FMT-UPDT-RSRT-INFO                                
W26600           PERFORM 7900-COMMIT-WORK                                       
W26600         END-IF                                                           
W26600         MOVE IN008-STORE-NUMBER TO SA-LOC-NBR                    INKUP101
W26600         MOVE IN008-AREA-NUMBER  TO SA-AREA-NBR                   INKUP101
00164      END-IF.                                                      INKUP101
00165                                                                      CL**3
W26600     MOVE IN008-STORE-NUMBER     TO INVDTL-LOC-NBR.               INKUP101
00167      MOVE IN008-AREA-NUMBER      TO INVDTL-AREA-NBR.              INKUP101
W26600     MOVE IN008-LINE-NUMBER      TO INVDTL-INV-LINE-NBR           INKUP101
W26600                                    SA-INV-LINE-NBR.                      
00169      MOVE IN008-SCAN-LINE-NUMBER TO INVDTL-SCAN-LINE-NBR.         INKUP101
00170      MOVE IN008-UPC-CODE         TO INVDTL-REC-UPC-NBR.           INKUP101
W21732     MOVE IN008-SKU              TO INVDTL-REC-SKU-NBR.                   
00174      MOVE IN008-QUANTITY         TO INVDTL-REC-QTY.               INKUP101
00175      MOVE IN008-PRICE            TO INVDTL-REC-PR-AMT.            INKUP101
00176      MOVE IN008-INVALID-RCD      TO INVDTL-INVALID-REC-IND.          CL**3
00177      MOVE IN008-INVALID-ITEM     TO INVDTL-INVALID-ITM-IND.          CL**3
W21732     INITIALIZE                     INVDTL-CORR-SKU-NBR           INKUP101
00181                                     INVDTL-CORR-QTY               INKUP101
00182                                     INVDTL-CORR-PR-AMT            INKUP101
00183                                     INVDTL-CORR-UPC-NBR           INKUP101
00184                                     INVDTL-CHG-TMST               INKUP101
00185                                     INVDTL-CHG-ID-NBR.            INKUP101
00186                                                                      CL**3
00187      IF IN008-INVALID-RCD = 'Y'                                      CL**3
00188          ADD 1                   TO PV-REC-COUNTER                INKUP101
00189      END-IF.                                                      INKUP101
00190      IF IN008-INVALID-ITEM = 'Y'                                     CL**3
00191          ADD 1                   TO PV-ITEM-COUNTER               INKUP101
00192      END-IF.                                                      INKUP101
W26600     MOVE PT-NBR-INVALID-DTL-RCD-READ                                     
W26600                                 TO SA-NBR-INVALID-DTL-RCD-READ           
W26600                                    TCKRSTCNTL-TRANS-QTY                  
W26600                                    TCKRSTCNTL-TRANS-PRCSD-QTY.           
W26600     PERFORM 7000-INSERT-TINVDTL-TABLE.                           INKUP101
W26600     PERFORM 8100-READ-INVALID-DTL-FILE.                                  
00194                                                                   INKUP101
00195 ******************************************************************INKUP101
00196 *                                                                 INKUP101
00197 ******************************************************************INKUP101
W26600 2000-FMT-UPDT-TINVLOC.                                           INKUP101
00199                                                                   INKUP101
W26600     MOVE SA-LOC-NBR             TO INVLOC-LOC-NBR.                       
00200      MOVE SA-AREA-NBR            TO INVLOC-AREA-NBR.              INKUP101
00201 *    PERFORM E100-DETERMINE-IF-ROW-EXISTS.                        INKUP101
W26600     PERFORM 7100-SELECT-TINVLOC-DTL.                             INKUP101
00203      MOVE PV-ITEM-COUNTER        TO INVLOC-TOT-ITM-INVLID-QTY.    INKUP101
00204      MOVE PV-REC-COUNTER         TO INVLOC-TOT-REC-INVLID-QTY.    INKUP101
00205      INITIALIZE                     INVLOC-TOT-REC-CORR-QTY       INKUP101
00206                                     INVLOC-TOT-ITM-CORR-QTY.      INKUP101
00207      IF PS-INSERT-ROW                                             INKUP101
00208          PERFORM 7110-INSERT-TINVLOC-ROW                          INKUP101
00209      ELSE                                                         INKUP101
00210          PERFORM 7120-UPDATE-TINVLOC-ROW                          INKUP101
00211      END-IF.                                                      INKUP101
W26600                                                                          
W26600******************************************************************INKUP101
W26600 2500-FMT-UPDT-RSRT-INFO.                                                 
W26600                                                                          
W26600     IF NOT PS-INVALID-DTL-EOF                                            
731886         MOVE 'Y'                TO PS-FIRST-TIME-SW                      
W26600         MOVE SA-SAVE-AREA       TO TCKRSTINF-WORK-STRG-DESC-TEXT         
W26600         MOVE LENGTH OF SA-SAVE-AREA                              INKUP101
W26600                                 TO TCKRSTINF-WORK-STRG-DESC-LEN          
W26600         PERFORM 7810-UPDT-TCKRSTINF-RCD                                  
W26600         IF PS-TCKRSTINF-RCD-FND                                          
W26600           CONTINUE                                                       
W26600         ELSE                                                             
P28545           MOVE PV-PROCESS-JOB-NAME TO TCKRSTINF-JOB-NAME                 
W26600           MOVE PC-CKPT-PLAN-NAME                                         
W26600                                 TO TCKRSTINF-PLAN-NAME                   
W26600           MOVE 0                TO TCKRSTINF-RST-TRANS-SEQ-NMBR          
W26600                                    TCKRSTINF-SEQ-NMBR                    
W26600           PERFORM 7820-INSRT-TCKRSTINF-RCD                               
W26600         END-IF                                                           
W26600         ADD +1                  TO TCKRSTCNTL-CKPT-TAKEN-QTY             
W26600         MOVE '9'                TO TCKRSTCNTL-CKPT-STAT-CODE     INKUP101
W26600     ELSE                                                                 
731886     IF PS-FIRST-TIME-THRU                                                
731886         CONTINUE                                                         
731886     ELSE                                                                 
W26600         PERFORM 7840-DLT-TCKRSTINF-RCD                                   
W26600         MOVE '0'                TO TCKRSTCNTL-CKPT-STAT-CODE     INKUP101
W26600     END-IF.                                                              
W26600                                                                          
W26600     PERFORM 7710-UPDT-TCKRSTCNTL-RCD.                            INKUP101
W26600                                                                          
00274 /*****************************************************************INKUP101
00275 *  THIS MODULE INSERTS A ROW INTO THE TINVDTL TABLE.             *INKUP101
00276 ******************************************************************INKUP101
W26600 7000-INSERT-TINVDTL-TABLE.                                       INKUP101
00278                                                                   INKUP101
00279      PERFORM                                                      INKUP101
00280        WITH TEST AFTER                                            INKUP101
00281        VARYING PV-RETRY-COUNT FROM 1 BY 1                         INKUP101
00282        UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                    INKUP101
00283        OR (SQLCODE = 0)                                           INKUP101
00284                                                                   INKUP101
00285        EXEC SQL                                                   INKUP101
00286          INSERT INTO TINVDTL                                      INKUP101
W26600             (LOC_NBR,                                            INKUP101
00288               AREA_NBR,                                           INKUP101
00289               INV_LINE_NBR,                                       INKUP101
00290               SCAN_LINE_NBR,                                      INKUP101
00291               REC_UPC_NBR,                                        INKUP101
W21732              REC_SKU_NBR,                                                
00295               REC_QTY,                                            INKUP101
00296               REC_PR_AMT,                                         INKUP101
00297               CORR_UPC_NBR,                                       INKUP101
W21732              CORR_SKU_NBR,                                               
00301               CORR_QTY,                                           INKUP101
00302               CORR_PR_AMT,                                        INKUP101
00303               INVALID_ITM_IND,                                    INKUP101
00304               INVALID_REC_IND,                                    INKUP101
00305               CHG_TMST,                                           INKUP101
00306               CHG_ID_NBR)                                         INKUP101
W26600         VALUES (:INVDTL-LOC-NBR,                                 INKUP101
00308                  :INVDTL-AREA-NBR,                                INKUP101
00309                  :INVDTL-INV-LINE-NBR,                            INKUP101
00310                  :INVDTL-SCAN-LINE-NBR,                           INKUP101
00311                  :INVDTL-REC-UPC-NBR,                             INKUP101
W21732                 :INVDTL-REC-SKU-NBR,                                     
00315                  :INVDTL-REC-QTY,                                 INKUP101
00316                  :INVDTL-REC-PR-AMT,                              INKUP101
00317                  :INVDTL-CORR-UPC-NBR,                            INKUP101
W21732                 :INVDTL-CORR-SKU-NBR,                                    
00321                  :INVDTL-CORR-QTY,                                INKUP101
00322                  :INVDTL-CORR-PR-AMT,                             INKUP101
00323                  :INVDTL-INVALID-ITM-IND,                         INKUP101
00324                  :INVDTL-INVALID-REC-IND,                         INKUP101
00325                  CURRENT TIMESTAMP,                               INKUP101
00326                  :INVDTL-CHG-ID-NBR)                              INKUP101
00327        END-EXEC                                                   INKUP101
00328                                                                   INKUP101
00329        EVALUATE TRUE                                              INKUP101
00330          WHEN SQLCODE = -904                                      INKUP101
00331          WHEN SQLCODE = 0                                         INKUP101
00332            CONTINUE                                               INKUP101
00333          WHEN OTHER                                               INKUP101
00334            MOVE '7000-INSERT-TINVDTL-TABLE'                       INKUP101
00335                                  TO PV-PARAGRAPH-NAME             INKUP101
00336            MOVE 'INSERTING RECORD INTO TINVDTL '                  INKUP101
00337                                  TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600           PERFORM 9900-SQL-ABEND                                 INKUP101
00339        END-EVALUATE                                               INKUP101
00340      END-PERFORM.                                                 INKUP101
00341                                                                   INKUP101
00342      IF PV-RETRY-COUNT > PC-MAX-RETRIES                           INKUP101
00343          MOVE '7000-INSERT-TINVDTL-TABLE'                         INKUP101
00344                                  TO PV-PARAGRAPH-NAME             INKUP101
00345          MOVE 'MAX NUMBER OF RETRIES EXCEEDED'                    INKUP101
00346                                  TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600         PERFORM 9900-SQL-ABEND                                   INKUP101
00348      END-IF.                                                      INKUP101
00212                                                                   INKUP101
00213 ******************************************************************INKUP101
00214 *                                                                 INKUP101
00215 ******************************************************************INKUP101
W26600 7100-SELECT-TINVLOC-DTL.                                         INKUP101
00217                                                                   INKUP101
00218      PERFORM                                                      INKUP101
00219        WITH TEST AFTER                                            INKUP101
00220        VARYING PV-SQL-SUB                                         INKUP101
00221        FROM 1 BY 1                                                INKUP101
00222        UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                         INKUP101
00223        OR SQLCODE = 0                                             INKUP101
00224        OR SQLCODE = +100)                                         INKUP101
00225                                                                   INKUP101
00226        EXEC SQL                                                   INKUP101
W26600         SELECT LOC_NBR                                           INKUP101
00228          INTO :INVLOC-LOC-NBR                                     INKUP101
00229          FROM TINVLOC                                             INKUP101
W26600         WHERE (LOC_NBR = :INVLOC-LOC-NBR)                        INKUP101
W26600         AND (AREA_NBR = :INVLOC-AREA-NBR)                        INKUP101
00232        END-EXEC                                                   INKUP101
00233                                                                   INKUP101
00234        EVALUATE TRUE                                              INKUP101
00247          WHEN SQLCODE = 0                                         INKUP101
00248            SET PS-UPDATE-ROW     TO TRUE                          INKUP101
00235          WHEN SQLCODE = -904                                      INKUP101
00236            CONTINUE                                               INKUP101
00237          WHEN SQLCODE = +100                                      INKUP101
00238            SET PS-INSERT-ROW     TO TRUE                          INKUP101
00239          WHEN SQLWARN0 NOT = SPACES                               INKUP101
00240          WHEN SQLCODE < 0                                         INKUP101
00241          WHEN SQLCODE > 0                                         INKUP101
W26600           MOVE '7100-SELECT-TINVLOC-DTL'                         INKUP101
00243                                  TO PV-PARAGRAPH-NAME             INKUP101
00244            MOVE 'READING TINVLOC TABLE'                           INKUP101
00245                                  TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600           PERFORM 9900-SQL-ABEND                                 INKUP101
00249        END-EVALUATE                                               INKUP101
00250      END-PERFORM.                                                 INKUP101
00251                                                                   INKUP101
00252      IF PV-SQL-SUB > PC-MAX-RETRIES                               INKUP101
W26600         MOVE '7100-SELECT-TINVLOC-DTL'                           INKUP101
00254                                  TO PV-PARAGRAPH-NAME             INKUP101
00255          MOVE 'MAX RETRIES EXCEEDED   '                           INKUP101
00256                                  TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600         PERFORM 9900-SQL-ABEND                                   INKUP101
00258      END-IF.                                                      INKUP101
00349                                                                   INKUP101
00350 ******************************************************************INKUP101
00351 *                                                                 INKUP101
00352 ******************************************************************INKUP101
00353  7110-INSERT-TINVLOC-ROW.                                         INKUP101
00354                                                                   INKUP101
00355      PERFORM                                                      INKUP101
00356        WITH TEST AFTER                                            INKUP101
00357        VARYING PV-SQL-SUB                                         INKUP101
00358        FROM 1 BY 1                                                INKUP101
00359        UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                         INKUP101
00360        OR SQLCODE = 0                                             INKUP101
00361        OR SQLCODE = +100)                                         INKUP101
00362                                                                   INKUP101
00363        EXEC SQL                                                   INKUP101
I0364          INSERT INTO TINVLOC                                      INKUP101
W26600             (LOC_NBR,                                                    
00366               AREA_NBR,                                           INKUP101
00367               TOT_REC_CORR_QTY,                                   INKUP101
00368               TOT_ITM_CORR_QTY,                                   INKUP101
00369               TOT_ITM_INVLID_QTY,                                 INKUP101
00370               TOT_REC_INVLID_QTY,                                 INKUP101
00371               CHG_TMST)                                           INKUP101
00372          VALUES (:INVLOC-LOC-NBR,                                 INKUP101
00373                  :INVLOC-AREA-NBR,                                INKUP101
00374                  :INVLOC-TOT-REC-CORR-QTY,                        INKUP101
00375                  :INVLOC-TOT-ITM-CORR-QTY,                        INKUP101
00376                  :INVLOC-TOT-ITM-INVLID-QTY,                      INKUP101
00377                  :INVLOC-TOT-REC-INVLID-QTY,                      INKUP101
00378                  CURRENT TIMESTAMP)                               INKUP101
00379        END-EXEC                                                   INKUP101
00380                                                                   INKUP101
00381        EVALUATE TRUE                                              INKUP101
00392          WHEN SQLCODE = 0                                         INKUP101
00393                  CONTINUE                                         INKUP101
00382          WHEN SQLCODE = -904                                      INKUP101
00383                  CONTINUE                                         INKUP101
00384          WHEN SQLWARN0 NOT = SPACES                               INKUP101
00385          WHEN SQLCODE < 0                                         INKUP101
00386          WHEN SQLCODE > 0                                         INKUP101
00387            MOVE '7110-INSERT-TINVLOC-ROW       '                  INKUP101
00388                                  TO PV-PARAGRAPH-NAME             INKUP101
00389            MOVE 'INSERTING INTO TINVLOC TABLE'                    INKUP101
00390                                  TO PV-DB2-OPER-ATTEMPTED         INKUP101
00391            PERFORM 9900-SQL-ABEND                                 INKUP101
00394        END-EVALUATE                                               INKUP101
00395      END-PERFORM.                                                 INKUP101
00396                                                                   INKUP101
00397      IF PV-SQL-SUB > PC-MAX-RETRIES                               INKUP101
00398          MOVE '7110-INSERT-TINVLOC-ROW       '                    INKUP101
00399                                  TO PV-PARAGRAPH-NAME             INKUP101
00400          MOVE 'MAX RETRIES EXCEEDED   '                           INKUP101
00401                                  TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600         PERFORM 9900-SQL-ABEND                                   INKUP101
00403      END-IF.                                                      INKUP101
00404                                                                   INKUP101
00405 ******************************************************************INKUP101
00406 *                                                                 INKUP101
00407 ******************************************************************INKUP101
00408  7120-UPDATE-TINVLOC-ROW.                                         INKUP101
00409                                                                   INKUP101
00410      PERFORM                                                      INKUP101
00411        WITH TEST AFTER                                            INKUP101
00412        VARYING PV-SQL-SUB                                         INKUP101
00413        FROM 1 BY 1                                                INKUP101
00414        UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                         INKUP101
00415        OR SQLCODE = 0                                             INKUP101
00416        OR SQLCODE = +100)                                         INKUP101
00417                                                                   INKUP101
00418        EXEC SQL                                                   INKUP101
00420          UPDATE TINVLOC                                           INKUP101
00421          SET TOT_ITM_INVLID_QTY = :INVLOC-TOT-ITM-INVLID-QTY,     INKUP101
00422              TOT_REC_INVLID_QTY = :INVLOC-TOT-REC-INVLID-QTY      INKUP101
00423          WHERE (LOC_NBR = :INVLOC-LOC-NBR)                        INKUP101
00424          AND (AREA_NBR = :INVLOC-AREA-NBR)                        INKUP101
00425        END-EXEC                                                   INKUP101
00426                                                                   INKUP101
00427        EVALUATE TRUE                                              INKUP101
00428          WHEN SQLCODE = -904                                      INKUP101
00429          WHEN SQLCODE = 0                                         INKUP101
00430            CONTINUE                                               INKUP101
00431          WHEN SQLWARN0 NOT = SPACES                               INKUP101
00432          WHEN SQLCODE < 0                                         INKUP101
00433          WHEN SQLCODE > 0                                         INKUP101
00434            MOVE '7120-UPDATE-TINVLOC-ROW       '                  INKUP101
00435                                  TO PV-PARAGRAPH-NAME             INKUP101
00436            MOVE 'UPDATING TINVLOC TABLE'                          INKUP101
00437                                  TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600           PERFORM 9900-SQL-ABEND                                 INKUP101
00439        END-EVALUATE                                               INKUP101
00440      END-PERFORM.                                                 INKUP101
00441                                                                   INKUP101
00442      IF PV-SQL-SUB > PC-MAX-RETRIES                               INKUP101
00443          MOVE '7120-UPDATE-TINVLOC-ROW       '                    INKUP101
00444                                  TO PV-PARAGRAPH-NAME             INKUP101
00445          MOVE 'MAX RETRIES EXCEEDED   '                           INKUP101
00446                                  TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600         PERFORM 9900-SQL-ABEND                                   INKUP101
00448      END-IF.                                                      INKUP101
W26600                                                                          
W26600******************************************************************INKUP101
W26600 7700-INIT-CHECKPOINT-SELECT.                                     INKUP101
W26600                                                                          
W26600     EXEC SQL                                                             
W26600       SELECT CKPT_STAT_CODE,                                             
W26600              TRANS_QTY,                                                  
W26600              CKPT_TAKEN_QTY,                                             
W26600              TRANS_PRCSD_QTY,                                            
W26600              PRCSD_TMESTP                                                
W26600       INTO   :TCKRSTCNTL-CKPT-STAT-CODE,                                 
W26600              :TCKRSTCNTL-TRANS-QTY,                                      
W26600              :TCKRSTCNTL-CKPT-TAKEN-QTY,                                 
W26600              :TCKRSTCNTL-TRANS-PRCSD-QTY,                                
W26600              :TCKRSTCNTL-PRCSD-TMESTP                                    
W26600       FROM   TCKRSTCNTL                                                  
P28545       WHERE  JOB_NAME  = :PV-PROCESS-JOB-NAME                            
W26600       AND    PLAN_NAME = :PC-CKPT-PLAN-NAME                              
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         SET PS-TCKRSTCNTL-RCD-FND                                        
W26600                                 TO TRUE                                  
W26600       WHEN SQLCODE = +100                                                
W26600         INITIALIZE              PS-TCKRSTCNTL-RCD-FND-SW                 
W26600       WHEN OTHER                                                         
W26600         INITIALIZE              PS-TCKRSTCNTL-RCD-FND-SW                 
W26600         MOVE '7700-INIT-CHECKPOINT-SELECT'                       INKUP101
W26600                                 TO PV-PARAGRAPH-NAME             INKUP101
W26600         MOVE 'SELECT TCKRSTCNTL TABLE'                           INKUP101
W26600                                 TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************INKUP101
W26600 7710-UPDT-TCKRSTCNTL-RCD.                                        INKUP101
W26600                                                                          
W26600     EXEC SQL                                                             
W26600       UPDATE TCKRSTCNTL                                                  
W26000       SET CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE,                   
W26600           TRANS_QTY = :TCKRSTCNTL-TRANS-QTY,                             
W26600           CKPT_TAKEN_QTY = :TCKRSTCNTL-CKPT-TAKEN-QTY,                   
W26600           TRANS_PRCSD_QTY = :TCKRSTCNTL-TRANS-PRCSD-QTY,                 
W26600           PRCSD_TMESTP = CURRENT_TIMESTAMP                               
P28545       WHERE  JOB_NAME  = :PV-PROCESS-JOB-NAME                            
W26600       AND    PLAN_NAME = :PC-CKPT-PLAN-NAME                              
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         CONTINUE                                                         
W26600       WHEN OTHER                                                         
W26600         MOVE '7710-UPDT-TCKRSTCNTL-RCD'                          INKUP101
W26600                                 TO PV-PARAGRAPH-NAME             INKUP101
W26600         MOVE 'UPDATE TCKRSTCNTL TABLE'                           INKUP101
W26600                                 TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************INKUP101
W26600 7800-SELECT-RESTART-INFO.                                                
W26600                                                                          
W26600     EXEC SQL                                                             
W26600       SELECT WORK_STRG_DESC                                              
W26600       INTO   :TCKRSTINF-WORK-STRG-DESC                                   
W26600       FROM   TCKRSTINF                                                   
P28545       WHERE  JOB_NAME  = :PV-PROCESS-JOB-NAME                            
W26600       AND    PLAN_NAME = :PC-CKPT-PLAN-NAME                              
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         SET PS-TCKRSTINF-RCD-FND                                         
W26600                                 TO TRUE                                  
W26600       WHEN SQLCODE = +100                                                
W26600         INITIALIZE              PS-TCKRSTINF-RCD-FND-SW                  
W26600       WHEN OTHER                                                         
W26600         INITIALIZE              PS-TCKRSTINF-RCD-FND-SW                  
W26600         MOVE '7800-SELECT-RESTART-INFO'                          INKUP101
W26600                                 TO PV-PARAGRAPH-NAME             INKUP101
W26600         MOVE 'SELECT TCKRSTINF TABLE'                            INKUP101
W26600                                 TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************INKUP101
W26600 7810-UPDT-TCKRSTINF-RCD.                                                 
W26600                                                                          
W26600     EXEC SQL                                                             
W26600       UPDATE TCKRSTINF                                                   
W26600       SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                     
P28545       WHERE  JOB_NAME  = :PV-PROCESS-JOB-NAME                            
W26600       AND    PLAN_NAME = :PC-CKPT-PLAN-NAME                              
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         SET PS-TCKRSTINF-RCD-FND                                         
W26600                                 TO TRUE                                  
W26600       WHEN SQLCODE = +100                                                
W26600         INITIALIZE              PS-TCKRSTINF-RCD-FND-SW                  
W26600       WHEN OTHER                                                         
W26600         MOVE '7810-UPDT-TCKRSTINF-RCD'                           INKUP101
W26600                                 TO PV-PARAGRAPH-NAME             INKUP101
W26600         MOVE 'UPDATE TCKRSTINF TABLE'                            INKUP101
W26600                                 TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************INKUP101
W26600 7820-INSRT-TCKRSTINF-RCD.                                                
W26600                                                                          
W26600     EXEC SQL                                                             
W26600       INSERT INTO TCKRSTINF                                              
W26600       VALUES (:TCKRSTINF-JOB-NAME,                                       
W26600               :TCKRSTINF-PLAN-NAME,                                      
W26600               :TCKRSTINF-RST-TRANS-SEQ-NMBR,                             
W26600               :TCKRSTINF-SEQ-NMBR,                                       
W26600               :TCKRSTINF-WORK-STRG-DESC)                                 
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         CONTINUE                                                         
W26600       WHEN OTHER                                                         
W26600         MOVE '7820-INSRT-TCKRSTINF-RCD'                          INKUP101
W26600                                 TO PV-PARAGRAPH-NAME             INKUP101
W26600         MOVE 'INSERT TCKRSTINF TABLE'                            INKUP101
W26600                                 TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************INKUP101
W26600 7840-DLT-TCKRSTINF-RCD.                                                  
W26600                                                                          
W26600     EXEC SQL                                                             
W26600       DELETE FROM TCKRSTINF                                              
P28545       WHERE  JOB_NAME  = :PV-PROCESS-JOB-NAME                            
W26600       AND    PLAN_NAME = :PC-CKPT-PLAN-NAME                              
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         CONTINUE                                                         
W26600       WHEN OTHER                                                         
W26600         MOVE '7840-DLT-TCKRSTINF-RCD'                            INKUP101
W26600                                 TO PV-PARAGRAPH-NAME             INKUP101
W26600         MOVE 'DELETE TCKRSTINF TABLE'                            INKUP101
W26600                                 TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************INKUP101
W26600 7900-COMMIT-WORK.                                                        
W26600                                                                          
W26600     EXEC SQL                                                             
W26600         COMMIT WORK                                                      
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         CONTINUE                                                         
W26600       WHEN OTHER                                                         
W26600         MOVE '7900-COMMIT-WORK'                                  INKUP101
W26600                                 TO PV-PARAGRAPH-NAME             INKUP101
W26600         MOVE 'DB2 COMMIT'                                        INKUP101
W26600                                 TO PV-DB2-OPER-ATTEMPTED         INKUP101
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************INKUP101
W26600 7910-ROLLBACK-WORK.                                                      
W26600                                                                          
W26600     EXEC SQL                                                             
W26600         ROLLBACK WORK                                                    
W26600     END-EXEC.                                                            
W26600                                                                          
W26600/                                                                         
W26600******************************************************************INKUP101
W26600 8000-OPEN-FILES.                                                         
W26600                                                                          
P28545     OPEN INPUT INVALID-DETAIL-FILE                                       
P28545          INPUT CC-JOBNAME-CNTL-FILE.                                     
W26600                                                                          
P28545     PERFORM 8200-READ-PROCESS-JOB-NAME.                                  
P28545                                                                          
P28545     DISPLAY ' '.                                                         
P28545     DISPLAY ' PROCESSING JOB NAME: ' PV-PROCESS-JOB-NAME.                
P28545                                                                          
P28545     IF PV-PROCESS-JOB-NAME = SPACES                                      
P28545         MOVE +4002                 TO ABEND-CODE                         
P28545         DISPLAY '**  ABEND     **'                                       
P28545         DISPLAY '**  PROGRAM   **  =  INKUP101'                          
P28545         DISPLAY '**  INVALID PROCESSING JOB NAME'                        
P28545         CALL 'ILBOABN0'  USING ABEND-CODE                                
P28545     END-IF.                                                              
P28545                                                                          
W26600******************************************************************INKUP101
W26600 8100-READ-INVALID-DTL-FILE.                                              
W26600                                                                          
W26600     READ INVALID-DETAIL-FILE                                             
W26600       INTO IN008-INVENTORY-COUNT-RECORD                                  
W26600         AT END SET PS-INVALID-DTL-EOF                                    
W26600                                 TO TRUE.                                 
W26600                                                                          
W26600     IF NOT PS-INVALID-DTL-EOF                                            
W26600         ADD +1                  TO PT-NBR-INVALID-DTL-RCD-READ           
W26600     END-IF.                                                              
W26600                                                                          
P28545******************************************************************02700000
P28545* READS THE PROCESSING JOB NAME CONTROL CARD FILE.                02710000
P28545******************************************************************02720000
P28545 8200-READ-PROCESS-JOB-NAME.                                      02730000
P28545     READ CC-JOBNAME-CNTL-FILE INTO PV-PROCESS-JOB-NAME-CC        02740000
P28545         AT END                                                   02750000
P28545             MOVE +4003                 TO ABEND-CODE                     
P28545             DISPLAY '**  ABEND     **'                                   
P28545             DISPLAY '**  PROGRAM   **  =  INKUP101'                      
P28545             DISPLAY '**  EMPTY PROCESSING JOB NAME CONTROL CARD'         
P28545             CALL 'ILBOABN0'  USING ABEND-CODE.                           
P28545                                                                  02770000
W26600******************************************************************INKUP101
W26600 8900-CLOSE-FILES.                                                        
W26600                                                                          
W26600     CLOSE INVALID-DETAIL-FILE                                            
P28545           CC-JOBNAME-CNTL-FILE.                                          
00449                                                                   INKUP101
00451 ******************************************************************INKUP101
00452 *  STANDARD DB2 ERROR ABEND ROUTINE                               INKUP101
00453 *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             INKUP101
00454 ******************************************************************INKUP101
W26600 9900-SQL-ABEND.                                                  INKUP101
00456                                                                   INKUP101
00457      MOVE +4013                  TO ABEND-CODE.                   INKUP101
00458      DISPLAY '**  ABEND     **'.                                  INKUP101
00459      DISPLAY '**  PROGRAM   **  =  INKUP101'.                     INKUP101
00460      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            INKUP101
00461      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        INKUP101
00462                                                                   INKUP101
00463 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         INKUP101
00464 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        INKUP101
00465                                                                   INKUP101
00466      COPY DPPD004.                                                INKUP101
W26600                                                                          
W26600     IF SQLCODE NOT = -911                                                
W26600         EXEC SQL                                                         
W26600             ROLLBACK                                                     
W26600         END-EXEC                                                         
W26600     END-IF.                                                              
00467                                                                   INKUP101
00468      CALL 'ILBOABN0'  USING ABEND-CODE.                           INKUP101
00469                                                                   INKUP101
W26600******************************************************************        
W26600*  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
W26600******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = INKUP101'.                              
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
