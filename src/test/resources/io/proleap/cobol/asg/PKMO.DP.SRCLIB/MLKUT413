       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              MLKUT413.                                       
       AUTHOR.                  TESS BESTE.                                     
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            06/23/98.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *             STORE INVENTORY REPORT EXTRACT.                             
      *                                                                         
      * THIS PROGRAM WILL EXTRACT FROM TMNSCLS, THE DATA TO BE REPORTED         
      * ON THE STORE INVENTORY REPORTS.                                         
      *                                                                         
      * IT IS DRIVEN BY A CURSOR OF SUBCLASSES THAT WERE ACTIVE ON              
      * MBS FOR MERCHANDISE LEDGER AS OF THE END OF THE MAX POSTING             
      * FISCAL MONTH.                                                           
      *                                                                         
      * FOR EACH SUBCLASS, THE PROGRAM WILL EXTRACT, BY STORE, ALL              
      * RETAIL AMOUNTS FOR THE POSTING FISCAL MONTH AND THE ENDING              
      * INVENTORY AT RETAIL FOR THE PREVIOUS FISCAL MONTH.                      
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
      * JBF 07/12/2000                                                          
      * - CHANGED MODULE 1080- TO THE POSTING DATE                              
      * - CHANGED ALL COMMENTS/DISPLAYS FROM MAX POSTING TO POSTING             
      * - CHANGED 8000-GET-MXPSTG-FSCL-MN TO 8000-GET-PSTG-FSCL-MN              
      * - CHANGED SELECT IN 8000- FROM MAX POSTING DATE TO POSTING DATE         
      *                                                                         
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION             
      ******************************************************************        
                                                                                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CONTROL-CARD-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT STR-INV-DTE-FILE                                              
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT STR-INV-EXTRACT-FILE                                          
               ASSIGN TO OUT02.                                                 
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CONTROL-CARD-RECORD              PIC X(80).                          
                                                                                
       FD  STR-INV-DTE-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  STR-INV-DTE-RECORD               PIC X(80).                          
                                                                                
       FD  STR-INV-EXTRACT-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
W26603 01  STR-INV-EXTRACT-RECORD           PIC X(93).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR STORE INVENTORY REPORT DATE RECORD                    
      ****************************************************************          
                                                                                
           COPY MLRD115.                                                        
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR STORE INVENTORY REPORT EXTRACT RECORD.                
      ****************************************************************          
                                                                                
           COPY MLRD109.                                                        
                                                                                
      ****************************************************************          
      *  ERROR MESSAGE AREA FOR DB2                                             
      ****************************************************************          
                                                                                
           COPY DPWS004.                                                        
                                                                                
       01  EXTRACT-COUNT                    PIC 9(9)   VALUE ZEROES.            
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-EXTRACT-DTE               PIC X(10).                          
           05  FILLER                       PIC X(70).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-OPERCO-NBR                PIC X(02)  VALUE '03'.              
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-CONTROL-CARDS-SW   PIC X      VALUE 'N'.               
               88  PS-END-OF-CONTROL-CARDS             VALUE 'Y'.               
           05  PS-DATE-FOUND-SW             PIC X      VALUE 'N'.               
               88  PS-DATE-FOUND                       VALUE 'Y'.               
               88  PS-DATE-NOT-FOUND                   VALUE 'N'.               
           05  PS-END-OF-SUBCLASS-CSR-SW    PIC X      VALUE 'N'.               
               88  PS-END-OF-SUBCLASS-CSR              VALUE 'Y'.               
           05  PS-END-OF-LEDGER-CSR-SW      PIC X      VALUE 'N'.               
               88  PS-END-OF-LEDGER-CSR                VALUE 'Y'.               
               88  PS-NOT-END-OF-LEDGER-CSR            VALUE 'N'.               
           05  PS-BEG-INV-FOUND-SW          PIC X      VALUE 'N'.               
               88  PS-BEG-INV-FOUND                    VALUE 'Y'.               
               88  PS-BEG-INV-NOT-FOUND                VALUE 'N'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PREV-FSCL-MN-END-DTE      PIC X(10)  VALUE SPACE.             
           05  PV-PREV-FSCL-MN-NBR          PIC X(02)  VALUE SPACE.             
           05  PV-SUB-CL-NBR                PIC X(02)  VALUE SPACE.             
                                                                                
           05  PV-DB2-OPER-ATTEMPTED        PIC X(30).                          
           05  PV-PARAGRAPH-NAME            PIC X(30).                          
       01  ABEND-CODE                       PIC S9(04) COMP                     
                                                       VALUE ZERO.              
                                                                                
      ****************************************************************          
      * LEDGER MONTH-TO-DATE SUBCLASS/STORE TABLE                               
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TMNSCLS                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * MERCHANDISE LEDGER CONTROL TABLE                                        
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * DATE ATTRIBUTE TABLE                                                    
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * MBS MERCHANDISE AREA TABLE                                              
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * MBS APPLICATION AVAILABILITY TABLE                                      
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TAPLAVL                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *  COMMUNICATION AREAS FOR DB2                                            
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *  DEPARTMENT NUMBER CUROSR                                               
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
              DECLARE SUBCLASS-CSR CURSOR FOR                                   
                  SELECT                                                        
                      GMA_NBR                                                   
                     ,DMA_NBR                                                   
                     ,BYR_NBR                                                   
                     ,DEPT_NBR                                                  
                     ,MAJ_CL_NBR                                                
                     ,SUB_CL_NBR                                                
                  FROM                                                          
                      TMDSEAR A                                                 
                     ,TAPLAVL B                                                 
                                                                                
                  WHERE A.OPERCO_NBR     = :PC-OPERCO-NBR                       
                    AND A.MDSELV_CDE     = 'SCL'                                
                    AND :ML115-EXTRACT-DTE                                      
                         BETWEEN A.STRT_DTE                                     
                             AND A.END_DTE                                      
                    AND A.MDSEAR_DKEY    =  B.MDSEAR_DKEY                       
                    AND B.APLSYS_CDE     = 'ML'                                 
                    AND :ML115-EXTRACT-DTE                                      
                         BETWEEN B.MDSEAR_STRT_DTE                              
                             AND B.MDSEAR_END_DTE                               
W26603        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *  LEDGER CURSOR                                                          
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
              DECLARE LEDGER-CSR CURSOR FOR                                     
                  SELECT                                                        
                      STR_NBR                                                   
                     ,SLS_RTL_AMT                                               
                     ,MKDN_RTL_AMT                                              
                     ,MKUP_RTL_AMT                                              
                     ,XFER_IN_RTL_AMT                                           
                     ,XFER_OUT_RTL_AMT                                          
                     ,RCPT_RTL_AMT                                              
                     ,PADJ_RTL_AMT                                              
                     ,INV_ADJ_RTL_AMT                                           
                     ,SHNK_RTL_AMT                                              
                     ,END_INV_RTL_AMT                                           
                  FROM                                                          
                      TMNSCLS                                                   
                  WHERE   FSCL_MN_NBR     = :ML115-EXTRACT-MN-NBR               
                    AND   FSCL_MN_END_DTE = :ML115-EXTRACT-DTE                  
                    AND   DEPT_NBR        = :MDSEAR-DEPT-NBR                    
                    AND   SUB_CL_NBR      = :PV-SUB-CL-NBR                      
                  ORDER BY STR_NBR                                              
              FOR FETCH ONLY                                                    
           END-EXEC.                                                            
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
       1000-MAIN-MODULE.                                                        
                                                                                
           OPEN INPUT  CONTROL-CARD-FILE                                        
                OUTPUT STR-INV-DTE-FILE                                         
                       STR-INV-EXTRACT-FILE.                                    
                                                                                
           PERFORM 1050-INIT.                                                   
                                                                                
           PERFORM 8100-OPEN-SUBCLASS-CSR.                                      
                                                                                
           PERFORM 8200-FETCH-SUBCLASS-CSR.                                     
                                                                                
           PERFORM 2000-EXTRACT-SUBCLASS                                        
               UNTIL PS-END-OF-SUBCLASS-CSR.                                    
                                                                                
           PERFORM 8300-CLOSE-SUBCLASS-CSR.                                     
                                                                                
SJ         DISPLAY 'EXTRACT RECORDS WRITTEN : ', EXTRACT-COUNT.                 
SJ                                                                              
           CLOSE CONTROL-CARD-FILE                                              
                 STR-INV-DTE-FILE                                               
                 STR-INV-EXTRACT-FILE.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1050-INIT.                                                               
                                                                                
029800     PERFORM 1060-SET-EXTRACT-DTE.                                        
                                                                                
           PERFORM 8050-GET-PREV-FSCL-MN.                                       
                                                                                
           MOVE DTATTR-FSCL-MN-END-DTE TO PV-PREV-FSCL-MN-END-DTE.              
           MOVE DTATTR-FSCL-MN-NBR     TO PV-PREV-FSCL-MN-NBR.                  
                                                                                
                                                                                
029800 1060-SET-EXTRACT-DTE.                                                    
029900                                                                          
030000     PERFORM 7000-READ-CONTROL-CARD-FILE.                                 
030100                                                                          
030200     IF PS-END-OF-CONTROL-CARDS                                           
030300     OR CC-EXTRACT-DTE = SPACE                                            
030400         PERFORM 1080-USE-PSTG-DTE                                        
030500     ELSE                                                                 
030600         DISPLAY '** INPUT CONTROL CARD: ' CC-CONTROL-CARD                
030700         PERFORM 1070-USE-CONTROL-CARD-DTE                                
030800     END-IF.                                                              
030900                                                                          
031000     DISPLAY '** SELECTED EXTRACT DATE: ' ML115-EXTRACT-DTE.              
031100                                                                          
031200     PERFORM  7100-WRITE-STR-INV-DTE.                                     
033300                                                                          
033400                                                                          
033500 1070-USE-CONTROL-CARD-DTE.                                               
033600                                                                          
033700     DISPLAY '** USING CONTROL CARD DATE FOR EXTRACT DATE.'               
033800                                                                          
033900     MOVE CC-EXTRACT-DTE TO DTATTR-KY-DTE.                                
034000                                                                          
034100     PERFORM 8025-GET-DATE-ATTRIBUTES.                                    
034200                                                                          
034300     IF  PS-DATE-FOUND                                                    
034400     AND DTATTR-KY-DTE = DTATTR-FSCL-MN-END-DTE                           
034500         MOVE DTATTR-FSCL-MN-END-DTE TO ML115-EXTRACT-DTE                 
034600         MOVE DTATTR-FSCL-MN-NBR     TO ML115-EXTRACT-MN-NBR              
034800     ELSE                                                                 
034900         DISPLAY '** ABEND **'                                            
035000         DISPLAY '** PROGRAM MLKUT413 **'                                 
035100         DISPLAY '** PARAGRAPH 1070-USE-CONTROL-CARD-DTE'                 
035200         DISPLAY '** CONTROL CARD DATE NOT A VALID FISCAL'                
035300         DISPLAY '** MONTH-END DATE. '                                    
035400         MOVE +4003 TO ABEND-CODE                                         
035500         PERFORM 9999-ABEND                                               
035600     END-IF.                                                              
035700                                                                          
035800                                                                          
035900 1080-USE-PSTG-DTE.                                                       
036000                                                                          
           PERFORM 8000-GET-PSTG-FSCL-MN.                                       
                                                                                
           DISPLAY                                                              
036200     '** POSTING MONTH DATE: ' MLCNTL-PSTG-FSCL-MN-DTE.                   
                                                                                
036800     MOVE MLCNTL-PSTG-FSCL-MN-DTE TO ML115-EXTRACT-DTE.                   
038200     MOVE MLCNTL-PSTG-FSCL-MN-NBR TO ML115-EXTRACT-MN-NBR.                
040700                                                                          
040800                                                                          
       2000-EXTRACT-SUBCLASS.                                                   
                                                                                
           INITIALIZE ML109-STR-INV-EXTRACT-REC.                                
                                                                                
           MOVE MDSEAR-GMA-NBR          TO ML109-GMA-NBR.                       
           MOVE MDSEAR-DMA-NBR          TO ML109-DMA-NBR.                       
           MOVE MDSEAR-BYR-NBR          TO ML109-BYR-NBR.                       
           MOVE MDSEAR-DEPT-NBR         TO ML109-DEPT-NBR.                      
           MOVE MDSEAR-MAJ-CL-NBR       TO ML109-MAJ-CL-NBR.                    
           MOVE MDSEAR-SUB-CL-NBR(2:2)  TO ML109-SUB-CL-NBR.                    
                                                                                
           SET PS-NOT-END-OF-LEDGER-CSR    TO TRUE.                             
           MOVE MDSEAR-SUB-CL-NBR(2:2)     TO PV-SUB-CL-NBR.                    
                                                                                
           PERFORM 8400-OPEN-LEDGER-CSR.                                        
                                                                                
           PERFORM 8500-FETCH-LEDGER-CSR.                                       
                                                                                
           IF PS-NOT-END-OF-LEDGER-CSR                                          
               PERFORM 2100-EXTRACT-STORES                                      
                   UNTIL PS-END-OF-LEDGER-CSR                                   
           ELSE                                                                 
               PERFORM 7200-WRITE-STR-INV-EXTRACT                               
           END-IF.                                                              
                                                                                
           PERFORM 8600-CLOSE-LEDGER-CSR.                                       
                                                                                
           PERFORM 8200-FETCH-SUBCLASS-CSR.                                     
                                                                                
                                                                                
       2100-EXTRACT-STORES.                                                     
                                                                                
           MOVE MNSCLS-STR-NBR          TO ML109-STR-NBR.                       
           MOVE MNSCLS-SLS-RTL-AMT      TO ML109-SLS-RTL-AMT.                   
           MOVE MNSCLS-MKDN-RTL-AMT     TO ML109-MKDN-RTL-AMT.                  
           MOVE MNSCLS-MKUP-RTL-AMT     TO ML109-MKUP-RTL-AMT.                  
           MOVE MNSCLS-XFER-IN-RTL-AMT  TO ML109-XFER-IN-RTL-AMT.               
           MOVE MNSCLS-XFER-OUT-RTL-AMT TO ML109-XFER-OUT-RTL-AMT.              
           MOVE MNSCLS-RCPT-RTL-AMT     TO ML109-RCPT-RTL-AMT.                  
           MOVE MNSCLS-PADJ-RTL-AMT     TO ML109-PADJ-RTL-AMT.                  
           MOVE MNSCLS-INV-ADJ-RTL-AMT  TO ML109-INV-ADJ-RTL-AMT.               
           MOVE MNSCLS-SHNK-RTL-AMT     TO ML109-SHNK-RTL-AMT.                  
           MOVE MNSCLS-END-INV-RTL-AMT  TO ML109-END-INV-RTL-AMT.               
                                                                                
           PERFORM 8075-GET-BEG-INV.                                            
                                                                                
           IF PS-BEG-INV-FOUND                                                  
               MOVE MNSCLS-END-INV-RTL-AMT  TO ML109-BEG-INV-RTL-AMT            
           ELSE                                                                 
               MOVE ZERO                    TO ML109-BEG-INV-RTL-AMT            
           END-IF.                                                              
                                                                                
           PERFORM 7200-WRITE-STR-INV-EXTRACT.                                  
                                                                                
           PERFORM 8500-FETCH-LEDGER-CSR.                                       
                                                                                
                                                                                
049700 7000-READ-CONTROL-CARD-FILE.                                             
049800                                                                          
049900     READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
050000         AT END SET PS-END-OF-CONTROL-CARDS TO TRUE.                      
050100                                                                          
050200                                                                          
050300 7100-WRITE-STR-INV-DTE.                                                  
050400                                                                          
050500     WRITE STR-INV-DTE-RECORD                                             
050600         FROM ML115-STR-INV-DTE-REC.                                      
050700                                                                          
050800                                                                          
       7200-WRITE-STR-INV-EXTRACT.                                              
                                                                                
           WRITE STR-INV-EXTRACT-RECORD                                         
               FROM ML109-STR-INV-EXTRACT-REC.                                  
                                                                                
SJ         ADD +1 TO EXTRACT-COUNT.                                             
                                                                                
                                                                                
       8000-GET-PSTG-FSCL-MN.                                                   
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    PSTG_FSCL_MN_DTE                                            
                   ,PSTG_FSCL_MN_NBR                                            
                INTO                                                            
                    :MLCNTL-PSTG-FSCL-MN-DTE                                    
                   ,:MLCNTL-PSTG-FSCL-MN-NBR                                    
                FROM                                                            
                    TMLCNTL                                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8000-GET-PSTG-FSCL-MN' TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT ROW FROM TMLCNTL'                               
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
053900 8025-GET-DATE-ATTRIBUTES.                                                
054000                                                                          
054100     EXEC SQL                                                             
054200          SELECT                                                          
054400              FSCL_MN_NBR                                                 
054500             ,FSCL_MN_END_DTE                                             
054800          INTO                                                            
055000              :DTATTR-FSCL-MN-NBR                                         
055100             ,:DTATTR-FSCL-MN-END-DTE                                     
055400          FROM                                                            
055500              TDTATTR                                                     
055600          WHERE                                                           
055700              KY_DTE = :DTATTR-KY-DTE                                     
055800     END-EXEC.                                                            
055900                                                                          
056000     EVALUATE TRUE                                                        
056100         WHEN SQLCODE = ZERO                                              
056200             SET PS-DATE-FOUND     TO TRUE                                
056300         WHEN SQLCODE = +100                                              
056400             SET PS-DATE-NOT-FOUND TO TRUE                                
056500         WHEN OTHER                                                       
056600             DISPLAY '** DTATTR-KY-DTE: ' DTATTR-KY-DTE                   
056700             MOVE    '8025-GET-DATE-ATTRIBUTES'                           
056800                                          TO PV-PARAGRAPH-NAME            
056900             MOVE    'SELECT ROW FROM TDTATTR'                            
057000                                          TO PV-DB2-OPER-ATTEMPTED        
057100             PERFORM 9998-SQL-ABEND                                       
057200     END-EVALUATE.                                                        
057300                                                                          
057400                                                                          
       8050-GET-PREV-FSCL-MN.                                                   
                                                                                
           EXEC SQL                                                             
                SELECT DISTINCT                                                 
                    FSCL_MN_NBR                                                 
                   ,FSCL_MN_END_DTE                                             
                INTO                                                            
                    :DTATTR-FSCL-MN-NBR                                         
                   ,:DTATTR-FSCL-MN-END-DTE                                     
                FROM                                                            
                    TDTATTR                                                     
                WHERE                                                           
                    NFM_END_DTE = :ML115-EXTRACT-DTE                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
      **           CONTINUE                                                     
                   DISPLAY 'PREV FISCAL MONTH : ',                              
                                          DTATTR-FSCL-MN-END-DTE                
               WHEN OTHER                                                       
                   DISPLAY 'DTATTR-KY-DTE: ' DTATTR-KY-DTE                      
                   MOVE    '8050-GET-PREV-FSCL-MN'                              
                                                TO PV-PARAGRAPH-NAME            
                   MOVE    'SELECT ROW FROM TDTATTR'                            
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8075-GET-BEG-INV.                                                        
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    END_INV_RTL_AMT                                             
                INTO                                                            
                    :MNSCLS-END-INV-RTL-AMT                                     
                FROM                                                            
                    TMNSCLS                                                     
                WHERE                                                           
                          DEPT_NBR        = :MDSEAR-DEPT-NBR                    
                    AND   SUB_CL_NBR      = :PV-SUB-CL-NBR                      
                    AND   FSCL_MN_NBR     = :PV-PREV-FSCL-MN-NBR                
                    AND   FSCL_MN_END_DTE = :PV-PREV-FSCL-MN-END-DTE            
                    AND   STR_NBR         = :MNSCLS-STR-NBR                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-BEG-INV-FOUND     TO TRUE                             
               WHEN SQLCODE = +100                                              
                   SET PS-BEG-INV-NOT-FOUND TO TRUE                             
               WHEN OTHER                                                       
                   MOVE '8075-GET-BEG-INV'      TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT ROW FROM TMNSCLS'                               
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8100-OPEN-SUBCLASS-CSR.                                                  
                                                                                
           EXEC SQL                                                             
                OPEN SUBCLASS-CSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8100-OPEN-SUBCLASS-CSR'                                
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'OPEN SUBCLASS-CSR'     TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8200-FETCH-SUBCLASS-CSR.                                                 
                                                                                
           EXEC SQL                                                             
               FETCH                                                            
                   SUBCLASS-CSR                                                 
               INTO                                                             
                   :MDSEAR-GMA-NBR                                              
                  ,:MDSEAR-DMA-NBR                                              
                  ,:MDSEAR-BYR-NBR                                              
                  ,:MDSEAR-DEPT-NBR                                             
                  ,:MDSEAR-MAJ-CL-NBR                                           
                  ,:MDSEAR-SUB-CL-NBR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-SUBCLASS-CSR TO TRUE                           
               WHEN OTHER                                                       
                   MOVE '8200-FETCH-SUBCLASS-CSR'                               
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'FETCH SUBCLASS-CSR'    TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8300-CLOSE-SUBCLASS-CSR.                                                 
                                                                                
           EXEC SQL                                                             
               CLOSE SUBCLASS-CSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8300-CLOSE-SUBCLASS-CSR'                               
                                                TO PV-PARAGRAPH-NAME            
                   MOVE 'CLOSE SUBCLASS-CSR'    TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8400-OPEN-LEDGER-CSR.                                                    
                                                                                
           EXEC SQL                                                             
                OPEN LEDGER-CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8400-OPEN-LEDGER-CSR'  TO PV-PARAGRAPH-NAME            
                   MOVE 'OPEN LEDGER-CSR'       TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8500-FETCH-LEDGER-CSR.                                                   
                                                                                
           EXEC SQL                                                             
               FETCH                                                            
                   LEDGER-CSR                                                   
               INTO                                                             
                  :MNSCLS-STR-NBR                                               
                 ,:MNSCLS-SLS-RTL-AMT                                           
                 ,:MNSCLS-MKDN-RTL-AMT                                          
                 ,:MNSCLS-MKUP-RTL-AMT                                          
                 ,:MNSCLS-XFER-IN-RTL-AMT                                       
                 ,:MNSCLS-XFER-OUT-RTL-AMT                                      
                 ,:MNSCLS-RCPT-RTL-AMT                                          
                 ,:MNSCLS-PADJ-RTL-AMT                                          
                 ,:MNSCLS-INV-ADJ-RTL-AMT                                       
                 ,:MNSCLS-SHNK-RTL-AMT                                          
                 ,:MNSCLS-END-INV-RTL-AMT                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-LEDGER-CSR TO TRUE                             
               WHEN OTHER                                                       
                   MOVE '8500-FETCH-LEDGER-CSR' TO PV-PARAGRAPH-NAME            
                   MOVE 'FETCH LEDGER-CSR'      TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8600-CLOSE-LEDGER-CSR.                                                   
                                                                                
           EXEC SQL                                                             
               CLOSE LEDGER-CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '8600-CLOSE-LEDGER-CSR' TO PV-PARAGRAPH-NAME            
                   MOVE 'CLOSE LEDGER-CSR'      TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       9998-SQL-ABEND.                                                          
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = MLKUT413'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ABEND.                                                  
                                                                                
                                                                                
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
