000100 IDENTIFICATION DIVISION.                                                 
000200*-----------------------*                                                 
000300 PROGRAM-ID.    IQKCS403.                                                 
000400 AUTHOR.        GREG SHEFF.                                               
000500 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000600 DATE-WRITTEN.  02-19-03.                                                 
000700 DATE-COMPILED.                                                           
000800                                                                          
000900*---------------------------------------------------------------*         
001000*    I403 - BUSINESS GROUP LIST .                               *         
001100*                                                               *         
001200*    YOU CAN GET TO THIS SCREEN IN THE FOLLOWING WAYS:          *         
001300*                                                               *         
001400*    F17 - IQLVEND  - IQKCS402 - VENDOR NAME / LABEL LIST       *         
001500*          IT WILL DISPLAY A LIST OF BUSINESS GROUPS ASSOCIATED *         
001600*          WITH A PARTICULAR VENDOR NAME / LABEL.               *         
001700*                                                               *         
001800*    F20 - IQSVEND  - IQKCS401 - UPC VERIFICATION LIST          *         
001900*          IT WILL DISPLAY S A LIST OF ALL AVAILABLE BUSINESS   *         
002000*          GROUPS FOR ALL VENDORS.                              *         
002100*                                                               *         
002200*    THEN YOU CAN SELECT A BUSINESS GROUP AND YOU WILL GO TO    *         
002300*    IQLDEPT (IQKCS404) AND DISPLAY A DEPARTMENT LIST FOR       *         
002400*    EITHER ONE VENDOR OR ALL VENDORS.                          *         
002500*        FORMAT CODE IS SPACE.                                  *         
002600*                                                               *         
002700*---------------------------------------------------------------*         
002710* DATE:        09/14/2021                                                 
002720* WR/IR#:      CHG0261678                                                 
002730* PROGRAMMER:  GERMAN BANDA                                               
002740* DESCRIPTION: CHANGE CALL OF DPKUT100 TO                                 
002750*              CALL DP010I-NUMERIC-EDIT-ROUTINE                           
002760*              MAKING THE CALL DYNAMIC VS STATIC                          
002770*---------------------------------------------------------------*         
CICS  * CHANGED 10/31/2022    BY JIM HUNTER      WR: CHG0277565                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  ******************************************************************        
002800 EJECT                                                                    
002900 ENVIRONMENT DIVISION.                                                    
003000                                                                          
003100 DATA DIVISION.                                                           
003200                                                                          
003300 WORKING-STORAGE SECTION.                                                 
003400 01  DK-DB2-KEYS.                                                         
003500     15  DK-LBL-NM                      PIC  X(30)  VALUE SPACE.          
003600                                                                          
003700 01  PC-PROGRAM-CONSTANTS.                                                
003800     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
003900                                                    'IQ403A  '.           
004000     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
004100                                                    'IQKM403 '.           
004200     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
004300                                                    'IQKCS403'.           
004400     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
004500     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
004600     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE ' '.            
004700     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +14             
004800                                                    COMP SYNC.            
004900     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
005000                                                    COMP SYNC.            
005100     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
005200                                                    +1000000              
005300                                                    COMP-3.               
005400     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
005500                                                    COMP SYNC.            
005600     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
005700                                                    COMP-3.               
005800*                                                                         
005900     05  PC-TSYMSG-NUMBERS.                                               
006000         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
006100         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
006200         10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
006300         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
006400         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
006500         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
006600         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
006700         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
006800         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
006900         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
007000         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
007100         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
007200         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
007300         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
007400*                                                                         
007500 01  PS-PROGRAM-SWITCHES.                                                 
007600     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
007700         88  PS-NO-ERROR                            VALUE 'Y'.            
007800         88  PS-ERROR                               VALUE 'N'.            
007900     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
008000         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
008100         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
008200     05  PS-BUSINESS-GROUP-SW           PIC  X(01)  VALUE ' '.            
008300         88  PS-BUSINESS-GROUP-ALL                  VALUE 'A'.            
008400         88  PS-BUSINESS-GROUP-PARTIAL              VALUE 'P'.            
008500*                                                                         
008600 01  PV-PROGRAM-VARIABLES.                                                
008700     05  PV-VENDOR-ABBREV               PIC  X(06)  VALUE SPACES.         
008710     05  PV-CURSOR-NAME                 PIC  X(18)  VALUE SPACES.         
008800     05  PV-SUB                         PIC S9(04)  VALUE +0              
008900                                                    COMP SYNC.            
009000     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
009100                                                    COMP SYNC.            
009200     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
009300                                                    COMP-3.               
009400     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
009500                                                    COMP-3.               
009600     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
009700                                                    COMP-3.               
009800     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
009900                                                    COMP-3.               
010000     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
010100                                                    COMP-3.               
010200     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
010300                                                    COMP-3.               
010400     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
010500                                                    COMP SYNC.            
010600     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
010700                                                    COMP SYNC.            
010800     05  PV-ITEM                        PIC S9(04)  VALUE +0              
010900                                                    COMP SYNC.            
011000     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
011100                                                    COMP SYNC.            
011200     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
011300                                                    COMP SYNC.            
011400     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
011500                                                    COMP SYNC.            
011600     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
011700                                                    COMP SYNC.            
011800     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
011900                                                    COMP SYNC.            
012000     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
012100                                                    COMP SYNC.            
012200     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
012300                                                    COMP SYNC.            
012400     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
012500                                                    COMP SYNC.            
012600     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
012700                                                    COMP SYNC.            
012800     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
012900                                                    COMP SYNC.            
013000     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
013100                                                    COMP SYNC.            
013200     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
013300                                                    COMP SYNC.            
013400******************************************************************        
013500**  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
013600**  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
013700**  APPLICATION-SPECIFIC COMMAREA.                              **        
013800******************************************************************        
013900 01  LQW-LIST-QUEUE-WORK-AREA.                                            
014000     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
014100     05  LQW-ITEM-ENTRIES               OCCURS 14 TIMES                   
014200                                        INDEXED BY LQW-IDX.               
014300         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
014400         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
014500         10  LQW-SELECTED-SW            PIC  X(01).                       
014600             88  LQW-SELECT                         VALUE 'S'.            
014700             88  LQW-DESELECT                       VALUE ' '.            
014800         10  LQW-GRP-NBR                PIC  9(03).                       
014900         10  LQW-GRP-DES                PIC  X(25).                       
015000                                                                          
015100     EJECT                                                                
015200*---------------------------------------------------------------*         
015300*  RECORD LAYOUT FOR THE SELECTION TEMP STORAGE QUEUE.          *         
015400*---------------------------------------------------------------*         
015500     COPY IQWS014.                                                        
015600                                                                          
015700     EJECT                                                                
015800*---------------------------------------------------------------*         
015900*    MAP LAYOUT                                                 *         
016000*---------------------------------------------------------------*         
016100                                                                          
016200     COPY IQKM403.                                                        
016300*                                                                         
016400 01  MR-OCCURS-LAYOUT                   REDEFINES  IQ403AO.               
016500     05  FILLER                         PIC X(77).                        
016600     05  MR-SCROLL-AREA                 OCCURS 14 TIMES.                  
016700         10  MR-SELECTL                 PIC S9(04) COMP.                  
016800         10  MR-SELECTA                 PIC  X(01).                       
016900         10  MR-SELECTC                 PIC  X(01).                       
017000         10  MR-SELECTH                 PIC  X(01).                       
017100         10  MR-SELECTI                 PIC  X(01).                       
017200                                                                          
017300         10  MR-GRP-NBRL                PIC S9(04) COMP.                  
017400         10  MR-GRP-NBRA                PIC  X(01).                       
017500         10  MR-GRP-NBRC                PIC  X(01).                       
017600         10  MR-GRP-NBRH                PIC  X(01).                       
017700         10  MR-GRP-NBR.                                                  
017800             15  MR-GRP-NBR9            PIC  9(03).                       
017900                                                                          
018000         10  MR-GRP-DESL                PIC S9(04) COMP.                  
018100         10  MR-GRP-DESA                PIC  X(01).                       
018200         10  MR-GRP-DESC                PIC  X(01).                       
018300         10  MR-GRP-DESH                PIC  X(01).                       
018400         10  MR-GRP-DES                 PIC  X(25).                       
018500     EJECT                                                                
018600*---------------------------------------------------------------*         
018700*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
018800*---------------------------------------------------------------*         
018900                                                                          
019000     COPY DPWS015.                                                        
019100                                                                          
019200*---------------------------------------------------------------*         
019300*    FUNCTION KEYS COPYBOOK.                                    *         
019400*---------------------------------------------------------------*         
019500                                                                          
019600     COPY DPWS016.                                                        
019700                                                                          
019800*---------------------------------------------------------------*         
019900*    NUMERIC EDIT LAYOUT.                                       *         
020000*---------------------------------------------------------------*         
020100                                                                          
020200     COPY DPWS010I.                                                       
020300 EJECT                                                                    
020400*---------------------------------------------------------------*         
020500*    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
020600*---------------------------------------------------------------*         
020700                                                                          
020800     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
020900 EJECT                                                                    
021000*---------------------------------------------------------------*         
021100*    STANDARD COMMAREA.                                         *         
021200*---------------------------------------------------------------*         
021300                                                                          
021400     COPY DPWS020.                                                        
021500     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
021600*---------------------------------------------------------------*         
021700* INTER-APPLICATION COMMAREA.                                   *         
021800*   IQWS016 - IS THE INPUT LAYOUT WHEN COMING FROM THE PREVIOUS *         
021900*             SCREEN.                                           *         
022000*   IQWS016 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     *         
022100*             THIS LIST PROGRAM.                                *         
022200*---------------------------------------------------------------*         
022300                                                                          
022400         10  INTER-APPL-COMM-AREA       PIC X(200).                       
022500     COPY IQWS016.                                                        
022600*---------------------------------------------------------------*         
022700*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
022800*        CURRENT LENGTH IS 1200 FOR IQSBUSGP MNEMONIC.                    
022900*        YOU MUST CHECK THE COMMAREA LENGTH FOR THE MNEMONIC              
023000*            IF YOU ADD ANY MORE FIELDS.                                  
023100*---------------------------------------------------------------*         
023200         10  ASC-SPECIFIC-COMMAREA.                                       
023300             15  ASC-LIST-KEYS.                                           
023400                 20  ASC-KEY-LBL-NM                  PIC  X(30).          
023500             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
023600                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
023700                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
023800                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
023900                                                                          
024000                 20  ASC-ITEM-ARRAY.                                      
024100                     25  ASC-ITEM-ENTRIES OCCURS 14 TIMES.                
024200                         30  ASC-LIST-ITEM-NUMBER                         
024300                                                     PIC S9(09)           
024400                                                     COMP-3.              
024500                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
024600                             88  ASC-VALID-SELECT    VALUE ' '            
024700                                                           'S'            
024800                                                           'B'            
024900                                                           'E'.           
025000                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
025100                             88  ASC-TEMP-SELECT     VALUE 'S'.           
025200                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
025300                             88  ASC-TEMP-END        VALUE 'E'.           
025400                         30  ASC-SELECTED-SW         PIC  X(01).          
025500                             88  ASC-NO-SELECT       VALUE ' '.           
025600                             88  ASC-SELECT          VALUE 'S'.           
025700                             88  ASC-BEGIN           VALUE 'B'.           
025800                             88  ASC-END             VALUE 'E'.           
025900                         30  ASC-GRP-NBR             PIC  9(03).          
026000                         30  ASC-GRP-DES             PIC  X(25).          
026100                                                                          
026200             15  ASC-BLK-SUB                         PIC S9(04)           
026300                                                          COMP            
026400                                                          SYNC.           
026500             15  ASC-ITEM-SUB                        PIC S9(04)           
026600                                                          COMP            
026700                                                          SYNC.           
026800             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
026900                                                          COMP-3.         
027000             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
027100                                                          COMP-3.         
027200             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
027300                                                          COMP-3.         
027400             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
027500                                                          COMP-3.         
027600             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
027700                                                          COMP-3.         
027800             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
027900                                                          COMP            
028000                                                          SYNC.           
028100             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
028200                                                          COMP-3.         
028300             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
028400                                                          COMP-3.         
028500             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
028600                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
028700                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
028800             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
028900                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
029000                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
029100             15  ASC-LIST-QUEUE-NAME.                                     
029200                 20  ASC-LIST-TERM-ID                PIC  X(04).          
029300                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
029400                                                          COMP-3.         
029500                 20  ASC-LIST-SEQ                    PIC  9(01).          
029600             15  ASC-SEL-QUEUE-NAME.                                      
029700                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
029800                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
029900                                                          COMP-3.         
030000                 20  ASC-SEL-SEQ                     PIC  9(01).          
030100             15  ASC-START-ITEM-REQUEST.                                  
030200                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
030300             15  ASC-FILLER                          PIC  X(126).         
030400         10  FILLER                                  PIC  X(1665).        
030500     EJECT                                                                
030600*---------------------------------------------------------------*         
030700*    ABEND PROCESSING COPYBOOK.                                 *         
030800*---------------------------------------------------------------*         
030900                                                                          
031000     COPY DPWS013.                                                        
031100                                                                          
031200 EJECT                                                                    
031300*---------------------------------------------------------------*         
031400*    DB2 AREA FOR XST_VND_STYL                                  *         
031500*---------------------------------------------------------------*         
031600     EXEC SQL                                                             
031700          INCLUDE XST00013                                                
031800     END-EXEC.                                                            
031900*---------------------------------------------------------------*         
032000*    DB2 AREA FOR XST_DEPT                                      *         
032100*---------------------------------------------------------------*         
032200     EXEC SQL                                                             
032300          INCLUDE XST00015                                                
032400     END-EXEC.                                                            
032500*---------------------------------------------------------------*         
032600*    DB2 AREA FOR XST_BUS_GP                                    *         
032700*---------------------------------------------------------------*         
032800     EXEC SQL                                                             
032900          INCLUDE XST00016                                                
033000     END-EXEC.                                                            
033100*---------------------------------------------------------------*         
033200*    DB2 AREA FOR XST_VND                                       *         
033300*---------------------------------------------------------------*         
033400     EXEC SQL                                                             
033500          INCLUDE XST00018                                                
033600     END-EXEC.                                                            
033700*---------------------------------------------------------------*         
033800     EXEC SQL                                                             
033900          INCLUDE SQLCA                                                   
034000     END-EXEC.                                                            
034100*---------------------------------------------------------------*         
034200*    SELECTS A LIST OF BUSINESS GROUPS ASSOCIATED WITH A        *         
034300*    PARTICULAR VENDOR LABEL/VENDOR NAME THAT WAS SELECTED ON   *         
034400*    THE PREVIOUS SCREEN.                                       *         
034500                                                                          
034600     EXEC SQL                                                             
034700          DECLARE BUS-GRP-CSR CURSOR FOR                                  
034800            SELECT DISTINCT                                               
034900                 A.BUS_GP_NBR,                                            
035000                 A.BUS_GP_DESC                                            
035100              FROM  XST_BUS_GP    A,                                      
035200                    XST_DEPT      B,                                      
035300                    XST_VND_STYL  D,                                      
035400                    XST_VND       E                                       
035500                WHERE  E.ENT_NM_DESC  = :DK-LBL-NM                        
035600                  AND  D.DEPT_NBR     = B.DEPT_NBR                        
035700                  AND  B.BUS_GP_NBR   = A.BUS_GP_NBR                      
035800                  AND  D.ENT_ID       = E.ENT_ID                          
035900            UNION                                                         
036000            SELECT DISTINCT                                               
036100                 A.BUS_GP_NBR,                                            
036200                 A.BUS_GP_DESC                                            
036300              FROM  XST_BUS_GP    A,                                      
036400                    XST_DEPT      B,                                      
036500                    XST_VND_STYL  D                                       
036600                WHERE  D.LBL_NM     = :DK-LBL-NM                          
036700                  AND  D.DEPT_NBR   = B.DEPT_NBR                          
036800                  AND  B.BUS_GP_NBR = A.BUS_GP_NBR                        
036900                    ORDER BY 2                                            
037000     END-EXEC.                                                            
037100                                                                          
037200     EXEC SQL                                                             
037300          DECLARE BUS-GRP-ALL CURSOR FOR                                  
037400            SELECT DISTINCT                                               
037500                   BUSGP.BUS_GP_NBR                                       
037600                  ,BUSGP.BUS_GP_DESC                                      
037700             FROM  XST_BUS_GP    BUSGP                                    
037800                  ,XST_DEPT      DEPT                                     
037900                  ,XST_VND_STYL  VNDSTYL                                  
038000            WHERE  VNDSTYL.DEPT_NBR = DEPT.DEPT_NBR                       
038100              AND  DEPT.BUS_GP_NBR  = BUSGP.BUS_GP_NBR                    
038200         ORDER BY  BUS_GP_DESC                                            
038300                                                                          
038400     END-EXEC.                                                            
038500                                                                          
038600 LINKAGE SECTION.                                                         
038700                                                                          
038800 01  DFHCOMMAREA.                                                         
038900     05  FILLER                         OCCURS  1 TO 4072 TIMES           
039000                                        DEPENDING ON EIBCALEN.            
039100         10  FILLER                     PIC X.                            
039200                                                                          
039300 EJECT                                                                    
039400 PROCEDURE DIVISION.                                                      
039500******************************************************************        
039600**  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
039700**  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
039800**  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
039900******************************************************************        
040000 0000-MAIN-MODULE.                                                        
040100     INITIALIZE DP030-CICS-API-FIELDS.                                    
040200     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
040300     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
040400     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
040500     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
040600     MOVE LENGTH OF IQ403AI        TO DP030-MAP-LENGTH (1).               
040700     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
040800     PERFORM 0001-CALL-CICS-ARCH-API.                                     
040900*                                                                         
041000     PERFORM 1000-CONTROL-PROCESSING                                      
041100*                                                                         
041200     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
041300     PERFORM 0001-CALL-CICS-ARCH-API.                                     
041400                                                                          
041500******************************************************************        
041600**  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
041700**  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
041800******************************************************************        
041900 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
042100                                    DFHCOMMAREA                           
042200                                    DP030-CICS-API-FIELDS                 
042300                                    DP020-STANDARD-COMMAREA               
042400                                    IQ403AI.                              
042500*                                                                         
042600     IF  DP030-RC-CALL-SUCCESSFUL                                         
042700         CONTINUE                                                         
042800     ELSE                                                                 
042900         SET DP013-NO-ROLLBACK                                            
043000             DP013-XCTL-DISPLAY-RESTART                                   
043100             DP013-CICS-ABEND      TO TRUE                                
043200         MOVE '0001-CALL-CICS-ARCH-API'                                   
043300                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
043600         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
043700         PERFORM DP013-0000-PROCESS-ABEND                                 
043800     END-IF.                                                              
043900*----------------------------------------------------------------*        
044000* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
044100* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
044200*                                                                *        
044300* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
044400*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
044500*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
044600*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
044700*----------------------------------------------------------------*        
044800                                                                          
044900 1000-CONTROL-PROCESSING.                                                 
045000                                                                          
045100     EVALUATE TRUE                                                        
045200         WHEN DP020-NEXT-ACT-INITIAL                                      
045300             INITIALIZE ASC-SPECIFIC-COMMAREA                             
045400             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
045500                                      ASC-SEL-TERM-ID                     
045600             MOVE DP020-SRC-TASK-NUMBER                                   
045700                                   TO ASC-LIST-TASK-NUMBER                
045800                                      ASC-SEL-TASK-NUMBER                 
045900             MOVE PC-LIST-QUEUE-SEQ                                       
046000                                   TO ASC-LIST-SEQ                        
046100             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
046200             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
046300***-----$TEMPLATE NOTE$-------------------------------------------        
046400*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
046500*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
046600*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
046700***---------------------------------------------------------------        
046800             IF  PS-COMMAREA-NOT-VALID                                    
046900                 SET DP020-NEXT-ACT-APPL-ERROR                            
047000                                   TO TRUE                                
047100             ELSE                                                         
047200                 PERFORM 4000-BUILD-INITIAL-PANEL                         
047300             END-IF                                                       
047400                                                                          
047500         WHEN DP020-NEXT-ACT-READ-MAP                                     
047600             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
047700                                   TO PV-TOP-ITEM                         
047800             PERFORM 2000-PROCESS-PANEL                                   
047900                                                                          
048000         WHEN DP020-NEXT-ACT-RETURN                                       
048100             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
048200                                   TO PV-TOP-ITEM                         
048300             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
048400             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
048600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
048700                                                                          
048800         WHEN OTHER                                                       
048900             SET DP013-LOGIC-ABEND TO TRUE                                
049000             SET DP013-NO-ROLLBACK TO TRUE                                
049100             MOVE '1000-CONTROL-PROCESSING'                               
049200                                   TO DP013-PARAGRAPH                     
049300             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
049400                                   TO DP013-MESSAGE-TEXT(1)               
049500             PERFORM DP013-0000-PROCESS-ABEND                             
049600     END-EVALUATE.                                                        
049700                                                                          
049800 EJECT                                                                    
049900*----------------------------------------------------------------*        
050000* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
050100* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
050200*----------------------------------------------------------------*        
050300                                                                          
050400 1100-PROCESS-INTER-APPL-COMM.                                            
050500     SET PS-COMMAREA-VALID         TO TRUE.                               
050600     MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
050700     IF  DP020-INT-APPL-FORMAT-IND = PC-CRIT-SPEC-FORMAT                  
050800         IF IQ016-LBL-NM = SPACES                                         
050900            SET PS-BUSINESS-GROUP-ALL TO TRUE                             
051000            MOVE 'ALL' TO ASC-KEY-LBL-NM                                  
051100                          DK-LBL-NM                                       
051200         ELSE                                                             
051300            SET PS-BUSINESS-GROUP-PARTIAL TO TRUE                         
051400            MOVE IQ016-LBL-NM        TO ASC-KEY-LBL-NM                    
051500                                      DK-LBL-NM                           
051600         END-IF                                                           
051700     ELSE                                                                 
051800         IF  DP020-NAV-KEY-DATA > SPACE                                   
051900             MOVE DP020-NAV-KEY-DATA                                      
052000                                   TO DK-LBL-NM                           
052100                                      ASC-KEY-LBL-NM                      
052200         END-IF                                                           
052300     END-IF.                                                              
052400                                                                          
052500 EJECT                                                                    
052600*----------------------------------------------------------------*        
052700* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
052800* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
052900* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
053000* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
053100* FUNCTION KEYS CAN GET THIS FAR.                                *        
053200*----------------------------------------------------------------*        
053300                                                                          
053400 2000-PROCESS-PANEL.                                                      
053500     EVALUATE TRUE                                                        
053600         WHEN DP020-SRC-AID = DP016-CLEAR                                 
053700             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
053800             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
053900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
054000                                                                          
054100         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
054200             MOVE ASC-KEY-LBL-NM TO DK-LBL-NM                             
054300             PERFORM 2050-DELETE-LIST-QUEUE                               
054400             PERFORM 4000-BUILD-INITIAL-PANEL                             
054500                                                                          
054600         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
054700             PERFORM 2140-DESELECT-ALL-ITEMS                              
054800             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
054900                                                                          
055000         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
055100             PERFORM 2150-SELECT-ALL-ITEMS                                
055200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
055300                                                                          
055400         WHEN OTHER                                                       
055500             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
055600             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
055700             IF  PS-NO-ERROR                                              
055800                 SET DP030-CONTINUE-GO                                    
055900                                 TO TRUE                                  
056000                 PERFORM 2100-CHECK-FUNCTION-KEY                          
056100             ELSE                                                         
056200                 SET DP030-OVERRIDE-GO                                    
056300                                 TO TRUE                                  
056400             END-IF                                                       
056500     END-EVALUATE.                                                        
056600 EJECT                                                                    
056700*----------------------------------------------------------------*        
056800*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
056900*----------------------------------------------------------------*        
057000 2050-DELETE-LIST-QUEUE.                                                  
057100                                                                          
057200     EXEC CICS                                                            
057300         DELETEQ TS                                                       
057400             QUEUE (ASC-LIST-QUEUE-NAME)                                  
057500             RESP  (PV-CICS-RESP)                                         
057600     END-EXEC.                                                            
057700                                                                          
057800     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
057900        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
058000         CONTINUE                                                         
058100     ELSE                                                                 
058200         SET DP013-CICS-ABEND      TO TRUE                                
058300         SET DP013-NO-ROLLBACK     TO TRUE                                
058400                                                                          
058500         MOVE '2050-DELETE-LIST-QUEUE'                                    
058600                                   TO DP013-PARAGRAPH                     
058700         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
058800                                   TO DP013-MESSAGE-TEXT (1)              
058900         PERFORM DP013-0000-PROCESS-ABEND                                 
059000     END-IF.                                                              
059100                                                                          
059200     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
059300 EJECT                                                                    
059400*----------------------------------------------------------------*        
059500* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
059600* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
059700* CICS ARCHITECTURE API.                                         *        
059800*----------------------------------------------------------------*        
059900 2100-CHECK-FUNCTION-KEY.                                                 
060000*    LOCAL-01    DEPT LST    IQLDEPT                                      
060100     EVALUATE TRUE                                                        
060200         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
060300             IF  ASC-NUMBER-OF-SELECTED-ITEMS = ZERO                      
060400*                --  A LIST ITEM MUST BE SELECTED --                      
060500                 MOVE PC-TSYMSG-00057                                     
060600                                   TO DP020-MSG-NUMBER                    
060700                 SET DP020-MSG-FATAL                                      
060800                       DP030-OVERRIDE-GO                                  
060900                                   TO TRUE                                
061000             ELSE                                                         
061100               IF  ASC-NUMBER-OF-SELECTED-ITEMS > 1                       
061200*                --  ONLY ONE LIST ITEM MAY BE SELECTED --                
061300                   MOVE PC-TSYMSG-00047                                   
061400                                   TO DP020-MSG-NUMBER                    
061500                   SET DP020-MSG-FATAL                                    
061600                       DP030-OVERRIDE-GO                                  
061700                                   TO TRUE                                
061800               ELSE                                                       
061900                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
062000                                TO IQ016-NUMBER-OF-SELECTED-ITEMS         
062100                 PERFORM 5000-PREPARE-SEL-QUEUE                           
062200               END-IF                                                     
062300             END-IF                                                       
062400                                                                          
062500         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
062600             MOVE +1               TO PV-TOP-ITEM                         
062700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
062800                                                                          
062900         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
063000             PERFORM 2110-BROWSE-BACKWARD                                 
063100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
063200                                                                          
063300         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
063400             PERFORM 2120-BROWSE-FORWARD                                  
063500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
063600                                                                          
063700         WHEN DP020-SRC-AID = DP016-ENTER                                 
063800             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
063900             IF  PS-ERROR                                                 
064000                 CONTINUE                                                 
064100             ELSE                                                         
064200                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
064300             END-IF                                                       
064400                                                                          
064500         WHEN OTHER                                                       
064600             SET DP013-NO-ROLLBACK                                        
064700                 DP013-XCTL-DISPLAY-RESTART                               
064800                 DP013-CICS-ABEND  TO TRUE                                
064900             MOVE '2100-CHECK-FUNCTION-KEY'                               
065000                                   TO DP013-PARAGRAPH                     
065100             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
065200                                   TO DP013-MESSAGE-TEXT (1)              
065300             PERFORM DP013-0000-PROCESS-ABEND                             
065400     END-EVALUATE.                                                        
065500*                                                                         
065600*----------------------------------------------------------------*        
065700*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
065800*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
065900*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
066000*----------------------------------------------------------------*        
066100                                                                          
066200 2110-BROWSE-BACKWARD.                                                    
066300     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
066400         GIVING PV-TOP-ITEM.                                              
066500*                                                                         
066600     IF  ((PV-TOP-ITEM < +1)                                              
066700       OR (PV-TOP-ITEM = +1))                                             
066800         MOVE +1                   TO PV-TOP-ITEM                         
066900*        -- TOP OF LIST --                                                
067000         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
067100         SET DP020-MSG-INFORMATIONAL TO TRUE                              
067200     END-IF.                                                              
067300                                                                          
067400 EJECT                                                                    
067500*----------------------------------------------------------------*        
067600*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
067700*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
067800*    PAST THE END, ISSUE THE BOTTOM OF LIST.                     *        
067900*----------------------------------------------------------------*        
068000                                                                          
068100 2120-BROWSE-FORWARD.                                                     
068200                                                                          
068300     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
068400         GIVING PV-TOP-ITEM.                                              
068500     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
068600         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
068700                                   TO PV-TOP-ITEM                         
068800*        -- BOTTOM OF LIST --                                             
068900         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
069000         SET DP020-MSG-INFORMATIONAL                                      
069100                                   TO TRUE                                
069200     ELSE                                                                 
069300         COMPUTE PV-BOTTOM-ITEM =                                         
069400             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
069500         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
069600*            -- BOTTOM OF LIST --                                         
069700             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
069800             SET DP020-MSG-INFORMATIONAL                                  
069900                                   TO TRUE                                
070000         END-IF                                                           
070100     END-IF.                                                              
070200 EJECT                                                                    
070300*----------------------------------------------------------------*        
070400*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
070500*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
070600*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
070700*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
070800*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
070900*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
071000*----------------------------------------------------------------*        
071100                                                                          
071200 2130-CHECK-FOR-JUMP-BROWSE.                                              
071300     SET PS-NO-ERROR               TO TRUE.                               
071400     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
071500         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
071600                                   TO PV-TOP-ITEM                         
071700     ELSE                                                                 
071800         MOVE ASC-START-ITEM-REQUEST                                      
071900                                   TO DP010I-UNEDITED-FIELD               
072000         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
072100         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
072200         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
072300                                   TO TRUE                                
072410         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
072420                         USING DP010I-NUMERIC-EDIT-AREA                   
072500                                                                          
072600         IF  NOT DP010I-ERROR-DETECTED                                    
072700             MOVE DP010I-NUMERIC-FIELD                                    
072800                                   TO  ASC-START-ITEM-REQUEST-N           
072900             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
073000                 IF  ASC-START-ITEM-REQUEST-N >                           
073100                                          ASC-NUMBER-OF-ITEMS-READ        
073200                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
073300                     COMPUTE PV-TOP-ITEM =                                
073400                             ASC-NUMBER-OF-ITEMS-READ                     
073500                             - PC-ITEMS-PER-PANEL + 1                     
073600                     IF  PV-TOP-ITEM < +1                                 
073700                         MOVE +1   TO PV-TOP-ITEM                         
073800                         MOVE PC-TSYMSG-00070                             
073900                                   TO DP020-MSG-NUMBER                    
074000                         SET DP020-MSG-INFORMATIONAL                      
074100                                   TO TRUE                                
074200                     END-IF                                               
074300                 ELSE                                                     
074400                     MOVE ASC-START-ITEM-REQUEST-N                        
074500                                   TO PV-TOP-ITEM                         
074600                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
074700                     COMPUTE PV-BOTTOM-ITEM =                             
074800                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
074900                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
075000*                        -- BOTTOM OF LIST --                             
075100                         MOVE PC-TSYMSG-00070                             
075200                                   TO DP020-MSG-NUMBER                    
075300                         SET DP020-MSG-INFORMATIONAL                      
075400                                   TO TRUE                                
075500                     END-IF                                               
075600                 END-IF                                                   
075700             ELSE                                                         
075800*                --- VALUE OUT OF RANGE ----                              
075900                 MOVE PC-TSYMSG-00101                                     
076000                                   TO DP020-MSG-NUMBER                    
076100                 MOVE -1           TO ASTRTITL                            
076200                 SET DP030-SET-CURSOR-APPL-1                              
076300                                   TO TRUE                                
076400                 MOVE DP015-REVERSE                                       
076500                                   TO ASTRTITH                            
076600                 MOVE DP015-RED    TO ASTRTITC                            
076700                 MOVE DP015-UNP-NUM-BRT-MDT                               
076800                                   TO ASTRTITA                            
076900                 SET DP020-MSG-FATAL                                      
077000                                   TO TRUE                                
077100                 SET PS-ERROR      TO TRUE                                
077200             END-IF                                                       
077300                                                                          
077400         ELSE                                                             
077500             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
077600             MOVE -1               TO ASTRTITL                            
077700             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
077800             MOVE DP015-REVERSE    TO ASTRTITH                            
077900             MOVE DP015-RED        TO ASTRTITC                            
078000             MOVE DP015-UNP-NUM-BRT-MDT                                   
078100                                   TO ASTRTITA                            
078200             SET DP020-MSG-FATAL   TO TRUE                                
078300             SET PS-ERROR          TO TRUE                                
078400         END-IF                                                           
078500     END-IF.                                                              
078600 EJECT                                                                    
078700*----------------------------------------------------------------*        
078800*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
078900*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
079000*----------------------------------------------------------------*        
079100 2140-DESELECT-ALL-ITEMS.                                                 
079200     MOVE +1                       TO ASC-BLK-SUB.                        
079300                                                                          
079400     PERFORM 2141-CLEAR-SELECTED-INDS                                     
079500         VARYING ASC-ITEM-SUB                                             
079600         FROM 1 BY 1                                                      
079700         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
079800                                                                          
079900     ADD +1                        TO ASC-BLK-SUB.                        
080000                                                                          
080100     PERFORM 2141-CLEAR-SELECTED-INDS                                     
080200         VARYING ASC-ITEM-SUB                                             
080300         FROM 1 BY 1                                                      
080400         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
080500                                                                          
080600     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
080700                                   TO PV-LIST-ITEM-NUMBER.                
080800                                                                          
080900     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
081000         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
081100                                                                          
081200     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
081300        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
081400         MOVE +1                   TO ASC-BLK-SUB                         
081500                                                                          
081600         PERFORM 5100-WRITE-TEMP-STORAGE                                  
081700                                                                          
081800         MOVE +1                   TO PV-BLOCK-NUMBER                     
081900                                                                          
082000         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
082100             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
082200                                                                          
082300         PERFORM                                                          
082400             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
082500                                                                          
082600             PERFORM 4410-READ-TEMP-STORAGE                               
082700                                                                          
082800             PERFORM 2141-CLEAR-SELECTED-INDS                             
082900                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
083000                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
083100                                                                          
083200             PERFORM 5100-WRITE-TEMP-STORAGE                              
083300                                                                          
083400             ADD +1                TO PV-BLOCK-NUMBER                     
083500                                                                          
083600         END-PERFORM                                                      
083700                                                                          
083800         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
083900             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
084000                                                                          
084100         PERFORM                                                          
084200             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
084300                   ASC-HIGHEST-BLOCK-WRITTEN                              
084400                                                                          
084500             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
084600                                   TO PV-BLOCK-NUMBER                     
084700                                                                          
084800             PERFORM 4410-READ-TEMP-STORAGE                               
084900                                                                          
085000             PERFORM 2141-CLEAR-SELECTED-INDS                             
085100                 VARYING ASC-ITEM-SUB                                     
085200                 FROM 1 BY 1                                              
085300                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
085400                                                                          
085500             PERFORM 5100-WRITE-TEMP-STORAGE                              
085600                                                                          
085700             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
085800                                                                          
085900         END-PERFORM                                                      
086000                                                                          
086100         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
086200                                   TO PV-BLOCK-NUMBER                     
086300                                                                          
086400         PERFORM 4410-READ-TEMP-STORAGE                                   
086500     END-IF.                                                              
086600                                                                          
086700                                                                          
086800     MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
086900     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.        
087000     MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.          
087100                                                                          
087200     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
087300                                                                          
087400 EJECT                                                                    
087500*----------------------------------------------------------------*        
087600*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
087700*----------------------------------------------------------------*        
087800                                                                          
087900 2141-CLEAR-SELECTED-INDS.                                                
088000                                                                          
088100     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
088200             NOT EQUAL ZERO                                               
088300         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
088400             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
088500                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
088600                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
088700                                   TO TRUE                                
088800         END-IF                                                           
088900     END-IF.                                                              
089000                                                                          
089100 EJECT                                                                    
089200*----------------------------------------------------------------*        
089300*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
089400*  FOR ALL ITEMS.                                                *        
089500*----------------------------------------------------------------*        
089600                                                                          
089700 2150-SELECT-ALL-ITEMS.                                                   
089800                                                                          
089900     MOVE +1                       TO ASC-BLK-SUB.                        
090000                                                                          
090100     PERFORM 2151-SET-SELECTED-INDS                                       
090200         VARYING ASC-ITEM-SUB                                             
090300         FROM 1 BY 1                                                      
090400         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
090500                                                                          
090600     ADD +1                        TO ASC-BLK-SUB.                        
090700                                                                          
090800     PERFORM 2151-SET-SELECTED-INDS                                       
090900         VARYING ASC-ITEM-SUB                                             
091000         FROM 1 BY 1                                                      
091100         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
091200                                                                          
091300     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
091400                                   TO PV-LIST-ITEM-NUMBER.                
091500                                                                          
091600     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
091700         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
091800                                                                          
091900     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
092000        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
092100         MOVE +1                   TO ASC-BLK-SUB                         
092200                                                                          
092300         PERFORM 5100-WRITE-TEMP-STORAGE                                  
092400                                                                          
092500         MOVE +1                   TO PV-BLOCK-NUMBER                     
092600                                                                          
092700         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
092800             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
092900                                                                          
093000         PERFORM                                                          
093100             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
093200                                                                          
093300             PERFORM 4410-READ-TEMP-STORAGE                               
093400                                                                          
093500             PERFORM 2151-SET-SELECTED-INDS                               
093600                 VARYING ASC-ITEM-SUB                                     
093700                 FROM 1 BY 1                                              
093800                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
093900                                                                          
094000             PERFORM 5100-WRITE-TEMP-STORAGE                              
094100                                                                          
094200             ADD +1                TO PV-BLOCK-NUMBER                     
094300                                                                          
094400         END-PERFORM                                                      
094500                                                                          
094600         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
094700             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
094800                                                                          
094900         PERFORM                                                          
095000             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
095100                   ASC-HIGHEST-BLOCK-WRITTEN                              
095200                                                                          
095300             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
095400                                   TO PV-BLOCK-NUMBER                     
095500                                                                          
095600             PERFORM 4410-READ-TEMP-STORAGE                               
095700                                                                          
095800             PERFORM 2151-SET-SELECTED-INDS                               
095900                 VARYING ASC-ITEM-SUB                                     
096000                 FROM 1 BY 1                                              
096100                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
096200                                                                          
096300             PERFORM 5100-WRITE-TEMP-STORAGE                              
096400                                                                          
096500             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
096600                                                                          
096700         END-PERFORM                                                      
096800                                                                          
096900         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
097000                                   TO PV-BLOCK-NUMBER                     
097100                                                                          
097200         PERFORM 4410-READ-TEMP-STORAGE                                   
097300     END-IF.                                                              
097400                                                                          
097500                                                                          
097600     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
097700                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
097800                                                                          
097900     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
098000                                                                          
098100 EJECT                                                                    
098200*----------------------------------------------------------------*        
098300*    MARK EACH ITEM AS BEING SELECTED.                           *        
098400*----------------------------------------------------------------*        
098500                                                                          
098600 2151-SET-SELECTED-INDS.                                                  
098700                                                                          
098800     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
098900             NOT EQUAL ZERO                                               
099000         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
099100             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
099200                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
099300                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
099400                               TO  TRUE                                   
099500         END-IF                                                           
099600     END-IF.                                                              
099700******************************************************************        
099800** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
099900** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
100000******************************************************************        
100100 2200-MOVE-SCREEN-TO-COMMAREA.                                            
100200                                                                          
100300     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
100400     MOVE +1                       TO PV-SUB.                             
100500*                                                                         
100600     PERFORM 3100-SET-SUBSCRIPTS.                                         
100700     IF  ASTRTITL > ZERO                                                  
100800         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
100900     ELSE                                                                 
101000         IF  ASTRTITF = DP015-ERASE-EOF                                   
101100             INITIALIZE ASC-START-ITEM-REQUEST                            
101200         END-IF                                                           
101300     END-IF.                                                              
101400     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
101500         VARYING PV-SUB                                                   
101600         FROM 1 BY 1                                                      
101700         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
101800******************************************************************        
101900** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
102000******************************************************************        
102100 2210-MOVE-LINES-TO-COMMAREA.                                             
102200     IF  MR-SELECTL (PV-SUB) > ZERO                                       
102300         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
102400         MOVE MR-SELECTI (PV-SUB)                                         
102500                                   TO ASC-TEMP-SELECTED-SW                
102600                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
102700     ELSE                                                                 
102800         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
102900                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
103000                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
103100                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
103200         END-IF                                                           
103300     END-IF.                                                              
103400     ADD +1                        TO ASC-ITEM-SUB.                       
103500     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
103600         ADD +1                    TO ASC-BLK-SUB                         
103700         MOVE +1                   TO ASC-ITEM-SUB                        
103800     END-IF.                                                              
103900******************************************************************        
104000** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
104100** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
104200******************************************************************        
104300 3000-EDIT-DATA-IN-COMMAREA.                                              
104400     SET PS-NO-ERROR               TO TRUE.                               
104500     PERFORM 3200-RESET-ATTRIBUTES.                                       
104600     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
104700**                                                                        
104800* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
104900* EDITTING IS DONE FROM THE BOTTOM UP.                                    
105000**                                                                        
105100     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
105200         ASC-ITEMS-DISPLAYED - 1.                                         
105300     PERFORM 3100-SET-SUBSCRIPTS.                                         
105400                                                                          
105500     PERFORM 3300-EDIT-SELECTIONS                                         
105600         VARYING PV-SUB                                                   
105700         FROM PV-SUB BY -1                                                
105800         UNTIL PV-SUB < +1                                                
105900                                                                          
106000     IF  PS-NO-ERROR                                                      
106100         PERFORM 3400-CHECK-RANGE-SEQUENCE                                
106200     END-IF.                                                              
106300                                                                          
106400     IF  ((PS-NO-ERROR)                                                   
106500       AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
106600       AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
106700             PERFORM 3500-MARK-RANGE-AS-SELECTED                          
106800     END-IF.                                                              
106900 EJECT                                                                    
107000*----------------------------------------------------------------*        
107100*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
107200*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
107300*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
107400*                                                                *        
107500*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
107600*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
107700*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
107800*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
107900*----------------------------------------------------------------*        
108000                                                                          
108100 3100-SET-SUBSCRIPTS.                                                     
108200                                                                          
108300     COMPUTE ASC-ITEM-SUB =                                               
108400         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
108500                                                                          
108600     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
108700         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
108800                                                                          
108900         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
109000     ELSE                                                                 
109100         MOVE +1                     TO ASC-BLK-SUB                       
109200     END-IF.                                                              
109300******************************************************************        
109400** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
109500******************************************************************        
109600                                                                          
109700 3200-RESET-ATTRIBUTES.                                                   
109800     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
109900     MOVE DP015-GREEN              TO ASTRTITC.                           
110000     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
110100*                                                                         
110200     PERFORM                                                              
110300         VARYING PV-SUB                                                   
110400         FROM 1 BY 1                                                      
110500         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
110600         MOVE DP015-UNP-ALP-NOR-OFF                                       
110700                                   TO MR-SELECTA (PV-SUB)                 
110800         MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
110900         MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
111000     END-PERFORM.                                                         
111100******************************************************************        
111200** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
111300** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
111400** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
111500** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
111600******************************************************************        
111700 3300-EDIT-SELECTIONS.                                                    
111800     EVALUATE TRUE                                                        
111900         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
112000             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
112100                 CONTINUE                                                 
112200         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
112300             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
112400                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
112500                 MOVE ASC-TEMP-SELECTED-SW                                
112600                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
112700                                TO ASC-SELECTED-SW                        
112800                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
112900             END-IF                                                       
113000         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
113100                 = SPACE                                                  
113200             EVALUATE TRUE                                                
113300                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
113400                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
113500                     MOVE SPACE TO ASC-SELECTED-SW                        
113600                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
113700                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
113800                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
113900                     MOVE SPACE TO ASC-SELECTED-SW                        
114000                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
114100                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
114200                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
114300                     MOVE SPACE TO ASC-SELECTED-SW                        
114400                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
114500             END-EVALUATE                                                 
114600         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
114700             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
114800                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
114900             END-IF                                                       
115000             PERFORM 3310-EDIT-RANGE                                      
115100         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
115200             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
115300                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
115400             END-IF                                                       
115500             PERFORM 3310-EDIT-RANGE                                      
115600         WHEN OTHER                                                       
115700             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
115800             SET DP020-MSG-FATAL    TO TRUE                               
115900             MOVE -1                TO MR-SELECTL (PV-SUB)                
116000             SET DP030-SET-CURSOR-APPL-1                                  
116100                                    TO TRUE                               
116200             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
116300             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
116400             SET PS-ERROR TO TRUE                                         
116500             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
116600                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
116700                 MOVE SPACE         TO ASC-SELECTED-SW                    
116800                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
116900             END-IF                                                       
117000     END-EVALUATE.                                                        
117100                                                                          
117200     SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
117300     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
117400     IF  ASC-ITEM-SUB < 1                                                 
117500         SUBTRACT +1 FROM ASC-BLK-SUB                                     
117600         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
117700     END-IF.                                                              
117800******************************************************************        
117900** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
118000** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
118100** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
118200** THE ENDING.                                                  **        
118300******************************************************************        
118400 3310-EDIT-RANGE.                                                         
118500     IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
118600         IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
118700             MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
118800                                   TO ASC-SELECTED-SW                     
118900                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
119000             MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
119100         ELSE                                                             
119200             IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
119300                 CONTINUE                                                 
119400             ELSE                                                         
119500*                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
119600                 MOVE PC-TSYMSG-00054                                     
119700                                   TO DP020-MSG-NUMBER                    
119800                 SET DP020-MSG-FATAL                                      
119900                                   TO TRUE                                
120000                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
120100                 SET DP030-SET-CURSOR-APPL-1                              
120200                                   TO TRUE                                
120300                 MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
120400                 MOVE DP015-UNDERLINE                                     
120500                                   TO MR-SELECTH (PV-SUB)                 
120600                 SET PS-ERROR      TO TRUE                                
120700             END-IF                                                       
120800         END-IF                                                           
120900     ELSE                                                                 
121000         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
121100             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
121200                                   TO ASC-SELECTED-SW                     
121300                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
121400             MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
121500         ELSE                                                             
121600             IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
121700                 CONTINUE                                                 
121800             ELSE                                                         
121900*                -- ONLY ONE END-OF-RANGE ALLOWED --                      
122000                 MOVE PC-TSYMSG-00055                                     
122100                                   TO DP020-MSG-NUMBER                    
122200                 SET DP020-MSG-FATAL                                      
122300                                   TO TRUE                                
122400                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
122500                 SET DP030-SET-CURSOR-APPL-1                              
122600                                   TO TRUE                                
122700                 SET PS-ERROR      TO TRUE                                
122800             END-IF                                                       
122900         END-IF                                                           
123000     END-IF.                                                              
123100*                                                                         
123200******************************************************************        
123300** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
123400** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
123500******************************************************************        
123600 3400-CHECK-RANGE-SEQUENCE.                                               
123700     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
123800       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
123900       AND  ASC-START-OF-SELECTED-RANGE >                                 
124000                                         ASC-END-OF-SELECTED-RANGE        
124100*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
124200         MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
124300         SET DP020-MSG-FATAL TO TRUE                                      
124400         SET PS-ERROR TO TRUE                                             
124500         PERFORM 3410-POSITION-AT-ERROR                                   
124600     END-IF.                                                              
124700******************************************************************        
124800** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
124900** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
125000** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
125100******************************************************************        
125200 3410-POSITION-AT-ERROR.                                                  
125300     SET DP030-SET-CURSOR-APPL-1                                          
125400                                   TO TRUE.                               
125500     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
125600                    - PV-TOP-ITEM + 1.                                    
125700     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
125800         CONTINUE                                                         
125900     ELSE                                                                 
126000         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
126100         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
126200         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
126300     END-IF.                                                              
126400*                                                                         
126500     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
126600                    - PV-TOP-ITEM + 1.                                    
126700     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
126800         CONTINUE                                                         
126900     ELSE                                                                 
127000         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
127100         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
127200         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
127300     END-IF.                                                              
127400 EJECT                                                                    
127500*----------------------------------------------------------------*        
127600*    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
127700*    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
127800*    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
127900*----------------------------------------------------------------*        
128000                                                                          
128100 3500-MARK-RANGE-AS-SELECTED.                                             
128200     MOVE +1                       TO  ASC-BLK-SUB.                       
128300     PERFORM 3510-MARK-SELECTED-INDS                                      
128400         VARYING ASC-ITEM-SUB                                             
128500         FROM 1 BY 1                                                      
128600         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
128700                                                                          
128800     ADD +1                        TO ASC-BLK-SUB.                        
128900     PERFORM 3510-MARK-SELECTED-INDS                                      
129000         VARYING ASC-ITEM-SUB                                             
129100         FROM 1 BY 1                                                      
129200         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
129300                                                                          
129400     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
129500                                   TO  PV-LIST-ITEM-NUMBER.               
129600     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
129700         1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
129800               PC-ITEMS-PER-PANEL).                                       
129900                                                                          
130000     MOVE ASC-START-OF-SELECTED-RANGE                                     
130100                                   TO PV-START-OF-SELECTED-RANGE.         
130200     COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
130300         1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
130400               PC-ITEMS-PER-PANEL).                                       
130500                                                                          
130600     MOVE ASC-END-OF-SELECTED-RANGE                                       
130700                                   TO  PV-END-OF-SELECTED-RANGE.          
130800     COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
130900         1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
131000               PC-ITEMS-PER-PANEL).                                       
131100     COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
131200         PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
131300                                                                          
131400     IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
131500        (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
131600         MOVE +1                   TO ASC-BLK-SUB                         
131700         PERFORM 5100-WRITE-TEMP-STORAGE                                  
131800         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
131900             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
132000                                                                          
132100         PERFORM                                                          
132200             UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
132300                   PV-PREV-BLOCK-IN-ARRAY                                 
132400             MOVE PV-FIRST-BLOCK-IN-RANGE                                 
132500                                   TO PV-BLOCK-NUMBER                     
132600             PERFORM 4410-READ-TEMP-STORAGE                               
132700             PERFORM 3510-MARK-SELECTED-INDS                              
132800                 VARYING ASC-ITEM-SUB                                     
132900                 FROM 1 BY 1                                              
133000                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
133100             PERFORM 5100-WRITE-TEMP-STORAGE                              
133200             ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
133300         END-PERFORM                                                      
133400                                                                          
133500         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
133600             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
133700         PERFORM                                                          
133800             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
133900                   PV-LAST-BLOCK-IN-RANGE                                 
134000             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
134100                                   TO PV-BLOCK-NUMBER                     
134200             PERFORM 4410-READ-TEMP-STORAGE                               
134300             PERFORM 3510-MARK-SELECTED-INDS                              
134400                 VARYING ASC-ITEM-SUB                                     
134500                 FROM 1 BY 1                                              
134600                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
134700             PERFORM 5100-WRITE-TEMP-STORAGE                              
134800             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
134900         END-PERFORM                                                      
135000                                                                          
135100         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
135200                                   TO PV-BLOCK-NUMBER                     
135300         PERFORM 4410-READ-TEMP-STORAGE                                   
135400     END-IF.                                                              
135500                                                                          
135600     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
135700                                      ASC-END-OF-SELECTED-RANGE.          
135800                                                                          
135900 EJECT                                                                    
136000*----------------------------------------------------------------*        
136100* SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
136200*----------------------------------------------------------------*        
136300                                                                          
136400 3510-MARK-SELECTED-INDS.                                                 
136500                                                                          
136600     IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
136700                                ASC-ITEM-SUB) >                           
136800         ASC-START-OF-SELECTED-RANGE)                                     
136900     OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
137000                                ASC-ITEM-SUB) =                           
137100         ASC-START-OF-SELECTED-RANGE)                                     
137200                                                                          
137300         IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
137400                                    ASC-ITEM-SUB) <                       
137500             ASC-END-OF-SELECTED-RANGE)                                   
137600         OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
137700                                    ASC-ITEM-SUB) =                       
137800             ASC-END-OF-SELECTED-RANGE)                                   
137900                                                                          
138000             IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
138100                 SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
138200                     ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
138300                               TO TRUE                                    
138400                 SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
138500                               TO TRUE                                    
138600                 ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
138700             END-IF                                                       
138800         END-IF                                                           
138900     END-IF.                                                              
139000                                                                          
139100 EJECT                                                                    
139200*----------------------------------------------------------------*        
139300*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
139400*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
139500*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
139600*    DISPLAY THE PANEL TO USER.                                  *        
139700*----------------------------------------------------------------*        
139800                                                                          
139900 4000-BUILD-INITIAL-PANEL.                                                
140000                                                                          
140100     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
140200                                 ASC-BLOCK-ENTRIES (2).                   
140300     MOVE +1                 TO ASC-BLK-SUB.                              
140400     SET ASC-ITEMS-LEFT-ON-DB                                             
140500                             TO TRUE.                                     
140600                                                                          
140700     MOVE +0                 TO ASC-ITEM-SUB                              
140800                                ASC-NUMBER-OF-ITEMS-READ                  
140900                                ASC-NUMBER-OF-SELECTED-ITEMS              
141000                                ASC-HIGHEST-BLOCK-WRITTEN                 
141100                                ASC-START-OF-SELECTED-RANGE               
141200                                ASC-END-OF-SELECTED-RANGE                 
141300                                ASC-ITEM-AT-TOP-OF-PANEL.                 
141310     IF IQ016-LBL-NM = SPACES                                             
141320        SET PS-BUSINESS-GROUP-ALL TO TRUE                                 
141330        MOVE 'ALL' TO ASC-KEY-LBL-NM                                      
141340                      DK-LBL-NM                                           
141341        MOVE 'BUS-GRP-ALL' TO PV-CURSOR-NAME                              
141350     ELSE                                                                 
141360        SET PS-BUSINESS-GROUP-PARTIAL TO TRUE                             
141370        MOVE IQ016-LBL-NM        TO ASC-KEY-LBL-NM                        
141380                                  DK-LBL-NM                               
141381        MOVE 'BUS-GRP-CSR' TO PV-CURSOR-NAME                              
141390     END-IF.                                                              
141400                                                                          
141500     PERFORM 4010-OPEN-CURSOR.                                            
141600                                                                          
141700     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
141800                                                                          
141900     PERFORM 4020-CLOSE-CURSOR.                                           
142000                                                                          
142100     MOVE +1                   TO  PV-TOP-ITEM.                           
142200     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
142300         SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
142400         SET DP020-MSG-FATAL   TO  TRUE                                   
142500         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
142600     ELSE                                                                 
142700         MOVE -1               TO MR-SELECTL (1)                          
142800         SET DP030-SET-CURSOR-APPL-1                                      
142900                               TO TRUE                                    
143000         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
143100     END-IF.                                                              
143200 EJECT                                                                    
143300*----------------------------------------------------------------*        
143400* OPEN ONE OF THE CURSORS.                                      *         
143500*----------------------------------------------------------------*        
143600                                                                          
143700 4010-OPEN-CURSOR.                                                        
143710                                                                          
143711     MOVE '4010-OPEN-CURSOR'   TO DP013-PARAGRAPH.                        
143720                                                                          
143800     PERFORM                                                              
143900         WITH TEST AFTER                                                  
144000         VARYING PV-RETRY-COUNTER                                         
144100         FROM 1 BY 1                                                      
144200         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
144300                   OR (SQLCODE = ZERO))                                   
144400                                                                          
144500         EVALUATE TRUE                                                    
144600             WHEN PS-BUSINESS-GROUP-ALL                                   
144700                  EXEC SQL                                                
144800                       OPEN BUS-GRP-ALL                                   
144900                  END-EXEC                                                
145000                                                                          
145100             WHEN PS-BUSINESS-GROUP-PARTIAL                               
145200                  EXEC SQL                                                
145300                       OPEN BUS-GRP-CSR                                   
145400                  END-EXEC                                                
145500                                                                          
145600         END-EVALUATE                                                     
145700                                                                          
145800         EVALUATE TRUE                                                    
145900             WHEN SQLCODE = ZERO                                          
146000             WHEN SQLCODE = -904                                          
146100             WHEN SQLCODE = -913                                          
146200                 CONTINUE                                                 
146300             WHEN SQLWARN0 NOT = SPACES                                   
146400             WHEN SQLCODE < ZERO                                          
146500             WHEN SQLCODE > ZERO                                          
146810                 STRING 'OPEN '                                           
146820                         PV-CURSOR-NAME                                   
146830                        ' CURSOR'                                         
146840                         DELIMITED BY SIZE                                
146900                         INTO DP013-MESSAGE-TEXT (1)                      
147000                 MOVE SQLCA        TO DP013-SQLCA                         
147100                 MOVE 'BUS GP, DEPT'                                      
147200                                   TO DP013-DB2-TABLE-NAME (1)            
147300                 MOVE 'VND, VND_STYL'                                     
147400                                   TO DP013-DB2-TABLE-NAME (2)            
147500                 SET DP013-DB2-ABEND                                      
147600                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
147700                 PERFORM DP013-0000-PROCESS-ABEND                         
147800         END-EVALUATE                                                     
147900     END-PERFORM.                                                         
148000*                                                                         
148100     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
148410         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN '            
148420                 PV-CURSOR-NAME                                           
148430                ' CURSOR'                                                 
148440                 DELIMITED BY SIZE                                        
148450                 INTO DP013-MESSAGE-TEXT (1)                              
148500         MOVE SQLCA                TO DP013-SQLCA                         
148600         MOVE 'BUS GP, DEPT ' TO DP013-DB2-TABLE-NAME (1)                 
148700         MOVE 'VND, VND_STYL' TO DP013-DB2-TABLE-NAME (2)                 
148800         SET DP013-DB2-ABEND                                              
148900             DP013-XCTL-DISPLAY-RESTART                                   
149000                                   TO TRUE                                
149100         PERFORM DP013-0000-PROCESS-ABEND                                 
149200     END-IF.                                                              
149300                                                                          
149400 EJECT                                                                    
149500*----------------------------------------------------------------*        
149600* CLOSE ONE OF THE CURSORS.                                      *        
149700*----------------------------------------------------------------*        
149800                                                                          
149900 4020-CLOSE-CURSOR.                                                       
150000                                                                          
150100     EVALUATE TRUE                                                        
150200         WHEN PS-BUSINESS-GROUP-ALL                                       
150300              EXEC SQL                                                    
150400                   CLOSE BUS-GRP-ALL                                      
150500              END-EXEC                                                    
150600                                                                          
150700         WHEN PS-BUSINESS-GROUP-PARTIAL                                   
150800              EXEC SQL                                                    
150900                   CLOSE BUS-GRP-CSR                                      
151000              END-EXEC                                                    
151100                                                                          
151200     END-EVALUATE.                                                        
151300                                                                          
151400     EVALUATE TRUE                                                        
151500         WHEN SQLCODE = ZERO                                              
151600             CONTINUE                                                     
151700         WHEN SQLWARN0 NOT = SPACES                                       
151800         WHEN SQLCODE < ZERO                                              
151900         WHEN SQLCODE > ZERO                                              
152000             MOVE '4020-CLOSE-CURSOR'                                     
152100                               TO DP013-PARAGRAPH                         
152210             STRING 'CLOSE '                                              
152220                     PV-CURSOR-NAME                                       
152230                    ' CURSOR'                                             
152240                     DELIMITED BY SIZE                                    
152250                     INTO DP013-MESSAGE-TEXT (1)                          
152400             MOVE SQLCA        TO DP013-SQLCA                             
152500             MOVE 'BUS GP, DEPT'                                          
152600                               TO DP013-DB2-TABLE-NAME (1)                
152700             MOVE 'VND, VND_STYL'                                         
152800                               TO DP013-DB2-TABLE-NAME (2)                
152900             SET DP013-DB2-ABEND                                          
153000                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
153100             PERFORM DP013-0000-PROCESS-ABEND                             
153200     END-EVALUATE.                                                        
153300                                                                          
153400*----------------------------------------------------------------*        
153500*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
153600*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
153700*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
153800*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
153900*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
154000*                                                                *        
154100*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
154200*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
154300*    UPON INITIALIZATION.                                        *        
154400*----------------------------------------------------------------*        
154500                                                                          
154600 4300-READ-ITEMS-FROM-DB.                                                 
154700                                                                          
154800     PERFORM                                                              
154900         VARYING PV-READ-COUNT                                            
155000         FROM 1 BY 1                                                      
155100         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
155200            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
155300                                                                          
155310         PERFORM 4310-FETCH                                               
156200                                                                          
156300         IF  ASC-ITEMS-LEFT-ON-DB                                         
156400             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
156500                 ADD +1            TO ASC-ITEM-SUB                        
156600             ELSE                                                         
156700                 ADD +1            TO ASC-BLK-SUB                         
156800                 MOVE +1           TO ASC-ITEM-SUB                        
156900                                                                          
157000                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
157100                     MOVE +1       TO ASC-BLK-SUB                         
157200                     SET ASC-BLOCK-MODIFIED (1)                           
157300                                   TO TRUE                                
157400                     PERFORM 5100-WRITE-TEMP-STORAGE                      
157500                     MOVE ASC-BLOCK-ENTRIES                               
157600                               (PC-MAX-BLOCKS-IN-ARRAY)                   
157700                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
157800                     ADD +1        TO ASC-BLK-SUB                         
157900                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
158000                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
158100                                   TO TRUE                                
158200                 END-IF                                                   
158300             END-IF                                                       
158400             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
158500             MOVE ASC-NUMBER-OF-ITEMS-READ                                
158600                                   TO ASC-LIST-ITEM-NUMBER                
158700                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
158800             MOVE XST00016-BUS-GP-NBR  TO ASC-GRP-NBR                     
158900                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
159000             MOVE XST00016-BUS-GP-DESC TO ASC-GRP-DES                     
159100                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
159200         END-IF                                                           
159300     END-PERFORM.                                                         
159400                                                                          
159500     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
159600         MOVE +1               TO ASC-BLK-SUB                             
159700         PERFORM 5100-WRITE-TEMP-STORAGE                                  
159800         ADD +1                TO ASC-BLK-SUB                             
159900         PERFORM 5100-WRITE-TEMP-STORAGE                                  
160000                                                                          
160100         MOVE 1                TO ASC-BLK-SUB                             
160200                                  PV-BLOCK-NUMBER                         
160300         PERFORM 4410-READ-TEMP-STORAGE                                   
160400         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
160500             MOVE 2            TO ASC-BLK-SUB                             
160600                                  PV-BLOCK-NUMBER                         
160700             PERFORM 4410-READ-TEMP-STORAGE                               
160800         END-IF                                                           
160900     END-IF.                                                              
161000 EJECT                                                                    
161100*----------------------------------------------------------------*        
161200*    FETCH THE DATA TO DISPLAY USING ONE OF THE CURSORS.         *        
161300*----------------------------------------------------------------*        
161400                                                                          
161500 4310-FETCH.                                                              
161600                                                                          
161700     MOVE '4310-FETCH       '  TO DP013-PARAGRAPH.                        
162100                                                                          
162101     PERFORM                                                              
162102         WITH TEST AFTER                                                  
162103         VARYING PV-RETRY-COUNTER                                         
162104         FROM 1 BY 1                                                      
162105         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
162106                   OR  SQLCODE = ZERO                                     
162107                   OR  SQLCODE = +100)                                    
162110         EVALUATE TRUE                                                    
162120             WHEN PS-BUSINESS-GROUP-ALL                                   
162131                  EXEC SQL                                                
162132                       FETCH BUS-GRP-ALL                                  
162133                             INTO                                         
162134                              :XST00016-BUS-GP-NBR,                       
162135                              :XST00016-BUS-GP-DESC                       
162136                                                                          
162137                  END-EXEC                                                
162140                                                                          
162150             WHEN PS-BUSINESS-GROUP-PARTIAL                               
162200                  EXEC SQL                                                
162300                       FETCH BUS-GRP-CSR                                  
162400                             INTO                                         
162500                              :XST00016-BUS-GP-NBR,                       
162600                              :XST00016-BUS-GP-DESC                       
162700                  END-EXEC                                                
162710                                                                          
162720         END-EVALUATE                                                     
162800                                                                          
162900         EVALUATE TRUE                                                    
163000             WHEN SQLCODE = ZERO                                          
163100             WHEN SQLCODE = +100                                          
163200                  CONTINUE                                                
163300             WHEN SQLWARN0 NOT = SPACE                                    
163400             WHEN SQLCODE NOT = ZERO                                      
163810                  STRING 'FETCH '                                         
163820                          PV-CURSOR-NAME                                  
163830                         ' CURSOR'                                        
163840                          DELIMITED BY SIZE                               
163850                          INTO DP013-MESSAGE-TEXT (1)                     
163900                  MOVE SQLCA       TO DP013-SQLCA                         
164000                  SET DP013-DB2-ABEND                                     
164100                                   TO TRUE                                
164200                  PERFORM DP013-0000-PROCESS-ABEND                        
164300         END-EVALUATE                                                     
164310                                                                          
164320     END-PERFORM.                                                         
164330*                                                                         
164340     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
164371         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO FETCH '           
164372                 PV-CURSOR-NAME                                           
164373                ' CURSOR'                                                 
164374                 DELIMITED BY SIZE                                        
164375                 INTO DP013-MESSAGE-TEXT (1)                              
164380         MOVE SQLCA                TO DP013-SQLCA                         
164381         MOVE 'BUS GP, DEPT ' TO DP013-DB2-TABLE-NAME (1)                 
164382         MOVE 'VND, VND_STYL' TO DP013-DB2-TABLE-NAME (2)                 
164391         SET DP013-DB2-ABEND                                              
164392             DP013-XCTL-DISPLAY-RESTART                                   
164393                                   TO TRUE                                
164394         PERFORM DP013-0000-PROCESS-ABEND                                 
164395     END-IF.                                                              
164396                                                                          
164500     IF  SQLCODE EQUAL +100                                               
164600         SET ASC-NO-ITEMS-LEFT-ON-DB                                      
164700                                   TO TRUE                                
164800     END-IF.                                                              
164900                                                                          
168500                                                                          
168600*----------------------------------------------------------------*        
168700* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
168800*----------------------------------------------------------------*        
168900                                                                          
169000 4400-MOVE-COMMAREA-TO-SCREEN.                                            
169100     MOVE ASC-KEY-LBL-NM      TO AVENDNMO.                                
169200                                                                          
169300     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
169400         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
169500         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
169600             MOVE +1    TO ASC-BLK-SUB                                    
169700             PERFORM 5100-WRITE-TEMP-STORAGE                              
169800             ADD +1     TO ASC-BLK-SUB                                    
169900             PERFORM 5100-WRITE-TEMP-STORAGE                              
170000             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
170100                 GIVING PV-BLOCK-NUMBER                                   
170200                 REMAINDER PV-REMAINDER                                   
170300             IF  PV-REMAINDER > ZERO                                      
170400                 ADD +1 TO PV-BLOCK-NUMBER                                
170500             END-IF                                                       
170600             MOVE +1    TO ASC-BLK-SUB                                    
170700             PERFORM 4410-READ-TEMP-STORAGE                               
170800             ADD +1     TO ASC-BLK-SUB                                    
170900                           PV-BLOCK-NUMBER                                
171000             PERFORM 4410-READ-TEMP-STORAGE                               
171100     END-IF.                                                              
171200     MOVE +1            TO ASC-BLK-SUB.                                   
171300     COMPUTE ASC-ITEM-SUB =                                               
171400         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
171500         + PC-ITEMS-PER-PANEL).                                           
171600*                                                                         
171700     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
171800         ADD +1         TO ASC-BLK-SUB                                    
171900         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
172000     END-IF.                                                              
172100*                                                                         
172200     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
172300         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
172400                                                                          
172500     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
172600     PERFORM 4420-FORMAT-LIST-LINES                                       
172700         VARYING PV-SUB                                                   
172800         FROM PC-ITEMS-PER-PANEL BY -1                                    
172900         UNTIL PV-SUB < +1.                                               
173000*                                                                         
173100     COMPUTE ASC-ITEMS-DISPLAYED =                                        
173200         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
173300     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
173400                                      ASTRTITO.                           
173500     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
173600     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
173700*                                                                         
173800     IF  DP020-MSG-NUMBER-X = SPACE                                       
173900         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
174000*            --- MORE ITEMS TO DISPLAY ----                               
174100             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
174200             SET DP020-MSG-INFORMATIONAL TO TRUE                          
174300         END-IF                                                           
174400     END-IF.                                                              
174500*----------------------------------------------------------------*        
174600*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
174700*----------------------------------------------------------------*        
174800                                                                          
174900 4410-READ-TEMP-STORAGE.                                                  
175000                                                                          
175100     EXEC CICS                                                            
175200         READQ TS                                                         
175300             QUEUE (ASC-LIST-QUEUE-NAME)                                  
175400             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
175500             ITEM  (PV-BLOCK-NUMBER)                                      
175600             RESP  (PV-CICS-RESP)                                         
175700     END-EXEC.                                                            
175800                                                                          
175900     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
176000       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
176100         CONTINUE                                                         
176200     ELSE                                                                 
176300         SET DP013-CICS-ABEND     TO TRUE                                 
176400         SET DP013-NO-ROLLBACK    TO TRUE                                 
176500         MOVE '4410-READ-TEMP-STORAGE'                                    
176600                                  TO DP013-PARAGRAPH                      
176700         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
176800                                  TO DP013-MESSAGE-TEXT(1)                
176900         PERFORM DP013-0000-PROCESS-ABEND                                 
177000     END-IF.                                                              
177100                                                                          
177200     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
177300*                                                                         
177400     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
177500         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
177600     END-IF.                                                              
177700 EJECT                                                                    
177800*----------------------------------------------------------------*        
177900*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
178000*----------------------------------------------------------------*        
178100                                                                          
178200 4420-FORMAT-LIST-LINES.                                                  
178300     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
178400         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
178500         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
178600         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
178700         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
178800                                    MR-GRP-NBR (PV-SUB)                   
178900                                    MR-GRP-DES (PV-SUB)                   
179000     ELSE                                                                 
179100         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
179200                                                   > SPACE                
179300             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
179400                                 TO MR-SELECTI (PV-SUB)                   
179500         ELSE                                                             
179600             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
179700                                 TO MR-SELECTI (PV-SUB)                   
179800         END-IF                                                           
179900*                                                                         
180000         IF  DP030-SET-CURSOR-TRAILER                                     
180100             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
180200                 > SPACE)                                                 
180300               OR (PV-SUB = 1))                                           
180400                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
180500                MOVE -1          TO MR-SELECTL (PV-SUB)                   
180600             END-IF                                                       
180700         END-IF                                                           
180800*                                                                         
180900         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
181000                                                                          
181100             MOVE ASC-GRP-NBR (ASC-BLK-SUB ASC-ITEM-SUB)                  
181200                                 TO MR-GRP-NBR (PV-SUB)                   
181300             MOVE ASC-GRP-DES (ASC-BLK-SUB ASC-ITEM-SUB)                  
181400                                 TO MR-GRP-DES (PV-SUB)                   
181500         END-IF                                                           
181600     END-IF.                                                              
181700     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
181800     IF  ASC-ITEM-SUB < + 1                                               
181900         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
182000         MOVE +1                 TO ASC-BLK-SUB                           
182100     END-IF.                                                              
182200******************************************************************        
182300** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
182400** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
182500** ISSUE A WARNING.                                             **        
182600******************************************************************        
182700 4430-CHECK-FOR-BEGIN-AND-END.                                            
182800     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
182900         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
183000*            -- NO END-OF-RANGE SPECFIED ---                              
183100             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
183200             SET DP020-MSG-WARNING TO TRUE                                
183300             PERFORM 4431-POSITION-AT-WARNING                             
183400         END-IF                                                           
183500     ELSE                                                                 
183600         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
183700*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
183800             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
183900             SET DP020-MSG-WARNING TO TRUE                                
184000             PERFORM 4431-POSITION-AT-WARNING                             
184100         END-IF                                                           
184200     END-IF.                                                              
184300******************************************************************        
184400** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
184500** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
184600** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
184700** POSITION THE CURSOR THERE.                                   **        
184800******************************************************************        
184900 4431-POSITION-AT-WARNING.                                                
185000     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
185100                    - PV-TOP-ITEM + 1.                                    
185200     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
185300         CONTINUE                                                         
185400     ELSE                                                                 
185500         SET DP030-SET-CURSOR-APPL-1                                      
185600                                   TO TRUE                                
185700         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
185800         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
185900         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
186000     END-IF.                                                              
186100*                                                                         
186200     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
186300                    - PV-TOP-ITEM + 1.                                    
186400     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
186500         CONTINUE                                                         
186600     ELSE                                                                 
186700         SET DP030-SET-CURSOR-APPL-1                                      
186800                                   TO TRUE                                
186900         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
187000         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
187100         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
187200     END-IF.                                                              
187300 EJECT                                                                    
187400*----------------------------------------------------------------*        
187500* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
187600* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
187700* FOR THE SELECTED ITEMS QUEUE.                                  *        
187800*                                                                *        
187900* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
188000* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
188100* THE ARRAY IS FULL.                                             *        
188200*----------------------------------------------------------------*        
188300                                                                          
188400 5000-PREPARE-SEL-QUEUE.                                                  
188500                                                                          
188600     MOVE +1                   TO  ASC-BLK-SUB.                           
188700     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
188800     ADD +1                    TO  ASC-BLK-SUB.                           
188900     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
189000                                                                          
189100     MOVE ASC-SEL-QUEUE-NAME   TO  IQ016-SEL-QUEUE-NAME.                  
189200     PERFORM 6010-DELETE-SEL-QUEUE.                                       
189300                                                                          
189400     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
189500                                                                          
189600     SET IQ014-IDX             TO  1.                                     
189700     MOVE ZERO                 TO IQ016-NUMBER-OF-TS-ITEMS.               
189800     INITIALIZE IQ014-SELECTED-ITEM-ARRAY.                                
189900                                                                          
190000     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
190100             VARYING PV-ITEM                                              
190200             FROM 1 BY 1                                                  
190300             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
190400                                                                          
190500     IF  IQ014-IDX > 1                                                    
190600         PERFORM 5205-WRITE-SEL-QUEUE                                     
190700         ADD +1                TO IQ016-NUMBER-OF-TS-ITEMS                
190800     END-IF.                                                              
190900 EJECT                                                                    
191000*----------------------------------------------------------------*        
191100*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
191200*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
191300*----------------------------------------------------------------*        
191400                                                                          
191500 5100-WRITE-TEMP-STORAGE.                                                 
191600     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
191700                                   TO PV-LIST-ITEM-NUMBER.                
191800     COMPUTE PV-BLOCK-NUMBER =                                            
191900         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
192000                                                                          
192100     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
192200     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
192300         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
192400             PERFORM 5105-REWRITE-ASC-LISTQ                               
192500         END-IF                                                           
192600     ELSE                                                                 
192700         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
192800         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
192900         PERFORM 5110-WRITE-ASC-LISTQ                                     
193000         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
193100     END-IF.                                                              
193200                                                                          
193300     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
193400******************************************************************        
193500** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
193600** SPECIFIC COMMAREA.                                           **        
193700******************************************************************        
193800 5105-REWRITE-ASC-LISTQ.                                                  
193900     EXEC CICS                                                            
194000         WRITEQ TS                                                        
194100             QUEUE (ASC-LIST-QUEUE-NAME)                                  
194200             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
194300             ITEM  (PV-BLOCK-NUMBER)                                      
194400             REWRITE                                                      
194500             RESP  (PV-CICS-RESP)                                         
194600     END-EXEC.                                                            
194700                                                                          
194800     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
194900         CONTINUE                                                         
195000     ELSE                                                                 
195100         SET DP013-CICS-ABEND            TO TRUE                          
195200         SET DP013-NO-ROLLBACK           TO TRUE                          
195300         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
195400         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
195500                                         TO DP013-MESSAGE-TEXT(1)         
195600         PERFORM DP013-0000-PROCESS-ABEND                                 
195700     END-IF.                                                              
195800                                                                          
195900******************************************************************        
196000** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
196100** SPECIFIC COMMAREA.                                           **        
196200******************************************************************        
196300 5110-WRITE-ASC-LISTQ.                                                    
196400     EXEC CICS                                                            
196500         WRITEQ TS                                                        
196600             QUEUE (ASC-LIST-QUEUE-NAME)                                  
196700             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
196800             ITEM  (PV-BLOCK-NUMBER)                                      
196900             RESP  (PV-CICS-RESP)                                         
197000     END-EXEC.                                                            
197100     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
197200         SET DP013-CICS-ABEND      TO TRUE                                
197300         SET DP013-NO-ROLLBACK     TO TRUE                                
197400         MOVE '5110-WRITE-ASC-LISTQ'                                      
197500                                   TO DP013-PARAGRAPH                     
197600         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
197700                                   TO DP013-MESSAGE-TEXT(1)               
197800         PERFORM DP013-0000-PROCESS-ABEND                                 
197900     END-IF.                                                              
198000 EJECT                                                                    
198100*----------------------------------------------------------------*        
198200* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
198300* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
198400*----------------------------------------------------------------*        
198500                                                                          
198600 5200-FILL-SEL-QUEUE-RECORD.                                              
198700                                                                          
198800     PERFORM 6112-READ-LIST-QUEUE.                                        
198900                                                                          
199000     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
199100         PERFORM                                                          
199200         VARYING LQW-IDX                                                  
199300         FROM 1 BY 1                                                      
199400         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
199500           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
199600                                                                          
199700             IF  LQW-SELECT (LQW-IDX)                                     
199800                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
199900                                   TO IQ014-ITEM (IQ014-IDX)              
200000                 MOVE LQW-GRP-NBR (LQW-IDX)                               
200100                             TO IQ014-GRP-NBR (IQ014-IDX)                 
200200                                IQ016-GP-NBR                              
200300                 MOVE LQW-GRP-DES (LQW-IDX)                               
200400                             TO IQ014-GRP-DES (IQ014-IDX)                 
200500                                IQ016-GP-DESC                             
200600                 SET IQ014-IDX UP BY 1                                    
200700                 IF  IQ014-IDX > IQ014-MAX-ITEMS                          
200800                     PERFORM 5205-WRITE-SEL-QUEUE                         
200900                     ADD +1        TO IQ016-NUMBER-OF-TS-ITEMS            
201000                     INITIALIZE IQ014-SELECTED-ITEM-ARRAY                 
201100                     SET IQ014-IDX TO  1                                  
201200                 END-IF                                                   
201300             END-IF                                                       
201400         END-PERFORM                                                      
201500     END-IF.                                                              
201600                                                                          
201700*----------------------------------------------------------------*        
201800*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
201900*----------------------------------------------------------------*        
202000                                                                          
202100 5205-WRITE-SEL-QUEUE.                                                    
202200                                                                          
202300     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
202400                                   TO IQ014-SELECTED-ITEMS.               
202500                                                                          
202600     EXEC CICS                                                            
202700          WRITEQ TS                                                       
202800              QUEUE (ASC-SEL-QUEUE-NAME)                                  
202900              FROM  (IQ014-MESSAGE-SEL-REC)                               
203000              ITEM  (PV-SEL-QUEUE-ITEM)                                   
203100              RESP  (PV-CICS-RESP)                                        
203200     END-EXEC.                                                            
203300                                                                          
203400     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
203500         CONTINUE                                                         
203600     ELSE                                                                 
203700         SET DP013-CICS-ABEND      TO TRUE                                
203800         SET DP013-NO-ROLLBACK     TO TRUE                                
203900         MOVE '5205-WRITE-SEL-QUEUE'                                      
204000                                   TO DP013-PARAGRAPH                     
204100         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
204200                                   TO DP013-MESSAGE-TEXT(1)               
204300         PERFORM DP013-0000-PROCESS-ABEND                                 
204400     END-IF.                                                              
204500 EJECT                                                                    
204600******************************************************************        
204700** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
204800** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
204900** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
205000** THE USER.                                                    **        
205100******************************************************************        
205200 6000-REFRESH-PANEL-FROM-TEMP.                                            
205300     IF  ASC-UPDATE-TSQ                                                   
205400         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
205500         MOVE ZERO                 TO PV-SAVE-ITEM                        
205600                                      PV-SEL-QUEUE-ITEM                   
205700         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
205800                                   TO PV-HOLD-SELECTED-ITEMS              
205900         MOVE +1                   TO PV-ITEMS-PROCESSED                  
206000         PERFORM 6100-UPDATE-TSQ                                          
206100             UNTIL PV-ITEMS-PROCESSED >                                   
206200                                     PV-HOLD-SELECTED-ITEMS               
206300         PERFORM 6010-DELETE-SEL-QUEUE                                    
206400         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
206500                    ASC-BLOCK-ENTRIES (2)                                 
206600     END-IF.                                                              
206700                                                                          
206800*----------------------------------------------------------------*        
206900*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
207000*----------------------------------------------------------------*        
207100 6010-DELETE-SEL-QUEUE.                                                   
207200                                                                          
207300     EXEC CICS                                                            
207400         DELETEQ TS                                                       
207500             QUEUE (ASC-SEL-QUEUE-NAME)                                   
207600             RESP  (PV-CICS-RESP)                                         
207700     END-EXEC.                                                            
207800*                                                                         
207900     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
208000        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
208100         CONTINUE                                                         
208200     ELSE                                                                 
208300         SET DP013-CICS-ABEND TO TRUE                                     
208400         SET DP013-NO-ROLLBACK TO TRUE                                    
208500         MOVE '6010-DELETE-SEL-QUEUE'                                     
208600                        TO DP013-PARAGRAPH                                
208700         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
208800                        TO DP013-MESSAGE-TEXT (1)                         
208900         PERFORM DP013-0000-PROCESS-ABEND                                 
209000     END-IF.                                                              
209100                                                                          
209200*----------------------------------------------------------------*        
209300*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
209400*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
209500*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
209600*    THE TEMP QUEUE.                                             *        
209700*----------------------------------------------------------------*        
209800                                                                          
209900 6100-UPDATE-TSQ.                                                         
210000                                                                          
210100     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
210200     PERFORM 6105-READ-SEL-QUEUE.                                         
210300     PERFORM                                                              
210400         VARYING IQ014-IDX                                                
210500         FROM 1 BY 1                                                      
210600         UNTIL ((PV-ITEMS-PROCESSED >                                     
210700                              PV-HOLD-SELECTED-ITEMS)                     
210800         OR (IQ014-IDX GREATER THAN IQ014-MAX-ITEMS))                     
210900         PERFORM 6110-PROCESS-ITEMS                                       
211000         ADD +1                     TO PV-ITEMS-PROCESSED                 
211100     END-PERFORM.                                                         
211200     PERFORM 6111-WRITE-LIST-QUEUE.                                       
211300*----------------------------------------------------------------*        
211400*    READ THE LIST QUEUE RECORD.                                 *        
211500*----------------------------------------------------------------*        
211600                                                                          
211700 6105-READ-SEL-QUEUE.                                                     
211800                                                                          
211900     EXEC CICS                                                            
212000          READQ TS                                                        
212100              QUEUE (ASC-SEL-QUEUE-NAME)                                  
212200              INTO  (IQ014-MESSAGE-SEL-REC)                               
212300              ITEM  (PV-SEL-QUEUE-ITEM)                                   
212400              RESP  (PV-CICS-RESP)                                        
212500     END-EXEC.                                                            
212600*                                                                         
212700     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
212800         CONTINUE                                                         
212900     ELSE                                                                 
213000         SET DP013-CICS-ABEND      TO  TRUE                               
213100         SET DP013-NO-ROLLBACK     TO  TRUE                               
213200         MOVE '6105-READ-SEL-QUEUE'                                       
213300                                   TO  DP013-PARAGRAPH                    
213400         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
213500                                   TO  DP013-MESSAGE-TEXT(1)              
213600         PERFORM DP013-0000-PROCESS-ABEND                                 
213700     END-IF.                                                              
213800 EJECT                                                                    
213900*----------------------------------------------------------------*        
214000*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
214100*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
214200*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
214300*----------------------------------------------------------------*        
214400                                                                          
214500 6110-PROCESS-ITEMS.                                                      
214600                                                                          
214700     COMPUTE PV-ITEM = 1 + ((IQ014-ITEM (IQ014-IDX) - 1)                  
214800                     / PC-ITEMS-PER-PANEL).                               
214900                                                                          
215000     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
215100         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
215200             PERFORM 6111-WRITE-LIST-QUEUE                                
215300         END-IF                                                           
215400         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
215500                                                                          
215600         PERFORM 6112-READ-LIST-QUEUE                                     
215700         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
215800             CONTINUE                                                     
215900         END-IF                                                           
216000     END-IF.                                                              
216100                                                                          
216200     COMPUTE PV-SUB = (IQ014-ITEM (IQ014-IDX)                             
216300                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
216400     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
216500     MOVE LQW-SELECTED-SW (PV-SUB)                                        
216600                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
216700                                                                          
216800     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
216900                                                                          
217000*----------------------------------------------------------------*        
217100*    WRITE THE TS QUEUE RECORD JUST UPDATED                      *        
217200*----------------------------------------------------------------*        
217300                                                                          
217400 6111-WRITE-LIST-QUEUE.                                                   
217500                                                                          
217600     EXEC CICS                                                            
217700         WRITEQ TS                                                        
217800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
217900             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
218000             ITEM  (PV-SAVE-ITEM)                                         
218100             REWRITE                                                      
218200             RESP  (PV-CICS-RESP)                                         
218300     END-EXEC.                                                            
218400*                                                                         
218500     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
218600         CONTINUE                                                         
218700     ELSE                                                                 
218800         SET DP013-CICS-ABEND  TO  TRUE                                   
218900         SET DP013-NO-ROLLBACK TO  TRUE                                   
219000         MOVE '6111-WRITE-LIST-QUEUE'                                     
219100                               TO  DP013-PARAGRAPH                        
219200         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
219300                               TO  DP013-MESSAGE-TEXT(1)                  
219400         PERFORM DP013-0000-PROCESS-ABEND                                 
219500     END-IF.                                                              
219600                                                                          
219700 EJECT                                                                    
219800*----------------------------------------------------------------*        
219900*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
220000*----------------------------------------------------------------*        
220100                                                                          
220200 6112-READ-LIST-QUEUE.                                                    
220300                                                                          
220400     EXEC CICS                                                            
220500         READQ TS                                                         
220600             QUEUE (ASC-LIST-QUEUE-NAME)                                  
220700             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
220800             ITEM  (PV-ITEM)                                              
220900             RESP  (PV-CICS-RESP)                                         
221000     END-EXEC.                                                            
221100*                                                                         
221200     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
221300         CONTINUE                                                         
221400     ELSE                                                                 
221500         SET DP013-CICS-ABEND        TO TRUE                              
221600         SET DP013-NO-ROLLBACK       TO TRUE                              
221700         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
221800         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
221900                                     TO DP013-MESSAGE-TEXT(1)             
222000         PERFORM DP013-0000-PROCESS-ABEND                                 
222100     END-IF.                                                              
222200                                                                          
222300 EJECT                                                                    
222400                                                                          
222500*----------------------------------------------------------------*        
222600*    ABEND PROCESSOR MODULE                                      *        
222700*----------------------------------------------------------------*        
222800                                                                          
222900     COPY DPPD013.                                                        
