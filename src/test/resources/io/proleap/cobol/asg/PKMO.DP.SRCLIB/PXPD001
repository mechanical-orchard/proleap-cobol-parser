      ******************************************************************        
      * PX001-A000-ORACLE-SETUP                                        *        
      ******************************************************************        
       PX001-A000-ORACLE-SETUP.                                                 
                                                                                
           DISPLAY '******  ORACLE CONNECTION  *******'.                        
           DISPLAY ' PX001-A000-ORACLE-SETUP'.                                  
                                                                                
           PERFORM PX001-READ-ORACLE-SRC-CNTL.                                  
                                                                                
           IF PS-ORA-SRC-EMPTY                                                  
             DISPLAY '**********************************'                       
             DISPLAY '* ABEND: PX001-A000-ORACLE-SETUP'                         
             DISPLAY '*   ORACLE SETUP CONTROL CARD EMPTY'                      
             DISPLAY '*   THE PROGRAM WILL ABEND'                               
             DISPLAY '**********************************'                       
             MOVE +4013           TO ABEND-CODE                                 
             CALL 'ILBOABN0'      USING ABEND-CODE                              
           ELSE                                                                 
             MOVE PX001-ORA-SRC-SYSTEM TO PX001-EDIT-ORA-SOURCE                 
             IF (NOT VALID-ORACLE-CONNECT-STRING)                               
               DISPLAY '**********************************'                     
               DISPLAY '* ABEND: PX001-A000-ORACLE-SETUP'                       
               DISPLAY '*   INVALID ORACLE SOURCE SYSTEM INFORMATION'           
               DISPLAY '*   EXISTS IN CONTROL CARD'                             
               DISPLAY '**********************************'                     
               DISPLAY 'PX001-ORACLE-SOURCE-RECORD'                             
               DISPLAY PX001-ORACLE-SOURCE-RECORD                               
               MOVE +4013           TO ABEND-CODE                               
               CALL 'ILBOABN0'      USING ABEND-CODE                            
             ELSE                                                               
               DISPLAY '* ORACLE CONNECTION STRING SOURCE: '                    
               DISPLAY IN-ORA-SRC-CTL-RECORD                                    
               END-IF.                                                          
                                                                                
      *** ORACLE DEVELOPMENT REGION CONNECT. (TST, QA, LRN, PRODT)              
           IF VALID-DEVELOPMENT-REGION                                          
             MOVE PX001-ORA-USER-NAME TO USERNAME-ARR                           
             MOVE PX001-ORA-USER-LGTH TO USERNAME-LEN                           
             MOVE PX001-ORA-PASSWORD TO PASSWD-ARR                              
             MOVE PX001-ORA-PASSWORD-LGTH TO PASSWD-LEN                         
             DISPLAY PX001-ORA-SRC-SYSTEM, ' - ATTEMPTING LOGON AS ',           
                     USERNAME-ARR                                               
             EXEC SQL                                                           
               CONNECT :USERNAME IDENTIFIED BY :PASSWD                          
             END-EXEC                                                           
           ELSE                                                                 
      **   RUNNING PRODUCTION WITH THE PX001-ORACLE-ID OF A SLASH               
      **   WILL DYNAMICALLY FIGURE OUT THE LOGON ID OF THE                      
      **   ENTITY EXECUTING THE PROGRAM.                                        
           IF ORA-PROD-REGION                                                   
             DISPLAY PX001-ORA-SRC-SYSTEM, ' - ATTEMPTING LOGON AS ',           
                     USERNAME-ARR                                               
             EXEC SQL                                                           
               CONNECT :PV-ORACLE-ID                                            
             END-EXEC                                                           
           END-IF.                                                              
                                                                                
           IF SQLCODE = 0                                                       
               DISPLAY 'CONNECTED TO ORACLE AS USER: ' PV-ORACLE-ID             
               DISPLAY ' LOGIN SQLCODE: ' SQLCODE                               
           ELSE                                                                 
               MOVE 'PX001-A000-ORACLE-SETUP' TO PV-PARAGRAPH-NAME              
               SET ABEND-4014-ORACLE-LOGON-ERROR TO TRUE                        
               PERFORM Z100-ABEND                                               
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
            ALTER SESSION                                                       
                  SET NLS_DATE_FORMAT = 'YYYY-MM-DD.HH24.MI.SS'                 
           END-EXEC.                                                            
           DISPLAY ' DATE FORMAT SQLCODE: ' SQLCODE.                            
                                                                                
           DISPLAY 'END OF PX001-A000-ORACLE-SETUP'.                            
                                                                                
      ******************************************************************        
      * PX001-READ-ORACLE-SRC-CNTL.                                    *        
      *     THE ROUTINE READS IN THE ORACLE SOURCE CONTROL CARD.                
      * THE CARD DEFINES WHICH ORACLE ENVIRONMENT WE ARE PROCESSING             
      * FROM.                                                                   
      ******************************************************************        
       PX001-READ-ORACLE-SRC-CNTL.                                              
                                                                                
           READ IN-ORACLE-SRC-CTL INTO PX001-ORACLE-SOURCE-RECORD               
               AT END  MOVE 'Y'     TO PS-ORA-SRC-CTL-DONE-SW.                  
                                                                                
