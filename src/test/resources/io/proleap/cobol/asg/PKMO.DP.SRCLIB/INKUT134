       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKUT134.                                            
       AUTHOR.             SCOTT JACKSON/RONNA MCDONALD/TODD MICHALEK.          
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       01/01.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  06/17/2008     TODD C. MICHALEK    CHANGE ID: INC000000402155          
      *  NOTE: THIS PROGRAM WAS ORIGINALLY COPIED FROM THE PROGRAM              
      *        INKUT110 BY RONNA MCDONALD TO CORRECT A JANUARY, 2008            
      *        PHYSICAL INVENTORY ISSUE.  IT WAS CREATED FOR                    
      *        E-COMMERCE/CENTRAL STOCK LOCATIONS THAT WERE WRONGLY             
      *        PASSING THE INPUT FILE PRICE TO THE OUTPUT FILE.                 
      *        IN IT THE PRICING MODULE IS CALLED TO VERIFY AN ITEM AND         
      *        RETURN A PRICE.  THE RETURNED PRICE IS THEN WRITTEN TO           
      *        THE OUTPUT FILE, SHOULD IT NOT MATCH THE INPUT FILE      E       
      *        PRICE.                                                           
      ******************************************************************        
      *               SHEET INVENTORY PROCESSING PROGRAM                        
      ******************************************************************        
      *  PGM PROCESSES FILES FROM RECEIVING SYSTEMS FOR 'CS' AND 'RTV'.         
      *  ALSO PROCESSES ONLINE 'SHEET ENTRIES' FOR PHYSICALLY COUNTED           
      *  STORES WHICH HAVE BEEN PREVIOUSLY EXTRACTED FROM TINENTY.              
      *                                                                         
      *  PROGRAM WILL MOVE ALL INPUT FIELDS TO OUTPUT VALIDS FILE.              
      *  ONLY 1 EDITTING VALIDATION WILL BE PERFORMED IN THIS PGM.              
      *  - IF THE RETAIL PROVIDED = $0.00 WRITE TO INVALIDS FILE                
      ******************************************************************        
      *  3/2/2001       S. JACKSON                                              
      *  LOCATIONS PASSING THRU 'SHEET PROCESSING' WILL NEED TO HAVE            
      *  THE ACTL_INV_RECD_DTE SET TO PROCESS DATE (IF NOT YET SET).            
      *  'DS' LOCATIONS WILL ALREADY BE SET WHEN INV DATA IS PROCESSED.         
      *  'CS' / 'RV' SHEET DATA WILL UPDATE ACTL_INV_RECD_DTE ON FIRST          
      *  RUN THROUGH INK307. SUBSEQUENT RUNS WILL BYPASS UPDATE LOGIC.          
      *****************************************************************         
      *  10/04/2001     S. JACKSON             CHANGE ID:10401                  
      *  ADDITIONAL EDITS AND VALIDATION TESTING HAVE BEEN INCORPORATED.        
      *  SHEET ENTRY AND CENTRAL STOCK SYSTEMS ARE NOT EDITING FOR SKUS         
      *  THAT MAY BE IN A RENUMBERED STATUS.                                    
      *  ADD THESE EDITS TO THIS PROGRAM TO ENSURE THAT THE MOST-RECENT         
      *  WILL BE LOADED TO TSUMINV. ANY SKU'S ON THE INPUT FILE FOUND           
      *  TO BE IN A RENUMBER STATUS MUST BE CONVERTED TO CURRENT SKU.           
      ******************************************************************        
      *  01/25/2002     S. JACKSON             CHANGE ID:IN2002                 
      *  SKU SEQUENCE NUMBER OF '000' WILL BE ACCEPTED AS VALID. THESE          
      *  SKUS WILL NOT TRY TO FIND A MATCH FOR ITEM. SEQUENCE NUMBERS OF        
      *  999 ARE NOT A VALID SCENARIO AND WILL BE FLAGGED AS INVALID.           
      ******************************************************************        
RB2002*  01/26/2002     R. BUTZ                CHANGE ID:RB2002                 
RB2002*  WHEN THE INVALID ITEM SWITCH IS SET, ALWAYS SET THE INVALID            
RB2002*  RECORD SWITCH.  THE INVALID RECORD SWITCH IS SO THAT LOSS              
RB2002*  PREVENTION CAN CORRECT THE INVALID AND THEY SHOULD BE ABLE TO          
RB2002*  CORRECT ANY INVALID.                                                   
RB2002*  ALSO, WHEN A SEQUENCE NUMBER IS 000, MOVE THE INPUT SKU                
RB2002*  TO THE OUTPUT SKU SO THAT THERE IS A DEPT AND CLASS TO CHECK           
RB2002*  FOR THE MBS EDITING.                                                   
RB2002******************************************************************        
W21732*  11/27/2002     ALBERT A VINOD         CHANGE ID:W21732                 
W21732*  3 DIGIT DEPT, 2 DIGIT CLASS AND 3 DIGIT SKU SEQ IS REPLACED            
W21732*  WITH 4 DIGIT DEPT, 4 DIGIT CLASS AND 8 DIGIT CLASS. THE                
W21732*  RECORD SIZE OF PHYSICAL-INVENTORY-FILE IS INCREASED TO 100,            
W21732*  EDITED-INVENTORY-FILE IS INCREASED TO 70 AND                           
W21732*  VALID-INV-REPORT-FILE IS INCREASED TO 100.                             
W21732******************************************************************        
C21732*  03/12/2003    SCOTT JACKSON           CHANGE ID:C21732                 
C21732* INVALIDS RECORDS CAN ALSO BE CREATED EARLIER IN PROC (INKUT112).        
C21732******************************************************************        
W21732*  03/21/2003    RONNA BUTZ              CHANGE ID:W21732                 
W21732* IF THIS PROGRAM IS RUN TO PROCESS AN E-COMMERCE DS LOCATION,            
W21732* UPDATE THE INVENTORY RECEIVED DATE.  THIS WILL ALLOW THE                
W21732* E-COMMERCE LOCATION TO BECOME A CANDIDATE FOR UNIT BOOKINGS             
W21732******************************************************************        
24521 *  06/27/2003    ROD JANSSEN             CHANGE ID:WR24521                
24521 *   ON A RENUMBERED SKU, WE HAD AN ISSUE WHERE THE RENUMBER INFO          
24521 * DID NOT EXIST IN TSKURNMBR BECAUSE IT HAD BEEN PURGED FROM VSM          
24521 * DUE TO ITS AGE. ADJUST RENUMBER LOGIC TO FIND THE CURRENT ACTIVE        
24521 * SKU USING TSKXREF AND THE INTR_UPC_ID.                                  
26342 ******************************************************************        
26342 *  01/23/2004    SCOTT JACKSON           CHANGE ID:WR26342                
26342 * FIX AN ISSUE WHERE MULTIPLE ROWS MAY BE RETURNED ON A ROW FOR           
26342 * AN INTERNAL UPC NUMBER. THIS WAS CAUSING A BAD SQL RETURN CODE          
26342 * AND FLAGGIN UPC'S AS INVALID. ADDED ANOTHER LIMITING CONDITION.         
26342 ******************************************************************        
W26600*  06/  /2004    M JOHNSON               CHANGE ID:  WR26600              
W26600*  PHYSICAL INVENTORY STORE EXPANSION                                     
26342 ******************************************************************        
W26006*  02/24/2005    ERIN SEARL              CHANGE ID:  W26006               
W26006*  REMOVE REFERENCES TO OBSOLETE STORE TABLE.                             
26342 ******************************************************************        
P28545* W28545 03-24-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE SETUP        
Z28545*W28545  08-07-2006 ROD JANSSEN   IN400-***-SKU-OUT LOGIC NEEDS TO        
Z28545*                               USE THE RESULTANT SKU RATHER THAN         
Z28545*                               CREATE A SKU BASED ON UPC_NBR.            
Z28545*                                 IN400-***-UPC-OUT LOGIC NEEDS TO        
Z28545*                               RETAIN THE PASSED IN UPC_NBR IF           
Z28545*                               PRESENT; OTHERWISE, PASS BACK THE         
Z28545*                               KOHLS UPC_NBR.                            
W26006******************************************************************        
RONNAZ*  09/19/2007    RONNA MCDONALD          CHANGE ID:  W26006               
RONNAZ*  MAP THE DUMMY SKU FROM THE ATTRIBUTE OUTPUT FROM PRICING               
RONNAZ*  MODULE.                                                                
W33304******************************************************************        
W33304*  10/13/2008    PATTY JAEGER-CODEWORKS  CHANGE ID:  W33304               
W33304*  HILO CHANGES                                                           
W33304******************************************************************        
SR3403*  05/23/2012    JOHN SELWITSCHKA        CHANGE ID:  SR3403               
SR3403*  LARGE RETAIL PROJECT                                                   
SR3403******************************************************************        
      *  02/14/2023    JEAN KURK                     CHG0281369                 
      *  REMOVE REFERENCES TO THE UPC CHECK DIGIT ROUTINE, WHICH WERE           
      *  ALREADY COMMENTED OUT                                                  
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT  PHYSICAL-INVENTORY-FILE                                      
               ASSIGN TO INP01.                                                 
           SELECT  STORE-PROCESS-FILE                                           
               ASSIGN TO INP02.                                                 
           SELECT  EDITED-INVENTORY-FILE                                        
               ASSIGN TO OUT01.                                                 
           SELECT  INVALID-DETAIL-FILE                                          
               ASSIGN TO OUT02.                                                 
           SELECT  VALID-INV-REPORT-FILE                                        
               ASSIGN TO OUT03.                                                 
25248      SELECT  MISSING-TABLE-ENTRY-FILE                             INKUT102
25248          ASSIGN TO OUT04.                                         INKUT102
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  PHYSICAL-INVENTORY-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W21732 01  PHYSICAL-INVENTORY-RECORD  PIC X(100).                               
                                                                                
10401  FD  STORE-PROCESS-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-PROCESS-RECORD       PIC X(80).                                
                                                                                
       FD  EDITED-INVENTORY-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
SR3403 01  EDITED-INVENTORY-RECORD    PIC X(100).                               
                                                                                
       FD  INVALID-DETAIL-FILE                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INVALID-DETAIL-RECORD      PIC X(88).                                
                                                                                
       FD  VALID-INV-REPORT-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W21732 01  VALID-INV-REPORT-RECORD    PIC X(100).                               
                                                                                
25248  FD  MISSING-TABLE-ENTRY-FILE                                     INKUT102
  |        LABEL RECORDS ARE STANDARD                                   INKUT102
  |        RECORDING MODE IS F                                          INKUT102
  |        BLOCK CONTAINS 0 RECORDS.                                    INKUT102
  |    01  MISSING-TABLE-ENTRY-RECORD PIC X(70).                        INKUT102
25248                                                                   INKUT102
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  DK-DATABASE-KEYS.                                                    
W26600     05  DK-STORE                PIC X(04)    VALUE SPACES.               
25248      05  DK-UPC-CODE             PIC S9(15)V  COMP-3 VALUE 0.     INKUT102
      *                                                                         
       01  CONTROL-VARIABLES.                                                   
25248      05  CC-STORE-NBR            PIC X(04)    VALUE SPACES.               
25248      05  CC-STORE-NBR-NUM REDEFINES                                       
25248          CC-STORE-NBR            PIC 9(04).                               
           05  CC-CURRENT-DATE.                                                 
               10  CC-DATE-YY          PIC X(02)    VALUE SPACES.               
               10  CC-DATE-MM          PIC X(02)    VALUE SPACES.               
               10  CC-DATE-DD          PIC X(02)    VALUE SPACES.               
           05  CC-DB2-DATE.                                                     
               10  CC-DB2-CC           PIC X(02)    VALUE '20'.                 
               10  CC-DB2-YY           PIC X(02)    VALUE SPACES.               
               10  FILLER              PIC X(01)    VALUE '-'.                  
               10  CC-DB2-MM           PIC X(02)    VALUE SPACES.               
               10  FILLER              PIC X(01)    VALUE '-'.                  
               10  CC-DB2-DD           PIC X(02)    VALUE SPACES.               
10401      05  CC-DB2-TMST.                                                     
               10  CC-DB2-TMST-DATE    PIC X(10) VALUE SPACES.                  
               10  FILLER              PIC X(16)                                
                                               VALUE '-23.59.59.999999'.        
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES          PIC S9(03)   VALUE +03  COMP-3.          
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-HOLD-MAJ-CL-DESC     PIC X(25) VALUE SPACES.                  
           05  PV-HOLD-BYR-NBR         PIC X(03) VALUE SPACES.                  
           05  PV-HOLD-GMA-NBR         PIC X(02) VALUE SPACES.                  
           05  PV-HOLD-DMA-NBR         PIC X(02) VALUE SPACES.                  
W21732     05  PV-HOLD-DEPT            PIC 9(04) VALUE ZERO.                    
W21732     05  PV-HOLD-SUB-CLASS       PIC 9(04) VALUE ZERO.                    
W21732     05  PV-HOLD-MAJ-CLASS.                                               
S21732         10 FILLER               PIC 9(02) VALUE ZERO.                    
S21732         10 PV-HOLD-MAJ-CLASS-34 PIC 9(02) VALUE ZERO.                    
S21732     05  PV-WORK-MAJ-CLASS.                                               
S21732         10 FILLER               PIC 9(01) VALUE ZERO.                    
W21732         10 PV-WORK-MAJ-CL-2     PIC 9(01) VALUE ZERO.                    
25248      05  PV-HOLD-UPC             PIC 9(15) VALUE ZEROS.                   
W21732     05  PV-HOLD-SKU.                                                     
W21732         10  PV-HOLD-SKU-DEPT    PIC 9(03) VALUE ZERO.                    
W21732         10  PV-HOLD-SKU-CL      PIC 9(02) VALUE ZERO.                    
W21732         10  PV-HOLD-SKU-SEQ     PIC 9(03) VALUE ZERO.                    
C21732     05  PV-RENUM-SKU.                                                    
C21732         10  PV-RENUM-SKU-DEPT   PIC 9(03) VALUE ZERO.                    
C21732         10  PV-RENUM-SKU-CL     PIC 9(02) VALUE ZERO.                    
C21732         10  PV-RENUM-SKU-SEQ    PIC 9(03) VALUE ZERO.                    
C21732     05  PV-RENUM-SKU-NBR        PIC S9(8) VALUE ZERO.                    
C21732     05  PV-XREF-SKU-NBR         PIC S9(8)V COMP-3.                       
W21732     05  PV-UPC-MIN.                                                      
W21732         10  FILLER              PIC 9(04) VALUE 0400.                    
W21732         10  PV-UPC-SKU-MIN      PIC 9(08) VALUE ZERO.                    
W21732         10  PV-MIN              PIC 9     VALUE 0.                       
W21732     05  PV-SIGN-UPC-MIN REDEFINES PV-UPC-MIN                             
W21732                                 PIC 9(13).                               
W21732     05  PV-UPC-MAX.                                                      
W21732         10  FILLER              PIC 9(04) VALUE 0400.                    
W21732         10  PV-UPC-SKU-MAX      PIC 9(08) VALUE ZERO.                    
W21732         10  PV-MAX              PIC 9     VALUE 9.                       
W21732     05  PV-SIGN-UPC-MAX REDEFINES PV-UPC-MAX                             
W21732                                 PIC 9(13).                               
W21732     05 PV-MDSEAR-UPC-MIN        PIC S9(13)V COMP-3.                      
W21732     05 PV-MDSEAR-UPC-MAX        PIC S9(13)V COMP-3.                      
           05  PV-MAJ-CL-NBR.                                                   
               10 PV-MAJ-CL-NBR-1      PIC X        VALUE SPACES.               
               10 PV-MAJ-CL-NBR-2      PIC X        VALUE SPACES.               
           05  PV-SUB-CL-NBR.                                                   
               10 PV-SUB-CL-NBR-1      PIC X        VALUE SPACES.               
               10 PV-SUB-CL-NBR-2-3    PIC X(02)    VALUE SPACES.               
           05  PV-PARAGRAPH-NAME       PIC X(30)    VALUE SPACES.               
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)    VALUE SPACES.               
           05  PV-RETRY-COUNT          PIC S9(03)   VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-ITEM-NBR             PIC S9(15)V  VALUE ZERO                  
                                                    COMP-3.                     
10401      05  PV-DB2-TMST             PIC X(26)    VALUE SPACES.               
10401      05  PV-TEMP-DATE            PIC X(10)    VALUE ZERO.                 
S26600     05  PV-STR-NBR              PIC 9(04)    VALUE ZERO.         04831002
25248      05  PV-EDIT-LOC-NBR         PIC S9(04)   COMP VALUE ZEROS.           
25248      05  PV-EDIT-LOC-NBR-NUM     PIC 9(04)    VALUE ZEROS.                
25248      05  PV-EDIT-LOC-NBR-ALF REDEFINES                                    
25248          PV-EDIT-LOC-NBR-NUM     PIC X(04).                               
25248      05  PV-EDIT-UPC-CODE        PIC 9(15)    VALUE ZEROS.                
25248      05  PV-EDIT-UPC-CODE-ALPHA REDEFINES                                 
25248          PV-EDIT-UPC-CODE        PIC X(15).                               
P28545     05  PV-EDIT-SKU             PIC 9(08)    VALUE ZEROS.                
P28545     05  PV-EDIT-DEPT            PIC 9(04)    VALUE ZEROS.                
P28545     05  PV-EDIT-MCL             PIC 9(04)    VALUE ZEROS.                
P28545     05  PV-EDIT-SCL             PIC 9(04)    VALUE ZEROS.                
25248                                                                           
25248      05  PV-WORK-UPC              PIC 9(13).                      INKUT102
25248      05  PV-WORK-UPC-X  REDEFINES  PV-WORK-UPC.                   INKUT102
25248          10  FILLER               PIC X(4).                       INKUT102
25248              88  PV-KOHLS-UPC                 VALUE '0400'.       INKUT102
25248          10  PV-UPC-SKU.                                                  
25248            15  PV-UPC-SKU-DEPT-CL PIC X(5).                               
25248            15  PV-UPC-SKU-SEQ     PIC X(3).                               
25248          10  PV-UPC-CHK-DIG       PIC 9.                          INKUT102
25248      05  PV-KOHLS-UPC-LOW.                                                
25248          10  FILLER              PIC 9(04)    VALUE 0400.                 
25248          10  PV-KOHLS-SKU-MIN    PIC 9(08)    VALUE ZEROS.                
25248          10  FILLER              PIC 9(01)    VALUE 0.                    
25248      05  PV-KOHLS-UPC-HIGH.                                               
25248          10  FILLER              PIC 9(04)    VALUE 0400.                 
25248          10  PV-KOHLS-SKU-MAX    PIC 9(08)    VALUE ZEROS.                
25248          10  FILLER              PIC 9(01)    VALUE 9.                    
25248      05  PV-KOHLS-SIGN-UPC.                                               
25248          10  PV-KOHLS-UPC-MIN-SIGNONLY                                    
25248                                  PIC S9(13).                              
25248          10  PV-KOHLS-UPC-MAX-SIGNONLY                                    
25248                                  PIC S9(13).                              
25248      05  PV-KOHLS-MDSEAR-UPC.                                             
25248          10  PV-KOHLS-UPC-MIN    PIC S9(13)V  COMP-3.                     
25248          10  PV-KOHLS-UPC-MAX    PIC S9(13)V  COMP-3.                     
25248      05  PV-TEMP-UPC-MIN.                                                 
25248          10  PV-TEMP-UPC-DIGIT-MIN       PIC 9(12) VALUE ZERO.            
25248          10  PV-TEMP-UPC-CHECK-DIGIT-MIN PIC 9(01) VALUE ZERO.            
25248      05  PV-TEMP-UPC-MAX.                                                 
25248          10  PV-TEMP-UPC-DIGIT-MAX       PIC 9(12) VALUE ZERO.            
25248          10  PV-TEMP-UPC-CHECK-DIGIT-MAX PIC 9(01) VALUE ZERO.            
25248                                                                           
25248  01  PV-UPC-ERROR-FILE-RECORD.                                            
25248      05  PV-UPC-ERROR-STORE-TEXT PIC X(07)  VALUE 'STORE: '.         CL*12
25248      05  PV-UPC-ERROR-STORE      PIC X(03).                          CL*12
25248      05  FILLER                  PIC X(03)  VALUE SPACES.            CL*12
25248      05  PV-UPC-ERROR-AREA-TEXT  PIC X(06)  VALUE 'AREA: '.          CL*12
25248      05  PV-UPC-ERROR-AREA       PIC X(06).                          CL*12
25248      05  FILLER                  PIC X(03)  VALUE SPACES.            CL*12
25248      05  PV-UPC-ERROR-SKU-TEXT   PIC X(05)  VALUE 'SKU: '.           CL*12
25248      05  PV-UPC-ERROR-SKU        PIC X(08).                          CL*12
25248      05  FILLER                  PIC X(03)  VALUE SPACES.            CL*12
25248      05  PV-UPC-ERROR-UPC-TEXT   PIC X(05)  VALUE 'UPC: '.           CL*12
25248      05  PV-UPC-ERROR-UPC        PIC X(15).                          CL*12
25248      05  FILLER                  PIC X(05)  VALUE SPACES.            CL*12
25248                                                                           
10401  01  STORE-TABLE.                                                         
W26600     05  ST-STORE-TAB     OCCURS 9999 TIMES.                              
W26600         10  ST-STORE-NUMBER     PIC X(04).                               
25248          10  ST-STORE-NUMBER-NUM REDEFINES                                
W26600             ST-STORE-NUMBER     PIC 9(04).                               
               10  ST-STORE-TYPE-CODE  PIC X(02).                               
               10  ST-E-BUS-LOC-IND    PIC X(01).                               
               10  ST-STORE-INV-DATE   PIC X(10).                               
                                                                        05220702
10401  01  SS-SUBSCRIPTS.                                               05220702
           05  SS-STR-NBR              PIC S9(04) COMP SYNC             05220802
                                                          VALUE ZERO.   05221002
      *                                                                         
       01  SA-SAVE-AREAS.                                                       
           05  SA-STORE-NUMBER         PIC  X(03)   VALUE SPACES.               
W21732     05  SA-DEPT-CL              PIC  9(12)   VALUE ZERO.                 
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW       PIC X        VALUE 'N'.                  
               88  PS-END-OF-FILE                   VALUE 'Y'.                  
           05  PS-END-OF-INVENTORY     PIC X        VALUE 'N'.                  
               88  PS-EOF-INVENTORY                 VALUE 'Y'.                  
           05  PS-INVALID-RECORD-SW    PIC X        VALUE 'Y'.                  
               88  PS-VALID-RECORD                  VALUE 'N'.                  
               88  PS-INVALID-RECORD                VALUE 'Y'.                  
           05  PS-INVALID-ITEM-SW      PIC X        VALUE 'Y'.                  
               88  PS-VALID-ITEM                    VALUE 'N'.                  
               88  PS-INVALID-ITEM                  VALUE 'Y'.                  
25248      05  PS-INVALID-UPC-SW       PIC X        VALUE 'Y'.                  
25248          88  PS-VALID-UPC                     VALUE 'N'.                  
25248          88  PS-INVALID-UPC                   VALUE 'Y'.                  
           05  PS-VALID-DEPT-CLASS-SW  PIC X        VALUE 'N'.                  
               88  PS-VALID-DEPT-CLASS              VALUE 'Y'.                  
               88  PS-INVALID-DEPT-CLASS            VALUE 'N'.                  
           05  PS-STATUS-FOUND-SW      PIC X        VALUE 'N'.                  
               88  PS-STATUS-FOUND                  VALUE 'Y'.                  
               88  PS-STATUS-NOT-FOUND              VALUE 'N'.                  
           05  PS-ITEM-FOUND-SW        PIC X        VALUE 'N'.                  
               88  PS-ITEM-FOUND                    VALUE 'Y'.                  
               88  PS-ITEM-NOT-FOUND                VALUE 'N'.                  
           05  PS-RENUMBER-SW          PIC X        VALUE 'Y'.                  
               88  PS-RENUMBER-SKU                  VALUE 'N'.                  
               88  PS-NOT-RENUMBER-SKU              VALUE 'Y'.                  
10401      05  PS-RENUM-SKU-SW         PIC X        VALUE 'N'.                  
               88  PS-RENUM-SKU-FOUND               VALUE 'Y'.                  
               88  PS-RENUM-SKU-NOT-FOUND           VALUE 'N'.                  
W21732*****                                                                     
W21732*                                                                         
W21732     COPY ISWS006.                                                        
W21732     EJECT                                                                
W21732*                                                                         
      *****                                                                     
      *                                                                         
           COPY INWS010.                                                        
           EJECT                                                                
      *                                                                         
      *****                                                                     
      *                                                                         
           COPY INWS009.                                                        
           EJECT                                                                
      *                                                                         
      *****                                                                     
      *                                                                         
           COPY INWS008.                                                        
W26600*01  PV-SPECIAL-IN008-LAYOUT        REDEFINES                             
W26600*    IN008-INVENTORY-COUNT-RECORD.                                        
W26600*    05  PV-OUTPUT-RECORD.                                                
W21732*        10  FILLER                 PIC X(85).                            
W21732*        10  FILLER                 PIC X(01).                            
W26600*        10  PV-OUTPUT-INVALID-REC  PIC X(01).                            
W26600*        10  PV-OUTPUT-INVALID-ITEM PIC X(01).                            
           EJECT                                                                
      *                                                                         
      *****                                                                     
           COPY INWS014.                                                        
           EJECT                                                                
                                                                                
25248 *****  WORKING STORAGE COPYBOOK FOR UPC CHECK DIGIT ROUTINE       INKUT102
25248      COPY DPWS040I.                                               INKUT102
25248 *                                                                 INKUT102
      *****  SQL ABEND ROUTINE WORKING STORAGE                                  
           COPY DPWS004.                                                        
      *                                                                         
P28545*****  DB2 MERCHANDISE AREA TABLE DCLGEN                                  
P28545*    EXEC SQL                                                             
P28545*        INCLUDE TMDSEAR                                                  
P28545*    END-EXEC.                                                            
P28545*                                                                         
P28545*****  DB2 APPLICATION AVAILABILITY TABLE DCLGEN                          
P28545*                                                                         
P28545*    EXEC SQL                                                             
P28545*        INCLUDE TAPLAVL                                                  
P28545*    END-EXEC.                                                            
P28545*                                                                         
      *****  DB2 INVENTORY PARAMETERS TABLE DCLGEN                              
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
P28545*****  DB2 INVENTORY CONTROL TABLE DCLGEN                                 
P28545*                                                                         
P28545     EXEC SQL                                                             
P28545         INCLUDE TINCNTL                                                  
P28545     END-EXEC.                                                            
P28545*                                                                         
      *****  DB2 LOCATION PARAMETERS TABLE DCLGEN                               
25248                                                                           
  |        EXEC SQL                                                             
  |            INCLUDE XMT00001                                                 
  |        END-EXEC.                                                            
25248                                                                           
P28545******************************************************************        
P28545* COPYBOOK - PRICING MODULE                                               
P28545******************************************************************        
P28545     COPY INWS400.                                                        
P28545                                                                          
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
       01  ABEND-CODE               PIC S9(04)  VALUE ZERO                      
                                                COMP SYNC.                      
           EJECT                                                                
       LINKAGE SECTION.                                                         
      *                                                                         
       PROCEDURE DIVISION.                                                      
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       1000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1100-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-EDIT-INVENTORY UNTIL                                    
               PS-EOF-INVENTORY.                                                
                                                                                
           PERFORM 1300-CLOSE-FILES.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       1100-INITIALIZATION.                                                     
      *                                                                         
           OPEN INPUT  PHYSICAL-INVENTORY-FILE                                  
10401                  STORE-PROCESS-FILE                                       
                OUTPUT VALID-INV-REPORT-FILE                                    
                       EDITED-INVENTORY-FILE                                    
25248                  MISSING-TABLE-ENTRY-FILE                         INKUT102
                       INVALID-DETAIL-FILE.                                     
      *                                                                         
           ACCEPT CC-CURRENT-DATE FROM DATE.                                    
                                                                                
           MOVE CC-DATE-YY        TO CC-DB2-YY.                                 
           MOVE CC-DATE-MM        TO CC-DB2-MM.                                 
           MOVE CC-DATE-DD        TO CC-DB2-DD.                                 
                                                                                
           MOVE CC-DB2-DATE       TO PV-TEMP-DATE.                              
      *                                                                         
10401      PERFORM 5000-PROCESS-STORE-FILE                                      
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           PERFORM 4100-READ-INVENTORY-FILE.                                    
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       2000-EDIT-INVENTORY.                                                     
                                                                                
           SET PS-VALID-ITEM                                                    
               PS-VALID-RECORD                                                  
25248          PS-VALID-UPC                                                     
               PS-RENUMBER-SKU  TO TRUE.                                        
                                                                                
P28545     INITIALIZE IN400-PV-INPUT-AREA.                                      
           INITIALIZE IN009-EDITED-INVENTORY-RECORD.                            
                                                                                
           IF IN010-PRICE > ZEROES                                              
               MOVE IN010-STORE-NUMBER      TO IN009-STORE-NUMBER               
               MOVE IN010-AREA-NUMBER       TO IN009-AREA-NUMBER                
               MOVE IN010-LINE-NUMBER       TO IN009-LINE-NUMBER                
               MOVE IN010-SCAN-LINE-NUMBER  TO IN009-SCAN-LINE-NUMBER           
               MOVE IN010-SCAN-DTE          TO IN009-SCAN-DTE                   
               MOVE IN010-SCAN-TIMESTAMP    TO IN009-SCAN-TIMESTAMP             
               MOVE IN010-QUANTITY          TO IN009-QUANTITY-COMP              
               MOVE IN010-PRICE             TO IN009-PRICE                      
               MOVE SPACES                  TO IN009-MISSED-MARKDOWN-IND        
           ELSE                                                                 
               SET PS-INVALID-RECORD TO TRUE                                    
           END-IF.                                                              
                                                                                
10401      MOVE IN010-STORE-NUMBER TO PV-STR-NBR.                               
10401      MOVE PV-STR-NBR         TO SS-STR-NBR.                               
                                                                                
25248      IF IN010-UPC-CODE IS NOT EQUAL TO ZEROS                              
  |            PERFORM 10000-EDIT-UPC-CODE                                      
  |        ELSE                                                                 
  |            PERFORM 15000-EDIT-SKU                                           
  |        END-IF.                                                              
25248                                                                           
           IF PS-VALID-ITEM  AND                                                
25248         PS-VALID-UPC   AND                                                
              PS-VALID-RECORD                                                   
RONNAX         IF IN400-PRC-UNIT-RTL-OUT NOT = IN010-PRICE                      
RONNAX            MOVE IN400-PRC-UNIT-RTL-OUT                                   
RONNAX                                 TO IN009-PRICE                           
RONNAX*           DISPLAY 'RENUM SKU EDIT PRICE CHANGE SKU: '                   
RONNAX*                IN010-SKU    ' UPC :  '                                  
RONNAX*                IN010-UPC-CODE ' OLD PRICE: '                            
RONNAX*                IN010-PRICE  ' NEW PRICE: ' IN009-PRICE                  
RONNAX         END-IF                                                           
               PERFORM 6100-WRITE-EDITED-RECORD                                 
               MOVE IN010-QUANTITY TO IN009-QUANTITY                    05090000
               PERFORM 6200-PREP-VALID-RPT-RECORD                               
           ELSE                                                                 
W26600         INITIALIZE              IN008-INVENTORY-COUNT-RECORD             
W26600         MOVE IN010-STORE-NUMBER TO IN008-STORE-NUMBER                    
W26600         MOVE IN010-AREA-NUMBER  TO IN008-AREA-NUMBER                     
W26600         MOVE IN010-LINE-NUMBER  TO IN008-LINE-NUMBER                     
W26600         MOVE IN010-SCAN-LINE-NUMBER                                      
W26600                                 TO IN008-SCAN-LINE-NUMBER                
W26600         MOVE IN010-SKU          TO IN008-SKU                             
W26600         MOVE IN010-QUANTITY     TO IN008-QUANTITY                        
W26600         MOVE IN010-PRICE        TO IN008-PRICE                           
W26600         MOVE IN010-UPC-CODE     TO IN008-UPC-CODE                        
W26600         MOVE IN010-INVALID-INDICATOR                                     
W26600                                 TO IN008-INVALID-INDICATOR               
W26600         MOVE IN010-SCAN-DTE     TO IN008-SCAN-DTE                        
W26600         MOVE IN010-SCAN-TIMESTAMP                                        
W26600                                 TO IN008-SCAN-TIMESTAMP                  
W26600         MOVE PS-INVALID-RECORD-SW                                        
W26600                                 TO IN008-INVALID-RCD                     
W26600         MOVE PS-INVALID-ITEM-SW TO IN008-INVALID-ITEM                    
               PERFORM 6400-WRITE-INVALID-DETAIL                                
           END-IF.                                                              
                                                                                
           PERFORM 4100-READ-INVENTORY-FILE.                                    
      *                                                                         
      ******************************************************************        
      *  CLOSE ALL FILES USED BY THIS PROGRAM.                                  
      ******************************************************************        
       1300-CLOSE-FILES.                                                        
      *                                                                         
           CLOSE       PHYSICAL-INVENTORY-FILE                                  
                       VALID-INV-REPORT-FILE                                    
                       EDITED-INVENTORY-FILE                                    
25248                  MISSING-TABLE-ENTRY-FILE                         INKUT102
                       INVALID-DETAIL-FILE.                                     
      *                                                                         
      ******************************************************************        
      * 3000-READ-TINVPAR                                                       
      ******************************************************************        
10401  3000-READ-TINVPAR.                                                       
                                                                                
25248      MOVE CC-STORE-NBR-NUM  TO INVPAR-LOC-NBR.                            
25248                                                                           
           EXEC SQL                                                             
                                                                                
               SELECT ACTL_INV_DTE                                              
                 INTO :INVPAR-ACTL-INV-DTE                                      
P28545           FROM TINVPAR A,                                                
P28545                TINCNTL B                                                 
P28545         WHERE B.ACTV_IND = 'Y'                                           
W33304           AND A.LOC_INV_STAT_CDE = 'IN'                                  
P28545           AND A.INV_ID = B.INV_ID                                        
W33304           AND A.LOC_NBR         = :INVPAR-LOC-NBR                        
W33304           AND A.ACTL_FIN_BK_DTE = '9999-09-09'                           
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   MOVE INVPAR-ACTL-INV-DTE TO                                  
                                      ST-STORE-INV-DATE (SS-STR-NBR)            
               WHEN OTHER                                                       
25248              DISPLAY 'INVALID STORE ' CC-STORE-NBR-NUM                    
                   MOVE '3000-READ-TINVPAR'                                     
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'SELECT ACTL_INV_DATE'                                  
                       TO PV-DB2-OPER-ATTEMPTED                                 
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      ******************************************************************        
      * RETRIEVE STORE TYPE CODE (SPECIAL PROCESSING FOR 'CS' AND 'RV')*        
      ******************************************************************        
SJ0301 3100-READ-XMTLOC.                                                        
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
               UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                          
                 OR  (SQLCODE = +100)                                           
                 OR  (SQLCODE = 0)                                              
                                                                                
25248          MOVE CC-STORE-NBR-NUM  TO PV-EDIT-LOC-NBR                        
  |            MOVE PV-EDIT-LOC-NBR   TO XMT00001-LOC-NBR                       
  |                                                                             
  |            EXEC SQL                                                         
  |                SELECT LOC_TYP_CDE                                           
  |                      ,LOC_NBR                                               
  |                      ,E_BUS_IND                                             
  |                INTO :XMT00001-LOC-TYP-CDE                                   
  |                    ,:XMT00001-LOC-NBR                                       
  |                    ,:XMT00001-E-BUS-IND                                     
  |                FROM XMT_LOC                                                 
  |                WHERE LOC_NBR = :XMT00001-LOC-NBR                            
  |            END-EXEC                                                         
25248                                                                           
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
25248                  MOVE XMT00001-LOC-NBR      TO SS-STR-NBR         09311107
W26600                 MOVE XMT00001-LOC-NBR                                    
W26600                                 TO ST-STORE-NUMBER-NUM                   
W26600                                    (SS-STR-NBR)                          
25248 *                MOVE XMT00001-LOC-NBR TO PV-EDIT-LOC-NBR-NUM             
25248 *                MOVE PV-EDIT-LOC-NBR-ALF (2:3) TO                        
      *                                 ST-STORE-NUMBER (SS-STR-NBR)            
25248                  MOVE XMT00001-LOC-TYP-CDE  TO                            
                                        ST-STORE-TYPE-CODE (SS-STR-NBR)         
25248                  MOVE XMT00001-E-BUS-IND    TO                            
W21732                                  ST-E-BUS-LOC-IND (SS-STR-NBR)           
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < 0                                             
                   WHEN SQLCODE > 0                                             
                        MOVE '3100-READ-STORE-TABLE'                            
                              TO PV-PARAGRAPH-NAME                              
25248                   MOVE 'READ ON XMT_LOC TBL'                              
                              TO PV-DB2-OPER-ATTEMPTED                          
                        PERFORM 9900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
                MOVE '3100-READ-STORE-TABLE'  TO PV-PARAGRAPH-NAME              
                MOVE 'EXCEEDED MAX RETRIES' TO PV-DB2-OPER-ATTEMPTED            
                PERFORM 9900-SQL-ABEND                                          
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * 4000-READ-STORE-FILE                                                    
      * READS THE FILE THAT HAS STORES TO BE PROCESSED IN THIS RUN              
      ******************************************************************        
       4000-READ-STORE-FILE.                                                    
                                                                                
           READ STORE-PROCESS-FILE                                              
               INTO CC-STORE-NBR                                                
                   AT END                                                       
                       MOVE 'Y' TO PS-END-OF-FILE-SW.                           
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 4100-READ-INVENTORY-FILE                                                
      * READS UNEDITTED PHYSICAL FILE TO BE INCLUDED AS COUNTED DATA            
      ******************************************************************        
       4100-READ-INVENTORY-FILE.                                                
                                                                                
           READ PHYSICAL-INVENTORY-FILE                                         
               INTO IN010-INVENTORY-COUNT-RECORD                                
                   AT END                                                       
                       MOVE 'Y' TO PS-END-OF-INVENTORY.                         
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
10401  5000-PROCESS-STORE-FILE.                                                 
                                                                                
           PERFORM 4000-READ-STORE-FILE.                                        
                                                                                
           IF NOT PS-END-OF-FILE                                                
               PERFORM 5100-LOAD-STORE-TABLE                                    
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
10401  5100-LOAD-STORE-TABLE.                                                   
                                                                                
           PERFORM 3100-READ-XMTLOC.                                            
           PERFORM 3000-READ-TINVPAR.                                           
                                                                                
      *    SET ACTL_INV_RECD_DTE FOR CENTRAL STOCK AND RTV LOCATIONS            
IN2002*    SET ACTL_INV_RECD_DTE FOR DC'S ALSO                                  
W21732     IF (ST-STORE-TYPE-CODE(SS-STR-NBR) = 'CS' OR 'RV' OR 'DC')           
W21732         OR (ST-STORE-TYPE-CODE(SS-STR-NBR) = 'DS' AND                    
W21732             ST-E-BUS-LOC-IND(SS-STR-NBR)   = 'Y')                        
                MOVE ST-STORE-NUMBER (SS-STR-NBR) TO CC-STORE-NBR               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * RECORDS WHICH SURVIVED THE EDITS ARE WRITTEN TO THE OUTPUT FILE.        
      ******************************************************************        
       6100-WRITE-EDITED-RECORD.                                                
           WRITE EDITED-INVENTORY-RECORD                                        
               FROM IN009-EDITED-INVENTORY-RECORD.                              
                                                                                
      ****************************************************************          
      *                                                                         
      ****************************************************************          
       6200-PREP-VALID-RPT-RECORD.                                              
      *                                                                         
           IF IN009-DEPT-CLASS NOT EQUAL SA-DEPT-CL                             
               MOVE IN009-DEPT-CLASS TO SA-DEPT-CL                              
      *                                                                         
W21732         MOVE IN009-DEPT            TO NI-DEPT-9-3                        
W21732         MOVE NI-DEPT-X-3           TO MDSEAR-DEPT-NBR                    
W21732         MOVE IN009-SUB-CLASS (3:2) TO PV-MAJ-CL-NBR                      
W21732                                       PV-SUB-CL-NBR-2-3                  
               MOVE '0'                   TO PV-SUB-CL-NBR-1                    
                                             PV-MAJ-CL-NBR-2                    
W21732         MOVE PV-SUB-CL-NBR  TO MDSEAR-SUB-CL-NBR                         
                                                                                
W21732         MOVE PV-MAJ-CL-NBR  TO MDSEAR-MAJ-CL-NBR                         
      *                                                                         
               PERFORM Z800-READ-MBS                                            
               IF PS-VALID-DEPT-CLASS                                           
                   MOVE MDSEAR-DESC      TO PV-HOLD-MAJ-CL-DESC                 
                   MOVE MDSEAR-BYR-NBR   TO PV-HOLD-BYR-NBR                     
                   MOVE MDSEAR-GMA-NBR   TO PV-HOLD-GMA-NBR                     
                   MOVE MDSEAR-DMA-NBR   TO PV-HOLD-DMA-NBR                     
               ELSE                                                             
                   MOVE +4008                 TO ABEND-CODE                     
                   DISPLAY '**  ABEND     **'                                   
                   DISPLAY '**  PROGRAM   **  =  INKUT134'                      
                   DISPLAY 'INVALID DEPT CLASS: ' IN009-DEPT-CLASS              
                   CALL 'ILBOABN0'  USING ABEND-CODE                            
               END-IF                                                           
           END-IF.                                                              
           MOVE IN010-INV-DATE                TO IN014-INV-DATE.                
25248      MOVE IN009-VALID-RPT-KEY-AREA                                        
W21732                                  TO IN014-VALID-RPT-KEY-AREA.            
W21732     MOVE IN009-DEPT                    TO IN014-DEPT.                    
W21732     MOVE IN009-SUB-CLASS               TO IN014-CLASS.                   
           MOVE PV-HOLD-MAJ-CL-DESC           TO IN014-CLASS-DESC.              
           MOVE PV-HOLD-BYR-NBR               TO IN014-BYR.                     
           MOVE PV-HOLD-GMA-NBR               TO IN014-GMA.                     
           MOVE PV-HOLD-DMA-NBR               TO IN014-DMA.                     
25248      MOVE IN009-UPC-CODE                TO IN014-UPC-CODE.                
           PERFORM 6300-WRITE-VALID-RPT-REC.                                    
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       6300-WRITE-VALID-RPT-REC.                                                
           WRITE VALID-INV-REPORT-RECORD                                        
               FROM IN014-VALID-REPORT-RECORD.                                  
                                                                                
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       6400-WRITE-INVALID-DETAIL.                                               
           WRITE INVALID-DETAIL-RECORD                                          
               FROM IN008-INVENTORY-COUNT-RECORD.                               
                                                                                
                                                                                
       Z800-READ-MBS.                                                           
      ******************************************************************        
      *                                                                         
      ******************************************************************        
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
               UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                          
                   OR (SQLCODE = 0)                                             
                   OR (SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                    SELECT GMA_NBR,                                             
                           DMA_NBR,                                             
                           MDSEAR_DESC,                                         
                           BYR_NBR                                              
                    INTO   :MDSEAR-GMA-NBR,                                     
                           :MDSEAR-DMA-NBR,                                     
                           :MDSEAR-DESC,                                        
                           :MDSEAR-BYR-NBR                                      
                    FROM   TMDSEAR A,                                           
                           TAPLAVL B                                            
                    WHERE  (A.OPERCO_NBR  = '03')                 AND           
W21732                     (A.DEPT_NBR    = :MDSEAR-DEPT-NBR)     AND           
W21732                     (A.MAJ_CL_NBR  = :MDSEAR-MAJ-CL-NBR)   AND           
W21732                     (A.SUB_CL_NBR  = :MDSEAR-SUB-CL-NBR)   AND           
                           (A.MDSEAR_DKEY = B.MDSEAR_DKEY)        AND           
                           (MDSELV_CDE    = 'SCL')                AND           
                           (APLSYS_CDE    = 'IN')                 AND           
                           (DATE (CURRENT TIMESTAMP) BETWEEN                    
                                            STRT_DTE AND END_DTE) AND           
                           (DATE (CURRENT TIMESTAMP) BETWEEN                    
                             MDSEAR_STRT_DTE AND MDSEAR_END_DTE)                
                                                                                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       SET PS-VALID-DEPT-CLASS TO TRUE                          
                   WHEN SQLCODE = +100                                          
                       SET PS-INVALID-DEPT-CLASS TO TRUE                        
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE 'Z800-READ-MBS' TO PV-PARAGRAPH-NAME                
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE 'Z800-READ-MBS' TO PV-PARAGRAPH-NAME                        
               MOVE 'EXCEEDED MAX NUMBER OF RETRIES'                            
                              TO PV-DB2-OPER-ATTEMPTED                          
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUT134'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
25248 /*****************************************************************        
  |   * 10000-EDIT-UPC-CODE.                                                    
  |   *     EDIT THE UPC NUMBER WHEN UPC IS PASSED FRO THE SOURCE SYSTEM        
  |   * THE EDIT PROCESS FOLLOWS INKUT102 EXCEPT FOR PRICING. THE PRICE         
  |   * IS ASSUMED TO BE ACCURATE FROM THE SOURCE SYSTEM. SHEETS, RTV,          
  |   * CENTRAL STOCK.                                                          
  |   ******************************************************************        
  |     10000-EDIT-UPC-CODE.                                                    
  |                                                                             
  |         SET PS-NOT-RENUMBER-SKU TO TRUE.                            INKUT102
  |                                                                             
  |         MOVE IN010-UPC-CODE     TO PV-WORK-UPC.                             
25248                                                                           
P28545      SET IN400-PV-UPC-NBR-LOOKUP TO TRUE.                                
RONNAX      MOVE INVPAR-ACTL-INV-DTE TO IN400-PV-EFF-END-DTE-IN.                
P28545      MOVE IN010-STORE-NUMBER TO IN400-CONVERT-LOC.                       
P28545      MOVE IN400-CONVERT-LOC  TO IN400-PV-LOC-IN.                         
P28545                                                                          
P28545      MOVE IN010-STORE-NUMBER TO PV-STR-NBR.                              
P28545      MOVE PV-STR-NBR         TO SS-STR-NBR.                              
P28545      MOVE ST-STORE-INV-DATE (SS-STR-NBR) TO IN400-PV-EFF-DTE-IN.         
P28545                                                                          
P28545      MOVE IN010-UPC-CODE     TO IN400-PV-UPC-NBR-IN.                     
P28545      MOVE IN010-SKU          TO IN400-PV-SKU-IN.                         
P28545                                                                          
P28545      PERFORM IN400-LOOKUP-RETAIL.                                        
P28545                                                                          
P28545*** A.  INVALID KEY CONDITION..                                           
P28545     IF IN400-PV-INVALID-KEY-INPUT                                        
P28545         SET PS-INVALID-ITEM                                      INKUT102
P28545             PS-INVALID-RECORD                                    INKUT102
P28545             PS-INVALID-UPC  TO TRUE                              INKUT102
P28545         DISPLAY 'INVALID - ', IN010-UPC-CODE,                            
P28545             ' LOC - ', IN010-STORE-NUMBER,                               
P28545             ' PRC RTN - ', IN400-PV-RETURN-CDE,                          
P28545             ' ATT RTN - ', IN400-PV-ATT-RTNCDE,                          
P28545             ' PRC SQL - ', IN400-PV-PRC-SQLCDE,                          
P28545             ' ATT SQL - ', IN400-PV-ATT-SQLCDE,                          
P28545         DISPLAY IN400-PV-RETURN-RSN                                      
P28545         DISPLAY IN400-PV-PARAGRAPH                                       
P28545     ELSE                                                                 
P28545*** B.  ZERO SEQUENCE SKU EDITS.                                          
P28545     IF IN400-PS-DUMMY-SKU                                                
P28545         SET PS-VALID-UPC TO TRUE                                 INKUT102
P28545         MOVE IN400-ATT-DEPT-OUT TO PV-EDIT-DEPT                          
P28545         MOVE PV-EDIT-DEPT       TO IN009-DEPT                            
P28545         MOVE IN400-ATT-MCL-OUT  TO PV-EDIT-MCL                           
P28545         MOVE PV-EDIT-MCL        TO IN009-MAJ-CLASS                       
P28545         MOVE IN400-ATT-SCL-OUT  TO PV-EDIT-SCL                           
P28545         MOVE PV-EDIT-SCL        TO IN009-SUB-CLASS                       
P28545         MOVE IN400-PRC-SKU-OUT  TO PV-EDIT-SKU                           
P28545         MOVE PV-EDIT-SKU        TO IN009-SKU                             
Z28545         MOVE PV-WORK-UPC        TO IN009-UPC-CODE                INKUT102
P28545     ELSE                                                                 
P28545*** C.  RENUMBER SKU EDITS.                                               
P28545     IF IN400-PS-RENUMBER-SKU                                             
P28545          SET PS-VALID-UPC TO TRUE                                INKUT102
P28545         MOVE IN400-ATT-DEPT-OUT TO PV-EDIT-DEPT                          
P28545         MOVE PV-EDIT-DEPT       TO IN009-DEPT                            
P28545         MOVE IN400-ATT-MCL-OUT  TO PV-EDIT-MCL                           
P28545         MOVE PV-EDIT-MCL        TO IN009-MAJ-CLASS                       
P28545         MOVE IN400-ATT-SCL-OUT  TO PV-EDIT-SCL                           
P28545         MOVE PV-EDIT-SCL        TO IN009-SUB-CLASS                       
P28545         MOVE IN400-PRC-SKU-OUT  TO PV-EDIT-SKU                           
P28545         MOVE PV-EDIT-SKU        TO IN009-SKU                             
Z28545         MOVE PV-WORK-UPC        TO IN009-UPC-CODE                INKUT102
P28545      ELSE                                                                
P28545*** D.  REGULAR SKU EDITS.                                                
P28545      IF IN400-PS-RTL-FOUND OR                                            
P28545         IN400-PS-ATTR-FOUND                                              
P28545          SET PS-VALID-UPC TO TRUE                                INKUT102
P28545          MOVE IN400-ATT-DEPT-OUT TO PV-EDIT-DEPT                         
P28545          MOVE PV-EDIT-DEPT       TO IN009-DEPT                           
P28545          MOVE IN400-ATT-MCL-OUT  TO PV-EDIT-MCL                          
P28545          MOVE PV-EDIT-MCL        TO IN009-MAJ-CLASS                      
P28545          MOVE IN400-ATT-SCL-OUT  TO PV-EDIT-SCL                          
P28545          MOVE PV-EDIT-SCL        TO IN009-SUB-CLASS                      
P28545          MOVE IN400-PRC-SKU-OUT  TO PV-EDIT-SKU                          
P28545          MOVE PV-EDIT-SKU        TO IN009-SKU                            
Z28545         MOVE PV-WORK-UPC        TO IN009-UPC-CODE                INKUT102
P28545     ELSE                                                                 
P28545         SET PS-INVALID-ITEM                                      INKUT102
P28545             PS-INVALID-RECORD                                    INKUT102
P28545             PS-INVALID-UPC  TO TRUE                              INKUT102
P28545     END-IF.                                                              
P28545                                                                          
W26342/*****************************************************************        
  |   * 15000-EDIT-SKU.                                                         
  |   *     EDIT THE SKU NUMBER WHEN UPC IS ZEROS AND SKU HAS BEEN              
  |   * PASSED FROM THE SOURCE SYSTEM. SKU EDITS FOLLOW THE EXISTING            
  |   * SHEET, RTC, CENTRAL STOCK LOGIC.                                        
  |   ******************************************************************        
  |     15000-EDIT-SKU.                                                         
  |                                                                             
  |         MOVE IN010-SKU          TO PV-HOLD-SKU                              
  |                                    SKXREF-SKU.                              
  |         MOVE IN010-DEPT         TO PV-HOLD-DEPT.                            
  |         MOVE IN010-CLASS        TO PV-HOLD-SUB-CLASS.                       
  |         MOVE IN010-UPC-CODE     TO PV-HOLD-UPC.                             
W26342                                                                          
P28545      SET PS-NOT-RENUMBER-SKU TO TRUE.                            INKUT102
P28545                                                                          
P28545      SET IN400-PV-SKU-LOOKUP TO TRUE.                                    
P28545      MOVE IN010-STORE-NUMBER TO IN400-CONVERT-LOC.                       
P28545      MOVE IN400-CONVERT-LOC  TO IN400-PV-LOC-IN.                         
P28545                                                                          
P28545      MOVE IN010-STORE-NUMBER TO PV-STR-NBR.                              
P28545      MOVE PV-STR-NBR         TO SS-STR-NBR.                              
P28545      MOVE ST-STORE-INV-DATE (SS-STR-NBR) TO IN400-PV-EFF-DTE-IN.         
P28545                                                                          
P28545      MOVE IN010-SKU          TO IN400-PV-SKU-IN.                         
P28545      MOVE IN010-UPC-CODE     TO IN400-PV-UPC-NBR-IN.                     
P28545                                                                          
P28545      PERFORM IN400-LOOKUP-RETAIL.                                        
P28545                                                                          
P28545*** A.  INVALID KEY CONDITION..                                           
P28545     IF IN400-PV-INVALID-KEY-INPUT                                        
P28545          SET PS-INVALID-ITEM                                             
P28545              PS-INVALID-RECORD TO TRUE                                   
P28545          DISPLAY 'INVALID - ', IN010-SKU,                                
P28545              ' LOC - ', IN010-STORE-NUMBER,                              
P28545              ' PRC RTN - ', IN400-PV-RETURN-CDE,                         
P28545              ' ATT RTN - ', IN400-PV-ATT-RTNCDE,                         
P28545              ' PRC SQL - ', IN400-PV-PRC-SQLCDE,                         
P28545              ' ATT SQL - ', IN400-PV-ATT-SQLCDE,                         
P28545          DISPLAY IN400-PV-RETURN-RSN                                     
P28545          DISPLAY IN400-PV-PARAGRAPH                                      
P28545     ELSE                                                                 
P28545*** B.  ZERO SEQUENCE SKU EDITS.                                          
P28545     IF IN400-PS-DUMMY-SKU                                                
P28545         MOVE IN400-ATT-DEPT-OUT TO PV-EDIT-DEPT                          
P28545         MOVE PV-EDIT-DEPT       TO IN009-DEPT                            
P28545         MOVE IN400-ATT-MCL-OUT  TO PV-EDIT-MCL                           
P28545         MOVE PV-EDIT-MCL        TO IN009-MAJ-CLASS                       
P28545         MOVE IN400-ATT-SCL-OUT  TO PV-EDIT-SCL                           
P28545         MOVE PV-EDIT-SCL        TO IN009-SUB-CLASS                       
RONNAZ*        MOVE IN400-PRC-SKU-OUT  TO PV-EDIT-SKU                           
RONNAZ         MOVE IN400-ATT-SKU-OUT  TO PV-EDIT-SKU                           
P28545         MOVE PV-EDIT-SKU        TO IN009-SKU                             
Z28545         MOVE IN010-UPC-CODE     TO IN009-UPC-CODE                INKUT102
P28545     ELSE                                                                 
P28545*** C.  RENUMBER SKU EDITS.                                               
P28545     IF IN400-PS-RENUMBER-SKU                                             
P28545         MOVE IN400-ATT-DEPT-OUT TO PV-EDIT-DEPT                          
P28545         MOVE PV-EDIT-DEPT       TO IN009-DEPT                            
P28545         MOVE IN400-ATT-MCL-OUT  TO PV-EDIT-MCL                           
P28545         MOVE PV-EDIT-MCL        TO IN009-MAJ-CLASS                       
P28545         MOVE IN400-ATT-SCL-OUT  TO PV-EDIT-SCL                           
P28545         MOVE PV-EDIT-SCL        TO IN009-SUB-CLASS                       
P28545         MOVE IN400-PRC-SKU-OUT  TO PV-EDIT-SKU                           
P28545         MOVE PV-EDIT-SKU        TO IN009-SKU                             
Z28545         MOVE IN010-UPC-CODE     TO IN009-UPC-CODE                INKUT102
P28545         DISPLAY '  STORE : ', IN010-STORE-NUMBER,                        
P28545          ' - INPUT SKU ', PV-HOLD-SKU,                                   
P28545          ' RENUMBERED TO DMS ', PV-EDIT-DEPT,                            
P28545          '-'                  , PV-EDIT-MCL,                             
P28545          '-'                  , PV-EDIT-SCL,                             
P28545          ' SKU '              , PV-EDIT-SKU                              
P28545          ' UPC '              , IN010-UPC-CODE                           
P28545      ELSE                                                                
P28545*** D.  REGULAR SKU EDITS.                                                
P28545      IF IN400-PS-RTL-FOUND OR                                            
P28545         IN400-PS-ATTR-FOUND                                              
P28545         MOVE IN400-ATT-DEPT-OUT TO PV-EDIT-DEPT                          
P28545         MOVE PV-EDIT-DEPT       TO IN009-DEPT                            
P28545         MOVE IN400-ATT-MCL-OUT  TO PV-EDIT-MCL                           
P28545         MOVE PV-EDIT-MCL        TO IN009-MAJ-CLASS                       
P28545         MOVE IN400-ATT-SCL-OUT  TO PV-EDIT-SCL                           
P28545         MOVE PV-EDIT-SCL        TO IN009-SUB-CLASS                       
P28545         MOVE IN400-PRC-SKU-OUT  TO PV-EDIT-SKU                           
P28545         MOVE PV-EDIT-SKU        TO IN009-SKU                             
Z28545         MOVE IN010-UPC-CODE     TO IN009-UPC-CODE                INKUT102
P28545     ELSE                                                                 
P28545          SET PS-INVALID-ITEM                                             
P28545              PS-INVALID-RECORD TO TRUE                                   
P28545     END-IF.                                                              
P28545                                                                          
P28545******************************************************************        
P28545*  PRICING MODULE.                                                        
P28545******************************************************************        
P28545     COPY INPD400.                                                        
P28545                                                                          
