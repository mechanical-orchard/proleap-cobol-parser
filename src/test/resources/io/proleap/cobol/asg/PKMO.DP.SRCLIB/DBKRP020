00001 ******************************************************************07/18/95
00002  IDENTIFICATION DIVISION.                                         DBKRP020
00003 ******************************************************************   LV030
00004                                                                      CL**1
00005  PROGRAM-ID.             DBKRP020.                                   CL**1
00006  AUTHOR.                 BRAD DORN.                                  CL**1
00007  INSTALLATION.           KOHLS.                                      CL**1
00008  DATE-WRITTEN            JULY 2009.                                  CL**1
00009  DATE-COMPILED.                                                      CL**1
00010                                                                      CL**1
00011 ******************************************************************   CL**1
00012 * THIS PROGRAM REPORTS ON PARTITIONING KEY VALUES.  TWO REPORTS      CL**1
00012 * WILL BE PRODUCED.                                                  CL**1
00012 *   OUT-REPORT1: TABLE-CONTROLLED PARTITIONING                       CL**1
00012 *   OUT-REPORT2: INDEX-CONTROLLED PARTITIONING                       CL**1
00016 ******************************************************************   CL**1
00020                                                                      CL**1
00021 ******************************************************************   CL**1
00022  ENVIRONMENT DIVISION.                                               CL**1
00023 ******************************************************************   CL**1
00024                                                                      CL**1
00025  CONFIGURATION SECTION.                                              CL**1
00026                                                                      CL**1
00027  SOURCE-COMPUTER.        IBM-3090.                                   CL**1
00028  OBJECT-COMPUTER.        IBM-3090.                                   CL**1
00029                                                                      CL**1
00030  INPUT-OUTPUT SECTION.                                               CL**1
00031                                                                      CL**1
00032  FILE-CONTROL.                                                       CL**1
00033                                                                      CL**1
00037      SELECT OUT-REPORT1              ASSIGN TO RPT01.                CL**1
00037      SELECT OUT-REPORT2              ASSIGN TO RPT02.                CL**1
00039                                                                      CL**1
00040 ******************************************************************   CL**1
00041  DATA DIVISION.                                                      CL**1
00042 ******************************************************************   CL**1
00043                                                                      CL**1
00044  FILE SECTION.                                                       CL**1
00045                                                                      CL**1
00067  FD  OUT-REPORT1                                                     CL**1
00068      RECORDING MODE IS F                                             CL**1
00069      LABEL RECORDS ARE STANDARD                                      CL**1
00070      BLOCK CONTAINS 0 RECORDS.                                       CL**1
00071                                                                      CL**1
00072  01  OUT-REPORT1-REC                 PIC  X(132).                    CL**1
00073                                                                      CL**1
00067  FD  OUT-REPORT2                                                     CL**1
00068      RECORDING MODE IS F                                             CL**1
00069      LABEL RECORDS ARE STANDARD                                      CL**1
00070      BLOCK CONTAINS 0 RECORDS.                                       CL**1
00071                                                                      CL**1
00072  01  OUT-REPORT2-REC                 PIC  X(132).                    CL**1
00073                                                                      CL**1
00081 ******************************************************************   CL**1
00082  WORKING-STORAGE SECTION.                                            CL**1
00083 ******************************************************************   CL**1
                                                                           CL**1
       01 WS-ABEND-CODE                PIC S9(04) VALUE +0   COMP SYNC.         
       01 WS-END-INDEX-PART-CSR        PIC X(01)  VALUE 'N'.               CL**3
       01 WS-END-TABLE-PART-CSR        PIC X(01)  VALUE 'N'.               CL**3
       01 WS-COLSEQ                    PIC S9(04) VALUE +0   COMP SYNC.         
       01 WS-LAST-KEY-VALUE-LENGTH     PIC S9(09) VALUE +0   COMP SYNC.         
       01 WS-2ND-LAST-KEY-VALUE-LENGTH PIC S9(09) VALUE +0   COMP SYNC.         
       01 WS-LINE-COUNT                PIC S9(04) VALUE +0   COMP SYNC.    CL**1
       01 WS-PAGE-COUNT                PIC S9(04) VALUE ZERO COMP SYNC.    CL**1
       01 WS-PAGE-SIZE                 PIC S9(04) VALUE +55  COMP SYNC.    CL**1
                                                                           CL**1
00085 *  STANDARD REPORT HEADER FOR 132 COLUMN REPORT                      CL**1
00086                                                                      CL**1
00112  01  HEADING-LINE-1.                                                 CL**1
00089      05  DP132O1-RUN-DATE.                                           CL**1
00090          10  DP132O1-RUN-MONTH             PIC 9(02) VALUE ZERO.     CL**1
00091          10  FILLER                        PIC X(01) VALUE '/'.      CL**1
00092          10  DP132O1-RUN-DAY               PIC 9(02) VALUE ZERO.     CL**1
00093          10  FILLER                        PIC X(01) VALUE '/'.      CL**1
00094          10  DP132O1-RUN-YEAR              PIC 9(02) VALUE ZERO.     CL**1
00095      05  FILLER                            PIC X(02) VALUE SPACE.    CL**1
00096      05  DP132O1-RUN-TIME.                                           CL**1
00097          10  DP132O1-RUN-HOUR              PIC 9(02) VALUE ZERO.     CL**1
00098          10  FILLER                        PIC X(01) VALUE ':'.      CL**1
00099          10  DP132O1-RUN-MINUTE            PIC 9(02) VALUE ZERO.     CL**1
00100      05  FILLER                            PIC X(31) VALUE SPACE.    CL**1
00101      05  DP132O1-REPORT-TITLE              PIC X(36) VALUE SPACES.   CL**1
00103      05  FILLER                            PIC X(25) VALUE SPACE.    CL**1
00104      05  DP132O1-PROGRAM-NAME              PIC X(08) VALUE SPACE.    CL**1
00107      05  FILLER                            PIC X(03) VALUE SPACE.    CL**1
00108      05  FILLER                            PIC X(05) VALUE 'PAGE'.   CL**1
00109      05  DP132O1-PAGE-NUMBER               PIC ZZZZ9 VALUE ZERO.     CL**1
00110      05  FILLER                            PIC X(01) VALUE SPACE.    CL**1
00111                                                                      CL**1
00112  01  HEADING-LINE-2.                                                 CL**1
00113      05  FILLER                      PIC  X(44)      VALUE SPACE.    CL**1
00114      05  FILLER                      PIC  X(88)      VALUE           CL**1
00115          ' *** LAST PARTITION KEY IS ENFORCED *** '.                 CL**1
00116                                                                      CL**1
00112  01  HEADING-LINE-3.                                                 CL**1
00113      05  FILLER                      PIC X(132) VALUE ALL '-'.       CL**1
00116                                                                      CL**1
00151  01  DETAIL-LINE1.                                                   CL**1
00119      05  FILLER                      PIC  X(09)      VALUE           CL*12
00120          '  TABLE: '.                                                CL**1
00153      05  DL1-TABLE-CREATOR-NAME      PIC  X(123)     VALUE SPACES.   CL**1
00287                                                                      CL**1
00151  01  DETAIL-LINE2A.                                                  CL**1
00153      05  FILLER                      PIC  X(29)      VALUE           CL**1
               '    2ND LAST PART KEY LIMIT: '.                                 
00159      05  DL2A-KEY-VALUE              PIC  X(100)     VALUE SPACES.   CL*26
00154      05  DL2A-TRUNC-INDICATOR        PIC  X(03)      VALUE SPACES.   CL*12
00287                                                                      CL**1
00151  01  DETAIL-LINE2B.                                                  CL**1
00153      05  FILLER                      PIC  X(29)      VALUE           CL**1
               '    LAST PART KEY LIMIT:     '.                                 
00159      05  DL2B-KEY-VALUE              PIC  X(100)     VALUE SPACES.   CL*26
00154      05  DL2B-TRUNC-INDICATOR        PIC  X(03)      VALUE SPACES.   CL*12
00287                                                                      CL**1
00130  01  DETAIL-LINE3.                                                   CL**1
00131      05  FILLER                      PIC  X(04)      VALUE SPACES.   CL*26
00132      05  DL3-TEXT                    PIC  X(21)      VALUE SPACES.   CL*12
00131      05  FILLER                      PIC  X(02)      VALUE SPACES.   CL*26
00155      05  DL3-COLUMN-NAME             PIC  X(40)      VALUE SPACES.   CL**1
00156      05  FILLER                      PIC  X(03)      VALUE SPACES.   CL*12
00157      05  DL3-COLUMN-TYPE             PIC  X(08)      VALUE SPACES.        
00158      05  FILLER                      PIC  X(03)      VALUE SPACES.   CL*26
00159      05  DL3-COLUMN-ORDERING         PIC  X(04)      VALUE SPACES.   CL*26
00158      05  FILLER                      PIC  X(47)      VALUE SPACES.   CL*26
00150                                                                      CL**1
00112  01  DETAIL-LINE4.                                                   CL**1
00113      05  FILLER                      PIC X(04)  VALUE SPACES.        CL**1
           05  DL4-LIT                     PIC X(18)  VALUE SPACES.             
00113      05  FILLER                      PIC X(110) VALUE SPACES.        CL**1
00116                                                                      CL**1
00288  01  END-OF-REPORT-LINE.                                             CL*30
00289      05  FILLER                      PIC  X(54)      VALUE SPACE.    CL*30
00290      05  FILLER                      PIC  X(25)      VALUE           CL*30
00291          '***** END OF REPORT *****'.                                CL*30
00292      05  FILLER                      PIC  X(53)      VALUE SPACE.    CL*30
00293                                                                      CL*30
00294  01  BLANK-LINE                      PIC X(132)      VALUE SPACES.   CL**1
00295                                                                      CL**1
00296  01  SD-SYSTEM-DATE.                                                 CL**1
00297      05  SD-SYSTEM-YEAR              PIC  9(02)      VALUE ZERO.     CL**1
00298      05  SD-SYSTEM-MONTH             PIC  9(02)      VALUE ZERO.     CL**1
00299      05  SD-SYSTEM-DAY               PIC  9(02)      VALUE ZERO.     CL**1
00300                                                                      CL**1
00301  01  ST-SYSTEM-TIME.                                                 CL**1
00302      05  ST-SYSTEM-HOUR              PIC  9(02)      VALUE ZERO.     CL**1
00303      05  ST-SYSTEM-MINUTE            PIC  9(02)      VALUE ZERO.     CL**1
00304      05  FILLER                      PIC  9(04)      VALUE ZERO.     CL**1
                                                                           CL**1
       01  DP004-DB2-ERROR-FIELDS.                                              
           05  DP004-ASTERISK-LINE       PIC  X(80)  VALUE ALL '*'.             
           05  DP004-MAX-MESSAGE-LINES   PIC S9(04)  VALUE +12                  
                                                     COMP SYNC.                 
           05  DP004-ERROR-LINE-LENGTH   PIC S9(09)  VALUE +75                  
                                                     COMP SYNC.                 
           05  DP004-FORMATTED-MESSAGE.                                         
               10  FILLER                PIC S9(04)  VALUE +900                 
                                                     COMP SYNC.                 
               10  DP004-MESSAGE-LINE    OCCURS 12 TIMES                        
                                         INDEXED BY DP004-MSG-IDX               
                                         PIC  X(75).                            
                                                                           CL**1
      ************************************************************              
      *    SYSIBM.SYSINDEXES DCLGEN                                             
      ************************************************************              
           EXEC SQL                                                             
               INCLUDE SYSINDEX                                                 
           END-EXEC.                                                            
                                                                                
      ************************************************************              
      *    SYSIBM.SYSTABLESPACE DCLGEN                                          
      ************************************************************              
           EXEC SQL                                                             
               INCLUDE SYSTABSP                                                 
           END-EXEC.                                                            
                                                                                
      ************************************************************              
      *    SYSIBM.SYSTABLES DCLGEN                               *              
      ************************************************************              
           EXEC SQL                                                             
               INCLUDE SYSTABLE                                                 
           END-EXEC.                                                            
      *                                                                         
      ************************************************************              
      *    SYSIBM.SYSTABLEPART DCLGEN                            *              
      ************************************************************              
           EXEC SQL                                                             
               INCLUDE SYSTABPT                                                 
           END-EXEC.                                                            
      ************************************************************              
      *    SYSIBM.SYSCOLUMNS DCLGEN                              *              
      ************************************************************              
           EXEC SQL                                                             
               INCLUDE SYSCOL                                                   
           END-EXEC.                                                            
      ************************************************************              
      *    SYSIBM.SYSKEY DCLGEN                                  *              
      ************************************************************              
           EXEC SQL                                                             
               INCLUDE SYSKEY                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ************************************************************              
      *    DECLARE TABLE_PART_CURS CURSOR                        *              
      ************************************************************              
           EXEC SQL                                                             
            DECLARE TABLE-PART-CSR CURSOR FOR                                   
              SELECT A.FULL_TBL_NAME,                                           
                     A.CREATOR,                                                 
                     A.NAME,                                                    
                     A.PARTKEYCOLNUM,                                           
                     A.LAST_PART_LIMITKEY,                                      
                     A.LAST_PART_KEY_LENGTH,                                    
                     COALESCE(SUBSTR(TP2.LIMITKEY,1,100),                       
                                    'NOT APPLICABLE'),                          
                     COALESCE(LENGTH(TP2.LIMITKEY),0)                           
              FROM                                                              
                (SELECT STRIP(TBL.CREATOR) || '.' || TBL.NAME                   
                          AS FULL_TBL_NAME,                                     
                     TBL.CREATOR,                                               
                     TBL.NAME,                                                  
                     TBL.PARTKEYCOLNUM,                                         
                     TBL.DBNAME,                                                
                     TBL.TSNAME,                                                
                     TS.PARTITIONS,                                             
                     SUBSTR(TP1.LIMITKEY,1,100)                                 
                       AS LAST_PART_LIMITKEY,                                   
                     LENGTH(TP1.LIMITKEY)                                       
                       AS LAST_PART_KEY_LENGTH                                  
                 FROM SYSIBM.SYSTABLES TBL,                                     
                      SYSIBM.SYSTABLESPACE TS,                                  
                      SYSIBM.SYSTABLEPART TP1                                   
                   WHERE TBL.PARTKEYCOLNUM > 0                                  
                     AND TS.DBNAME   = TBL.DBNAME                               
                     AND TS.NAME     = TBL.TSNAME                               
                     AND TP1.DBNAME  = TBL.DBNAME                               
                     AND TP1.TSNAME  = TBL.TSNAME                               
                     AND TP1.LOGICAL_PART = TS.PARTITIONS) AS A                 
                 LEFT OUTER JOIN SYSIBM.SYSTABLEPART TP2                        
                   ON TP2.DBNAME          = A.DBNAME                            
                     AND TP2.TSNAME       = A.TSNAME                            
                     AND TP2.LOGICAL_PART = (A.PARTITIONS - 1)                  
              ORDER BY 1 WITH UR                                                
           END-EXEC.                                                            
                                                                           CL**1
      ************************************************************              
      *    DECLARE INDEX_PART_CURS CURSOR                        *              
      ************************************************************              
           EXEC SQL                                                             
            DECLARE INDEX-PART-CSR CURSOR FOR                                   
              SELECT A.FULL_TBL_NAME,                                           
                     A.TBCREATOR,                                               
                     A.TBNAME,                                                  
                     A.CREATOR,                                                 
                     A.NAME,                                                    
                     A.COLCOUNT,                                                
                     A.TYPE,                                                    
                     A.LAST_PART_LIMITKEY,                                      
                     A.LAST_PART_KEY_LENGTH,                                    
                     COALESCE(SUBSTR(TP2.LIMITKEY,1,100),                       
                                    'NOT APPLICABLE'),                          
                     COALESCE(LENGTH(TP2.LIMITKEY),0)                           
              FROM                                                              
                (SELECT STRIP(IX.TBCREATOR) || '.' || IX.TBNAME                 
                          AS FULL_TBL_NAME,                                     
                     IX.TBCREATOR,                                              
                     IX.TBNAME,                                                 
                     IX.COLCOUNT,                                               
                     IX.CREATOR,                                                
                     IX.NAME,                                                   
                     TBL.DBNAME,                                                
                     TBL.TSNAME,                                                
                     TS.PARTITIONS,                                             
                     TS.TYPE,                                                   
                     SUBSTR(TP1.LIMITKEY,1,100)                                 
                       AS LAST_PART_LIMITKEY,                                   
                     LENGTH(TP1.LIMITKEY)                                       
                       AS LAST_PART_KEY_LENGTH                                  
                 FROM SYSIBM.SYSTABLES TBL,                                     
                      SYSIBM.SYSTABLESPACE TS,                                  
                      SYSIBM.SYSTABLEPART TP1,                                  
                      SYSIBM.SYSINDEXES IX                                      
                   WHERE TS.PARTITIONS > 0                                      
                     AND TS.DBNAME = TBL.DBNAME                                 
                     AND TS.NAME   = TBL.TSNAME                                 
                     AND TBL.PARTKEYCOLNUM = 0                                  
                     AND TBL.TYPE          = 'T'                                
                     AND TP1.DBNAME  = TS.DBNAME                                
                     AND TP1.TSNAME  = TS.NAME                                  
                     AND TP1.PARTITION = TS.PARTITIONS                          
                     AND TP1.IXCREATOR = IX.CREATOR                             
                     AND TP1.IXNAME    = IX.NAME) AS A                          
              LEFT OUTER JOIN SYSIBM.SYSTABLEPART TP2                           
                ON TP2.DBNAME       = A.DBNAME                                  
                  AND TP2.TSNAME    = A.TSNAME                                  
                  AND TP2.PARTITION = (A.PARTITIONS - 1)                        
              ORDER BY 1 WITH UR                                                
           END-EXEC.                                                            
                                                                           CL**1
                                                                           CL**1
00432 ******************************************************************   CL**1
00433  LINKAGE SECTION.                                                    CL**1
00434 ******************************************************************   CL**1
00435                                                                      CL**1
00436 ******************************************************************   CL**1
00437  PROCEDURE DIVISION.                                                 CL**1
00438 ******************************************************************   CL**1
00439  0000-PROGRAM-MAINLINE.                                              CL**1
00440                                                                      CL**1
00441      PERFORM 1000-INITIALIZATION.                                    CL**6
00442                                                                      CL**1
00454      PERFORM 2000-PROCESS-TABLE-PART-CSR                             CL**6
00455         UNTIL WS-END-TABLE-PART-CSR = 'Y'.                           CL**6
00456                                                                      CL**1
00454      PERFORM 3000-PROCESS-INDEX-PART-CSR                             CL**6
00455         UNTIL WS-END-INDEX-PART-CSR = 'Y'.                           CL**6
00456                                                                      CL**1
00468      GOBACK.                                                         CL**1
00469                                                                      CL**1
00477                                                                      CL**1
00478  1000-INITIALIZATION.                                                CL**1
00479                                                                      CL**1
00482      OPEN OUTPUT OUT-REPORT1                                         CL**6
00482                  OUT-REPORT2.                                        CL**6
00495                                                                      CL**1
00496      ACCEPT SD-SYSTEM-DATE FROM DATE.                                CL**1
00497      ACCEPT ST-SYSTEM-TIME FROM TIME.                                CL**1
00498                                                                      CL**1
00499      MOVE SD-SYSTEM-YEAR         TO DP132O1-RUN-YEAR.                CL**1
00501      MOVE SD-SYSTEM-MONTH        TO DP132O1-RUN-MONTH.               CL**1
00503      MOVE SD-SYSTEM-DAY          TO DP132O1-RUN-DAY.                 CL**1
00505      MOVE ST-SYSTEM-HOUR         TO DP132O1-RUN-HOUR.                CL**1
00507      MOVE ST-SYSTEM-MINUTE       TO DP132O1-RUN-MINUTE.              CL**1
00509      MOVE 'DBKRP020'             TO DP132O1-PROGRAM-NAME.            CL**1
00513                                                                      CL**1
                                                                           CL**1
       2000-PROCESS-TABLE-PART-CSR.                                        CL**6
                                                                                
           EXEC SQL                                                             
             OPEN TABLE-PART-CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               CONTINUE                                                         
             WHEN SQLCODE = +100                                                
               MOVE 'Y' TO WS-END-TABLE-PART-CSR                                
             WHEN SQLCODE NOT = ZERO                                            
               DISPLAY '*********************************************'          
               DISPLAY '*** PROGRAM ERROR - DBKRP020              ***'          
               DISPLAY '***                                       ***'          
               DISPLAY '*** PARAGRAPH 2000-PROCESS-TABLE-PART-CSR ***'          
               DISPLAY '*** BAD OPEN ON TABLE-PART-CSR            ***'          
               DISPLAY '*********************************************'          
               PERFORM 9999-DB2-ABEND                                           
           END-EVALUATE.                                                        
                                                                           CL*17
           MOVE 'TABLE-CONTROLLED PARTITIONING REPORT'                          
             TO DP132O1-REPORT-TITLE.                                           
           PERFORM 2300-WRITE-PAGE-HEADINGS.                               CL**6
                                                                                
           PERFORM 2100-FETCH-ROWS                                              
             UNTIL WS-END-TABLE-PART-CSR = 'Y'.                                 
                                                                                
           PERFORM 2200-CLOSE-CURSOR.                                      CL**1
                                                                                
           WRITE OUT-REPORT1-REC FROM END-OF-REPORT-LINE                   CL*30
             AFTER ADVANCING 3 LINES.                                      CL*30
                                                                                
           CLOSE OUT-REPORT1.                                              CL**6
                                                                           CL**1
                                                                           CL**1
       2100-FETCH-ROWS.                                                    CL**6
                                                                                
           MOVE SPACES TO SYSTBL-CREATOR-TEXT                                   
                          SYSTBL-NAME-TEXT.                                     
                                                                                
           EXEC SQL                                                             
               FETCH TABLE-PART-CSR                                             
                INTO :DL1-TABLE-CREATOR-NAME,                                   
                     :SYSTBL-CREATOR,                                           
                     :SYSTBL-NAME,                                              
                     :SYSTBL-PARTKEYCOLNUM,                                     
                     :DL2B-KEY-VALUE,                                           
                     :WS-LAST-KEY-VALUE-LENGTH,                                 
                     :DL2A-KEY-VALUE,                                           
                     :WS-2ND-LAST-KEY-VALUE-LENGTH                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               PERFORM 2110-PROCESS-DATA                                        
             WHEN SQLCODE = +100                                                
               MOVE 'Y' TO WS-END-TABLE-PART-CSR                                
             WHEN SQLCODE NOT = ZERO                                            
               DISPLAY '*************************************'                  
               DISPLAY '*** PROGRAM ERROR - DBKRP020      ***'                  
               DISPLAY '***                               ***'                  
               DISPLAY '*** PARAGRAPH 2100-FETCH-ROWS     ***'                  
               DISPLAY '*** BAD FETCH ON TABLE-PART-CSR   ***'                  
               DISPLAY '*************************************'                  
               PERFORM 9999-DB2-ABEND                                           
           END-EVALUATE.                                                        
                                                                           CL*17
                                                                                
       2110-PROCESS-DATA.                                                  CL**6
                                                                           CL*17
           IF WS-LAST-KEY-VALUE-LENGTH > 100                                    
               MOVE '***' TO DL2B-TRUNC-INDICATOR                               
           ELSE                                                                 
               MOVE SPACES TO DL2B-TRUNC-INDICATOR                              
           END-IF.                                                              
                                                                                
           IF DL2A-KEY-VALUE = 'NOT APPLICABLE'                                 
               MOVE SPACES TO DL2A-TRUNC-INDICATOR                              
           ELSE                                                                 
               IF WS-2ND-LAST-KEY-VALUE-LENGTH > 100                            
                   MOVE '***' TO DL2A-TRUNC-INDICATOR                           
               ELSE                                                             
                   MOVE SPACES TO DL2A-TRUNC-INDICATOR                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF WS-LINE-COUNT >= WS-PAGE-SIZE                                CL**1
               PERFORM 2300-WRITE-PAGE-HEADINGS                            CL**6
           END-IF.                                                         CL**1
                                                                                
           IF WS-LINE-COUNT = 4                                                 
               WRITE OUT-REPORT1-REC FROM HEADING-LINE-3                   CL*12
               ADD +1 TO WS-LINE-COUNT                                          
           ELSE                                                                 
               WRITE OUT-REPORT1-REC FROM HEADING-LINE-3                   CL*12
                   AFTER ADVANCING 2 LINES                                      
               ADD +2 TO WS-LINE-COUNT                                          
           END-IF.                                                              
                                                                                
           WRITE OUT-REPORT1-REC FROM DETAIL-LINE1.                        CL**1
           WRITE OUT-REPORT1-REC FROM BLANK-LINE.                          CL*12
           WRITE OUT-REPORT1-REC FROM DETAIL-LINE2A.                       CL**1
           WRITE OUT-REPORT1-REC FROM DETAIL-LINE2B.                       CL**1
           ADD +4 TO WS-LINE-COUNT.                                        CL**1
                                                                           CL**1
           PERFORM                                                              
             VARYING WS-COLSEQ FROM 1 BY 1                                      
                     UNTIL WS-COLSEQ > SYSTBL-PARTKEYCOLNUM                     
               PERFORM 2120-GET-COLUMN-INFO                                     
           END-PERFORM.                                                         
                                                                                
                                                                                
       2120-GET-COLUMN-INFO.                                               CL**6
                                                                                
           MOVE SPACES TO SYSCOL-NAME-TEXT.                                     
                                                                                
           EXEC SQL                                                             
             SELECT SUBSTR(CO.NAME,1,40),                                       
                    CO.COLTYPE,                                                 
                    CASE                                                        
                      WHEN CO.PARTKEY_ORDERING = 'D' THEN 'DESC'                
                      WHEN CO.PARTKEY_ORDERING = 'A' THEN 'ASC'                 
                      ELSE ' '                                                  
                    END                                                         
               INTO :SYSCOL-NAME,                                               
                    :SYSCOL-COLTYPE,                                            
                    :DL3-COLUMN-ORDERING                                        
              FROM SYSIBM.SYSCOLUMNS  CO                                        
               WHERE CO.TBCREATOR     = :SYSTBL-CREATOR                         
                AND CO.TBNAME         = :SYSTBL-NAME                            
                AND CO.PARTKEY_COLSEQ = :WS-COLSEQ                              
                  WITH UR                                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               CONTINUE                                                         
             WHEN SQLCODE NOT = ZERO                                            
               DISPLAY '***************************************'                
               DISPLAY '*** PROGRAM ERROR - DBKRP020        ***'                
               DISPLAY '***                                 ***'                
               DISPLAY '*** PARAGRAPH 2120-GET-COLUMN-INFO  ***'                
               DISPLAY '*** BAD SELECT ON SYSCOLUMNS        ***'                
               DISPLAY '***************************************'                
               PERFORM 9999-DB2-ABEND                                           
           END-EVALUATE.                                                        
                                                                           CL*17
           IF WS-COLSEQ = 1                                                     
               MOVE 'PARTITIONING COLUMNS:' TO DL3-TEXT                         
           ELSE                                                                 
               MOVE SPACES                  TO DL3-TEXT                         
           END-IF.                                                              
                                                                                
           MOVE SYSCOL-NAME-TEXT     TO DL3-COLUMN-NAME.                        
           MOVE SYSCOL-COLTYPE       TO DL3-COLUMN-TYPE.                        
                                                                           CL*17
           IF WS-LINE-COUNT > WS-PAGE-SIZE                                 CL**1
               PERFORM 2300-WRITE-PAGE-HEADINGS                            CL**6
           END-IF.                                                         CL**1
                                                                           CL**1
           IF WS-COLSEQ = 1                                                CL**1
               WRITE OUT-REPORT1-REC FROM BLANK-LINE                       CL**6
               ADD +1 TO WS-LINE-COUNT                                     CL*12
           END-IF.                                                         CL**1
                                                                           CL**1
           WRITE OUT-REPORT1-REC                                           CL**1
             FROM DETAIL-LINE3                                             CL**1
               AFTER ADVANCING 1 LINES.                                    CL**1
           ADD +1 TO WS-LINE-COUNT.                                        CL*12
                                                                                
                                                                           CL**1
       2200-CLOSE-CURSOR.                                                  CL**1
                                                                                
           EXEC SQL                                                             
             CLOSE TABLE-PART-CSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               CONTINUE                                                         
             WHEN SQLCODE NOT = ZERO                                            
               DISPLAY '******************************************'             
               DISPLAY '*** PROGRAM ERROR - DBKRP020           ***'             
               DISPLAY '***                                    ***'             
               DISPLAY '*** PARAGRAPH 2200-CLOSE-CURSOR        ***'             
               DISPLAY '*** BAD CLOSE ON TABLE-PART-CSR        ***'             
               DISPLAY '******************************************'             
               PERFORM 9999-DB2-ABEND                                           
           END-EVALUATE.                                                        
                                                                           CL*17
                                                                           CL*17
00755  2300-WRITE-PAGE-HEADINGS.                                           CL**6
00756                                                                      CL**1
00757      ADD +1               TO WS-PAGE-COUNT.                          CL**1
00758      MOVE WS-PAGE-COUNT   TO DP132O1-PAGE-NUMBER.                    CL**1
00759                                                                      CL**1
00766      WRITE OUT-REPORT1-REC                                           CL**1
00767        FROM HEADING-LINE-1                                           CL**1
00762          AFTER ADVANCING PAGE.                                       CL**1
00769                                                                      CL**1
00766      WRITE OUT-REPORT1-REC                                           CL**1
00767        FROM HEADING-LINE-2                                           CL**1
00768          AFTER ADVANCING 1 LINES.                                    CL**1
00769                                                                      CL**1
00778      WRITE OUT-REPORT1-REC                                           CL**1
00779        FROM BLANK-LINE                                               CL**1
00780          AFTER ADVANCING 2 LINES.                                    CL**1
00781                                                                      CL**1
00782      MOVE +4              TO WS-LINE-COUNT.                          CL**1
00783                                                                      CL**1
                                                                        DBKRP020
                                                                           CL**1
       3000-PROCESS-INDEX-PART-CSR.                                        CL**6
                                                                                
           EXEC SQL                                                             
             OPEN INDEX-PART-CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               CONTINUE                                                         
             WHEN SQLCODE = +100                                                
               MOVE 'Y' TO WS-END-INDEX-PART-CSR                                
             WHEN SQLCODE NOT = ZERO                                            
               DISPLAY '*********************************************'          
               DISPLAY '*** PROGRAM ERROR - DBKRP020              ***'          
               DISPLAY '***                                       ***'          
               DISPLAY '*** PARAGRAPH 3000-PROCESS-INDEX-PART-CSR ***'          
               DISPLAY '*** BAD OPEN ON INDEX-PART-CSR            ***'          
               DISPLAY '*********************************************'          
               PERFORM 9999-DB2-ABEND                                           
           END-EVALUATE.                                                        
                                                                           CL*17
           MOVE +0 TO WS-PAGE-COUNT.                                            
           MOVE 'INDEX-CONTROLLED PARTITIONING REPORT'                          
             TO DP132O1-REPORT-TITLE.                                           
           PERFORM 3300-WRITE-PAGE-HEADINGS.                                    
                                                                                
           PERFORM 3100-FETCH-ROWS                                              
             UNTIL WS-END-INDEX-PART-CSR = 'Y'.                                 
                                                                                
           PERFORM 3200-CLOSE-CURSOR.                                           
                                                                                
           WRITE OUT-REPORT2-REC FROM END-OF-REPORT-LINE                        
             AFTER ADVANCING 3 LINES.                                           
                                                                                
           CLOSE OUT-REPORT2.                                                   
                                                                           CL**1
                                                                           CL**1
       3100-FETCH-ROWS.                                                    CL**6
                                                                                
           MOVE SPACES TO SYSTBL-CREATOR-TEXT                                   
                          SYSTBL-NAME-TEXT                                      
                          SYSINDEX-CREATOR-TEXT                                 
                          SYSINDEX-NAME-TEXT.                                   
                                                                                
           EXEC SQL                                                             
               FETCH INDEX-PART-CSR                                             
                INTO :DL1-TABLE-CREATOR-NAME,                                   
                     :SYSTBL-CREATOR,                                           
                     :SYSTBL-NAME,                                              
                     :SYSINDEX-CREATOR,                                         
                     :SYSINDEX-NAME,                                            
                     :SYSINDEX-COLCOUNT,                                        
                     :SYSTABSP-TYPE,                                            
                     :DL2B-KEY-VALUE,                                           
                     :WS-LAST-KEY-VALUE-LENGTH,                                 
                     :DL2A-KEY-VALUE,                                           
                     :WS-2ND-LAST-KEY-VALUE-LENGTH                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               PERFORM 3110-PROCESS-DATA                                        
             WHEN SQLCODE = +100                                                
               MOVE 'Y' TO WS-END-INDEX-PART-CSR                                
             WHEN SQLCODE NOT = ZERO                                            
               DISPLAY '*************************************'                  
               DISPLAY '*** PROGRAM ERROR - DBKRP020      ***'                  
               DISPLAY '***                               ***'                  
               DISPLAY '*** PARAGRAPH 3100-FETCH-ROWS     ***'                  
               DISPLAY '*** BAD FETCH ON INDEX-PART-CSR   ***'                  
               DISPLAY '*************************************'                  
               PERFORM 9999-DB2-ABEND                                           
           END-EVALUATE.                                                        
                                                                           CL*17
                                                                                
       3110-PROCESS-DATA.                                                  CL**6
                                                                           CL*17
           IF WS-LAST-KEY-VALUE-LENGTH > 100                                    
               MOVE '***' TO DL2B-TRUNC-INDICATOR                               
           ELSE                                                                 
               MOVE SPACES TO DL2B-TRUNC-INDICATOR                              
           END-IF.                                                              
                                                                                
           IF DL2A-KEY-VALUE = 'NOT APPLICABLE'                                 
               MOVE SPACES TO DL2A-TRUNC-INDICATOR                              
           ELSE                                                                 
               IF WS-2ND-LAST-KEY-VALUE-LENGTH > 100                            
                   MOVE '***' TO DL2A-TRUNC-INDICATOR                           
               ELSE                                                             
                   MOVE SPACES TO DL2A-TRUNC-INDICATOR                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF WS-LINE-COUNT >= WS-PAGE-SIZE                                CL**1
               PERFORM 3300-WRITE-PAGE-HEADINGS                            CL**6
           END-IF.                                                         CL**1
                                                                                
           IF WS-LINE-COUNT = 4                                                 
               WRITE OUT-REPORT2-REC FROM HEADING-LINE-3                   CL*12
               ADD +1 TO WS-LINE-COUNT                                          
           ELSE                                                                 
               WRITE OUT-REPORT2-REC FROM HEADING-LINE-3                   CL*12
                   AFTER ADVANCING 2 LINES                                      
               ADD +2 TO WS-LINE-COUNT                                          
           END-IF.                                                              
                                                                                
           WRITE OUT-REPORT2-REC FROM DETAIL-LINE1.                        CL**1
           WRITE OUT-REPORT2-REC FROM BLANK-LINE.                          CL*12
           WRITE OUT-REPORT2-REC FROM DETAIL-LINE2A.                       CL**1
           WRITE OUT-REPORT2-REC FROM DETAIL-LINE2B.                       CL**1
                                                                                
           IF (SYSTABSP-TYPE = 'L'                                              
                 OR SYSTABSP-TYPE = 'K')                                        
             MOVE '** ENFORCED **' TO DL4-LIT                                   
           ELSE                                                                 
             MOVE '** NOT ENFORCED **' TO DL4-LIT.                              
           WRITE OUT-REPORT2-REC FROM DETAIL-LINE4.                        CL**1
                                                                                
           ADD +5 TO WS-LINE-COUNT.                                        CL**1
                                                                           CL**1
           PERFORM                                                              
             VARYING WS-COLSEQ FROM 1 BY 1                                      
                     UNTIL WS-COLSEQ > SYSINDEX-COLCOUNT                        
               PERFORM 3120-GET-COLUMN-INFO                                     
           END-PERFORM.                                                         
                                                                                
                                                                                
       3120-GET-COLUMN-INFO.                                               CL**6
                                                                                
           MOVE SPACES TO SYSCOL-NAME-TEXT.                                     
                                                                                
           EXEC SQL                                                             
             SELECT SUBSTR(CO.NAME,1,40),                                       
                    CO.COLTYPE,                                                 
                    CASE                                                        
                      WHEN KY.ORDERING = 'D' THEN 'DESC'                        
                      WHEN KY.ORDERING = 'A' THEN 'ASC'                         
                      ELSE ' '                                                  
                    END                                                         
               INTO :SYSCOL-NAME,                                               
                    :SYSCOL-COLTYPE,                                            
                    :DL3-COLUMN-ORDERING                                        
             FROM SYSIBM.SYSCOLUMNS  CO,                                        
                  SYSIBM.SYSKEYS     KY                                         
               WHERE KY.IXCREATOR  = :SYSINDEX-CREATOR                          
                 AND KY.IXNAME     = :SYSINDEX-NAME                             
                 AND KY.COLSEQ     = :WS-COLSEQ                                 
                 AND KY.COLNAME    = CO.NAME                                    
                 AND CO.TBCREATOR  = :SYSTBL-CREATOR                            
                 AND CO.TBNAME     = :SYSTBL-NAME                               
             WITH UR                                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               CONTINUE                                                         
             WHEN SQLCODE NOT = ZERO                                            
               DISPLAY '***************************************'                
               DISPLAY '*** PROGRAM ERROR - DBKRP020        ***'                
               DISPLAY '***                                 ***'                
               DISPLAY '*** PARAGRAPH 3120-GET-COLUMN-INFO  ***'                
               DISPLAY '*** BAD SELECT ON SYSCOLUMNS        ***'                
               DISPLAY '***************************************'                
               PERFORM 9999-DB2-ABEND                                           
           END-EVALUATE.                                                        
                                                                           CL*17
           IF WS-COLSEQ = 1                                                     
               MOVE 'PARTITIONING COLUMNS:' TO DL3-TEXT                         
           ELSE                                                                 
               MOVE SPACES                  TO DL3-TEXT                         
           END-IF.                                                              
                                                                                
           MOVE SYSCOL-NAME-TEXT     TO DL3-COLUMN-NAME.                        
           MOVE SYSCOL-COLTYPE       TO DL3-COLUMN-TYPE.                        
                                                                           CL*17
           IF WS-LINE-COUNT > WS-PAGE-SIZE                                 CL**1
               PERFORM 3300-WRITE-PAGE-HEADINGS                            CL**6
           END-IF.                                                         CL**1
                                                                           CL**1
           IF WS-COLSEQ = 1                                                CL**1
               WRITE OUT-REPORT2-REC FROM BLANK-LINE                       CL**6
               ADD +1 TO WS-LINE-COUNT                                     CL*12
           END-IF.                                                         CL**1
                                                                           CL**1
           WRITE OUT-REPORT2-REC                                           CL**1
             FROM DETAIL-LINE3                                             CL**1
               AFTER ADVANCING 1 LINES.                                    CL**1
           ADD +1 TO WS-LINE-COUNT.                                        CL*12
                                                                                
                                                                           CL**1
       3200-CLOSE-CURSOR.                                                  CL**1
                                                                                
           EXEC SQL                                                             
             CLOSE INDEX-PART-CSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               CONTINUE                                                         
             WHEN SQLCODE NOT = ZERO                                            
               DISPLAY '******************************************'             
               DISPLAY '*** PROGRAM ERROR - DBKRP020           ***'             
               DISPLAY '***                                    ***'             
               DISPLAY '*** PARAGRAPH 3200-CLOSE-CURSOR        ***'             
               DISPLAY '*** BAD CLOSE ON INDEX-PART-CSR        ***'             
               DISPLAY '******************************************'             
               PERFORM 9999-DB2-ABEND                                           
           END-EVALUATE.                                                        
                                                                           CL*17
                                                                           CL**1
00755  3300-WRITE-PAGE-HEADINGS.                                           CL**6
00756                                                                      CL**1
00757      ADD +1               TO WS-PAGE-COUNT.                          CL**1
00758      MOVE WS-PAGE-COUNT   TO DP132O1-PAGE-NUMBER.                    CL**1
00759                                                                      CL**1
00766      WRITE OUT-REPORT2-REC                                           CL**1
00767        FROM HEADING-LINE-1                                           CL**1
00762          AFTER ADVANCING PAGE.                                       CL**1
00769                                                                      CL**1
00778      WRITE OUT-REPORT2-REC                                           CL**1
00779        FROM BLANK-LINE                                               CL**1
00780          AFTER ADVANCING 2 LINES.                                    CL**1
00781                                                                      CL**1
00782      MOVE +3              TO WS-LINE-COUNT.                          CL**1
00783                                                                      CL**1
                                                                        DBKRP020
                                                                           CL**1
       9999-DB2-ABEND.                                                          
                                                                                
           CALL 'DSNTIAR' USING SQLCA                                           
                                DP004-FORMATTED-MESSAGE                         
                                DP004-ERROR-LINE-LENGTH.                        
                                                                                
           DISPLAY DP004-ASTERISK-LINE.                                         
                                                                                
           PERFORM                                                              
               VARYING DP004-MSG-IDX                                            
                   FROM 1 BY 1                                                  
                   UNTIL DP004-MSG-IDX > DP004-MAX-MESSAGE-LINES                
                       DISPLAY '**'                                             
                               DP004-MESSAGE-LINE (DP004-MSG-IDX)               
                               ' **'                                            
           END-PERFORM.                                                         
                                                                                
           DISPLAY DP004-ASTERISK-LINE.                                         
           MOVE +4013      TO WS-ABEND-CODE                                     
           CALL 'ILBOABN0' USING WS-ABEND-CODE.                                 
                                                                           CL**1
