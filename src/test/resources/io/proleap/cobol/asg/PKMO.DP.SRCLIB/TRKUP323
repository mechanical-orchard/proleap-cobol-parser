000100*************************                                         00010006
000200 IDENTIFICATION DIVISION.                                         00020006
000300*************************                                         00030006
000400                                                                  00040006
000500 PROGRAM-ID.      TRKUP323.                                       00050006
000600 AUTHOR.          ABRAHAM IVERSON.                                00060006
000700 DATE-WRITTEN.    09/02/03.                                       00070006
000800                                                                  00080006
000900******************************************************************00090006
001000* CHANGE SKUS ON TIMCNUP ROWS FOR BULK POS SENT BY CONSOLIDATORS  00100006
001100* AS ZERO.  THIS WILL SAVE THE TRANSPORTATION IS TEAM FROM                
001200* MANUALLY CHANGING HUNDREDS OF ROWS ON TIMCNUP WHERE THE                 
001300* CONSOLIDATOR SENT ZEROS IN THE SKU FIELD.  IT WILL ALSO                 
001400* GUARANTEE THE SPEEDY PROCESSING OF DC SPLITS FOR THE                    
001500* MERCHANDISE.                                                            
001600*                                                                         
001700* THIS IS A TEMPORARY PROGRAM AND WON'T BE NEEDED AFTER                   
001800* CONSOLIDATORS FIX THERE SYSTEMS TO ALWAYS SEND THE SKU FOR UPC'S        
001900* FOR TIMCNUP. THE TIME FRAME WILL LIKELY EXTEND TO 1/1/2004              
002000* BECAUSE FILLING OF QUOTA CATEGORIES CAUSED THE PREMATURE                
002100* SHIPPING OF MERCHANDISE NEEDED DURING THE FALL/WINTER OF 2003.          
002400******************************************************************00150006
002500                                                                  00160006
002600 ENVIRONMENT DIVISION.                                            00170006
002700 CONFIGURATION SECTION.                                           00180006
002800                                                                  00190006
002900 SOURCE-COMPUTER. IBM-3090.                                       00200006
003000 OBJECT-COMPUTER. IBM-3090.                                       00210006
003100                                                                  00220006
003200 INPUT-OUTPUT SECTION.                                            00230006
003300 FILE-CONTROL.                                                    00240006
003400                                                                  00250006
004800***************                                                   00310006
004900 DATA DIVISION.                                                   00320006
005000***************                                                   00330006
005100 FILE SECTION.                                                    00340006
007510************************                                          00510006
007520 WORKING-STORAGE SECTION.                                         00520006
007530************************                                          00530006
007540                                                                  00540006
013000 01  PS-PROGRAM-SWITCHES.                                         00550006
019300     05  PS-PACK-UPC-MATCH-FOUND-SW       PIC X(01)  VALUE 'N'.   00580006
019400         88  PS-PACK-UPC-MATCH-FOUND                 VALUE 'Y'.   00590006
019500         88  PS-PACK-UPC-MATCH-FOUND-NO              VALUE 'N'.   00600006
019501     05  PS-END-OF-NO-SKU-CUR-SW          PIC X(01)  VALUE 'N'.           
019502         88  PS-END-OF-NO-SKU-CUR                    VALUE 'Y'.           
019503         88  PS-END-OF-NO-SKU-CUR-NO                 VALUE 'N'.           
019504     05  PS-END-OF-COMPONENT-CUR-SW       PIC X(01)  VALUE 'N'.           
019505         88  PS-END-OF-COMPONENT-CUR                 VALUE 'Y'.           
019506         88  PS-END-OF-COMPONENT-CUR-NO              VALUE 'N'.           
019507     05  PS-ALL-COMPONENTS-AUDITED-SW     PIC X(01)  VALUE 'N'.           
019508         88  PS-ALL-COMPONENTS-AUDITED               VALUE 'Y'.           
019509         88  PS-ALL-COMPONENTS-AUDITED-NO            VALUE 'N'.           
019507     05  PS-UPC-FOUND-SW                  PIC X(01)  VALUE 'N'.           
019508         88  PS-UPC-FOUND                            VALUE 'Y'.           
019509         88  PS-UPC-NOT-FOUND                        VALUE 'N'.           
033835                                                                  00610006
033836 01  PC-PROGRAM-CONSTANTS.                                        00620006
033837     05  PC-PGM-NAME               PIC X(08) VALUE 'TRKUP323'.    00630006
370594     05  PC-A                      PIC X(01) VALUE 'A'.                   
370595     05  PC-D                      PIC X(01) VALUE 'D'.                   
370596                                                                  00660006
370597 01  PV-PROGRAM-VARIABLES.                                        00670006
370598     05  PV-CURRENT-TMST               PIC X(26)  VALUE SPACES.   00680006
370599     05  PV-PARAGRAPH-NAME             PIC X(30)  VALUE SPACES.           
370600     05  PV-DB2-OPERATION-MESSAGE      PIC X(79)  VALUE SPACES.           
370601     05  PV-DB2-TABLE-NAME-1           PIC X(18)  VALUE SPACES.           
370602     05  PV-DB2-TABLE-NAME-2           PIC X(18)  VALUE SPACES.           
370603     05  PV-DB2-TABLE-NAME-3           PIC X(18)  VALUE SPACES.           
370604     05  PV-NO-SKU-COUNT               PIC 9(09)  VALUE ZEROES.           
370605                                                                          
370606 01  PV-NEW-IMCNUP-PREPK-REC.                                     00670006
370607     05 PV-NEW-IMCNUP-CONTNR-ID           PIC   X(12).                    
370608     05 PV-NEW-IMCNUP-CONTNR-SEQ-NBR      PIC  S9(04) COMP.               
370609     05 PV-NEW-IMCNUP-PO-NBR              PIC  S9(09) COMP.               
370610     05 PV-NEW-IMCNUP-VS-NBR              PIC   X(20).                    
370620     05 PV-NEW-IMCNUP-UPC-NBR             PIC  S9(15)V COMP-3.            
370630     05 PV-NEW-IMCNUP-PO-LNE-NBR          PIC  S9(09) COMP.               
370640     05 PV-NEW-IMCNUP-UPC-CART-CNT        PIC  S9(09) COMP.               
370650     05 PV-NEW-IMCNUP-UPC-CBMT-QTY        PIC  S9(07)V9(2) COMP-3.        
370660     05 PV-NEW-IMCNUP-UPC-WT-KGRM-QTY     PIC  S9(09) COMP.               
370670     05 PV-NEW-IMCNUP-UPC-UNT-QTY         PIC  S9(09) COMP.               
370680     05 PV-NEW-IMCNUP-CRE-BY-USR-ID       PIC   X(08).                    
370690     05 PV-NEW-IMCNUP-CRE-TMST            PIC   X(26).                    
370700     05 PV-NEW-IMCNUP-LAST-UPD-BY-USR     PIC   X(08).                    
370800     05 PV-NEW-IMCNUP-LAST-UPD-TMST       PIC   X(26).                    
370900     05 PV-NEW-IMCNUP-SKU-NBR             PIC  S9(08)V COMP-3.            
371000     05 PV-NEW-IMCNUP-ASN-PKGNG-CDE       PIC   X(02).                    
371001                                                                          
371002 01  ABEND-CODE                           PIC S9(4) VALUE ZEROS   00850006
371003                                                    COMP SYNC.    00860006
371004     88  PC-CONTROL-CARD-ERROR                      VALUE +4001.  00870006
371005     88  PC-DB2-ERROR                               VALUE +4013.  00880006
371006                                                                  00890006
371007*---------------------------------------------------------------*         
371008*---------------------------------------------------------------*         
371009     EXEC SQL                                                             
371010          INCLUDE TPURCHS                                                 
371011     END-EXEC.                                                            
371012*---------------------------------------------------------------*         
371013*---------------------------------------------------------------*         
371014     EXEC SQL                                                             
371015          INCLUDE TIMCNTK                                                 
371016     END-EXEC.                                                            
371017*---------------------------------------------------------------*         
371018*---------------------------------------------------------------*         
371019     EXEC SQL                                                             
371020          INCLUDE TIMCNPO                                                 
371021     END-EXEC.                                                            
371022*---------------------------------------------------------------*         
371023*---------------------------------------------------------------*         
371024     EXEC SQL                                                             
371025          INCLUDE TIMCNRL                                                 
371026     END-EXEC.                                                            
371027*---------------------------------------------------------------*         
371028*---------------------------------------------------------------*         
371029     EXEC SQL                                                             
371030          INCLUDE TIMPORL                                                 
371031     END-EXEC.                                                            
371032*---------------------------------------------------------------*         
371033*---------------------------------------------------------------*         
371034     EXEC SQL                                                             
371035          INCLUDE TSHPTPO                                                 
371036     END-EXEC.                                                            
371037*---------------------------------------------------------------*         
371038*---------------------------------------------------------------*         
371039     EXEC SQL                                                             
371040          INCLUDE TSHIPMT                                                 
371041     END-EXEC.                                                            
371042*---------------------------------------------------------------*         
371043*---------------------------------------------------------------*         
371044     EXEC SQL                                                             
371045          INCLUDE TVHTRIP                                                 
371046     END-EXEC.                                                            
371047*---------------------------------------------------------------*         
371048*---------------------------------------------------------------*         
371049     EXEC SQL                                                             
371050          INCLUDE TVEHICL                                                 
371051     END-EXEC.                                                            
371052*---------------------------------------------------------------*         
371053*---------------------------------------------------------------*         
371054     EXEC SQL                                                             
371055          INCLUDE TIMCNUP                                                 
371056     END-EXEC.                                                            
371057*---------------------------------------------------------------*         
371058*---------------------------------------------------------------*         
371059     EXEC SQL                                                             
371060          INCLUDE TAUCNUP                                                 
371061     END-EXEC.                                                            
371062*---------------------------------------------------------------*         
371063*---------------------------------------------------------------*         
371064     EXEC SQL                                                             
371065          INCLUDE TPOPRPK                                                 
371066     END-EXEC.                                                            
371067*---------------------------------------------------------------*         
371068*---------------------------------------------------------------*         
371069     EXEC SQL                                                             
371070          INCLUDE TPURITM                                                 
371071     END-EXEC.                                                            
371072*---------------------------------------------------------------*         
371073*---------------------------------------------------------------*         
371074     EXEC SQL                                                             
371075          INCLUDE TUPCSKU                                                 
371076     END-EXEC.                                                            
371077*---------------------------------------------------------------*         
371078*---------------------------------------------------------------*         
371079     EXEC SQL                                                             
371080          INCLUDE TUPC                                                    
371081     END-EXEC.                                                            
371082*---------------------------------------------------------------*         
371083*---------------------------------------------------------------*         
371084     EXEC SQL                                                             
371085          INCLUDE TSKXREF                                                 
371086     END-EXEC.                                                            
371087*---------------------------------------------------------------*         
371088*  ABEND PROCESSING COPYBOOK.                                    *        
371089*---------------------------------------------------------------*         
371090     COPY DPWS004.                                                        
371091*---------------------------------------------------------------* 01180006
371092*  DB2 COMMUNICATION AREA.                                        01190006
371093*---------------------------------------------------------------* 01200006
371094     EXEC SQL                                                     01210006
371095         INCLUDE SQLCA                                            01220006
371096     END-EXEC.                                                    01230006
371097*---------------------------------------------------------------* 01250006
371098*  THIS CURSOR PICKS UP UPC NUMBERS WHERE THE SKU IS ZERO ON              
371099*  TIMCNUP.                                                               
371100*---------------------------------------------------------------* 01250006
371101     EXEC SQL                                                             
371102          DECLARE NO-SKU-CSR CURSOR FOR                                   
371103             SELECT DISTINCT                                              
371104                    CNUP.UPC_NBR                                          
371105                   ,CNUP.PO_NBR                                           
371106                   ,CNUP.CONTNR_ID                                        
371107             FROM   TPURCHS PURC                                          
371108                   ,TIMCNUP CNUP                                          
371109                   ,TIMCNPO CNPO                                          
371110                   ,TIMCNRL CNRL                                          
371111                   ,TIMPORL IMPO                                          
371112             WHERE  CNUP.SKU_NBR          = 0                             
371113               AND  PURC.VER_STATUS_CDE   = 'A'                           
371114               AND  PURC.STATUS_CDE       IN ('AC','AP')                  
371115               AND  PURC.PKGNG_CDE        IN ('C','B')                    
371116               AND (CNRL.DCONSLR_RLSE_CDE IN ('S','N')                    
371117                OR (CNRL.DCONSLR_RLSE_CDE = 'D'                           
371118               AND  CNRL.DCONSLR_ACTL_DTE = '9999-09-09'))                
371119               AND (IMPO.DCONSLR_RLSE_CDE = 'N'                           
371120                OR  IMPO.DCONSLR_ACTL_DTE = '9999-09-09')                 
371130               AND  CNUP.PO_NBR           = PURC.PO_NBR                   
371140               AND  IMPO.PO_NBR           = PURC.PO_NBR                   
371150               AND  CNPO.PO_NBR           = PURC.PO_NBR                   
371160               AND  CNPO.CONTNR_ID        = CNRL.CONTNR_ID                
371170               AND  CNPO.PO_NBR           = CNRL.PO_NBR                   
371180               AND  CNPO.CONTNR_SEQ_NBR   = CNRL.CONTNR_SEQ_NBR           
371190               AND  1 > (SELECT COUNT(*)                                  
371200                         FROM   TSHPTPO A                                 
371300                               ,TSHIPMT B                                 
371400                               ,TVHTRIP C                                 
371500                               ,TVEHICL D                                 
371600                         WHERE  A.PO_NBR        = PURC.PO_NBR             
371700                           AND  B.SHIPT_ID      = A.SHIPT_ID              
371800                           AND  B.PRO_NBR       = CNRL.CONTNR_ID          
371900                           AND  C.TRIP_ID       = B.TRIP_ID               
372000                           AND  D.CURR_TRIP_ID  = C.TRIP_ID               
372001                           AND  D.VEH_TYP_CDE   IN ('VS','AV'))           
372002     END-EXEC.                                                            
372003***************************************************************** 01370006
372004 PROCEDURE DIVISION.                                              01380006
372005***************************************************************** 01390006
372006                                                                  01400006
372007*--------------------------------------------------------------*  01410006
372008* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE  *  01420006
372009* PROGRAM.                                                     *  01430006
372010*--------------------------------------------------------------*  01440006
372011 0000-PROGRAM-MAINLINE.                                           01450006
372012                                                                  01460006
372013     PERFORM 2110-OPEN-NO-SKU-CURSOR.                                     
372014     PERFORM 2000-PROCESS-NO-SKU-CURSOR                                   
372015       UNTIL PS-END-OF-NO-SKU-CUR.                                        
372016     PERFORM 2120-CLOSE-NO-SKU-CURSOR.                                    
372017                                                                          
372018     GOBACK.                                                      01490006
372019*--------------------------------------------------------------*  01640006
372020* FETCH DRIVING CURSOR, THEN GET SKU AND UPDATE TIMCNUP.                  
372030*--------------------------------------------------------------*  01640006
372040 2000-PROCESS-NO-SKU-CURSOR.                                      01650006
372050                                                                          
372060     PERFORM 2100-FETCH-NO-SKU-CURSOR.                                    
372070                                                                          
372080     IF PS-END-OF-NO-SKU-CUR                                              
372090        DISPLAY 'CHANGE COUNT = ' PV-NO-SKU-COUNT                         
372100     ELSE                                                                 
372200        DISPLAY 'PO NUM = '     IMCNUP-PO-NBR                             
372300                ' CONTNR ID = ' IMCNUP-CONTNR-ID                          
372400                ' UPC NUM = '   IMCNUP-UPC-NBR                            
              SET PS-UPC-NOT-FOUND TO TRUE                                      
372500        PERFORM 3000-SELECT-SKU-NBR                                       
372700        ADD +1 TO PV-NO-SKU-COUNT                                         
              IF PS-UPC-FOUND                                                   
                PERFORM 4000-UPDATE-IMCNUP-SKU                                  
              END-IF                                                            
372800     END-IF.                                                              
372900                                                                          
373000*--------------------------------------------------------------*  01640006
373100* FETCH DRIVING CURSOR.                                                   
373200*--------------------------------------------------------------*  01640006
373300 2100-FETCH-NO-SKU-CURSOR.                                        01650006
473541                                                                          
473542     EXEC SQL                                                             
473543         FETCH NO-SKU-CSR                                                 
473544         INTO :IMCNUP-UPC-NBR                                             
473545             ,:IMCNUP-PO-NBR                                              
473546             ,:IMCNUP-CONTNR-ID                                           
473572     END-EXEC.                                                            
473573                                                                          
473574     EVALUATE TRUE                                                        
473575         WHEN SQLCODE = ZERO                                              
473578              SET PS-END-OF-NO-SKU-CUR-NO TO TRUE                         
473579         WHEN SQLCODE = +100                                              
473580              SET PS-END-OF-NO-SKU-CUR    TO TRUE                         
473581         WHEN SQLWARN0 NOT = SPACES                                       
473582         WHEN SQLCODE < ZERO                                              
473583         WHEN SQLCODE > ZERO                                              
473584             MOVE '2100-FETCH-NO-SKU-CURSOR'                              
473585                                 TO PV-PARAGRAPH-NAME                     
473586             MOVE 'ERROR FETCHING WITH THE NO-SKU-CSR'                    
473587                                 TO PV-DB2-OPERATION-MESSAGE              
473588             MOVE 'TIMCNUP'      TO PV-DB2-TABLE-NAME-1                   
473589             MOVE 'TPURCHS'      TO PV-DB2-TABLE-NAME-2                   
473590             PERFORM 9100-DB2-ABEND                                       
473591     END-EVALUATE.                                                        
473592 EJECT                                                                    
473593                                                                          
473594*--------------------------------------------------------------*  01640006
473595* OPEN DRIVING CURSOR.                                                    
473596*--------------------------------------------------------------*  01640006
473597 2110-OPEN-NO-SKU-CURSOR.                                                 
473598                                                                          
473599     EXEC SQL                                                             
473600          OPEN NO-SKU-CSR                                                 
473601     END-EXEC.                                                            
473602                                                                          
473603     EVALUATE TRUE                                                        
476377         WHEN SQLCODE = ZERO                                              
476378             CONTINUE                                                     
476379         WHEN SQLWARN0 NOT = SPACES                                       
476380         WHEN SQLCODE < ZERO                                              
476381         WHEN SQLCODE > ZERO                                              
476382             MOVE '2110-OPEN-NO-UPC-CURSOR'                               
476383                                      TO PV-PARAGRAPH-NAME                
476384             MOVE 'ATTEMPTING TO OPEN THE NO-SKU-CSR'                     
476385                                      TO PV-DB2-OPERATION-MESSAGE         
476386             MOVE 'TIMCNUP'           TO PV-DB2-TABLE-NAME-1              
476387             MOVE 'TPURCHS'           TO PV-DB2-TABLE-NAME-3              
476388             PERFORM 9100-DB2-ABEND                                       
476389     END-EVALUATE.                                                        
476390                                                                          
476391*--------------------------------------------------------------*  01640006
476392* CLOSE DRIVING CURSOR.                                                   
476393*--------------------------------------------------------------*  01640006
476394 2120-CLOSE-NO-SKU-CURSOR.                                                
476395                                                                          
476396     EXEC SQL                                                             
476397         CLOSE NO-SKU-CSR                                                 
476398     END-EXEC.                                                            
476399                                                                          
476400     EVALUATE TRUE                                                        
476401         WHEN SQLCODE = ZERO                                              
476402             CONTINUE                                                     
476403         WHEN SQLWARN0 NOT = SPACES                                       
476404         WHEN SQLCODE < ZERO                                              
476405         WHEN SQLCODE > ZERO                                              
476406             MOVE '2120-CLOSE-NO-SKU-CURSOR'                              
476407                                      TO PV-PARAGRAPH-NAME                
476408             MOVE 'ATTEMPTING TO CLOSE THE NO-SKU-CSR'                    
476409                                      TO PV-DB2-OPERATION-MESSAGE         
476410             MOVE 'TIMCNUP'           TO PV-DB2-TABLE-NAME-1              
476411             MOVE 'TPURCHS'           TO PV-DB2-TABLE-NAME-2              
476412             PERFORM 9100-DB2-ABEND                                       
476413     END-EVALUATE.                                                        
476414                                                                          
476415*--------------------------------------------------------------*  01640006
476416* GET NEW SKU NUMBER.                                                     
476417*--------------------------------------------------------------*  01640006
476418 3000-SELECT-SKU-NBR.                                                     
476419                                                                          
476420     EXEC SQL                                                             
476430          SELECT XREF.SKU                                                 
476440          INTO  :SKXREF-SKU                                               
476450          FROM   TUPCSKU SKU                                              
476460                ,TSKXREF XREF                                             
476470          WHERE  SKU.UPC_NBR = :IMCNUP-UPC-NBR                            
476480            AND  SKU.ITEM_NBR = XREF.ITM_NBR                              
476490     END-EXEC.                                                            
476491                                                                          
476492     EVALUATE TRUE                                                        
476493          WHEN SQLCODE = ZERO                                             
476494              DISPLAY '  SKU = ' SKXREF-SKU                               
                    SET PS-UPC-FOUND TO TRUE                                    
476495          WHEN SQLCODE = +100                                             
476496              DISPLAY '  NO SKU FOUND'                                    
476497          WHEN SQLWARN0 NOT = SPACES                                      
476498          WHEN SQLCODE < ZERO                                             
476499          WHEN SQLCODE > ZERO                                             
476500              MOVE '3000-SELECT-SKU-NBR'                                  
476501                                   TO  PV-PARAGRAPH-NAME                  
476502              MOVE 'ERROR ISSUED ON SELECT'                               
476503                                   TO  PV-DB2-OPERATION-MESSAGE           
476504              MOVE 'TIMCNUP'       TO  PV-DB2-TABLE-NAME-1                
476505              PERFORM 9100-DB2-ABEND                                      
476506     END-EVALUATE.                                                        
476507                                                                          
476508*--------------------------------------------------------------*  01640006
476509* UPDATE TIMCNUP WITH SKU NUMBER.                                         
476510*--------------------------------------------------------------*  01640006
476520 4000-UPDATE-IMCNUP-SKU.                                                  
476530                                                                          
476540     EXEC SQL                                                             
476550          UPDATE TIMCNUP                                                  
476560          SET    SKU_NBR   = :SKXREF-SKU                                  
476570          WHERE  UPC_NBR   = :IMCNUP-UPC-NBR                              
476580            AND  PO_NBR    = :IMCNUP-PO-NBR                               
476590            AND  CONTNR_ID = :IMCNUP-CONTNR-ID                            
476600     END-EXEC.                                                            
476601                                                                          
476602     EVALUATE TRUE                                                        
476603          WHEN SQLCODE = ZERO                                             
476604              CONTINUE                                                    
476605          WHEN SQLWARN0 NOT = SPACES                                      
476606          WHEN SQLCODE < ZERO                                             
476607          WHEN SQLCODE > ZERO                                             
476608              MOVE '4000-UPDATE-IMCNUP-SKU'                               
476609                                   TO  PV-PARAGRAPH-NAME                  
476610              MOVE 'ERROR ISSUED ON UPDATE'                               
476611                                   TO  PV-DB2-OPERATION-MESSAGE           
476612              MOVE 'TIMCNUP'       TO  PV-DB2-TABLE-NAME-1                
476613              PERFORM 9100-DB2-ABEND                                      
476614     END-EVALUATE.                                                        
476615                                                                          
496502*----------------------------------------------------------------*        
496503*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
496504*  ERROR OR WARNING CODE OCCURS.                                          
496505*----------------------------------------------------------------*        
496506 9100-DB2-ABEND.                                                          
496507                                                                          
496508     DISPLAY '9100-DB2-ABEND'.                                            
496509     DISPLAY '** ABEND           **'.                                     
496510     DISPLAY '** PROGRAM         ** = ' PC-PGM-NAME.                      
496511     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
496512     DISPLAY '** TABLES AFFECTED ** = ' PV-DB2-TABLE-NAME-1.              
496513     DISPLAY '**                 ** = ' PV-DB2-TABLE-NAME-2.              
496514     DISPLAY '**                 ** = ' PV-DB2-TABLE-NAME-3.              
496515     DISPLAY '** OPERATION       ** = ' PV-DB2-OPERATION-MESSAGE.         
496519                                                                          
496520*----------------------------------------------------------------*        
496521* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
496522* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
496523* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
496524* FORMAT.                                                                 
496525*----------------------------------------------------------------*        
496526     COPY DPPD004.                                                        
496530                                                                          
496531     SET PC-DB2-ERROR TO TRUE.                                            
496532     CALL 'ILBOABN0' USING ABEND-CODE.                                    
496533*                                                                         
