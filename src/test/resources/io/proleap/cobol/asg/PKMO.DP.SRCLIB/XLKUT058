000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400 PROGRAM-ID.    XLKUT058.                                                 
000500 AUTHOR.        PAUL OMAN.                                                
000600 INSTALLATION.  KOHLS.                                                    
000700 DATE-WRITTEN   APRIL 2003.                                               
000800 DATE-COMPILED.                                                           
000810                                                                          
000820****************************************************************          
000830*    PROGRAM READS LOGISTICS DOMAIN PACK UPC INTO XLT00009     *          
000831*    LAYOUT SELECTING ONLY THOSE FIELDS NEEDED TO FORMAT       *          
000840*    THE OUTPUT RECORDS (XLRD031) PACK UPC DATA.               *          
000860*                                                              *          
000900*****************************************************************         
001000                                                                          
001300 ENVIRONMENT DIVISION.                                                    
001400 INPUT-OUTPUT SECTION.                                                    
001500                                                                          
001600 FILE-CONTROL.                                                            
001700                                                                          
001800     SELECT LEGACY-PACK-UPC-FILE                                          
001900         ASSIGN TO INP01.                                                 
002000                                                                          
002100     SELECT PACK-UPC-FILE                                                 
002200         ASSIGN TO OUT01.                                                 
002300                                                                          
002400*****************************************************************         
002500 DATA DIVISION.                                                           
002600*****************************************************************         
002700 FILE SECTION.                                                            
002800                                                                          
002900 FD  LEGACY-PACK-UPC-FILE                                                 
003000     RECORDING MODE IS F                                                  
003100     BLOCK CONTAINS 0 RECORDS                                             
003200     LABEL RECORDS ARE STANDARD.                                          
003300                                                                          
003400 01  LEGACY-PACK-UPC-RECORD       PIC X(14).                              
003500                                                                          
003600 FD  PACK-UPC-FILE                                                        
003700     RECORDING MODE IS F                                                  
003800     BLOCK CONTAINS 0 RECORDS                                             
003900     LABEL RECORDS ARE STANDARD.                                          
004000                                                                          
004100 01  PACK-UPC-RECORD              PIC X(144).                             
004200                                                                          
004300                                                                          
004400*****************************************************************         
004500 WORKING-STORAGE SECTION.                                                 
004600                                                                          
004700 01  PS-PROGRAM-SWITCHES.                                                 
004800     05  PS-EOF-INPUT-SW              PIC X(01)  VALUE 'N'.               
004900         88  PS-EOF-INPUT-YES                    VALUE 'Y'.               
005000         88  PS-EOF-INPUT-NO                     VALUE 'N'.               
005100                                                                          
005200 01  PC-PROGRAM-CONSTANTS.                                                
005300     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XLKUT058'.        
005400                                                                          
005500 01  PV-PROGRAM-VARIABLES.                                                
005600     05  PV-RECORDS-READ              PIC  9(09) VALUE  0.                
005700     05  PV-RECORDS-WRITTEN           PIC  9(09) VALUE  0.                
005800                                                                          
005900 01  PV-ABEND-FIELDS.                                                     
006000     05  PV-ABEND-CODE                PIC  S9(4) VALUE +0                 
006100                                                 COMP SYNC.               
006200     05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'XLKUT058'.        
006300     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
006400     05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.            
006500     05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.            
006600     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
006700     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
006800     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
006900                                                                          
007100***********************************************************               
007200*    LAYOUT OF PACK UPC INPUT FILE                        *               
007300***********************************************************               
007495 01  PV-XLT00009-RECORD.                                                  
007496     10 XLT00009-PK-SKU-NBR           PIC  S9(8)V COMP-3.                 
007497     10 XLT00009-UPC-NBR              PIC  S9(15)V COMP-3.                
007498     10 XLT00009-EDI-ORD-IND          PIC   X(1).                         
007510                                                                          
007600***********************************************************               
007700*    PACK UPC OUTPUT FILE                                 *               
007800***********************************************************               
007900 01  PV-PACK-UPC-RECORD.                                                  
008000     COPY XLRD031.                                                        
008100                                                                          
008200*****************************************************************         
008300 PROCEDURE DIVISION.                                                      
008400*****************************************************************         
008500     DISPLAY ' '.                                                         
008600     DISPLAY '**** BEGINNING XLKUT058 ****'.                              
008700     DISPLAY ' '.                                                         
008800                                                                          
008900     OPEN INPUT  LEGACY-PACK-UPC-FILE                                     
009000          OUTPUT PACK-UPC-FILE.                                           
009100                                                                          
009110     SET PS-EOF-INPUT-NO TO TRUE.                                         
009200     PERFORM 1000-READ-INPUT-FILE.                                        
009300                                                                          
009400     PERFORM 2000-PROCESS-INPUT-FILE                                      
009500       UNTIL PS-EOF-INPUT-YES.                                            
009600                                                                          
009700     CLOSE LEGACY-PACK-UPC-FILE                                           
009800           PACK-UPC-FILE.                                                 
009900                                                                          
010000     DISPLAY '      INPUT LEGACY PACK RECS = '                            
010100              PV-RECORDS-READ.                                            
010200     DISPLAY 'OUTPUT LEGACY PACK DATA RECS = '                            
010300              PV-RECORDS-WRITTEN.                                         
010400     DISPLAY ' '.                                                         
010500     DISPLAY '****  END OF  XLKUT058  ****'.                              
010600     DISPLAY ' '.                                                         
010700                                                                          
010800     GOBACK.                                                              
010900                                                                          
011000                                                                          
011100*****************************************************************         
011200*  READ A LEGACY SKU RECORD                                               
011300*****************************************************************         
011400 1000-READ-INPUT-FILE.                                                    
011500     INITIALIZE PV-XLT00009-RECORD.                                       
011600                                                                          
011700     READ LEGACY-PACK-UPC-FILE                                            
011800          INTO PV-XLT00009-RECORD                                         
011900            AT END                                                        
012000               SET PS-EOF-INPUT-YES TO TRUE                               
012100     END-READ.                                                            
012200                                                                          
012300     IF PS-EOF-INPUT-YES                                                  
012400        CONTINUE                                                          
012500     ELSE                                                                 
012600        ADD +1 TO PV-RECORDS-READ                                         
012700     END-IF.                                                              
012800                                                                          
012900*****************************************************************         
013000*  REFORMAT THE LEGACY PACK HEADER FILE                         *         
013100*****************************************************************         
013200 2000-PROCESS-INPUT-FILE.                                                 
013300                                                                          
013310     INITIALIZE PV-PACK-UPC-RECORD.                                       
013400     MOVE XLT00009-PK-SKU-NBR       TO XL031-UPC-PK-SKU-NBR.              
013500     MOVE XLT00009-UPC-NBR          TO XL031-UPC-UPC-NBR.                 
013600     MOVE XLT00009-EDI-ORD-IND      TO XL031-UPC-EDI-ORD-IND.             
013600     MOVE SPACES                    TO XL031-UPC-ERROR-MESSAGE.           
013700                                                                          
013800     PERFORM 2100-WRITE-OUTPUT.                                           
013900                                                                          
014000     PERFORM 1000-READ-INPUT-FILE.                                        
014100                                                                          
014200                                                                          
014300*****************************************************************         
014400*  WRITE A RECORD TO THE PACK DATA OUTPUT FILE                  *         
014500*****************************************************************         
014600 2100-WRITE-OUTPUT.                                                       
014700     WRITE PACK-UPC-RECORD FROM                                           
014800           PV-PACK-UPC-RECORD                                             
014900     END-WRITE.                                                           
015000     ADD +1                 TO PV-RECORDS-WRITTEN.                        
015100                                                                          
