      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PEKUT100.                                        
       AUTHOR.                 KELLY LARUM                                      
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           MAY 2004.                                        
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PEKUT100 - PRICING INTERFACE CONTROL PROGRAM                *        
      *                                                                *        
      *    THIS PROGRAM CREATES THE DATE RANGE AND TIME STAMP FILES    *        
      *    THAT ARE TO BE USED FOR THE DATA INTEGRITY PROGRAMS.        *        
      *    THIS WAY ALL THE PROGRAMS ARE GOING OFF OF THE SAME DATE    *        
      *    AND TIME.                                                   *        
      *    THIS PROGRAM ALSO INSERTS THE END-OF-DAY ROW ONTO THE       *        
      *    PERM INTERFACE CONTROL TABLE.                               *        
      *    THIS ROW IS READ BY THE PERM INTERFACE AND AN EOD MESSAGE   *        
      *    IS SENT TO MESSAGING DOMAINS.                               *        
      *    WHEN OTHER DOMAINS READ THIS MESSAGE, THEY STOP READING     *        
      *    FROM THE PERM QUEUE AND START RUNNING THEIR DATA INTEGRITY  *        
      *    EXTRACTS.                                                   *        
      *    AFTER THIS JOB FINISHES, THE PERM DATA INTEGRITY EXTRACTS   *        
      *    ARE KICKED OFF AS WELL.                                     *        
      *                                                                *        
      *    THIS PROGRAM WAS COPIED FROM PMKUT070.                      *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      * DATE:         07/30/2018                                       *        
      * WR/IR#:       CHG0204126                                       *        
      * PROGRAMMER:   COGNIZANT                                        *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT CONTROL-CARD                                                  
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT DATE-RANGE-FILE                                               
               ASSIGN                       TO OUT01.                           
                                                                                
           SELECT TMST-FILE                                                     
               ASSIGN                       TO OUT02.                           
                                                                                
           SELECT SQL-PLUS-FILE                                                 
               ASSIGN                       TO OUT03.                           
                                                                                
070941                                                                          
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
070941                                                                          
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD                                                         
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-LINE                PIC  X(80).                         
                                                                                
       FD  DATE-RANGE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  DATE-RANGE-LINE                  PIC X(80).                          
                                                                                
       FD  TMST-FILE                                                            
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  TMST-LINE                        PIC X(80).                          
                                                                                
       FD  SQL-PLUS-FILE                                                        
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  SQL-PLUS-LINE                    PIC X(79).                          
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD      PIC X(80).                               
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-RUN-DATE                  PIC X(08)   VALUE SPACES.           
           05  FILLER                       PIC X(01).                          
           05  CC-DAYS-OF-HISTORY           PIC 9(04)   VALUE ZEROS.            
           05  FILLER                       PIC X(01).                          
           05  CC-DAYS-IN-FUTURE            PIC 9(04)   VALUE ZEROS.            
           05  FILLER                       PIC X(01).                          
           05  CC-NUMBER-OF-HOURS           PIC 9(02)   VALUE ZEROS.            
           05  FILLER                       PIC X(53).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)     VALUE                
                                                          'PEKUT100'.           
           05  PC-BEG-TMST                  PIC  X(09)     VALUE                
                                                          '-00.00.00'.          
           05  PC-END-TMST                  PIC  X(09)     VALUE                
                                                          '-23.59.59'.          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CONTROL-CARD-SW           PIC  X(01)     VALUE 'N'.           
               88  PS-CONTROL-CARD-END                     VALUE 'Y'.           
               88  PS-CONTROL-CARD-START                   VALUE 'N'.           
070941     05  PS-END-OF-LOGON-FILE-SW         PIC X(1) VALUE 'N'.              
070941         88  PS-END-OF-LOGON-FILE                 VALUE 'Y'.              
070941     05  PS-ORA-SRC-CTL-DONE-SW          PIC X(1) VALUE 'N'.              
070941         88  PS-ORA-SRC-EMPTY                     VALUE 'Y'.              
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                         VALUE '/'.              
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4) VALUE ZEROS               
070941                                              COMP SYNC.                  
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4) VALUE ZEROS               
070941                                              COMP SYNC.                  
           05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACES.         
           05  PV-OPERATION-MSG             PIC  X(40)    VALUE SPACES.         
           05  PV-OPERATION-MSG-1           PIC  X(40)    VALUE SPACES.         
           05  PV-OPERATION-MSG-2           PIC  X(40)    VALUE SPACES.         
           05  PV-SYS-DATE                  PIC  X(10)    VALUE SPACES.         
           05  PV-DATE-RANGE.                                                   
               10  PV-DR-HISTORY-DTE        PIC X(10).                          
               10  FILLER                   PIC X(01)  VALUE SPACES.            
               10  PV-DR-FUTURE-DTE         PIC X(10).                          
           05  PV-TMST                      PIC X(19)  VALUE SPACES.            
           05  PV-PRCS-STRT-DTE-LNE.                                            
               10  PC-STRT-DEFINE           PIC X(23)  VALUE                    
                                   'DEFINE PRCS_STRT_DT = '''.                  
               10  PV-PRCS-STRT-DTE         PIC X(19).                          
               10  FILLER                   PIC X(01)  VALUE ''''.              
           05  PV-PRCS-END-DTE-LNE.                                             
               10  PC-END-DEFINE            PIC X(22)  VALUE                    
                                   'DEFINE PRCS_END_DT = '''.                   
               10  PV-PRCS-END-DTE          PIC X(19).                          
               10  FILLER                   PIC X(01)  VALUE ''''.              
           05  PV-PRCS-RUN-DTE-LNE.                                             
               10  PC-RUN-DEFINE            PIC X(22)  VALUE                    
                                   'DEFINE PRCS_RUN_DT = '''.                   
               10  PV-PRCS-RUN-DTE          PIC X(19).                          
               10  FILLER                   PIC X(01)  VALUE ''''.              
                                                                                
      * DECLARE VARIABLES FOR SUBROUTINE CALL  (SQLCODE TEXT)                   
       01  WS-MSG-TEXT                   PIC X(512).                            
       01  WS-MAX-SIZE                   PIC S9(09) COMP                        
                                                    VALUE 512.                  
       01  WS-MSG-LENGTH                 PIC S9(09) COMP.                       
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - LOGON TO ORACLE                                            *         
      *  - OPEN THE INPUT AND OUTPUT FILES                            *         
      *  - READ THE CONTROL CARD                                      *         
      *  - RUN A SELECT STATEMENT TO GET THE DATES                    *         
      *  - WRITE TO THE DATERANGE FILE                                *         
      *  - WRITE TO THE TIMESTAMP FILE                                *         
      *  - CLOSE INPUT AND OUTPUT FILES                               *         
      * ==> PERFORMED FROM:  START                                    *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
070941*    PERFORM PE700-A000-ORACLE-SETUP.                                     
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B150-READ-CONTROL-CARD.                                      
                                                                                
           PERFORM C100-FIND-DATES.                                             
                                                                                
           PERFORM B200-WRITE-TO-DATERANGE-FILE.                                
           PERFORM B250-WRITE-TO-TMST-FILE.                                     
           PERFORM B275-WRITE-TO-SQL-PLUS-FILE.                                 
                                                                                
           PERFORM D100-INSERT-EOD-ROW.                                         
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES                                               *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE CONTROL CARD AND THE TWO OUTPUT FILES             *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  CONTROL-CARD                                             
                OUTPUT DATE-RANGE-FILE                                          
                OUTPUT TMST-FILE                                                
                OUTPUT SQL-PLUS-FILE.                                           
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
070941                                                                          
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941                ' ORACLE LOGON CONTROL CARD NOT PROVIDED'                 
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-CURRENT-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'        
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-CURRENT-PROGRAM-NAME                                     
070941              ' - LOGON TO ORACLE AS USER '                               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY 'B100-OPEN-FILES'                                        
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941                 ' - ERROR LOGGING ON TO ORACLE'                          
070941         PERFORM Z100-ABEND                                               
070941     END-IF.                                                              
070941                                                                          
070941                                                                          
070941     EXEC SQL                                                             
070941        ALTER SESSION                                                     
070941           SET NLS_DATE_FORMAT = 'YYYY-MM-DD-HH24.MI.SS'                  
070941     END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * B150-READ-CONTROL-CARD                                        *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE CONTROL CARD                                   *         
      *  - YYYYMMDD #### #### ##                                                
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B150-READ-CONTROL-CARD.                                                  
                                                                                
           MOVE 'B150-READ-CONTROL-CARD'   TO PV-PARAGRAPH-NAME.                
           SET PS-CONTROL-CARD-START       TO TRUE.                             
                                                                                
           READ CONTROL-CARD INTO CC-CONTROL-CARD                               
               AT END                                                           
                   SET PS-CONTROL-CARD-END TO TRUE                              
           END-READ.                                                            
                                                                                
           DISPLAY 'CC-RUN-DATE = ' CC-RUN-DATE.                                
           DISPLAY 'CC-DAYS-OF-HISTORY = ' CC-DAYS-OF-HISTORY.                  
           DISPLAY 'CC-DAYS-IN-FUTURE = ' CC-DAYS-IN-FUTURE.                    
           DISPLAY 'CC-NUMBER-OF-HOURS = ' CC-NUMBER-OF-HOURS.                  
                                                                                
           IF PS-CONTROL-CARD-END                                               
               MOVE 'NO CONTROL CARD TO PROCESS' TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'PEKUT100 CONTROL CARD = ' CC-CONTROL-CARD               
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B200-WRITE-TO-DATERANGE-FILE                                  *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE START/END EFFECTIVE DATE RANGE TO A FILE THAT    *         
      *    WILL BE READ BY THE XS DOMAIN PROGRAMS.                    *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-WRITE-TO-DATERANGE-FILE.                                            
                                                                                
           DISPLAY 'PV-DATE-RANGE = ' PV-DATE-RANGE.                            
           MOVE PV-DATE-RANGE TO DATE-RANGE-LINE.                               
           WRITE DATE-RANGE-LINE.                                               
                                                                                
      *****************************************************************         
      * B250-WRITE-TO-TMST-FILE                                       *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE CPU TIMESTAMP TO A FILE THAT WILL BE READ BY     *         
      *    THE XS DOMAIN PROGRAMS.                                    *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B250-WRITE-TO-TMST-FILE.                                                 
                                                                                
           DISPLAY 'PV-TMST = ' PV-TMST.                                        
           MOVE PV-TMST TO TMST-LINE.                                           
           WRITE TMST-LINE.                                                     
                                                                                
      *****************************************************************         
      * B275-WRITE-TO-SQL-PLUS-FILE                                   *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE CPU TIMESTAMP TO A FILE THAT WILL BE READ BY     *         
      *    THE XS DOMAIN PROGRAMS.                                    *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B275-WRITE-TO-SQL-PLUS-FILE.                                             
                                                                                
           DISPLAY 'PV-TMST = ' PV-TMST.                                        
           STRING PV-DR-HISTORY-DTE DELIMITED BY SPACE                          
                  PC-BEG-TMST   DELIMITED BY SIZE                               
                  INTO PV-PRCS-STRT-DTE.                                        
           STRING PV-DR-FUTURE-DTE DELIMITED BY SPACE                           
                  PC-END-TMST   DELIMITED BY SIZE                               
                  INTO PV-PRCS-END-DTE.                                         
           MOVE PV-TMST TO PV-PRCS-RUN-DTE.                                     
                                                                                
           MOVE PV-PRCS-STRT-DTE-LNE  TO SQL-PLUS-LINE.                         
           WRITE SQL-PLUS-LINE AFTER ADVANCING 1.                               
           MOVE PV-PRCS-END-DTE-LNE   TO SQL-PLUS-LINE.                         
           WRITE SQL-PLUS-LINE AFTER ADVANCING 1.                               
           MOVE PV-PRCS-RUN-DTE-LNE   TO SQL-PLUS-LINE.                         
           WRITE SQL-PLUS-LINE AFTER ADVANCING 1.                               
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE CONTROL CARD AND TWO OUTPUT FILES.               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  CONTROL-CARD                                                  
                  DATE-RANGE-FILE                                               
                  TMST-FILE                                                     
                  SQL-PLUS-FILE.                                                
                                                                                
      *****************************************************************         
      * C100-FIND-DATES:                                              *         
      * ==> PROCESSES:                                                *         
      *  - SINCE NO TABLE IS ACTUAL USED WE USE THE DUAL TABLE.       *         
      *  - FIND THE START AND END EFFECTIVE DATE                      *         
      *  - FIND THE RUN TIME MINUS THE NBR OF HOURS SPECIFIED.        *         
      *  - IF THE DAYS OF HISTORY ARE 9999 THEN DEFAULT THE DATE TO   *         
      *    0001-01-01                                                 *         
      *  - IF THE DAYS OF FUTURE ARE 9999 THEN DEFAULT THE DATE TO    *         
      *    9999-12-31                                                 *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C100-FIND-DATES.                                                         
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                   TO_CHAR(TO_DATE(:CC-RUN-DATE, 'YYYYMMDD')                    
                                 - :CC-DAYS-OF-HISTORY, 'YYYY-MM-DD'),          
                   TO_CHAR(TO_DATE(:CC-RUN-DATE, 'YYYYMMDD')                    
                                 + :CC-DAYS-IN-FUTURE, 'YYYY-MM-DD'),           
                   TO_CHAR(SYSDATE - (:CC-NUMBER-OF-HOURS / 24),                
                                'YYYY-MM-DD-HH24.MI.SS')                        
               INTO                                                             
                   :PV-DR-HISTORY-DTE,                                          
                   :PV-DR-FUTURE-DTE,                                           
                   :PV-TMST                                                     
               FROM                                                             
                   DUAL                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   IF CC-DAYS-OF-HISTORY = '9999'                               
                       MOVE '0001-01-01'         TO PV-DR-HISTORY-DTE           
                   END-IF                                                       
                                                                                
                   IF CC-DAYS-IN-FUTURE = '9999'                                
                       MOVE '9999-12-31'         TO PV-DR-FUTURE-DTE            
                   END-IF                                                       
                                                                                
                   DISPLAY 'HISTORY DATE = ' PV-DR-HISTORY-DTE                  
                   DISPLAY 'FUTURE DATE  = ' PV-DR-FUTURE-DTE                   
                   DISPLAY 'NBR OF HOURS = ' PV-TMST                            
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY 'C100-FIND-DATES'                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D100-INSERT-EOD-ROW.                                          *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       D100-INSERT-EOD-ROW.                                                     
                                                                                
           EXEC SQL                                                             
               INSERT INTO PET_INTFC_CNTL_TBL                                   
               (                                                                
               INTFC_CNTL_TBL_ID,                                               
               TBL_NM,                                                          
               KY_COL_NM,                                                       
               KY_COL_ID,                                                       
               PRC_CDE,                                                         
               ACTN_CDE,                                                        
               ISRT_TMST,                                                       
               PRTY_CDE,                                                        
               REC_PRC_TMST                                                     
               )                                                                
               VALUES                                                           
               (                                                                
               PES_INTFC_CNTL_TBL_SEQ.NEXTVAL,                                  
               'END-OF-DAY-1.0',                                                
               'END-OF-DAY',                                                    
               9999,                                                            
               'N',                                                             
               'I',                                                             
               SYSDATE,                                                         
               NULL,                                                            
               NULL)                                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   DISPLAY 'EOD ROW INSERTED SUCCESSFULLY'                      
               WHEN OTHER                                                       
                   MOVE 'D100-INSERT-EOD-ROW' TO PV-PARAGRAPH-NAME              
                   DISPLAY 'ERROR INSERTING EOD ROW TO: '                       
                   DISPLAY 'PET_PERM_INTFC_CNTL_TBL.'                           
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PEPD700.                                                        
                                                                                
      *****************************************************************         
      *Z100-ABEND                                                     *         
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE            *         
      *****************************************************************         
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
                                                                                
                                                                                
           MOVE SPACES TO WS-MSG-TEXT.                                          
      *    GET FULL TEXT OF ERROR MESSAGE                                       
                                                                                
           CALL 'SQLGLM' USING WS-MSG-TEXT,                                     
                               WS-MAX-SIZE,                                     
                               WS-MSG-LENGTH.                                   
           DISPLAY 'MESSAGE TEXT      ** = ' WS-MSG-TEXT.                       
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
