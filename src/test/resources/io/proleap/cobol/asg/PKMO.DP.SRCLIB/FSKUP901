       IDENTIFICATION DIVISION.                                         00010099
       PROGRAM-ID.         FSKUP901.                                    00020099
       AUTHOR.             COGNIZANT.                                   00030099
       INSTALLATION.       KOHLS DEPARTMENT STORES.                     00040099
       DATE-WRITTEN.       OCT  2014.                                   00050099
       DATE-COMPILED.                                                   00060099
                                                                        00070099
      ******************************************************************00080099
      *         FSKUP901 - PROGRAM TO UPDATE TIMESTAMP                 *00090099
      ******************************************************************00100099
      * THIS BATCH PROGRAM IS USED TO UPDATE THE TIMESTAMP IN TSKST    *00110099
      * TABLE.                                                         *00120099
      *                                                                *00130099
      * FOR EACH TYPE OF RECORDS THE LAST UPDATED TIMESTAMP IS UPDATED *00140099
      * TO THE CURRENT TIMESTAMP. THE FOLLOWING ARE THE TYPES OF       *00150099
      * RECORDS USED                                                   *00151099
      * 1. STORE NUMBER:                                               *00160099
      *    THE TIMESTAMP IS UPDATED WITH CURRENT TIME STAMP BASED ON   *00170099
      *    VARYING ITEM RANGE FOR THE REQUESTED STORES IN INP01.       *00180099
      * 2. STORE NUMBER,SKU:                                           *00190099
      *    THE TIMESTAMP IS UPDATED WITH CURRENT TIME STAMP BASED FOR  *00200099
      *    THE GIVE STORE NUMBER AND SKU IN INP02.                     *00210099
      *                                                                *00220099
      * WR/PROJ DATE       PROGAMMER    DESCRIPTION OF CHANGES         *00230099
      * ------- ---------- ------------ -------------------------------*00240099
      * P50594  10-30-2014 COGNIZANT    INITIAL CREATION               *00250099
FIX113* FIX113  11-03-2014 COGNIZANT    MODIFIED THE LOCATION COMMIT   *00251099
FIX113* FREQUENCY TO COMMIT EVERY ITEM RANGE AND ADDED DISPLAYS.       *00260099
      ******************************************************************00270099
                                                                        00280099
       ENVIRONMENT DIVISION.                                            00290099
       CONFIGURATION SECTION.                                           00300099
                                                                        00310099
       SOURCE-COMPUTER.    IBM-3090.                                    00320099
       OBJECT-COMPUTER.    IBM-3090.                                    00330099
                                                                        00340099
       INPUT-OUTPUT SECTION.                                            00350099
       FILE-CONTROL.                                                    00360099
           SELECT LOC-NBR-FILE              ASSIGN TO INP01.            00370099
                                                                        00380099
           SELECT LOC-SKU-FILE              ASSIGN TO INP02.            00390099
                                                                        00400099
           SELECT LOC-ITEM-RANGE-FILE       ASSIGN TO INP03.            00410099
                                                                        00420099
           SELECT LOC-NBR-FILE-OP           ASSIGN TO OUT01.            00430099
                                                                        00440099
           SELECT LOC-SKU-FILE-OP           ASSIGN TO OUT02.            00441099
                                                                        00442099
      ******************************************************************00450099
       DATA DIVISION.                                                   00460099
      ******************************************************************00470099
                                                                        00480099
       FILE SECTION.                                                    00490099
                                                                        00500099
       FD  LOC-NBR-FILE                                                 00510099
           LABEL RECORDS ARE STANDARD                                   00520099
           RECORDING MODE IS F                                          00530099
           BLOCK CONTAINS 0 RECORDS                                     00540099
           RECORD CONTAINS 10 CHARACTERS                                00550099
           DATA RECORD IS LOC-NBR-RECORD.                               00560099
       01  LOC-NBR-RECORD              PIC  9(10).                      00570099
                                                                        00580099
       FD  LOC-SKU-FILE                                                 00590099
           LABEL RECORDS ARE STANDARD                                   00600099
           RECORDING MODE IS F                                          00610099
           BLOCK CONTAINS 0 RECORDS                                     00620099
           RECORD CONTAINS 20 CHARACTERS                                00630099
           DATA RECORD IS LOC-SKU-RECORD.                               00640099
       01  LOC-SKU-RECORD              PIC  9(20).                      00650099
                                                                        00660099
       FD  LOC-ITEM-RANGE-FILE                                          00670099
           LABEL RECORDS ARE STANDARD                                   00680099
           RECORDING MODE IS F                                          00690099
           BLOCK CONTAINS 0 RECORDS                                     00700099
           RECORD CONTAINS 80 CHARACTERS                                00710099
           DATA RECORD IS LOC-ITEM-RANGE-REC.                           00720099
       01  LOC-ITEM-RANGE-REC         PIC  X(80).                       00730099
                                                                        00740099
       FD  LOC-NBR-FILE-OP                                              00750099
           LABEL RECORDS ARE STANDARD                                   00760099
           RECORDING MODE IS F                                          00770099
           BLOCK CONTAINS 0 RECORDS                                     00780099
           RECORD CONTAINS 10 CHARACTERS                                00790099
           DATA RECORD IS LOC-NBR-OP-RECORD.                            00800099
       01  LOC-NBR-OP-RECORD          PIC  9(10).                       00810099
                                                                        00820099
       FD  LOC-SKU-FILE-OP                                              00821099
           LABEL RECORDS ARE STANDARD                                   00822099
           RECORDING MODE IS F                                          00823099
           BLOCK CONTAINS 0 RECORDS                                     00824099
           RECORD CONTAINS 20 CHARACTERS                                00825099
           DATA RECORD IS LOC-SKU-FILE-OP-REC.                          00826099
       01  LOC-SKU-FILE-OP-REC        PIC  9(20).                       00827099
                                                                        00828099
       WORKING-STORAGE SECTION.                                         00830099
                                                                        00840099
       01  ABEND-CODE                  PIC S9(04)  VALUE +0 COMP SYNC.  00850099
           88  PV-ABEND-4001                   VALUE +4001.             00860099
                                                                        00870099
       01  WS-LOC-NBR-FILE.                                             00880099
           05  WS-LOC-NBR                   PIC 9(04).                  00890099
           05  FILLER                       PIC X(06).                  00900099
                                                                        00910099
       01  WS-LOC-TEMP                      PIC 9(04).                  00920099
                                                                        00930099
       01  WS-TEMP-VARIABLES.                                           00940099
           05  WS-MAX-COUNT                 PIC 9(08).                  00950099
           05  FILLER                       PIC X(01).                  00960099
           05  WS-CMT-FREQ                  PIC 9(09).                  00970099
           05  FILLER                       PIC X(01).                  00980099
           05  WS-TIME-STAMP                PIC X(26).                  00990099
           05  FILLER                       PIC X(35).                  01000099
                                                                        01010099
       01  WS-LOC-SKU.                                                  01020099
           05  WS-LOC-NBR1                  PIC 9(04).                  01030099
           05  FILLER                       PIC X(01).                  01040099
           05  WS-SKU-NBR                   PIC 9(08).                  01050099
           05  FILLER                       PIC X(07).                  01060099
                                                                        01070099
      ******************************************************************01080099
      *    PC-PROGRAM CONSTANTS                                        *01090099
      ******************************************************************01100099
       01  PC-PROGRAM-CONSTANTS.                                        01110099
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'FSKUP901'.01120099
           05  PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACES.    01130099
                                                                        01140099
                                                                        01150099
      ******************************************************************01160099
      *       PROGRAM VARIABLES                                        *01170099
      ******************************************************************01180099
       01  PA-PROGRAM-ACCUMULATORS.                                     01190099
           05  PA-LOC-NBR-COUNT             PIC 9(10)  VALUE ZERO.      01200099
           05  PA-LOC-SKU-COUNT             PIC 9(10)  VALUE ZERO.      01210099
           05  PA-LOC-NBR-COMMITS           PIC 9(10)  VALUE ZERO.      01220099
           05  PA-LOC-SKU-COMMITS           PIC 9(10)  VALUE ZERO.      01230099
           05  PA-LOC-COUNT-CHK             PIC S9(08) VALUE ZERO       01240099
                                                       COMP.            01250099
           05  PA-LOC-COUNT-TEMP            PIC 9(08) VALUE ZERO.       01260099
           05  PA-LOC-SKU-CNT-CHK           PIC 9(08) VALUE ZERO.       01270099
           05  PA-COMMITS                   PIC 9(10) VALUE ZERO.       01280099
           05  PA-ITEM-TEMP                 PIC 9(02) VALUE ZERO.       01290099
                                                                        01300099
       01  PV-ITEM-RANGE.                                               01310099
FIX113     05  PV-ITEM-RNGE-INDEX               OCCURS 10 TIMES         01320099
                                                INDEXED BY PV-INDEX.    01330099
               10  PV-ITM-STRT-RNGE             PIC 9(15).              01340099
               10  FILLER                       PIC X(01).              01350099
               10  PV-ITM-END-RNGE              PIC 9(15).              01360099
               10  FILLER                       PIC X(49).              01370099
                                                                        01380099
           05  PV-ITEM-RANGE-TEMP.                                      01390099
               10  PV-ITM-RNG-TEMP-STRT         PIC S9(15)V COMP-3.     01400099
               10  FILLER                       PIC X(01).              01410099
               10  PV-ITM-RNG-TEMP-END          PIC S9(15)V COMP-3.     01420099
               10  FILLER                       PIC X(63).              01430099
                                                                        01440099
                                                                        01450099
      ******************************************************************01460099
      *    PS-PROGRAM SWITCHES                                         *01470099
      ******************************************************************01480099
       01  PS-PROGRAM-SWITCHES.                                         01490099
           05  PS-END-OF-LOC-NBR            PIC  X(01)   VALUE 'N'.     01500099
               88  PS-EOF-LOC-NBR-YES                    VALUE 'Y'.     01510099
               88  PS-EOF-LOC-NBR-NO                     VALUE 'N'.     01520099
           05  PS-END-OF-LOC-SKU            PIC  X(01)   VALUE 'N'.     01530099
               88  PS-EOF-LOC-SKU-YES                    VALUE 'Y'.     01540099
               88  PS-EOF-LOC-SKU-NO                     VALUE 'N'.     01550099
           05  PS-ITM-RNGE                  PIC  X(01)   VALUE 'N'.     01560099
               88  PS-EOF-ITM-RNGE-YES                   VALUE 'Y'.     01570099
               88  PS-EOF-ITM-RNGE-NO                    VALUE 'N'.     01580099
           05  PS-MAX-CNT-CHK               PIC  X(01)   VALUE 'N'.     01581099
               88  PS-MAX-CNT-YES                        VALUE 'Y'.     01582099
               88  PS-MAX-CNT-NO                         VALUE 'N'.     01583099
           05  PS-MAX-SKU-CNT               PIC  X(01)   VALUE 'N'.     01584099
               88  PS-MAX-SKU-CNT-YES                    VALUE 'Y'.     01585099
               88  PS-MAX-SKU-CNT-NO                     VALUE 'N'.     01586099
                                                                        01590099
                                                                        01600099
      ******************************************************************01610099
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *01620099
      ******************************************************************01630099
           COPY DPWS004.                                                01640099
                                                                        01650099
      ******************************************************************01660099
      *    SKU STORE                                                   *01670099
      ******************************************************************01680099
           EXEC SQL                                                     01690099
               INCLUDE TSKST                                            01700099
           END-EXEC.                                                    01710099
                                                                        01720099
      ******************************************************************01730099
      *    TSKXREF  GET SKU FROM ITEM NMB                              *01740099
      ******************************************************************01750099
           EXEC SQL                                                     01760099
               INCLUDE TSKXREF                                          01770099
           END-EXEC.                                                    01780099
                                                                        01790099
           EXEC SQL                                                     01800099
               INCLUDE SQLCA                                            01810099
           END-EXEC.                                                    01820099
      ******************************************************************01830099
       PROCEDURE DIVISION.                                              01840099
      ******************************************************************01850099
                                                                        01860099
       A100-MAINLINE.                                                   01870099
           PERFORM B100-INITIALIZE.                                     01880099
                                                                        01890099
           PERFORM C400-READ-ITM-RNG                                    01900099
           VARYING PV-INDEX                                             01910099
              FROM 1 BY 1                                               01920099
             UNTIL PS-EOF-ITM-RNGE-YES.                                 01930099
                                                                        01940099
           IF PS-EOF-LOC-SKU-NO                                         01950099
              DISPLAY '***********************************'             01960099
              DISPLAY ' START OF SKU LOC UPDATE.'                       01970099
              DISPLAY '***********************************'             01980099
                                                                        01990099
              PERFORM B300-PROCESS-LOC-SKU                              02000099
                UNTIL PS-EOF-LOC-SKU-YES                                02010099
           END-IF.                                                      02020099
                                                                        02030099
           COMPUTE PA-LOC-COUNT-TEMP = PA-LOC-COUNT-TEMP +              02040099
                                       PA-LOC-SKU-COUNT.                02050099
                                                                        02060099
                                                                        02070099
           IF PS-EOF-LOC-NBR-NO                                         02080099
              DISPLAY '***********************************'             02090099
              DISPLAY ' START OF LOC UPDATE.'                           02100099
              DISPLAY '***********************************'             02110099
                                                                        02120099
              PERFORM B200-PROCESS-LOC                                  02130099
                UNTIL PS-EOF-LOC-NBR-YES                                02140099
           END-IF.                                                      02150099
                                                                        02160099
           PERFORM B400-DISPLAY-COUNT.                                  02170099
                                                                        02180099
           GOBACK.                                                      02190099
                                                                        02200099
       B100-INITIALIZE.                                                 02210099
           OPEN INPUT  LOC-NBR-FILE                                     02220099
                INPUT  LOC-SKU-FILE                                     02230099
                INPUT  LOC-ITEM-RANGE-FILE.                             02240099
           OPEN OUTPUT LOC-NBR-FILE-OP                                  02250099
                       LOC-SKU-FILE-OP.                                 02251099
           INITIALIZE  PA-PROGRAM-ACCUMULATORS.                         02260099
                                                                        02270099
           PERFORM C200-READ-LOC-NBR.                                   02280099
           IF PS-EOF-LOC-NBR-YES                                        02290099
               DISPLAY ' '                                              02300099
               DISPLAY '***********************************'            02310099
               DISPLAY ' NO LOC NBR TO PROCESS.'                        02320099
               DISPLAY '***********************************'            02330099
               DISPLAY ' '                                              02340099
           END-IF.                                                      02350099
                                                                        02360099
           PERFORM C300-READ-LOC-SKU.                                   02370099
           IF PS-EOF-LOC-SKU-YES                                        02380099
               DISPLAY ' '                                              02390099
               DISPLAY '***********************************'            02400099
               DISPLAY ' NO LOC SKU TO PROCESS.'                        02410099
               DISPLAY '***********************************'            02420099
               DISPLAY ' '                                              02430099
           END-IF.                                                      02440099
                                                                        02450099
      ******************************************************************02460099
      * TO READ DATA FROM FILE WITH INPUT AS LOCATION NUMBER            02470099
      ******************************************************************02480099
       C200-READ-LOC-NBR.                                               02490099
           ACCEPT WS-TEMP-VARIABLES.                                    02500099
           MOVE 'C200-READ-LOC-NBR' TO PV-PARAGRAPH-NAME.               02510099
           INITIALIZE WS-LOC-NBR-FILE.                                  02520099
           READ LOC-NBR-FILE                                            02530099
               INTO WS-LOC-NBR-FILE                                     02540099
                   AT END                                               02550099
                       SET PS-EOF-LOC-NBR-YES TO TRUE                   02560099
                   NOT AT END                                           02570099
                       ADD 1       TO PA-LOC-NBR-COUNT                  02580099
                   MOVE WS-LOC-NBR          TO WS-LOC-TEMP              02590099
           END-READ.                                                    02600099
                                                                        02610099
      ******************************************************************02620099
      * TO READ DATA FROM FILE WITH INPUT AS LOCATION NUMBER AND SKU    02630099
      ******************************************************************02640099
                                                                        02650099
       C300-READ-LOC-SKU.                                               02660099
           MOVE 'C300-READ-LOC-SKU'       TO PV-PARAGRAPH-NAME.         02670099
           INITIALIZE WS-LOC-SKU.                                       02680099
           READ LOC-SKU-FILE                                            02690099
               INTO WS-LOC-SKU                                          02700099
                   AT END                                               02710099
                       SET PS-EOF-LOC-SKU-YES TO TRUE                   02720099
                     PERFORM B304-COMMIT-PROCESS                        02730099
                   NOT AT END                                           02740099
                       ADD  1             TO PA-LOC-SKU-COUNT           02750099
                       MOVE WS-LOC-NBR1   TO TSKST-LOC-NBR              02760099
                       MOVE WS-SKU-NBR    TO SKXREF-SKU                 02770099
           END-READ.                                                    02780099
                                                                        02790099
      ******************************************************************02800099
      * TO READ TIMESTAMP FILE                                          02810099
      ******************************************************************02820099
       C400-READ-ITM-RNG.                                               02830099
           MOVE 'C400-READ-ITM-RNG'      TO PV-PARAGRAPH-NAME.          02840099
           READ LOC-ITEM-RANGE-FILE                                     02850099
               INTO PV-ITEM-RANGE-TEMP                                  02860099
                   AT END                                               02870099
                       SET PS-EOF-ITM-RNGE-YES TO TRUE                  02880099
                   NOT AT END                                           02890099
                       MOVE PV-ITEM-RANGE-TEMP TO                       02900099
                                       PV-ITEM-RNGE-INDEX(PV-INDEX)     02910099
                       ADD 1 TO PA-ITEM-TEMP                            02920099
                    INITIALIZE PV-ITEM-RANGE-TEMP                       02930099
           END-READ.                                                    02940099
                                                                        02950099
       B200-PROCESS-LOC.                                                02960099
            MOVE  WS-TIME-STAMP TO TSKST-LAST-UPD-TMST                  02970099
              PERFORM B205-MAX-COUNT-CHK.                               02980099
              IF PA-LOC-COUNT-TEMP  < WS-MAX-COUNT                      02990099
                    PERFORM B201-UPD-LOC-ITMRNG1                        03000099
                    VARYING PV-INDEX                                    03010099
                       FROM 1 BY 1                                      03020099
                      UNTIL PV-INDEX  > PA-ITEM-TEMP                    03030099
                    PERFORM C200-READ-LOC-NBR                           03040099
              ELSE                                                      03050099
                 IF PS-MAX-CNT-NO                                       03051099
                    DISPLAY WS-MAX-COUNT ' MAX LIMIT REACHED'           03060099
                    SET PS-MAX-CNT-YES TO TRUE                          03061099
                 END-IF                                                 03062099
                 WRITE LOC-NBR-OP-RECORD FROM WS-LOC-NBR-FILE           03070099
                 PERFORM C200-READ-LOC-NBR                              03080099
              END-IF.                                                   03090099
                                                                        03100099
      ******************************************************************03110099
      * TO UPDATE TSKST TIMESTAMP GIVEN THE LOCATION NUMBER             03120099
      ******************************************************************03130099
       B201-UPD-LOC-ITMRNG1.                                            03140099
            MOVE PV-ITM-STRT-RNGE(PV-INDEX) TO                          03150099
                                  PV-ITM-RNG-TEMP-STRT                  03160099
            MOVE PV-ITM-END-RNGE(PV-INDEX) TO                           03170099
                                  PV-ITM-RNG-TEMP-END                   03180099
            MOVE WS-LOC-TEMP   TO TSKST-LOC-NBR                         03190099
            EXEC SQL                                                    03200099
              UPDATE TSKST                                              03210099
              SET LAST_UPD_TMST = CURRENT TIMESTAMP                     03220099
              WHERE LOC_NBR = :TSKST-LOC-NBR                            03230099
              AND LAST_UPD_TMST  > :TSKST-LAST-UPD-TMST                 03240099
              AND ITEM_NMBR  BETWEEN :PV-ITM-RNG-TEMP-STRT              03250099
                                 AND :PV-ITM-RNG-TEMP-END               03260099
            END-EXEC                                                    03270099
                                                                        03280099
           EVALUATE TRUE                                                03290099
             WHEN SQLCODE = 0                                           03300099
FIX113            PERFORM B204-COMMIT-PROCESS                           03320099
               INITIALIZE PV-ITEM-RANGE-TEMP                            03340099
             WHEN SQLCODE = 100                                         03350099
               DISPLAY 'ROW NOT FOUND FOR LOC NUMBER:' TSKST-LOC-NBR    03360099
               DISPLAY 'AND ITEM NUMBERS BETWEEN ' PV-ITM-RNG-TEMP-STRT 03370099
                       ' AND ' PV-ITM-RNG-TEMP-END                      03380099
               INITIALIZE PV-ITEM-RANGE-TEMP                            03390099
             WHEN OTHER                                                 03400099
               DISPLAY 'UPDATE UNSUCCESSFUL FOR LOCATION:' TSKST-LOC-NBR03410099
               DISPLAY 'AND ITEM NUMBERS BETWEEN ' PV-ITM-RNG-TEMP-STRT 03420099
                       'AND ' PV-ITM-RNG-TEMP-END                       03430099
               MOVE 'B201-UPD-LOC-ITMRNG1' TO PV-PARAGRAPH-NAME         03440099
               PERFORM Z910-ABEND                                       03450099
           END-EVALUATE.                                                03460099
                                                                        03470099
       B204-COMMIT-PROCESS.                                             03480099
           EXEC SQL                                                     03490099
               COMMIT                                                   03500099
           END-EXEC                                                     03510099
067400                                                                  03520099
           EVALUATE TRUE                                                03530099
067500       WHEN  SQLCODE = ZEROES                                     03540099
               ADD +1 TO PA-LOC-NBR-COMMITS                             03550099
FIX113         DISPLAY 'CURRENT ITEM RANGE IS '                         03551099
FIX113                 PV-ITEM-RNGE-INDEX(PV-INDEX)                     03552099
FIX113         DISPLAY 'COMMIT SUCCESSFUL FOR ABOVE RANGE AND LOC: '    03560099
FIX113                 TSKST-LOC-NBR                                    03561099
067700       WHEN OTHER                                                 03570099
067800         DISPLAY 'PGM ABEND DURING A COMMIT'                      03580099
067900         DISPLAY 'B204-COMMIT-PROCESS'                            03590099
               MOVE 'B204-COMMIT-PROCESS' TO PV-PARAGRAPH-NAME          03600099
068000         PERFORM Z910-ABEND                                       03610099
068100     END-EVALUATE.                                                03620099
068200                                                                  03630099
       B205-MAX-COUNT-CHK.                                              03640099
           MOVE WS-LOC-TEMP   TO TSKST-LOC-NBR                          03650099
           EXEC SQL                                                     03660099
               SELECT COUNT(*)                                          03670099
                      INTO :PA-LOC-COUNT-CHK                            03680099
                      FROM TSKST                                        03690099
                     WHERE LOC_NBR = :TSKST-LOC-NBR                     03700099
                       AND LAST_UPD_TMST  > :TSKST-LAST-UPD-TMST        03710099
                     WITH UR                                            03720099
           END-EXEC                                                     03730099
067400                                                                  03740099
           EVALUATE TRUE                                                03750099
067500       WHEN  SQLCODE = ZEROES                                     03760099
               COMPUTE  PA-LOC-COUNT-TEMP = PA-LOC-COUNT-CHK +          03770099
                                            PA-LOC-COUNT-TEMP           03780099
FIX113         DISPLAY 'TOTAL COUNTS FOR LOC' TSKST-LOC-NBR  ' IS '     03781099
FIX113                 PA-LOC-COUNT-CHK                                 03782099
               INITIALIZE PA-LOC-COUNT-CHK                              03790099
067700       WHEN OTHER                                                 03800099
067900         DISPLAY 'B205-MAX-COUNT-CHK'                             03810099
               MOVE 'B205-MAX-COUNT-CHK' TO PV-PARAGRAPH-NAME           03820099
068000         PERFORM Z910-ABEND                                       03830099
068100     END-EVALUATE.                                                03840099
068200                                                                  03850099
       B300-PROCESS-LOC-SKU.                                            03860099
             IF PA-LOC-SKU-COUNT  <= WS-MAX-COUNT                       03870099
                PERFORM B301-UPD-LOCSKU-ITM                             03880099
                PERFORM C300-READ-LOC-SKU                               03890099
             ELSE                                                       03900099
                 IF PS-MAX-SKU-CNT-NO                                   03901099
                    DISPLAY WS-MAX-COUNT ' MAX LIMIT REACHED'           03910099
                    SET PS-MAX-SKU-CNT-YES TO TRUE                      03910299
                 END-IF                                                 03910399
                 WRITE LOC-SKU-FILE-OP-REC FROM WS-LOC-SKU              03911099
                 PERFORM C300-READ-LOC-SKU                              03912099
             END-IF.                                                    03930099
                                                                        03940099
      ******************************************************************03950099
      * TO UPDATE TSKST TIMESTAMP GIVEN THE LOCATION NUMBER AND SKU     03960099
      ******************************************************************03970099
       B301-UPD-LOCSKU-ITM.                                             03980099
                                                                        03990099
            EXEC SQL                                                    04000099
              UPDATE TSKST                                              04010099
               SET LAST_UPD_TMST = CURRENT TIMESTAMP                    04020099
               WHERE     LOC_NBR = :TSKST-LOC-NBR                       04030099
               AND ITEM_NMBR IN (SELECT ITM_NBR                         04040099
                           FROM TSKXREF                                 04050099
                           WHERE SKU = :SKXREF-SKU)                     04060099
             END-EXEC                                                   04070099
                                                                        04080099
           EVALUATE TRUE                                                04090099
             WHEN SQLCODE = 0                                           04100099
               ADD 1 TO PA-COMMITS                                      04110099
               IF PA-COMMITS  = WS-CMT-FREQ                             04120099
                     PERFORM B304-COMMIT-PROCESS                        04130099
                  INITIALIZE PA-COMMITS                                 04140099
               END-IF                                                   04150099
             WHEN SQLCODE = 100                                         04160099
               DISPLAY 'ROW NOT FOUND FOR SKU:' SKXREF-SKU              04170099
               DISPLAY 'LOC NBR:'               TSKST-LOC-NBR           04180099
               ADD 1 TO PA-COMMITS                                      04190099
               IF PA-COMMITS  = WS-CMT-FREQ                             04200099
                     PERFORM B304-COMMIT-PROCESS                        04210099
                  INITIALIZE PA-COMMITS                                 04220099
               END-IF                                                   04230099
             WHEN OTHER                                                 04240099
               DISPLAY 'UPDATE FAILURE FOR SKU:' SKXREF-SKU             04250099
               DISPLAY 'LOC NBR:'                TSKST-LOC-NBR          04260099
               MOVE 'B301-UPD-LOCSKU-ITM' TO PV-PARAGRAPH-NAME          04270099
               PERFORM Z910-ABEND                                       04280099
           END-EVALUATE.                                                04290099
                                                                        04300099
       B304-COMMIT-PROCESS.                                             04310099
           EXEC SQL                                                     04320099
               COMMIT                                                   04330099
           END-EXEC                                                     04340099
067400                                                                  04350099
           EVALUATE TRUE                                                04360099
067500       WHEN SQLCODE = ZEROES                                      04370099
               ADD 1 TO PA-LOC-SKU-COMMITS                              04380099
FIX113         DISPLAY  WS-CMT-FREQ ' ITEM SKU RECORDS COMMITED'        04381099
067700       WHEN OTHER                                                 04390099
067800         DISPLAY 'PGM ABEND DURING A COMMIT'                      04400099
067900         DISPLAY 'B304-COMMIT-PROCESS'                            04410099
               MOVE 'B304-COMMIT-PROCESS' TO PV-PARAGRAPH-NAME          04420099
068000         PERFORM Z910-ABEND                                       04430099
068100     END-EVALUATE.                                                04440099
                                                                        04450099
       B400-DISPLAY-COUNT.                                              04460099
           CLOSE LOC-NBR-FILE                                           04470099
                 LOC-SKU-FILE                                           04480099
                 LOC-ITEM-RANGE-FILE                                    04490099
                 LOC-NBR-FILE-OP                                        04500099
                 LOC-SKU-FILE-OP.                                       04501099
                                                                        04510099
           DISPLAY '***********************************'.               04520099
           DISPLAY 'MESSAGES FOR PROGRAM   '  PC-PROGRAM-NAME.          04530099
           DISPLAY '***********************************'.               04540099
           DISPLAY 'RECS READ IN LOC FILE        :'  PA-LOC-NBR-COUNT.  04550099
           DISPLAY 'RECS READ IN LOC SKU FILE    :'  PA-LOC-SKU-COUNT.  04560099
W39690     DISPLAY 'TOTAL COMMIT IN LOC FILE     :'  PA-LOC-NBR-COMMITS.04570099
W39690     DISPLAY 'TOTAL COMMIT IN LOC SKU FILE :'  PA-LOC-SKU-COMMITS.04580099
      ******************************************************************04590099
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *04600099
      ******************************************************************04610099
       Z910-ABEND.                                                      04620099
                                                                        04630099
           DISPLAY '** ABEND           **'.                             04640099
           DISPLAY '** SQLCODE         **'    SQLCODE.                  04650099
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          04660099
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        04670099
           DISPLAY 'RECS READ IN LOC FILE        :'  PA-LOC-NBR-COUNT.  04680099
           DISPLAY 'RECS READ IN LOC SKU FILE    :'  PA-LOC-SKU-COUNT.  04690099
                                                                        04700099
00463 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         04710099
00464 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        04720099
00465                                                                   04730099
00466      COPY DPPD004.                                                04740099
                                                                        04750099
           CALL 'ILBOABN0' USING ABEND-CODE.                            04760099
