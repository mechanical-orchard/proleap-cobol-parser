000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.     APKUP175.                                                
000300 AUTHOR.         COGNIZANT.                                               
000400 DATE-WRITTEN.   MARCH 2019.                                              
000500 DATE-COMPILED.                                                           
000600*****************************************************************         
000700*** CLEAN UP AP MEMORANDUM TABLE                              ***         
000800***                                                           ***         
000900*** COBOL 2 WITH SQL                                          ***         
001000***                                                           ***         
001100*** PURPOSE IS TO DELETE RECORDS FROM THE AP MEMORANDUM       ***         
001200*** TABLE THAT ARE MORE THAN 3 YEARS OLD.                     ***         
001300***                                                           ***         
001400***     NOTE ON PARAGRAPH NAMES-                              ***         
001500***     ALL I/O PARAGRAPHS START WITH:                        ***         
001600***        R  FOR  READS / FETCHES                            ***         
001700***        U  FOR  DELETES, UPDATES                           ***         
001800***        W  FOR  WRITES                                     ***         
001900***        Y  FOR  CURSOR OPENS/CLOSES.                       ***         
002000***    COMMIT AND ABEND PARAGRAPHS BEGIN WITH Z.              ***         
002100***                                                           ***         
002200*** INPUT:                                                    ***         
002300***                                                           ***         
002400***    1. AP MEMORANDUM TABLE          (APT_AP_MEMO)          ***         
002500***                                                           ***         
002600*** INPUT/OUTPUT:                                             ***         
002700***                                                           ***         
002800***    NO INPUT AND OUTPUT FILES                             ***          
003000***                                                           ***         
      *---------------------------------------------------------------*         
      * CHANGE LOG:                                                   *         
      *   DATE  03/13/2019                                            *         
      *       CHANGE MADE: NEW PROGRAM                                *         
      *       MADE BY: COGNIZANT               WR/IR #: CHG0220043    *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * CHANGE LOG:                                                   *         
      *   DATE  08/29/2023                                            *         
      *       CHANGE MADE: CHANGE WHERE COMMIT CHECKPOINT IS TAKEN -  *         
      *                    NEED TO COMMIT MORE OFTEN.                 *         
      *       CHANGE TAG:  STT-3467 JIRA STORY CHANGE                 *         
      *       MADE BY: SARVESWARAN KANNAN      WR/IR #: CHG0292858    *         
      *                                                               *         
      *---------------------------------------------------------------*         
004000   EJECT                                                                  
004100 ENVIRONMENT DIVISION.                                                    
004200 CONFIGURATION SECTION.                                                   
004300 SOURCE-COMPUTER.     IBM-3090.                                           
004400 OBJECT-COMPUTER.     IBM-3090.                                           
004500                                                                          
004600 INPUT-OUTPUT SECTION.                                                    
004700 FILE-CONTROL.                                                            
004900                                                                          
005000   EJECT                                                                  
005100 DATA DIVISION.                                                           
005200 FILE SECTION.                                                            
005300                                                                          
006300   EJECT                                                                  
006400 WORKING-STORAGE SECTION.                                                 
006500                                                                          
006600 01  PV-PROGRAM-VARIABLES.                                                
006700     05  FILLER                         PIC  X(31)  VALUE                 
006800         '** BEGINNING OF APKUP175 W/S **'.                               
007500                                                                          
007600 01  PC-PROGRAM-CONSTANTS.                                                
007700     05  PC-PROGRAM-NAME           PIC  X(08)  VALUE 'APKUP175'.          
007800     05  PC-IN-PROCESS-CODE        PIC  X(01)  VALUE '9'.                 
007900     05  PC-RUN-COMPLETE-CODE      PIC  X(01)  VALUE '0'.                 
008000                                                                          
008100 01  PROGRAM-SWITCHES.                                                    
008200                                                                          
008300     05  PS-COMMITS-TAKEN-SW        PIC X(01) VALUE 'N'.                  
008400         88  COMMITS-TAKEN                    VALUE 'Y'.                  
008500         88  NO-COMMITS-TAKEN                 VALUE 'N'.                  
008600     05  PS-EOF-AP-MEMO-CSR-SW     PIC X(01) VALUE 'N'.                   
008700         88  EOF-AP-MEMO-CSR                  VALUE 'Y'.                  
008800   EJECT                                                                  
008900 01  RECORD-COUNTS.                                                       
009000     05  MEMO-FETCH-COUNT-DISP     PIC Z9(09)    VALUE ZEROS.             
009200     05  MEMO-DELETED-COUNT-DISP   PIC Z9(09)    VALUE ZEROS.             
009300     05  MEMO-FETCH-COUNTER        PIC S9(09)    VALUE ZEROS.             
009500     05  MEMO-DELETE-COUNTER       PIC S9(09)    VALUE ZEROS.             
009600**   COUNTS SINCE THE LAST COMMIT                                         
009700 01  RECORD-COUNTS-PER-COMMIT.                                            
009800     05  MEMO-FETCH-COUNTER-C      PIC S9(09)    VALUE ZEROS.             
010000     05  MEMO-DELETE-COUNTER-C     PIC S9(09)    VALUE ZEROS.             
010100                                                                          
010200 01  WS-SQLCODE                      PIC S9(9) COMP-4 VALUE ZERO.         
010300 01  WS-SQLWARN0                     PIC X.                               
010600                                                                          
012600 01  CR-CHECKPOINT-RESTART-AREA.                                          
012700     05  CR-CKPT-STAT-CODE        PIC  X(01)   VALUE SPACE.               
012800         88  NORMAL-RUN                        VALUE '0'.                 
012900         88  RESTART-RUN                       VALUE '9'.                 
013000     05  CR-CKPT-RESTART-INFO.                                            
013100         10  CR-VENDOR-ID         PIC  9(09)   VALUE ZEROS.               
013200         10  CR-FETCH-COUNTER     PIC  9(09)   VALUE ZEROS.               
013300         10  CR-DELETE-COUNTER    PIC  9(09)   VALUE ZEROS.               
013500     05  CKPT-RESTART-TMST.                                               
013600         10  CR-NEXT-TMST         PIC  X(26)   VALUE SPACES.              
013700         10  CR-CURR-TMST         PIC  X(26)   VALUE SPACES.              
013800                                                                          
013900   EJECT                                                                  
014000*---------------------------------------------------------------          
014100*  COMMUNICATIONS AREA2 FOR DB2                                           
014200*---------------------------------------------------------------          
014300     COPY SQLCA2.                                                         
014400                                                                          
014500     EXEC SQL                                                             
014600         INCLUDE SQLCA                                                    
014700     END-EXEC.                                                            
014800   EJECT                                                                  
      *----------------------------------------------------------------*        
      *    AP NEW MEMORANDUM TABLE - APT_AP_MEMO                                
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE APT00001                                                
           END-EXEC.                                                            
016300*---------------------------------------------------------------          
016400*  CHECKPOINT/RESTART CONTROL TABLE                                       
016500*---------------------------------------------------------------          
016600     EXEC SQL                                                             
016700        INCLUDE TCKRSTCN                                                  
016800     END-EXEC.                                                            
016900   EJECT                                                                  
017000*---------------------------------------------------------------          
017100*  CHECKPOINT/RESTART DATA TABLE                                          
017200*---------------------------------------------------------------          
017300     EXEC SQL                                                             
017400        INCLUDE TCKRSTIN                                                  
017500     END-EXEC.                                                            
017600   EJECT                                                                  
017700*-----------------------------------------------------------*             
017800* AP MEMO CURSOR                                            *             
017900* THIS CURSOR IS USED TO CHECK THE AP MEMORANDUM TABLE      *             
018000* FOR ROWS WHERE PERIOD END DATE IS < CURRENTDATE -180 DAYS *             
018200*-----------------------------------------------------------*             
018300     EXEC SQL                                                             
018400        DECLARE AP_MEMO_CSR CURSOR WITH HOLD FOR                          
018500           SELECT DISTINCT                                                
018600                    VENDOR_ID                                             
018700                   ,INVOICE_ID                                            
018800                   ,KC_PO_NBR                                             
018900                   ,ORIGIN                                                
019000                   ,KC_CRE_DTE                                            
019700           FROM APT_AP_MEMO                                               
019800           WHERE KC_AP_MEMO_TMST < CURRENT TIMESTAMP - 3 YEARS            
           END-EXEC.                                                            
020100   EJECT                                                                  
020200*----------------------------------------------------------------         
020300*  ABEND DATA AREAS                                                       
020400*----------------------------------------------------------               
020500 01  ABEND-CODE                  PIC S9(04)  VALUE ZEROS                  
020600                                             COMP SYNC.                   
020700     88  AC-DB2-ERROR                        VALUE +4013.                 
020800     88  AC-DATE-ERROR                       VALUE +4019.                 
020900                                                                          
021000 01  ABEND-AREAS.                                                         
021100     05  AA-ABEND-LIT            PIC  X(40)  VALUE                        
021200          '*****       ABEND'.                                            
021300     05  AA-PROGRAM-LIT          PIC  X(40)  VALUE                        
021400          '*****   PROGRAM: APKUP175'.                                    
021500     05  AA-PARAGRAPH-LIT.                                                
021600         10  FILLER              PIC  X(17)  VALUE                        
021700          '***** PARAGRAPH: '.                                            
021800         10  AA-PARAGRAPH-NAME   PIC  X(35)  VALUE SPACES.                
021900     05  AA-MESSAGE-LINE.                                                 
022000         10  FILLER              PIC  X(06)  VALUE '*****'.               
022100         10  AA-MESSAGE          PIC  X(127) VALUE SPACES.                
022200     05  AA-DB2-ERROR-LIT        PIC  X(40)  VALUE                        
022300           '*****    DB2 ERROR'.                                          
022400     05  AA-DB2-OPERATION-LIT.                                            
022500         10  FILLER              PIC  X(17)  VALUE                        
022600           '***** OPERATION: '.                                           
022700         10  AA-DB2-OPERATION.                                            
022800             15  AA-DB2-OP-1     PIC  X(25)  VALUE SPACES.                
022900             15  AA-DB2-OP-2     PIC  X(25)  VALUE SPACES.                
023000     05  AA-DB2-TABLE-1          PIC  X(08)  VALUE SPACES.                
023100     05  AA-DB2-TABLE-2          PIC  X(08)  VALUE SPACES.                
023200     05  AA-DB2-TABLE-3          PIC  X(08)  VALUE SPACES.                
023300                                                                          
023400     COPY DPWS004.                                                        
023500                                                                          
023600 01  FILLER                      PIC  X(25)  VALUE                        
023700     '** END OF APKUP175 W/S **'.                                         
023800   EJECT                                                                  
023900 PROCEDURE DIVISION.                                                      
024000 A100-MAIN.                                                               
024100     PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM R300-FETCH-AP-MEMO-CSR.                                      
024200                                                                          
024300     PERFORM B200-PROCESS-AP-MEMORANDUM                                   
                   UNTIL EOF-AP-MEMO-CSR.                                       
024500                                                                          
024600     PERFORM B300-END-PROGRAM.                                            
024700                                                                          
024800     GOBACK.                                                              
024900   EJECT                                                                  
025000*--------------------------------------------------------------*          
025100*    INITIALIZATION PROCESSING                                 *          
025400*    - SETUP FOR CHECKPOINT RESTART                            *          
025500*    - INITIALIZE COUNTERS                                     *          
025600*    - OPEN AP MEMORANDUM CURSOR                              *           
025700*--------------------------------------------------------------*          
025800 B100-INITIALIZE.                                                         
025900                                                                          
026100                                                                          
026200     INITIALIZE DCLAPT00001                                               
026400                DCLTCKRSTCNTL                                             
026500                DCLTCKRSTINF.                                             
026600                                                                          
026700     MOVE ZEROS TO MEMO-FETCH-COUNTER-C                                   
026900                   MEMO-DELETE-COUNTER-C.                                 
027000                                                                          
027200                                                                          
027500     PERFORM R800-SELECT-CHECKPOINT-CNTL.                                 
027600     IF RESTART-RUN                                                       
027700        PERFORM R900-SELECT-CHECKPOINT-INFO                               
027800        MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                                
027900                              TO CR-CKPT-RESTART-INFO                     
028000                                                                          
028100        DISPLAY '***********************************************'         
028200        DISPLAY '** APKUP175 CHECKPOINT RESTART               **'         
028300        DISPLAY '** VENDOR ID ON RESTART = '  CR-VENDOR-ID                
028400        DISPLAY '***********************************************'         
028500                                                                          
028600        MOVE ZERO                  TO MEMO-FETCH-COUNTER-C                
028800                                      MEMO-DELETE-COUNTER-C               
028900                                                                          
029000        MOVE CR-FETCH-COUNTER      TO MEMO-FETCH-COUNTER                  
029200        MOVE CR-DELETE-COUNTER     TO MEMO-DELETE-COUNTER.                
029300                                                                          
029400     PERFORM Y100-OPEN-AP-MEMO-CSR.                                       
029500                                                                          
029600   EJECT                                                                  
029700*--------------------------------------------------------------*          
029800* FETCH AP MEMORANDUM RECORDS WHERE PERIOD ENDING DATE IS <    *          
029900* CURRENT DATE - 180DAYS, THEN DELETE THIS ROW FROM THE        *          
030000*    AP MEMORANDUM TABLE.                                      *          
030100*--------------------------------------------------------------*          
030200 B200-PROCESS-AP-MEMORANDUM.                                              
030300                                                                          
030500                                                                          
030800     PERFORM U100-DELETE-AP-MEMO-ROW.                                     
                                                                                
031000     PERFORM Y300-CHECKPOINT.                                             
031100                                                                          
030400     PERFORM R300-FETCH-AP-MEMO-CSR.                                      
                                                                                
031200   EJECT                                                                  
031300*---------------------------------------------------------------*         
031400*ENDING PROCESSING:DISPLAYS COUNT OF VENDORID AND CLOSES CURSOR *         
031500*---------------------------------------------------------------*         
031600 B300-END-PROGRAM.                                                        
031700                                                                          
           PERFORM Y200-CLOSE-AP-MEMO-CSR.                                      
031900                                                                          
032000     MOVE PC-RUN-COMPLETE-CODE TO CR-CKPT-STAT-CODE.                      
032100     MOVE ZERO                 TO CR-CKPT-RESTART-INFO.                   
032200     PERFORM U300-UPDATE-CHECKPOINT-CNTL.                                 
032300     PERFORM Z400-CHECKPOINT-COMMIT.                                      
032400                                                                          
032600     MOVE MEMO-DELETE-COUNTER    TO MEMO-DELETED-COUNT-DISP.              
032700     MOVE MEMO-FETCH-COUNTER     TO MEMO-FETCH-COUNT-DISP.                
032800                                                                          
033000     DISPLAY '*****   RECORD COUNTS FROM APKUP175    ******'.             
033400     DISPLAY '                                             '.             
033500     DISPLAY 'AP MEMORANDUM FETCH  = ' MEMO-FETCH-COUNT-DISP.             
033600     DISPLAY '                                             '.             
033900     DISPLAY 'AP MEMORANDUM DELETED = ' MEMO-DELETED-COUNT-DISP.          
034000     DISPLAY '---------------------------------------------'.             
034100                                                                          
034300                                                                          
036000 R300-FETCH-AP-MEMO-CSR.                                                  
036100                                                                          
036200     EXEC SQL                                                             
036300        FETCH AP_MEMO_CSR                                                 
036400         INTO :APT00001-VENDOR-ID                                         
036500              ,:APT00001-INVOICE-ID                                       
036600              ,:APT00001-KC-PO-NBR                                        
036700              ,:APT00001-ORIGIN                                           
036800              ,:APT00001-KC-CRE-DTE                                       
037600     END-EXEC.                                                            
037700                                                                          
037800     EVALUATE TRUE                                                        
037900      WHEN SQLCODE = ZERO                                                 
038000        ADD 1 TO MEMO-FETCH-COUNTER-C                                     
038100      WHEN SQLCODE = +100                                                 
                 SET EOF-AP-MEMO-CSR TO TRUE                                    
038300      WHEN SQLWARN0 NOT = SPACES                                          
038400      WHEN SQLCODE  NOT = ZERO                                            
038500        MOVE 'R300-FETCH-AP-MEMO-CSR'                                     
038600                           TO AA-PARAGRAPH-NAME                           
038700        MOVE                                                              
038800             'UNSUCCESSFUL FETCH WITH AP-MEMO-CSR'                        
038900                           TO AA-DB2-OPERATION                            
039000        MOVE 'APT_AP_MEMO' TO AA-DB2-TABLE-1                              
039100        MOVE SPACES        TO AA-DB2-TABLE-2                              
039200                              AA-DB2-TABLE-3                              
039300        PERFORM Z998-DB2-ABEND                                            
039400     END-EVALUATE.                                                        
039500                                                                          
039600   EJECT                                                                  
042200*---------------------------------------------------------------          
042300*    SETS THE TIME THE NEXT CHECKPOINT NEEDS TO BE TAKEN.                 
042400*---------------------------------------------------------------          
042500 R700-SET-NEXT-CKPT-TMST.                                                 
042600                                                                          
042700     EXEC SQL                                                             
      *STT-3467 JIRA STORY CHANGE START                                         
042800*       SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)              
042800        SELECT (CURRENT TIMESTAMP + .5              SECONDS)              
      *STT-3467 JIRA STORY CHANGE END                                           
042900          INTO :CR-NEXT-TMST                                              
043000          FROM TCKRSTCNTL                                                 
043100         WHERE PLAN_NAME = :PC-PROGRAM-NAME                               
043200     END-EXEC.                                                            
043300                                                                          
043400     EVALUATE TRUE                                                        
043500                                                                          
043600        WHEN SQLCODE = ZERO                                               
043700           CONTINUE                                                       
043800                                                                          
043900        WHEN SQLWARN0 NOT = SPACES                                        
044000        WHEN SQLCODE  NOT = ZERO                                          
044100           MOVE 'R700-SET-NEXT-CKPT-TMST'                                 
044200                              TO AA-PARAGRAPH-NAME                        
044300           MOVE 'UNSUCCESSFUL SELECT NEXT TMST'                           
044400                              TO AA-DB2-OPERATION                         
044500           MOVE 'TCKRSTCNTL'       TO AA-DB2-TABLE-1                      
044600           MOVE SPACES             TO AA-DB2-TABLE-2                      
044700                                      AA-DB2-TABLE-3                      
044800           PERFORM Z998-DB2-ABEND                                         
044900     END-EVALUATE.                                                        
045000   EJECT                                                                  
045100                                                                          
045200*---------------------------------------------------------------          
045300*    GETS THE RESTART STATUS CODE FROM THE CHECKPOINT/RESTART             
045400*    CONTROL TABLE.                                                       
045500*---------------------------------------------------------------          
045600 R800-SELECT-CHECKPOINT-CNTL.                                             
045700                                                                          
045800     EXEC SQL                                                             
045900       SELECT CKPT_STAT_CODE                                              
046000         INTO :CR-CKPT-STAT-CODE                                          
046100         FROM TCKRSTCNTL                                                  
046200         WHERE PLAN_NAME = :PC-PROGRAM-NAME                               
046300     END-EXEC.                                                            
046400                                                                          
046500     EVALUATE TRUE                                                        
046600         WHEN SQLCODE = ZERO                                              
046700            CONTINUE                                                      
046800                                                                          
046900         WHEN SQLWARN0 NOT = SPACES                                       
047000         WHEN SQLCODE  NOT = ZERO                                         
047100         MOVE 'R800-SELECT-CHECKPOINT-CNTL'                               
047200                              TO AA-PARAGRAPH-NAME                        
047300         MOVE 'UNSUCCESSFUL SELECT TCKRSTCNTL'                            
047400                              TO AA-DB2-OPERATION                         
047500         MOVE 'TCKRSTCNTL'       TO AA-DB2-TABLE-1                        
047600         MOVE SPACES             TO AA-DB2-TABLE-2                        
047700                                    AA-DB2-TABLE-3                        
047800         PERFORM Z998-DB2-ABEND                                           
047900     END-EVALUATE.                                                        
048000   EJECT                                                                  
048100*---------------------------------------------------------------          
048200*   GETS THE WS DESCRIPTION INFO FROM THE CHECKPOINT/RESTART              
048300*   INFORMATION TABLE.                                                    
048400*---------------------------------------------------------------          
048500 R900-SELECT-CHECKPOINT-INFO.                                             
048600                                                                          
048700     EXEC SQL                                                             
048800        SELECT WORK_STRG_DESC                                             
048900          INTO :TCKRSTINF-WORK-STRG-DESC                                  
049000          FROM TCKRSTINF                                                  
049100         WHERE PLAN_NAME = :PC-PROGRAM-NAME                               
049200     END-EXEC.                                                            
049300                                                                          
049400     EVALUATE TRUE                                                        
049500      WHEN SQLCODE = ZERO                                                 
049600         CONTINUE                                                         
049700                                                                          
049800      WHEN SQLWARN0 NOT = SPACES                                          
049900      WHEN SQLCODE  NOT = ZERO                                            
050000         MOVE 'R900-SELECT-CHECKPOINT-INFO'                               
050100                              TO AA-PARAGRAPH-NAME                        
050200         MOVE 'UNSUCCESSFUL SELECT TCKRSTINF'                             
050300                                 TO AA-DB2-OPERATION                      
050400         MOVE 'TCKRSTINF'        TO AA-DB2-TABLE-1                        
050500         MOVE SPACES             TO AA-DB2-TABLE-2                        
050600                                    AA-DB2-TABLE-3                        
050700         PERFORM Z998-DB2-ABEND                                           
050800     END-EVALUATE.                                                        
050900                                                                          
051000  EJECT                                                                   
051100*---------------------------------------------------------------          
051200*   DELETE ROW FROM THE AP MEMO TABLE                                     
051300*---------------------------------------------------------------          
051400 U100-DELETE-AP-MEMO-ROW.                                                 
051500                                                                          
052100     EXEC SQL                                                             
052200        DELETE FROM APT_AP_MEMO                                           
052300            WHERE VENDOR_ID  = :APT00001-VENDOR-ID                        
052400              AND INVOICE_ID = :APT00001-INVOICE-ID                       
052500              AND KC_PO_NBR  = :APT00001-KC-PO-NBR                        
052400              AND ORIGIN     = :APT00001-ORIGIN                           
052500              AND KC_CRE_DTE = :APT00001-KC-CRE-DTE                       
052600     END-EXEC                                                             
052700                                                                          
052800     MOVE SQLWARN0  TO WS-SQLWARN0                                        
052900     MOVE SQLCODE   TO WS-SQLCODE                                         
053000                                                                          
053100     EVALUATE TRUE                                                        
053200       WHEN WS-SQLCODE = ZERO                                             
053300           ADD 1 TO MEMO-DELETE-COUNTER-C                                 
053400       WHEN WS-SQLCODE = -904                                             
053500       WHEN WS-SQLCODE = -913                                             
053600           CONTINUE                                                       
053700       WHEN WS-SQLWARN0 NOT = SPACES                                      
053800       WHEN WS-SQLCODE NOT = ZERO                                         
053900           MOVE 'U100-DELETE-AP-MEMO-ROW'                                 
054000                                         TO AA-PARAGRAPH-NAME             
054100           MOVE 'UNSUCCESSFUL DELETE APMEMO ROW'                          
054200                                         TO AA-DB2-OPERATION              
054300           MOVE 'APT-AP-MEMO'            TO AA-DB2-TABLE-1                
054400           MOVE SPACES                   TO AA-DB2-TABLE-2                
054500                                            AA-DB2-TABLE-3                
054600           PERFORM Z998-DB2-ABEND                                         
                 DISPLAY 'DELETE COUNT =' SQLERRD3                              
054700     END-EVALUATE.                                                        
054900                                                                          
057400*---------------------------------------------------------------          
057500*    UPDATES THE CHECKPOINT RESTART CONTROL TABLE.  THE STATUS            
057600*    CODE IS SET TO '9' (RUN IN PROGRESS) OR '0' (RUN COMPLETE).          
057700*---------------------------------------------------------------          
057800 U300-UPDATE-CHECKPOINT-CNTL.                                             
057900                                                                          
058000     EXEC SQL                                                             
058100        UPDATE TCKRSTCNTL                                                 
058200          SET CKPT_STAT_CODE = :CR-CKPT-STAT-CODE                         
058300          WHERE PLAN_NAME    = :PC-PROGRAM-NAME                           
058400     END-EXEC.                                                            
058500                                                                          
058600     EVALUATE TRUE                                                        
058700        WHEN SQLCODE = ZEROS                                              
058800            CONTINUE                                                      
058900                                                                          
059000        WHEN OTHER                                                        
059100            MOVE 'U300-UPDATE-CHECKPOINT-CNTL'                            
059200                             TO AA-PARAGRAPH-NAME                         
059300            MOVE 'UNSUCCESSFUL UPDATE'                                    
059400                             TO AA-DB2-OPERATION                          
059500            MOVE 'TCKRSTCNTL'   TO AA-DB2-TABLE-1                         
059600            MOVE SPACES         TO AA-DB2-TABLE-2                         
059700                                   AA-DB2-TABLE-3                         
059800            PERFORM Z998-DB2-ABEND                                        
059900     END-EVALUATE.                                                        
060000                                                                          
060100   EJECT                                                                  
060200*---------------------------------------------------------------          
060300*    UPDATES THE CHECKPOINT RESTART INFO TABLE.  THE WORKING              
060400*    STORAGE DESCRIPTION FIELD IS UPDATED WITH THE LAST COMMITED          
060500*    PO STORE NUMBER COMBINATION.                                         
060600*---------------------------------------------------------------          
060700 U400-UPDATE-CHECKPOINT-INFO.                                             
060800                                                                          
060900     EXEC SQL                                                             
061000        UPDATE TCKRSTINF                                                  
061100         SET WORK_STRG_DESC   = :TCKRSTINF-WORK-STRG-DESC                 
061200           WHERE PLAN_NAME      = :PC-PROGRAM-NAME                        
061300     END-EXEC.                                                            
061400                                                                          
061500     EVALUATE TRUE                                                        
061600        WHEN SQLCODE = ZEROS                                              
061700            CONTINUE                                                      
061800                                                                          
061900        WHEN OTHER                                                        
062000            MOVE 'U400-UPDATE-CHECKPOINT-INFO'                            
062100                               TO AA-PARAGRAPH-NAME                       
062200            MOVE 'UNSUCCESSFUL UPDATE'                                    
062300                               TO AA-DB2-OPERATION                        
062400            MOVE 'TCKRSTINF'    TO AA-DB2-TABLE-1                         
062500            MOVE SPACES         TO AA-DB2-TABLE-2                         
062600                                   AA-DB2-TABLE-3                         
062700            PERFORM Z998-DB2-ABEND                                        
062800     END-EVALUATE.                                                        
062900                                                                          
063000   EJECT                                                                  
063900                                                                          
064100*---------------------------------------------------------------          
064200*    OPENS THE AP MEMORANDUM CURSOR.                                      
064300*---------------------------------------------------------------          
064400 Y100-OPEN-AP-MEMO-CSR.                                                   
064500                                                                          
064600     EXEC SQL                                                             
064700        OPEN AP_MEMO_CSR                                                  
064800     END-EXEC.                                                            
064900                                                                          
065000     EVALUATE TRUE                                                        
065100        WHEN SQLCODE = ZERO                                               
065200            CONTINUE                                                      
065300                                                                          
065400        WHEN SQLWARN0 NOT = SPACES                                        
065500        WHEN SQLCODE  NOT = ZERO                                          
065600          MOVE 'Y100-OPEN-AP-MEMO-CSR'                                    
065700                              TO AA-PARAGRAPH-NAME                        
065800          MOVE 'UNSUCCESSFUL OPEN ON AP-MEMO-CSR'                         
065900                              TO AA-DB2-OPERATION                         
066000          MOVE 'APT_AP_MEMO'  TO AA-DB2-TABLE-1                           
066100          MOVE SPACES         TO AA-DB2-TABLE-2                           
066200                                 AA-DB2-TABLE-3                           
066300          PERFORM Z998-DB2-ABEND                                          
066400     END-EVALUATE.                                                        
066500                                                                          
066600   EJECT                                                                  
066700*---------------------------------------------------------------          
066800*    CLOSES THE AP MEMO CURSOR.                                           
066900*---------------------------------------------------------------          
067000 Y200-CLOSE-AP-MEMO-CSR.                                                  
067100                                                                          
067200     EXEC SQL                                                             
067300        CLOSE AP_MEMO_CSR                                                 
067400     END-EXEC.                                                            
067500                                                                          
067600     EVALUATE TRUE                                                        
067700       WHEN SQLCODE = ZERO                                                
067800           CONTINUE                                                       
067900                                                                          
068000       WHEN SQLWARN0 NOT = SPACES                                         
068100       WHEN SQLCODE  NOT = ZERO                                           
068200          MOVE 'Y200-CLOSE-AP-MEMO-CSR'                                   
068300                             TO AA-PARAGRAPH-NAME                         
068400          MOVE 'UNSUCCESSFUL CLOSE ON AP-MEMO-CSR'                        
068500                             TO AA-DB2-OPERATION                          
068600          MOVE 'AP_MEMO_CSR' TO AA-DB2-TABLE-1                            
068700          MOVE SPACES        TO AA-DB2-TABLE-2                            
068800                                AA-DB2-TABLE-3                            
068900          PERFORM Z998-DB2-ABEND                                          
069000     END-EVALUATE.                                                        
069100                                                                          
069200   EJECT                                                                  
069300*---------------------------------------------------------------          
069400*    CHECKPOINT RESTART LOGICAL TO BE INVOKED AT THE END OF               
069500*    A LOGICAL UNIT OF WORK.                                              
069600*---------------------------------------------------------------          
069700 Y300-CHECKPOINT.                                                         
069800                                                                          
070000     IF CR-CURR-TMST > CR-NEXT-TMST                                       
070100         IF NO-COMMITS-TAKEN                                              
070200           MOVE PC-IN-PROCESS-CODE  TO CR-CKPT-STAT-CODE                  
070300           PERFORM U300-UPDATE-CHECKPOINT-CNTL                            
070400           SET COMMITS-TAKEN        TO TRUE                               
070500         END-IF                                                           
070600         PERFORM Z400-CHECKPOINT-COMMIT                                   
070700     END-IF.                                                              
070800                                                                          
070900*---------------------------------------------------------------          
071000*    COMMITS ALL DB2 UPDATES PERFORMED.                                   
071100*---------------------------------------------------------------          
071200 Z100-COMMIT.                                                             
071300                                                                          
071400     EXEC SQL                                                             
071500        COMMIT                                                            
071600     END-EXEC.                                                            
071700                                                                          
071800     EVALUATE TRUE                                                        
071900        WHEN SQLCODE = ZERO                                               
072000           CONTINUE                                                       
072100                                                                          
072200        WHEN SQLWARN0 NOT = SPACES                                        
072300        WHEN SQLCODE  NOT = ZERO                                          
072400           MOVE 'Z100-COMMIT'  TO AA-PARAGRAPH-NAME                       
072500           MOVE 'UNSUCCESSFUL COMMIT'                                     
072600                               TO AA-DB2-OPERATION                        
072700           MOVE SPACES         TO AA-DB2-TABLE-1                          
072800                                  AA-DB2-TABLE-2                          
072900                                  AA-DB2-TABLE-3                          
073000           PERFORM Z998-DB2-ABEND                                         
073100     END-EVALUATE.                                                        
073200                                                                          
073300   EJECT                                                                  
073400                                                                          
073500*---------------------------------------------------------------          
073600*    UPDATE THE CHECKPOINT/RESTART TABLES AND THEN COMMIT                 
073700*---------------------------------------------------------------          
073800 Z400-CHECKPOINT-COMMIT.                                                  
073900                                                                          
074000     MOVE APT00001-VENDOR-ID   TO CR-VENDOR-ID.                           
074100                                                                          
074200     COMPUTE MEMO-FETCH-COUNTER                                           
074300          =  MEMO-FETCH-COUNTER + MEMO-FETCH-COUNTER-C.                   
074600     COMPUTE MEMO-DELETE-COUNTER                                          
074700          =  MEMO-DELETE-COUNTER + MEMO-DELETE-COUNTER-C.                 
074800     MOVE  MEMO-FETCH-COUNTER      TO CR-FETCH-COUNTER.                   
075000     MOVE  MEMO-DELETE-COUNTER     TO CR-DELETE-COUNTER.                  
075100                                                                          
075200     MOVE CR-CKPT-RESTART-INFO  TO TCKRSTINF-WORK-STRG-DESC-TEXT.         
075300     MOVE 4022                  TO TCKRSTINF-WORK-STRG-DESC-LEN.          
075400     PERFORM U400-UPDATE-CHECKPOINT-INFO.                                 
075500                                                                          
075600     PERFORM Z100-COMMIT.                                                 
075700                                                                          
075800     PERFORM R700-SET-NEXT-CKPT-TMST.                                     
075900                                                                          
076000     MOVE ZERO                  TO MEMO-FETCH-COUNTER-C                   
076200                                   MEMO-DELETE-COUNTER-C.                 
076300                                                                          
076400   EJECT                                                                  
076500 Z998-DB2-ABEND.                                                          
076600                                                                          
076800                                                                          
076900     DISPLAY AA-ABEND-LIT.                                                
077000     DISPLAY AA-DB2-ERROR-LIT.                                            
077100     DISPLAY AA-PROGRAM-LIT.                                              
077200     DISPLAY AA-PARAGRAPH-LIT.                                            
077300     DISPLAY AA-DB2-OPERATION-LIT.                                        
077400     DISPLAY AA-DB2-TABLE-1.                                              
077500     DISPLAY AA-DB2-TABLE-2.                                              
077600     DISPLAY AA-DB2-TABLE-3.                                              
077700     DISPLAY '*****     VENDOR ID = ' APT00001-VENDOR-ID.                 
077800     SET AC-DB2-ERROR TO TRUE.                                            
077900                                                                          
078000     COPY DPPD004.                                                        
078100     CALL 'ILBOABN0' USING ABEND-CODE.                                    
