       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ICKUP026.                                                 
       AUTHOR.        COGNIZANT.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  05/28/2010.                                               
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      * ICKUP026 - MOVE ZERO VALUE FOR O/H DATA FOR ZERO O/H FLOW               
      *****************************************************************         
      *        THIS PROGRAM WILL LOOK UP USA IC COUNTS AND WILL PULL            
      *        LOC-NBR,CNT-STUP-ID, CNT-SEQ-NBR,EXEC-LVL-CDE,                   
      *        ONHAND_QTY ON ICT_SKU_LOC_CNT TABLE TO BE USED BY                
      *        PROGRAM ICKUP057 (ICK011 JOB).                                   
      *        THIS PROGRAM POPULATES ZERO VALUE FOR O/H DATA.                  
      *        THIS PROGRAM HAS 1 CURSORS FOR THIS PROCESS,                     
      *        THE CURSOR WILL GATHER UPDATE DATA FOR THE NIGHTLY               
      *        PROCESS.                                                         
      *****************************************************************         
      *                                                                         
      *  TABLES USED ARE:                                                       
      *      ICT_CNT_STUP    (RO) - IC COUNT SETUP                              
      *      ICT_LOC_CNT_STAT(RO) - IC LOCATION COUNT STAT                      
      *      ICT_SKU_LOC_CNT (RO) - IC SKU LOCATION COUNT                       
      *      ICT_CNT_EXTR_PRC(U)  - IC COUNT LAST PROCESS TIMESTAMP             
      *  FILES READ ARE:                                                        
      *      NONE                                                               
      *****************************************************************         
      * 05/28/10 - COGNIZANT        -  CLARITY ID 3132 - STORE ENHANCE-         
      *          - NEW PROGRAM         MENTS PROJECT                            
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT NIGHTLY-HOURLY-CTL-FILE    ASSIGN TO INP01.                   
                                                                                
           SELECT IC-COUNT-QTY-MAINT-FILE    ASSIGN TO OUT01.                   
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
       FILE SECTION.                                                            
                                                                                
       FD  NIGHTLY-HOURLY-CTL-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS NIGHTLY-HOURLY-CTL-REC.                               
       01  NIGHTLY-HOURLY-CTL-REC.                                              
           05  NIGHTLY-HOURLY-CTL-IND PIC X.                                    
           05  FILLER                 PIC X(79).                                
                                                                                
       FD  IC-COUNT-QTY-MAINT-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS IC-COUNT-QTY-MAINT-FILE.                              
       01  IC-COUNT-QTY-MAINT-REC     PIC  X(60).                               
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME                PIC X(08)    VALUE                
                                                 'ICKUP026'.                    
           05  PC-MAXIMUM-RETRY-COUNT         PIC S9(04)   VALUE +2             
                                                           COMP SYNC.           
           05  PC-DEFAULT-TMST                PIC X(26)    VALUE                
               '2007-01-01-00.00.00.000000'.                                    
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-WORK-VARIABLES.                                                   
           05  PV-RETRIES                     PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.        
           05  PV-NIGHTLY-HOURLY-CTL-IND      PIC  X       VALUE SPACES.        
             88 PV-NIGHTLY-RUN                VALUE 'N'.                        
           05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.        
           05  PV-SQLCODE-DISPLAY             PIC +(8)9    VALUE ZEROES.00830000
           05  PV-EXTR-PRC-TMST-NI         PIC S9(04)  COMP                CL*15
                                                         VALUE ZERO.       CL*15
               88 PV-EXTR-PRC-TMST-IS-NULL VALUE -1.                            
           05  PV-ORIG-CLR-DTE-NULL-IND    PIC S9(04)  COMP                CL*15
                                                         VALUE ZERO.       CL*15
               88 PV-ORIG-CLR-DTE-IS-NULL  VALUE -1.                            
           05  PV-LOC-NBR                  PIC  S9(4) COMP.                     
           05  PV-CURRENT-WEEKDAY          PIC 9.                          CL**4
           05  PV-CURRENT-WEEK-NBR         PIC 9.                          CL**4
           05  PV-CURRENT-DATE             PIC X(10).                      CL**4
           05  PV-CURRENT-TMST             PIC X(26).                      CL**4
           05  PV-LOCATION-TMST            PIC X(26).                      CL**4
           05  PV-LAST-RUN-TMST            PIC X(26).                      CL**4
           05  PV-YEST-DTE                 PIC X(10).                      CL**4
           05  PV-YEST-TMST                PIC X(26).                      CL**4
           05  PV-YEST-TMST-BOD.                                                
             10 PV-YEST-CC-BOD             PIC XX.                              
             10 PV-YEST-YY-BOD             PIC XX.                              
             10 FILLER                     PIC X.                               
             10 PV-YEST-MM-BOD             PIC XX.                              
             10 FILLER                     PIC X.                               
             10 PV-YEST-DD-BOD             PIC XX.                              
             10 FILLER                     PIC X.                               
             10 PV-YEST-TIME-BOD.                                               
               15 PV-YEST-HH-BOD           PIC XX.                              
               15 FILLER                   PIC X.                               
               15 PV-YEST-MI-BOD           PIC XX.                              
               15 FILLER                   PIC X.                               
               15 PV-YEST-SS-BOD           PIC XX.                              
               15 FILLER                   PIC X.                               
               15 PV-YEST-MS-BOD           PIC X(6).                            
           05  PV-YEST-TMST-BOD-RED        PIC X(26).                           
           05  PV-YEST-TMST-EOD.                                                
             10 PV-YEST-CC-EOD             PIC XX.                              
             10 PV-YEST-YY-EOD             PIC XX.                              
             10 FILLER                     PIC X.                               
             10 PV-YEST-MM-EOD             PIC XX.                              
             10 FILLER                     PIC X.                               
             10 PV-YEST-DD-EOD             PIC XX.                              
             10 FILLER                     PIC X.                               
             10 PV-YEST-TIME-EOD.                                               
               15 PV-YEST-HH-EOD           PIC XX.                              
               15 FILLER                   PIC X.                               
               15 PV-YEST-MI-EOD           PIC XX.                              
               15 FILLER                   PIC X.                               
               15 PV-YEST-SS-EOD           PIC XX.                              
               15 FILLER                   PIC X.                               
               15 PV-YEST-MS-EOD           PIC X(6).                            
           05  PV-YEST-TMST-EOD-RED        PIC X(26).                           
           05  PV-COMPLETED-COUNT-FLG      PIC X.                          CL**4
               88  PV-NOT-COMPLETED-COUNT  VALUE 'N'.                           
               88  PV-COMPLETED-COUNT      VALUE 'Y'.                           
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-MAN-FL-SW            PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-MAN-FL                        VALUE 'Y'.           
           05  PS-END-OF-CTL-FL-SW            PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-CTL-FILE                       VALUE 'Y'.          
           05  PS-NO-MORE-NIGHTLY-CNT-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-NO-MORE-NIGHTLY-CNT                  VALUE 'Y'.           
               88  PS-MORE-NIGHTLY-CNT                     VALUE 'N'.           
           05  PS-NO-MORE-NIGHTLY-LOC-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-NO-MORE-NIGHTLY-LOC                  VALUE 'Y'.           
               88  PS-MORE-NIGHTLY-LOC                     VALUE 'N'.           
           05  PS-PROCESS-STOP-SW             PIC  X(01)   VALUE 'N'.           
               88  PS-PROCESS-STOP                         VALUE 'Y'.           
           05  PS-LAST-RUN-TMST-FOUND-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-LAST-RUN-TMST-FOUND                  VALUE 'Y'.           
                                                                                
       01  ABEND-CODE                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
                                                                                
       01  DISPLAY-EDITS.                                                       
           05  DI-CNTS                      PIC  9(09)  VALUE ZEROS.            
                                                                                
      *                                                                         
      *****************************************************************         
      *    INPUT COPYBOOKS                                                      
      *****************************************************************         
           COPY ICRD058.                                                        
      *****************************************************************         
      *    OUTPUT COPYBOOKS                                                     
      *****************************************************************         
           COPY ICRD057.                                                        
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      *  XST_SKU  TABLE                                                         
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE XST00010                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  XST_SKU_LOC  TABLE                                                     
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE XST00008                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  ICT_CNT_EXTR_PRC TABLE                                                 
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE ICT00007                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  ICT_CNT_STUP_STAT TABLE                                                
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE ICT00019                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  PCT_TRN_CLSN_BAL TABLE                                                 
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE PCT00014                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TSKXREF TABLE                                                          
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TSKXREF                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  ICT_CNT_STUP TABLE                                                     
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE ICT00010                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  ICT_LOC_CNT_STAT TABLE                                                 
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE ICT00020                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  ICT_SKU_LOC_CNT TABLE                                                  
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE ICT00011                                                
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      *----------------------------------------------------------------         
      *  USA IC COUNTS CURSOR WILL FETCH ACTIVE NIGHTLY COUNT LOCATIONS         
      *----------------------------------------------------------------         
           EXEC SQL                                                             
             DECLARE NIGHTLY-LOC-CSR CURSOR FOR                                 
             SELECT DISTINCT A.LOC_NBR                                          
             FROM   ICT_LOC_CNT_STAT    A                                       
             WHERE A.CNT_STAT_CDE   = 'AVA'                                     
               AND A.LAST_UPD_TMST  =                                           
               (SELECT MAX(B.LAST_UPD_TMST)                                     
                  FROM ICT_LOC_CNT_STAT B                                       
                 WHERE B.LOC_NBR      = A.LOC_NBR                               
                   AND B.CNT_SEQ_NBR  = A.CNT_SEQ_NBR                           
                   AND B.CNT_STUP_ID  = A.CNT_STUP_ID)                          
              WITH CS                                                           
           END-EXEC.                                                            
      *----------------------------------------------------------------         
      *  USA IC COUNTS CURSOR WILL FETCH ACTIVE NIGHTLY COUNTS FOR LOC          
      *----------------------------------------------------------------         
           EXEC SQL                                                             
             DECLARE NIGHTLY-CNT-CSR CURSOR FOR                                 
             SELECT                                                             
                A.LOC_NBR                                                       
               ,A.SKU_NBR                                                       
               ,A.CNT_STUP_ID                                                   
               ,D.EXEC_LVL_CDE                                                  
               ,A.CNT_SEQ_NBR                                                   
             FROM                                                               
                  ICT_LOC_CNT_STAT    B                                         
                 ,ICT_CNT_STUP        D                                         
                 ,ICT_SKU_LOC_CNT     A                                         
                 ,TSKXREF           XREF                                        
             WHERE  B.LOC_NBR = :PV-LOC-NBR                                     
               AND B.CNT_STAT_CDE = 'AVA'                                       
               AND A.LOC_NBR  = B.LOC_NBR                                       
               AND A.CNT_STUP_ID    = B.CNT_STUP_ID                             
               AND A.CNT_SEQ_NBR    = B.CNT_SEQ_NBR                             
               AND D.CNT_STUP_ID    = B.CNT_STUP_ID                             
               AND D.LAST_ACTN_CDE  = 'A'                                       
               AND XREF.SKU = A.SKU_NBR                                         
               AND DATE(B.LAST_UPD_TMST)  =  DATE(A.LAST_UPD_TMST)              
               AND B.LAST_UPD_TMST  =                                           
                 (SELECT MAX(C.LAST_UPD_TMST)                                   
                  FROM ICT_LOC_CNT_STAT C                                       
                  WHERE C.LOC_NBR      = B.LOC_NBR                              
                    AND C.CNT_SEQ_NBR  = B.CNT_SEQ_NBR                          
                     AND C.CNT_STUP_ID  = B.CNT_STUP_ID)                        
                     AND D.LAST_UPD_TMST=                                       
                     (SELECT MAX(E.LAST_UPD_TMST)                               
                      FROM ICT_CNT_STUP     E                                   
                      WHERE E.CNT_STUP_ID    = D.CNT_STUP_ID)                   
                        AND A.LAST_UPD_TMST=                                    
                      (SELECT MAX(F.LAST_UPD_TMST)                              
                       FROM ICT_SKU_LOC_CNT  F                                  
                       WHERE F.CNT_STUP_ID = A.CNT_STUP_ID                      
                         AND F.SKU_NBR     = A.SKU_NBR                          
                         AND F.CNT_SEQ_NBR = A.CNT_SEQ_NBR                      
                         AND F.LOC_NBR     = A.LOC_NBR)                         
             WITH CS                                                            
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * 0000-MAIN-MODULE.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-COUNTS.                                         
                                                                                
           PERFORM 3000-EOJ-PROCESSING.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                           *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.                    
                                                                                
      * THE SAME 'QMT' ICT_CNT_EXTR_PRC ROW IS USED FOR THE NIGHTLY AND         
      * HOURLY RUN TO DETERMINE WHAT HAS HAPPENED SINCE THE LAST RUN.           
                                                                                
           PERFORM 8300-SELECT-LAST-RUN-TMST.                                   
           IF PV-EXTR-PRC-TMST-IS-NULL                                          
             MOVE PC-DEFAULT-TMST TO PV-LAST-RUN-TMST                           
           ELSE                                                                 
             MOVE ICT00007-EXTR-PRC-TMST TO PV-LAST-RUN-TMST.                   
                                                                                
           OPEN INPUT  NIGHTLY-HOURLY-CTL-FILE                                  
                OUTPUT IC-COUNT-QTY-MAINT-FILE.                                 
                                                                                
           EXEC SQL                                                             
               SET :PV-CURRENT-DATE = CURRENT DATE                              
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               SET :PV-CURRENT-TMST = CURRENT TIMESTAMP                         
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               SET :PV-YEST-DTE = CURRENT DATE - 1 DAY                          
           END-EXEC.                                                            
           EXEC SQL                                                             
               SET :PV-YEST-TMST = CURRENT TIMESTAMP - 1 DAY                    
           END-EXEC.                                                            
                                                                                
           MOVE PV-YEST-TMST TO PV-YEST-TMST-BOD                                
                                PV-YEST-TMST-EOD.                               
           MOVE '00.00.00.000000'    TO PV-YEST-TIME-BOD.                       
           MOVE '11.59.59.999999'    TO PV-YEST-TIME-EOD.                       
           MOVE PV-YEST-TMST-BOD     TO PV-YEST-TMST-BOD-RED.                   
           MOVE PV-YEST-TMST-EOD     TO PV-YEST-TMST-EOD-RED.                   
           DISPLAY 'YESTERDAYS DATES - ' PV-YEST-DTE ' - '                      
                                         PV-YEST-TMST-BOD ' - '                 
                                         PV-YEST-TMST-EOD.                      
                                                                                
           PERFORM 7000-READ-CTL-FILE.                                          
           IF PS-END-OF-CTL-FILE                                                
             DISPLAY 'NO CONTROL RECORD PRESENT' UPON CONSOLE                   
             PERFORM 9999-SQL-ABEND.                                            
           MOVE NIGHTLY-HOURLY-CTL-IND TO PV-NIGHTLY-HOURLY-CTL-IND.            
      ******************************************************************        
      * 2000-PROCESS-COUNTS.                                           *        
      *  ==> FINDS NON-COMPLETED/CANCELLED COUNTS TO BE UPDATE WITH    *        
      *  ==> VARIOUS QUANTITIES.                                       *        
      ******************************************************************        
       2000-PROCESS-COUNTS.                                                     
           MOVE '2000-PROCESS-COUNTS ' TO PV-PARAGRAPH-NAME.                    
           IF PV-NIGHTLY-RUN                                                    
             SET  PS-MORE-NIGHTLY-LOC TO TRUE                                   
             PERFORM 8130-OPEN-NIGHTLY-LOC-CSR                                  
             PERFORM 8140-FETCH-NIGHTLY-LOC-CSR                                 
             PERFORM                                                            
               UNTIL PS-NO-MORE-NIGHTLY-LOC                                     
               MOVE ICT00020-LOC-NBR TO PV-LOC-NBR                              
               SET  PS-MORE-NIGHTLY-CNT TO TRUE                                 
               PERFORM 8100-OPEN-NIGHTLY-CNT                                    
               PERFORM 8110-FETCH-NIGHTLY-CNT                                   
               PERFORM                                                          
                 UNTIL PS-NO-MORE-NIGHTLY-CNT                                   
                 PERFORM 2100-PROCESS-ACT-COUNTS                                
                 PERFORM 8110-FETCH-NIGHTLY-CNT                                 
               END-PERFORM                                                      
               PERFORM 8120-CLOSE-NIGHTLY-CNT                                   
               PERFORM 8140-FETCH-NIGHTLY-LOC-CSR                               
             END-PERFORM                                                        
             PERFORM 8150-CLOSE-NIGHTLY-LOC-CSR                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2100-PROCESS-ACT-COUNTS.                                      *         
      *  ==> CREATES IC COUNT QTY MAINT RECORDS                                 
      ******************************************************************        
       2100-PROCESS-ACT-COUNTS.                                                 
           MOVE ICT00011-CNT-STUP-ID     TO ICRD057-CNT-STUP-ID.                
           MOVE ICT00011-LOC-NBR         TO ICRD057-LOC-NBR.                    
           MOVE ICT00011-SKU-NBR         TO ICRD057-SKU-NBR.                    
           MOVE ICT00020-CNT-SEQ-NBR     TO ICRD057-CNT-SEQ-NBR.                
           MOVE ICT00010-EXEC-LVL-CDE    TO ICRD057-EXEC-LVL-CDE.               
           MOVE 0                        TO ICRD057-LOC-EXPTED-UNT-QTY.         
           PERFORM 7100-WRITE-QTY-MAINT-FILE.                                   
                                                                                
      ******************************************************************        
      * 3000-EOJ-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - CLOSE FILES                                               *        
      *    - DISPLAYS PROCESSING TOTALS                                *        
      *    - TAKES A FINAL COMMIT                                      *        
      ******************************************************************        
       3000-EOJ-PROCESSING.                                                     
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.                     
           CLOSE NIGHTLY-HOURLY-CTL-FILE                                        
                 IC-COUNT-QTY-MAINT-FILE.                                       
                                                                                
           IF PS-LAST-RUN-TMST-FOUND                                            
             PERFORM 8400-UPDATE-LAST-RUN-TMST                                  
           ELSE                                                                 
      **THIS SHOULD ONLY BE USED THE FIRST TIME THIS PGM IS RUN IN PROD         
             PERFORM 8500-INSERT-LAST-RUN-TMST.                                 
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
                                                                                
      *----------------------------------------------------------               
      * 7000-READ-CTL-FILE.                                                     
      *----------------------------------------------------------               
       7000-READ-CTL-FILE.                                                      
           READ NIGHTLY-HOURLY-CTL-FILE                                         
             AT END SET PS-END-OF-CTL-FILE TO TRUE.                             
                                                                                
                                                                                
      *----------------------------------------------------------               
      * 7100-WRITE-QTY-MAINT-FILE.                                              
      *----------------------------------------------------------               
       7100-WRITE-QTY-MAINT-FILE.                                               
           WRITE IC-COUNT-QTY-MAINT-REC                                         
             FROM ICRD057-IC-CNT-QTY-MAINT-FILE.                                
                                                                                
      *----------------------------------------------------------               
      * 8100-OPEN-NIGHTLY-CNT.                                                  
      *      OPEN THE CURSOR OF RELEASED COUNTS.                                
      *----------------------------------------------------------               
       8100-OPEN-NIGHTLY-CNT.                                                   
                                                                                
           MOVE '8100-OPEN-NIGHTLY-CNT' TO PV-PARAGRAPH-NAME.                   
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRIES                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR              
                      PS-PROCESS-STOP                    OR                     
                      SQLCODE = 0)                                              
                                                                                
                 EXEC SQL                                                       
                    OPEN NIGHTLY-CNT-CSR                                        
                 END-EXEC                                                       
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       SET PS-PROCESS-STOP TO TRUE                              
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR                      
              PS-PROCESS-STOP                                                   
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP026 OPEN OF NIGHTLY-CNT CURSOR FOR '            
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------               
      * 8110-FETCH-NIGHTLY-CNT.                                                 
      *      FETCH A RELEASED COUNTS ROW.                                       
      *----------------------------------------------------------               
       8110-FETCH-NIGHTLY-CNT.                                                  
                                                                                
           MOVE '8110-FETCH-NIGHTLY-CNT' TO PV-PARAGRAPH-NAME.                  
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRIES                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR              
                      PS-PROCESS-STOP                   OR                      
                      SQLCODE = 0                       OR                      
                      SQLCODE = +100)                                           
                                                                                
                 EXEC SQL                                                       
                    FETCH NIGHTLY-CNT-CSR                                       
                    INTO  :ICT00011-LOC-NBR                                     
                         ,:ICT00011-SKU-NBR                                     
                         ,:ICT00011-CNT-STUP-ID                                 
                         ,:ICT00010-EXEC-LVL-CDE                                
                         ,:ICT00020-CNT-SEQ-NBR                                 
                 END-EXEC                                                       
                                                                                
                EVALUATE TRUE                                                   
                    WHEN SQLCODE = -904                                         
                    WHEN SQLCODE = -911                                         
                    WHEN SQLCODE = -913                                         
                        CONTINUE                                                
                    WHEN SQLCODE = ZERO                                         
                        ADD +1 TO DI-CNTS                                       
                    WHEN SQLCODE = +100                                         
                        SET PS-NO-MORE-NIGHTLY-CNT TO TRUE                      
                    WHEN OTHER                                                  
                        SET PS-PROCESS-STOP        TO TRUE                      
                END-EVALUATE                                                    
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR                      
              PS-PROCESS-STOP                                                   
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP026 FETCH OF NIGHTLY-CNT CURSOR'                
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      * 8120-CLOSE-NIGHTLY-CNT.                                                 
      *      CLOSE THE RELEASED COUNTS CURSOR.                                  
      *-------------------------------------------------------------            
       8120-CLOSE-NIGHTLY-CNT.                                                  
                                                                                
           MOVE '8120-CLOSE-NIGHTLY-CNT' TO PV-PARAGRAPH-NAME.                  
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRIES                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR              
                      PS-PROCESS-STOP                    OR                     
                      SQLCODE = 0)                                              
                                                                                
                 EXEC SQL                                                       
                    CLOSE NIGHTLY-CNT-CSR                                       
                 END-EXEC                                                       
                                                                                
                EVALUATE TRUE                                                   
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       SET PS-PROCESS-STOP TO TRUE                              
                END-EVALUATE                                                    
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR                      
              PS-PROCESS-STOP                                                   
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP026 CLOSE OF NIGHTLY-CNT CURSOR'                
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------               
      * 8130-OPEN-NIGHTLY-CNT.                                                  
      *      OPEN THE CURSOR OF RELEASED COUNTS.                                
      *----------------------------------------------------------               
       8130-OPEN-NIGHTLY-LOC-CSR.                                               
                                                                                
           MOVE '8130-OPEN-NIGHTLY-LOC-CSR' TO PV-PARAGRAPH-NAME.               
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRIES                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR              
                      PS-PROCESS-STOP                    OR                     
                      SQLCODE = 0)                                              
                                                                                
                 EXEC SQL                                                       
                    OPEN NIGHTLY-LOC-CSR                                        
                 END-EXEC                                                       
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       SET PS-PROCESS-STOP TO TRUE                              
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR                      
              PS-PROCESS-STOP                                                   
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP026 OPEN OF NIGHTLY-LOC-CSR FOR '               
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------               
      * 8140-FETCH-NIGHTLY-LOC-CSR.                                             
      *      FETCH A RELEASED COUNTS ROW.                                       
      *----------------------------------------------------------               
       8140-FETCH-NIGHTLY-LOC-CSR.                                              
                                                                                
           MOVE '8140-FETCH-NIGHTLY-LOC-CSR' TO PV-PARAGRAPH-NAME.              
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRIES                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR              
                      PS-PROCESS-STOP                   OR                      
                      SQLCODE = 0                       OR                      
                      SQLCODE = +100)                                           
                                                                                
                 EXEC SQL                                                       
                    FETCH NIGHTLY-LOC-CSR                                       
                    INTO  :ICT00020-LOC-NBR                                     
                 END-EXEC                                                       
                                                                                
                EVALUATE TRUE                                                   
                    WHEN SQLCODE = -904                                         
                    WHEN SQLCODE = -911                                         
                    WHEN SQLCODE = -913                                         
                        CONTINUE                                                
                    WHEN SQLCODE = ZERO                                         
                        CONTINUE                                                
                    WHEN SQLCODE = +100                                         
                        SET PS-NO-MORE-NIGHTLY-LOC TO TRUE                      
                    WHEN OTHER                                                  
                        SET PS-PROCESS-STOP        TO TRUE                      
                END-EVALUATE                                                    
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR                      
              PS-PROCESS-STOP                                                   
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP026 FETCH OF NIGHTLY-LOC-CSR'                   
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      * 8150-CLOSE-NIGHTLY-LOC-CSR.                                             
      *      CLOSE THE RELEASED COUNTS CURSOR.                                  
      *-------------------------------------------------------------            
       8150-CLOSE-NIGHTLY-LOC-CSR.                                              
                                                                                
           MOVE '8150-CLOSE-NIGHTLY-LOC-CSR' TO PV-PARAGRAPH-NAME.              
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRIES                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR              
                      PS-PROCESS-STOP                    OR                     
                      SQLCODE = 0)                                              
                                                                                
                 EXEC SQL                                                       
                    CLOSE NIGHTLY-LOC-CSR                                       
                 END-EXEC                                                       
                                                                                
                EVALUATE TRUE                                                   
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       SET PS-PROCESS-STOP TO TRUE                              
                END-EVALUATE                                                    
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR                      
              PS-PROCESS-STOP                                                   
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP026 CLOSE OF NIGHTLY-LOC-CSR'                   
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      * 8300-SELECT-LAST-RUN-TMST                                               
      *      GET TIMESTAMP OF LAST RUN OF PROGRAM                               
      *-------------------------------------------------------------            
       8300-SELECT-LAST-RUN-TMST.                                               
           MOVE '8300-SELECT-LAST-RUN-TMST' TO PV-PARAGRAPH-NAME.               
           EXEC SQL                                                             
             SELECT   EXTR_PRC_DTE                                              
                     ,EXTR_PRC_TMST                                             
               INTO :ICT00007-EXTR-PRC-DTE                                      
                   ,:ICT00007-EXTR-PRC-TMST:PV-EXTR-PRC-TMST-NI                 
               FROM   ICT_CNT_EXTR_PRC    A                                     
               WHERE A.EXTR_PRC_TYP_CDE  = 'QMT'                                
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  SET PS-LAST-RUN-TMST-FOUND TO TRUE                            
              WHEN SQLCODE = +100                                               
               MOVE PC-DEFAULT-TMST TO ICT00007-EXTR-PRC-TMST                   
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP026 CHECK LAST RUN TMST'                        
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *-------------------------------------------------------------            
      * 8400-UPDATE-LAST-RUN-TMST                                               
      *      UPDATE LAST RUN TIMESTAMP OF TO ICT_CNT_STUP_STAT TABLE            
      *-------------------------------------------------------------            
       8400-UPDATE-LAST-RUN-TMST.                                               
           MOVE '8400-UPDATE-LAST-RUN-TMST' TO PV-PARAGRAPH-NAME.               
           EXEC SQL                                                             
           UPDATE ICT_CNT_EXTR_PRC                                              
           SET EXTR_PRC_TMST = :PV-CURRENT-TMST                                 
           WHERE EXTR_PRC_TYP_CDE = 'QMT'                                       
             AND EXTR_PRC_DTE     = :ICT00007-EXTR-PRC-DTE                      
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  CONTINUE                                                      
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP026 UPDATE LAST RUN TMST'                       
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
      *-------------------------------------------------------------            
      * 8500-INSERT-LAST-RUN-TMST                                               
      *      INSERT LAST RUN TIMESTAMP OF TO ICT_CNT_EXTR_PRC TABLE             
      *-------------------------------------------------------------            
       8500-INSERT-LAST-RUN-TMST.                                               
           MOVE '8500-INSERT-LAST-RUN-TMST' TO PV-PARAGRAPH-NAME.               
           EXEC SQL                                                             
             INSERT INTO ICT_CNT_EXTR_PRC                                       
             (EXTR_PRC_TYP_CDE                                                  
             ,EXTR_PRC_DTE                                                      
             ,EXTR_PRC_TMST)                                                    
             VALUES                                                             
             ('QMT'                                                             
             ,:PV-CURRENT-DATE                                                  
             ,:PV-CURRENT-TMST)                                                 
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  CONTINUE                                                      
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP026 INSERT LAST RUN TMST'                       
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 9990-DISPLAY-CONTROL-INFO.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
      ******************************************************************        
       9990-DISPLAY-CONTROL-INFO.                                               
                                                                                
           DISPLAY '********************************************'.              
           DISPLAY '**** RUN STATISTICS FOR PROGRAM ICKUP026 ***'.              
           DISPLAY '****'.                                                      
           DISPLAY 'COUNT ROWS PROCESSED       :' DI-CNTS                       
           DISPLAY '****'.                                                      
           DISPLAY '**** RUN STATS END FOR PROGRAM ICKUP026  ***'.              
           DISPLAY '********************************************'.              
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND.                                                *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *        
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
                                                                                
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'ICKUP026 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                           
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
