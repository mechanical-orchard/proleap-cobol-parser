       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    PXKUT103.                                         00020000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  07/26/2010.                                       00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      * THIS PROGRAM WILL DETERMINE THE OSA & STORE EFFECTIVE REPORTING 00080000
      * DATES AND ONLY WRITE OUT RECORDS FOR THOSE DATES INTO THEIR     00090000
      * RESPECTIVE FILES.                                               00091000
      *                                                                 00091100
      * BUSINESS RULES:                                                 00091200
      * THE OSA FILE WILL ONLY CONTAIN RECORDS FOR THE EFFECTIVE DATE   00091300
      * RANGE THAT IS 18 DAYS OUT PLUS 1 THRU 7 FROM TODAY'S DATE.      00091401
      * ADDITIONALLY, WE WILL ONLY PURSUE THIS LOGIC EACH TUESDAY.      00091504
      * THE STORE FILE WILL ONLY CONTAIN RECORDS FOR THE EFFECTIVE DATE 00091604
      * THAT IS 2 DAYS OUT FROM TODAY'S DATE.                           00091704
      ******************************************************************00091804
      * DATE: 07/26/2010 | MADE BY: THOMAS HANSON | PROJECT: WR37816    00091904
      * DESCRIPTION OF CHANGES: INITIAL PROGRAM IMPLEMENTATION.         00092004
      ******************************************************************00092104
      * DATE: 08/26/2010 | MADE BY: THOMAS HANSON | PROJECT: WR37816    00092204
      * DESCRIPTION OF CHANGES: OSA DATE RANGE SELECTION CHANGE.        00092304
      ******************************************************************00092404
       ENVIRONMENT DIVISION.                                            00093000
       CONFIGURATION SECTION.                                           00094000
       SOURCE-COMPUTER.    IBM-3090.                                    00095000
       OBJECT-COMPUTER.    IBM-3090.                                    00096000
       INPUT-OUTPUT SECTION.                                            00097000
       FILE-CONTROL.                                                    00098000
                                                                        00099000
           SELECT CONSOLIDATION-SKUS-FILE                               00100000
                  ASSIGN TO INP01.                                      00110000
           SELECT TODAYS-DATE-CARD                                      00120000
                  ASSIGN TO INP02.                                      00130000
           SELECT OSA-OUTPUT-FILE                                       00140000
                  ASSIGN TO OUT01.                                      00150000
           SELECT STR-OUTPUT-FILE                                       00160000
                  ASSIGN TO OUT02.                                      00170000
                                                                        00180000
       DATA DIVISION.                                                   00190000
       FILE SECTION.                                                    00200000
                                                                        00210000
       FD  CONSOLIDATION-SKUS-FILE                                      00220000
           RECORDING MODE IS F                                          00230000
           LABEL RECORDS OMITTED                                        00240000
           BLOCK CONTAINS 0 RECORDS                                     00250000
           DATA RECORD IS CONSOLIDATION-SKUS-RECORD.                    00260000
       01  CONSOLIDATION-SKUS-RECORD           PIC X(365).              00270000
                                                                        00280000
       FD  TODAYS-DATE-CARD                                             00290000
           RECORDING MODE IS F                                          00300000
           LABEL RECORDS OMITTED                                        00310000
           BLOCK CONTAINS 0 RECORDS                                     00320000
           DATA RECORD IS TODAYS-DATE-RECORD.                           00330000
       01  TODAYS-DATE-RECORD                  PIC X(080).              00340000
                                                                        00350000
       FD  OSA-OUTPUT-FILE                                              00360000
           RECORDING MODE IS F                                          00370000
           LABEL RECORDS OMITTED                                        00380000
           BLOCK CONTAINS 0 RECORDS                                     00390000
           DATA RECORD IS OSA-OUTPUT-RECORD.                            00400000
       01  OSA-OUTPUT-RECORD                   PIC X(365).              00410000
                                                                        00410100
       FD  STR-OUTPUT-FILE                                              00410200
           RECORDING MODE IS F                                          00410300
           LABEL RECORDS OMITTED                                        00410400
           BLOCK CONTAINS 0 RECORDS                                     00410500
           DATA RECORD IS STR-OUTPUT-RECORD.                            00410600
       01  STR-OUTPUT-RECORD                   PIC X(365).              00410700
                                                                        00410800
       WORKING-STORAGE SECTION.                                         00410900
      ******************************************************************00411000
      **PROGRAM SWITCHES                                                00412000
      ******************************************************************00413000
       01  PS-SWITCH-AREAS.                                             00414000
           05  PS-END-OF-FILE-SW               PIC X(01)  VALUE 'N'.    00415000
               88  PS-END-OF-FILE                         VALUE 'Y'.    00416000
               88  PS-NOT-END-OF-FILE                     VALUE 'N'.    00417000
      ******************************************************************00418000
      **SYSTEM DATE AND TIME VARIABLES                                  00419000
      ******************************************************************00420000
       01  SYS-DATE-TIME.                                               00430000
           05  SYS-DATE                        PIC X(08) VALUE SPACES.  00440000
           05  SYS-TIME                        PIC X(08) VALUE SPACES.  00450000
                                                                        00460000
       01  ST-BEG-TIME.                                                 00470000
           05  ST-BEG-HR                       PIC 9(02) VALUE ZEROS.   00480000
           05  FILLER                          PIC X(01) VALUE ':'.     00490000
           05  ST-BEG-MN                       PIC 9(02) VALUE ZEROS.   00500000
                                                                        00510000
       01  ST-END-TIME.                                                 00520000
           05  ST-END-HR                       PIC 9(02) VALUE ZEROS.   00530000
           05  FILLER                          PIC X(01) VALUE ':'.     00540000
           05  ST-END-MN                       PIC 9(02) VALUE ZEROS.   00550000
                                                                        00560000
       01  SD-EDIT-DATE.                                                00570000
           05  SD-MONTH                        PIC 9(02) VALUE ZEROS.   00580000
           05  FILLER                          PIC X(01) VALUE '-'.     00590000
           05  SD-DAY                          PIC 9(02) VALUE ZEROS.   00600000
           05  FILLER                          PIC X(01) VALUE '-'.     00610000
           05  SD-CENT                         PIC 9(02) VALUE ZEROS.   00620000
           05  SD-YEAR                         PIC 9(02) VALUE ZEROS.   00630000
                                                                        00640000
      ******************************************************************00650000
      *DATE CONTROL CARD LAYOUT                                        *00651000
      ******************************************************************00652000
       01 CC-CONTROL-DATE.                                              00653000
          05 CC-DATE                           PIC X(10) VALUE SPACES.  00654000
          05  FILLER                           PIC X(70) VALUE SPACE.   00655000
      ***************************************************************** 00656000
      ******************************************************************00657000
      **PROGRAM ACCUMULATORS (COUNTERS)                                 00658000
      ******************************************************************00659000
       01  PA-PROGRAM-ACCUMULATORS.                                     00660000
           05  PA-READ                         PIC 9(11) VALUE ZEROS.   00670000
           05  PA-WRITE-OSA                    PIC 9(11) VALUE ZEROS.   00680000
           05  PA-WRITE-STR                    PIC 9(11) VALUE ZEROS.   00690000
      ******************************************************************00700000
      **PROGRAM VARIABLES                                               00710000
      ******************************************************************00720000
       01  PV-PROGRAM-VARIABLES.                                        00730000
           05  PV-INPUT-RECORD                 PIC X(365) VALUE SPACES. 00740000
           05  PV-INC-DAYS-AHEAD               PIC 9(04)  VALUE ZERO.   00750000
           05  PV-OSA-EFF-DTE-BEG              PIC X(10)  VALUE SPACES. 00760001
           05  PV-OSA-EFF-DTE-END              PIC X(10)  VALUE SPACES. 00761001
           05  PV-STR-EFF-DTE                  PIC X(10)  VALUE SPACES. 00770000
           05  PV-DAY-OF-WEEK                  PIC X(09)  VALUE SPACES. 00780000
       01  CC-TODAYS-DATE-CARD-LAYOUT.                                  00781000
           05  CC-TODAYS-DATE-CCYY-MM-DD       PIC X(10)  VALUE SPACES. 00782000
           05  FILLER                          PIC X(70)  VALUE ZEROS.  00782100
       01  PV-STR-EFF-DTE-COMP.                                         00783600
           05  PV-STR-MONTH                    PIC X(02) VALUE SPACES.  00783700
           05  FILLER                          PIC X(01) VALUE '/'.     00783800
           05  PV-STR-DAY                      PIC X(02) VALUE SPACES.  00783900
           05  FILLER                          PIC X(01) VALUE '/'.     00784000
           05  PV-STR-CENT                     PIC X(02) VALUE SPACES.  00784100
           05  PV-STR-YEAR                     PIC X(02) VALUE SPACES.  00784200
       01  PV-OSA-INP-EFF-DTE-COMP.                                     00784305
           05  PV-OSA-CENT                     PIC X(02) VALUE SPACES.  00784405
           05  PV-OSA-YEAR                     PIC X(02) VALUE SPACES.  00784505
           05  FILLER                          PIC X(01) VALUE '-'.     00784606
           05  PV-OSA-MONTH                    PIC X(02) VALUE SPACES.  00784705
           05  FILLER                          PIC X(01) VALUE '-'.     00784806
           05  PV-OSA-DAY                      PIC X(02) VALUE SPACES.  00784905
      ******************************************************************00785100
      **PROGRAM CONSTANTS                                               00785200
      ******************************************************************00785300
       01  PC-PROGRAM-CONSTANTS.                                        00785400
           05  PC-PROGRAM-NAME               PIC X(08) VALUE 'PXKUT103'.00785500
      ******************************************************************00785600
      **ABEND CODE INFORMATION                                          00785700
      ******************************************************************00785800
       01  AC-ABEND-FIELDS.                                             00785900
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    00786000
                                                           COMP SYNC.   00786100
               88  AC-EMPTY-FILE-ERROR                   VALUE +4005.   00786200
               88  AC-CALENDAR-ROUTINE-ERROR             VALUE +4019.   00786300
       01  AC-MISC-ABEND-FIELDS.                                        00786400
           05  AC-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   00786500
           05  AC-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   00786600
           05  AC-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   00787000
      ******************************************************************00788000
      **COPYBOOKS                                                       00789000
      ******************************************************************00790000
      *INP01                                                            00800000
           COPY PXRD102 REPLACING 'PXRD102-' BY 'PX102-INP-'.           00810000
      *OUT01-OSA FILE                                                   00820000
           COPY PXRD102 REPLACING 'PXRD102-' BY 'PX102-OSA-'.           00830000
      *OUT02-STR FILE                                                   00840000
           COPY PXRD102 REPLACING 'PXRD102-' BY 'PX102-STR-'.           00841000
      ******************************************************************00842000
      * DATE ROUTINE COPY MEMBERS                                       00843000
      ******************************************************************00844000
           COPY DPWS500.                                                00845000
                                                                        00846000
       PROCEDURE DIVISION.                                              00847000
      ******************************************************************00848000
       0000-MAINLINE.                                                   00849000
                                                                        00850000
           PERFORM 1000-INITIALIZATION.                                 00860000
                                                                        00870000
           PERFORM 2000-PROCESS-RECORDS                                 00880000
                   UNTIL PS-END-OF-FILE.                                00890000
                                                                        00900000
           PERFORM 9100-WRAP-UP.                                        00910000
           GOBACK.                                                      00920000
                                                                        00930000
      ******************************************************************00940000
      **INITIALIZATION ROUTINE                                          00950000
      ******************************************************************00960000
       1000-INITIALIZATION.                                             00970000
                                                                        00980000
           OPEN OUTPUT OSA-OUTPUT-FILE                                  00990000
                       STR-OUTPUT-FILE.                                 01000000
           OPEN INPUT CONSOLIDATION-SKUS-FILE.                          01010000
           OPEN INPUT TODAYS-DATE-CARD.                                 01020000
                                                                        01030000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 01040000
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            01050000
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            01060000
           MOVE SYS-DATE (1:2) TO SD-CENT.                              01070000
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              01080000
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             01090000
           MOVE SYS-DATE (7:2) TO SD-DAY.                               01100000
                                                                        01110000
           PERFORM 5000-READ-FROM-INPUT-FILE.                           01120000
           IF PS-NOT-END-OF-FILE                                        01130000
              PERFORM 1100-READ-TODAYS-DATE-CARD                        01140000
              PERFORM 1200-CALCULATE-OSA-EFF-DTE-RNG                    01150001
              PERFORM 1300-CALCULATE-STR-EFF-DTE                        01160000
           ELSE                                                         01170000
              SET AC-EMPTY-FILE-ERROR        TO TRUE                    01180000
              MOVE '1000-INITIALIZATION'     TO AC-PARAGRAPH-NAME       01190000
              MOVE 'INP01 FILE IS EMPTY'     TO AC-OPERATION-MSG-1      01200000
              PERFORM 9000-NON-SQL-ABEND                                01210000
           END-IF.                                                      01220000
                                                                        01221000
      ***************************************************************** 01222000
      **READ THE RUN DATE CC - CREATED FROM ESP IN THE JOBCARD          01223000
      ***************************************************************** 01224000
       1100-READ-TODAYS-DATE-CARD.                                      01225000
                                                                        01225100
           READ TODAYS-DATE-CARD INTO CC-TODAYS-DATE-CARD-LAYOUT.       01225200
           IF CC-TODAYS-DATE-CCYY-MM-DD EQUAL SPACES                    01225300
              SET AC-EMPTY-FILE-ERROR           TO TRUE                 01225400
              MOVE '1100-READ-TODAYS-DATE-CARD' TO AC-PARAGRAPH-NAME    01225500
              MOVE 'INP02 FILE IS EMPTY'        TO AC-OPERATION-MSG-1   01225600
              PERFORM 9000-NON-SQL-ABEND                                01225700
           ELSE                                                         01225800
              DISPLAY ' '                                               01225900
              DISPLAY 'TODAYS DATE FROM CC: ' CC-TODAYS-DATE-CCYY-MM-DD 01226000
           END-IF.                                                      01226100
                                                                        01226200
      ******************************************************************01226300
      **FIGURE OUT THE OSA SUMMARY EFFECTIVE DATE                       01226400
      **PER BUSINESS RULES,ONLY CHECK OSA DATES IF TODAY IS A TUESDAY!  01226501
      ******************************************************************01226601
       1200-CALCULATE-OSA-EFF-DTE-RNG.                                  01226701
                                                                        01226801
           PERFORM 1250-GET-DAY-OF-THE-WEEK.                            01226901
           IF PV-DAY-OF-WEEK EQUAL 'TUESDAY'                            01227001
      ***GET THE FIRST DATE FOR THE OSA COMPARE DATE RANGE              01227102
              MOVE 18 TO PV-INC-DAYS-AHEAD                              01227301
              ADD   1 TO PV-INC-DAYS-AHEAD                              01227401
              INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56            01227601
              MOVE CC-TODAYS-DATE-CCYY-MM-DD TO DPG52-LK-DATE-INPUT     01227701
              SET DPG51-FISCAL-454-CALENDAR TO TRUE                     01227801
              SET DPG52-LK-DTE-ISO-FMT      TO TRUE                     01227901
              SET DPG51-INCREMENT-DATE      TO TRUE                     01228001
              MOVE PV-INC-DAYS-AHEAD        TO DPG51-INCR-DECR-DAYS-9   01228101
              PERFORM 8000-DATE-ROUTINE                                 01228201
              MOVE DPG55-DB2-ISO-DATE-FMT   TO PV-OSA-EFF-DTE-BEG       01228302
              DISPLAY 'OSA BEGIN DTE RANGE = ' PV-OSA-EFF-DTE-BEG       01228502
      ***GET THE LAST DATE FOR THE OSA COMPARE DATE RANGE               01229102
              ADD   6 TO PV-INC-DAYS-AHEAD                              01229302
              INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56            01229402
              MOVE CC-TODAYS-DATE-CCYY-MM-DD TO DPG52-LK-DATE-INPUT     01229502
              SET DPG51-FISCAL-454-CALENDAR TO TRUE                     01229602
              SET DPG52-LK-DTE-ISO-FMT      TO TRUE                     01229702
              SET DPG51-INCREMENT-DATE      TO TRUE                     01229802
              MOVE PV-INC-DAYS-AHEAD        TO DPG51-INCR-DECR-DAYS-9   01229902
              PERFORM 8000-DATE-ROUTINE                                 01230002
              MOVE DPG55-DB2-ISO-DATE-FMT   TO PV-OSA-EFF-DTE-END       01230102
              DISPLAY 'OSA ENDNG DTE RANGE = ' PV-OSA-EFF-DTE-END       01230202
           ELSE                                                         01230801
              DISPLAY '*** TODAY IS NOT TUESDAY, SKIPPING OSA RPT LOGIC'01230901
           END-IF.                                                      01231001
                                                                        01232500
      ******************************************************************01232600
       1250-GET-DAY-OF-THE-WEEK.                                        01232700
      *    RETRIEVES DAY OF THE WEEK USING DATE CCYY-MM-DD AS INPUT     01232800
      ******************************************************************01232900
                                                                        01233000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01233100
           SET  DPG51-FISCAL-454-CALENDAR    TO TRUE.                   01233200
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   01233300
           SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                   01233400
                                                                        01233500
           MOVE CC-TODAYS-DATE-CCYY-MM-DD TO DPG52-LK-DATE-INPUT.       01233600
           PERFORM 8000-DATE-ROUTINE.                                   01233700
           STRING DPG55-ALPHA-WEEK-DAY                                  01233800
                               DELIMITED BY SPACE                       01233900
                ' '            DELIMITED BY SIZE                        01234000
           INTO PV-DAY-OF-WEEK.                                         01234100
           DISPLAY '         - DAY-OF-THE-WEEK = ' PV-DAY-OF-WEEK.      01234200
                                                                        01234300
      ******************************************************************01234400
      *FIGURE OUT THE STORE EFFECTIVE DATE                              01234500
      ******************************************************************01234600
       1300-CALCULATE-STR-EFF-DTE.                                      01234700
                                                                        01234800
           MOVE 2 TO PV-INC-DAYS-AHEAD.                                 01234900
           DISPLAY 'STR RPT DAYS TO INC = ' PV-INC-DAYS-AHEAD.          01235000
                                                                        01236000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01236100
           MOVE CC-TODAYS-DATE-CCYY-MM-DD TO DPG52-LK-DATE-INPUT.       01236200
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       01236300
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                       01236400
           SET DPG51-INCREMENT-DATE      TO TRUE                        01236500
           MOVE PV-INC-DAYS-AHEAD        TO DPG51-INCR-DECR-DAYS-9.     01236600
           PERFORM 8000-DATE-ROUTINE.                                   01236700
           MOVE DPG55-DB2-ISO-DATE-FMT   TO PV-STR-EFF-DTE.             01236800
                                                                        01236900
           DISPLAY 'STR DETAILS RPT DTE = ' PV-STR-EFF-DTE.             01237000
           MOVE PV-STR-EFF-DTE (1:2) TO PV-STR-CENT.                    01237105
           MOVE PV-STR-EFF-DTE (3:2) TO PV-STR-YEAR.                    01237205
           MOVE PV-STR-EFF-DTE (6:2) TO PV-STR-MONTH.                   01237305
           MOVE PV-STR-EFF-DTE (9:2) TO PV-STR-DAY.                     01237405
           DISPLAY 'STR COMPARE EFF DTE = ' PV-STR-EFF-DTE-COMP.        01237500
                                                                        01237600
      ******************************************************************01237700
      **MAIN PROCESSING FOR EACH RECORD...                              01237800
      ******************************************************************01237900
       2000-PROCESS-RECORDS.                                            01238000
                                                                        01239104
           IF PV-DAY-OF-WEEK EQUAL 'TUESDAY'                            01239205
              MOVE PX102-INP-CONSOLIDATE-EFF-DTE (7:2) TO PV-OSA-CENT   01239306
              MOVE PX102-INP-CONSOLIDATE-EFF-DTE (9:2) TO PV-OSA-YEAR   01239406
              MOVE PX102-INP-CONSOLIDATE-EFF-DTE (1:2) TO PV-OSA-MONTH  01239505
              MOVE PX102-INP-CONSOLIDATE-EFF-DTE (4:2) TO PV-OSA-DAY    01239605
              IF PV-OSA-INP-EFF-DTE-COMP >= PV-OSA-EFF-DTE-BEG          01240005
               AND PV-OSA-INP-EFF-DTE-COMP <= PV-OSA-EFF-DTE-END        01241005
                 PERFORM 4000-WRITE-TO-OUTPUT-OSA-FILE                  01250005
              END-IF                                                    01260005
           END-IF.                                                      01261005
                                                                        01270000
           IF PX102-INP-CONSOLIDATE-EFF-DTE EQUAL PV-STR-EFF-DTE-COMP   01271000
              PERFORM 4100-WRITE-TO-OUTPUT-STR-FILE                     01272000
           END-IF.                                                      01273000
                                                                        01274000
           PERFORM 5000-READ-FROM-INPUT-FILE.                           01275000
                                                                        01275100
      ******************************************************************01275200
      **WRITE OUT TO THE OSA FILE.                                      01275300
      ******************************************************************01275400
       4000-WRITE-TO-OUTPUT-OSA-FILE.                                   01275500
                                                                        01275600
           MOVE PX102-INP-CNSLDTN-SKUS TO PX102-OSA-CNSLDTN-SKUS.       01275700
           WRITE OSA-OUTPUT-RECORD                                      01275800
                 FROM PX102-OSA-CNSLDTN-SKUS.                           01275900
           ADD +1 TO PA-WRITE-OSA.                                      01276000
                                                                        01277000
      ******************************************************************01278000
      **WRITE OUT TO THE STORE FILE.                                    01279000
      ******************************************************************01280000
       4100-WRITE-TO-OUTPUT-STR-FILE.                                   01290000
                                                                        01300000
           MOVE PX102-INP-CNSLDTN-SKUS TO PX102-STR-CNSLDTN-SKUS.       01310000
           WRITE STR-OUTPUT-RECORD                                      01311000
                 FROM PX102-STR-CNSLDTN-SKUS.                           01312000
           ADD +1 TO PA-WRITE-STR.                                      01313000
                                                                        01314000
      ******************************************************************01315000
      **READ IN FROM INPUT.                                             01316000
      ******************************************************************01317000
       5000-READ-FROM-INPUT-FILE.                                       01318000
                                                                        01319000
           INITIALIZE PX102-INP-CNSLDTN-SKUS                            01320000
                      CONSOLIDATION-SKUS-RECORD.                        01330000
                                                                        01340000
           READ CONSOLIDATION-SKUS-FILE                                 01350000
              INTO PX102-INP-CNSLDTN-SKUS                               01360000
                 AT END                                                 01370000
                 SET PS-END-OF-FILE TO TRUE                             01380000
           END-READ.                                                    01390000
                                                                        01400000
           IF  PS-NOT-END-OF-FILE                                       01410000
               ADD +1                       TO PA-READ                  01420000
           END-IF.                                                      01430000
                                                                        01440000
      ******************************************************************01450000
      **RUN DATE ROUTINE                                                01460000
      ******************************************************************01470000
       8000-DATE-ROUTINE.                                               01480000
                                                                        01490000
           CALL DP500-KOHLS-CALENDAR-ROUTINE                            01500000
                      USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.        01501000
                                                                        01502000
           EVALUATE TRUE                                                01503000
                WHEN DPG54-NO-ERROR                                     01504000
                     CONTINUE                                           01505000
                WHEN OTHER                                              01506000
                   DISPLAY DPG54-ERROR-MESSAGE                          01507000
                   SET AC-CALENDAR-ROUTINE-ERROR TO TRUE                01508000
                   MOVE '8000-DATE-ROUTINE  '    TO AC-PARAGRAPH-NAME   01509000
                   MOVE 'SEE ABOVE ERROR MSG'    TO AC-OPERATION-MSG-1  01510000
                   PERFORM 9000-NON-SQL-ABEND                           01510100
           END-EVALUATE.                                                01510200
                                                                        01510300
      ******************************************************************01510400
      *  9000-NON-SQL-ABEND                                             01510500
      *  PERFORMS AN ABEND WHEN A NON-SQL BASED ERROR TAKES PLACE DURING01510600
      *  PROGRAM EXECUTION. LIKELY A CONTROL CARD OR DATE ROUTINE ISSUE.01510700
      ******************************************************************01510800
       9000-NON-SQL-ABEND.                                              01510900
                                                                        01511000
           PERFORM 9100-WRAP-UP.                                        01512000
                                                                        01513000
           DISPLAY '** ABEND           **'.                             01514000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          01515000
           DISPLAY '** PARAGRAPH       ** = ' AC-PARAGRAPH-NAME.        01516000
           DISPLAY '** OPERATION       ** = ' AC-OPERATION-MSG-1.       01517000
           DISPLAY '**                 ** = ' AC-OPERATION-MSG-2.       01518000
                                                                        01519000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         01520000
                                                                        01530000
      ******************************************************************01540000
      **WRAP UP PROCESSING                                              01550000
      ******************************************************************01560000
       9100-WRAP-UP.                                                    01570000
                                                                        01580000
           CLOSE OSA-OUTPUT-FILE                                        01590000
                 STR-OUTPUT-FILE.                                       01600000
           CLOSE CONSOLIDATION-SKUS-FILE.                               01610000
           CLOSE TODAYS-DATE-CARD.                                      01620000
                                                                        01630000
           PERFORM 9999-DISPLAY-STATISTICS.                             01640000
                                                                        01650000
      ******************************************************************01660000
      **DISPLAY PROGRAM STATS                                           01670000
      ******************************************************************01680000
       9999-DISPLAY-STATISTICS.                                         01690000
                                                                        01700000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 01710000
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            01720000
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            01730000
                                                                        01740000
           DISPLAY '*************************************************'. 01750000
           DISPLAY '* PROGRAM   = ' PC-PROGRAM-NAME.                    01760000
           DISPLAY '* PURPOSE   = ONLY INCLUDE RPT EFF DTE RECORDS.'.   01770000
           DISPLAY '* RUN DATE  = ' SD-EDIT-DATE.                       01780000
           DISPLAY '* BEG TIME  = ' ST-BEG-TIME.                        01790000
           DISPLAY '* END TIME  = ' ST-END-TIME.                        01800000
           DISPLAY '*************************************************'. 01810000
           DISPLAY '* INPUT RECORDS READ: ' PA-READ.                    01820000
           DISPLAY '*************************************************'. 01830000
           DISPLAY '* OSA BEG DTE = ' PV-OSA-EFF-DTE-BEG.               01840002
           DISPLAY '* OSA END DTE = ' PV-OSA-EFF-DTE-END.               01841002
           DISPLAY '* OSA RECORD WRITES: ' PA-WRITE-OSA.                01850000
           DISPLAY '*************************************************'. 01860000
           DISPLAY '* STR EFF DTE = ' PV-STR-EFF-DTE.                   01870000
           DISPLAY '* STR RECORD WRITES: ' PA-WRITE-STR.                01880000
           DISPLAY '*************************************************'. 01890000
                                                                        01900000
      ******************************************************************01910000
