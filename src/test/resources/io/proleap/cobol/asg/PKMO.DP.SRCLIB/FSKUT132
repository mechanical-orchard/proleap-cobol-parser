       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              FSKUT132.                                       
       AUTHOR.                  BOB FORD.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            11/01/04.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *             TUPC,SKU/LOC DOMAIN MERGE TO TSKST                 *        
      *             STYLE SUMMARY EXTRACT                              *        
      *                                                                *        
      *   THIS PROGRAM READS A MERGED INPUT FILE OF ITEMS UNLOADED FROM*        
      *   TUPC AND SKU/LOC DOMAIN RECORDS BY FSKUT131.  THE INPUT IS   *        
      *   SORTED BY ITEM-NMBR AND STORE.  THE FILE IS MATCHED/MERGED   *        
      *   WITH A TSKST UNLOAD FILE, MATCHING ON ITEM/STORE.            *        
      *                                                                *        
      ******************************************************************        
      *  CHANGE LOG:                                                            
      *  11/07/04 - BOB FORD                                                    
      *             (WR #26357) NEW PROGRAM - CLONED PROGRAM IMKUT080           
RK0906*  09/06/06 - RICH KELLEN                                                 
RK0906*             (WR #30352) CHANGE OUTPUT FILE LENGTH TO FROM 130           
RK0906*             TO 126 TO CORRESPOND WITH REMOVAL OF FILLER FROM            
RK0906*             COPYBOOK FSRD122.                                           
RK1006*  10/19/06 - RICH KELLEN                                                 
RK1006*             (WR #31380) CREATE SECOND OUTPUT FILE OF ONLY               
RK1006*             REGULAR PRICED ITEMS (STATUS 10, 20, 40, & 90)              
RK1006*                - EXCLUDE STORE 000 AND 873                              
RK1006*                - EXCLUDE WHERE ONHAND QTY = 0                           
RK1006*                - EXCLUDE WHERE BOTH LAST SALE DATE AND LAST             
RK1006*                          RCPT DATE = 0                                  
RK1006*                - EXCLUDE GWP ITEMS (RETAIL AMT <= $0.01)                
RK1006*             THIS FILE WILL BE USED IN SEPARATE PROCESSES TO             
RK1006*             CREATE THE WEEKLY MARKET SOLUTIONS EXTRACT AND              
RK1006*             ZERO SALES REPORT.                                          
SS0407*  04/03/07 - SHARON SZPISZAR (STRATAGEM)                                 
SS0407*             (WR #32001) ADDED FIELDS UNT-TRL-AMT AND                    
SS0407*             KOHLS-SIZE-CDE TO OUT02 FILE ITEM-REG-FILE-OUT.             
W38972*  09/29/10 - COGNIZANT (SANDEEP)                                 00260000
W38972*             (WR #38972) MLOF PROJECT CHANGES.                   00260000
W38972*             INTRODUCED A INPUT EFC UNLOAD FILE LOADED THE FILE  00260000
W38972*             IN TO AN INTERNAL TABLE WITH EFC STORES ONLY.       00260000
W38972*             REMOVED THE HARD CODED EFC LOCATION CHECK AND       00260000
W38972*             REPLACED THAT WITH THE LOOK UP TO THE INTERNAL EFC  00260000
W38972*             TABLE CHECK.                                        00260000
208057*  09/11/18 - JIM HUNTER                                          00260000
208057*             (CHG0208057) MAINFRAME REFACTORING CHANGES.         00260000
208057*             REPLACE THE LOGIC TO GET THE JOB NAME (COPYBOOKS    00260000
208057*             MLWS502, MLLS502 AND MLPD502) WITH A CONTROL CARD   00260000
208057*             THAT WILL INDICATE IF EXTRA FILES WILL BE OPENED,   00260000
208057*             PROCESSED AND CLOSED INSTEAD OF A HARD-CODED CHECK  00260000
208057*             FOR JOB MHK118.                                     00260000
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT TSKST-UNLD-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT MERGED-INPUT-FILE                                             
               ASSIGN TO INP02.                                                 
                                                                                
W38972     SELECT EFCLOC-INPUT-FILE                                             
W38972         ASSIGN TO INP03.                                                 
W38972                                                                          
208057     SELECT PROCESS-FILES-CTLCD                                           
208057         ASSIGN TO INP04.                                                 
208057                                                                          
           SELECT ITEM-SUMMARY-FILE-OUT                                         
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT ITEM-REG-FILE-OUT                                             
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  TSKST-UNLD-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 34 CHARACTERS.                                       
       01  TSKST-UNLD-REC                  PIC  X(34).                          
                                                                                
       FD  MERGED-INPUT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 102 CHARACTERS.                                      
       01  ITEM-LOC-REC                    PIC  X(102).                         
                                                                                
W38972 FD  EFCLOC-INPUT-FILE                                                    
W38972     RECORDING MODE IS F                                                  
W38972     LABEL RECORDS ARE STANDARD                                           
W38972     BLOCK CONTAINS 0 RECORDS                                             
W38972     RECORD CONTAINS 008 CHARACTERS.                                      
W38972 01  EFC-LOC-REC.                                                         
W38972     05  EFCLOC                      PIC  9(05).                          
W38972     05  FILLER                      PIC  X(01).                          
W38972     05  LOC-TYP-CODE                PIC  X(02).                          
W38972                                                                          
208057 FD  PROCESS-FILES-CTLCD                                                  
208057     RECORDING MODE IS F                                                  
208057     RECORD CONTAINS 80 CHARACTERS                                        
208057     BLOCK CONTAINS 0 RECORDS                                             
208057     DATA RECORD IS PROCESS-FILES-CTLCD-REC.                              
208057 01  PROCESS-FILES-CTLCD-REC         PIC X(80).                           
208057                                                                          
       FD  ITEM-SUMMARY-FILE-OUT                                                
           RECORDING MODE IS F                                                  
RK0906     RECORD CONTAINS 126 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS ITEM-SUMMARY-REC.                                     
RK0906 01  ITEM-SUMMARY-REC                PIC X(126).                          
                                                                                
       FD  ITEM-REG-FILE-OUT                                                    
           RECORDING MODE IS F                                                  
SS0407     RECORD CONTAINS 071 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
SS0407     DATA RECORD IS ITEM-REG-REC.                                         
SS0407 01  ITEM-REG-REC                    PIC X(071).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-Y                    PIC X(1)        VALUE 'Y'.               
           05  PC-PROGRAM-NAME         PIC X(8)        VALUE 'FSKUT132'.        
           05  PC-25                   PIC X(2)        VALUE '25'.              
           05  PC-20                   PIC X(2)        VALUE '20'.              
                                                                                
       01  PS-CORP-DATA.                                                        
           05  PS-ITEM-NMBR              PIC  S9(15) COMP.                      
           05  PS-INTR-UPC-ID            PIC  S9(9) COMP.                       
           05  PS-UPC-STAT-CDE           PIC   X(2).                            
           05  PS-UNT-COST-AMT           PIC  S9(7)V9(3) COMP-3.                
           05  PS-UNT-RTL-AMT            PIC  S9(7)V9(2) COMP-3.                
           05  PS-MEITGP-NBR             PIC  S9(9) COMP.                       
           05  PS-STAT-CHG-DTE           PIC   X(10).                           
           05  PS-SUB-CL-MDSEAR-ID       PIC  S9(6)V COMP-3.                    
           05  PS-VS-ID                  PIC  S9(9) COMP.                       
           05  PS-KOHLS-SIZE-CDE         PIC  S9(9) COMP.                       
           05  PS-CSTM-CLOR-DESC         PIC   X(10).                           
           05  PS-GP-CDE                 PIC   X(3).                            
           05  PS-RPLNMT-IND             PIC   X(1).                            
           05  PS-KY-ITM-IND             PIC   X(1).                            
           05  PS-SEA-SEQ-NBR            PIC  S9(4) COMP.                       
           05  PS-REGN-PRIC-IND          PIC   X(1).                            
           05  PS-INTR-UPC-DESC          PIC   X(30).                           
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME       PIC X(25)       VALUE SPACE.             
           05  PV-ABEND-MESSAGE        PIC X(50)       VALUE SPACE.             
           05  PV-OPER-ATTEMPTED       PIC X(30)       VALUE SPACES.            
           05  PV-UPC-INTR-UPC-ID      PIC S9(9)       VALUE +0.                
           05  PV-TSKST-READ-CNT       PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-MERGED-INPUT-READ-CNT PIC 9(9)      VALUE ZERO                
                                                       COMP-3.                  
W38972     05  PV-EFCLOC-INPUT-READ-CNT PIC 9(9)      VALUE ZERO                
W38972                                                 COMP-3.                  
           05  PV-RECS-WRITTEN-CNT     PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-OUTPUT-MAX           PIC S9(7)      VALUE +100000             
                                                       COMP.                    
           05  PV-OUTPUT-CNT           PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-REG-CNT              PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
W38972     05  PV-EFCLOC-SUB           PIC 9(03)      VALUE ZERO.               
W38972     05  PV-EFCLOC-MAX           PIC 9(02)      VALUE 99.                 
           05  PV-9-4                  PIC 9(04).                               
           05  PV-X-4 REDEFINES PV-9-4 PIC X(04).                               
           05  PV-JOBNAME              PIC X(08)      VALUE SPACES.             
                                                                                
           05  FILLER REDEFINES PV-JOBNAME.                                     
                                                                                
               10  PV-JOBNAME-APPL         PIC X(02).                           
                   88  PV-PROD-JOBS                        VALUES ARE           
                       'DA' 'IN' 'MJ' 'ML' 'MH' 'FS'.                           
               10  FILLER                  PIC X(06).                           
                                                                                
                                                                                
       01  PS-SWITCHES.                                                         
           05  PS-TSKST-UNLD-EOF-SW        PIC X       VALUE 'N'.               
               88  PS-TSKST-UNLD-EOF                   VALUE 'Y'.               
           05  PS-SKU-LOC-EOF-SW           PIC X       VALUE 'N'.               
               88  PS-SKU-LOC-EOF                      VALUE 'Y'.               
W38972     05  PS-EFC-LOC-EOF-SW           PIC X       VALUE 'N'.               
W38972         88  PS-EFC-LOC-EOF                      VALUE 'Y'.               
W38972     05  PS-E-BUS-IND-SW             PIC X       VALUE 'N'.               
W38972         88  PS-E-BUS-IND-NO                     VALUE 'N'.               
W38972         88  PS-E-BUS-IND-YES                    VALUE 'Y'.               
208057     05  PS-CTLCD-EOF-SW             PIC X       VALUE 'N'.               
208057         88  PS-CTLCD-EOF-NO                     VALUE 'N'.               
208057         88  PS-CTLCD-EOF-YES                    VALUE 'Y'.               
W38972                                                                          
W38972 01  TABLES.                                                              
W38972     05  EFC-LOC-ARRAY OCCURS 99 TIMES.                                   
W38972         10  EFC-LOC-NBR                 PIC 9(05).                       
                                                                                
       01  ABEND-CODE                          PIC S9(04)  VALUE +4003          
                                                           COMP SYNC.           
208057*                                                                         
208057******************************************************************        
208057*  INPUT CONTROL CARD RECORD                                              
208057*  CTL-PROGRAM = 'FSKUT132'                                               
208057*  CTL-FILES   = 'PROCESS ALL FILES' (FSK157)                             
208057*             OR 'NOT ALL FILES'     (MHK118)                             
208057******************************************************************        
208057 01  CONTROL-CARD-REC.                                                    
208057     05  CTL-PROGRAM                   PIC X(8)   VALUE SPACES.           
208057     05  FILLER                        PIC X(1)   VALUE SPACES.           
208057     05  CTL-FILES                     PIC X(20)  VALUE SPACES.           
208057     05  FILLER                        PIC X(51)  VALUE SPACES.           
208057                                                                          
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE TSKST UNLOAD FILE                                  
      ******************************************************************        
           COPY FSRD121.                                                        
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE SKULOC DOMAIN/UPC MERGED INPUT FILE                
      ******************************************************************        
           COPY FSRD120.                                                        
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE MERGED OUTPUT FILE - ALL ITEMS                     
      ******************************************************************        
           COPY FSRD122.                                                        
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE SECOND OUTPUT FILE - REGULAR-PRICED ITEMS          
      ******************************************************************        
           COPY FSRD131.                                                        
                                                                                
           EJECT                                                                
       LINKAGE SECTION.                                                         
                                                                                
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* START OF FSKUT132               '.                        
           DISPLAY '**********************************'.                        
                                                                                
           PERFORM 1000-HOUSEKEEPING.                                           
                                                                                
           PERFORM 2000-READ-TSKST-UNLD-FILE.                                   
           IF PS-TSKST-UNLD-EOF                                                 
              DISPLAY '***********************************************'         
              DISPLAY '*** NO SKUS FOUND ***'                                   
              DISPLAY '***********************************************'         
           ELSE                                                                 
              PERFORM 2100-READ-MERGED-INPUT-FILE                               
              IF  FS120-ITM-NBR    = FS121-ITEM-NMBR                            
              AND FS120-STR-NBR    = ZERO                                       
                  PERFORM 2400-SAVE-CORP                                        
              END-IF                                                            
              PERFORM 2200-PROCESS-TSKST-UNLDS                                  
                  UNTIL PS-TSKST-UNLD-EOF                                       
           END-IF.                                                              
                                                                                
           PERFORM 1900-EOJ.                                                    
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* FSKUT132 PROGRAM STATISTICS:    '.                        
           DISPLAY '**********************************'.                        
           DISPLAY '* TOTAL SKUS READ     : ' PV-TSKST-READ-CNT.                
           DISPLAY '* TOTAL ITEM-LOC READ : ' PV-MERGED-INPUT-READ-CNT.         
W38972     DISPLAY '* TOTAL EFC-LOC  READ : ' PV-EFCLOC-INPUT-READ-CNT.         
           DISPLAY '* TOTAL OUTPUT RECORDS: '                                   
                                         PV-RECS-WRITTEN-CNT.                   
           DISPLAY '* TOTAL REG RECORDS   : '                                   
                                         PV-REG-CNT.                            
           DISPLAY '**********************************'.                        
           DISPLAY '**********************************'.                        
           DISPLAY '* END OF FSKUT132                 '.                        
           DISPLAY '**********************************'.                        
                                                                                
           STOP RUN.                                                            
      ******************************************************************        
      *    OPEN FILES & INITIALIZE                                              
      ******************************************************************        
       1000-HOUSEKEEPING.                                                       
                                                                                
           OPEN INPUT  TSKST-UNLD-FILE                                          
                       MERGED-INPUT-FILE                                        
208057                 PROCESS-FILES-CTLCD                                      
                OUTPUT ITEM-SUMMARY-FILE-OUT.                                   
                                                                                
208057     PERFORM 1010-READ-CTLCD.                                             
208057                                                                          
      * THIS FILE IS NOT NEEDED FOR JOB MHK118                                  
208057     IF CTL-FILES  = 'PROCESS ALL FILES'                                  
W38972       OPEN INPUT  EFCLOC-INPUT-FILE                                      
             OPEN OUTPUT ITEM-REG-FILE-OUT                                      
                                                                                
W38972       PERFORM 2150-READ-EFCLOC-FILE                                      
W38972       PERFORM 1100-LOAD-EFC                                              
W38972         UNTIL PS-EFC-LOC-EOF                                             
W38972       MOVE  PV-EFCLOC-SUB    TO PV-EFCLOC-MAX                            
           END-IF.                                                              
W38972                                                                          
           INITIALIZE FS121-FS-TSKST-RECORD.                                    
                                                                                
208057******************************************************************        
208057* LOAD THE CONTROL CARD AND VALIDATE THE PROGRAM ID AND CONTROL           
208057* FILE INDICATOR FOR DETERMINING HOW MANY FILES TO PROCESS.               
208057******************************************************************        
208057 1010-READ-CTLCD.                                                         
208057     READ PROCESS-FILES-CTLCD INTO CONTROL-CARD-REC                       
208057          AT END SET PS-CTLCD-EOF-YES TO TRUE.                            
208057*                                                                         
208057     IF PS-CTLCD-EOF-YES                                                  
208057         DISPLAY '******************************************'             
208057         DISPLAY ' CONTROL CARD RECORD NOT FOUND            '             
208057         DISPLAY '******************************************'             
208057         MOVE '1010-READ-CTLCD' TO PV-PARAGRAPH-NAME                      
208057         MOVE 'CTLCD READ     ' TO PV-OPER-ATTEMPTED                      
208057         MOVE +4003             TO ABEND-CODE                             
208057         PERFORM 9999-ABEND                                               
208057     ELSE                                                                 
208057         DISPLAY 'CTL-PROGRAM = ' CTL-PROGRAM                             
208057         DISPLAY 'CTL-FILES   = ' CTL-FILES                               
208057         IF CTL-PROGRAM NOT = 'FSKUT132'                                  
208057             DISPLAY '**********************************'                 
208057             DISPLAY ' CTLCD PROGRAM ID NOT = FSKUT132  '                 
208057             DISPLAY '**********************************'                 
208057             MOVE '1010-READ-CTLCD' TO PV-PARAGRAPH-NAME                  
208057             MOVE 'VALIDATE CTLCD PROGRAM' TO PV-OPER-ATTEMPTED           
208057             MOVE +4003 TO ABEND-CODE                                     
208057             PERFORM 9999-ABEND                                           
208057         ELSE                                                             
208057             IF  CTL-FILES NOT = 'PROCESS ALL FILES'                      
208057             AND CTL-FILES NOT = 'NOT ALL FILES'                          
208057                 DISPLAY '*************************************'          
208057                 DISPLAY ' CTLCD FILES NOT = PROCESS ALL FILES '          
208057                 DISPLAY '         AND NOT = NOT ALL FILES     '          
208057                 DISPLAY '*************************************'          
208057                 MOVE '1010-READ-CTLCD' TO PV-PARAGRAPH-NAME              
208057                 MOVE 'VALIDATE CTLCD FILES' TO PV-OPER-ATTEMPTED         
208057                 MOVE +4003 TO ABEND-CODE                                 
208057                 PERFORM 9999-ABEND                                       
208057             END-IF                                                       
208057         END-IF                                                           
208057     END-IF.                                                              
                                                                                
W38972******************************************************************        
W38972* LOAD THE EFC LOCATION NUMBER AND THE LOCATION TYPE CODE.                
W38972******************************************************************        
W38972 1100-LOAD-EFC.                                                           
W38972                                                                          
W38972     IF LOC-TYP-CODE = 'DS'                                               
W38972        ADD  +1            TO PV-EFCLOC-SUB                               
W38972        IF PV-EFCLOC-SUB > PV-EFCLOC-MAX                                  
W38972            DISPLAY '******************************************'          
W38972            DISPLAY ' TABLE OVERFLOW ON EFC-TABLE '                       
W38972            DISPLAY ' INCREASE PV-EFCLOC-MAX LIMIT             '          
W38972            DISPLAY ' CURRENT LIMIT = ' PV-EFCLOC-MAX                     
W38972            DISPLAY '******************************************'          
W38972            DISPLAY '  '                                                  
W38972            MOVE '1100-LOAD-EFC' TO PV-PARAGRAPH-NAME                     
W38972            MOVE 'EFC-TABLE LOAD' TO PV-OPER-ATTEMPTED                    
W38972            MOVE +4004 TO ABEND-CODE                                      
W38972            PERFORM 9999-ABEND                                            
W38972        END-IF                                                            
W38972        MOVE EFCLOC        TO EFC-LOC-NBR (PV-EFCLOC-SUB)                 
W38972     END-IF.                                                              
W38972                                                                          
W38972     PERFORM 2150-READ-EFCLOC-FILE.                                       
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
       1900-EOJ.                                                                
                                                                                
           CLOSE TSKST-UNLD-FILE                                                
                 MERGED-INPUT-FILE                                              
208057           PROCESS-FILES-CTLCD                                            
                 ITEM-SUMMARY-FILE-OUT.                                         
                                                                                
      * THIS FILE IS NOT NEEDED FOR JOB MHK118                                  
208057     IF CTL-FILES  = 'PROCESS ALL FILES'                                  
W38972       CLOSE EFCLOC-INPUT-FILE                                            
W38972             ITEM-REG-FILE-OUT                                            
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE TSKST-UNLD-FILE.                            
      ******************************************************************        
       2000-READ-TSKST-UNLD-FILE.                                               
                                                                                
           READ TSKST-UNLD-FILE                                                 
               INTO FS121-FS-TSKST-RECORD                                       
               AT END                                                           
                   SET PS-TSKST-UNLD-EOF TO TRUE                                
               NOT AT END                                                       
                   ADD +1 TO PV-TSKST-READ-CNT.                                 
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE MERGED-INPUT-FILE.                          
      ******************************************************************        
       2100-READ-MERGED-INPUT-FILE.                                             
                                                                                
           READ MERGED-INPUT-FILE                                               
               INTO FS120-FS-MERGED-RECORD                                      
               AT END                                                           
      *            MOVE ZERO TO FS120-ITM-NBR                                   
                   SET PS-SKU-LOC-EOF TO TRUE                                   
               NOT AT END                                                       
                   ADD +1 TO PV-MERGED-INPUT-READ-CNT.                          
W38972******************************************************************        
W38972* READ THE NEXT RECORD ON THE EFCLOC-INPUT-FILE.                          
W38972******************************************************************        
W38972 2150-READ-EFCLOC-FILE.                                                   
W38972                                                                          
W38972     READ EFCLOC-INPUT-FILE                                               
W38972         AT END                                                           
W38972             SET PS-EFC-LOC-EOF TO TRUE                                   
W38972         NOT AT END                                                       
W38972             ADD +1 TO PV-EFCLOC-INPUT-READ-CNT.                          
      ******************************************************************        
      *    PROCESS ALL TSKST ITEMS                                              
      *    IF THERE IS A MATCH BETWEEN THE 2 FILES BUILD AND WRITE A            
      *    RECORD.  IF THERE IS A TSKST ITEM/LOC WITHOUT A MERGE ITEM/          
      *    LOC MATCH BUILD A RECORD USING TSKST AND CORP DATA AND WRITE.        
      *    IF THERE IS A MERGE ITEM/LOC WITHOUT A TSKST, BYPASS.                
      ******************************************************************        
       2200-PROCESS-TSKST-UNLDS.                                                
                                                                                
           IF  FS120-ITM-NBR       = FS121-ITEM-NMBR                            
           AND FS120-STR-NBR       = FS121-LOC-NBR                              
                PERFORM 2500-BUILD-MERGE                                        
                PERFORM 5000-WRITE-OUTPUT-REC                                   
           ELSE                                                                 
               IF ((FS120-ITM-NBR   = FS121-ITEM-NMBR  AND                      
                   FS120-STR-NBR   > FS121-LOC-NBR)                             
               OR (FS120-ITM-NBR   > FS121-ITEM-NMBR)                           
               OR (PS-SKU-LOC-EOF))                                             
               AND FS121-ITEM-NMBR = PS-ITEM-NMBR                               
                   PERFORM 2600-BUILD-CORP                                      
                   PERFORM 5000-WRITE-OUTPUT-REC                                
           END-IF.                                                              
                                                                                
           IF (FS120-ITM-NBR       = FS121-ITEM-NMBR                            
           AND FS120-STR-NBR       = FS121-LOC-NBR)                             
                PERFORM 2100-READ-MERGED-INPUT-FILE                             
                PERFORM 2000-READ-TSKST-UNLD-FILE                               
           ELSE                                                                 
               IF ((FS120-ITM-NBR   = FS121-ITEM-NMBR  AND                      
                   FS120-STR-NBR   > FS121-LOC-NBR)                             
               OR (FS120-ITM-NBR   > FS121-ITEM-NMBR))                          
               OR PS-SKU-LOC-EOF                                                
                   PERFORM 2000-READ-TSKST-UNLD-FILE                            
               ELSE                                                             
                   PERFORM 2100-READ-MERGED-INPUT-FILE                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  FS120-ITM-NBR       = FS121-ITEM-NMBR                            
           AND FS120-STR-NBR       = ZERO                                       
               PERFORM 2400-SAVE-CORP                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    SAVE CORPORATE TSKST DATA                                            
      ******************************************************************        
       2400-SAVE-CORP.                                                          
                                                                                
           MOVE FS120-ITM-NBR              TO PS-ITEM-NMBR.                     
           MOVE FS120-INTR-UPC-ID          TO PS-INTR-UPC-ID.                   
           MOVE FS120-UPC-STAT-CDE         TO PS-UPC-STAT-CDE.                  
           MOVE FS120-UNT-COST-AMT         TO PS-UNT-COST-AMT.                  
           MOVE FS120-UNT-RTL-AMT          TO PS-UNT-RTL-AMT.                   
           MOVE FS120-MEITGP-NBR           TO PS-MEITGP-NBR.                    
           MOVE FS120-STAT-CHG-DTE         TO PS-STAT-CHG-DTE.                  
           MOVE FS120-SUB-CL-MDSEAR-ID     TO PS-SUB-CL-MDSEAR-ID.              
           MOVE FS120-VS-ID                TO PS-VS-ID.                         
           MOVE FS120-KOHLS-SIZE-CDE       TO PS-KOHLS-SIZE-CDE.                
           MOVE FS120-CSTM-CLOR-DESC       TO PS-CSTM-CLOR-DESC.                
           MOVE FS120-GP-CDE               TO PS-GP-CDE.                        
           MOVE FS120-RPLNMT-IND           TO PS-RPLNMT-IND.                    
           MOVE FS120-KY-ITM-IND           TO PS-KY-ITM-IND.                    
           MOVE FS120-SEA-SEQ-NBR          TO PS-SEA-SEQ-NBR.                   
           MOVE FS120-REGN-PRIC-IND        TO PS-REGN-PRIC-IND.                 
           MOVE FS120-INTR-UPC-DESC        TO PS-INTR-UPC-DESC.                 
      ******************************************************************        
      *    MOVE THE UNLOAD AND MERGED-FILE DATA                                 
      ******************************************************************        
       2500-BUILD-MERGE.                                                        
                                                                                
           MOVE FS120-ITM-NBR              TO FS122-ITEM-NMBR.                  
           MOVE FS120-INTR-UPC-ID          TO FS122-INTR-UPC-ID.                
           MOVE FS120-STR-NBR              TO FS122-LOC-NBR.                    
           MOVE FS120-UPC-STAT-CDE         TO FS122-UPC-STAT-CDE.               
           MOVE FS120-UNT-COST-AMT         TO FS122-UNT-COST-AMT.               
           MOVE FS120-UNT-RTL-AMT          TO FS122-UNT-RTL-AMT.                
           MOVE FS120-MEITGP-NBR           TO FS122-MEITGP-NBR.                 
           MOVE FS120-STAT-CHG-DTE         TO FS122-STAT-CHG-DTE.               
           MOVE FS120-SUB-CL-MDSEAR-ID     TO FS122-SUB-CL-MDSEAR-ID.           
           MOVE FS120-VS-ID                TO FS122-VS-ID.                      
           MOVE FS120-KOHLS-SIZE-CDE       TO FS122-KOHLS-SIZE-CDE.             
           MOVE FS120-CSTM-CLOR-DESC       TO FS122-CSTM-CLOR-DESC.             
           MOVE FS120-GP-CDE               TO FS122-GP-CDE.                     
           MOVE FS120-RPLNMT-IND           TO FS122-RPLNMT-IND.                 
           MOVE FS120-KY-ITM-IND           TO FS122-KY-ITM-IND.                 
           MOVE FS120-SEA-SEQ-NBR          TO FS122-SEA-SEQ-NBR.                
           MOVE FS120-REGN-PRIC-IND        TO FS122-REGN-PRIC-IND.              
           MOVE FS120-INTR-UPC-DESC        TO FS122-INTR-UPC-DESC.              
           MOVE FS121-LAST-SALE-DATE       TO FS122-LAST-SALE-DATE.             
           MOVE FS121-LAST-RCVD-DATE       TO FS122-LAST-RCVD-DATE.             
           MOVE FS121-ONHAND-QTY           TO FS122-ONHAND-QTY.                 
           MOVE FS121-FIRST-RCVD-DATE      TO FS122-FIRST-RCVD-DATE.            
           MOVE FS121-FIRST-SALE-DATE      TO FS122-FIRST-SALE-DATE.            
           MOVE FS121-YTD-SALES-UNIT-QTY                                        
             TO FS122-YTD-SALES-UNIT-QTY.                                       
           MOVE PC-Y                       TO FS122-FOUND-ON-TSKST-IND.         
      ******************************************************************        
      *    MOVE THE UNLOAD AND MERGED-FILE DATA                                 
      ******************************************************************        
       2600-BUILD-CORP.                                                         
                                                                                
           MOVE FS121-ITEM-NMBR            TO FS122-ITEM-NMBR.                  
           MOVE PS-INTR-UPC-ID             TO FS122-INTR-UPC-ID.                
           MOVE FS121-LOC-NBR              TO FS122-LOC-NBR.                    
           MOVE PS-UPC-STAT-CDE            TO FS122-UPC-STAT-CDE.               
           MOVE PS-UNT-COST-AMT            TO FS122-UNT-COST-AMT.               
           MOVE PS-UNT-RTL-AMT             TO FS122-UNT-RTL-AMT.                
           MOVE PS-MEITGP-NBR              TO FS122-MEITGP-NBR.                 
           MOVE PS-STAT-CHG-DTE            TO FS122-STAT-CHG-DTE.               
           MOVE PS-SUB-CL-MDSEAR-ID        TO FS122-SUB-CL-MDSEAR-ID.           
           MOVE PS-VS-ID                   TO FS122-VS-ID.                      
           MOVE PS-KOHLS-SIZE-CDE          TO FS122-KOHLS-SIZE-CDE.             
           MOVE PS-CSTM-CLOR-DESC          TO FS122-CSTM-CLOR-DESC.             
           MOVE PS-GP-CDE                  TO FS122-GP-CDE.                     
           MOVE PS-RPLNMT-IND              TO FS122-RPLNMT-IND.                 
           MOVE PS-KY-ITM-IND              TO FS122-KY-ITM-IND.                 
           MOVE PS-SEA-SEQ-NBR             TO FS122-SEA-SEQ-NBR.                
           MOVE PS-REGN-PRIC-IND           TO FS122-REGN-PRIC-IND.              
           MOVE PS-INTR-UPC-DESC           TO FS122-INTR-UPC-DESC.              
           MOVE FS121-LAST-SALE-DATE       TO FS122-LAST-SALE-DATE.             
           MOVE FS121-LAST-RCVD-DATE       TO FS122-LAST-RCVD-DATE.             
           MOVE FS121-ONHAND-QTY           TO FS122-ONHAND-QTY.                 
           MOVE FS121-FIRST-RCVD-DATE      TO FS122-FIRST-RCVD-DATE.            
           MOVE FS121-FIRST-SALE-DATE      TO FS122-FIRST-SALE-DATE.            
           MOVE FS121-YTD-SALES-UNIT-QTY                                        
             TO FS122-YTD-SALES-UNIT-QTY.                                       
           MOVE PC-Y                       TO FS122-FOUND-ON-TSKST-IND.         
           IF FS122-UPC-STAT-CDE = PC-25                                        
               MOVE PC-20                  TO FS122-UPC-STAT-CDE.               
      ******************************************************************        
      *    WRITE SUMMARY RECORDS                                                
      ******************************************************************        
       5000-WRITE-OUTPUT-REC.                                                   
                                                                                
           WRITE ITEM-SUMMARY-REC FROM FS122-FS-MERGED-RECORD.                  
           ADD +1 TO PV-OUTPUT-CNT                                              
                     PV-RECS-WRITTEN-CNT.                                       
                                                                                
      *  THIS LOGIC WILL WRITE A RECORD TO THE SECOND OUTPUT FILE IF            
      *  THIS PROGRAM IS NOT RUNNING AS PART OF JOB MHK118 AND THE              
      *  DATA MEETS THE SELECTION CRITERIA.                                     
208057     IF CTL-FILES  = 'PROCESS ALL FILES'                                  
W38972         PERFORM 5200-GET-E-BUS-IND                                       
W38972*        IF  FS122-LOC-NBR NOT = (0 AND 873)                              
W38972         IF  FS122-LOC-NBR NOT = 0                                        
W38972         AND PS-E-BUS-IND-NO                                              
               AND (FS122-UPC-STAT-CDE  = ('10'                                 
                                        OR '20'                                 
                                        OR '40'                                 
                                        OR '90'))                               
               AND FS122-UNT-RTL-AMT > .01                                      
               AND (FS122-LAST-SALE-DATE > 0                                    
                OR FS122-LAST-RCVD-DATE > 0)                                    
               AND FS122-ONHAND-QTY NOT = 0                                     
                   MOVE FS122-ITEM-NMBR       TO FS131-ITEM-NMBR                
                   MOVE FS122-LOC-NBR         TO FS131-LOC-NBR                  
                   MOVE FS122-INTR-UPC-DESC   TO FS131-INTR-UPC-DESC            
                   MOVE FS122-INTR-UPC-ID     TO FS131-INTR-UPC-ID              
                   MOVE FS122-UPC-STAT-CDE    TO FS131-UPC-STAT-CDE             
                   MOVE FS122-LAST-SALE-DATE  TO FS131-LAST-SALE-DATE           
                   MOVE FS122-LAST-RCVD-DATE  TO FS131-LAST-RCVD-DATE           
                   MOVE FS122-ONHAND-QTY      TO FS131-ONHAND-QTY               
                   MOVE FS122-VS-ID           TO FS131-VS-ID                    
SS0407             MOVE FS122-UNT-RTL-AMT     TO FS131-UNT-RTL-AMT              
SS0407             MOVE FS122-KOHLS-SIZE-CDE  TO FS131-KOHLS-SIZE-CDE           
                   PERFORM 5010-WRITE-REG-REC                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    WRITE A RECORD TO THE SECOND OUTPUT FILE OF REGULAR-PRICED           
      *    ITEMS ONLY.                                                          
      ******************************************************************        
       5010-WRITE-REG-REC.                                                      
                                                                                
           WRITE ITEM-REG-REC FROM FS131-FS-MERGED-RECORD.                      
           ADD +1 TO PV-REG-CNT.                                                
                                                                                
W38972*****************************************************************         
W38972*    SEARCH WHETHER THE GIVEN LOCATION IS A EFC LOCATION OR NOT.          
W38972*****************************************************************         
W38972 5200-GET-E-BUS-IND.                                                      
W38972                                                                          
W38972     SET PS-E-BUS-IND-NO     TO TRUE.                                     
W38972     PERFORM 5210-LOOP-EFC-LOC VARYING PV-EFCLOC-SUB FROM                 
W38972             1 BY 1 UNTIL PV-EFCLOC-SUB > PV-EFCLOC-MAX                   
W38972                       OR PS-E-BUS-IND-YES.                               
W38972                                                                          
W38972*****************************************************************         
W38972*    SET THE E-BUS-IND FLAG IF THE LOCATION NUMBER MATCHES WITH           
W38972*    THE ENTRY IN THE ARRAY.                                              
W38972*****************************************************************         
W38972 5210-LOOP-EFC-LOC.                                                       
W38972                                                                          
W38972     IF EFC-LOC-NBR (PV-EFCLOC-SUB) = FS122-LOC-NBR                       
W38972        SET  PS-E-BUS-IND-YES   TO TRUE                                   
W38972     END-IF.                                                              
W38972                                                                          
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPER-ATTEMPTED.                    
           DISPLAY '**********************************'.                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
