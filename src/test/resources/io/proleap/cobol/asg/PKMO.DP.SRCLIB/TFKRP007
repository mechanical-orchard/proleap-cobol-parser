      ******************************************************************00010049
       IDENTIFICATION DIVISION.                                         00020049
      ******************************************************************00030049
                                                                        00040049
       PROGRAM-ID.              TFKRP007.                               00050049
       AUTHOR.                  C PARMLEY.                              00060049
       INSTALLATION.            KOHLS DEPARTMENT STORES.                00070049
       DATE-WRITTEN.            07/14/2011.                             00080049
       DATE-COMPILED.                                                   00090049
                                                                        00100049
      ******************************************************************00110049
      * PROGRAM DESCRIPTION:                                            00120049
      * --------------------                                            00130049
      * GENCO DAILY TRANSFER POSITIVE RECEIPT REPORT                    00140049
      *                                                                 00150049
      * PROGRAM OBJECTIVE:                                              00160049
      * ------------------                                              00170049
      * SEQUENTIALLY READ DATA CREATED IN PROGRAM TFKUT008 AND CREATE   00180049
      * A REPORT OF POSITIVE RECEIVED TRANSFERS.                        00190049
      *                                                                 00200049
      * WILL DETERMINE HOW MANY RTV LOCATIONS AND PRINT FOR EACH        00210049
      * RTV LOCATION. SUMMARY AT END.                                   00220049
      ******************************************************************00230049
      * CHANGE LOG:                                                     00240049
      * -----------                                                     00250049
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00260049
      * ------- ---------- ----------- -------------------------------- 00270049
      * 40189   07-14-2011 C PARMLEY   ORIGINAL IMPLEMENTATION          00280049
      ******************************************************************00290049
                                                                        00300049
      ******************************************************************00310049
       ENVIRONMENT DIVISION.                                            00320049
      ******************************************************************00330049
                                                                        00340049
      ******************************************************************00350049
       CONFIGURATION SECTION.                                           00360049
                                                                        00370049
      ******************************************************************00380049
       INPUT-OUTPUT SECTION.                                            00390049
                                                                        00400049
      ******************************************************************00410049
       FILE-CONTROL.                                                    00420049
                                                                        00430049
           SELECT POSITIVE-XFER-DTL                                     00440049
               ASSIGN TO INP01.                                         00450049
                                                                        00460049
           SELECT CONTROL-CARD-DTE                                      00470049
                  ASSIGN TO INP02.                                      00480049
                                                                        00490049
           SELECT POSITIVE-XFER-RPT                                     00500049
               ASSIGN TO RPT01.                                         00510049
                                                                        00520049
      /*****************************************************************00530049
       DATA DIVISION.                                                   00540049
      ******************************************************************00550049
                                                                        00560049
      ******************************************************************00570049
       FILE SECTION.                                                    00580049
                                                                        00590049
       FD  POSITIVE-XFER-DTL                                            00600049
           RECORDING MODE IS F                                          00610049
           BLOCK CONTAINS 0 RECORDS.                                    00620049
                                                                        00630049
       01  POSITIVE-XFER-DTL-RCD       PIC  X(200).                     00640049
                                                                        00650049
       FD  CONTROL-CARD-DTE                                             00660049
           RECORDING MODE F                                             00670049
           LABEL RECORDS ARE STANDARD                                   00680049
           RECORD CONTAINS 80 CHARACTERS                                00690049
           BLOCK CONTAINS 0 RECORDS                                     00700049
           DATA RECORD IS CNTRL-CARD-DTE-REC.                           00710049
                                                                        00720049
       01  CNTRL-CARD-DTE-REC          PIC  X(80).                      00730049
                                                                        00740049
       FD  POSITIVE-XFER-RPT                                            00750049
           RECORDING MODE IS F                                          00760049
           BLOCK CONTAINS 0 RECORDS.                                    00770049
                                                                        00780049
       01  POSITIVE-XFER-RPT-RCD       PIC  X(133).                     00790049
                                                                        00800049
      ******************************************************************00810049
       WORKING-STORAGE SECTION.                                         00820049
                                                                        00830049
      ******************************************************************00840049
       01  PS-PROGRAM-SWITCHES.                                         00850049
           05  PS-POSITIVE-XFER-DTL-EOF-SW                              00860049
                                       PIC  X(01)          VALUE 'N'.   00870049
               88  PS-POSITIVE-XFER-DTL-EOF                VALUE 'Y'.   00880049
               88  PS-NOT-POSITIVE-XFER-DTL-EOF            VALUE 'N'.   00890049
           05  PS-END-OF-CNTL-CRD-SW       PIC X(01).                   00900049
               88  PS-END-OF-CNTL-CRD-FILE              VALUE 'Y'.      00910049
               88  PS-NOT-END-OF-CNTL-CRD-FILE          VALUE 'N'.      00920049
           05  PS-IS-RTV-LOCS-SW           PIC X(01).                   00930049
               88  PS-RTV-LOCS-AT-END                   VALUE 'Y'.      00940049
               88  PS-NOT-RTV-LOCS-AT-END               VALUE 'N'.      00950049
                                                                        00960049
      ******************************************************************00970049
       01  PC-PROGRAM-CONSTANTS.                                        00980049
           05  PC-PROGRAM-NAME         PIC  X(08)          VALUE        00990049
               'TFKRP007'.                                              01000049
           05  PC-LINE-MAX             PIC S9(03)          VALUE +66    01010049
                                                           COMP-3.      01020049
           05  PC-ALL                  PIC  X(04)          VALUE 'ALL'. 01030049
           05  PC-MINUS                PIC  X(01)          VALUE '-'.   01040049
                                                                        01050049
      ******************************************************************01060049
       01  PT-PROGRAM-TOTALS.                                           01070049
           05  PT-FIRSTXFER-RCD-FND    PIC S9(09)          VALUE ZEROS  01080049
                                                           COMP-3.      01090049
           05  PT-RCD-WRTTN            PIC S9(09)          VALUE ZEROS  01100049
                                                           COMP-3.      01110049
                                                                        01120049
      ******************************************************************01130049
       01  PV-PROGRAM-VARIABLES.                                        01140049
           05  PV-PARAGRAPH-NAME       PIC  X(25)          VALUE SPACE. 01150049
           05  PV-ABEND-MESSAGE        PIC  X(50)          VALUE SPACE. 01160049
           05  PV-OPER-ATTEMPTED       PIC  X(30)          VALUE SPACE. 01170049
           05  PV-PREV-FR-LOC-NBR      PIC S9(04)          VALUE +0     01180049
                                                           COMP.        01190049
           05  PV-PREV-TO-LOC-NBR      PIC S9(04)          VALUE +0     01200049
                                                           COMP.        01210049
           05  PV-PREV-XFER-QTY-TOTAL  PIC S9(09)          VALUE +0     01220049
                                                           COMP.        01230049
           05  PV-PREV-XFER-RTL-TOTAL  PIC S9(09)V99       VALUE +0     01240049
                                                           COMP-3.      01250049
                                                                        01260049
           05  PV-LINE-COUNT           PIC S9(03)          VALUE +67    01270049
                                                           COMP-3.      01280049
           05  PV-PAGE-COUNT           PIC S9(05)          VALUE +0     01290049
                                                           COMP-3.      01300049
           05  PV-TOTAL-TRANS          PIC S9(09)          VALUE +0     01310049
                                                           COMP.        01320049
           05  PV-TOTAL-QTY            PIC S9(09)V99       VALUE +0     01330049
                                                           COMP-3.      01340049
           05  PV-TOTAL-RTL            PIC S9(09)V99       VALUE +0     01350049
                                                           COMP-3.      01360049
           05  PV-TOTAL-TRANS-ALL      PIC S9(09)          VALUE +0     01370049
                                                           COMP.        01380049
           05  PV-TOTAL-QTY-ALL        PIC S9(09)V99       VALUE +0     01390049
                                                           COMP-3.      01400049
           05  PV-TOTAL-RTL-ALL        PIC S9(09)V99       VALUE +0     01410049
                                                           COMP-3.      01420049
                                                                        01430049
                                                                        01440049
      ******************************************************************01450049
       01  ABEND-CODE                   PIC S9(04)  VALUE +0  COMP SYNC.01460049
           88  AC-DB2-ERROR                         VALUE +4013.        01470049
           88  AC-NON-DB2-ERROR                     VALUE +4003.        01480049
           88  AC-DATE-ROUTINE-ERROR                VALUE +4019.        01490049
      ******************************************************************01500049
                                                                        01510049
       01  SDT-SYSTEM-DATE-TIME.                                        01520049
           05  SDT-YYYYMMDD            PIC  9(08)          VALUE ZEROS. 01530049
           05  FILLER REDEFINES SDT-YYYYMMDD.                           01540049
               10  SDT-YYYYMMDD-CCYY.                                   01550049
                   15  SDT-YYYYMMDD-CC PIC  X(02).                      01560049
                   15  SDT-YYYYMMDD-YY PIC  X(02).                      01570049
               10  SDT-YYYYMMDD-MM     PIC  X(02).                      01580049
               10  SDT-YYYYMMDD-DD     PIC  X(02).                      01590049
           05  SDT-HHMMSS              PIC  9(08)          VALUE ZEROS. 01600049
           05  FILLER REDEFINES SDT-HHMMSS.                             01610049
               10  SDT-HHMMSS-HH       PIC  X(02).                      01620049
               10  SDT-HHMMSS-MM       PIC  X(02).                      01630049
               10  SDT-HHMMSS-SS       PIC  X(02).                      01640049
               10  FILLER              PIC  X(02).                      01650049
                                                                        01660049
      ******************************************************************01670049
      **DATE LAYOUT                                                     01680049
      ******************************************************************01690049
       01  DB2-DATE.                                                    01700049
           05  DB2-DATE-CCYY           PIC X(04).                       01710049
           05  FILLER                  PIC X(01) VALUE '-'.             01720049
           05  DB2-DATE-MM             PIC X(02).                       01730049
           05  FILLER                  PIC X(01) VALUE '-'.             01740049
           05  DB2-DATE-DD             PIC X(02).                       01750049
                                                                        01760049
      *----------------------------------------------------------------*01770049
      *    COPYBOOK LAYOUT FOR THE CALENDAR ROUTINE                    *01780049
      *----------------------------------------------------------------*01790049
           COPY DPWS500.                                                01800049
                                                                        01810049
      *----------------------------------------------------------------*01820049
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *01830049
      *----------------------------------------------------------------*01840049
           COPY DPWS004.                                                01850049
                                                                        01860049
      *----------------------------------------------------------------*01870049
      *  POSITIVE XFER DETAIL                                           01880049
      *----------------------------------------------------------------*01890049
           COPY TFRD065.                                                01900049
                                                                        01910049
      *----------------------------------------------------------------*01920049
      *    COPYBOOK LAYOUT FOR THE CONTROL CARD                        *01930049
      *----------------------------------------------------------------*01940049
           COPY TFRD069.                                                01950049
                                                                        01960049
      *----------------------------------------------------------------*01970049
      * DB2 MESSAGE AREA                                                01980049
      *----------------------------------------------------------------*01990049
           COPY SQLCA2.                                                 02000049
                                                                        02010049
      *----------------------------------------------------------------*02020049
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                 02030049
      *----------------------------------------------------------------*02040049
           EXEC SQL                                                     02050049
               INCLUDE SQLCA                                            02060049
           END-EXEC.                                                    02070049
                                                                        02080049
      *----------------------------------------------------------------*02090049
      * XST_LOC                                                         02100049
      *----------------------------------------------------------------*02110049
           EXEC SQL                                                     02120049
               INCLUDE XST00001                                         02130049
           END-EXEC.                                                    02140049
                                                                        02150049
      *----------------------------------------------------------------*02160049
      * XST_RTV_LOC_RLTNSHP                                             02170049
      *----------------------------------------------------------------*02180049
           EXEC SQL                                                     02190049
               INCLUDE XST00135                                         02200049
           END-EXEC.                                                    02210049
                                                                        02220049
      *----------------------------------------------------------------*02230049
      * CURSOR FOR GETTING RTV LOCATIONS FROM XST_RTV_LOC_RLTNSHP       02240049
      *----------------------------------------------------------------*02250049
                                                                        02260049
           EXEC SQL                                                     02270049
                DECLARE RTV-LOCS-CSR CURSOR FOR                         02280049
                   SELECT DISTINCT A.RTV_LOC_NBR                        02290049
                       FROM  XST_RTV_LOC_RLTNSHP A                      02300049
                        WHERE A.EFF_BEG_DTE = (SELECT MAX(EFF_BEG_DTE)  02310049
                        FROM  XST_RTV_LOC_RLTNSHP B                     02320049
                          WHERE A.RTV_LOC_NBR = B.RTV_LOC_NBR)          02330049
                        WITH UR                                         02340049
           END-EXEC.                                                    02350049
                                                                        02360049
      ******************************************************************02370049
      * COPY FOR STANDARD HEADING                                       02380049
      ******************************************************************02390049
           COPY DPWS132O.                                               02400049
                                                                        02410049
      ******************************************************************02420049
       01  PV-RTV-LOC-TBL.                                              02430049
           10  PV-RTV-LOC-ENTRIES OCCURS 10 TIMES.                      02440049
               15  PV-RTV-LOC             PIC 9(04).                    02450049
               15  PV-RTV-TRANS           PIC 9(09).                    02460049
               15  PV-RTV-QTY             PIC 9(09).                    02470049
               15  PV-RTV-TOT-RTL         PIC 9(09)V99.                 02480049
       01  SUB                            PIC 9(02) VALUE ZERO.         02490049
       01  MAX-SUB                        PIC 9(02) VALUE 10.           02500049
       01  NO-RTVS                        PIC 9(02) VALUE ZERO.         02510049
                                                                        02520049
      ******************************************************************02530049
       01  RHL1-RPT-HEAD-LINE1.                                         02540049
           05  FILLER                  PIC  X(43)          VALUE SPACE. 02550049
           05  FILLER                  PIC  X(50)          VALUE        02560049
               'GENCO DAILY TRANSFER POSITIVE RECEIPT REPORT FOR '.     02570049
           05  RHL1-RTV-LOC            PIC  X(04)          VALUE SPACE. 02580049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 02590049
           05  RHL1-RTV-NM             PIC  X(20)          VALUE SPACE. 02600049
                                                                        02610049
      ******************************************************************02620049
       01  RHL2-RPT-HEAD-LINE2.                                         02630049
           05  FILLER                  PIC  X(49)          VALUE SPACE. 02640049
           05  FILLER                  PIC  X(11)          VALUE        02650049
               'FOR DATE  '.                                            02660049
           05  RHL2-DTE-BEGIN          PIC  X(10)          VALUE SPACE. 02670049
                                                                        02680049
      ******************************************************************02690049
       01  RHL3-RPT-HEAD-LINE3.                                         02700049
           05  FILLER                  PIC  X(05)          VALUE        02710049
               '  TO '.                                                 02720049
           05  FILLER                  PIC  X(23)          VALUE SPACE. 02730049
           05  FILLER                  PIC  X(08)          VALUE        02740049
               'TRANSFER'.                                              02750049
           05  FILLER                  PIC  X(04)          VALUE SPACE. 02760049
           05  FILLER                  PIC  X(08)          VALUE        02770049
               'CALLBACK'.                                              02780049
           05  FILLER                  PIC  X(03)          VALUE SPACE. 02790049
           05  FILLER                  PIC  X(06)          VALUE        02800049
               'CREATE'.                                                02810049
           05  FILLER                  PIC  X(06)          VALUE SPACE. 02820054
           05  FILLER                  PIC  X(08)          VALUE        02830049
               'RECEIVED'.                                              02840049
           05  FILLER                  PIC  X(03)          VALUE SPACE. 02850055
           05  FILLER                  PIC  X(05)          VALUE        02860049
               'TOTAL'.                                                 02870049
           05  FILLER                  PIC  X(10)          VALUE SPACE. 02880049
           05  FILLER                  PIC  X(05)          VALUE        02890049
               'TOTAL'.                                                 02900049
                                                                        02910049
      ******************************************************************02920049
       01  RHL4-RPT-HEAD-LINE4.                                         02930049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 02940049
           05  FILLER                  PIC  X(03)          VALUE        02950049
               'LOC'.                                                   02960049
           05  FILLER                  PIC  X(02)          VALUE SPACE. 02970049
           05  FILLER                  PIC  X(13)          VALUE        02980049
               'TRANSFER TYPE'.                                         02990049
           05  FILLER                  PIC  X(11)          VALUE SPACE. 03000049
           05  FILLER                  PIC  X(02)          VALUE        03010049
               'NO'.                                                    03020049
           05  FILLER                  PIC  X(09)          VALUE SPACE. 03030049
           05  FILLER                  PIC  X(02)          VALUE        03040049
               'NO'.                                                    03050049
           05  FILLER                  PIC  X(08)          VALUE SPACE. 03060049
           05  FILLER                  PIC  X(05)          VALUE        03070049
               'DATE'.                                                  03080049
           05  FILLER                  PIC  X(08)          VALUE SPACE. 03090054
           05  FILLER                  PIC  X(04)          VALUE        03100049
               'DATE'.                                                  03110049
           05  FILLER                  PIC  X(06)          VALUE SPACE. 03120055
           05  FILLER                  PIC  X(08)          VALUE        03130049
               'QUANTITY'.                                              03140049
           05  FILLER                  PIC  X(06)          VALUE SPACE. 03150049
           05  FILLER                  PIC  X(06)          VALUE        03160055
               'RETAIL'.                                                03170049
           05  FILLER                  PIC  X(02)          VALUE SPACE. 03180049
           05  FILLER                  PIC  X(11)          VALUE        03190049
               'RECEIVED BY'.                                           03200049
                                                                        03210049
      ******************************************************************03220049
       01  RHL5-RPT-HEAD-LINE5.                                         03230049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03240049
           05  FILLER                  PIC  X(04)          VALUE        03250049
               '----'.                                                  03260049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03270049
           05  FILLER                  PIC  X(20)          VALUE        03280049
               '--------------------'.                                  03290049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03300049
           05  FILLER                  PIC  X(11)          VALUE        03310049
               '-----------'.                                           03320049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03330049
           05  FILLER                  PIC  X(08)          VALUE        03340049
               '--------'.                                              03350049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03360049
           05  FILLER                  PIC  X(10)          VALUE        03370049
               '----------'.                                            03380049
           05  FILLER                  PIC  X(04)          VALUE SPACE. 03390054
           05  FILLER                  PIC  X(10)          VALUE        03400049
               '----------'.                                            03410049
           05  FILLER                  PIC  X(02)          VALUE SPACE. 03420049
           05  FILLER                  PIC  X(08)          VALUE        03430049
               '--------'.                                              03440049
           05  FILLER                  PIC  X(04)          VALUE SPACE. 03450049
           05  FILLER                  PIC  X(09)          VALUE        03460049
               '-------- '.                                             03470054
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03480049
           05  FILLER                  PIC  X(11)          VALUE        03490049
               '-----------'.                                           03500049
                                                                        03510049
      ******************************************************************03520049
      *  REPORT DETAIL                                                  03530049
      ******************************************************************03540049
       01  XRDL-XFER-RPT-DTL-LINE.                                      03550049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03560049
           05  XRDL-TO-LOC-NBR         PIC  9(04)          VALUE ZEROS. 03570049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03580049
           05  XRDL-TRANS-TYPE         PIC  X(20)          VALUE SPACE. 03590049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03600049
           05  XRDL-STR-NBR            PIC  9(04)          VALUE ZEROS. 03610049
           05  FILLER                  PIC  X(01)          VALUE '-'.   03620049
           05  XRDL-SEQ-NBR            PIC  9(06)          VALUE ZEROS. 03630049
           05  FILLER                  PIC  X(03)          VALUE SPACE. 03640049
           05  XRDL-CLBK-NBR           PIC  X(04)          VALUE SPACE. 03650049
           05  FILLER                  PIC  X(03)          VALUE SPACE. 03660049
           05  XRDL-DTE-CMPLT-XFER     PIC  X(10)          VALUE SPACE. 03670049
           05  FILLER                  PIC  X(04)          VALUE SPACE. 03680054
           05  XRDL-XFER-DTE-RCVD      PIC  X(10)          VALUE SPACE. 03690049
           05  FILLER                  PIC  X(02)          VALUE SPACE. 03700049
           05  XRDL-TOTAL-QTY          PIC  Z(06)9(01)     VALUE ZEROS. 03710049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03720049
           05  XRDL-TOTAL-RTL          PIC  Z(08)9(01).99  VALUE ZEROS. 03730057
           05  XRDL-MINUS              PIC  X(01)          VALUE SPACE. 03740049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03750057
           05  XRDL-RCVD-1ST-INIT      PIC  X(01)          VALUE SPACE. 03760049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03770049
           05  XRDL-RCVD-LAST-NM       PIC  X(20)          VALUE SPACE. 03780049
      ******************************************************************03790049
      *  REPORT TOTALS                                                  03800049
      ******************************************************************03810049
       01  XRDL-XFER-RPT-TTL-LINE.                                      03820049
           05  FILLER                  PIC  X(10)          VALUE SPACE. 03830049
           05  XRDL-TTL-TO-LOC         PIC  9(04)          VALUE ZEROS. 03840049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03850049
           05  FILLER                  PIC  X(08)          VALUE        03860049
               'TOTALS: '.                                              03870049
           05  XRDL-TTL-TOTAL-TRANS    PIC  Z(06)9(01)     VALUE ZEROS. 03880049
           05  FILLER                  PIC  X(44)          VALUE SPACE. 03890054
           05  XRDL-TTL-TOTAL-QTY      PIC  Z(06)9(01)     VALUE ZEROS. 03900049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 03910049
           05  XRDL-TTL-TOTAL-RTL      PIC  Z(08)9(01).99- VALUE ZEROS. 03920057
                                                                        03930049
       01  RHL1-RPT-SUM-LINE1.                                          03940049
           05  FILLER                  PIC  X(43)          VALUE SPACE. 03950049
           05  FILLER                  PIC  X(47)          VALUE        03960049
               'GENCO DAILY TRANSFER POSITIVE RECEIPT REPORT - '.       03970049
           05  FILLER                  PIC  X(15)          VALUE        03980051
               '   SUMMARY PAGE'.                                       03990051
                                                                        04000049
      ******************************************************************04010049
       01  RHL1-RPT-SUM-LINE2.                                          04020049
           05  FILLER                  PIC  X(17)          VALUE SPACE. 04030049
           05  FILLER                  PIC  X(05)          VALUE        04040049
               'TOTAL'.                                                 04050049
           05  FILLER                  PIC  X(05)          VALUE SPACE. 04060049
           05  FILLER                  PIC  X(05)          VALUE        04070049
               'TOTAL'.                                                 04080049
           05  FILLER                  PIC  X(13)          VALUE SPACE. 04090060
           05  FILLER                  PIC  X(05)          VALUE        04100049
               'TOTAL'.                                                 04110049
      ******************************************************************04120049
       01  RHL1-RPT-SUM-LINE3.                                          04130049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 04140049
           05  FILLER                  PIC  X(12)          VALUE        04150049
               'RTV LOCATION'.                                          04160049
           05  FILLER                  PIC  X(03)          VALUE SPACE. 04170049
           05  FILLER                  PIC  X(09)          VALUE        04180049
               'TRANSFERS'.                                             04190049
           05  FILLER                  PIC  X(02)          VALUE SPACE. 04200049
           05  FILLER                  PIC  X(08)          VALUE        04210049
               'QUANTITY'.                                              04220049
           05  FILLER                  PIC  X(09)          VALUE SPACE. 04230060
           05  FILLER                  PIC  X(06)          VALUE        04240049
               'RETAIL'.                                                04250049
           05  FILLER                  PIC  X(02)          VALUE SPACE. 04260049
      ******************************************************************04270049
       01  RHL1-RPT-SUM-LINE4.                                          04280049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 04290049
           05  FILLER                  PIC  X(12)          VALUE        04300049
               '------------'.                                          04310049
           05  FILLER                  PIC  X(03)          VALUE SPACE. 04320049
           05  FILLER                  PIC  X(09)          VALUE        04330049
               '---------'.                                             04340049
           05  FILLER                  PIC  X(02)          VALUE SPACE. 04350049
           05  FILLER                  PIC  X(08)          VALUE        04360049
               '--------'.                                              04370049
           05  FILLER                  PIC  X(07)          VALUE SPACE. 04380060
           05  FILLER                  PIC  X(08)          VALUE        04390060
               '--------'.                                              04400057
           05  FILLER                  PIC  X(02)          VALUE SPACE. 04410049
                                                                        04420049
       01  XRDL-XFER-SUM-DTL-LINE.                                      04430049
           05  FILLER                  PIC  X(01)          VALUE SPACE. 04440049
           05  XRDL-SUM-TO-LOC-NBR     PIC  X(04)          VALUE SPACE. 04450049
           05  FILLER                  PIC  X(12)          VALUE SPACE. 04460059
           05  XRDL-SUM-TOTAL-TRANS    PIC  Z(06)9(01)     VALUE ZEROS. 04470049
           05  FILLER                  PIC  X(03)          VALUE SPACE. 04480049
           05  XRDL-SUM-TOTAL-QTY      PIC  Z(06)9(01)     VALUE ZEROS. 04490049
           05  FILLER                  PIC  X(03)          VALUE SPACE. 04500058
           05  XRDL-SUM-TOTAL-RTL      PIC  Z(08)9(01).99- VALUE ZEROS. 04510057
      ******************************************************************04520049
       LINKAGE SECTION.                                                 04530049
                                                                        04540049
      /*****************************************************************04550049
       PROCEDURE DIVISION.                                              04560049
      ******************************************************************04570049
                                                                        04580049
      ******************************************************************04590049
      *  MAIN PROCESSING.                                               04600049
      ******************************************************************04610049
       0000-MAIN-MODULE.                                                04620049
                                                                        04630049
           DISPLAY '**********************************'.                04640049
           DISPLAY '* START OF TFKRP007               '.                04650049
           DISPLAY '**********************************'.                04660049
                                                                        04670049
           PERFORM 0100-INITIALIZE.                                     04680049
                                                                        04690049
           PERFORM 1000-PROCESS-XFER-DTL                                04700049
             UNTIL PS-POSITIVE-XFER-DTL-EOF.                            04710049
                                                                        04720049
           PERFORM 8240-WRITE-TOTAL-LINE.                               04730049
                                                                        04740049
           PERFORM 8250-WRITE-SUMMARY-PAGE.                             04750049
                                                                        04760049
           PERFORM 8900-CLOSE-FILES.                                    04770049
                                                                        04780049
           PERFORM 9000-DISPLAY-PROGRAM-TOTALS.                         04790049
                                                                        04800049
           GOBACK.                                                      04810049
                                                                        04820049
      ******************************************************************04830049
      *                                                                 04840049
      ******************************************************************04850049
       0100-INITIALIZE.                                                 04860049
                                                                        04870049
           ACCEPT SDT-YYYYMMDD         FROM DATE YYYYMMDD.              04880049
           ACCEPT SDT-HHMMSS           FROM TIME.                       04890049
                                                                        04900049
           DISPLAY 'TFKRP007 RUN DATE: ' SDT-YYYYMMDD ' - '             04910049
              SDT-HHMMSS.                                               04920049
                                                                        04930049
           MOVE SDT-YYYYMMDD-YY        TO DP132O-RUN-YEAR.              04940049
           MOVE SDT-YYYYMMDD-MM        TO DP132O-RUN-MONTH.             04950049
           MOVE SDT-YYYYMMDD-DD        TO DP132O-RUN-DAY.               04960049
           MOVE SDT-HHMMSS-HH          TO DP132O-RUN-HOUR.              04970049
           MOVE SDT-HHMMSS-MM          TO DP132O-RUN-MINUTE.            04980049
           MOVE 'TFKRP007'             TO DP132O-PROGRAM-NAME.          04990049
           MOVE 1                      TO DP132O-REPORT-NUMBER.         05000049
                                                                        05010049
           PERFORM 2100-OPEN-FILES.                                     05020049
                                                                        05030049
           PERFORM 2200-READ-POSITIVE-XFER-DTL.                         05040049
                                                                        05050049
           IF NOT PS-POSITIVE-XFER-DTL-EOF                              05060049
               INITIALIZE DB2-DATE                                      05070049
               MOVE TF065-TRANSFER-DATE(1:4) TO DB2-DATE-CCYY           05080049
               MOVE TF065-TRANSFER-DATE(5:2) TO DB2-DATE-MM             05090049
               MOVE TF065-TRANSFER-DATE(7:2) TO DB2-DATE-DD             05100049
               MOVE DB2-DATE                 TO RHL2-DTE-BEGIN          05110049
           ELSE                                                         05120049
               PERFORM 8300-READ-CURR-DTE-CARD                          05130049
           END-IF.                                                      05140049
                                                                        05150049
           PERFORM 2300-INITIALIZE-TABLE VARYING SUB                    05160049
               FROM 1 BY 1 UNTIL SUB > MAX-SUB.                         05170049
                                                                        05180049
           MOVE ZERO TO SUB.                                            05190049
                                                                        05200049
           PERFORM 3000-OPEN-RTV-CURSOR.                                05210049
           PERFORM 3100-FETCH-RTV-LOCS                                  05220049
                   UNTIL PS-RTV-LOCS-AT-END OR SUB > MAX-SUB.           05230049
           PERFORM 3200-CLOSE-RTV-CURSOR.                               05240049
      ******************************************************************05250049
      /*****************************************************************05260049
      *                                                                 05270049
      ******************************************************************05280049
       1000-PROCESS-XFER-DTL.                                           05290049
                                                                        05300049
           IF TF065-TO-STR-NBR NOT = PV-PREV-TO-LOC-NBR                 05310049
               PERFORM 8210-WRITE-DTL-BREAK-LINE                        05320049
               MOVE TF065-TO-STR-NBR   TO PV-PREV-TO-LOC-NBR            05330049
           END-IF.                                                      05340049
                                                                        05350049
           PERFORM 2000-FMT-XFER-RPT-RCD.                               05360049
                                                                        05370049
           PERFORM 2200-READ-POSITIVE-XFER-DTL.                         05380049
                                                                        05390049
      ******************************************************************05400049
      * MOVE RECORD TO OUTPUT                                           05410049
      ******************************************************************05420049
       2000-FMT-XFER-RPT-RCD.                                           05430049
                                                                        05440049
           INITIALIZE                  XRDL-XFER-RPT-DTL-LINE.          05450049
           MOVE TF065-TO-STR-NBR         TO XRDL-TO-LOC-NBR.            05460049
           MOVE TF065-TRANSFER-TYPE-DESC TO XRDL-TRANS-TYPE.            05470049
           MOVE TF065-TRANSFER-NBR-STR   TO XRDL-STR-NBR.               05480049
           MOVE TF065-TRANSFER-NBR-SEQ   TO XRDL-SEQ-NBR.               05490049
           MOVE TF065-CALLBK-NBR       TO XRDL-CLBK-NBR.                05500049
           MOVE TF065-CREATE-DTE       TO XRDL-DTE-CMPLT-XFER.          05510049
           MOVE TF065-RECVD-DTE        TO XRDL-XFER-DTE-RCVD.           05520049
           MOVE TF065-TOTAL-QTY        TO XRDL-TOTAL-QTY.               05530049
           MOVE TF065-TOTAL-RETAIL     TO XRDL-TOTAL-RTL.               05540049
           MOVE TF065-RCVD-BY-1ST-INIT TO XRDL-RCVD-1ST-INIT.           05550049
           MOVE TF065-RCVD-BY-LAST-NAME TO XRDL-RCVD-LAST-NM.           05560049
           ADD +1                       TO PV-TOTAL-TRANS               05570049
                                           PV-TOTAL-TRANS-ALL.          05580049
           ADD TF065-TOTAL-QTY          TO PV-TOTAL-QTY                 05590049
                                           PV-TOTAL-QTY-ALL.            05600049
           IF TF065-AMT-SIGN = PC-MINUS                                 05610049
               MOVE PC-MINUS            TO XRDL-MINUS                   05620049
               SUBTRACT TF065-TOTAL-RETAIL FROM PV-TOTAL-RTL            05630049
                                                PV-TOTAL-RTL-ALL        05640049
           ELSE                                                         05650049
               ADD      TF065-TOTAL-RETAIL TO   PV-TOTAL-RTL            05660049
                                                PV-TOTAL-RTL-ALL        05670049
           END-IF.                                                      05680049
                                                                        05690049
           PERFORM 3300-ACCUM-TOTALS VARYING SUB                        05700049
               FROM 1 BY 1                                              05710049
               UNTIL SUB > NO-RTVS.                                     05720049
                                                                        05730049
           PERFORM 8200-WRITE-XFER-RPT-DTL.                             05740049
                                                                        05750049
      /*****************************************************************05760049
      *  OPEN FILES                                                     05770049
      ******************************************************************05780049
       2100-OPEN-FILES.                                                 05790049
                                                                        05800049
           OPEN INPUT POSITIVE-XFER-DTL.                                05810049
           OPEN OUTPUT POSITIVE-XFER-RPT.                               05820049
                                                                        05830049
      ******************************************************************05840049
      *  READ INPUT DATA                                                05850049
      ******************************************************************05860049
       2200-READ-POSITIVE-XFER-DTL.                                     05870049
                                                                        05880049
           READ POSITIVE-XFER-DTL                                       05890049
             INTO TF065-POSITIVE-RECVD-REC                              05900049
               AT END                                                   05910049
                 SET PS-POSITIVE-XFER-DTL-EOF                           05920049
                                       TO TRUE.                         05930049
                                                                        05940049
           IF PS-NOT-POSITIVE-XFER-DTL-EOF                              05950049
               ADD +1 TO PT-FIRSTXFER-RCD-FND                           05960049
           END-IF.                                                      05970049
                                                                        05980049
      ******************************************************************05990049
      *  INITIALIZE RTV TABLE                                           06000049
      ******************************************************************06010049
       2300-INITIALIZE-TABLE.                                           06020049
                                                                        06030049
           MOVE ZERO TO PV-RTV-LOC    (SUB)                             06040049
                        PV-RTV-TRANS  (SUB)                             06050049
                        PV-RTV-QTY    (SUB)                             06060049
                        PV-RTV-TOT-RTL(SUB).                            06070049
                                                                        06080049
      ******************************************************************06090049
      *  OPEN RTV TABLE                                                 06100049
      ******************************************************************06110049
       3000-OPEN-RTV-CURSOR.                                            06120049
                                                                        06130049
           EXEC SQL                                                     06140049
              OPEN RTV-LOCS-CSR                                         06150049
           END-EXEC.                                                    06160049
                                                                        06170049
           EVALUATE TRUE                                                06180049
               WHEN SQLCODE = +0                                        06190049
                   CONTINUE                                             06200049
               WHEN SQLCODE = +100                                      06210049
                   CONTINUE                                             06220049
               WHEN OTHER                                               06230049
                   MOVE '3000-OPEN-RTV-CURSOR' TO PV-PARAGRAPH-NAME     06240049
                   PERFORM 8800-DB2-ERROR-ROUTINE                       06250050
           END-EVALUATE.                                                06260049
                                                                        06270049
      ******************************************************************06280049
      *  GET THE RTV LOCATIONS LOAD TO A TABLE                          06290049
      ******************************************************************06300049
       3100-FETCH-RTV-LOCS.                                             06310049
                                                                        06320049
           ADD +1 TO SUB.                                               06330049
                                                                        06340049
           EXEC SQL                                                     06350049
               FETCH RTV-LOCS-CSR                                       06360049
               INTO   :XST00135-RTV-LOC-NBR                             06370049
           END-EXEC                                                     06380049
                                                                        06390049
           EVALUATE TRUE                                                06400049
               WHEN SQLCODE = +0                                        06410049
                   MOVE XST00135-RTV-LOC-NBR TO PV-RTV-LOC(SUB)         06420049
                   MOVE SUB                  TO NO-RTVS                 06430049
               WHEN SQLCODE = +100                                      06440049
                   SET PS-RTV-LOCS-AT-END TO TRUE                       06450049
               WHEN OTHER                                               06460049
                   MOVE '3100-FETCH-RTV-LOCS' TO PV-PARAGRAPH-NAME      06470049
                   PERFORM 8800-DB2-ERROR-ROUTINE                       06480050
           END-EVALUATE.                                                06490049
                                                                        06500049
      ******************************************************************06510049
      * CLOSE RTV CURSOR                                                06520049
      ******************************************************************06530049
       3200-CLOSE-RTV-CURSOR.                                           06540049
                                                                        06550049
           EXEC SQL                                                     06560049
               CLOSE RTV-LOCS-CSR                                       06570049
           END-EXEC.                                                    06580049
                                                                        06590049
           EVALUATE TRUE                                                06600049
               WHEN SQLCODE = +0                                        06610049
                   CONTINUE                                             06620049
               WHEN OTHER                                               06630049
                   MOVE '3200-CLOSE-RTV-CURSOR' TO PV-PARAGRAPH-NAME    06640049
                   PERFORM 8800-DB2-ERROR-ROUTINE                       06650050
           END-EVALUATE.                                                06660049
                                                                        06670049
      ******************************************************************06680049
      * ACCUMULATE THE RTV LOCATIONS TOTALS                             06690049
      ******************************************************************06700049
       3300-ACCUM-TOTALS.                                               06710049
                                                                        06720049
           IF TF065-TO-STR-NBR = PV-RTV-LOC (SUB)                       06730049
               ADD +1                       TO PV-RTV-TRANS (SUB)       06740049
               ADD TF065-TOTAL-QTY          TO PV-RTV-QTY   (SUB)       06750049
               IF TF065-AMT-SIGN = PC-MINUS                             06760049
                  SUBTRACT TF065-TOTAL-RETAIL FROM PV-RTV-TOT-RTL(SUB)  06770049
               ELSE                                                     06780049
                  ADD TF065-TOTAL-RETAIL       TO  PV-RTV-TOT-RTL(SUB)  06790049
               END-IF                                                   06800049
           END-IF.                                                      06810049
                                                                        06820049
      ******************************************************************06830049
      * 8200-WRITE-XFER-RPT-DTL                                         06840049
      ******************************************************************06850049
       8200-WRITE-XFER-RPT-DTL.                                         06860049
                                                                        06870049
           IF PV-LINE-COUNT >= PC-LINE-MAX                              06880049
           OR TF065-TO-STR-NBR NOT = PV-PREV-TO-LOC-NBR                 06890049
               PERFORM 8220-WRITE-XFER-RPT-HDR                          06900049
           END-IF.                                                      06910049
                                                                        06920049
           WRITE POSITIVE-XFER-RPT-RCD                                  06930049
             FROM XRDL-XFER-RPT-DTL-LINE.                               06940049
                                                                        06950049
           ADD +1                      TO PT-RCD-WRTTN                  06960049
                                          PV-LINE-COUNT.                06970049
                                                                        06980049
      ******************************************************************06990049
      *                                                                 07000049
      ******************************************************************07010049
       8210-WRITE-DTL-BREAK-LINE.                                       07020049
                                                                        07030049
           IF PV-LINE-COUNT >= PC-LINE-MAX                              07040049
           OR TF065-TO-STR-NBR NOT = PV-PREV-TO-LOC-NBR                 07050049
               PERFORM 8220-WRITE-XFER-RPT-HDR                          07060049
           ELSE                                                         07070049
               MOVE SPACE              TO POSITIVE-XFER-RPT-RCD         07080049
               WRITE POSITIVE-XFER-RPT-RCD                              07090049
                 AFTER ADVANCING 1 LINE                                 07100049
                                                                        07110049
               ADD +1                  TO PV-LINE-COUNT                 07120049
           END-IF.                                                      07130049
                                                                        07140049
      ******************************************************************07150049
      * CHECK FOR NEW RTV LOCATION BREAK                                07160049
      ******************************************************************07170049
       8220-WRITE-XFER-RPT-HDR.                                         07180049
                                                                        07190049
           IF TF065-TO-STR-NBR NOT = PV-PREV-TO-LOC-NBR                 07200049
               PERFORM 8240-WRITE-TOTAL-LINE                            07210049
               MOVE ZERO               TO PV-PAGE-COUNT                 07220049
               MOVE TF065-TO-STR-NBR   TO XST00001-LOC-NBR              07230049
                                          RHL1-RTV-LOC                  07240049
               PERFORM 8230-GET-RTV-NAME                                07250049
               MOVE XST00001-LOC-NM    TO RHL1-RTV-NM                   07260049
           END-IF.                                                      07270049
                                                                        07280049
           ADD +1                      TO PV-PAGE-COUNT.                07290049
           MOVE PV-PAGE-COUNT          TO DP132O-PAGE-NUMBER.           07300049
                                                                        07310049
           WRITE POSITIVE-XFER-RPT-RCD                                  07320049
             FROM DP132O-STANDRD-HEADER-132-COLS                        07330049
             AFTER ADVANCING PAGE.                                      07340049
                                                                        07350049
           WRITE POSITIVE-XFER-RPT-RCD                                  07360049
             FROM RHL1-RPT-HEAD-LINE1                                   07370049
             AFTER ADVANCING 1 LINE.                                    07380049
                                                                        07390049
           WRITE POSITIVE-XFER-RPT-RCD                                  07400049
             FROM RHL2-RPT-HEAD-LINE2                                   07410049
             AFTER ADVANCING 1 LINE.                                    07421053
                                                                        07430049
           WRITE POSITIVE-XFER-RPT-RCD                                  07440049
             FROM RHL3-RPT-HEAD-LINE3                                   07450049
             AFTER ADVANCING 2 LINE.                                    07460053
                                                                        07470049
           WRITE POSITIVE-XFER-RPT-RCD                                  07480049
             FROM RHL4-RPT-HEAD-LINE4                                   07490049
             AFTER ADVANCING 1 LINE.                                    07500049
                                                                        07510049
           WRITE POSITIVE-XFER-RPT-RCD                                  07520049
             FROM RHL5-RPT-HEAD-LINE5                                   07530049
             AFTER ADVANCING 1 LINE.                                    07540049
                                                                        07550049
           MOVE SPACE                  TO POSITIVE-XFER-RPT-RCD.        07560049
           WRITE POSITIVE-XFER-RPT-RCD                                  07570049
             AFTER ADVANCING 1 LINE.                                    07580049
                                                                        07590049
           MOVE +8                     TO PV-LINE-COUNT.                07600049
                                                                        07610049
      ******************************************************************07620049
      *  THIS ROUTINE GETS THE RTV LOCATION NAME                        07630049
      ******************************************************************07640049
       8230-GET-RTV-NAME.                                               07650049
                                                                        07660049
           EXEC SQL                                                     07670049
             SELECT LOC_NM                                              07680049
             INTO :XST00001-LOC-NM                                      07690049
             FROM XST_LOC                                               07700049
             WHERE LOC_NBR = :XST00001-LOC-NBR                          07710049
               WITH UR                                                  07720049
           END-EXEC.                                                    07730049
                                                                        07740049
           EVALUATE TRUE                                                07750049
               WHEN SQLCODE = +0                                        07760049
                   CONTINUE                                             07770049
               WHEN SQLCODE = +100                                      07780049
                   CONTINUE                                             07790049
               WHEN OTHER                                               07800049
                   MOVE 'SELECT-LOC-NAME' TO PV-PARAGRAPH-NAME          07810049
                   PERFORM 8800-DB2-ERROR-ROUTINE                       07820050
           END-EVALUATE.                                                07830049
                                                                        07840049
      *-----------------------------------------------------------------07850049
      * PRINTS THE TOTAL TRANSFERS, QTY AND RETAIL FOR RTV              07860049
      *-----------------------------------------------------------------07870049
       8240-WRITE-TOTAL-LINE.                                           07880049
           IF PV-PREV-TO-LOC-NBR NOT = ZERO                             07890049
               MOVE SPACE                  TO POSITIVE-XFER-RPT-RCD     07900049
               WRITE POSITIVE-XFER-RPT-RCD                              07910049
                 AFTER ADVANCING 1 LINE                                 07920049
               MOVE PV-PREV-TO-LOC-NBR     TO XRDL-TTL-TO-LOC           07930049
               MOVE PV-TOTAL-TRANS         TO XRDL-TTL-TOTAL-TRANS      07940049
               MOVE PV-TOTAL-QTY           TO XRDL-TTL-TOTAL-QTY        07950049
               MOVE PV-TOTAL-RTL           TO XRDL-TTL-TOTAL-RTL        07960049
               WRITE POSITIVE-XFER-RPT-RCD                              07970049
               FROM XRDL-XFER-RPT-TTL-LINE                              07980049
                  AFTER ADVANCING 1 LINE                                07990049
               MOVE ZERO                   TO PV-TOTAL-TRANS            08000049
                                              PV-TOTAL-QTY              08010049
                                              PV-TOTAL-RTL              08020049
           END-IF.                                                      08030049
                                                                        08040049
      *-----------------------------------------------------------------08050049
      * PRINTS SUMMARY OF ALL RTV LOCATIONS                             08060049
      *-----------------------------------------------------------------08070049
       8250-WRITE-SUMMARY-PAGE.                                         08080049
                                                                        08090049
           MOVE 1                      TO DP132O-PAGE-NUMBER.           08100049
                                                                        08110049
           WRITE POSITIVE-XFER-RPT-RCD                                  08120049
             FROM DP132O-STANDRD-HEADER-132-COLS                        08130049
             AFTER ADVANCING PAGE.                                      08140049
                                                                        08150049
           WRITE POSITIVE-XFER-RPT-RCD                                  08160049
             FROM RHL1-RPT-SUM-LINE1                                    08170049
             AFTER ADVANCING 1 LINE.                                    08180049
                                                                        08190049
           WRITE POSITIVE-XFER-RPT-RCD                                  08200049
             FROM RHL2-RPT-HEAD-LINE2                                   08210049
             AFTER ADVANCING 1 LINE.                                    08220049
                                                                        08230049
           WRITE POSITIVE-XFER-RPT-RCD                                  08240049
             FROM RHL1-RPT-SUM-LINE2                                    08250049
             AFTER ADVANCING 2 LINE.                                    08260053
                                                                        08270049
           WRITE POSITIVE-XFER-RPT-RCD                                  08280049
             FROM RHL1-RPT-SUM-LINE3                                    08290049
             AFTER ADVANCING 1 LINE.                                    08300049
                                                                        08310049
           WRITE POSITIVE-XFER-RPT-RCD                                  08320049
             FROM RHL1-RPT-SUM-LINE4                                    08330049
             AFTER ADVANCING 1 LINE.                                    08340049
                                                                        08350049
           PERFORM 8260-MOVE-TOTALS                                     08360049
               VARYING SUB FROM 1 BY 1                                  08370049
               UNTIL SUB > NO-RTVS.                                     08380049
                                                                        08390049
           MOVE PC-ALL                 TO XRDL-SUM-TO-LOC-NBR.          08400049
           MOVE PV-TOTAL-TRANS-ALL     TO XRDL-SUM-TOTAL-TRANS.         08410049
           MOVE PV-TOTAL-QTY-ALL       TO XRDL-SUM-TOTAL-QTY.           08420049
           MOVE PV-TOTAL-RTL-ALL       TO XRDL-SUM-TOTAL-RTL.           08430049
           WRITE POSITIVE-XFER-RPT-RCD                                  08440049
             FROM XRDL-XFER-SUM-DTL-LINE                                08450049
             AFTER ADVANCING 1 LINE.                                    08460049
      *-----------------------------------------------------------------08470049
      * PRINTS TOTALS FOR ALL RTV LOCATIONS                             08480049
      *-----------------------------------------------------------------08490049
       8260-MOVE-TOTALS.                                                08500049
                                                                        08510049
           MOVE PV-RTV-LOC(SUB)        TO XRDL-SUM-TO-LOC-NBR.          08520049
           MOVE PV-RTV-TRANS(SUB)      TO XRDL-SUM-TOTAL-TRANS.         08530049
           MOVE PV-RTV-QTY(SUB)        TO XRDL-SUM-TOTAL-QTY.           08540049
           MOVE PV-RTV-TOT-RTL(SUB)    TO XRDL-SUM-TOTAL-RTL.           08550049
           WRITE POSITIVE-XFER-RPT-RCD                                  08560049
             FROM XRDL-XFER-SUM-DTL-LINE                                08570049
             AFTER ADVANCING 1 LINE.                                    08580049
                                                                        08590049
072500*----------------------------------------------------------       08600049
072700*     READ CONTROL CARD TFRD069.                                  08610049
072900*----------------------------------------------------------       08620049
073000 8300-READ-CURR-DTE-CARD.                                         08630049
073100                                                                  08640049
           OPEN INPUT CONTROL-CARD-DTE.                                 08650049
                                                                        08660049
073200     READ CONTROL-CARD-DTE                                        08670049
073300                          INTO TF069-CC-CONTROL-CARD              08680049
073400       AT END                                                     08690049
073500          SET PS-END-OF-CNTL-CRD-FILE TO TRUE.                    08700049
073600                                                                  08710049
073700     IF PS-END-OF-CNTL-CRD-FILE                                   08720049
073800       DISPLAY ' '                                                08730049
073900       DISPLAY '** ABEND     **'                                  08740049
074000       DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME              08750049
074100       DISPLAY '** PARAGRAPH **  = 8300-READ-CURR-DTE-CARD'       08760049
074200       DISPLAY '** CONTROL FILE IS EMPTY  '                       08770049
074300       DISPLAY ' '                                                08780049
074400       MOVE +4003 TO ABEND-CODE                                   08790049
074500       CALL 'ILBOABN0' USING ABEND-CODE                           08800049
074600     ELSE                                                         08810049
074700       DISPLAY '** CURRENT TRANSFER DATE CONTROL CARD  **'        08820049
074800       DISPLAY '** TFCC011       **  = ' TF069-CC-CRNT-DTE        08830049
074900       DISPLAY ' '                                                08840049
075000     END-IF.                                                      08850049
075100                                                                  08860049
           INITIALIZE DB2-DATE.                                         08870049
           MOVE TF069-CC-CRNT-DTE(1:4)     TO DB2-DATE-CCYY.            08880049
           MOVE TF069-CC-CRNT-DTE(5:2)     TO DB2-DATE-MM.              08890049
           MOVE TF069-CC-CRNT-DTE(7:2)     TO DB2-DATE-DD.              08900049
                                                                        08910049
           CLOSE CONTROL-CARD-DTE.                                      08920049
                                                                        08930049
           PERFORM 8310-DATE-ROUTINE.                                   08940049
           MOVE DB2-DATE                   TO RHL2-DTE-BEGIN.           08950049
                                                                        08960049
268140*----------------------------------------------------------       08970049
268200* DATE ROUTINE TO SET UP THE PROCESS DATE IN                      08980049
268300* CCYYMMDD FORMAT.                                                08990049
268400*----------------------------------------------------------       09000049
268500 8310-DATE-ROUTINE.                                               09010049
268600                                                                  09020049
268700     INITIALIZE DPG51 DPG52 DPG53                                 09030049
268800                DPG54 DPG55 DPG56.                                09040049
268900                                                                  09050049
269000     SET DPG51-FISCAL-454-CALENDAR  TO TRUE.                      09060049
269100     SET DPG52-LK-DTE-ISO-FMT       TO TRUE.                      09070049
269200     MOVE DB2-DATE                   TO DPG52-DB2-ISO-DATE-FMT.   09080049
           DISPLAY 'DB2-DATE = ' DB2-DATE.                              09090049
269300                                                                  09100049
269400     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    09110049
269500                                             DPG54 DPG55 DPG56.   09120049
269600                                                                  09130049
269700     IF DPG54-SEVERE-ERROR OR DPG54-DATE-INVALID                  09140049
269800       DISPLAY '** ABEND **'                                      09150049
269900       DISPLAY '** PROGRAM = TFKRP007'                            09160049
270000       DISPLAY 'DATE = ' TF069-CC-CRNT-DTE                        09170049
270100       DISPLAY DPG54-ERROR-MESSAGE                                09180049
270200       MOVE 4019 TO ABEND-CODE                                    09190049
270300       CALL 'ILBOABN0' USING ABEND-CODE                           09200049
270400     END-IF.                                                      09210049
270500                                                                  09220049
                                                                        09230049
      *-----------------------------------------------------------------09240049
      *8800-DB2-ERROR-ROUTINE.                                          09250050
      *-----------------------------------------------------------------09260049
       8800-DB2-ERROR-ROUTINE.                                          09270050
                                                                        09280049
           PERFORM 8900-CLOSE-FILES.                                    09290049
           MOVE SQLCODE TO SQLCODE2.                                    09300049
           MOVE SQLERRD(3) TO SQLERRD3.                                 09310049
                                                                        09320049
           DISPLAY '** ERROR **        = SQL ERROR'.                    09330049
           DISPLAY '** PROGRAM **      = '  PC-PROGRAM-NAME.            09340049
           DISPLAY '** PARAGRAPH **    = '  PV-PARAGRAPH-NAME.          09350049
           DISPLAY '** SQL ERROR **    = '  SQLCODE2.                   09360049
           DISPLAY '** SQLERRM  **     = '  SQLERRM.                    09370049
                                                                        09380049
      *----------------------------------------------------------------*09390049
      *  THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   09400049
      *  MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        09410049
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     09420049
      *  FORMAT.                                                        09430049
      *----------------------------------------------------------------*09440049
           COPY DPPD004.                                                09450049
                                                                        09460049
           CALL 'DSNTIAR' USING SQLCA                                   09470049
                                DP004-FORMATTED-MESSAGE                 09480049
                                DP004-ERROR-LINE-LENGTH.                09490049
                                                                        09500049
           DISPLAY DP004-ASTERISK-LINE.                                 09510049
                                                                        09520049
           PERFORM                                                      09530049
              VARYING DP004-MSG-IDX                                     09540049
              FROM 1 BY 1                                               09550049
              UNTIL DP004-MSG-IDX > DP004-MAX-MESSAGE-LINES             09560049
                  DISPLAY '**'                                          09570049
                          DP004-MESSAGE-LINE (DP004-MSG-IDX)            09580049
                          ' **'                                         09590049
           END-PERFORM.                                                 09600049
                                                                        09610049
           DISPLAY DP004-ASTERISK-LINE.                                 09620049
                                                                        09630049
           IF SQLCODE NOT = -911                                        09640049
               EXEC SQL                                                 09650049
                   COMMIT                                               09660049
               END-EXEC                                                 09670049
           END-IF.                                                      09680049
                                                                        09690049
           SET AC-DB2-ERROR TO TRUE.                                    09700049
                                                                        09710049
           CALL 'ILBOABN0' USING ABEND-CODE.                            09720049
                                                                        09730049
      ******************************************************************09740049
      *    CLOSE THE FILES.                                             09750049
      ******************************************************************09760049
       8900-CLOSE-FILES.                                                09770049
                                                                        09780049
           CLOSE POSITIVE-XFER-DTL                                      09790049
                 POSITIVE-XFER-RPT.                                     09800049
                                                                        09810049
      ******************************************************************09820049
      *    DISPLAY PROGRAM STATS                                        09830049
      ******************************************************************09840049
       9000-DISPLAY-PROGRAM-TOTALS.                                     09850049
                                                                        09860049
           DISPLAY '**********************************'.                09870049
           DISPLAY '* TFKRP007 PROGRAM STATISTICS:    '.                09880049
           DISPLAY '**********************************'.                09890049
           DISPLAY '* TOTAL XFERS READ: ' PT-FIRSTXFER-RCD-FND.         09900049
           DISPLAY '* REPORT DETAIL WRITTEN: ' PT-RCD-WRTTN.            09910049
           DISPLAY '**********************************'.                09920049
           DISPLAY '**********************************'.                09930049
           DISPLAY '* END OF TFKRP007                 '.                09940049
           DISPLAY '**********************************'.                09950049
                                                                        09960049
      ******************************************************************09970049
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM. 09980049
      ******************************************************************09990049
       9999-ABEND.                                                      10000049
                                                                        10010049
           DISPLAY '**********************************'.                10020049
           DISPLAY '**  ABEND     **'.                                  10030049
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              10040049
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            10050049
           DISPLAY '**  OPERATION **  = ' PV-OPER-ATTEMPTED.            10060049
           DISPLAY '**********************************'.                10070049
           SET AC-NON-DB2-ERROR TO TRUE.                                10080049
           CALL 'ILBOABN0'  USING ABEND-CODE.                           10090049
                                                                        10100049
