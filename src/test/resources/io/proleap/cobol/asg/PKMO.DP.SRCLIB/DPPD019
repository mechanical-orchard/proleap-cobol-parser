00001                                                                   03/25/92
00002 ****************************************************************  DPPD019 
00003 *  THIS ROUTINE PERFORMS LEFT JUSTIFICATION AND               **     LV001
00004 *  THE FOLLOWING ALPHA NUMERIC EDITS ON THE IN FIELD          **  DPPD019 
00005 *                                                             **  DPPD019 
00006 *  AN ALPHA FIELD IS VALID IF :                               **  DPPD019 
00007 *  -   THE ENTIRE FIELD SENT IS SPACES (NO ERRORS ARE FLAGGED)**  DPPD019 
00008 *  -   NO MORE THAN 1 IMBEDDED SPACE IS ENCOUNTERED BETWEEN   **  DPPD019 
00009 *        CHARACTERS (UNTIL NO MORE CHARACTERS FOUND)          **  DPPD019 
00010 *  -   A FIELD MAY CONTAIN ALL ASTERISKS '*'.                 **  DPPD019 
00011 *  -   AN '*' MAY BE THE FIRST OR LAST CHARACTER              **  DPPD019 
00012 *        (FOLLOWED BY ALL SPACES) (THIS IS A WILD CARD VALUE) **  DPPD019 
00013 *  -   SPECIAL CHARACTERS ('), (-), AND (*) ARE ALLOWED       **  DPPD019 
00014 *  -   (') AND (-) MUST OCCUR BETWEEN 2 LETTERS               **  DPPD019 
00015 *        TO SIGNIFY HYPHENATED NAMES, ETC.                    **  DPPD019 
00016 *                                                             **  DPPD019 
00017 *  INSTRUCTIONS ON HOW TO USE:                                **  DPPD019 
00018 *  -   INCLUDE THE REQUIRED WORKING STORAGE 'DPWS025'         **  DPPD019 
00019 *  -   INCLUDE THIS CODE IN YOUR PROCEDURE DIVISION 'DPPD019' **  DPPD019 
00020 *  -   MOVE THE APLHA NUMERIC FIELD LENGTH THE FIELD          **  DPPD019 
00021 *       'DP019-FLD-LENGTH'                                    **  DPPD019 
00022 *  -   MOVE THE APLHA NUMERIC FIELD TO THE FIELD              **  DPPD019 
00023 *       'DP019-IN-FLD'                                        **  DPPD019 
00024 *       (MUST OCCUR AFTER THE LENGTH FIELD IS INITIALIZED)    **  DPPD019 
00025 *  -   PERFORM PARA 'DP019-ALPHA-NUMERIC-EDIT'                **  DPPD019 
00026 *  -   CHECK DP019-ERROR-SW FOR ANY ERRORS                    **  DPPD019 
00027 *  _   'DP019-OUT-FLD' CONTAINS THE EDITED FIELD              **  DPPD019 
00028 *                                                             **  DPPD019 
00029 ****************************************************************  DPPD019 
00030                                                                   DPPD019 
00031                                                                   DPPD019 
00032  DP019-ALPHA-NUMERIC-EDIT.                                        DPPD019 
00033 ****************************************************************  DPPD019 
00034 *    CONTROL PARAGRAPH - PERFORMED BY HOST MODULES                DPPD019 
00035 ****************************************************************  DPPD019 
00036                                                                   DPPD019 
00037      MOVE 'N'    TO  DP019-ERROR-SW.                              DPPD019 
00038      MOVE 'N'    TO  DP019-LEFT-JUSTIFIED-SW.                     DPPD019 
00039                                                                   DPPD019 
00040      MOVE ZERO   TO  DP019-OUT-FLD-INDEX.                         DPPD019 
00041      MOVE SPACES TO  DP019-OUT-FLD.                               DPPD019 
00042                                                                   DPPD019 
00043      SET DP019-BEGINNING          TO TRUE.                        DPPD019 
00044      SET DP019-NOT-END-OF-FLD     TO TRUE.                        DPPD019 
00045                                                                   DPPD019 
00046      IF DP019-IN-FLD = SPACES                                     DPPD019 
00047          MOVE DP019-IN-FLD TO DP019-OUT-FLD                       DPPD019 
00048          SET DP019-LEFT-JUSTIFIED TO TRUE                         DPPD019 
00049      END-IF.                                                      DPPD019 
00050                                                                   DPPD019 
00051      IF DP019-IN-FLD NUMERIC                                      DPPD019 
00052          SET DP019-ERROR TO TRUE                                  DPPD019 
00053          MOVE DP019-IN-FLD TO DP019-OUT-FLD                       DPPD019 
00054      END-IF.                                                      DPPD019 
00055                                                                   DPPD019 
00056      PERFORM DP100-EDIT-FLD                                       DPPD019 
00057          VARYING  DP019-IN-FLD-INDEX                              DPPD019 
00058          FROM  +1 BY +1                                           DPPD019 
00059          UNTIL  (DP019-IN-FLD-INDEX = (DP019-FLD-LENGTH + 1) ) OR DPPD019 
00060                   DP019-LEFT-JUSTIFIED  OR  DP019-ERROR.          DPPD019 
00061                                                                   DPPD019 
00062  EJECT                                                            DPPD019 
00063                                                                   DPPD019 
00064  DP100-EDIT-FLD.                                                  DPPD019 
00065 ******************************************************************DPPD019 
00066 *    DETERMINE WHERE YOU ARE IN PROCESSING THE FIELD              DPPD019 
00067 ******************************************************************DPPD019 
00068                                                                   DPPD019 
00069      IF DP019-BEGINNING                                           DPPD019 
00070          PERFORM DP200-LEFT-JUSTIFY-FLD                           DPPD019 
00071          IF DP019-NOT-END-OF-FLD                                  DPPD019 
00072 *            ** PROCESS THE 1ST OUT FIELD CHARACTER **            DPPD019 
00073              PERFORM DP400-PROCESS-CHARACTER                      DPPD019 
00074          ELSE                                                     DPPD019 
00075              MOVE 'Y' TO DP019-LEFT-JUSTIFIED-SW                  DPPD019 
00076          END-IF                                                   DPPD019 
00077      ELSE                                                         DPPD019 
00078 *      (DP019-NOT-BEGINNING)                                      DPPD019 
00079          PERFORM DP300-CHECK-SPACES                               DPPD019 
00080          IF DP019-NOT-END-OF-FLD AND                              DPPD019 
00081             DP019-NO-ERROR                                        DPPD019 
00082               PERFORM DP400-PROCESS-CHARACTER                     DPPD019 
00083          ELSE                                                     DPPD019 
00084              MOVE 'Y' TO DP019-LEFT-JUSTIFIED-SW                  DPPD019 
00085          END-IF                                                   DPPD019 
00086      END-IF.                                                      DPPD019 
00087                                                                   DPPD019 
00088  EJECT                                                            DPPD019 
00089                                                                   DPPD019 
00090  DP200-LEFT-JUSTIFY-FLD.                                          DPPD019 
00091 ******************************************************************DPPD019 
00092 *    LEFT JUSTIFY THE FIRST ALPHA NUMERIC CHARACTER               DPPD019 
00093 ******************************************************************DPPD019 
00094                                                                   DPPD019 
00095      IF DP019-FLD-IN-X(DP019-IN-FLD-INDEX) = SPACE                DPPD019 
00096          IF  DP019-IN-FLD-INDEX = DP019-FLD-LENGTH                DPPD019 
00097               SET DP019-END-OF-FLD TO TRUE                        DPPD019 
00098          END-IF                                                   DPPD019 
00099      ELSE                                                         DPPD019 
00100 *        ** MOVE THE 1ST CHARACTER TO THE OUT FIELD **            DPPD019 
00101          ADD +1 TO DP019-OUT-FLD-INDEX                            DPPD019 
00102          MOVE DP019-FLD-IN-X(DP019-IN-FLD-INDEX) TO               DPPD019 
00103             DP019-FLD-OUT-X(DP019-OUT-FLD-INDEX)                  DPPD019 
00104          SET DP019-NOT-BEGINNING TO TRUE                          DPPD019 
00105      END-IF.                                                      DPPD019 
00106                                                                   DPPD019 
00107  EJECT                                                            DPPD019 
00108                                                                   DPPD019 
00109  DP300-CHECK-SPACES.                                              DPPD019 
00110 ******************************************************************DPPD019 
00111 *    DETERMINE IF THE END OF THE FIELD HAS BEEN REACHED           DPPD019 
00112 *    OR IF 2 OR MORE SPACES ARE ENCOUNTERED BETWEEN CHARACTERS    DPPD019 
00113 ******************************************************************DPPD019 
00114                                                                   DPPD019 
00115      IF DP019-FLD-IN-X(DP019-IN-FLD-INDEX) = SPACE AND            DPPD019 
00116         DP019-FLD-IN-X(DP019-IN-FLD-INDEX + 1) = SPACE            DPPD019 
00117                                                                   DPPD019 
00118          PERFORM VARYING DP019-TEMP-INDEX                         DPPD019 
00119             FROM DP019-IN-FLD-INDEX    BY  1                      DPPD019 
00120            UNTIL DP019-TEMP-INDEX = DP019-FLD-LENGTH              DPPD019 
00121                                                                   DPPD019 
00122 *             * MOVE ALL REMAINING CHARACTERS *                   DPPD019 
00123               ADD +1 TO DP019-OUT-FLD-INDEX                       DPPD019 
00124               MOVE DP019-FLD-IN-X(DP019-TEMP-INDEX) TO            DPPD019 
00125                  DP019-FLD-OUT-X(DP019-OUT-FLD-INDEX)             DPPD019 
00126                                                                   DPPD019 
00127               IF DP019-FLD-IN-X(DP019-TEMP-INDEX) = SPACE         DPPD019 
00128                   CONTINUE                                        DPPD019 
00129               ELSE                                                DPPD019 
00130 *                 * CHARACTERS EXIST AFTER 2 OR MORE SPACES *     DPPD019 
00131                   SET DP019-ERROR TO TRUE                         DPPD019 
00132                   MOVE DP019-IN-FLD TO DP019-OUT-FLD              DPPD019 
00133               END-IF                                              DPPD019 
00134          END-PERFORM                                              DPPD019 
00135                                                                   DPPD019 
00136          MOVE DP019-FLD-LENGTH  TO DP019-IN-FLD-INDEX             DPPD019 
00137          SET DP019-END-OF-FLD TO TRUE                             DPPD019 
00138                                                                   DPPD019 
00139      ELSE                                                         DPPD019 
00140          ADD +1 TO DP019-OUT-FLD-INDEX                            DPPD019 
00141          MOVE DP019-FLD-IN-X(DP019-IN-FLD-INDEX) TO               DPPD019 
00142             DP019-FLD-OUT-X(DP019-OUT-FLD-INDEX)                  DPPD019 
00143                                                                   DPPD019 
00144      END-IF.                                                      DPPD019 
00145                                                                   DPPD019 
00146  DP400-PROCESS-CHARACTER.                                         DPPD019 
00147 ******************************************************************DPPD019 
00148 *    EVALUATE THE CONTENTS OF THE CHARACTER FIELD                *DPPD019 
00149 ******************************************************************DPPD019 
00150                                                                   DPPD019 
00151      IF DP019-FLD-OUT-X(DP019-OUT-FLD-INDEX) = SPACE              DPPD019 
00152          CONTINUE                                                 DPPD019 
00153      ELSE                                                         DPPD019 
00154         IF DP019-FLD-OUT-X(DP019-OUT-FLD-INDEX) = '*'             DPPD019 
00155            IF DP019-OUT-FLD-INDEX = DP019-FLD-LENGTH              DPPD019 
00156                SET DP019-END-OF-FLD TO TRUE                       DPPD019 
00157            ELSE                                                   DPPD019 
00158               IF DP019-FLD-IN-X(DP019-IN-FLD-INDEX + 1) = SPACE   DPPD019 
00159                   MOVE DP019-FLD-LENGTH  TO DP019-IN-FLD-INDEX    DPPD019 
00160                   SET DP019-END-OF-FLD TO TRUE                    DPPD019 
00161               ELSE                                                DPPD019 
00162                   PERFORM DP500-CHK-ASTERISKS                     DPPD019 
00163               END-IF                                              DPPD019 
00164            END-IF                                                 DPPD019 
00165         END-IF                                                    DPPD019 
00166      END-IF.                                                      DPPD019 
00167                                                                   DPPD019 
00168      MOVE DP019-FLD-OUT-X(DP019-OUT-FLD-INDEX) TO DP019-TEST-BYTE DPPD019 
00169                                                                   DPPD019 
00170      IF DP019-SINGLE-QUOTE                                        DPPD019 
00171          IF (DP019-OUT-FLD-INDEX - 1) <= +0                       DPPD019 
00172               SET DP019-ERROR TO TRUE                             DPPD019 
00173          ELSE                                                     DPPD019 
00174               IF DP019-FLD-OUT-X(DP019-OUT-FLD-INDEX - 1) NUMERIC DPPD019 
00175                    SET DP019-ERROR TO TRUE                        DPPD019 
00176               END-IF                                              DPPD019 
00177               IF DP019-FLD-IN-X(DP019-IN-FLD-INDEX + 1) NUMERIC   DPPD019 
00178                    SET DP019-ERROR TO TRUE                        DPPD019 
00179               END-IF                                              DPPD019 
00180          END-IF                                                   DPPD019 
00181      END-IF.                                                      DPPD019 
00182                                                                   DPPD019 
00183 *    ** TEST THE 88 LEVEL ON THE OUTPUT FIELD BYTE **             DPPD019 
00184 *    **    VALID:   '*' OR "'" OR ',' OR '.'       **             DPPD019 
00185                                                                   DPPD019 
00186      IF DP019-INVALID-CHARACTER                                   DPPD019 
00187          SET DP019-ERROR TO TRUE                                  DPPD019 
00188      END-IF.                                                      DPPD019 
00189                                                                   DPPD019 
00190      IF DP019-ERROR                                               DPPD019 
00191          MOVE DP019-IN-FLD TO DP019-OUT-FLD                       DPPD019 
00192      END-IF.                                                      DPPD019 
00193                                                                   DPPD019 
00194  DP500-CHK-ASTERISKS.                                             DPPD019 
00195 ******************************************************************DPPD019 
00196 *    VERIFY THE REMAINING CHARACTERS AFTER '*' ARE ASTERISKS     *DPPD019 
00197 ******************************************************************DPPD019 
00198                                                                   DPPD019 
00199      MOVE +0 TO DP019-INSPECT-CTR                                 DPPD019 
00200                                                                   DPPD019 
00201      INSPECT DP019-IN-FLD                                         DPPD019 
00202              TALLYING DP019-INSPECT-CTR FOR ALL '*'               DPPD019 
00203                                                                   DPPD019 
00204      COMPUTE DP019-REMAINDER = ( DP019-FLD-LENGTH                 DPPD019 
00205                                 - ( DP019-IN-FLD-INDEX - 1 ))     DPPD019 
00206                                                                   DPPD019 
00207      IF DP019-INSPECT-CTR = DP019-REMAINDER                       DPPD019 
00208 *       ** ALL REMAINING CHARACTERS ARE ASTERISKS **              DPPD019 
00209                                                                   DPPD019 
00210          COMPUTE DP019-TEMP-INDEX = ( DP019-FLD-LENGTH            DPPD019 
00211                                        - DP019-REMAINDER + 2 )    DPPD019 
00212                                                                   DPPD019 
00213          PERFORM VARYING DP019-IN-FLD-INDEX                       DPPD019 
00214             FROM DP019-TEMP-INDEX  BY  1                          DPPD019 
00215            UNTIL DP019-IN-FLD-INDEX = (DP019-FLD-LENGTH + 1)      DPPD019 
00216                                                                   DPPD019 
00217 *             * MOVE ALL REMAINING ASTERISKS *                    DPPD019 
00218               ADD +1 TO DP019-OUT-FLD-INDEX                       DPPD019 
00219               MOVE DP019-FLD-IN-X(DP019-IN-FLD-INDEX) TO          DPPD019 
00220                  DP019-FLD-OUT-X(DP019-OUT-FLD-INDEX)             DPPD019 
00221                                                                   DPPD019 
00222          END-PERFORM                                              DPPD019 
00223                                                                   DPPD019 
00224          SET DP019-END-OF-FLD TO TRUE                             DPPD019 
00225      ELSE                                                         DPPD019 
00226          SET DP019-ERROR TO TRUE                                  DPPD019 
00227      END-IF.                                                      DPPD019 
00228                                                                   DPPD019 
