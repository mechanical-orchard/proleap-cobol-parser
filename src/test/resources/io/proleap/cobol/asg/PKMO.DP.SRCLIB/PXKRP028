       IDENTIFICATION DIVISION.                                         00070001
       PROGRAM-ID.      PXKRP028.                                       00080001
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00090001
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00100001
       DATE-WRITTEN.    07/26/2010.                                     00110001
       DATE-COMPILED.                                                   00120001
      ******************************************************************00130001
      * CONSOLIDATION REPORTING - STORE LEVEL DETAILED REPORTS          00140012
      ******************************************************************00150001
      * DATE: 07/26/2010 | MADE BY: THOMAS HANSON | PROJECT: WR37816    00160011
      * DESCRIPTION OF CHANGES: INITIAL PROGRAM IMPLEMENTATION.         00170011
      ******************************************************************00700001
       ENVIRONMENT DIVISION.                                            00710001
       CONFIGURATION SECTION.                                           00720001
       SOURCE-COMPUTER.        IBM-3090.                                00730001
       OBJECT-COMPUTER.        IBM-3090.                                00740001
       INPUT-OUTPUT SECTION.                                            00750001
       FILE-CONTROL.                                                    00760001
                                                                        00770001
           SELECT INPUT-FILE                                            00780001
                  ASSIGN TO INP01.                                      00790001
           SELECT REPORT-FILE                                           00920001
                  ASSIGN TO OUT01.                                      00930001
                                                                        00940001
       DATA DIVISION.                                                   00950001
       FILE SECTION.                                                    00960001
                                                                        00970001
       FD  INPUT-FILE                                                   00980001
           RECORDING MODE F                                             00990001
           BLOCK CONTAINS 0 RECORDS                                     01000001
           LABEL RECORDS ARE OMITTED                                    01010001
           DATA RECORD IS INPUT-RECORD.                                 01020001
       01  INPUT-RECORD                    PIC X(171).                  01030007
                                                                        01040001
       FD  REPORT-FILE                                                  01470001
           RECORDING MODE F                                             01480001
           BLOCK CONTAINS 0 RECORDS                                     01490001
           LABEL RECORDS ARE OMITTED                                    01500001
           DATA RECORD IS REPORT-RECORD.                                01510001
       01  REPORT-RECORD                   PIC X(132).                  01520001
                                                                        01530001
       WORKING-STORAGE SECTION.                                         01540001
      ******************************************************************01550001
      **SWITCHES                                                        01560001
      ******************************************************************01570001
       01  PS-PROGRAM-SWITCHES.                                         01580001
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)   VALUE 'N'.   01590001
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.   01600001
               88  PS-NOT-END-OF-INPUT-FILE                VALUE 'N'.   01610001
      ******************************************************************01910001
      **PROGRAM HOLDS                                                   01920001
      ******************************************************************01930001
       01  PH-PROGRAM-HOLDS-1.                                          01940001
           05  PH-STORE-NBR-HOLD             PIC 9(04) VALUE ZEROS.     01950003
           05  PH-FLOOR-PAD-HOLD             PIC X(02) VALUE SPACES.    01970003
       01  PH-PROGRAM-HOLDS-2.                                          01990001
           05  PH-DEPT-NBR-HOLD              PIC 9(04) VALUE ZEROS.     02000003
           05  PH-SUB-CLSS-HOLD              PIC 9(04) VALUE ZEROS.     02020003
       01  PV-PROGRAM-HOLDS-3.                                          02030007
           05  PH-STORE-NAME-HOLD            PIC X(20) VALUE SPACES.    02050007
      ******************************************************************02090001
      **TIMESTAMP VARIABLES                                             02100001
      ******************************************************************02110001
       01 DF-DATE-TIME-FIELDS.                                          02120001
          05 DF-ACCEPT-DATE.                                            02130001
             10  DF-ACCEPT-YR                  PIC 9(02) VALUE ZEROS.   02140001
             10  DF-ACCEPT-MO                  PIC 9(02) VALUE ZEROS.   02150001
             10  DF-ACCEPT-DY                  PIC 9(02) VALUE ZEROS.   02160001
          05 DF-ACCEPT-TIME.                                            02170001
             10  DF-ACCEPT-HR                  PIC 9(02) VALUE ZEROS.   02180001
             10  DF-ACCEPT-MN                  PIC 9(02) VALUE ZEROS.   02190001
             10  DF-ACCEPT-SS                  PIC 9(04) VALUE ZEROS.   02200001
          05 DF-FORMAT-DATE.                                            02210001
             10  DF-FORMAT-DATE-MM             PIC 9(02) VALUE ZEROS.   02220001
             10  FILLER                        PIC X(01) VALUE '/'.     02230001
             10  DF-FORMAT-DATE-DD             PIC 9(02) VALUE ZEROS.   02240001
             10  FILLER                        PIC X(01) VALUE '/'.     02250001
             10  DF-FORMAT-DATE-YY             PIC 9(02) VALUE ZEROS.   02260001
      ******************************************************************02270001
      **PROGRAM CONSTANTS                                               02280001
      ******************************************************************02290001
       01  PC-PROGRAM-CONSTANTS.                                        02300001
           05  PC-CURRENT-PROGRAM-NAME       PIC X(08) VALUE 'PXKRP028'.02310003
           05  PC-LINES-PER-PAGE-LIMIT       PIC 9(02) VALUE 60.        02320003
      ******************************************************************02400001
      **PROGRAM COUNTERS                                                02410001
      ******************************************************************02420001
       01  PC-PROGRAM-COUNTER.                                          02430001
           05  PC-INPUT-RECORDS-READ           PIC 9(09) VALUE ZEROS.   02440001
           05  PC-REPORT-RECORDS-WRITTEN       PIC 9(09) VALUE ZEROS.   02470001
           05  PC-REPORT-LINE-COUNTER          PIC 9(03) VALUE ZEROS.   02500001
           05  PC-PAGE-COUNTER                 PIC 9(03) VALUE ZEROS.   02510001
      ******************************************************************02520001
      **REPORT HEADINGS                                                 02530001
      ******************************************************************02540001
       01  RH-TITLE-1.                                                  02550002
           05  FILLER                          PIC X(56)  VALUE SPACES. 02560002
           05  FILLER                          PIC X(32)                02570002
                             VALUE 'CONSOLIDATION REPORTING         '.  02580002
           05  FILLER                          PIC X(44)  VALUE SPACES. 02590002
       01  RH-TITLE-2.                                                  02591002
           05  FILLER                          PIC X(61)  VALUE SPACES. 02592003
           05  FILLER                          PIC X(32)                02593002
                             VALUE 'STORE DETAILS                   '.  02594002
           05  FILLER                          PIC X(39)  VALUE SPACES. 02595003
       01  RH-HEADINGS-1.                                               02595102
           05  FILLER                          PIC X(02)  VALUE SPACES. 02595202
           05  FILLER                          PIC X(14)                02595302
                             VALUE 'STORE NUMBER:'.                     02595402
           05  RH-HEADINGS-1-STR-NBR           PIC 9999  VALUE ZEROS.   02595502
           05  FILLER                          PIC X(01)  VALUE SPACES. 02595602
           05  RH-HEADINGS-1-STR-NAME          PIC X(20)  VALUE SPACES. 02595703
           05  FILLER                          PIC X(90)  VALUE SPACES. 02595802
       01  RH-HEADINGS-2.                                               02596002
           05  FILLER                          PIC X(02)  VALUE SPACES. 02597002
           05  FILLER                          PIC X(20)                02598002
                             VALUE 'CONSOLIDATION DATE: '.              02599002
           05  RH-HEADINGS-2-CNSLDTN-DTE       PIC X(10)  VALUE SPACES. 02600002
           05  FILLER                          PIC X(100) VALUE SPACES. 02610002
       01  RH-HEADINGS-3.                                               02770002
           05  FILLER                          PIC X(02)  VALUE SPACES. 02771002
           05  FILLER                          PIC X(11)                02780002
                             VALUE 'FLOOR PAD: '.                       02790002
           05  RH-HEADINGS-3-FLOOR-PAD-CDE     PIC X(02)  VALUE SPACES. 02800002
           05  FILLER                          PIC X(01)  VALUE SPACES. 02801002
           05  RH-HEADINGS-3-FLOOR-PAD-DESC    PIC X(25)  VALUE SPACES. 02802003
           05  FILLER                          PIC X(01)  VALUE SPACES. 02803002
       01  RH-HEADINGS-4.                                               02803102
           05  FILLER                          PIC X(26)  VALUE SPACES. 02803203
           05  FILLER                          PIC X(03)                02803302
                             VALUE 'SUB'.                               02803402
           05  FILLER                          PIC X(03)  VALUE SPACES. 02803502
           05  FILLER                          PIC X(03)                02803602
                             VALUE 'SUB'.                               02803702
           05  FILLER                          PIC X(91)  VALUE SPACES. 02803803
           05  FILLER                          PIC X(05)                02803902
                             VALUE 'STORE'.                             02804002
       01  RH-HEADINGS-5.                                               02804202
           05  FILLER                          PIC X(04)                02804402
                             VALUE 'DEPT'.                              02804502
           05  FILLER                          PIC X(01)  VALUE SPACES. 02804603
           05  FILLER                          PIC X(16)                02804702
                             VALUE 'DEPT DESCRIPTION'.                  02804802
           05  FILLER                          PIC X(05)  VALUE SPACES. 02804902
           05  FILLER                          PIC X(05)                02805002
                             VALUE 'CLASS'.                             02805102
           05  FILLER                          PIC X(01)  VALUE SPACES. 02805202
           05  FILLER                          PIC X(17)                02805303
                             VALUE 'CLASS DESCRIPTION'.                 02805402
           05  FILLER                          PIC X(04)  VALUE SPACES. 02805502
           05  FILLER                          PIC X(05)                02805602
                             VALUE 'BRAND'.                             02805702
           05  FILLER                          PIC X(16)  VALUE SPACES. 02805802
           05  FILLER                          PIC X(05)                02805902
                             VALUE 'STYLE'.                             02806002
           05  FILLER                          PIC X(16)  VALUE SPACES. 02806102
           05  FILLER                          PIC X(17)                02806202
                             VALUE 'STYLE DESCRIPTION'.                 02806302
           05  FILLER                          PIC X(14)  VALUE SPACES. 02806402
           05  FILLER                          PIC X(06)                02806502
                             VALUE 'ONHAND'.                            02806602
           05  FILLER                          PIC X(01)  VALUE SPACES. 02806702
       01  RH-HEADINGS-6.                                               02806802
           05  FILLER                          PIC X(04)                02807003
                             VALUE '===='.                              02807103
           05  FILLER                          PIC X(01)  VALUE SPACES. 02807203
           05  FILLER                          PIC X(20)                02807302
                             VALUE '===================='.              02807402
           05  FILLER                          PIC X(01)  VALUE SPACES. 02807503
           05  FILLER                          PIC X(05)                02807602
                             VALUE '====='.                             02807702
           05  FILLER                          PIC X(01)  VALUE SPACES. 02807802
           05  FILLER                          PIC X(20)                02807903
                             VALUE '===================='.              02808002
           05  FILLER                          PIC X(01)  VALUE SPACES. 02808103
           05  FILLER                          PIC X(20)                02808202
                             VALUE '===================='.              02808302
           05  FILLER                          PIC X(01)  VALUE SPACES. 02808403
           05  FILLER                          PIC X(20)                02808502
                             VALUE '===================='.              02808602
           05  FILLER                          PIC X(01)  VALUE SPACES. 02808703
           05  FILLER                          PIC X(30)                02808802
                             VALUE '=============================='.    02808902
           05  FILLER                          PIC X(01)  VALUE SPACES. 02809003
           05  FILLER                          PIC X(06)                02809102
                             VALUE '======'.                            02809202
           05  FILLER                          PIC X(01)  VALUE SPACES. 02809302
       01  RH-DETAIL-LINE.                                              02809402
           05  RH-DL-DEPT-NBR                  PIC ZZZZ   VALUE ZEROS.  02809603
           05  FILLER                          PIC X(01)  VALUE SPACES. 02809703
           05  RH-DL-DEPT-DESC                 PIC X(20)  VALUE SPACES. 02809802
           05  FILLER                          PIC X(02)  VALUE SPACES. 02809903
           05  RH-DL-SUB-CL-NBR                PIC ZZZZ   VALUE ZEROS.  02810003
           05  FILLER                          PIC X(01)  VALUE SPACES. 02811003
           05  RH-DL-SUB-CL-DESC               PIC X(20)  VALUE SPACES. 02812002
           05  FILLER                          PIC X(01)  VALUE SPACES. 02820002
           05  RH-DL-LABEL-BRAND               PIC X(20)  VALUE SPACES. 02821002
           05  FILLER                          PIC X(01)  VALUE SPACES. 02822002
           05  RH-DL-STYLE-VS-ID               PIC X(20)  VALUE SPACES. 02823002
           05  FILLER                          PIC X(01)  VALUE SPACES. 02824002
           05  RH-DL-STYLE-VS-DESC             PIC X(30)  VALUE SPACES. 02825002
           05  FILLER                          PIC X(01)  VALUE SPACES. 02826002
           05  RH-DL-STORE-ON-HAND             PIC -ZZZZ9 VALUE ZEROS.  02827002
           05  FILLER                          PIC X(01)  VALUE SPACES. 02828002
       01  RH-BLANK-LINE.                                               04371002
           05  FILLER                          PIC X(132) VALUE SPACES. 04372002
       01  RH-FOOTERS-1.                                                04440002
           05  FILLER                          PIC X(54)  VALUE SPACES. 04450001
           05  FILLER                          PIC X(19)                04460001
               VALUE '***END OF REPORT***'.                             04470001
           05  FILLER                          PIC X(59)  VALUE SPACES. 04471002
      ******************************************************************04472007
      **ERROR HANDLING                                                  04473007
      ******************************************************************04474007
       01  PV-MISC-ABEND-FIELDS.                                        04475007
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   04476007
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   04477007
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   04478007
      ******************************************************************04480001
      **COPYBOOKS                                                       04490001
      ******************************************************************04500001
      ***STORE INPUT FILE FORMAT                                        04530002
           COPY PXRD104.                                                04540002
      ***STANDARD KOHLS HEADING FOR 132 BYTE REPORT                     04610001
           COPY DPWS132O.                                               04620001
      *----------------------------------------------------------------*04621007
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        04622007
      *----------------------------------------------------------------*04623007
           COPY DPWS004.                                                04624007
                                                                        04625007
      *----------------------------------------------------------------*04626007
      *  DB2 ABEND COPYBOOK                                             04627007
      *----------------------------------------------------------------*04628007
           COPY DPWS900.                                                04629007
                                                                        04629107
      *----------------------------------------------------------------*04629207
      *  DB2 MESSAGE AREA                                               04629307
      *----------------------------------------------------------------*04629407
           COPY SQLCA2.                                                 04629507
                                                                        04629607
      *----------------------------------------------------------------*04629707
      *  DB2 COMMUNICATIONS AREA                                        04629807
      *----------------------------------------------------------------*04629907
           EXEC SQL                                                     04630007
             INCLUDE SQLCA                                              04630107
           END-EXEC.                                                    04630207
      *----------------------------------------------------------------*04630307
      *  DB2 TABLE XST_LOC                                              04630407
      *----------------------------------------------------------------*04630507
           EXEC SQL                                                     04630607
               INCLUDE XST00001                                         04630707
           END-EXEC.                                                    04630807
                                                                        04700001
      ***************************************************************** 04701007
       PROCEDURE DIVISION.                                              04710001
      ***************************************************************** 04720001
       0000-MAINLINE.                                                   04732002
                                                                        04733002
           PERFORM 1000-INITIALIZATION.                                 04740001
                                                                        04750002
           PERFORM 2000-PROCESS-RECORDS                                 04760002
                   UNTIL PS-END-OF-INPUT-FILE                           04770002
                                                                        04780002
           PERFORM 9100-WRAP-UP.                                        04830009
           GOBACK.                                                      04840001
                                                                        04860002
      ******************************************************************04870001
      **INITIALIZATION AREA                                             04880001
      ******************************************************************04890001
       1000-INITIALIZATION.                                             04900001
                                                                        04910001
           OPEN INPUT  INPUT-FILE.                                      04940002
           OPEN OUTPUT REPORT-FILE.                                     05010002
                                                                        05020001
      **RETRIEVE DATE FROM SYSTEM                                       05030001
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             05040001
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             05050001
                                                                        05060001
           MOVE DF-ACCEPT-YR          TO DF-FORMAT-DATE-YY.             05070001
           MOVE DF-ACCEPT-MO          TO DF-FORMAT-DATE-MM.             05080001
           MOVE DF-ACCEPT-DY          TO DF-FORMAT-DATE-DD.             05090001
                                                                        05100001
           PERFORM 2999-READ-INPUT-FILE.                                05281002
           IF PS-NOT-END-OF-INPUT-FILE                                  05282002
              PERFORM 1100-HEADING-PREP                                 05283002
              PERFORM 2100-WRITE-HEADINGS                               05284002
              MOVE PXRD104-STORE-NBR TO PH-STORE-NBR-HOLD               05284103
              MOVE PXRD104-FLOOR-PAD-CDE TO PH-FLOOR-PAD-HOLD           05284203
           END-IF.                                                      05285002
                                                                        05286002
      ******************************************************************05460001
      **MOVE DATA TO FIELDS IN HEADING FOR REPORT                       05470001
      ******************************************************************05480001
       1100-HEADING-PREP.                                               05490001
                                                                        05500001
      ***PREPS KOHLS STANDARD HEADING                                   05530001
           MOVE PC-CURRENT-PROGRAM-NAME                                 05540001
                                     TO DP132O-PROGRAM-NAME.            05550001
           MOVE DF-FORMAT-DATE       TO DP132O-RUN-DATE.                05560001
           MOVE DF-ACCEPT-HR         TO DP132O-RUN-HOUR.                05570001
           MOVE DF-ACCEPT-MN         TO DP132O-RUN-MINUTE.              05580001
           MOVE 1                    TO DP132O-REPORT-NUMBER.           05590002
                                                                        05610001
      ******************************************************************06170001
      **PROCESSES INPUT FILE                                            06180001
      ******************************************************************06190001
       2000-PROCESS-RECORDS.                                            06200001
                                                                        06210001
           EVALUATE TRUE                                                06240001
              WHEN PXRD104-STORE-NBR EQUAL PH-STORE-NBR-HOLD            06250003
                                                                        06251002
                   IF  PC-REPORT-LINE-COUNTER > PC-LINES-PER-PAGE-LIMIT 06260003
                    OR PXRD104-FLOOR-PAD-CDE NOT EQUAL PH-FLOOR-PAD-HOLD06270003
                       PERFORM 2100-WRITE-HEADINGS                      06290001
      *->RESET DEPT/SUBCL HOLDS SO THEY PRINT ON NEXT PAGE REGARDLESS   06291003
                       INITIALIZE PH-PROGRAM-HOLDS-2                    06300001
                       MOVE PXRD104-FLOOR-PAD-CDE                       06310003
                                        TO PH-FLOOR-PAD-HOLD            06320003
                   END-IF                                               06330001
                   PERFORM 2200-WRITE-DETAIL                            06340001
                                                                        06341002
              WHEN PXRD104-STORE-NBR NOT EQUAL PH-STORE-NBR-HOLD        06350003
                                                                        06351002
                   PERFORM 9200-WRITE-FOOTER                            06352010
                   INITIALIZE PC-PAGE-COUNTER                           06360001
                   PERFORM 2100-WRITE-HEADINGS                          06470001
      *->RESET DEPT/SUBCL HOLDS SO THEY PRINT ON NEXT PAGE REGARDLESS   06471005
                   INITIALIZE PH-PROGRAM-HOLDS-2                        06480001
                   PERFORM 2200-WRITE-DETAIL                            06490001
                   MOVE PXRD104-STORE-NBR TO PH-STORE-NBR-HOLD          06500003
                   MOVE PXRD104-FLOOR-PAD-CDE TO PH-FLOOR-PAD-HOLD      06501003
                                                                        06502003
           END-EVALUATE.                                                06530001
                                                                        06540001
           PERFORM 2999-READ-INPUT-FILE.                                06550001
                                                                        06560001
      ***************************************************************** 06780001
      **WRITE REPORT HEADERS                                            06790001
      ***************************************************************** 06800001
       2100-WRITE-HEADINGS.                                             06810001
                                                                        06820001
           INITIALIZE PC-REPORT-LINE-COUNTER.                           06850001
                                                                        06860003
           ADD +1                      TO PC-PAGE-COUNTER.              06870001
           MOVE PC-PAGE-COUNTER        TO DP132O-PAGE-NUMBER.           06880001
                                                                        06890001
           MOVE PXRD104-STORE-NBR      TO RH-HEADINGS-1-STR-NBR.        06900003
                                                                        06901007
           IF PXRD104-STORE-NBR NOT EQUAL PH-STORE-NBR-HOLD             06901107
              PERFORM 3000-LOOKUP-STORE-NAME                            06901207
           ELSE                                                         06901407
              MOVE PH-STORE-NAME-HOLD  TO RH-HEADINGS-1-STR-NAME        06901507
           END-IF.                                                      06901607
                                                                        06904007
           MOVE PXRD104-CONSOLIDATE-EFF-DTE                             06911003
                                       TO RH-HEADINGS-2-CNSLDTN-DTE.    06912003
           MOVE PXRD104-FLOOR-PAD-CDE  TO RH-HEADINGS-3-FLOOR-PAD-CDE.  06920003
           MOVE PXRD104-FLOOR-PAD-DESC TO RH-HEADINGS-3-FLOOR-PAD-DESC. 06930003
                                                                        06940003
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      07010001
                               AFTER ADVANCING PAGE.                    07020001
           WRITE REPORT-RECORD FROM RH-TITLE-1 AFTER ADVANCING 1 LINE.  07060002
           WRITE REPORT-RECORD FROM RH-TITLE-2 AFTER ADVANCING 1 LINE.  07110001
                                                                        07120001
           WRITE REPORT-RECORD FROM RH-HEADINGS-1                       07130001
                               AFTER ADVANCING 1 LINES.                 07140003
           WRITE REPORT-RECORD FROM RH-HEADINGS-2                       07150001
                               AFTER ADVANCING 1 LINES.                 07160001
           WRITE REPORT-RECORD FROM RH-HEADINGS-3                       07170001
                               AFTER ADVANCING 1 LINES.                 07180001
           WRITE REPORT-RECORD FROM RH-BLANK-LINE                       07181003
                               AFTER ADVANCING 1 LINES.                 07182003
           WRITE REPORT-RECORD FROM RH-HEADINGS-4                       07183003
                               AFTER ADVANCING 1 LINES.                 07184003
           WRITE REPORT-RECORD FROM RH-HEADINGS-5                       07185003
                               AFTER ADVANCING 1 LINES.                 07186003
           WRITE REPORT-RECORD FROM RH-HEADINGS-6                       07187003
                               AFTER ADVANCING 1 LINES.                 07188003
                                                                        07290001
           ADD +10             TO PC-REPORT-LINE-COUNTER.               07300003
                                                                        07310001
      ***************************************************************** 07330001
      **WRITE DETAIL LINE                                               07340001
      ***************************************************************** 07350001
       2200-WRITE-DETAIL.                                               07360001
                                                                        07370001
      *->WIPE OUT PREVIOUS DETAIL LINE DATA                             07380003
           INITIALIZE RH-DETAIL-LINE.                                   07400003
                                                                        07410003
      *->WHEN THE NEW RECORD HAS A DIFFERENT VALUE THAN THE PREVIOUS    07411003
      *->PUT THE FIELDS TO THE OUTPUT FILE, BUT WHEN EQUAL SKIP MOVES   07412003
           IF PH-DEPT-NBR-HOLD NOT EQUAL PXRD104-DEPT-NBR               07420003
              MOVE PXRD104-DEPT-NBR   TO RH-DL-DEPT-NBR                 07430003
              MOVE PXRD104-DEPT-DESC  TO RH-DL-DEPT-DESC                07431003
              MOVE PXRD104-SUB-CL-NBR TO RH-DL-SUB-CL-NBR               07432004
              MOVE PXRD104-SUB-CL-DESC TO RH-DL-SUB-CL-DESC             07433004
           END-IF.                                                      07440001
           IF PH-SUB-CLSS-HOLD NOT EQUAL PXRD104-SUB-CL-NBR             07460003
              MOVE PXRD104-SUB-CL-NBR TO RH-DL-SUB-CL-NBR               07470004
              MOVE PXRD104-SUB-CL-DESC TO RH-DL-SUB-CL-DESC             07471004
           END-IF.                                                      07480001
                                                                        07490001
           MOVE PXRD104-LABEL-NAME      TO RH-DL-LABEL-BRAND.           07500003
           MOVE PXRD104-VS-STYLE-NBR    TO RH-DL-STYLE-VS-ID.           07510003
           MOVE PXRD104-VS-STYLE-DESC   TO RH-DL-STYLE-VS-DESC.         07520003
           MOVE PXRD104-FS-ON-HANDS-QTY TO RH-DL-STORE-ON-HAND.         07530003
                                                                        07820001
      *->EVEN THOUGH OUR SORTS REMOVE ZERO QTY ROWS SOME NEGATIVE       07821005
      *->ON-HANDS SUMMED WITH SMALL POSITIVE ONHANDS CAN STILL GIVE     07822005
      *->A NET OF ZERO QTY. WE DON'T WANT TO DISPLAY A ZERO QTY ROW.    07823005
           IF PXRD104-FS-ON-HANDS-QTY > 0                               07830005
              WRITE REPORT-RECORD FROM RH-DETAIL-LINE                   07850005
      *->SAVE THE DEPT, & SUB CLASS NBR TO COMPARE TO THE NEXT RECORD   07861003
              MOVE PXRD104-DEPT-NBR       TO PH-DEPT-NBR-HOLD           07870005
              MOVE PXRD104-SUB-CL-NBR     TO PH-SUB-CLSS-HOLD           07880005
      *->IF WE WROTE A REPORT RECORD INCREMENT THE COUNTERS             07890005
              ADD +1                      TO PC-REPORT-LINE-COUNTER     07891005
              ADD +1                      TO PC-REPORT-RECORDS-WRITTEN  07900005
           END-IF.                                                      07901005
                                                                        07910001
      ***************************************************************** 08070001
      **READ INPUT FILE                                                 08080001
      ***************************************************************** 08090001
       2999-READ-INPUT-FILE.                                            08100001
                                                                        08110001
           READ INPUT-FILE INTO PXRD104-STR-CNSLDTN-RPT-DATA            08120003
                AT END SET PS-END-OF-INPUT-FILE TO TRUE                 08130001
           END-READ.                                                    08140001
                                                                        08150001
           IF  PS-NOT-END-OF-INPUT-FILE                                 08160001
               ADD +1 TO PC-INPUT-RECORDS-READ                          08170001
           END-IF.                                                      08180001
                                                                        08190001
      ******************************************************************08200007
      **FIND THE STORE'S NAME                                           08210007
      ******************************************************************08220007
       3000-LOOKUP-STORE-NAME.                                          08230007
                                                                        08240007
           INITIALIZE DCLXST00001.                                      08250007
           MOVE PXRD104-STORE-NBR TO XST00001-LOC-NBR.                  08260007
                                                                        08270007
           EXEC SQL                                                     08280007
                 SELECT LOC_NM                                          08290007
                   INTO :XST00001-LOC-NM                                08300007
                   FROM XST_LOC                                         08310007
                  WHERE LOC_NBR = :XST00001-LOC-NBR                     08320007
           END-EXEC.                                                    08330007
                                                                        08340007
           EVALUATE TRUE                                                08350007
              WHEN SQLCODE = +0                                         08360007
                 MOVE XST00001-LOC-NM  TO RH-HEADINGS-1-STR-NAME        08370007
              WHEN OTHER                                                08380007
                 MOVE SPACES           TO RH-HEADINGS-1-STR-NAME        08390007
           END-EVALUATE.                                                08400007
           MOVE RH-HEADINGS-1-STR-NAME TO PH-STORE-NAME-HOLD.           08401007
                                                                        08410007
      ****************************************************************  09790001
      *END OF PROGRAM ROUTINE                                           09800001
      ****************************************************************  09810001
       9100-WRAP-UP.                                                    09820009
                                                                        09830001
           IF  PC-REPORT-RECORDS-WRITTEN > 0                            09840001
               PERFORM 9200-WRITE-FOOTER                                09850010
           END-IF.                                                      09860001
                                                                        09870001
           CLOSE INPUT-FILE.                                            09880003
           CLOSE REPORT-FILE.                                           09950003
                                                                        09960001
           PERFORM 9999-DISPLAY-STATISTICS.                             09970001
                                                                        09980001
      ***************************************************************** 09990010
      **WRITE FOOTER                                                    09991010
      ***************************************************************** 09992010
       9200-WRITE-FOOTER.                                               09993010
                                                                        09994010
           WRITE REPORT-RECORD FROM RH-FOOTERS-1.                       09995010
                                                                        09996010
      ****************************************************************  10000001
      *DISPLAY PROGRAM STATISTICS                                       10010001
      ****************************************************************  10020001
       9999-DISPLAY-STATISTICS.                                         10030001
                                                                        10040001
           DISPLAY '*************************************************'. 10050001
           DISPLAY '* PROGRAM...............: ' PC-CURRENT-PROGRAM-NAME.10060001
           DISPLAY '* DATE..................: ' DF-ACCEPT-DATE.         10070001
           DISPLAY '* TIMESTAMP.............: ' DF-ACCEPT-TIME.         10080001
           DISPLAY '*************************************************'. 10090001
           DISPLAY '* INPUT RECORDS READ.........: '                    10180001
                                          PC-INPUT-RECORDS-READ.        10190001
           DISPLAY '* REPORT RECORDS WRITTEN.....: '                    10220001
                                          PC-REPORT-RECORDS-WRITTEN.    10230001
           DISPLAY '*************************************************'. 10280001
                                                                        10290001
      ****************************************************************  10300003
