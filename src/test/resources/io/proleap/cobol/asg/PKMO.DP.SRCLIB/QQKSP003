      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.      QQKSP003.                                               
       AUTHOR.          SIVA NAGISETTY.                                         
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    MARCH 2007.                                             
       DATE-COMPILED.   MARCH 2007.                                             
                                                                                
      ******************************************************************        
      * THIS DB2 STORED PROCEDURE WILL INSERT/UPDATE VENDOR ADS        *        
      * TABLE QQT_EXENT_REP_CNTCT.                                     *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE                    
                                                       'QQKSP003'.              
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.            
           05  PV-CURRENT-TMST              PIC  X(26) VALUE SPACES.            
           05  PV-DB2-OPERATION-ATTEMPTED   PIC  X(30) VALUE SPACES.            
           05  PV-HOLD-SQLCODE              PIC S9(09) VALUE ZEROES             
                                                           COMP.                
           05  PV-FULL-TEL-NBR.                                                 
               10 PV-CNTRY-CDE             PIC X(3).                            
      *        10 FILLER                   PIC X VALUE SPACES.                  
               10 PV-TEL-NBR               PIC X(18).                           
      *        10 FILLER                   PIC X VALUE SPACES.                  
               10 PV-TEL-EXT               PIC X(4).                            
                                                                                
           05 PV-HOLD-PREV-CNTCT-TEXT.                                          
               49 HOLD-PREV-CNTCT-TXT-LEN        PIC S9(4) COMP.                
               49 HOLD-PREV-CNTCT-TXT-TEXT       PIC X(250).                    
                                                                                
           05  PS-ABEND-SW                  PIC  X(01) VALUE 'N'.               
               88  PS-NO-ABEND                         VALUE 'N'.               
               88  PS-ABEND                            VALUE 'Y'.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-SQL-ERROR-SW          PIC X          VALUE 'N'.               
               88  PS-SQL-ERROR                        VALUE 'Y'.               
               88  PS-NO-SQL-ERROR                     VALUE 'N'.               
                                                                                
      ******************************************************************        
      * MSG   ERROR MESSAGE TO BE DISPLAYED IN THE STORED PROCEDURE    *        
      *       ADDRESS SPACE                                            *        
      ******************************************************************        
       01  MSG-ERROR-DISPLAY.                                                   
           05  FILLER                       PIC  X(05) VALUE                    
               'ERROR'.                                                         
           05  FILLER                       PIC  X(01) VALUE SPACES.            
           05  FILLER                       PIC  X(01) VALUE '-'.               
           05  FILLER                       PIC  X(01) VALUE SPACES.            
           05  MSG-STORED-PROCEDURE-NAME    PIC  X(08) VALUE SPACES.            
           05  FILLER                       PIC  X(01) VALUE SPACES.            
           05  MSG-SGN-NBR                  PIC  X(10) VALUE SPACES.            
           05  FILLER                       PIC  X(01) VALUE ':'.               
           05  FILLER                       PIC  X(02) VALUE SPACES.            
                                                                                
      ******************************************************************        
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *        
      ******************************************************************        
                                                                                
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *    QQT_EXENT_REP  - REPRESENTITIVE                             *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE QQT00010                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    QQT_EXENT_REP_CNTCT - REPRESENTITIVE CONTRACT               *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE QQT00011                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    SQL COMMUNICATIONS AREA DCLGEN                              *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * AC ABEND CODES                                                 *        
      ******************************************************************        
       01  ABEND-CODE                       PIC S9(04) VALUE ZEROES             
                                                       COMP                     
                                                       SYNC.                    
           88  AC-ABEND-4013-DB2-ERROR                 VALUE +4013.             
                                                                                
      ******************************************************************        
       LINKAGE SECTION.                                                         
      ******************************************************************        
       01  LS-ACTION-IND                  PIC  X(1).                            
       01  LS-ENT-ID-PREV                 PIC  S9(9) COMP.                      
       01  LS-ENT-ID-CURR                 PIC  S9(9) COMP.                      
       01  LS-FUNCTION-ID-PREV            PIC  S9(4) COMP.                      
       01  LS-FUNCTION-ID-CURR            PIC  S9(4) COMP.                      
       01  LS-ENT-BUS-LOC-ID-PREV         PIC  S9(4) COMP.                      
       01  LS-ENT-BUS-LOC-ID-CURR         PIC  S9(4) COMP.                      
       01  LS-SEQ-NBR-PREV                PIC  S9(4) COMP.                      
       01  LS-SEQ-NBR-CURR                PIC  S9(4) COMP.                      
       01  LS-FIRST-NAME-PREV             PIC  X(15).                           
       01  LS-FIRST-NAME-CURR             PIC  X(15).                           
       01  LS-LAST-NAME-PREV              PIC  X(15).                           
       01  LS-LAST-NAME-CURR              PIC  X(15).                           
       01  LS-TITLE-NAME-PREV             PIC  X(35).                           
       01  LS-TITLE-NAME-CURR             PIC  X(35).                           
       01  LS-TEL-1-CNTRY-CDE-PREV        PIC  X(3).                            
       01  LS-TEL-1-CNTRY-CDE-CURR        PIC  X(3).                            
       01  LS-TEL1-NBR-PREV               PIC  X(18).                           
       01  LS-TEL1-NBR-CURR               PIC  X(18).                           
       01  LS-TEL1-EXT-NBR-PREV           PIC  X(4).                            
       01  LS-TEL1-EXT-NBR-CURR           PIC  X(4).                            
       01  LS-TEL-2-CNTRY-CDE-PREV        PIC  X(3).                            
       01  LS-TEL-2-CNTRY-CDE-CURR        PIC  X(3).                            
       01  LS-TEL2-NBR-PREV               PIC  X(18).                           
       01  LS-TEL2-NBR-CURR               PIC  X(18).                           
       01  LS-TEL2-EXT-NBR-PREV           PIC  X(4).                            
       01  LS-TEL2-EXT-NBR-CURR           PIC  X(4).                            
       01  LS-FAX-NBR-PREV                PIC  X(24).                           
       01  LS-FAX-NBR-CURR                PIC  X(24).                           
       01  LS-EMAIL-ADDR-ID-PREV          PIC  X(64).                           
       01  LS-EMAIL-ADDR-ID-CURR          PIC  X(64).                           
       01  LS-LAST-UPD-TMST-PREV          PIC  X(26).                           
       01  LS-LAST-UPD-TMST-CURR          PIC  X(26).                           
       01  LS-LAST-UPDATE-ID-PREV         PIC  X(8).                            
       01  LS-LAST-UPDATE-ID-CURR         PIC  X(8).                            
                                                                                
       PROCEDURE DIVISION USING   LS-ACTION-IND                                 
                                , LS-ENT-ID-PREV                                
                                , LS-ENT-ID-CURR                                
                                , LS-FUNCTION-ID-PREV                           
                                , LS-FUNCTION-ID-CURR                           
                                , LS-ENT-BUS-LOC-ID-PREV                        
                                , LS-ENT-BUS-LOC-ID-CURR                        
                                , LS-SEQ-NBR-PREV                               
                                , LS-SEQ-NBR-CURR                               
                                , LS-FIRST-NAME-PREV                            
                                , LS-FIRST-NAME-CURR                            
                                , LS-LAST-NAME-PREV                             
                                , LS-LAST-NAME-CURR                             
                                , LS-TITLE-NAME-PREV                            
                                , LS-TITLE-NAME-CURR                            
                                , LS-TEL-1-CNTRY-CDE-PREV                       
                                , LS-TEL-1-CNTRY-CDE-CURR                       
                                , LS-TEL1-NBR-PREV                              
                                , LS-TEL1-NBR-CURR                              
                                , LS-TEL1-EXT-NBR-PREV                          
                                , LS-TEL1-EXT-NBR-CURR                          
                                , LS-TEL-2-CNTRY-CDE-PREV                       
                                , LS-TEL-2-CNTRY-CDE-CURR                       
                                , LS-TEL2-NBR-PREV                              
                                , LS-TEL2-NBR-CURR                              
                                , LS-TEL2-EXT-NBR-PREV                          
                                , LS-TEL2-EXT-NBR-CURR                          
                                , LS-FAX-NBR-PREV                               
                                , LS-FAX-NBR-CURR                               
                                , LS-EMAIL-ADDR-ID-PREV                         
                                , LS-EMAIL-ADDR-ID-CURR                         
                                , LS-LAST-UPD-TMST-PREV                         
                                , LS-LAST-UPD-TMST-CURR                         
                                , LS-LAST-UPDATE-ID-PREV                        
                                , LS-LAST-UPDATE-ID-CURR.                       
                                                                                
      ******************************************************************        
       A100-MAINLINE.                                                           
      ******************************************************************        
                                                                                
      **   DISPLAY PC-PROGRAM-NAME ':  STARTING PROCEDURE'.                     
                                                                                
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM B200-PROCESS-TRIGGERS.                                       
                                                                                
      **   DISPLAY PC-PROGRAM-NAME ':  ENDING PROCEDURE'.                       
                                                                                
           GOBACK.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
      *     B100-INITIALIZE                                            *        
      * ==> PROCESSES:                                                 *        
      *     - INITIALIZE THE PROGRAM VARIABLES.                        *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       B100-INITIALIZE.                                                         
                                                                                
                                                                                
           MOVE ZEROES                   TO PV-HOLD-SQLCODE.                    
           MOVE SPACES                   TO PV-PARAGRAPH-NAME.                  
                                                                                
           EXEC SQL                                                             
               SET :PV-CURRENT-TMST = CURRENT TIMESTAMP                         
           END-EXEC.                                                            
                                                                                
      **   DISPLAY 'TIMESTAMP:'  PV-CURRENT-TMST.                               
                                                                                
                                                                                
      ******************************************************************        
      *     B200-PROCESS-TRIGGERS.                                     *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT / UPDATE ROWS FROM QQT_EXENT_REP_CNTCT            *        
      *       DEPENDING ON ACTION INDICATOR.                           *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
                                                                                
       B200-PROCESS-TRIGGERS.                                                   
                                                                                
           EVALUATE LS-ACTION-IND                                               
                                                                                
             WHEN 'I'                                                           
                                                                                
               PERFORM C100-INSERT-EXENT-REP                                    
               PERFORM C200-INSERT-EXENT-REP-CNTCT                              
                                                                                
             WHEN 'U'                                                           
                                                                                
               PERFORM D100-UPDATE-EXENT-REP                                    
               PERFORM D200-UPDATE-EXENT-REP-CNTCT                              
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     C100-INSERT-EXENT-REP.                                     *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXENT_REP.                          *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       C100-INSERT-EXENT-REP.                                                   
                                                                                
      *  INSERT ROW INTO QQT_EXENT_REP.                                         
                                                                                
            MOVE  LS-ENT-ID-CURR         TO QQT00010-EXENT-ID.                  
            MOVE  LS-FUNCTION-ID-CURR    TO QQT00010-ADDR-FUNC-ID.              
            MOVE  LS-ENT-BUS-LOC-ID-CURR TO QQT00010-EXENT-ADDR-SEQ-NBR.        
            MOVE  LS-SEQ-NBR-CURR        TO QQT00010-REP-SEQ-NBR.               
            MOVE  LS-FIRST-NAME-CURR     TO QQT00010-FRST-NM.                   
            MOVE  LS-LAST-NAME-CURR      TO QQT00010-LAST-NM.                   
            MOVE  LS-TITLE-NAME-CURR     TO QQT00010-TITL-NM.                   
            MOVE  LS-LAST-UPD-TMST-CURR  TO QQT00010-SOR-LAST-UPD-TMST.         
            MOVE  LS-LAST-UPDATE-ID-CURR TO QQT00010-SOR-LAST-UPD-USRID.        
                                                                                
            PERFORM C150-INS-EXENT-REP-ROWS.                                    
                                                                                
      ******************************************************************        
      *     C150-INS-EXENT-REP-ROWS.                                   *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXENT_REP.                          *        
      * ==> PERFORMED FROM: C100-INSERT-EXENT-REP.                     *        
      ******************************************************************        
       C150-INS-EXENT-REP-ROWS.                                                 
                                                                                
           EXEC SQL                                                             
               INSERT INTO QQT_EXENT_REP                                        
                  ( EXENT_ID                                                    
                   ,ADDR_FUNC_ID                                                
                   ,EXENT_ADDR_SEQ_NBR                                          
                   ,REP_SEQ_NBR                                                 
                   ,FRST_NM                                                     
                   ,LAST_NM                                                     
                   ,TITL_NM                                                     
                   ,SOR_LAST_UPD_TMST                                           
                   ,SOR_LAST_UPD_USRID)                                         
               VALUES                                                           
                  ( :QQT00010-EXENT-ID                                          
                   ,:QQT00010-ADDR-FUNC-ID                                      
                   ,:QQT00010-EXENT-ADDR-SEQ-NBR                                
                   ,:QQT00010-REP-SEQ-NBR                                       
                   ,:QQT00010-FRST-NM                                           
                   ,:QQT00010-LAST-NM                                           
                   ,:QQT00010-TITL-NM                                           
                   ,:QQT00010-SOR-LAST-UPD-TMST                                 
                   ,:QQT00010-SOR-LAST-UPD-USRID)                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'C150-INSERT-REP        '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'INSERT ERROR ON REP        '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *     C200-INSERT-EXENT-REP-CNTCT.                               *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXENT_REP_CNTC.                     *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       C200-INSERT-EXENT-REP-CNTCT.                                             
                                                                                
      *  INSERT ROW WITH EMAIL.                                                 
                                                                                
           IF LS-EMAIL-ADDR-ID-CURR NOT =  SPACES                               
                                                                                
            MOVE  LS-ENT-ID-CURR         TO QQT00011-EXENT-ID                   
            MOVE  LS-FUNCTION-ID-CURR    TO QQT00011-ADDR-FUNC-ID               
            MOVE  LS-ENT-BUS-LOC-ID-CURR TO QQT00011-EXENT-ADDR-SEQ-NBR         
            MOVE  LS-SEQ-NBR-CURR        TO QQT00011-REP-SEQ-NBR                
            MOVE  'EML'                  TO QQT00011-CNTCT-TYP-CDE              
            MOVE  LS-EMAIL-ADDR-ID-CURR  TO QQT00011-CNTCT-TXT-TEXT             
            MOVE  +250                   TO QQT00011-CNTCT-TXT-LEN              
            MOVE  LS-LAST-UPD-TMST-CURR  TO QQT00011-SOR-LAST-UPD-TMST          
            MOVE  LS-LAST-UPDATE-ID-CURR TO QQT00011-SOR-LAST-UPD-USRID         
                                                                                
                                                                                
            PERFORM C250-INS-EXENT-REP-CNTC-ROWS                                
                                                                                
           END-IF.                                                              
                                                                                
      *  INSERT ROW WITH TEL-1.                                                 
                                                                                
           IF LS-TEL1-NBR-CURR NOT =  SPACES                                    
                                                                                
            MOVE  LS-ENT-ID-CURR          TO QQT00011-EXENT-ID                  
            MOVE  LS-FUNCTION-ID-CURR     TO QQT00011-ADDR-FUNC-ID              
            MOVE  LS-ENT-BUS-LOC-ID-CURR  TO QQT00011-EXENT-ADDR-SEQ-NBR        
            MOVE  LS-SEQ-NBR-CURR         TO QQT00011-REP-SEQ-NBR               
            MOVE  'TEL'                   TO QQT00011-CNTCT-TYP-CDE             
                                                                                
            MOVE  LS-TEL-1-CNTRY-CDE-CURR TO PV-CNTRY-CDE                       
            MOVE  LS-TEL1-NBR-CURR        TO PV-TEL-NBR                         
            MOVE  LS-TEL1-EXT-NBR-CURR    TO PV-TEL-EXT                         
            MOVE  SPACES                  TO QQT00011-CNTCT-TXT-TEXT            
            MOVE  PV-FULL-TEL-NBR         TO QQT00011-CNTCT-TXT-TEXT            
            MOVE  +250                    TO QQT00011-CNTCT-TXT-LEN             
                                                                                
            MOVE  LS-LAST-UPD-TMST-CURR   TO QQT00011-SOR-LAST-UPD-TMST         
            MOVE  LS-LAST-UPDATE-ID-CURR  TO QQT00011-SOR-LAST-UPD-USRID        
                                                                                
            PERFORM C250-INS-EXENT-REP-CNTC-ROWS                                
                                                                                
           END-IF.                                                              
                                                                                
      *  INSERT ROW WITH TEL-2.                                                 
                                                                                
           IF LS-TEL2-NBR-CURR NOT =  SPACES                                    
                                                                                
            MOVE  LS-ENT-ID-CURR          TO QQT00011-EXENT-ID                  
            MOVE  LS-FUNCTION-ID-CURR     TO QQT00011-ADDR-FUNC-ID              
            MOVE  LS-ENT-BUS-LOC-ID-CURR  TO QQT00011-EXENT-ADDR-SEQ-NBR        
            MOVE  LS-SEQ-NBR-CURR         TO QQT00011-REP-SEQ-NBR               
            MOVE  'TE2'                   TO QQT00011-CNTCT-TYP-CDE             
                                                                                
            MOVE  LS-TEL-2-CNTRY-CDE-CURR TO PV-CNTRY-CDE                       
            MOVE  LS-TEL2-NBR-CURR        TO PV-TEL-NBR                         
            MOVE  LS-TEL2-EXT-NBR-CURR    TO PV-TEL-EXT                         
            MOVE  SPACES                  TO QQT00011-CNTCT-TXT-TEXT            
            MOVE  PV-FULL-TEL-NBR         TO QQT00011-CNTCT-TXT-TEXT            
            MOVE  +250                    TO QQT00011-CNTCT-TXT-LEN             
                                                                                
            MOVE  LS-LAST-UPD-TMST-CURR   TO QQT00011-SOR-LAST-UPD-TMST         
            MOVE  LS-LAST-UPDATE-ID-CURR  TO QQT00011-SOR-LAST-UPD-USRID        
                                                                                
            PERFORM C250-INS-EXENT-REP-CNTC-ROWS                                
                                                                                
           END-IF.                                                              
                                                                                
      *  INSERT ROW WITH FAX.                                                   
                                                                                
           IF LS-FAX-NBR-CURR  NOT =  SPACES                                    
                                                                                
            MOVE  LS-ENT-ID-CURR         TO QQT00011-EXENT-ID                   
            MOVE  LS-FUNCTION-ID-CURR    TO QQT00011-ADDR-FUNC-ID               
            MOVE  LS-ENT-BUS-LOC-ID-CURR TO QQT00011-EXENT-ADDR-SEQ-NBR         
            MOVE  LS-SEQ-NBR-CURR        TO QQT00011-REP-SEQ-NBR                
            MOVE  'FAX'                  TO QQT00011-CNTCT-TYP-CDE              
            MOVE  SPACES                 TO QQT00011-CNTCT-TXT-TEXT             
            MOVE  LS-FAX-NBR-CURR        TO QQT00011-CNTCT-TXT-TEXT             
            MOVE  +250                   TO QQT00011-CNTCT-TXT-LEN              
            MOVE  LS-LAST-UPD-TMST-CURR  TO QQT00011-SOR-LAST-UPD-TMST          
            MOVE  LS-LAST-UPDATE-ID-CURR TO QQT00011-SOR-LAST-UPD-USRID         
                                                                                
            PERFORM C250-INS-EXENT-REP-CNTC-ROWS                                
                                                                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     C250-INS-EXENT-REP-CNTC-ROWS.                              *        
      * ==> PROCESSES:                                                 *        
      *     - INSERT ROWS INTO QQT_EXENT_REP_CNTC.                     *        
      * ==> PERFORMED FROM: C200-INSERT-EXENT-REP-CNTC.                *        
      ******************************************************************        
       C250-INS-EXENT-REP-CNTC-ROWS.                                            
                                                                                
                                                                                
           EXEC SQL                                                             
               INSERT INTO QQT_EXENT_REP_CNTC                                   
                  ( EXENT_ID                                                    
                   ,ADDR_FUNC_ID                                                
                   ,EXENT_ADDR_SEQ_NBR                                          
                   ,REP_SEQ_NBR                                                 
                   ,CNTCT_TYP_CDE                                               
                   ,CNTCT_TXT                                                   
                   ,SOR_LAST_UPD_TMST                                           
                   ,SOR_LAST_UPD_USRID)                                         
               VALUES                                                           
                  ( :QQT00011-EXENT-ID                                          
                   ,:QQT00011-ADDR-FUNC-ID                                      
                   ,:QQT00011-EXENT-ADDR-SEQ-NBR                                
                   ,:QQT00011-REP-SEQ-NBR                                       
                   ,:QQT00011-CNTCT-TYP-CDE                                     
                   ,:QQT00011-CNTCT-TXT                                         
                   ,:QQT00011-SOR-LAST-UPD-TMST                                 
                   ,:QQT00011-SOR-LAST-UPD-USRID)                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'C250-INSERT-REP-CNTC   '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'INSERT ERROR ON REP CNTC   '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *     D100-UPDATE-EXENT-REP.                                     *        
      * ==> PROCESSES:                                                 *        
      *     - UPDATE QQT_EXENT_REP.                                    *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       D100-UPDATE-EXENT-REP.                                                   
                                                                                
      *  UPDATE QQT_EXENT_REP WHEN NAME CHANGES.                                
                                                                                
           IF  LS-FIRST-NAME-CURR NOT = LS-FIRST-NAME-PREV                      
            OR LS-LAST-NAME-CURR NOT = LS-LAST-NAME-PREV                        
            OR LS-TITLE-NAME-CURR NOT = LS-TITLE-NAME-PREV                      
                                                                                
            MOVE  LS-ENT-ID-CURR         TO QQT00010-EXENT-ID                   
            MOVE  LS-FUNCTION-ID-CURR    TO QQT00010-ADDR-FUNC-ID               
            MOVE  LS-ENT-BUS-LOC-ID-CURR TO QQT00010-EXENT-ADDR-SEQ-NBR         
            MOVE  LS-SEQ-NBR-CURR        TO QQT00010-REP-SEQ-NBR                
            MOVE  LS-FIRST-NAME-CURR     TO QQT00010-FRST-NM                    
            MOVE  LS-LAST-NAME-CURR      TO QQT00010-LAST-NM                    
            MOVE  LS-TITLE-NAME-CURR     TO QQT00010-TITL-NM                    
            MOVE  LS-LAST-UPD-TMST-CURR  TO QQT00010-SOR-LAST-UPD-TMST          
            MOVE  LS-LAST-UPDATE-ID-CURR TO QQT00010-SOR-LAST-UPD-USRID         
                                                                                
            PERFORM D150-UPD-EXENT-REP-ROWS                                     
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *     D150-UPD-EXENT-REP-ROWS.                                   *        
      * ==> PROCESSES:                                                 *        
      *     - UPDATE QQT_EXENT_REP.                                    *        
      * ==> PERFORMED FROM: D100-UPDATE-EXENT-REP.                     *        
      ******************************************************************        
       D150-UPD-EXENT-REP-ROWS.                                                 
                                                                                
           EXEC SQL                                                             
               UPDATE QQT_EXENT_REP                                             
               SET                                                              
                    FRST_NM            = :QQT00010-FRST-NM                      
                  , LAST_NM            = :QQT00010-LAST-NM                      
                  , TITL_NM            = :QQT00010-TITL-NM                      
                  , SOR_LAST_UPD_TMST  = :QQT00010-SOR-LAST-UPD-TMST            
                  , SOR_LAST_UPD_USRID = :QQT00010-SOR-LAST-UPD-USRID           
               WHERE                                                            
                    EXENT_ID           = :QQT00010-EXENT-ID                     
                AND ADDR_FUNC_ID       = :QQT00010-ADDR-FUNC-ID                 
                AND EXENT_ADDR_SEQ_NBR = :QQT00010-EXENT-ADDR-SEQ-NBR           
                AND REP_SEQ_NBR        = :QQT00010-REP-SEQ-NBR                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'D150-UPDATE-REP        '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'UPDATE ERROR ON REP        '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     D200-UPDATE-EXENT-REP-CNTCT.                               *        
      * ==> PROCESSES:                                                 *        
      *     - UPDATE ROWS QQT_EXENT_REP_CNTC.    .                     *        
      * ==> PERFORMED FROM: B200-PROCESS-TRIGGERS.                     *        
      ******************************************************************        
       D200-UPDATE-EXENT-REP-CNTCT.                                             
                                                                                
      *  UPDATE WHEN EMAIL CHANGE.                                              
                                                                                
           IF LS-EMAIL-ADDR-ID-CURR NOT =  LS-EMAIL-ADDR-ID-PREV                
                                                                                
            MOVE  LS-ENT-ID-CURR         TO QQT00011-EXENT-ID                   
            MOVE  LS-FUNCTION-ID-CURR    TO QQT00011-ADDR-FUNC-ID               
            MOVE  LS-ENT-BUS-LOC-ID-CURR TO QQT00011-EXENT-ADDR-SEQ-NBR         
            MOVE  LS-SEQ-NBR-CURR        TO QQT00011-REP-SEQ-NBR                
            MOVE  'EML'                  TO QQT00011-CNTCT-TYP-CDE              
            MOVE  LS-EMAIL-ADDR-ID-PREV  TO HOLD-PREV-CNTCT-TXT-TEXT            
            MOVE  +250                   TO HOLD-PREV-CNTCT-TXT-LEN             
            MOVE  LS-EMAIL-ADDR-ID-CURR  TO QQT00011-CNTCT-TXT-TEXT             
            MOVE  +250                    TO QQT00011-CNTCT-TXT-LEN             
            MOVE  LS-LAST-UPD-TMST-CURR  TO QQT00011-SOR-LAST-UPD-TMST          
            MOVE  LS-LAST-UPDATE-ID-CURR TO QQT00011-SOR-LAST-UPD-USRID         
                                                                                
            PERFORM D250-UPD-EXENT-REP-CNTC-ROWS                                
                                                                                
           END-IF.                                                              
                                                                                
      *  UPDATE WHEN TEL-1 CHANGE.                                              
                                                                                
           IF LS-TEL-1-CNTRY-CDE-CURR NOT = LS-TEL-1-CNTRY-CDE-PREV             
             OR LS-TEL1-NBR-CURR      NOT = LS-TEL1-NBR-PREV                    
             OR LS-TEL1-EXT-NBR-CURR  NOT = LS-TEL1-EXT-NBR-PREV                
                                                                                
            MOVE  LS-ENT-ID-CURR          TO QQT00011-EXENT-ID                  
            MOVE  LS-FUNCTION-ID-CURR     TO QQT00011-ADDR-FUNC-ID              
            MOVE  LS-ENT-BUS-LOC-ID-CURR  TO QQT00011-EXENT-ADDR-SEQ-NBR        
            MOVE  LS-SEQ-NBR-CURR         TO QQT00011-REP-SEQ-NBR               
            MOVE  'TEL'                   TO QQT00011-CNTCT-TYP-CDE             
                                                                                
            MOVE  LS-TEL-1-CNTRY-CDE-CURR TO PV-CNTRY-CDE                       
            MOVE  LS-TEL1-NBR-CURR        TO PV-TEL-NBR                         
            MOVE  LS-TEL1-EXT-NBR-CURR    TO PV-TEL-EXT                         
            MOVE  PV-FULL-TEL-NBR         TO QQT00011-CNTCT-TXT-TEXT            
            MOVE  +250                    TO QQT00011-CNTCT-TXT-LEN             
            MOVE  SPACES                  TO PV-FULL-TEL-NBR                    
            MOVE  LS-TEL-1-CNTRY-CDE-PREV TO PV-CNTRY-CDE                       
            MOVE  LS-TEL1-NBR-PREV        TO PV-TEL-NBR                         
            MOVE  LS-TEL1-EXT-NBR-PREV    TO PV-TEL-EXT                         
            MOVE  PV-FULL-TEL-NBR         TO HOLD-PREV-CNTCT-TXT-TEXT           
            MOVE  +250                    TO HOLD-PREV-CNTCT-TXT-LEN            
                                                                                
            MOVE  LS-LAST-UPD-TMST-CURR   TO QQT00011-SOR-LAST-UPD-TMST         
            MOVE  LS-LAST-UPDATE-ID-CURR  TO QQT00011-SOR-LAST-UPD-USRID        
                                                                                
            PERFORM D250-UPD-EXENT-REP-CNTC-ROWS                                
                                                                                
           END-IF.                                                              
                                                                                
      *  UPDATE WHEN TEL-2 CHANGE.                                              
                                                                                
           IF LS-TEL-2-CNTRY-CDE-CURR NOT = LS-TEL-2-CNTRY-CDE-PREV             
             OR LS-TEL2-NBR-CURR      NOT = LS-TEL2-NBR-PREV                    
             OR LS-TEL2-EXT-NBR-CURR  NOT = LS-TEL2-EXT-NBR-PREV                
                                                                                
            MOVE  LS-ENT-ID-CURR          TO QQT00011-EXENT-ID                  
            MOVE  LS-FUNCTION-ID-CURR     TO QQT00011-ADDR-FUNC-ID              
            MOVE  LS-ENT-BUS-LOC-ID-CURR  TO QQT00011-EXENT-ADDR-SEQ-NBR        
            MOVE  LS-SEQ-NBR-CURR         TO QQT00011-REP-SEQ-NBR               
            MOVE  'TE2'                   TO QQT00011-CNTCT-TYP-CDE             
                                                                                
            MOVE  LS-TEL-2-CNTRY-CDE-CURR TO PV-CNTRY-CDE                       
            MOVE  LS-TEL2-NBR-CURR        TO PV-TEL-NBR                         
            MOVE  LS-TEL2-EXT-NBR-CURR    TO PV-TEL-EXT                         
            MOVE  PV-FULL-TEL-NBR         TO QQT00011-CNTCT-TXT-TEXT            
            MOVE  +250                    TO QQT00011-CNTCT-TXT-LEN             
            MOVE  SPACES                  TO PV-FULL-TEL-NBR                    
            MOVE  LS-TEL-2-CNTRY-CDE-PREV TO PV-CNTRY-CDE                       
            MOVE  LS-TEL2-NBR-PREV        TO PV-TEL-NBR                         
            MOVE  LS-TEL2-EXT-NBR-PREV    TO PV-TEL-EXT                         
            MOVE  PV-FULL-TEL-NBR         TO HOLD-PREV-CNTCT-TXT-TEXT           
            MOVE  +250                    TO HOLD-PREV-CNTCT-TXT-LEN            
                                                                                
            MOVE  LS-LAST-UPD-TMST-CURR   TO QQT00011-SOR-LAST-UPD-TMST         
            MOVE  LS-LAST-UPDATE-ID-CURR  TO QQT00011-SOR-LAST-UPD-USRID        
                                                                                
            PERFORM D250-UPD-EXENT-REP-CNTC-ROWS                                
                                                                                
           END-IF.                                                              
                                                                                
      *  UPDATE WHEN FAX CHANGE.                                                
                                                                                
           IF LS-FAX-NBR-CURR  NOT =  LS-FAX-NBR-PREV                           
                                                                                
            MOVE  LS-ENT-ID-CURR         TO QQT00011-EXENT-ID                   
            MOVE  LS-FUNCTION-ID-CURR    TO QQT00011-ADDR-FUNC-ID               
            MOVE  LS-ENT-BUS-LOC-ID-CURR TO QQT00011-EXENT-ADDR-SEQ-NBR         
            MOVE  LS-SEQ-NBR-CURR        TO QQT00011-REP-SEQ-NBR                
            MOVE  'FAX'                  TO QQT00011-CNTCT-TYP-CDE              
            MOVE  LS-FAX-NBR-CURR        TO QQT00011-CNTCT-TXT-TEXT             
            MOVE  +250                   TO QQT00011-CNTCT-TXT-LEN              
            MOVE  SPACES                  TO PV-FULL-TEL-NBR                    
            MOVE  LS-FAX-NBR-PREV         TO HOLD-PREV-CNTCT-TXT-TEXT           
            MOVE  +250                    TO HOLD-PREV-CNTCT-TXT-LEN            
            MOVE  LS-LAST-UPD-TMST-CURR  TO QQT00011-SOR-LAST-UPD-TMST          
            MOVE  LS-LAST-UPDATE-ID-CURR TO QQT00011-SOR-LAST-UPD-USRID         
                                                                                
            PERFORM D250-UPD-EXENT-REP-CNTC-ROWS                                
                                                                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     D250-UPD-EXENT-REP-CNTC-ROWS.                              *        
      * ==> PROCESSES:                                                 *        
      *     - UPDATE QQT_EXENT_REP_CNTC.                               *        
      * ==> PERFORMED FROM: D200-UPDATE-EXENT-REP-CNTC.                *        
      ******************************************************************        
       D250-UPD-EXENT-REP-CNTC-ROWS.                                            
                                                                                
           EXEC SQL                                                             
             UPDATE QQT_EXENT_REP_CNTC                                          
               SET                                                              
                  CNTCT_TXT          = :QQT00011-CNTCT-TXT                      
                 ,SOR_LAST_UPD_TMST  = :QQT00011-SOR-LAST-UPD-TMST              
                 ,SOR_LAST_UPD_USRID = :QQT00011-SOR-LAST-UPD-USRID             
               WHERE                                                            
                    EXENT_ID             = :QQT00011-EXENT-ID                   
                AND ADDR_FUNC_ID         = :QQT00011-ADDR-FUNC-ID               
                AND EXENT_ADDR_SEQ_NBR   = :QQT00011-EXENT-ADDR-SEQ-NBR         
                AND REP_SEQ_NBR          = :QQT00011-REP-SEQ-NBR                
                AND CNTCT_TYP_CDE        = :QQT00011-CNTCT-TYP-CDE              
                AND CNTCT_TXT            = :PV-HOLD-PREV-CNTCT-TEXT             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
               WHEN SQLCODE = +100                                              
                   PERFORM C250-INS-EXENT-REP-CNTC-ROWS                         
                                                                                
               WHEN OTHER                                                       
                   MOVE SQLCODE          TO PV-HOLD-SQLCODE                     
                   MOVE 'D250-UPDATE-REP-CNTC   '                               
                                         TO PV-PARAGRAPH-NAME                   
                   MOVE 'UPDATE ERROR ON REP CNTC   '                           
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
      * FORMAT.                                                        *        
      ******************************************************************        
      *    COPY DPPD004.                                                        
                                                                                
      *-----------------------------------------------------------------        
      * DB2 SQL ERROR PROCESSING                                                
      *-----------------------------------------------------------------        
       9998-SQL-ERROR.                                                          
                                                                                
           SET PS-SQL-ERROR TO TRUE.                                            
                                                                                
           DISPLAY '** '                                                        
                   PC-PROGRAM-NAME                                              
                   ' ABEND IN PARAGRAPH '                                       
                   PV-PARAGRAPH-NAME.                                           
           DISPLAY '** '                                                        
                   PC-PROGRAM-NAME                                              
                   ' OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                  
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES MODULE DSNTIAR TO FORMAT             
      *    THE INFO IN SQLCA INTO UNDERSTANDABLE VERBAGE.                       
                                                                                
           COPY DPPD004.                                                        
      *                                                                         
           EXEC SQL                                                             
               ROLLBACK                                                         
           END-EXEC.                                                            
