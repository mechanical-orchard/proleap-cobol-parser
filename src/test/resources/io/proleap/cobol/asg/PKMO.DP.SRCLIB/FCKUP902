       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.      FCKUP902.                                               
       AUTHOR.          STEPHANIE CISKE.                                        
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    OCTOBER 2009.                                           
       DATE-COMPILED.                                                           
      ****************************************************************          
      *       **********************************************                    
      *       **  THIS PROGRAM MUST BE COMPILED AS ORACLE **                    
      *       **********************************************                    
      *                                                                         
      * THIS WILL READ AN INPUT FROM WHICH SHOULD BE A CSV                      
      * FILE. FLOOR PAD CAPACITY ROWS WILL BE INSERTED/UPDATED                  
      *                                                                         
      *    WR/IR #          DATE       DESCRIPTION                     *        
      *    --------         ----       ------------------------------  *        
      *    INC000000938444  10/27/2009  INITIAL IMPLEMENTATION                  
      *    PKE000000002413  01/25/2010  CORRECTED MISSING DATE FMT              
      *    PRB0051977       03/20/2015  MODIFIED PROGRAM TO MOVE 'NO'           
      *                                 TO FW-FLRPD-CPCTY FOR ECOM              
      *                                 STORES.                                 
      *    PRB0051977       03/30/2015  MODIFIED PROGRAM TO INSERT 'S'          
      *                                 RECORD WITH CAPACITY GROUP 'NO'         
      *                                 FOR ECOMM STORES.                       
216764*    CHG0216764       01/29/2019  ORACLE LOGON STANDARDS                  
216764*                                 PROGRAM CHANGE.                         
253883*    CHG0253883       02/13/2021  ADDED E-COM LOCATION 849 TO             
253883*                                 LIST OF E-COM LOCATIONS.                
      ****************************************************************          
       ENVIRONMENT DIVISION.                                                    
      ****************************************************************          
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT INPUT-FILE             ASSIGN TO INP01.                       
           SELECT OUTPUT-FILE            ASSIGN TO OUT01.                       
216764     SELECT ORACLE-LOGON-FILE      ASSIGN TO ORALOGON.                    
                                                                                
      ****************************************************************          
       DATA DIVISION.                                                           
      ****************************************************************          
       FILE SECTION.                                                            
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  INPUT-RECORD                  PIC X(80).                             
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  OUTPUT-SKIP-RECORD            PIC X(80).                             
                                                                                
216764 FD  ORACLE-LOGON-FILE                                                    
216764     RECORDING MODE IS F                                                  
216764     LABEL RECORDS ARE STANDARD                                           
216764     BLOCK CONTAINS 0 RECORDS                                             
216764     RECORD CONTAINS 80 CHARACTERS                                        
216764     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
216764 01  OL-ORACLE-LOGON-RECORD          PIC X(80).                           
216764                                                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PV-LOGON-SYSIN.                                                      
           05  PV-SYSIN-ORACLE-ID          PIC X(10) VALUE SPACES.              
           05  FILLER                      PIC X(01) VALUE '*'.                 
           05  PV-SYSIN-PASSWD             PIC X(10) VALUE SPACES.              
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-COMMA-DATA-SW    PIC X(01)  VALUE 'N'.                 
               88  PS-END-OF-COMMA-DATA              VALUE 'Y'.                 
           05  PS-FETCH-DONE              PIC X(01)  VALUE 'N'.                 
               88  PS-FETCH-IS-DONE                  VALUE 'Y'.                 
               88  PS-FETCH-IS-NOT-DONE              VALUE 'N'.                 
           05  PS-STORE-EXISTS            PIC X(01)  VALUE 'N'.                 
               88  PS-STORE-DOES-EXIST               VALUE 'Y'.                 
               88  PS-STORE-NOT-EXIST                VALUE 'N'.                 
           05  PS-FLRPD-CDE-EXIST         PIC X(01)  VALUE 'N'.                 
               88  PS-FLRPD-CDE-EXISTS               VALUE 'Y'.                 
               88  PS-FLRPD-CDE-NOT-EXIST           VALUE 'N'.                  
           05  PS-FLRPD-ID-EXIST         PIC X(01)  VALUE 'N'.                  
               88  PS-FLRPD-ID-EXISTS               VALUE 'Y'.                  
               88  PS-FLRPD-ID-NOT-EXIST            VALUE 'N'.                  
           05  PS-U-REC-EXIST            PIC X(01)  VALUE 'N'.                  
               88  PS-U-REC-EXISTS                  VALUE 'Y'.                  
               88  PS-U-REC-NOT-EXIST               VALUE 'N'.                  
           05  PS-S-REC-EXIST            PIC X(01)  VALUE 'N'.                  
               88  PS-S-REC-EXISTS                  VALUE 'Y'.                  
               88  PS-S-REC-NOT-EXIST               VALUE 'N'.                  
           05  PS-U-CPTY-SAME            PIC X(01)  VALUE 'N'.                  
               88  PS-U-CPTY-SAME-YES               VALUE 'Y'.                  
               88  PS-U-CPTY-SAME-NO                VALUE 'N'.                  
           05  PS-CPCTY-GP-EXIST         PIC X(01)  VALUE 'N'.                  
               88  PS-CPCTY-GP-EXISTS               VALUE 'Y'.                  
               88  PS-CPCTY-GP-NOT-EXIST            VALUE 'N'.                  
216764     05  PS-END-OF-LOGON-FILE-SW      PIC  X(01) VALUE 'N'.               
216764         88 PS-END-OF-LOGON-FILE                 VALUE 'Y'.               
216764     05  PS-ORA-SRC-CTL-DONE-SW       PIC  X(01) VALUE 'N'.               
216764         88  PS-ORA-SRC-EMPTY                    VALUE 'Y'.               
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGMNAME                PIC X(08)  VALUE 'FCKUP902'.           
           05  PC-FIELD-LENGTH           PIC S9(03) VALUE +10 COMP-3.           
           05  PC-COMMIT-INTERVAL         PIC  9(05) VALUE 1000.                
216764     05  PC-AUTO-LOGON                PIC  X(1)  VALUE '/'.               
216764     05  PC-ORACLE-PROD-ID-LGTH       PIC  9(02) VALUE 1.                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-UNSTR-PTR               PIC S9(04) COMP SYNC                  
                                                      VALUE ZERO.               
           05  PV-ABEND-PARAGRAPH         PIC  X(30) VALUE SPACES.              
           05  PV-TALLY                   PIC S9(03) COMP-3 VALUE +0.           
           05  PV-RECS-READ               PIC  9(6) VALUE 0.                    
           05  PV-COMMA-SEASON            PIC X(80).                            
           05  PV-COMMA-DATA              PIC X(80).                            
           05  PV-SEASON-COUNT            PIC  9(1) VALUE 0.                    
           05  PV-SEASON-LENGTH           PIC  9(1) VALUE 0.                    
           05  PV-SEASON-DESC             PIC X(6).                             
           05  PV-SEASON-YEAR             PIC X(4).                             
           05  PV-DB2-OPERATION            PIC X(30).                           
           05  PV-PARAGRAPH-NAME           PIC X(30).                           
           05  PV-MESSAGE                  PIC X(30).                           
           05  PV-U-RECS-INSERTED          PIC  9(6) VALUE 0.                   
           05  PV-S-RECS-INSERTED          PIC  9(6) VALUE 0.                   
           05  PV-U-RECS-UPDATED           PIC  9(6) VALUE 0.                   
           05  PV-RECS-SKIPPED             PIC  9(6) VALUE 0.                   
216764     05  PV-ORACLE-USERID-LEN        PIC  S9(4) VALUE ZEROS               
216764                                                  COMP SYNC.              
216764     05  PV-ORACLE-PASSWORD-LEN      PIC  S9(4) VALUE ZEROS               
216764                                                  COMP SYNC.              
                                                                                
       01 PV-CURRENT.                                                           
           05 PV-CURRENT-YEAR              PIC X(04).                           
           05 PV-CURRENT-MONTH             PIC X(02).                           
           05 PV-CURRENT-DAY               PIC X(02).                           
           05 PV-CURRENT-DTE               PIC X(11).                           
           05 PV-SEARCH-DTE                PIC X(11).                           
                                                                                
        01 PV-DATE.                                                             
            05 PV-DATE-YEAR                PIC X(04).                           
            05 PV-DATE-MONTH               PIC X(3).                            
            05 PV-DATE-MONTH-NM            PIC  X(8) VALUE SPACES.              
            05 PV-DATE-MONTH-NBR           PIC X(2).                            
            05 PV-DATE-DAY                 PIC X(2).                            
            05 PV-DATE-DTE                 PIC X(11).                           
                                                                                
       01  PV-CSV-FILE-LAYOUT.                                                  
           05  PV-CSV-SEASONS              PIC X(11) OCCURS 6                   
                                               INDEXED BY IDX.                  
           05  PV-CSV-SEASON-BEG           PIC X(11) OCCURS 6                   
                                               INDEXED BY IDX2.                 
           05  PV-CSV-SEASON-END           PIC X(11) OCCURS 6                   
                                               INDEXED BY IDX3.                 
           05  PV-SEASON-BEGIN             PIC X(11) VALUE SPACES.              
           05  PV-SEASON-END               PIC X(11) VALUE SPACES.              
                                                                                
       01  FW-FIXED-WIDTH-OUTPUT.                                               
           05  FW-STORE-NBR                PIC  9(5) VALUE 0.                   
               88 FW-ECOM-STORE-NBR   VALUES ARE  809, 819,                     
                                                  829, 873,                     
                                                  859, 869,                     
                                                  879, 889,                     
253883                                            839, 849.                     
           05  FW-FLRPD-GRP-CDE            PIC X(2).                            
           05  FW-FLRPD-GRP-ID             PIC  9(3) VALUE 0.                   
           05  FW-FLRPD-CPCTY              PIC X(2).                            
P51977     05  FW-FLRPD-CPCTY-S            PIC X(2).                            
           05  FW-FC-STORE-NBR             PIC X(5).                            
           05  FW-FC-FLRPD-GRP-CDE         PIC X(2).                            
           05  FW-FC-FLRPD-GRP-ID          PIC  9(3) VALUE 0.                   
           05  FW-FC-FLRPD-CPCTY           PIC X(2).                            
                                                                                
P51977 01  FW-SKIP-OUT-REC.                                                     
P51977     05  FW-SKIP-STORE-NBR           PIC 9(5)  VALUE ZEROES.              
P51977     05  FILLER                      PIC X(01) VALUE SPACES.              
P51977     05  FW-SKIP-FLRPD-GRP-CDE       PIC X(2)  VALUE SPACES.              
P51977     05  FILLER                      PIC X(01) VALUE SPACES.              
P51977     05  FW-SKIP-FLRPD-CPCTY         PIC X(2)  VALUE SPACES.              
                                                                                
216764******************************************************************        
216764*      ORACLE CONNECTION VARIABLES                                        
216764******************************************************************        
216764 01  CC-ORACLE-CONNECTION.                                                
216764     05  CC-ORACLE-USERID             PIC X(40).                          
216764         88 CC-AUTO-LOGON                            VALUE '/'.           
216764     05  CC-ORACLE-PASSWORD           PIC X(40).                          
216764                                                                          
216764****************************************************************  00930000
216764**ABEND INFORMATION                                               00940000
216764****************************************************************  00930000
216764 01  PV-ABEND-CODE                   PIC S9(04) VALUE +0                  
216764                                                COMP SYNC.                
216764     88  PV-DB2-ERROR                           VALUE +4013.              
      *                                                                         
      * DECLARE VARIABLES FOR SUBROUTINE CALL  (SQLCODE TEXT) ????              
      *                                                                         
       01  WS-MSG-TEXT                   PIC X(512).                            
       01  WS-MAX-SIZE                   PIC S9(09) COMP VALUE 512.             
       01  WS-MSG-LENGTH                 PIC S9(09) COMP.                       
                                                                                
      *************************************************************             
      * COPYBOOK FOR NUMERIC EDIT ROUTINE                         *             
      *************************************************************             
           COPY DPWS010I.                                                       
      *--------------------------------------------------------------*          
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004              
      *--------------------------------------------------------------*          
           COPY DPWS004.                                                        
                                                                                
      ****************************************************************          
      * ORACLE DECLARE SECTION                                                  
      ****************************************************************          
           EXEC SQL                                                             
               BEGIN DECLARE SECTION                                            
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    ORACLE LOGON WORKING STORAGE COPYBOOK.                               
      *---------------------------------------------------------------*         
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
      *---------------------------------------------------------------*         
      * K_FC01.FCT_FLR_PLN_PROTYP ORACLE TABLE                                  
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE FCT00005                                                 
           END-EXEC.                                                            
      ******************************************************************        
      * DATE ATTRIBUTE TABLE TO FIND SEASON BEGIN AND END DATES                 
      ****************************************************************          
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               END DECLARE SECTION                                              
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCACOB                                                 
           END-EXEC.                                                            
      ****************************************************************          
      *  COMMUNICATION AREAS FOR DB2                                            
      ****************************************************************          
           COPY SQLCA2.                                                         
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAINLINE.                                                           
           DISPLAY '******** START OF PROGRAM  ********'.                       
                                                                                
           PERFORM 1000-INITIAL.                                                
                                                                                
           PERFORM 2000-PROCESS-SEASONS.                                        
           PERFORM 4000-PROCESS-FLRPD-CPCTYS.                                   
                                                                                
                                                                                
           PERFORM 9000-END-ROUTINE.                                            
                                                                                
                                                                                
           DISPLAY '********  END OF PROGRAM   ********'.                       
           GOBACK.                                                              
                                                                                
      ****************************************************************          
       1000-INITIAL.                                                            
                                                                                
216764     PERFORM 1200-CONNECT-TO-ORACLE.                                      
           OPEN INPUT INPUT-FILE.                                               
           DISPLAY 'INPUT FILE OPENED'.                                         
           OPEN OUTPUT OUTPUT-FILE.                                             
           DISPLAY 'OUTPUT FILE OPENED'.                                        
                                                                                
      *    PERFORM 1200-CONNECT-TO-ORACLE.                                      
      ****************************************************************          
      * IF USERNAME-LEN IS ZERO, THE LOGON ID IS MISSING FROM SYSIN.            
      * THEREFORE IT IS A PRODUCTION RUN WHERE THE LOGON INFO COMES             
      * FROM THE JCL                                                            
      ****************************************************************          
       1200-CONNECT-TO-ORACLE.                                                  
                                                                                
      *** ORACLE CONNECT - TKMA (TEST).                                         
      **   MOVE 'FCTEAM' TO USERNAME-ARR.                                       
      **   MOVE 8  TO USERNAME-LEN.                                             
      **   MOVE 'FCTEAM' TO PASSWD-ARR.                                         
      **   MOVE 8  TO PASSWD-LEN.                                               
      **   DISPLAY '** ATTEMPTING LOGON AS ' USERNAME-ARR.                      
                                                                                
      **   EXEC SQL                                                             
      **    CONNECT :USERNAME IDENTIFIED BY :PASSWD                             
      **   END-EXEC.                                                            
                                                                                
                                                                                
                                                                                
      *** ORACLE CONNECT - PKMO (PROD).                                         
                                                                                
      *    IF USERNAME-LEN > ZERO                                               
      *        EXEC SQL                                                         
      *            CONNECT :USERNAME IDENTIFIED BY :PASSWD                      
      *        END-EXEC                                                         
      *    ELSE                                                                 
      *        EXEC SQL                                                         
      *            CONNECT :PV-ORACLE-ID                                        
      *        END-EXEC                                                         
      *    END-IF.                                                              
                                                                                
      *    IF SQLCODE = ZERO                                                    
      *        IF USERNAME-LEN > ZERO                                           
      *            DISPLAY 'CONNECTED TO ORACLE AS USER: ' USERNAME-ARR         
      *        ELSE                                                             
      *            DISPLAY 'CONNECTED TO ORACLE AS USER: ' PV-ORACLE-ID         
      *        END-IF                                                           
      *    ELSE                                                                 
      *        DISPLAY '1200-CONNECT-TO-ORACLE'                                 
      *        PERFORM 9998-SQL-ABEND                                           
      *    END-IF.                                                              
                                                                                
      *    DISPLAY ' '.                                                         
216764     OPEN INPUT ORACLE-LOGON-FILE.                                        
216764                                                                          
216764     DISPLAY 'ORACLE FILE READ'.                                          
216764     PERFORM 8000-READ-ORACLE-LOGON.                                      
216764                                                                          
216764     DISPLAY 'ORACLE FILE CLOSE'.                                         
216764     CLOSE ORACLE-LOGON-FILE.                                             
216764                                                                          
216764     IF ((PS-END-OF-LOGON-FILE)                                           
216764              OR (CC-ORACLE-CONNECTION = SPACES))                         
216764           DISPLAY '** ABEND **'                                          
216764           DISPLAY PC-PGMNAME                                             
216764           ' - ORACLE LOGON CONTROL CARD NOT PROVIDED'                    
216764           SET PS-ORA-SRC-EMPTY TO TRUE                                   
216764           CALL 'ILBOABN0' USING PV-ABEND-CODE                            
216764     END-IF.                                                              
216764                                                                          
216764     IF CC-AUTO-LOGON                                                     
216764             DISPLAY PC-PGMNAME                                           
216764             ' **AUTO LOGON TO ORACLE**'                                  
216764             EXEC SQL                                                     
216764               CONNECT  :PC-AUTO-LOGON                                    
216764             END-EXEC                                                     
216764       ELSE                                                               
216764         INITIALIZE PV-ORACLE-USERID-LEN                                  
216764                    PV-ORACLE-PASSWORD-LEN                                
216764           INSPECT    CC-ORACLE-USERID                                    
216764             TALLYING   PV-ORACLE-USERID-LEN                              
216764             FOR CHARACTERS BEFORE INITIAL SPACE                          
216764           INSPECT    CC-ORACLE-PASSWORD                                  
216764             TALLYING   PV-ORACLE-PASSWORD-LEN                            
216764             FOR CHARACTERS BEFORE INITIAL SPACE                          
216764         MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                
216764                    USERNAME-ARR                                          
216764         MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                        
216764         MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO            
216764                      PASSWD-ARR                                          
216764         MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                        
216764           DISPLAY PC-PGMNAME                                             
216764           ' - LOGON TO ORACLE AS USER ' USERNAME-ARR                     
216764            EXEC SQL                                                      
216764                 CONNECT :USERNAME IDENTIFIED BY :PASSWD                  
216764            END-EXEC                                                      
216764         INITIALIZE CC-ORACLE-PASSWORD                                    
216764                    PASSWD                                                
216764     END-IF.                                                              
216764             DISPLAY '** CONNECTING TO ORACLE BY USERID **'               
216764         IF CC-AUTO-LOGON                                                 
216764               DISPLAY 'TRIED TO CONNECT TO ORACLE UNDER: '               
216764                                  PC-AUTO-LOGON                           
216764         ELSE                                                             
216764               DISPLAY 'TRIED TO CONNECT TO ORACLE UNDER: '               
216764                                  PASSWD                                  
               END-IF                                                           
216764         IF  SQLCODE = 0                                                  
216764             DISPLAY 'CONNECTED TO ORACLE'                                
216764         ELSE                                                             
216764             DISPLAY 'FAILED TO CONNECT TO ORACLE'                        
216764         MOVE '1200-CONNECT-TO-ORACLE' TO PV-PARAGRAPH-NAME               
216764            SET PV-DB2-ERROR  TO TRUE                                     
216764               PERFORM 9999-ABEND                                         
216764         END-IF.                                                          
                                                                                
      ****************************************************************          
      * GET CURRENT SYSTEM DATE                                                 
      ****************************************************************          
       1500-GET-CURRENT-DATE.                                                   
           MOVE FUNCTION CURRENT-DATE (1:8) TO PV-CURRENT                       
                                                                                
           STRING PV-CURRENT-YEAR, '-', PV-CURRENT-MONTH, "-",                  
           PV-CURRENT-DAY DELIMITED BY SPACES                                   
                INTO PV-CURRENT-DTE                                             
           END-STRING.                                                          
                                                                                
           MOVE PV-CURRENT-DTE TO PV-SEARCH-DTE.                                
                                                                                
           PERFORM 1800-GET-DATE-FORMAT.                                        
                                                                                
           MOVE PV-SEARCH-DTE TO PV-CURRENT-DTE.                                
      ****************************************************************          
      * GET CORRECT DATE FORMAT FOR INSERTING RECORDS                           
      ****************************************************************          
       1800-GET-DATE-FORMAT.                                                    
           STRING PV-SEARCH-DTE (6:2)                                           
                  DELIMITED BY SIZE                                             
                  INTO PV-DATE-MONTH-NBR.                                       
                                                                                
           EXEC SQL                                                             
           SELECT DISTINCT FSCL_MN_NM                                           
           INTO :PV-DATE-MONTH-NM                                               
           FROM TDTATTR                                                         
           WHERE CAL_MN_NBR = :PV-DATE-MONTH-NBR                                
           END-EXEC.                                                            
                                                                                
           STRING PV-DATE-MONTH-NM (1:3)                                        
                  DELIMITED BY SIZE                                             
                  INTO PV-DATE-MONTH.                                           
                                                                                
           STRING PV-SEARCH-DTE (9:2)                                           
                  DELIMITED BY SIZE                                             
                  INTO PV-DATE-DAY.                                             
                                                                                
           STRING PV-SEARCH-DTE (1:4)                                           
                  DELIMITED BY SIZE                                             
                  INTO PV-DATE-YEAR.                                            
                                                                                
           STRING PV-DATE-DAY '-' PV-DATE-MONTH '-' PV-DATE-YEAR                
           DELIMITED BY SIZE                                                    
           INTO PV-SEARCH-DTE.                                                  
      *****************************************************************         
      * PROCESS CSV SEASONS                                                     
      ******************************************************************        
       2000-PROCESS-SEASONS.                                                    
           PERFORM 1500-GET-CURRENT-DATE.                                       
           PERFORM 2200-READ-CSV-SEASONS.                                       
           PERFORM 2400-GET-SEASON-DATES.                                       
      ******************************************************************        
      * READ SEASONS FROM CONTROL FILE                                          
      ******************************************************************        
       2200-READ-CSV-SEASONS.                                                   
           READ INPUT-FILE INTO PV-COMMA-SEASON.                                
                                                                                
           INSPECT PV-COMMA-SEASON TALLYING PV-SEASON-COUNT                     
                   FOR ALL ",".                                                 
           ADD 1 TO PV-SEASON-COUNT.                                            
           DISPLAY 'NUMBER OF SEASONS FOUND ===> ' PV-SEASON-COUNT.             
           DISPLAY ' '.                                                         
                                                                                
           IF PV-SEASON-COUNT > 6 OR PV-SEASON-COUNT = 0                        
              STRING "ERROR: NUMBER OF SEASONS FOUND IN CSV FILE ",             
                     "WAS INVALID."                                             
                     DELIMITED BY SIZE                                          
                     INTO WS-MSG-TEXT                                           
              END-STRING                                                        
              PERFORM 9999-ABEND                                                
           END-IF.                                                              
                                                                                
           MOVE 1 TO PV-UNSTR-PTR                                               
                                                                                
           PERFORM VARYING IDX FROM 1 BY 1 UNTIL IDX > 6                        
              UNSTRING PV-COMMA-SEASON DELIMITED BY ","                         
                  INTO PV-CSV-SEASONS (IDX) WITH POINTER PV-UNSTR-PTR           
           END-PERFORM.                                                         
                                                                                
           ADD 1 TO PV-RECS-READ.                                               
                                                                                
                                                                                
      ******************************************************************        
      * FIND BEGIN AND END DATES FOR EACH SEASON                                
      ******************************************************************        
       2400-GET-SEASON-DATES.                                                   
           MOVE '3000-FIND-SEASON-DATES          ' TO PV-PARAGRAPH-NAME.        
                                                                                
           PERFORM VARYING IDX FROM 1 BY 1 UNTIL IDX > PV-SEASON-COUNT          
             PERFORM 2600-GET-SEASON-AND-YEAR                                   
             PERFORM 2800-FETCH-SEASON-CSR                                      
           END-PERFORM.                                                         
      ******************************************************************        
      * FIND THE SEASON DESCRIPTION AND YEAR                                    
      ******************************************************************        
       2600-GET-SEASON-AND-YEAR.                                                
                                                                                
           UNSTRING PV-CSV-SEASONS (IDX) DELIMITED BY " "                       
               INTO PV-SEASON-DESC,                                             
                    PV-SEASON-YEAR                                              
                                                                                
      **MAKE SURE THE SEASON DESCRIPTION IS IN UPPER-CASE FORMAT                
           MOVE FUNCTION UPPER-CASE(PV-SEASON-DESC) TO PV-SEASON-DESC.          
                                                                                
      *****************************************************************         
      * FETCH THE CURRENT SEASON CURSOR                                         
      *****************************************************************         
       2800-FETCH-SEASON-CSR.                                                   
           MOVE '2800-FETCH-SEASON-CSR  ' TO PV-PARAGRAPH-NAME.                 
           EXEC SQL                                                             
              SELECT DISTINCT                                                   
                   SEA_BEG_DTE                                                  
                   ,SEA_END_DTE                                                 
              INTO :PV-SEASON-BEGIN                                             
                  ,:PV-SEASON-END                                               
              FROM TDTATTR                                                      
                 WHERE SEA_DESC = :PV-SEASON-DESC                               
                 AND FSCL_YR_NBR = :PV-SEASON-YEAR                              
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
                 WHEN +1403                                                     
                    DISPLAY 'ERROR! SEASON DATES WERE NOT FOUND FOR '           
                                    PV-CSV-SEASONS (IDX)                        
                    DISPLAY 'CORRECT CSV FILE AND RERUN'                        
                                                                                
                  WHEN ZERO                                                     
                                                                                
                      MOVE PV-SEASON-BEGIN TO PV-SEARCH-DTE                     
                      PERFORM 1800-GET-DATE-FORMAT                              
                      MOVE PV-SEARCH-DTE TO PV-CSV-SEASON-BEG (IDX)             
                                                                                
                      MOVE PV-SEASON-END TO PV-SEARCH-DTE                       
                      PERFORM 1800-GET-DATE-FORMAT                              
                      MOVE PV-SEARCH-DTE TO PV-CSV-SEASON-END (IDX)             
                                                                                
                      CONTINUE                                                  
                                                                                
                  WHEN +100                                                     
                      SET PS-FETCH-IS-DONE TO TRUE                              
                                                                                
                  WHEN OTHER                                                    
                      MOVE '5010-5010-FETCH-SEASON-CSR'                         
                                           TO PV-DB2-OPERATION                  
                      PERFORM 9998-SQL-ABEND                                    
             END-EVALUATE.                                                      
                                                                                
      *****************************************************************         
      * PROCESS FLOORPAD CAPACITY UPDATES                                       
      *****************************************************************         
       4000-PROCESS-FLRPD-CPCTYS.                                               
           PERFORM 4200-PROCESS-CSV-CPCTY-ROWS                                  
              UNTIL PS-END-OF-COMMA-DATA.                                       
                                                                                
           PERFORM 8900-COMMIT-DATA.                                            
      *****************************************************************         
      * PROCESS FLOORPAD CAPACITIES IN CSV FILES                                
      *****************************************************************         
       4200-PROCESS-CSV-CPCTY-ROWS.                                             
           DISPLAY ' '                                                          
                                                                                
           READ INPUT-FILE INTO PV-COMMA-DATA                                   
                AT END SET PS-END-OF-COMMA-DATA TO TRUE.                        
                                                                                
           UNSTRING PV-COMMA-DATA DELIMITED BY ","                              
                 INTO FW-STORE-NBR                                              
                      FW-FLRPD-GRP-CDE                                          
                      FW-FLRPD-CPCTY                                            
                                                                                
           MOVE FUNCTION UPPER-CASE(FW-FLRPD-GRP-CDE)                           
                                   TO FW-FLRPD-GRP-CDE.                         
           MOVE FUNCTION UPPER-CASE(FW-FLRPD-CPCTY)                             
                                   TO FW-FLRPD-CPCTY.                           
                                                                                
                                                                                
           IF NOT PS-END-OF-COMMA-DATA                                          
              ADD 1 TO PV-RECS-READ                                             
            DISPLAY 'PROCESSING ROW: ' FW-STORE-NBR ',' FW-FLRPD-GRP-CDE        
                     ',' FW-FLRPD-CPCTY                                         
                                                                                
                                                                                
P51977        IF NOT FW-ECOM-STORE-NBR                                          
P51977        AND FW-FLRPD-CPCTY = 'NO'                                         
P51977             PERFORM 8800-WRITE-STR-NBR-SKIP                              
P51977        ELSE                                                              
                                                                                
             PERFORM 7400-CHECK-FLRPD-GP-CDE                                    
             PERFORM 8200-CHECK-CPCTY-GP-CDE                                    
                                                                                
             IF PS-FLRPD-ID-EXISTS AND PS-CPCTY-GP-EXISTS                       
               PERFORM 7200-CHECK-IF-STORE-EXISTS                               
P51977            IF PS-STORE-DOES-EXIST                                        
                     PERFORM VARYING IDX FROM 1 BY 1                            
                               UNTIL IDX > PV-SEASON-COUNT                      
                              PERFORM 7000-FLRPD-CPCTY-UPDATES                  
P51977                 END-PERFORM                                              
P51977              END-IF                                                      
P51977           END-IF                                                         
P51977        END-IF                                                            
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * PROCES FLOORPAD CAPACITY UPDATES FOUND IN CSV FILE                      
      ******************************************************************        
       7000-FLRPD-CPCTY-UPDATES.                                                
                                                                                
           MOVE PV-CSV-SEASON-BEG (IDX) TO PV-SEASON-BEGIN.                     
           MOVE PV-CSV-SEASON-END (IDX) TO PV-SEASON-END.                       
                                                                                
P51977     IF  FW-ECOM-STORE-NBR                                                
P51977          MOVE 'NO' TO FW-FLRPD-CPCTY                                     
P51977     END-IF                                                               
P51977                                                                          
           PERFORM 7600-CHECK-U-RECORD-EXISTS.                                  
           PERFORM 7800-CHECK-S-RECORD-EXISTS.                                  
           IF PS-U-REC-EXISTS                                                   
              IF PS-U-CPTY-SAME-NO                                              
                 PERFORM 8700-UPDATE-U-RECORD                                   
              END-IF                                                            
           ELSE                                                                 
             IF PS-S-REC-EXISTS                                                 
               PERFORM 8500-INSERT-U-RECORD                                     
             ELSE                                                               
               PERFORM 8000-INSERT-S-RECORD                                     
               PERFORM 8500-INSERT-U-RECORD                                     
             END-IF                                                             
           END-IF.                                                              
      *****************************************************************         
      * CHECK IF STORE EXISTS                                                   
      *****************************************************************         
       7200-CHECK-IF-STORE-EXISTS.                                              
           MOVE '7100-CHECK-IF-STORE-EXISTS  ' TO PV-PARAGRAPH-NAME.            
           EXEC SQL                                                             
             SELECT  LOC_NBR                                                    
             INTO :FW-FC-STORE-NBR                                              
             FROM  XMT_LOC                                                      
             WHERE LOC_NBR = :FW-STORE-NBR                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +1403                                                        
                 DISPLAY 'WARNING: STORE NUMBER  '                              
                           FW-STORE-NBR ' DOES NOT EXIST. ROW SKIPPED'          
                 ADD 1 TO PV-RECS-SKIPPED                                       
              WHEN ZERO                                                         
                  SET PS-STORE-DOES-EXIST TO TRUE                               
                  CONTINUE                                                      
              WHEN OTHER                                                        
                  MOVE '7200-CHECK-IF-STORE-EXISTS'                             
                                       TO PV-DB2-OPERATION                      
                  PERFORM 9998-SQL-ABEND                                        
            END-EVALUATE.                                                       
      *****************************************************************         
      * CHECK IF FLOORPAD GROUP CODE EXISTS                                     
      *****************************************************************         
       7400-CHECK-FLRPD-GP-CDE.                                                 
           MOVE '7400-CHECK-FLRPD-GP-CDE  ' TO PV-PARAGRAPH-NAME.               
           EXEC SQL                                                             
                SELECT FLR_PAD_GP_ID                                            
                INTO :FW-FLRPD-GRP-ID                                           
                FROM FCT_FLR_PAD_GP                                             
                WHERE FLR_PAD_GP_CDE = :FW-FLRPD-GRP-CDE                        
                AND EFF_END_DTE >=                                              
                TO_DATE(:PV-CURRENT-DTE,'DD-MON-YYYY HH24:MI:SS')               
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN +1403                                                         
               DISPLAY 'WARNING: GROUP CODE  '                                  
                         FW-FLRPD-GRP-CDE ' DOES NOT EXIST. ROW SKIPPED'        
               ADD 1 TO PV-RECS-SKIPPED                                         
             WHEN ZERO                                                          
                SET PS-FLRPD-ID-EXISTS TO TRUE                                  
                CONTINUE                                                        
             WHEN OTHER                                                         
                MOVE '7400-CHECK-FLRPD-GP-CDE'                                  
                                     TO PV-DB2-OPERATION                        
                PERFORM 9998-SQL-ABEND                                          
             END-EVALUATE.                                                      
      *****************************************************************         
      * CHECK IF 'U' RECORD EXISTS                                              
      *****************************************************************         
       7600-CHECK-U-RECORD-EXISTS.                                              
           MOVE '7600-CHECK-U-RECORD-EXISTS' TO PV-PARAGRAPH-NAME.              
             EXEC SQL                                                           
             SELECT FLR_PAD_GP_ID                                               
                    ,LOC_NBR                                                    
                    ,CPCTY_GP_CDE                                               
             INTO :FW-FC-FLRPD-GRP-ID                                           
                  ,:FW-FC-STORE-NBR                                             
                  ,:FW-FC-FLRPD-CPCTY                                           
             FROM FCT_STR_FLR_PAD_GP_CPCTY                                      
             WHERE FLR_PAD_GP_ID = :FW-FLRPD-GRP-ID                             
             AND LOC_NBR =  :FW-STORE-NBR                                       
             AND STR_CPCTY_GP_SRC_CDE = 'U'                                     
             AND EFF_BEG_DTE =                                                  
             TO_DATE(:PV-SEASON-BEGIN,'DD-MON-YYYY HH24:MI:SS')                 
           END-EXEC.                                                            
                                                                                
            EVALUATE SQLCODE                                                    
                WHEN +1403                                                      
                  SET PS-U-REC-NOT-EXIST TO TRUE                                
                WHEN ZERO                                                       
                   SET PS-U-REC-EXISTS TO TRUE                                  
                   IF FW-FC-FLRPD-CPCTY = FW-FLRPD-CPCTY                        
                      DISPLAY 'WARNING: FLOORPAD CAPACITY '                     
                             FW-FC-FLRPD-CPCTY ' FOR THIS U RECORD '            
                             'IN ' PV-CSV-SEASONS (IDX)                         
                                ' DID NOT CHANGE. ROW SKIPPED.'                 
                      ADD 1 TO PV-RECS-SKIPPED                                  
                      SET PS-U-CPTY-SAME-YES TO TRUE                            
                   ELSE                                                         
                      SET PS-U-CPTY-SAME-NO TO TRUE                             
                   END-IF                                                       
                   CONTINUE                                                     
                WHEN OTHER                                                      
                   MOVE '7600-CHECK-U-RECORD-EXISTS'                            
                                        TO PV-DB2-OPERATION                     
                   PERFORM 9998-SQL-ABEND                                       
             END-EVALUATE.                                                      
                                                                                
      ****************************************************************          
      * CHECK IF 'S' RECORD EXISTS                                              
      ****************************************************************          
       7800-CHECK-S-RECORD-EXISTS.                                              
           MOVE '7800-CHECK-S-RECORD-EXISTS' TO PV-PARAGRAPH-NAME.              
             EXEC SQL                                                           
                SELECT FLR_PAD_GP_ID                                            
                       ,LOC_NBR                                                 
                       ,CPCTY_GP_CDE                                            
                INTO :FW-FC-FLRPD-GRP-ID                                        
                     ,:FW-FC-STORE-NBR                                          
                     ,:FW-FC-FLRPD-GRP-CDE                                      
                FROM FCT_STR_FLR_PAD_GP_CPCTY                                   
                WHERE FLR_PAD_GP_ID = :FW-FLRPD-GRP-ID                          
                AND LOC_NBR =  :FW-STORE-NBR                                    
                AND STR_CPCTY_GP_SRC_CDE = 'S'                                  
                AND EFF_END_DTE =                                               
              TO_DATE('31-DEC-9999 23:59:59','DD-MON-YYYY HH24:MI:SS')          
             END-EXEC.                                                          
            EVALUATE SQLCODE                                                    
                WHEN +1403                                                      
                  SET PS-S-REC-NOT-EXIST TO TRUE                                
                WHEN ZERO                                                       
                   SET PS-S-REC-EXISTS TO TRUE                                  
                   CONTINUE                                                     
                WHEN OTHER                                                      
                   MOVE '7600-CHECK-S-RECORD-EXISTS'                            
                                  TO PV-DB2-OPERATION                           
                   PERFORM 9998-SQL-ABEND                                       
             END-EVALUATE.                                                      
      ****************************************************************          
      * INSERT S RECORD                                                         
      ****************************************************************          
         8000-INSERT-S-RECORD.                                                  
             MOVE '8000-INSERT-S-RECORD' TO PV-PARAGRAPH-NAME.                  
                                                                                
P51977       IF FW-ECOM-STORE-NBR                                               
P51977          MOVE 'NO' TO FW-FLRPD-CPCTY-S                                   
P51977       ELSE                                                               
P51977          MOVE 'ST' TO FW-FLRPD-CPCTY-S                                   
P51977       END-IF                                                             
P51977                                                                          
             EXEC SQL                                                           
             INSERT INTO FCT_STR_FLR_PAD_GP_CPCTY                               
               (STR_FLR_PAD_GP_CPCTY_ID                                         
               ,FLR_PAD_GP_ID                                                   
               ,STR_CPCTY_GP_SRC_CDE                                            
               ,LOC_NBR                                                         
               ,EFF_BEG_DTE                                                     
               ,EFF_END_DTE                                                     
               ,CPCTY_GP_CDE                                                    
               ,LAST_UPD_TMST                                                   
               ,LAST_UPD_USR_ID                                                 
               ,CPCTY_OVRRID_RSN_CDE )                                          
              VALUES (                                                          
                FCS_STR_FLR_PAD_GP_CPCTY.NEXTVAL                                
               ,:FW-FLRPD-GRP-ID                                                
               ,'S'                                                             
               ,:FW-STORE-NBR                                                   
             ,TO_DATE(:PV-SEASON-BEGIN,'DD-MON-YYYY HH24:MI:SS')                
               ,TO_DATE('31-DEC-9999 23:59:59','DD-MON-YYYY HH24:MI:SS')        
P51977         ,:FW-FLRPD-CPCTY-S                                               
              ,TO_DATE(:PV-CURRENT-DTE,'DD-MON-YYYY HH24:MI:SS')                
               ,'SYSTEM'                                                        
               ,'SV' )                                                          
             END-EXEC.                                                          
                                                                                
             EVALUATE SQLCODE                                                   
                 WHEN +1403                                                     
                   DISPLAY ' INSERT FAILED!'                                    
                 WHEN ZERO                                                      
                   SET PS-S-REC-EXISTS TO TRUE                                  
                   ADD 1 TO PV-S-RECS-INSERTED                                  
                   CONTINUE                                                     
                 WHEN OTHER                                                     
                    DISPLAY ' INSERT FAILED!'                                   
                    MOVE '8000-INSERT-S-RECORD'                                 
                                       TO PV-DB2-OPERATION                      
                    PERFORM 9998-SQL-ABEND                                      
             END-EVALUATE.                                                      
      ****************************************************************          
      ****************************************************************          
      * CHECK FOR VALID MERCHANDISE CAPACITY                                    
      ****************************************************************          
          8200-CHECK-CPCTY-GP-CDE.                                              
              MOVE '7400-CHECK-FLRPD-GP-CDE  ' TO PV-PARAGRAPH-NAME.            
                EXEC SQL                                                        
                     SELECT CPCTY_GP_CDE                                        
                     INTO :FW-FC-FLRPD-CPCTY                                    
                     FROM FCT_MDSE_CPCTY_GP                                     
                     WHERE CPCTY_GP_CDE = :FW-FLRPD-CPCTY                       
                END-EXEC.                                                       
               EVALUATE SQLCODE                                                 
                WHEN +1403                                                      
                  DISPLAY 'WARNING: CAPACITY GROUP CODE '                       
                         FW-FLRPD-CPCTY ' IS NOT VALID. ROW SKIPPED'            
                  ADD 1 TO PV-RECS-SKIPPED                                      
                WHEN ZERO                                                       
                   SET PS-CPCTY-GP-EXISTS TO TRUE                               
                   CONTINUE                                                     
                WHEN OTHER                                                      
                   MOVE '8200-CHECK-CPCTY-GP-CDE'                               
                                    TO PV-DB2-OPERATION                         
                   PERFORM 9998-SQL-ABEND                                       
              END-EVALUATE.                                                     
      ****************************************************************          
      * INSERT U RECORD                                                         
      ****************************************************************          
          8500-INSERT-U-RECORD.                                                 
            MOVE '8500-INSERT-U-RECORD' TO PV-PARAGRAPH-NAME.                   
                                                                                
                EXEC SQL                                                        
                INSERT INTO FCT_STR_FLR_PAD_GP_CPCTY                            
                  (STR_FLR_PAD_GP_CPCTY_ID                                      
                  ,FLR_PAD_GP_ID                                                
                  ,STR_CPCTY_GP_SRC_CDE                                         
                  ,LOC_NBR                                                      
                  ,EFF_BEG_DTE                                                  
                  ,EFF_END_DTE                                                  
                  ,CPCTY_GP_CDE                                                 
                  ,LAST_UPD_TMST                                                
                  ,LAST_UPD_USR_ID                                              
                  ,CPCTY_OVRRID_RSN_CDE )                                       
              VALUES (                                                          
                FCS_STR_FLR_PAD_GP_CPCTY.NEXTVAL                                
               ,:FW-FLRPD-GRP-ID                                                
               ,'U'                                                             
               ,:FW-STORE-NBR                                                   
         ,TO_DATE(:PV-SEASON-BEGIN, 'DD-MON-YYYY HH24:MI:SS')                   
         ,TO_DATE(:PV-SEASON-END, 'DD-MON-YYYY HH24:MI:SS')                     
               ,:FW-FLRPD-CPCTY                                                 
               ,TO_DATE(:PV-CURRENT-DTE, 'DD-MON-YYYY HH24:MI:SS')              
               ,'SYSTEM'                                                        
               ,'SV' )                                                          
               END-EXEC.                                                        
                                                                                
               EVALUATE SQLCODE                                                 
               WHEN +1403                                                       
                 DISPLAY ' INSERT FAILED!'                                      
               WHEN ZERO                                                        
                 SET PS-U-REC-EXISTS TO TRUE                                    
                 ADD 1 TO PV-U-RECS-INSERTED                                    
                 CONTINUE                                                       
               WHEN OTHER                                                       
                 DISPLAY ' INSERT FAILED!'                                      
                 MOVE '8500-INSERT-U-RECORD'                                    
                                  TO PV-DB2-OPERATION                           
P51977          DISPLAY 'STORE NO : ' FW-STORE-NBR                              
P51977          DISPLAY 'GROUP ID : ' FW-FLRPD-GRP-ID                           
P51977          DISPLAY 'FLOORPD CPCTY: ' FW-FLRPD-CPCTY                        
                 PERFORM 9998-SQL-ABEND                                         
             END-EVALUATE.                                                      
                                                                                
      ****************************************************************          
      * UPDATE 'U' RECORD WITH CAPACITY SPECIFIED IN CSV FILE                   
      ****************************************************************          
          8700-UPDATE-U-RECORD.                                                 
            MOVE '8700-UPDATE-U-RECORD' TO PV-PARAGRAPH-NAME.                   
                                                                                
             EXEC SQL                                                           
               UPDATE FCT_STR_FLR_PAD_GP_CPCTY                                  
               SET CPCTY_GP_CDE = :FW-FLRPD-CPCTY                               
                  ,LAST_UPD_TMST =                                              
                     TO_DATE(:PV-CURRENT-DTE, 'DD-MON-YYYY HH24:MI:SS')         
                  ,LAST_UPD_USR_ID = 'SYSTEM'                                   
                  ,EFF_BEG_DTE =                                                
                     TO_DATE(:PV-SEASON-BEGIN, 'DD-MON-YYYY HH24:MI:SS')        
                  ,EFF_END_DTE =                                                
                     TO_DATE(:PV-SEASON-END, 'DD-MON-YYYY HH24:MI:SS')          
                WHERE FLR_PAD_GP_ID = :FW-FLRPD-GRP-ID                          
                AND LOC_NBR =   :FW-STORE-NBR                                   
                AND STR_CPCTY_GP_SRC_CDE = 'U'                                  
                AND EFF_BEG_DTE =                                               
                     TO_DATE(:PV-SEASON-BEGIN, 'DD-MON-YYYY HH24:MI:SS')        
              END-EXEC.                                                         
                                                                                
              EVALUATE SQLCODE                                                  
                  WHEN +1403                                                    
                    DISPLAY  ' UPDATE FAILED!'                                  
                  WHEN ZERO                                                     
                  DISPLAY 'FLOORPAD CAPACITY UPDATE TO ' FW-FLRPD-CPCTY         
                          ' FOR ' PV-CSV-SEASONS (IDX) ' WAS SUCCESSFUL'        
                     ADD 1 TO PV-U-RECS-UPDATED                                 
                     CONTINUE                                                   
                  WHEN OTHER                                                    
                     DISPLAY ' INSERT FAILED!'                                  
                     MOVE '8700-UPDATE-U-RECORD'                                
                                       TO PV-DB2-OPERATION                      
                     PERFORM 9998-SQL-ABEND                                     
              END-EVALUATE.                                                     
      ****************************************************************          
       8800-WRITE-STR-NBR-SKIP.                                                 
                                                                                
              INITIALIZE FW-SKIP-OUT-REC.                                       
                                                                                
              MOVE FW-STORE-NBR     TO FW-SKIP-STORE-NBR                        
              MOVE FW-FLRPD-GRP-CDE TO FW-SKIP-FLRPD-GRP-CDE                    
              MOVE FW-FLRPD-CPCTY   TO FW-SKIP-FLRPD-CPCTY                      
              ADD  1                TO PV-RECS-SKIPPED.                         
              DISPLAY 'WARNING: CAPACITY GROUP CODE '                           
                   FW-FLRPD-CPCTY ' FOR' FW-STORE-NBR ' IS NOT VALID.'          
                          'ROW SKIPPED'                                         
              WRITE OUTPUT-SKIP-RECORD FROM FW-SKIP-OUT-REC.                    
                                                                                
      ****************************************************************          
          8900-COMMIT-DATA.                                                     
              EXEC SQL                                                          
                  COMMIT                                                        
              END-EXEC.                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                    CONTINUE                                                    
               WHEN OTHER                                                       
                      DISPLAY 'UNABLE TO COMMIT DATA'                           
                      MOVE '8900-COMMIT-DATA' TO PV-PARAGRAPH-NAME              
                      PERFORM 9998-SQL-ABEND                                    
               END-EVALUATE.                                                    
216764****************************************************************          
216764* LOGON TO ORACLE                                                         
216764****************************************************************          
216764 8000-READ-ORACLE-LOGON.                                                  
216764                                                                          
216764     READ ORACLE-LOGON-FILE                                               
216764         INTO CC-ORACLE-CONNECTION                                        
216764         AT END                                                           
216764             SET PS-END-OF-LOGON-FILE TO TRUE                             
216764     END-READ.                                                            
216764                                                                          
      ****************************************************************          
          9000-END-ROUTINE.                                                     
             CLOSE INPUT-FILE.                                                  
             DISPLAY ' '.                                                       
             DISPLAY 'NUMBER OF CSV SEASONS READ: ' PV-SEASON-COUNT.            
             DISPLAY 'TOTAL CSV RECORDS READ: ' PV-RECS-READ.                   
             DISPLAY 'S RECORDS INSERTED: ' PV-S-RECS-INSERTED.                 
             DISPLAY 'U RECORDS INSERTED: ' PV-U-RECS-INSERTED.                 
             DISPLAY 'U RECORDS UPDATED:  ' PV-U-RECS-UPDATED.                  
             DISPLAY ' '.                                                       
             DISPLAY 'TOTAL RECORDS SKIPPED: ' PV-RECS-SKIPPED.                 
             DISPLAY ' '.                                                       
                                                                                
      ****************************************************************          
                                                                                
                                                                                
      ***************************************************************           
      *                                                                         
      ***************************************************************           
          9998-SQL-ABEND.                                                       
             MOVE SQLCODE        TO  SQLCODE2.                                  
             MOVE SQLERRD(3)     TO  SQLERRD3.                                  
             DISPLAY '***************************'                              
             DISPLAY '**       ABEND           **'                              
             DISPLAY '***************************'                              
             DISPLAY '** OPERATION **      = ' PV-DB2-OPERATION                 
             DISPLAY 'PARAGRAPH            = ' PV-PARAGRAPH-NAME                
             DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                        
P51977       DISPLAY '** SQLCODE2**        = ' SQLCODE2.                        
             MOVE +4013         TO PV-ABEND-CODE                                
             CALL 'ILBOABN0' USING PV-ABEND-CODE.                               
                                                                                
      ****************************************************************          
      *    PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT A    *          
      *    NON-SQL ERROR OCCURS.                                     *          
      ****************************************************************          
          9999-ABEND.                                                           
           DISPLAY ' '.                                                         
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = '                                         
                    PC-PGMNAME.                                                 
           DISPLAY '** PARAGRAPH ** = '                                         
                    PV-PARAGRAPH-NAME.                                          
           DISPLAY WS-MSG-TEXT.                                                 
                                                                                
           MOVE +4007 TO PV-ABEND-CODE.                                         
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
      ****************************************************************          
      *  COPY BOOK CONTAINS THE PROCEDURE DIVISION STATEMENTS      *            
      *    THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         *          
      *    "DSNTIAR" AND CONVERT THE SQLCA INFORMATION INTO A        *          
      *    READABLE FORMAT.                                          *          
      ****************************************************************          
           COPY DPPD004.                                                        
                                                                                
