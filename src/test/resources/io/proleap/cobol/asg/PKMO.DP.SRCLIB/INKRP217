       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    INKRP217.                                                 
       AUTHOR.        NIJESH/INFOSYS.                                           
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11/28/08.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *           INKRP217 - BLOCKED ML TRANSACTIONS REPORT                     
      *                                                                         
      *      THIS PROGRAM READS A FILE OF MERCHANDISE LEDGER                    
      * TRANSACTIONS WHOSE DEPARTMENT/CLASSES WERE SKIPPED, AND                 
      * CREATES A BLOCKED ML TRANSACTIONS REPORT.                               
      *      EACH TRANSACTION ON THE INPUT FILE IS REPORTED.  A PAGE            
      * BREAK OCCURS WHENEVER THE TRANSACTION CODE CHANGES.  THE                
      * TRANSACTION CATEGORIES ARE: PURCHASES, SALES, TRANSFERS,                
      * MARKDOWNS, MARKUPS, AND ADJUSTMENTS.                                    
      *                                                                         
      *      THIS PROGRAM IS COPIED FROM MLKRP217 AND IT BREAKS THE             
      * REPORT ON CHANGE IN FISCAL MONTH,CYCLE NUMBER, TRANS CODE,              
      * BATCH NUMBER.                                                           
      *                                                                         
      * CHANGE HISTORY:                                                         
      *    11/28/08 - NIJESH/INFOSYS - WR# 33304                                
      *      HOLDING INVENTORY LEDGER OPEN ACROSS PERIOD (HILO).                
      *                                                                         
208057*    09/06/18 - JIM HUNTER     - CHG0208057                               
208057*      MAINFRAME REFACTORING - REMOVE UNUSED COPYBOOKS                    
208057*                              MLWS502 AND MLLS502.                       
208057*                                                                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT BLOCKED-TRANS-FILE                                            
               ASSIGN TO INP01.                                                 
           SELECT BLOCKED-TRANS-REPORT                                          
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BLOCKED-TRANS-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F.                                                
       01  BLOCKED-TRANS-RECORD        PIC X(80).                               
                                                                                
       FD  BLOCKED-TRANS-REPORT                                                 
           LABEL RECORDS ARE OMITTED                                            
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F.                                                
       01  BLOCKED-TRANS-REPORT-LINE   PIC X(100).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-LINE-COUNT            PIC S9(04)    VALUE +59             
                                                          COMP SYNC.            
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'INKRP217'.        
           05  PC-MAX-RETRIES               PIC S9(04)    VALUE +3              
                                                          COMP SYNC.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-JOB-INFO.                                                     
               10  FILLER                  PIC X(01)       VALUE '('.           
               10  PV-JOBNAME              PIC X(08)       VALUE SPACES.        
               10  FILLER                  PIC X(01)       VALUE ')'.           
           05  PV-MCL-NBR.                                                      
               10  PV-MAJ-CLASS-NBR         PIC  9(01)    VALUE ZERO.           
               10  PV-MAJ-CLASS-ZERO        PIC  9(01)    VALUE ZERO.           
                                                                                
           05  PV-ADVANCE                   PIC S9(03)    VALUE ZERO            
                                                          COMP-3.               
           05  PV-LINE-COUNT                PIC S9(04)    VALUE ZERO            
                                                          COMP SYNC.            
           05  PV-PAGE-COUNT                PIC S9(05)    VALUE ZERO.           
           05  PV-OPER-ATTEMPTED            PIC  X(35)    VALUE SPACES.         
           05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACE.          
           05  PV-PREV-TRANS-CDE            PIC  X(01)    VALUE SPACE.          
           05  PV-PREV-BATCH-NBR            PIC  X(03)    VALUE SPACE.          
           05  PV-PREV-FISC-MN-DTE          PIC  X(10)    VALUE SPACES.         
           05  PV-PREV-INV-ID               PIC S9(09)    COMP                  
                                                          VALUE ZERO.           
           05  PV-RETRY-COUNT               PIC S9(04)    VALUE ZERO            
                                                          COMP SYNC.            
           05  PV-GRND-RETAIL-TOTAL         PIC S9(09)V99 VALUE +0              
                                                          COMP-3.               
           05  PV-GRND-COST-TOTAL           PIC S9(09)V99 VALUE +0              
                                                          COMP-3.               
           05  PV-DATE-RETAIL-TOTAL         PIC S9(09)V99 VALUE +0              
                                                          COMP-3.               
           05  PV-DATE-COST-TOTAL           PIC S9(09)V99 VALUE +0              
                                                          COMP-3.               
           05  PV-CYCL-RETAIL-TOTAL         PIC S9(09)V99 VALUE +0              
                                                          COMP-3.               
           05  PV-CYCL-COST-TOTAL           PIC S9(09)V99 VALUE +0              
                                                          COMP-3.               
           05  PV-BTCH-RETAIL-TOTAL         PIC S9(09)V99 VALUE +0              
                                                          COMP-3.               
           05  PV-BTCH-COST-TOTAL           PIC S9(09)V99 VALUE +0              
                                                          COMP-3.               
           05  PV-POS-RETAIL-TOTAL          PIC S9(09)V99 VALUE +0              
                                                          COMP-3.               
           05  PV-POS-COST-TOTAL            PIC S9(09)V99 VALUE +0              
                                                          COMP-3.               
           05  PV-COSA-RETAIL-TOTAL         PIC S9(09)V99 VALUE +0              
                                                          COMP-3.               
           05  PV-COSA-COST-TOTAL           PIC S9(09)V99 VALUE +0              
                                                          COMP-3.               
                                                                                
       01  SW-SWITCHES.                                                         
           05  SW-END-OF-INPUT-FILE-SW PIC X       VALUE 'N'.                   
               88  SW-END-OF-INPUT-FILE            VALUE 'Y'.                   
               88  SW-NOT-END-OF-INPUT-FILE        VALUE 'N'.                   
           05  SW-CHANGE-IN-HEADER-SW       PIC X         VALUE 'N'.            
               88  SW-HEADER-CHANGE-YES                   VALUE 'Y'.            
               88  SW-HEADER-CHANGE-NO                    VALUE 'N'.            
           05  SW-GRAND-TOTAL-SW            PIC X         VALUE 'N'.            
               88  SW-GRAND-TOTAL-YES                     VALUE 'Y'.            
               88  SW-GRAND-TOTAL-NO                      VALUE 'N'.            
           05  SW-DATE-TOTAL-SW             PIC X         VALUE 'N'.            
               88  SW-DATE-TOTAL-YES                      VALUE 'Y'.            
               88  SW-DATE-TOTAL-NO                       VALUE 'N'.            
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
      *****************************************************************         
      * BLOCKED-TRANS-RECORD LAYOUT.                                            
      *****************************************************************         
                                                                                
           COPY MLRD123.                                                        
                                                                                
      ******************************************************************        
      * STANDARD REPORT HEADING                                                 
      ******************************************************************        
      *01  DP100-STANDRD-HEADER-100-COLS.                                       
      *01  DP100-CNFDL-HDR-100-COLS.                                            
           COPY DPWS100.                                                        
                                                                                
      ******************************************************************        
      * HEADER LINE                                                             
      ******************************************************************        
      *01  DP085-GET-CURR-JOB-INFO-PGM.                                         
      *01  DP085-GET-CURR-JOB-INFO-PARMS.                                       
           COPY DPWS085.                                                        
                                                                                
       01  RD-REPORT-TITLE-LINE.                                                
           05  FILLER                  PIC  X(35)    VALUE SPACES.              
           05  FILLER                  PIC  X(30)    VALUE                      
               'BLOCKED ML TRANSACTIONS REPORT'.                                
           05  FILLER                  PIC  X(35)    VALUE SPACES.              
                                                                                
       01  RD-REPORT-HDR-LINE1.                                                 
           05  RD-HDR-CATEGORY         PIC  X(11)    VALUE SPACES.              
           05  FILLER                  PIC  X(89)    VALUE SPACES.              
                                                                                
       01  RD-REPORT-HDR-LINE2.                                                 
           05  FILLER                  PIC  X(04)    VALUE SPACES.              
           05  FILLER                  PIC  X(03)    VALUE SPACES.              
           05  FILLER                  PIC  X(05)    VALUE SPACES.              
           05  FILLER                  PIC  X(03)    VALUE SPACES.              
           05  FILLER                  PIC  X(03)    VALUE SPACES.              
           05  FILLER                  PIC  X(09)    VALUE SPACES.              
           05  FILLER                  PIC  X(06)    VALUE SPACES.              
           05  FILLER                  PIC  X(14)    VALUE SPACES.              
           05  FILLER                  PIC  X(04)    VALUE SPACES.              
           05  FILLER                  PIC  X(06)    VALUE SPACES.              
           05  FILLER                  PIC  X(02)    VALUE SPACES.              
           05  FILLER                  PIC  X(09)    VALUE SPACES.              
           05  FILLER                  PIC  X(05)    VALUE 'SALES'.             
           05  FILLER                  PIC  X(08)    VALUE SPACES.              
           05  FILLER                  PIC  X(03)    VALUE SPACES.              
           05  FILLER                  PIC  X(16)    VALUE SPACES.              
                                                                                
       01  RD-REPORT-HDR-LINE3.                                                 
           05  FILLER                  PIC  X(08)    VALUE '  SKU   '.          
           05  FILLER                  PIC  X(02)    VALUE SPACES.              
           05  FILLER                  PIC  X(04)    VALUE 'DEPT'.              
           05  FILLER                  PIC  X(03)    VALUE SPACES.              
           05  FILLER                  PIC  X(05)    VALUE 'SUBCL'.             
           05  FILLER                  PIC  X(02)    VALUE SPACES.              
           05  FILLER                  PIC  X(05)    VALUE 'CLASS'.             
           05  FILLER                  PIC  X(08)    VALUE SPACES.              
           05  FILLER                  PIC  X(06)    VALUE 'RETAIL'.            
           05  FILLER                  PIC  X(14)    VALUE SPACES.              
           05  FILLER                  PIC  X(04)    VALUE 'COST'.              
           05  FILLER                  PIC  X(06)    VALUE SPACES.              
           05  FILLER                  PIC  X(02)    VALUE 'PO'.                
           05  FILLER                  PIC  X(09)    VALUE SPACES.              
           05  FILLER                  PIC  X(11)    VALUE                      
               'CORRECTIONS'.                                                   
           05  FILLER                  PIC  X(02)    VALUE SPACES.              
           05  FILLER                  PIC  X(03)    VALUE 'POS'.               
      *    05  FILLER                  PIC  X(16)    VALUE SPACES.              
           05  FILLER                  PIC  X(06)    VALUE SPACES.              
                                                                                
       01  RD-REPORT-HDR-LINE4.                                                 
           05  FILLER                  PIC  X(08)    VALUE '--------'.          
           05  FILLER                  PIC  X(02)    VALUE SPACES.              
           05  FILLER                  PIC  X(04)    VALUE '----'.              
           05  FILLER                  PIC  X(03)    VALUE SPACES.              
           05  FILLER                  PIC  X(05)    VALUE '-----'.             
           05  FILLER                  PIC  X(02)    VALUE SPACES.              
           05  FILLER                  PIC  X(05)    VALUE '-----'.             
           05  FILLER                  PIC  X(02)    VALUE SPACES.              
           05  FILLER                  PIC  X(14)    VALUE                      
               '--------------'.                                                
           05  FILLER                  PIC  X(04)    VALUE SPACES.              
           05  FILLER                  PIC  X(14)    VALUE                      
               '--------------'.                                                
           05  FILLER                  PIC  X(04)    VALUE SPACES.              
           05  FILLER                  PIC  X(05)    VALUE '-----'.             
           05  FILLER                  PIC  X(06)    VALUE SPACES.              
           05  FILLER                  PIC  X(11)    VALUE                      
               '-----------'.                                                   
           05  FILLER                  PIC  X(02)    VALUE SPACES.              
           05  FILLER                  PIC  X(03)    VALUE '---'.               
           05  FILLER                  PIC  X(16)    VALUE SPACES.              
                                                                                
       01  RT-REPORT-TOTAL-LINE-1.                                              
      *    05  FILLER                  PIC  X(21)    VALUE SPACES.              
           05  FILLER                  PIC  X(31)    VALUE SPACES.              
           05  FILLER                  PIC  X(14)    VALUE                      
               '--------------'.                                                
           05  FILLER                  PIC  X(04)    VALUE SPACES.              
           05  FILLER                  PIC  X(14)    VALUE                      
               '--------------'.                                                
      *    05  FILLER                  PIC  X(47)    VALUE SPACES.              
           05  FILLER                  PIC  X(37)    VALUE SPACES.              
                                                                                
       01  CYCLE-MONTH-HDR-LINE.                                                
           05  FILLER                       PIC  X(05)    VALUE                 
                                                              'CYCLE'.          
           05  FILLER                       PIC  X(01)    VALUE SPACES.         
           05  CYCLE-ID                     PIC  9(05)    VALUE ZEROES.         
           05  FILLER                       PIC  X(30)    VALUE SPACES.         
           05  CYCLE-NAME                   PIC  X(20)    VALUE SPACES.         
           05  FILLER                       PIC  X(32)    VALUE SPACES.         
                                                                                
       01  MONTH-TOTAL-HEADER.                                                  
           05  FILLER                       PIC  X(06)    VALUE                 
                                                              'MONTH '.         
           05  MONTH-NAM                    PIC  X(03)    VALUE SPACES.         
           05  FILLER                       PIC  X(39)    VALUE SPACES.         
           05  MNTH-NAME                    PIC  X(10)    VALUE SPACES.         
           05  FILLER                       PIC  X(50)    VALUE SPACES.         
                                                                                
       01  GRAND-TOTAL-HEADER.                                                  
           05  FILLER                       PIC  X(11)    VALUE                 
                                                         'CYCLE 99999'.         
           05  FILLER                       PIC  X(95)    VALUE SPACES.         
                                                                                
       01  RT-REPORT-SUB-TOTAL-LINE-1.                                          
           05  FILLER                  PIC  X(01)    VALUE SPACES.              
           05  FILLER                  PIC  X(17)    VALUE                      
               'POINT OF SALE S/T'.                                             
      *    05  FILLER                  PIC  X(03)    VALUE SPACES.              
           05  FILLER                  PIC  X(13)    VALUE SPACES.              
           05  RT-POS-RETAIL-AMT-TTL   PIC  ZZZ,ZZZ,ZZ9.99-                     
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(03)    VALUE SPACES.              
           05  RT-POS-COST-AMT-TTL     PIC  ZZZ,ZZZ,ZZ9.99-                     
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(28)    VALUE SPACES.              
           05  RT-POS-IND              PIC  X(01)    VALUE SPACES.              
      *    05  FILLER                  PIC  X(17)    VALUE SPACES.              
           05  FILLER                  PIC  X(07)    VALUE SPACES.              
                                                                                
       01  RT-REPORT-SUB-TOTAL-LINE-2.                                          
           05  FILLER                  PIC  X(01)    VALUE SPACES.              
           05  FILLER                  PIC  X(17)    VALUE                      
               'SALES CORRECT S/T'.                                             
      *    05  FILLER                  PIC  X(03)    VALUE SPACES.              
           05  FILLER                  PIC  X(13)    VALUE SPACES.              
           05  RT-COSA-RETAIL-AMT-TTL  PIC  ZZZ,ZZZ,ZZ9.99-                     
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(03)    VALUE SPACES.              
           05  RT-COSA-COST-AMT-TTL    PIC  ZZZ,ZZZ,ZZ9.99-                     
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(16)    VALUE SPACES.              
           05  RT-COSA-IND             PIC  X(01)    VALUE SPACES.              
      *    05  FILLER                  PIC  X(28)    VALUE SPACES.              
           05  FILLER                  PIC  X(18)    VALUE SPACES.              
                                                                                
       01  RT-REPORT-SUB-TOTAL-LINE-3.                                          
           05  FILLER                  PIC  X(08)  VALUE ' BATCH  '.            
           05  RT-BTCH-BATCH-NBR       PIC  X(03)  VALUE SPACES.                
      *    05  FILLER                  PIC  X(10)  VALUE SPACES.                
           05  FILLER                  PIC  X(20)  VALUE SPACES.                
           05  RT-BTCH-RETAIL-AMT-TTL  PIC  ZZZ,ZZZ,ZZ9.99- VALUE ZERO.         
           05  FILLER                  PIC  X(03)  VALUE SPACES.                
           05  RT-BTCH-COST-AMT-TTL    PIC  ZZZ,ZZZ,ZZ9.99- VALUE ZERO.         
      *    05  FILLER                  PIC  X(46)  VALUE SPACES.                
           05  FILLER                  PIC  X(36)  VALUE SPACES.                
                                                                                
       01  RD-REPORT-DETAIL-LINE.                                               
           05  RD-SKU-NBR              PIC  X(08)    VALUE SPACES.              
           05  FILLER                  PIC  X(02)    VALUE SPACES.              
           05  RD-DEPT-NBR             PIC  X(03)    VALUE SPACES.              
           05  FILLER                  PIC  X(05)    VALUE SPACES.              
           05  RD-CLASS-NBR            PIC  X(02)    VALUE SPACES.              
           05  FILLER                  PIC  X(05)    VALUE SPACES.              
      *    05  RD-SEQ-NBR              PIC  X(03)    VALUE SPACES.              
           05  RD-MCL-NBR              PIC  X(02)    VALUE SPACES.              
           05  FILLER                  PIC  X(04)    VALUE SPACES.              
           05  RD-RETAIL-AMT           PIC  ZZZ,ZZZ,ZZ9.99-                     
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(03)    VALUE SPACES.              
           05  RD-COST-AMT             PIC  ZZZ,ZZZ,ZZ9.99-                     
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(03)    VALUE SPACES.              
           05  RD-PO-NBR               PIC  X(09)    VALUE SPACES.              
           05  FILLER                  PIC  X(04)    VALUE SPACES.              
           05  RD-COSA-IND             PIC  X(01)    VALUE SPACES.              
           05  FILLER                  PIC  X(11)    VALUE SPACES.              
           05  RD-POS-IND              PIC  X(01)    VALUE SPACES.              
      *    05  FILLER                  PIC  X(17)    VALUE SPACES.              
           05  FILLER                  PIC  X(07)    VALUE SPACES.              
                                                                                
       01  RT-REPORT-CYCLE-TOTAL-LINE.                                          
           05  FILLER                       PIC  X(06)    VALUE                 
                                                            'CYCLE '.           
           05  RT-CYCLE-NBR                 PIC  9(05)    VALUE ZEROES.         
           05  FILLER                       PIC  X(02)    VALUE SPACES.         
           05  FILLER                       PIC  X(06)    VALUE                 
                                                              'TOTALS'.         
           05  FILLER                       PIC  X(12)    VALUE SPACES.         
           05  RT-CYCL-RETAIL-AMT-TTL       PIC  ZZZ,ZZZ,ZZ9.99-                
                                                          VALUE ZERO.           
           05  FILLER                       PIC  X(03)    VALUE SPACES.         
           05  RT-CYCL-COST-AMT-TTL         PIC  ZZZ,ZZZ,ZZ9.99-                
                                                          VALUE ZERO.           
           05  FILLER                       PIC  X(36)    VALUE SPACES.         
                                                                                
       01  RT-REPORT-MONTH-TOTAL-LINE.                                          
           05  FILLER                       PIC  X(01)    VALUE SPACES.         
           05  RT-MONTH-NAME                PIC  X(10)    VALUE SPACES.         
           05  FILLER                       PIC  X(02)    VALUE SPACES.         
           05  FILLER                       PIC  X(06)    VALUE                 
                                                              'TOTALS'.         
           05  FILLER                       PIC  X(12)    VALUE SPACES.         
           05  RT-MNTH-RETAIL-AMT-TTL       PIC  ZZZ,ZZZ,ZZ9.99-                
                                                          VALUE ZERO.           
           05  FILLER                       PIC  X(03)    VALUE SPACES.         
           05  RT-MNTH-COST-AMT-TTL         PIC  ZZZ,ZZZ,ZZ9.99-                
                                                          VALUE ZERO.           
           05  FILLER                       PIC  X(36)    VALUE SPACES.         
                                                                                
       01  RT-REPORT-GRAND-TOTAL-LINE.                                          
           05  FILLER                       PIC  X(03)    VALUE SPACES.         
           05  FILLER                       PIC  X(12)    VALUE                 
                                                        'GRAND TOTALS'.         
           05  FILLER                       PIC  X(16)    VALUE SPACES.         
           05  RT-GRND-RETAIL-AMT-TTL       PIC  ZZZ,ZZZ,ZZ9.99-                
                                                          VALUE ZERO.           
           05  FILLER                       PIC  X(03)    VALUE SPACES.         
           05  RT-GRND-COST-AMT-TTL         PIC  ZZZ,ZZZ,ZZ9.99-                
                                                          VALUE ZERO.           
           05  FILLER                       PIC  X(36)    VALUE SPACES.         
                                                                                
       01  ER-END-OF-REPORT-LINE.                                               
           05  FILLER                       PIC  X(21)    VALUE                 
                                                '*** END OF REPORT ***'.        
                                                                                
      ******************************************************************        
      * DCLGEN FOR TDTATTR.                                                     
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DCLGEN FOR TINCNTL.                                                     
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * USED FOR SQL COMMUNICATION AREA.                                        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 ERROR MESSAGES FORMAT AREA.                                         
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
           OPEN INPUT  BLOCKED-TRANS-FILE                                       
                OUTPUT BLOCKED-TRANS-REPORT.                                    
                                                                                
           PERFORM B100-INITIALIZATION.                                         
                                                                                
           PERFORM C100-PROCESS-INPUT-FILE                                      
               UNTIL SW-END-OF-INPUT-FILE.                                      
                                                                                
           IF PV-PREV-TRANS-CDE NOT = SPACES OR                                 
              PV-PREV-BATCH-NBR NOT = SPACES                                    
               PERFORM D100-PRINT-BATCH-TOTAL                                   
               PERFORM D200-PRINT-CYCLE-TOTAL                                   
               PERFORM D300-PRINT-DATE-TOTAL                                    
               PERFORM D400-PRINT-GRAND-TOTAL                                   
           END-IF.                                                              
                                                                                
           MOVE ER-END-OF-REPORT-LINE TO BLOCKED-TRANS-REPORT-LINE.             
           MOVE 3 TO PV-ADVANCE.                                                
           PERFORM Z200-WRITE-REPORT-LINE.                                      
                                                                                
           CLOSE BLOCKED-TRANS-FILE                                             
                 BLOCKED-TRANS-REPORT.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       B100-INITIALIZATION.                                                     
                                                                                
           MOVE FUNCTION CURRENT-DATE (1:4) TO DP100-RUN-YEAR.                  
           MOVE FUNCTION CURRENT-DATE (5:2) TO DP100-RUN-MONTH.                 
           MOVE FUNCTION CURRENT-DATE (7:2) TO DP100-RUN-DAY.                   
           MOVE FUNCTION CURRENT-DATE (9:2) TO DP100-RUN-HOUR.                  
           MOVE FUNCTION CURRENT-DATE (11:2) TO DP100-RUN-MINUTE.               
           MOVE PC-PROGRAM-NAME     TO DP100-PROGRAM-NAME.                      
           MOVE 1                   TO DP100-REPORT-NUMBER.                     
                                                                                
           PERFORM Z100-READ-BLOCKED-TRANS-FILE.                                
           IF SW-NOT-END-OF-INPUT-FILE                                          
               PERFORM E100-GET-FSCL-MONTH-NAME                                 
               IF  ((ML123-INV-ID < 1)                                          
                   OR (ML123-INV-ID > 99))                                      
                   MOVE ZEROES              TO CYCLE-ID                         
                   MOVE 'NO ACTIVE INVENTORY '                                  
                                            TO CYCLE-NAME                       
               ELSE                                                             
                   PERFORM E200-GET-INVENTORY-NAME                              
               END-IF                                                           
               MOVE ML123-OPN-FSCL-MN-DTE   TO PV-PREV-FISC-MN-DTE              
               MOVE ML123-INV-ID            TO PV-PREV-INV-ID                   
               MOVE ML123-TRN-CDE           TO PV-PREV-TRANS-CDE                
               MOVE ML123-BATCH-NBR         TO PV-PREV-BATCH-NBR                
           END-IF.                                                              
                                                                                
      *  GET CURRENT JOB INFORMATION FOR USE IN THE REPORT                      
           CALL DP085-GET-CURR-JOB-INFO-PGM                                     
               USING DP085-GET-CURR-JOB-INFO-PARMS.                             
           MOVE DP085-JOB-NAME     TO PV-JOBNAME.                               
                                                                                
           MOVE PV-JOB-INFO                                                     
               TO DP100-STANDRD-HEADER-100-COLS(20:10).                         
                                                                                
           PERFORM Z300-PRINT-RPT-HEADINGS.                                     
                                                                                
                                                                                
      ******************************************************************        
      * READ AND PROCESS THE INPUT FILE.                                        
      ******************************************************************        
       C100-PROCESS-INPUT-FILE.                                                 
                                                                                
           IF  ML123-OPN-FSCL-MN-DTE NOT = PV-PREV-FISC-MN-DTE                  
               PERFORM D100-PRINT-BATCH-TOTAL                                   
               PERFORM D200-PRINT-CYCLE-TOTAL                                   
               PERFORM D300-PRINT-DATE-TOTAL                                    
               MOVE ML123-OPN-FSCL-MN-DTE   TO PV-PREV-FISC-MN-DTE              
               MOVE ML123-INV-ID            TO PV-PREV-INV-ID                   
               MOVE ML123-TRN-CDE           TO PV-PREV-TRANS-CDE                
               MOVE ML123-BATCH-NBR         TO PV-PREV-BATCH-NBR                
               MOVE 99                      TO PV-LINE-COUNT                    
               SET SW-HEADER-CHANGE-YES     TO TRUE                             
               PERFORM E100-GET-FSCL-MONTH-NAME                                 
               IF  ((ML123-INV-ID < 1)                                          
                   OR (ML123-INV-ID > 99))                                      
                   MOVE ZEROES              TO CYCLE-ID                         
                   MOVE 'NO ACTIVE INVENTORY '                                  
                                            TO CYCLE-NAME                       
               ELSE                                                             
                   PERFORM E200-GET-INVENTORY-NAME                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ((ML123-INV-ID NOT = PV-PREV-INV-ID)                             
               AND (SW-HEADER-CHANGE-NO))                                       
               PERFORM D100-PRINT-BATCH-TOTAL                                   
               PERFORM D200-PRINT-CYCLE-TOTAL                                   
               MOVE ML123-INV-ID            TO PV-PREV-INV-ID                   
               MOVE ML123-TRN-CDE           TO PV-PREV-TRANS-CDE                
               MOVE ML123-BATCH-NBR         TO PV-PREV-BATCH-NBR                
               MOVE 99                      TO PV-LINE-COUNT                    
               SET SW-HEADER-CHANGE-YES     TO TRUE                             
               IF  ((ML123-INV-ID < 1)                                          
                   OR (ML123-INV-ID > 99))                                      
                   MOVE ZEROES              TO CYCLE-ID                         
                   MOVE 'NO ACTIVE INVENTORY '                                  
                                            TO CYCLE-NAME                       
               ELSE                                                             
                   PERFORM E200-GET-INVENTORY-NAME                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ((ML123-TRN-CDE NOT = PV-PREV-TRANS-CDE)                         
               AND (SW-HEADER-CHANGE-NO))                                       
               PERFORM D100-PRINT-BATCH-TOTAL                                   
               MOVE ML123-TRN-CDE           TO PV-PREV-TRANS-CDE                
               MOVE ML123-BATCH-NBR         TO PV-PREV-BATCH-NBR                
               MOVE 99                      TO PV-LINE-COUNT                    
               SET SW-HEADER-CHANGE-YES     TO TRUE                             
           END-IF.                                                              
                                                                                
           IF  ((ML123-BATCH-NBR NOT = PV-PREV-BATCH-NBR)                       
               AND (SW-HEADER-CHANGE-NO))                                       
               PERFORM D100-PRINT-BATCH-TOTAL                                   
               MOVE ML123-BATCH-NBR         TO PV-PREV-BATCH-NBR                
           END-IF.                                                              
                                                                                
           SET SW-HEADER-CHANGE-NO          TO TRUE.                            
                                                                                
           IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                                 
               PERFORM Z300-PRINT-RPT-HEADINGS                                  
           END-IF.                                                              
                                                                                
           PERFORM C110-WRITE-DETAIL-LINE.                                      
                                                                                
      ******************************************************************        
      * WRITE A REPORT DETAIL LINE FOR EACH INPUT RECORD.                       
      ******************************************************************        
       C110-WRITE-DETAIL-LINE.                                                  
                                                                                
           MOVE ML123-DEPT-NBR    TO RD-DEPT-NBR.                               
           MOVE ML123-CL-NBR      TO RD-CLASS-NBR.                              
           MOVE ML123-SKU         TO RD-SKU-NBR.                                
           MOVE ML123-CL-NBR(1:1) TO PV-MAJ-CLASS-NBR                           
           MOVE PV-MCL-NBR        TO RD-MCL-NBR.                                
           MOVE ML123-RTL-AMT     TO RD-RETAIL-AMT.                             
           MOVE ML123-CST-AMT     TO RD-COST-AMT.                               
           MOVE ML123-PO-NBR      TO RD-PO-NBR.                                 
           IF ML123-COSA-IND = 'Y'                                              
               MOVE '*'           TO RD-COSA-IND                                
               MOVE '*'           TO RT-COSA-IND                                
               ADD ML123-RTL-AMT  TO PV-COSA-RETAIL-TOTAL                       
               ADD ML123-CST-AMT  TO PV-COSA-COST-TOTAL                         
           ELSE                                                                 
               MOVE SPACE         TO RD-COSA-IND                                
           END-IF.                                                              
                                                                                
           IF ML123-POS-IND = 'Y'                                               
               MOVE '*'           TO RD-POS-IND                                 
               MOVE '*'           TO RT-POS-IND                                 
               ADD ML123-RTL-AMT  TO PV-POS-RETAIL-TOTAL                        
               ADD ML123-CST-AMT  TO PV-POS-COST-TOTAL                          
           ELSE                                                                 
               MOVE SPACE         TO RD-POS-IND                                 
           END-IF.                                                              
                                                                                
           MOVE RD-REPORT-DETAIL-LINE TO BLOCKED-TRANS-REPORT-LINE.             
           PERFORM Z200-WRITE-REPORT-LINE.                                      
           MOVE 1  TO PV-ADVANCE.                                               
                                                                                
           ADD ML123-RTL-AMT                TO PV-BTCH-RETAIL-TOTAL             
                                               PV-CYCL-RETAIL-TOTAL             
                                               PV-DATE-RETAIL-TOTAL             
                                               PV-GRND-RETAIL-TOTAL.            
           ADD ML123-CST-AMT                TO PV-BTCH-COST-TOTAL               
                                               PV-CYCL-COST-TOTAL               
                                               PV-DATE-COST-TOTAL               
                                               PV-GRND-COST-TOTAL.              
                                                                                
           PERFORM Z100-READ-BLOCKED-TRANS-FILE.                                
                                                                                
      ******************************************************************        
      * WRITE THE BATCH TOTAL FOR THE CHANGE IN BATCH NUMBER.                   
      ******************************************************************        
       D100-PRINT-BATCH-TOTAL.                                                  
                                                                                
           IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                                 
               PERFORM Z300-PRINT-RPT-HEADINGS                                  
           END-IF.                                                              
                                                                                
           MOVE RT-REPORT-TOTAL-LINE-1      TO                                  
                                             BLOCKED-TRANS-REPORT-LINE.         
           PERFORM Z200-WRITE-REPORT-LINE.                                      
           MOVE 1                           TO PV-ADVANCE.                      
                                                                                
           MOVE PV-PREV-BATCH-NBR           TO RT-BTCH-BATCH-NBR.               
           MOVE PV-BTCH-RETAIL-TOTAL        TO RT-BTCH-RETAIL-AMT-TTL.          
           MOVE PV-BTCH-COST-TOTAL          TO RT-BTCH-COST-AMT-TTL.            
           MOVE RT-REPORT-SUB-TOTAL-LINE-3  TO                                  
                                             BLOCKED-TRANS-REPORT-LINE.         
           PERFORM Z200-WRITE-REPORT-LINE.                                      
           MOVE 1                           TO PV-ADVANCE.                      
                                                                                
           MOVE ZERO                        TO PV-BTCH-RETAIL-TOTAL             
                                               PV-BTCH-COST-TOTAL.              
                                                                                
           IF RT-POS-IND = '*' OR                                               
              RT-COSA-IND = '*'                                                 
               MOVE 2  TO PV-ADVANCE                                            
           END-IF.                                                              
                                                                                
           IF RT-POS-IND = '*'                                                  
               MOVE PV-POS-RETAIL-TOTAL    TO RT-POS-RETAIL-AMT-TTL             
               MOVE PV-POS-COST-TOTAL      TO RT-POS-COST-AMT-TTL               
               MOVE RT-REPORT-SUB-TOTAL-LINE-1                                  
                   TO BLOCKED-TRANS-REPORT-LINE                                 
               PERFORM Z200-WRITE-REPORT-LINE                                   
               MOVE 1  TO PV-ADVANCE                                            
           END-IF.                                                              
                                                                                
           IF RT-COSA-IND = '*'                                                 
               MOVE PV-COSA-RETAIL-TOTAL    TO RT-COSA-RETAIL-AMT-TTL           
               MOVE PV-COSA-COST-TOTAL      TO RT-COSA-COST-AMT-TTL             
               MOVE RT-REPORT-SUB-TOTAL-LINE-2                                  
                   TO BLOCKED-TRANS-REPORT-LINE                                 
               PERFORM Z200-WRITE-REPORT-LINE                                   
               MOVE 1  TO PV-ADVANCE                                            
           END-IF.                                                              
                                                                                
                                                                                
           MOVE SPACES                      TO                                  
                                              BLOCKED-TRANS-REPORT-LINE.        
           PERFORM Z200-WRITE-REPORT-LINE.                                      
           MOVE 1                           TO PV-ADVANCE.                      
                                                                                
           MOVE ZERO                        TO PV-POS-RETAIL-TOTAL              
                                               PV-POS-COST-TOTAL                
                                               PV-COSA-RETAIL-TOTAL             
                                               PV-COSA-COST-TOTAL.              
                                                                                
           MOVE SPACE             TO RT-POS-IND                                 
                                     RT-COSA-IND.                               
                                                                                
      ******************************************************************        
      * WRITE THE CYCLE TOTAL FOR THE CHANGE IN CYCLE NUMBER.                   
      ******************************************************************        
       D200-PRINT-CYCLE-TOTAL.                                                  
                                                                                
           IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                                 
               PERFORM Z300-PRINT-RPT-HEADINGS                                  
           END-IF.                                                              
                                                                                
           MOVE SPACES                      TO                                  
                                             BLOCKED-TRANS-REPORT-LINE.         
           PERFORM Z200-WRITE-REPORT-LINE.                                      
           MOVE 1                           TO PV-ADVANCE.                      
                                                                                
           MOVE PV-PREV-INV-ID              TO RT-CYCLE-NBR.                    
           MOVE PV-CYCL-RETAIL-TOTAL        TO RT-CYCL-RETAIL-AMT-TTL.          
           MOVE PV-CYCL-COST-TOTAL          TO RT-CYCL-COST-AMT-TTL.            
           MOVE RT-REPORT-CYCLE-TOTAL-LINE  TO                                  
                                             BLOCKED-TRANS-REPORT-LINE.         
           PERFORM Z200-WRITE-REPORT-LINE.                                      
           MOVE 1                           TO PV-ADVANCE.                      
                                                                                
           MOVE SPACES                      TO                                  
                                             BLOCKED-TRANS-REPORT-LINE.         
           PERFORM Z200-WRITE-REPORT-LINE.                                      
           MOVE 1                           TO PV-ADVANCE.                      
                                                                                
           MOVE ZERO                        TO PV-CYCL-RETAIL-TOTAL             
                                               PV-CYCL-COST-TOTAL.              
                                                                                
      ******************************************************************        
      * WRITE THE DATE/MONTH TOTAL FOR THE CHANGE IN OPEN FISCAL DATE.          
      ******************************************************************        
       D300-PRINT-DATE-TOTAL.                                                   
                                                                                
           MOVE DTATTR-FSCL-MN-NM           TO MONTH-NAM                        
                                               MNTH-NAME.                       
                                                                                
           SET SW-DATE-TOTAL-YES            TO TRUE.                            
                                                                                
           PERFORM Z400-PRINT-DATE-GRAND-HDNG.                                  
                                                                                
           MOVE SPACES                      TO                                  
                                             BLOCKED-TRANS-REPORT-LINE.         
           PERFORM Z200-WRITE-REPORT-LINE.                                      
           MOVE 1                           TO PV-ADVANCE.                      
                                                                                
           MOVE DTATTR-FSCL-MN-NM           TO RT-MONTH-NAME.                   
           MOVE PV-DATE-RETAIL-TOTAL        TO RT-MNTH-RETAIL-AMT-TTL.          
           MOVE PV-DATE-COST-TOTAL          TO RT-MNTH-COST-AMT-TTL.            
           MOVE RT-REPORT-MONTH-TOTAL-LINE  TO                                  
                                             BLOCKED-TRANS-REPORT-LINE.         
           PERFORM Z200-WRITE-REPORT-LINE.                                      
           MOVE 1                           TO PV-ADVANCE.                      
                                                                                
           MOVE SPACES                      TO                                  
                                             BLOCKED-TRANS-REPORT-LINE.         
           PERFORM Z200-WRITE-REPORT-LINE.                                      
           MOVE 1                           TO PV-ADVANCE.                      
                                                                                
           MOVE ZERO                        TO PV-DATE-RETAIL-TOTAL             
                                               PV-DATE-COST-TOTAL.              
                                                                                
      ******************************************************************        
      * WRITE THE GRAND TOTAL OF ALL RECORDS.                                   
      ******************************************************************        
       D400-PRINT-GRAND-TOTAL.                                                  
                                                                                
           SET SW-GRAND-TOTAL-YES           TO TRUE.                            
                                                                                
           PERFORM Z400-PRINT-DATE-GRAND-HDNG.                                  
                                                                                
           MOVE SPACES                      TO                                  
                                             BLOCKED-TRANS-REPORT-LINE.         
           PERFORM Z200-WRITE-REPORT-LINE.                                      
           MOVE 1                           TO PV-ADVANCE.                      
                                                                                
           MOVE PV-GRND-RETAIL-TOTAL        TO RT-GRND-RETAIL-AMT-TTL.          
           MOVE PV-GRND-COST-TOTAL          TO RT-GRND-COST-AMT-TTL.            
           MOVE RT-REPORT-GRAND-TOTAL-LINE  TO                                  
                                             BLOCKED-TRANS-REPORT-LINE.         
           PERFORM Z200-WRITE-REPORT-LINE.                                      
                                                                                
      ******************************************************************        
      * TO GET THE FISCAL MONTH NAME FOR A PARTICULAR DATE.                     
      ******************************************************************        
       E100-GET-FSCL-MONTH-NAME.                                                
                                                                                
           MOVE ZERO                        TO PV-RETRY-COUNT.                  
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT       FROM 1 BY 1                         
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                          
                    OR SQLCODE = +0                                             
                                                                                
           EXEC SQL                                                             
                SELECT FSCL_MN_NM                                               
                INTO   :DTATTR-FSCL-MN-NM                                       
                FROM   TDTATTR                                                  
                WHERE  KY_DTE = :ML123-OPN-FSCL-MN-DTE                          
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                    CONTINUE                                                    
               WHEN -904                                                        
               WHEN -911                                                        
               WHEN -913                                                        
                    CONTINUE                                                    
               WHEN OTHER                                                       
                   DISPLAY "ERROR-DATE : " ML123-OPN-FSCL-MN-DTE                
                   MOVE 'E100-GET-FSCL-MONTH-NAME'                              
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'SELECT ROW FROM TDTATTR'                               
                                            TO PV-OPER-ATTEMPTED                
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE                                                         
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE 'E100-GET-FSCL-MONTH-NAME'                                  
                                            TO PV-PARAGRAPH-NAME                
               MOVE 'TDTATTR MAX RETRIES EXCEEDED'                              
                                            TO PV-OPER-ATTEMPTED                
               PERFORM 9000-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * TO GET THE INVENTORY NAME FOR A PARTICULAR INVENTORY ID.                
      ******************************************************************        
       E200-GET-INVENTORY-NAME.                                                 
                                                                                
           MOVE ZERO                        TO PV-RETRY-COUNT.                  
           MOVE ML123-INV-ID                TO CYCLE-ID.                        
           INITIALIZE INCNTL-INV-NM-TEXT.                                       
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT       FROM 1 BY 1                         
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                          
                    OR SQLCODE = +0                                             
                                                                                
           EXEC SQL                                                             
                SELECT INV_NM                                                   
                INTO   :INCNTL-INV-NM                                           
                FROM   TINCNTL                                                  
                WHERE  INV_ID = :ML123-INV-ID                                   
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                    MOVE INCNTL-INV-NM-TEXT TO CYCLE-NAME                       
               WHEN -904                                                        
               WHEN -911                                                        
               WHEN -913                                                        
                    CONTINUE                                                    
               WHEN OTHER                                                       
                   DISPLAY "ERROR-INVENTORY-ID : " CYCLE-ID                     
                   MOVE 'E200-GET-INVENTORY-NAME'                               
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'SELECT ROW FROM TINCNTL'                               
                                            TO PV-OPER-ATTEMPTED                
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE                                                         
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE 'E200-GET-INVENTORY-NAME'                                   
                                            TO PV-PARAGRAPH-NAME                
               MOVE 'TINCNTL MAX RETRIES EXCEEDED'                              
                                            TO PV-OPER-ATTEMPTED                
               PERFORM 9000-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       Z100-READ-BLOCKED-TRANS-FILE.                                            
                                                                                
           READ BLOCKED-TRANS-FILE                                              
               INTO ML123-RECORD                                                
                   AT END SET SW-END-OF-INPUT-FILE TO TRUE.                     
                                                                                
                                                                                
       Z200-WRITE-REPORT-LINE.                                                  
                                                                                
           WRITE BLOCKED-TRANS-REPORT-LINE                                      
               AFTER ADVANCING PV-ADVANCE.                                      
           ADD PV-ADVANCE TO PV-LINE-COUNT.                                     
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE REPORT HEADING LINES.                                         
      ******************************************************************        
       Z300-PRINT-RPT-HEADINGS.                                                 
                                                                                
           ADD 1 TO PV-PAGE-COUNT.                                              
           MOVE PV-PAGE-COUNT TO DP100-PAGE-NUMBER.                             
           WRITE BLOCKED-TRANS-REPORT-LINE                                      
               FROM DP100-STANDRD-HEADER-100-COLS                               
               AFTER ADVANCING PAGE.                                            
           MOVE +1 TO PV-LINE-COUNT.                                            
                                                                                
           WRITE BLOCKED-TRANS-REPORT-LINE FROM RD-REPORT-TITLE-LINE            
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           EVALUATE PV-PREV-TRANS-CDE                                           
               WHEN '1'                                                         
                   MOVE 'PURCHASES'     TO RD-HDR-CATEGORY                      
               WHEN '2'                                                         
                   MOVE 'SALES'         TO RD-HDR-CATEGORY                      
               WHEN '3'                                                         
                   MOVE 'TRANSFERS'     TO RD-HDR-CATEGORY                      
               WHEN '4'                                                         
                   MOVE 'MARKDOWNS'     TO RD-HDR-CATEGORY                      
               WHEN '5'                                                         
                   MOVE 'MARKUPS'       TO RD-HDR-CATEGORY                      
               WHEN '8'                                                         
                   MOVE 'ADJUSTMENTS'   TO RD-HDR-CATEGORY                      
               WHEN OTHER                                                       
                   MOVE SPACES          TO RD-HDR-CATEGORY                      
           END-EVALUATE.                                                        
                                                                                
           WRITE BLOCKED-TRANS-REPORT-LINE FROM CYCLE-MONTH-HDR-LINE            
               AFTER ADVANCING 2 LINES.                                         
           WRITE BLOCKED-TRANS-REPORT-LINE FROM RD-REPORT-HDR-LINE1             
               AFTER ADVANCING 2 LINES.                                         
           WRITE BLOCKED-TRANS-REPORT-LINE FROM RD-REPORT-HDR-LINE2             
               AFTER ADVANCING 1 LINES.                                         
           WRITE BLOCKED-TRANS-REPORT-LINE FROM RD-REPORT-HDR-LINE3             
               AFTER ADVANCING 1 LINES.                                         
           WRITE BLOCKED-TRANS-REPORT-LINE FROM RD-REPORT-HDR-LINE4             
               AFTER ADVANCING 1 LINES.                                         
           ADD +6 TO PV-LINE-COUNT.                                             
           MOVE 2  TO PV-ADVANCE.                                               
                                                                                
      ******************************************************************        
      * PRINT THE REPORT HEADING LINES FOR DATE TOTAL AND GRAND TOTAL.          
      ******************************************************************        
       Z400-PRINT-DATE-GRAND-HDNG.                                              
                                                                                
           ADD 1                            TO PV-PAGE-COUNT.                   
           MOVE PV-PAGE-COUNT               TO DP100-PAGE-NUMBER.               
           WRITE BLOCKED-TRANS-REPORT-LINE  FROM                                
                                           DP100-STANDRD-HEADER-100-COLS        
           AFTER ADVANCING PAGE.                                                
                                                                                
           WRITE BLOCKED-TRANS-REPORT-LINE  FROM RD-REPORT-TITLE-LINE           
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           IF  SW-DATE-TOTAL-YES                                                
               WRITE BLOCKED-TRANS-REPORT-LINE                                  
                                            FROM MONTH-TOTAL-HEADER             
               AFTER ADVANCING 2 LINES                                          
               SET SW-DATE-TOTAL-NO         TO TRUE                             
           END-IF.                                                              
                                                                                
           IF  SW-GRAND-TOTAL-YES                                               
               WRITE BLOCKED-TRANS-REPORT-LINE                                  
                                            FROM GRAND-TOTAL-HEADER             
               AFTER ADVANCING 2 LINES                                          
               SET SW-GRAND-TOTAL-NO        TO TRUE                             
           END-IF.                                                              
                                                                                
           ADD +8                           TO PV-LINE-COUNT.                   
           MOVE 2                           TO PV-ADVANCE.                      
                                                                                
      ******************************************************************        
      * SQL ERROR - ABEND.                                                      
      ******************************************************************        
       9000-SQL-ABEND.                                                          
                                                                                
           CLOSE BLOCKED-TRANS-FILE                                             
                 BLOCKED-TRANS-REPORT.                                          
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = '  PC-PROGRAM-NAME.                     
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED.                   
           DISPLAY '**  PARAGRAPH **  = '  PV-PARAGRAPH-NAME.                   
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
           PERFORM 9999-ABEND.                                                  
                                                                                
      ******************************************************************        
      * ABEND CALL.                                                             
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      ******************************************************************        
      * END OF PROGRAM INKRP217.                                                
      ******************************************************************        
