      *-----------------------*                                         12/22/94
       IDENTIFICATION DIVISION.                                         INKCS046
      *-----------------------*                                            LV001
       PROGRAM-ID.    INKCS046.                                         INKCS046
       AUTHOR.        PAUL KEBER.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          INKCS046
       DATE-WRITTEN.  03/27/2000.                                       INKCS046
       DATE-COMPILED.                                                   INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    INKCS046- INVENTORY STORE STATUS                           * INKCS046
      *                                                               * INKCS046
      *    THERE IS A BATCH VERSION OF THIS PROGRAM (INKRP173).       * INKCS046
      *    ANY CHANGES TO THE REPORT SHOULD BE REFLECTED IN BOTH      * INKCS046
      *    PROGRAMS.                                                  * INKCS046
      *                                                               * INKCS046
      *    THIS PROGRAM GENERATES EIGHT DIFFERENT STORE STATUS        * INKCS046
      *    SCREENS.  THEY ARE:                                        * INKCS046
      *    1)  ALL LOCATION INQUIRY                                   * INKCS046
      *    2)  SCANNED INVENTORY DATA NOT PROCESSED                   * INKCS046
      *    3)  ON-LINE SHEETS NOT PROCESSED                           * INKCS046
      *    4)  INVALIDS NOT CORRECTED                                 * INKCS046
      *    5)  STOCKROOM WITHDRAWALS NOT ENTERED                      * INKCS046
      *    6)  SALES ADJUSTMENTS NOT COMPLETED                        * INKCS046
      *    7)  UNIT BOOKINGS NOT COMPLETED                            * INKCS046
      *    8)  UNIT BOOKINGS COMPLETED                                * INKCS046
      *                                                               * INKCS046
      *---------------------------------------------------------------* INKCS046
      *                      CHANGE LOG                               * INKCS046
      *---------------------------------------------------------------* INKCS046
      * 06/08/2000 JULIE SEIDLER                                      * INKCS046
      *            CHANGE THE STOCKROOM SHEETS NOT ENTERED SCREEN     * INKCS046
      *            TO CONTINUE TO DISPLAY DATA UNTIL 21 DAYS AFTER    * INKCS046
      *            ACTL-FIN-BK-DATE                                   * INKCS046
      *                                                               * INKCS046
      *            ALSO, CHANGED "FAKE" COMMENT TO READ "EST SHR"     * INKCS046
      *            COMMENT                                            * INKCS046
      * CHG ID/                                                       *         
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                   *         
      * -------  ----------  ---------------------------------------- *         
      * 22000    02-28-2001  CHANGE TO ALLOW ON-DEMAND SUBMISSION     *         
      *                      OF THE BATCH VERSION OF THIS STORE       *         
      *                      STATUS REPORT.       -V. LEE YANG        *         
      *                                                               *         
RB0501* RB0501   05-25-2001  DO NOT DISPLAY STOCKROOM WITHDRAWL       *         
RB0501*                      FOR STORES THAT DO NOT HAVE ANY.         *         
RB0501*                      INITIALLY IN TINVPAR, A STORE IS SET     *         
RB0501*                      UP WITH A STOCKROOM WITHDRAWAL INDICATOR *         
RB0501*                      OF Y, AND IF THE STORE DOES NOT HAVE     *         
RB0501*                      ANY STOCKROOM WITHDRAWALS TO ENTER, THE  *         
RB0501*                      INDICATOR IS MANUALLY CHANGED BY SOME ONE*         
RB0501*                      AT THE STORE.                            *         
RB0501*                                                               *         
      * 22960    01-30-2002  CORRECT BATCH JOB SUBMITTED TO INCLUDE   *         
      *                      EOF AT THE END OF JOB, SO IT WILL EXECUTE*         
      *                      RIGHT AWAY.  ALSO, CORRECT STOCKROOM     *         
      *                      WITHDRAWAL SCREEN.                       *         
      *                                           -V. LEE YANG        *         
      * 23511C   06-26-2002  LOOK AT INVPAR-STKRM-WDRWL-IND WHEN      *         
      *                      SETTING THE WAIT-STRM INDICATOR          *         
RB0612*                                                               *         
RB0612* RB0612   06-12-2003  REMOVE UCC11 FROM THE GENERATED JCL      *         
RB0612*                                           -RONNA MCDONALD     *         
W26600*                                                               *         
W26600* W26600   07-  -2004  PHYSICAL INVENTORY STORE EXPANSION       *         
W26600*                      M JOHNSON                                *         
W26600*                                                               *         
W26006* W26006   02-24-2005  REMOVE REFERENCES TO OBSOLETE STORE TABLE*         
W26006*                      ERIN SEARL                               *         
W26006*                                                               *         
      * W29908   01-23-2006  ADD PKMO.PROCLIB TO THE JCLLIB           *         
      *                      LEE YANG                                 *         
      *                                                               *         
28545 * W28545   08-10-2006  DEPARTMENT LEVEL INVENTORY               *         
28545 *                      JOIN TINVPAR/TINCNTL                     *         
28545 *                      B. GRAHAM - STRATAGEM                    *         
28545 *                                                               *         
      * INC000000455847                                               *         
      *          07-24-2008  PER SUE THOMA AND KAREN HAAG THIS        *         
      *                      PROGRAM SHOULD ONLY RETURN LOCATIONS THAT*         
      *                      ARE IN A CURRENTLY ACTIVE INVENTORY      *         
      *                      CYCLE (TINCNTL.ACTV_IND = 'Y').  IN THE  *         
      *                      UNIT-BOOK-CMPL-CSR, ALL-LOC-INQ-CSR, AND *         
      *                      STCKRM-WITHDRW-CSR CURSORS, REMOVE THE   *         
      *                      TINVPAR.ACTL_FIN_BK_DTE <> '9999-09-09'  *         
      *                      AND TINVPAR.ACTL_FIN_BK_DTE >= CURRENT   *         
      *                      DATE - 21 DAYS CHECK, REPLACING IT WITH  *         
      *                      TINCNTL.ACTV_IND = 'Y'.                  *         
      * W33304   05-01-2008  HOLDING INVENTORY LEDGER OPEN ACROSS               
      *                      PERIODS INVENTORY SHORTAGE REPORTING               
      *                      J. LIN                                             
PK1165* PKE000000001165                                               *         
      *          10-20-2010  PER SUE THOMA, THE RTV STORES WILL NOT   *         
      *                      INVOLVE UNIT BOOKING HENCE THIS RTV TYPE *         
      *                      CODE (RV) NEEDS TO BE REMOVED FROM THIS  *         
      *                      PROGRAM                                  *         
      *---------------------------------------------------------------* INKCS046
263126* CHANGED 10/25/2021    BY JIM HUNTER      WR: CHG0263126                 
263126* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263126* MAKING THE CALL DYNAMIC VS STATIC.                                      
263126*----------------------------------------------------------------*        
1122RM* CHANGED 11/03/2022    BY BOB MCASEY      WR: CHG0277188                 
1122RM* ADDED THE DPKCS930 COPYBOOK. CHANGED THE PARAMETERS FOR CALLING         
1122RM* THE CICS ARCHITECTURE API.                                              
      *----------------------------------------------------------------*        
      /                                                                 INKCS046
       ENVIRONMENT DIVISION.                                            INKCS046
                                                                        INKCS046
       DATA DIVISION.                                                   INKCS046
                                                                        INKCS046
       WORKING-STORAGE SECTION.                                         INKCS046
22000                                                                   INKCS046
22000  01  TD-TRANSIENT-DATA-AREA.                                              
22000      05  TD-QUEUE-NAME           PIC X(4)     VALUE 'IRTD'.               
22000      05  TD-QUEUE-NAME-LENGTH    PIC S9(4)    VALUE +4                    
22000                                  COMP SYNC.                               
22000      05  TD-JCL-LINE             PIC X(80)    VALUE SPACES.               
22000      05  TD-JCL-LINE-LENGTH      PIC S9(4)    VALUE +80                   
22000                                  COMP SYNC.                               
22000                                                                           
22000  01  JL-JCL-WORK-AREA.                                            INKCS046
22000      05  JL-JCL-INK309-LINE-1.                                            
22000          10  FILLER                     PIC X(02)    VALUE '//'.          
22000          10  JL-JCL-INK309-JOBNAME      PIC X(08)    VALUE SPACES.        
22000          10  FILLER REDEFINES JL-JCL-INK309-JOBNAME.                      
22000              15  FILLER                 PIC X(07).                        
22000              15  JL-JCL-INK309-JOB-UNQ  PIC X(01).                        
22000          10  FILLER                     PIC X(11)    VALUE                
22000              ' JOB KOHLS,'.                                               
22000          10  FILLER                     PIC X(01)    VALUE QUOTE.         
22000          10  FILLER                     PIC X(18)    VALUE                
22000              'INV STR STATUS RPT'.                                        
22000          10  FILLER                     PIC X(01)    VALUE QUOTE.         
22000          10  FILLER                     PIC X(01)    VALUE ','.           
22000      05  JL-JCL-INK309-LINE-2.                                            
22000          10  FILLER                     PIC X(21)    VALUE                
22000              '//             CLASS='.                                     
22000          10  JL-JCL-INK309-JOBCLASS     PIC X(01)    VALUE 'Q'.           
22000          10  FILLER                     PIC X(10)    VALUE                
22000              ',MSGCLASS='.                                                
22000          10  JL-JCL-INK309-MSGCLASS     PIC X(01)    VALUE 'P'.           
22000          10  JL-JCL-INK309-NOTIFY-OPT   PIC X(16)    VALUE SPACES.        
22000      05  JL-JCL-INK309-LINE-3.                                            
22000          10  FILLER                     PIC X(13)    VALUE                
22000              '/*JOBPARM K=0'.                                             
22000      05  JL-JCL-INK309-CMT-SHORT.                                         
22000          10  FILLER                     PIC X(03)    VALUE '//*'.         
22000      05  JL-JCL-INK309-CMT-LONG.                                          
22000          10  FILLER                     PIC X(40)    VALUE                
22000              '//**************************************'.          ********
22000          10  FILLER                     PIC X(32)    VALUE                
22000              '********************************'.                  ********
22000      05  JL-JCL-INK309-CMT-USER-INFO.                                     
22000          10  FILLER                     PIC X(26)    VALUE                
22000              '//* INK309 - SUBMITTED BY '.                                
22000          10  JL-JCL-INK309-CMT-USRID    PIC X(08)    VALUE SPACES.        
22000          10  FILLER                     PIC X(06)    VALUE                
22000              ' FROM '.                                                    
22000          10  JL-JCL-INK309-CMT-REGION   PIC X(08)    VALUE SPACES.        
22000          10  FILLER                     PIC X(04)    VALUE                
22000              ' ON '.                                                      
22000          10  JL-JCL-INK309-CMT-SUBDATE  PIC X(10)    VALUE SPACES.        
22000          10  FILLER                     PIC X(02)    VALUE SPACES.        
22000          10  JL-JCL-INK309-CMT-SUBTIME  PIC X(08)    VALUE SPACES.        
22000      05  JL-JCL-INK309-CMT-1.                                             
22000          10  FILLER                     PIC X(40)    VALUE                
22000              '//* THIS JOB PRODUCES VARIOUS SECTION OF'.          ********
22000          10  FILLER                     PIC X(32)    VALUE                
22000              'THE INVENTORY STORE STATUS      '.                  ********
22000      05  JL-JCL-INK309-CMT-2.                                             
22000          10  FILLER                     PIC X(40)    VALUE                
22000              '//* REPORT AND PRINT THEM AT THE USER DE'.          ********
22000          10  FILLER                     PIC X(32)    VALUE                
22000              'FAULT PRINT DESTINATION OR      '.                  ********
22000      05  JL-JCL-INK309-CMT-3.                                             
22000          10  FILLER                     PIC X(40)    VALUE                
22000              '//* LOCAL, IF THERE IS NO DEFAULT PRINTE'.          ********
22000          10  FILLER                     PIC X(32)    VALUE                
22000              'R DEFINED TO THE USER.          '.                  ********
22000      05  JL-JCL-INK309-CMT-4.                                             
22000          10  FILLER                     PIC X(22)    VALUE                
22000              '//* OPTION REQUESTED: '.                            ********
22000          10  JL-JCL-INK309-CMT-OPTION   PIC X(20)    VALUE SPACES.        
22000      05  JL-JCL-INK309-LINE-10.                                           
22000          10  FILLER                     PIC X(39)    VALUE                
22000              '//JOBLIB   DD DSN=PKMO.LOADLIB,DISP=SHR'.                   
22000      05  JL-JCL-INK309-LINE-11.                                           
RB0612         10  FILLER                     PIC X(03)    VALUE                
RB0612             '//*'.                                                       
22000      05  JL-JCL-INK309-LINE-12.                                           
22000          10  FILLER                     PIC X(32)    VALUE                
22000              '//PS020    EXEC INK3091,PRTDEST='.                          
22000          10  JL-JCL-INK309-PRTDEST      PIC X(08)    VALUE                
22000              'LOCAL'.                                                     
22000      05  JL-JCL-INK309-LINE-13.                                           
22000          10  FILLER                     PIC X(20)    VALUE                
22000              '//S010.SYSUT1   DD *'.                                      
22000      05  JL-JCL-INK309-LINE-14.                                           
22000          10  FILLER                     PIC X(04)    VALUE                
22000              'RPT '.                                                      
22000          10  JL-JCL-INK309-REQ-OPTION   PIC X(16)    VALUE SPACES.        
W26600         10  FILLER                     PIC X(01)    VALUE SPACE.         
W26600         10  JL-REQ-USERID              PIC X(08)    VALUE SPACE.         
22000      05  JL-JCL-INK309-LINE-EOD.                                          
22000          10  FILLER                     PIC X(02)    VALUE '/*'.          
22960      05  JL-JCL-INK309-LINE-EOF.                                          
22960          10  FILLER                     PIC X(05)    VALUE                
22960              '/*EOF'.                                                     
22000      05  JL-JCL-TEST-LINE-10.                                             
22000          10  FILLER                     PIC X(35)    VALUE                
22000              '//PROCS  JCLLIB ORDER=(TKMA.PROCLIB'.                       
W29908         10  FILLER                     PIC X(19)    VALUE                
W29908             ',QKMO.PROCLIB.STAGE'.                                       
W29908         10  FILLER                     PIC X(18)    VALUE                
W29908             ',PKMO.PROCLIB)'.                                            
22000      05  JL-JCL-TEST-LINE-11A.                                            
22000          10  FILLER                     PIC X(39)    VALUE                
22000              '//JOBLIB   DD DSN=TKMA.LOADLIB,DISP=SHR'.                   
22000      05  JL-JCL-TEST-LINE-11B.                                            
22000          10  FILLER                     PIC X(39)    VALUE                
22000              '//         DD DSN=QKMO.LOADLIB,DISP=SHR'.                   
22000      05  JL-JCL-TEST-LINE-11C.                                            
22000          10  FILLER                     PIC X(39)    VALUE                
22000              '//         DD DSN=PKMO.LOADLIB,DISP=SHR'.                   
22000      05  JL-JCL-TEST-NOTIFY.                                              
22000          10  FILLER                     PIC X(08)    VALUE                
22000              ',NOTIFY='.                                                  
22000          10  JL-JCL-TEST-USER           PIC X(08)    VALUE SPACES.        
W26600                                                                          
W26600******************************************************************        
W26600 01  NI-NULL-INDICATORS.                                                  
W26600     05  NI-LOC-10-ABBR-NM           PIC S9(04)      VALUE +0     INKCS046
W26600                                                     COMP.                
                                                                        INKCS046
       01  PC-PROGRAM-CONSTANTS.                                        INKCS046
           05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE         INKCS046
                                                          'IN046A  '.   INKCS046
           05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE         INKCS046
                                                          'INKM046 '.   INKCS046
           05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE         INKCS046
                                                          'INKCS046'.   INKCS046
           05  PC-INCOMPLETE-DTE              PIC  X(10)  VALUE         INKCS046
                                                        '9999-09-09'.   INKCS046
           05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.      INKCS046
           05  PC-COMPANY                     PIC  X(02)  VALUE '03'.           
           05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +14     INKCS046
                                                          COMP SYNC.    INKCS046
           05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PC-MAX-ITEMS                   PIC S9(09)  VALUE         INKCS046
                                                          +1000000      INKCS046
                                                          COMP-3.       INKCS046
           05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3       INKCS046
                                                          COMP-3.       INKCS046
           05  PC-CS                          PIC  X(02)  VALUE 'CS'.   INKCS046
           05  PC-C                           PIC  X(01)  VALUE 'C'.    INKCS046
           05  PC-N                           PIC  X(01)  VALUE 'N'.    INKCS046
           05  PC-Y                           PIC  X(01)  VALUE 'Y'.    INKCS046
                                                                        INKCS046
           05  PC-TSYMSG-NUMBERS.                                       INKCS046
               10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.  INKCS046
               10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.  INKCS046
               10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.  INKCS046
               10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.  INKCS046
22000          10  PC-TSYMSG-00098            PIC  9(05)  VALUE 00098.  INKCS046
               10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.  INKCS046
               10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.  INKCS046
22000                                                                   INKCS046
22000      05  PC-PRINT-ALL-RPT               PIC  X(16)  VALUE         INKCS046
22000          'ALL REPT        '.                                              
22000      05  PC-PRINT-LOCATION              PIC  X(16)  VALUE         INKCS046
22000          'LOCATION        '.                                              
22000      05  PC-PRINT-SCANNED-INV           PIC  X(16)  VALUE         INKCS046
22000          'SCANNED INV     '.                                              
22000      05  PC-PRINT-SHEETS                PIC  X(16)  VALUE         INKCS046
22000          'SHEETS          '.                                              
22000      05  PC-PRINT-INVALIDS              PIC  X(16)  VALUE         INKCS046
22000          'INVALIDS        '.                                              
22000      05  PC-PRINT-STOCKROOM             PIC  X(16)  VALUE         INKCS046
22000          'STOCKROOM       '.                                              
22000      05  PC-PRINT-SALES-ADJUSTMENT      PIC  X(16)  VALUE         INKCS046
22000          'SALES ADJUSTMENT'.                                              
22000      05  PC-PRINT-BOOKING-NOT-CPLD      PIC  X(16)  VALUE         INKCS046
22000          'BOOKING NOT CPLD'.                                              
22000      05  PC-PRINT-BOOKING-CPLD          PIC  X(16)  VALUE         INKCS046
22000          'BOOKING COMPLETE'.                                              
22000                                                                   INKCS046
      *                                                                 INKCS046
       01  PS-PROGRAM-SWITCHES.                                         INKCS046
           05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.    INKCS046
               88  PS-NO-ERROR                            VALUE 'Y'.    INKCS046
               88  PS-ERROR                               VALUE 'N'.    INKCS046
           05  PS-TINENTY-FOUND-SW            PIC  X(01)  VALUE 'N'.    INKCS046
               88  PS-TINENTY-FOUND                       VALUE 'Y'.    INKCS046
               88  PS-TINENTY-NOT-FOUND                   VALUE 'N'.    INKCS046
           05  PS-TINVDTL-FOUND-SW            PIC  X(01)  VALUE 'N'.    INKCS046
               88  PS-TINVDTL-FOUND                       VALUE 'Y'.    INKCS046
               88  PS-TINVDTL-NOT-FOUND                   VALUE 'N'.    INKCS046
JS0400     05  PS-TSTKRMW-FOUND-SW            PIC  X(01)  VALUE 'N'.    INKCS046
               88  PS-TSTKRMW-FOUND                       VALUE 'Y'.    INKCS046
               88  PS-TSTKRMW-NOT-FOUND                   VALUE 'N'.    INKCS046
22000      05  PS-INTERNAL-READER-ENQ-DEQ-IND PIC X(1)    VALUE 'D'.            
22000          88  PS-INTERNAL-READER-ENQUEUED            VALUE 'E'.            
22000          88  PS-INTERNAL-READER-DEQUEUED            VALUE 'D'.            
      *                                                                 INKCS046
       01  PV-PROGRAM-VARIABLES.                                        INKCS046
22000      05  PV-CICS-ID.                                                      
22000          10  PV-FIRST-4              PIC X(4)       VALUE SPACES.         
22000          10  PV-CICS-REGION-TYPE     PIC X          VALUE SPACE.          
22000              88  PV-CICS-REGION-PRODUCTION          VALUE 'P'.            
22000              88  PV-CICS-REGION-ACCEPTANCE          VALUE 'A'.            
22000              88  PV-CICS-REGION-QUALITY             VALUE 'Q'.            
22000              88  PV-CICS-REGION-LEARNING            VALUE 'L'.            
22000              88  PV-CICS-REGION-TEST                VALUE 'T'.            
22000              88  PV-CICS-REGION-SYSTEST             VALUE 'S'.            
22000          10  PV-CICS-REGION-CODE     PIC X          VALUE SPACES.         
22000          10  PV-LAST-2               PIC X(2)       VALUE SPACES.         
           05  PV-STKRM-WITHD-DAYS-LEFT       PIC S9(09)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-SUB                         PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-REMAINDER                   PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-CICS-RESP                   PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-ITEM                        PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-SQL-SUB                     PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0      INKCS046
                                                          COMP SYNC.    INKCS046
           05  PV-READ-COUNT                  PIC S9(09)  VALUE +0      INKCS046
                                                          COMP-3.       INKCS046
           05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0      INKCS046
                                                          COMP-3.       INKCS046
           05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0      INKCS046
                                                          COMP-3.       INKCS046
           05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0      INKCS046
                                                          COMP-3.       INKCS046
           05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0      INKCS046
                                                          COMP-3.       INKCS046
           05  PV-CURRENT-DATE                PIC X(10)   VALUE SPACE.  INKCS046
W26600*    05  PV-NUM-STORE                   PIC 9(03)   VALUE ZERO.   INKCS046
22000      05  PV-SQL-TINENTY-DATE-CHECK      PIC X(10)   VALUE SPACES.         
22000      05  PV-SQL-TADJDTL-DATE-CHECK      PIC X(10)   VALUE SPACES.         
           05  PV-LITERAL                     PIC X(01)   VALUE SPACE.  INKCS046
           05  PV-SCREEN-TITLES.                                                
               10  PV-ST-ALL-LOC-INQ          PIC X(36)   VALUE                 
                   '           ALL LOCATIONS            '.                      
               10  PV-ST-SCANNED-INV-NP       PIC X(36)   VALUE                 
                   'SCANNED INVENTORY DATA NOT PROCESSED'.                      
               10  PV-ST-ONLINE-SHEETS-NP     PIC X(36)   VALUE                 
                   '    ON-LINE SHEETS NOT PROCESSED    '.                      
               10  PV-ST-INVALIDS-NOT-CORR    PIC X(36)   VALUE                 
                   '       INVALIDS NOT CORRECTED       '.                      
               10  PV-ST-STCKRM-WITHDRWL-NE   PIC X(36)   VALUE                 
                   ' STOCKROOM WITHDRAWALS NOT ENTERED  '.                      
               10  PV-ST-SALES-ADJUSTS-NC     PIC X(36)   VALUE                 
                   '   SALES ADJUSTMENT NOT COMPLETED   '.                      
               10  PV-ST-UNIT-BOOKINGS-NC     PIC X(36)   VALUE                 
                   '    UNIT BOOKINGS NOT COMPLETED     '.                      
               10  PV-ST-UNIT-BOOKINGS-COMPLT PIC X(36)   VALUE                 
                   '      UNIT BOOKINGS COMPLETED       '.                      
                                                                                
      /                                                                 INKCS046
      *---------------------------------------------------------------* INKCS046
      *    MAP LAYOUT                                                 * INKCS046
      *---------------------------------------------------------------* INKCS046
                                                                        INKCS046
           COPY INKM046.                                                INKCS046
      *                                                                 INKCS046
       01  MR-OCCURS-LAYOUT                   REDEFINES  IN046AO.       INKCS046
           05  FILLER                         PIC X(83).                INKCS046
           05  MR-SCROLL-AREA                 OCCURS 14 TIMES.          INKCS046
                                                                        INKCS046
               10  MR-STOREL                  PIC S9(04) COMP.                  
               10  MR-STOREA                  PIC  X(01).               INKCS046
               10  MR-STOREC                  PIC  X(01).               INKCS046
               10  MR-STOREH                  PIC  X(01).               INKCS046
               10  MR-STORE                   PIC  X(04).               INKCS046
                                                                        INKCS046
               10  MR-STORE-NAMEL             PIC S9(04) COMP.          INKCS046
               10  MR-STORE-NAMEA             PIC  X(01).               INKCS046
               10  MR-STORE-NAMEC             PIC  X(01).               INKCS046
               10  MR-STORE-NAMEH             PIC  X(01).               INKCS046
               10  MR-STORE-NAME              PIC  X(10).               INKCS046
                                                                        INKCS046
               10  MR-LOC-TYPEL               PIC S9(04) COMP.          INKCS046
               10  MR-LOC-TYPEA               PIC  X(01).               INKCS046
               10  MR-LOC-TYPEC               PIC  X(01).               INKCS046
               10  MR-LOC-TYPEH               PIC  X(01).               INKCS046
               10  MR-LOC-TYPE                PIC  X(05).               INKCS046
                                                                        INKCS046
               10  MR-ACT-INV-DATEL           PIC S9(04) COMP.          INKCS046
               10  MR-ACT-INV-DATEA           PIC  X(01).               INKCS046
               10  MR-ACT-INV-DATEC           PIC  X(01).               INKCS046
               10  MR-ACT-INV-DATEH           PIC  X(01).               INKCS046
               10  MR-ACT-INV-DATE            PIC  X(05).               INKCS046
                                                                        INKCS046
               10  MR-ACT-RECD-DATEL          PIC S9(04) COMP.          INKCS046
               10  MR-ACT-RECD-DATEA          PIC  X(01).               INKCS046
               10  MR-ACT-RECD-DATEC          PIC  X(01).               INKCS046
               10  MR-ACT-RECD-DATEH          PIC  X(01).               INKCS046
               10  MR-ACT-RECD-DATE           PIC  X(08).               INKCS046
                                                                        INKCS046
               10  MR-ON-LINE-SHEETL          PIC S9(04) COMP.          INKCS046
               10  MR-ON-LINE-SHEETA          PIC  X(01).               INKCS046
               10  MR-ON-LINE-SHEETC          PIC  X(01).               INKCS046
               10  MR-ON-LINE-SHEETH          PIC  X(01).               INKCS046
               10  MR-ON-LINE-SHEET           PIC  X(09).               INKCS046
                                                                        INKCS046
               10  MR-SALES-ADJUSTL           PIC S9(04) COMP.          INKCS046
               10  MR-SALES-ADJUSTA           PIC  X(01).               INKCS046
               10  MR-SALES-ADJUSTC           PIC  X(01).               INKCS046
               10  MR-SALES-ADJUSTH           PIC  X(01).               INKCS046
               10  MR-SALES-ADJUST            PIC  X(09).               INKCS046
                                                                        INKCS046
               10  MR-UNIT-BOOKINGL           PIC S9(04) COMP.          INKCS046
               10  MR-UNIT-BOOKINGA           PIC  X(01).               INKCS046
               10  MR-UNIT-BOOKINGC           PIC  X(01).               INKCS046
               10  MR-UNIT-BOOKINGH           PIC  X(01).               INKCS046
               10  MR-UNIT-BOOKING            PIC  X(12).               INKCS046
                                                                        INKCS046
      /                                                                 INKCS046
      *---------------------------------------------------------------* INKCS046
      *    ATTRIBUTE SETTINGS COPYBOOK.                               * INKCS046
      *---------------------------------------------------------------* INKCS046
                                                                        INKCS046
           COPY DPWS015.                                                INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    FUNCTION KEYS COPYBOOK.                                    * INKCS046
      *---------------------------------------------------------------* INKCS046
                                                                        INKCS046
           COPY DPWS016.                                                INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    NUMERIC EDIT LAYOUT.                                       * INKCS046
      *---------------------------------------------------------------* INKCS046
                                                                        INKCS046
           COPY DPWS010I.                                               INKCS046
      /                                                                 INKCS046
      *---------------------------------------------------------------* INKCS046
      *    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   * INKCS046
      *---------------------------------------------------------------* INKCS046
                                                                        INKCS046
           COPY DPWS030.                                                INKCS046
1122RM     COPY DPWS930.                                                INKCS046
      /                                                                 INKCS046
      *---------------------------------------------------------------* INKCS046
      *    STANDARD COMMAREA.                                         * INKCS046
      *---------------------------------------------------------------* INKCS046
                                                                        INKCS046
           COPY DPWS020.                                                INKCS046
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                INKCS046
                                                                        INKCS046
               10  INTER-APPL-COMM-AREA       PIC X(200).               INKCS046
      *---------------------------------------------------------------* INKCS046
      *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   * INKCS046
      *---------------------------------------------------------------* INKCS046
               10  ASC-SPECIFIC-COMMAREA.                               INKCS046
                   15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                INKCS046
                       20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).  INKCS046
                           88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.   INKCS046
                           88  ASC-BLOCK-MODIFIED          VALUE 'Y'.   INKCS046
                                                                        INKCS046
                       20  ASC-ITEM-ARRAY.                              INKCS046
                           25  ASC-ITEM-ENTRIES OCCURS 14 TIMES.        INKCS046
                               30  ASC-LIST-ITEM-NUMBER                 INKCS006
                                                           PIC S9(09)   INKCS006
                                                           COMP-3.      INKCS006
                                                                                
W26600                         30  ASC-STORE-NUMBER        PIC  9(04).  INKCS046
                               30  ASC-STORE-NAME          PIC  X(10).  INKCS046
                               30  ASC-LOC-TYPE            PIC  X(06).  INKCS046
                               30  ASC-ACT-INV-DATE        PIC  X(05).  INKCS046
                               30  ASC-ACT-RECD-DATE       PIC  X(08).  INKCS046
                               30  ASC-ON-LINE-SHEET       PIC  X(09).  INKCS046
                               30  ASC-SALES-ADJUST        PIC  X(09).  INKCS046
                               30  ASC-UNIT-BOOKING        PIC  X(12).  INKCS046
                                                                        INKCS046
                   15  ASC-BLK-SUB                         PIC S9(04)   INKCS046
                                                                COMP    INKCS046
                                                                SYNC.   INKCS046
                   15  ASC-ITEM-SUB                        PIC S9(04)   INKCS046
                                                                COMP    INKCS046
                                                                SYNC.   INKCS046
                   15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)   INKCS046
                                                                COMP-3. INKCS046
                   15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)   INKCS046
                                                                COMP-3. INKCS046
                   15  ASC-ITEMS-DISPLAYED                 PIC S9(03)   INKCS046
                                                                COMP-3. INKCS046
                   15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)   INKCS046
                                                                COMP-3. INKCS046
                   15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)   INKCS046
                                                                COMP-3. INKCS046
                   15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)   INKCS046
                                                                COMP    INKCS046
                                                                SYNC.   INKCS046
                   15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)   INKCS046
                                                                COMP-3. INKCS046
                   15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)   INKCS046
                                                                COMP-3. INKCS046
                   15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).  INKCS046
                       88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.   INKCS046
                       88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.   INKCS046
                   15  ASC-LIST-QUEUE-NAME.                             INKCS046
                       20  ASC-LIST-TERM-ID                PIC  X(04).  INKCS046
                       20  ASC-LIST-TASK-NUMBER            PIC S9(05)   INKCS046
                                                                COMP-3. INKCS046
                       20  ASC-LIST-SEQ                    PIC  9(01).  INKCS046
                                                                        INKCS046
                   15  ASC-INT-APPL-FORMAT-IND             PIC  X(01).  INKCS046
                       88  ASC-ALL-LOC-INQ                 VALUE 'A'.   INKCS046
                       88  ASC-SCANNED-INV-NP              VALUE 'B'.   INKCS046
                       88  ASC-ONLINE-SHEETS-NP            VALUE 'C'.   INKCS046
                       88  ASC-INVALIDS-NOT-CORR           VALUE 'D'.   INKCS046
                       88  ASC-STCKRM-WITHDRWL-NE          VALUE 'E'.   INKCS046
                       88  ASC-SALES-ADJUSTS-NC            VALUE 'F'.   INKCS046
                       88  ASC-UNIT-BOOKINGS-NC            VALUE 'G'.   INKCS046
                       88  ASC-UNIT-BOOKINGS-COMPLET       VALUE 'H'.   INKCS046
                                                                        INKCS046
                   15  ASC-TITLE                           PIC  X(36).  INKCS046
                   15  ASC-START-ITEM-REQUEST.                          INKCS046
                       20  ASC-START-ITEM-REQUEST-N        PIC  9(05).  INKCS046
      /                                                                 INKCS046
      *---------------------------------------------------------------* INKCS046
      *    ABEND PROCESSING COPYBOOK.                                 * INKCS046
      *---------------------------------------------------------------* INKCS046
                                                                        INKCS046
           COPY DPWS013.                                                INKCS046
                                                                        INKCS046
      /                                                                 INKCS046
      *---------------------------------------------------------------* INKCS046
      *    DATE  PROCESSING COPYBOOK.                                 * INKCS046
      *---------------------------------------------------------------* INKCS046
           COPY DPWS017.                                                INKCS046
                                                                                
28545                                                                           
28545 *****************************************************************         
28545 * TINCNTL DCLGEN                                                          
28545 *****************************************************************         
28545      EXEC SQL                                                             
28545          INCLUDE TINCNTL                                                  
28545      END-EXEC.                                                            
      *---------------------------------------------------------------* INKCS046
      *    TINVPAR - PHYSICAL INVENTORY TABLE                         * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                INCLUDE TINVPAR                                         INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TINENTY - PHYSICAL INVENTORY OF PRE-COUNTED ITEMS TABLE    * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                INCLUDE TINENTY                                         INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TINVDTL - INVALID INVENTORY DETAIL TABLE                   * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                INCLUDE TINVDTL                                         INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TSTKRMW - STOCKROOM WITHDRAWL LINE TABLE                   * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                INCLUDE TSTKRMW                                         INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
22960 *---------------------------------------------------------------* INKCS046
22960 *    TSUMINV - SUM INVENTORY TABLE                              * INKCS046
22960 *---------------------------------------------------------------* INKCS046
22960      EXEC SQL                                                     INKCS046
22960           INCLUDE TSUMINV                                         INKCS046
22960      END-EXEC.                                                    INKCS046
22960                                                                   INKCS046
22000 *---------------------------------------------------------------*         
22000 *    TADJDTL - SALES ADJUSTMENT DETAIL TABLE                    *         
22000 *---------------------------------------------------------------*         
22000      EXEC SQL                                                             
22000           INCLUDE TADJDTL                                                 
22000      END-EXEC.                                                            
22000                                                                           
W26600*---------------------------------------------------------------*         
W26600*    XMT_LOC - LOCATION TABLE                                   *         
W26600*---------------------------------------------------------------*         
W26600     EXEC SQL                                                             
W26600          INCLUDE XMT00001                                                
W26600     END-EXEC.                                                            
                                                                                
22000 *---------------------------------------------------------------*         
22000 *    TUSER   - USER INFORMATION TABLE                           *         
22000 *---------------------------------------------------------------*         
22000      EXEC SQL                                                             
22000           INCLUDE TUSER                                                   
22000      END-EXEC.                                                            
22000                                                                           
      *---------------------------------------------------------------* INKCS046
      *    DB2 AREA FOR COMMUNICATIONS                                * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                INCLUDE SQLCA                                           INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TINVPAR ALL LOCATION INQUIRY CURSOR DECLARE                * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                DECLARE ALL-LOC-INQ-CSR CURSOR FOR                      INKCS046
W26600          SELECT   A.LOC_NBR                                      INKCS046
                       , A.ACTL_INV_DTE                                         
                       , A.PLND_INV_DTE                                         
                       , A.SCAN_INV_IND                                         
                       , A.ACTL_UNIT_BK_DTE                             INKCS046
                       , A.ACTL_FIN_BK_DTE                              INKCS046
                       , A.ACTL_INV_RECD_DTE                            INKCS046
                       , DAYS(A.ACTL_INV_DTE) + A.STKRM_DAY_QTY         INKCS046
                                              - DAYS(CURRENT_DATE)              
                       , A.SLS_ADJ_CPLD_IND                             INKCS046
W33304                 , A.SCHD_PHY_CNT_CDE                             INKCS046
                       , A.SHEET_INV_CPLD_IND                           INKCS046
                       , A.UNT_BKG_STAT_CDE                             INKCS046
                       , A.FIL_TOT_BAL_CDE                              INKCS046
                       , A.STKRM_WDRWL_IND                                      
W26600                 , B.LOC_TYP_CDE                                  INKCS046
W26600                 , B.LOC_10_ABBR_NM                               INKCS046
                FROM   TINVPAR A                                        INKCS046
W26600                ,XMT_LOC B                                                
28545                 ,TINCNTL C                                                
W26600          WHERE  A.LOC_NBR          =  B.LOC_NBR                          
28545             AND  A.INV_ID           =  C.INV_ID                           
28545             AND  C.ACTV_IND = 'Y'                                 INKCS046
W33304            AND  A.ACTL_FIN_BK_DTE  = '9999-09-09'                INKCS046
W33304            AND  A.LOC_INV_STAT_CDE  = 'IN'                       INKCS046
W33304          ORDER BY C.PLND_FIN_BKG_DTE                             INKCS046
W33304                 , A.INV_ID                                       INKCS046
W33304                 , A.LOC_NBR                                      INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TINVPAR SCANNED INVENTORY DATA NOT PROCESSED INQUIRY DECLAR* INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                DECLARE SCANNED-INV-NP-CSR CURSOR FOR                   INKCS046
W26600          SELECT   A.LOC_NBR                                      INKCS046
                       , A.ACTL_INV_DTE                                         
                       , A.PLND_INV_DTE                                         
                       , A.SCAN_INV_IND                                         
                       , A.ACTL_UNIT_BK_DTE                             INKCS046
                       , A.ACTL_FIN_BK_DTE                              INKCS046
                       , A.ACTL_INV_RECD_DTE                            INKCS046
                       , DAYS(A.ACTL_INV_DTE) + A.STKRM_DAY_QTY         INKCS046
                                              - DAYS(CURRENT_DATE)              
                       , A.SLS_ADJ_CPLD_IND                             INKCS046
W33304                 , A.SCHD_PHY_CNT_CDE                             INKCS046
                       , A.SHEET_INV_CPLD_IND                           INKCS046
                       , A.UNT_BKG_STAT_CDE                             INKCS046
                       , A.FIL_TOT_BAL_CDE                              INKCS046
                       , A.STKRM_WDRWL_IND                                      
                       , B.LOC_TYP_CDE                                  INKCS046
                       , B.LOC_10_ABBR_NM                               INKCS046
                FROM   TINVPAR A                                        INKCS046
W26600                ,XMT_LOC B                                                
28545                 ,TINCNTL C                                                
W26600          WHERE  A.LOC_NBR           =  B.LOC_NBR                         
28545             AND  A.INV_ID            =  C.INV_ID                          
28545             AND  C.ACTV_IND          = 'Y'                        INKCS046
W33304            AND  A.ACTL_FIN_BK_DTE  = '9999-09-09'                INKCS046
                  AND  CURRENT DATE        >  A.ACTL_INV_DTE            INKCS046
22960             AND  A.ACTL_INV_RECD_DTE = '9999-09-09'               INKCS046
22960             AND  A.SCAN_INV_IND      = 'Y'                        INKCS046
W33304            AND  A.SCHD_PHY_CNT_CDE  IN ('PI','NS')               INKCS046
W33304            AND  A.LOC_INV_STAT_CDE  = 'IN'                       INKCS046
W33304          ORDER BY C.PLND_FIN_BKG_DTE                             INKCS046
W33304                 , A.INV_ID                                       INKCS046
W33304                 , A.LOC_NBR                                      INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TINVPAR ON-LINE SHEETS NOT PROCESSED DECLARE               * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                DECLARE ONLINE-SHEETS-CSR CURSOR FOR                    INKCS046
W26600          SELECT   A.LOC_NBR                                      INKCS046
                       , A.ACTL_INV_DTE                                         
                       , A.PLND_INV_DTE                                         
                       , A.SCAN_INV_IND                                         
                       , A.ACTL_UNIT_BK_DTE                             INKCS046
                       , A.ACTL_FIN_BK_DTE                              INKCS046
                       , A.ACTL_INV_RECD_DTE                            INKCS046
                       , DAYS(A.ACTL_INV_DTE) + A.STKRM_DAY_QTY         INKCS046
                                              - DAYS(CURRENT_DATE)              
                       , A.SLS_ADJ_CPLD_IND                             INKCS046
W33304                 , A.SCHD_PHY_CNT_CDE                             INKCS046
                       , A.SHEET_INV_CPLD_IND                           INKCS046
                       , A.UNT_BKG_STAT_CDE                             INKCS046
                       , A.FIL_TOT_BAL_CDE                              INKCS046
                       , A.STKRM_WDRWL_IND                                      
                       , B.LOC_TYP_CDE                                  INKCS046
W26600                 , B.LOC_10_ABBR_NM                               INKCS046
                FROM   TINVPAR A                                        INKCS046
W26600                ,XMT_LOC B                                                
28545                 ,TINCNTL C                                                
W26600          WHERE  A.LOC_NBR          =  B.LOC_NBR                          
28545             AND  A.INV_ID           =  C.INV_ID                           
28545             AND  C.ACTV_IND         = 'Y'                         INKCS046
22960             AND  A.ACTL_FIN_BK_DTE  = '9999-09-09'                INKCS046
W33304            AND  A.LOC_INV_STAT_CDE  = 'IN'                       INKCS046
JS0300            AND  CURRENT DATE      >=  A.ACTL_INV_DTE             INKCS046
22960             AND ((A.SHEET_INV_CPLD_IND <> 'Y'                     INKCS046
22960                  AND A.SCAN_INV_IND = 'Y')                                
22960              OR (A.SCAN_INV_IND = 'N'                                     
W33304                 AND A.SCHD_PHY_CNT_CDE IN ('PI','NS')            INKCS046
22960                  AND A.ACTL_INV_RECD_DTE = '9999-09-09'))                 
W33304          ORDER BY C.PLND_FIN_BKG_DTE                             INKCS046
W33304                 , A.INV_ID                                       INKCS046
W33304                 , A.LOC_NBR                                      INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TINVPAR INVALIDS NOT CORRECTED DECLARE                     * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                DECLARE INVLD-NOT-CORR-CSR CURSOR FOR                   INKCS046
W26600          SELECT   A.LOC_NBR                                      INKCS046
                       , A.ACTL_INV_DTE                                         
                       , A.PLND_INV_DTE                                         
                       , A.SCAN_INV_IND                                         
                       , A.ACTL_UNIT_BK_DTE                             INKCS046
                       , A.ACTL_FIN_BK_DTE                              INKCS046
                       , A.ACTL_INV_RECD_DTE                            INKCS046
                       , DAYS(A.ACTL_INV_DTE) + A.STKRM_DAY_QTY         INKCS046
                                              - DAYS(CURRENT_DATE)              
                       , A.SLS_ADJ_CPLD_IND                             INKCS046
W33304                 , A.SCHD_PHY_CNT_CDE                             INKCS046
                       , A.SHEET_INV_CPLD_IND                           INKCS046
                       , A.UNT_BKG_STAT_CDE                             INKCS046
                       , A.FIL_TOT_BAL_CDE                              INKCS046
                       , A.STKRM_WDRWL_IND                                      
                       , B.LOC_TYP_CDE                                  INKCS046
W26600                 , B.LOC_10_ABBR_NM                               INKCS046
                FROM   TINVPAR A                                        INKCS046
W26600                ,XMT_LOC B                                                
28545                 ,TINCNTL C                                                
W26600          WHERE  A.LOC_NBR          =  B.LOC_NBR                          
28545             AND  A.INV_ID           =  C.INV_ID                           
28545             AND  C.ACTV_IND         = 'Y'                         INKCS046
22960             AND  A.ACTL_FIN_BK_DTE  = '9999-09-09'                INKCS046
W33304            AND  A.LOC_INV_STAT_CDE  = 'IN'                       INKCS046
                  AND  EXISTS                                           INKCS046
                       (SELECT 1                                        INKCS046
                        FROM TINVDTL C                                  INKCS046
W26600                  WHERE C.LOC_NBR = A.LOC_NBR                     INKCS046
22960                     AND C.INVALID_REC_IND = 'Y')                  INKCS046
W33304          ORDER BY C.PLND_FIN_BKG_DTE                             INKCS046
W33304                 , A.INV_ID                                       INKCS046
W33304                 , A.LOC_NBR                                      INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TINVPAR STOCKROOM WITHDRAWALS NOT ENTERED DECLARE          * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                DECLARE STCKRM-WITHDRW-CSR CURSOR FOR                   INKCS046
W26600          SELECT   A.LOC_NBR                                      INKCS046
                       , A.ACTL_INV_DTE                                         
                       , A.PLND_INV_DTE                                         
                       , A.SCAN_INV_IND                                         
                       , A.ACTL_UNIT_BK_DTE                             INKCS046
                       , A.ACTL_FIN_BK_DTE                              INKCS046
                       , A.ACTL_INV_RECD_DTE                            INKCS046
                       , DAYS(A.ACTL_INV_DTE) + A.STKRM_DAY_QTY         INKCS046
                                              - DAYS(CURRENT_DATE)              
                       , A.SLS_ADJ_CPLD_IND                             INKCS046
W33304                 , A.SCHD_PHY_CNT_CDE                             INKCS046
                       , A.SHEET_INV_CPLD_IND                           INKCS046
                       , A.UNT_BKG_STAT_CDE                             INKCS046
                       , A.FIL_TOT_BAL_CDE                              INKCS046
                       , A.STKRM_WDRWL_IND                                      
                       , B.LOC_TYP_CDE                                  INKCS046
W26600                 , B.LOC_10_ABBR_NM                               INKCS046
                FROM   TINVPAR A                                        INKCS046
W26600                ,XMT_LOC B                                                
28545                 ,TINCNTL C                                                
W26600          WHERE  A.LOC_NBR          =  B.LOC_NBR                          
28545             AND  A.INV_ID           =  C.INV_ID                           
28545             AND  C.ACTV_IND = 'Y'                                 INKCS046
W33304            AND  A.ACTL_FIN_BK_DTE  = '9999-09-09'                INKCS046
W33304            AND  A.LOC_INV_STAT_CDE  = 'IN'                       INKCS046
                  AND  CURRENT_DATE       >  A.ACTL_INV_DTE                     
22960             AND  A.SCAN_INV_IND      = 'Y'                        INKCS046
W33304            AND  A.SCHD_PHY_CNT_CDE  IN ('PI','NS')               INKCS046
W33304            AND  A.UNT_BKG_STAT_CDE  IN ('IN','SL')               INKCS046
22960             AND  A.STKRM_WDRWL_IND   = 'Y'                        INKCS046
                  AND  NOT EXISTS                                       INKCS046
                       (SELECT 1                                        INKCS046
                        FROM TSTKRMW C                                  INKCS046
W26600                  WHERE C.LOC_NBR = A.LOC_NBR)                    INKCS046
W33304          ORDER BY C.PLND_FIN_BKG_DTE                             INKCS046
W33304                 , A.INV_ID                                       INKCS046
W33304                 , A.LOC_NBR                                      INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TINVPAR SALES ADJUSTMENT NOT COMPLETED INQUIRY DECLARE     * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                DECLARE SALES-ADJS-NC-CSR CURSOR FOR                    INKCS046
W26600          SELECT   A.LOC_NBR                                      INKCS046
                       , A.ACTL_INV_DTE                                         
                       , A.PLND_INV_DTE                                         
                       , A.SCAN_INV_IND                                         
                       , A.ACTL_UNIT_BK_DTE                             INKCS046
                       , A.ACTL_FIN_BK_DTE                              INKCS046
                       , A.ACTL_INV_RECD_DTE                            INKCS046
                       , DAYS(A.ACTL_INV_DTE) + A.STKRM_DAY_QTY         INKCS046
                                              - DAYS(CURRENT_DATE)              
                       , A.SLS_ADJ_CPLD_IND                             INKCS046
W33304                 , A.SCHD_PHY_CNT_CDE                             INKCS046
                       , A.SHEET_INV_CPLD_IND                           INKCS046
                       , A.UNT_BKG_STAT_CDE                             INKCS046
                       , A.FIL_TOT_BAL_CDE                              INKCS046
                       , A.STKRM_WDRWL_IND                                      
                       , B.LOC_TYP_CDE                                  INKCS046
W26600                 , B.LOC_10_ABBR_NM                               INKCS046
                FROM   TINVPAR A                                        INKCS046
W26600                ,XMT_LOC B                                                
28545                 ,TINCNTL C                                                
W26000          WHERE  A.LOC_NBR            =  B.LOC_NBR                        
28545             AND  A.INV_ID             =  C.INV_ID                         
28545             AND  C.ACTV_IND           = 'Y'                       INKCS046
22960             AND  A.ACTL_FIN_BK_DTE    = '9999-09-09'              INKCS046
22960             AND  A.ACTL_INV_RECD_DTE <> '9999-09-09'              INKCS046
22960             AND  A.SLS_ADJ_CPLD_IND  <> 'Y'                       INKCS046
22960             AND  A.SCAN_INV_IND       = 'Y'                       INKCS046
W33304            AND  A.LOC_INV_STAT_CDE  = 'IN'                       INKCS046
W33304          ORDER BY C.PLND_FIN_BKG_DTE                             INKCS046
W33304                 , A.INV_ID                                       INKCS046
W33304                 , A.LOC_NBR                                      INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TINVPAR UNIT BOOKINGS NOT COMPLETED INQUIRY DECLARE        * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                DECLARE UNIT-BOOKNG-NC-CSR CURSOR FOR                   INKCS046
W26600          SELECT   A.LOC_NBR                                      INKCS046
                       , A.ACTL_INV_DTE                                         
                       , A.PLND_INV_DTE                                         
                       , A.SCAN_INV_IND                                         
                       , A.ACTL_UNIT_BK_DTE                             INKCS046
                       , A.ACTL_FIN_BK_DTE                              INKCS046
                       , A.ACTL_INV_RECD_DTE                            INKCS046
                       , DAYS(A.ACTL_INV_DTE) + A.STKRM_DAY_QTY         INKCS046
                                              - DAYS(CURRENT_DATE)              
                       , A.SLS_ADJ_CPLD_IND                             INKCS046
W33304                 , A.SCHD_PHY_CNT_CDE                             INKCS046
                       , A.SHEET_INV_CPLD_IND                           INKCS046
                       , A.UNT_BKG_STAT_CDE                             INKCS046
                       , A.FIL_TOT_BAL_CDE                              INKCS046
                       , A.STKRM_WDRWL_IND                                      
                       , B.LOC_TYP_CDE                                  INKCS046
W26600                 , B.LOC_10_ABBR_NM                               INKCS046
                FROM   TINVPAR A                                        INKCS046
                      ,XMT_LOC B                                                
28545                 ,TINCNTL C                                                
W26600          WHERE  A.LOC_NBR            =  B.LOC_NBR                        
28545             AND  A.INV_ID             =  C.INV_ID                         
28545             AND  C.ACTV_IND           = 'Y'                       INKCS046
W33304            AND  A.ACTL_FIN_BK_DTE    = '9999-09-09'              INKCS046
W33304            AND  A.LOC_INV_STAT_CDE  = 'IN'                       INKCS046
22960             AND  A.ACTL_UNIT_BK_DTE   = '9999-09-09'              INKCS046
22960             AND ((A.ACTL_INV_RECD_DTE <> '9999-09-09'             INKCS046
22960                   AND ((A.SCAN_INV_IND = 'Y'                      INKCS046
W33304                        AND A.SCHD_PHY_CNT_CDE IN ('PI','NS'))    INKCS046
22960                        OR (A.SCAN_INV_IND = 'N'                   INKCS046
W33304                           AND A.SCHD_PHY_CNT_CDE IN ('PI','NS')  INKCS046
W26600                           AND B.LOC_TYP_CDE IN                           
PK1165* REMOVED RV                   ('CS', 'DS', 'RV')                         
PK1165                               ('CS', 'DS')                               
22960                            AND EXISTS                                     
22960                                (SELECT 1                                  
22960                                  FROM TSUMINV C                           
W26600                                 WHERE C.LOC_NBR = A.LOC_NBR)             
22960                           )                                               
22960                       ))                                                  
W33304                 OR (A.SCHD_PHY_CNT_CDE = 'CL'                    INKCS046
                           AND CURRENT_DATE > A.ACTL_INV_DTE))                  
W33304          ORDER BY C.PLND_FIN_BKG_DTE                             INKCS046
W33304                 , A.INV_ID                                       INKCS046
W33304                 , A.LOC_NBR                                      INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TINVPAR UNIT BOOKINGS COMPLETED INQUIRY DECLARE            * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
                DECLARE UNIT-BOOK-CMPL-CSR CURSOR FOR                   INKCS046
W26600          SELECT   A.LOC_NBR                                      INKCS046
                       , A.ACTL_INV_DTE                                         
                       , A.PLND_INV_DTE                                         
                       , A.SCAN_INV_IND                                         
                       , A.ACTL_UNIT_BK_DTE                             INKCS046
                       , A.ACTL_FIN_BK_DTE                              INKCS046
                       , A.ACTL_INV_RECD_DTE                            INKCS046
                       , DAYS(A.ACTL_INV_DTE) + A.STKRM_DAY_QTY         INKCS046
                                              - DAYS(CURRENT_DATE)              
                       , A.SLS_ADJ_CPLD_IND                             INKCS046
W33304                 , A.SCHD_PHY_CNT_CDE                             INKCS046
                       , A.SHEET_INV_CPLD_IND                           INKCS046
                       , A.UNT_BKG_STAT_CDE                             INKCS046
                       , A.FIL_TOT_BAL_CDE                              INKCS046
                       , A.STKRM_WDRWL_IND                                      
                       , B.LOC_TYP_CDE                                  INKCS046
W26600                 , B.LOC_10_ABBR_NM                               INKCS046
                FROM   TINVPAR A                                        INKCS046
W26600                ,XMT_LOC B                                                
28545                 ,TINCNTL C                                                
W26600          WHERE  A.LOC_NBR            =  B.LOC_NBR                        
28545             AND  A.INV_ID             =  C.INV_ID                         
22960             AND  ((A.ACTL_INV_RECD_DTE <> '9999-09-09'            INKCS046
22960                   OR  (A.ACTL_INV_RECD_DTE = '9999-09-09'         INKCS046
W33304                  AND  SCHD_PHY_CNT_CDE = 'CL'))                  INKCS046
22960             AND  A.ACTL_UNIT_BK_DTE  <> '9999-09-09'              INKCS046
W33304            AND  A.ACTL_FIN_BK_DTE    = '9999-09-09'              INKCS046
W33304            AND  A.LOC_INV_STAT_CDE  = 'IN'                       INKCS046
22960             AND  C.ACTV_IND = 'Y')                                INKCS046
W33304          ORDER BY C.PLND_FIN_BKG_DTE                             INKCS046
W33304                 , A.INV_ID                                       INKCS046
W33304                 , A.LOC_NBR                                      INKCS046
           END-EXEC.                                                    INKCS046
W26600                                                                  INKCS046
      /                                                                 INKCS046
       LINKAGE SECTION.                                                 INKCS046
                                                                        INKCS046
       01  DFHCOMMAREA.                                                 INKCS046
           05  FILLER                         OCCURS  1 TO 4072 TIMES   INKCS046
                                              DEPENDING ON EIBCALEN.    INKCS046
               10  FILLER                     PIC X.                    INKCS046
                                                                        INKCS046
       PROCEDURE DIVISION.                                              INKCS046
      **--------------------------------------------------------------**INKCS046
      **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **INKCS046
      **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **INKCS046
      **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **INKCS046
      **--------------------------------------------------------------**INKCS046
                                                                        INKCS046
       0000-MAIN-MODULE.                                                INKCS046
                                                                        INKCS046
           INITIALIZE DP030-CICS-API-FIELDS.                            INKCS046
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.       INKCS046
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          INKCS046
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         INKCS046
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       INKCS046
                                                                        INKCS046
           MOVE LENGTH OF IN046AI        TO DP030-MAP-LENGTH (1).       INKCS046
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     INKCS046
           PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS046
                                                                        INKCS046
           PERFORM 1000-CONTROL-PROCESSING                              INKCS046
                                                                        INKCS046
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     INKCS046
           PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS046
                                                                        INKCS046
      *----------------------------------------------------------------*INKCS046
      *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *INKCS046
      *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       0001-CALL-CICS-ARCH-API.                                         INKCS046
                                                                        INKCS046
1122RM*    CALL 'DPKCS030' USING DFHEIBLK                               INKCS046
1122RM     CALL DP930-CICS-ARCH-API                                             
1122RM                     USING DFHEIBLK                                       
                                 DFHCOMMAREA                            INKCS046
                                 DP030-CICS-API-FIELDS                  INKCS046
                                 DP020-STANDARD-COMMAREA                INKCS046
                                 IN046AI.                               INKCS046
                                                                        INKCS046
           IF  DP030-RC-CALL-SUCCESSFUL                                 INKCS046
               CONTINUE                                                 INKCS046
           ELSE                                                         INKCS046
               SET DP013-NO-ROLLBACK                                    INKCS046
                   DP013-XCTL-DISPLAY-RESTART                           INKCS046
                   DP013-CICS-ABEND      TO TRUE                        INKCS046
               MOVE '0001-CALL-CICS-ARCH-API'                           INKCS046
                                         TO DP013-PARAGRAPH             INKCS046
1122RM*        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEXINKCS046
1122RM*             'T LINE'             TO DP013-MESSAGE-TEXT (2)      INKCS046
1122RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1122RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)      INKCS046
               PERFORM DP013-0000-PROCESS-ABEND                         INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      *----------------------------------------------------------------*INKCS046
      * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *INKCS046
      * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *INKCS046
      *                                                                *INKCS046
      * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *INKCS046
      *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *INKCS046
      *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *INKCS046
      *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       1000-CONTROL-PROCESSING.                                         INKCS046
                                                                        INKCS046
           EVALUATE TRUE                                                INKCS046
               WHEN DP020-NEXT-ACT-INITIAL                              INKCS046
                    MOVE EIBTRMID    TO ASC-LIST-TERM-ID                        
                    MOVE DP020-SRC-TASK-NUMBER                                  
                                     TO ASC-LIST-TASK-NUMBER                    
                    MOVE PC-LIST-QUEUE-SEQ                                      
                                     TO ASC-LIST-SEQ                            
                    MOVE DP020-INT-APPL-FORMAT-IND                              
                                     TO ASC-INT-APPL-FORMAT-IND                 
                    EVALUATE TRUE                                       INKCS046
                        WHEN ASC-ALL-LOC-INQ                            INKCS046
                            MOVE PV-ST-ALL-LOC-INQ        TO ASC-TITLE          
                        WHEN ASC-SCANNED-INV-NP                         INKCS046
                            MOVE PV-ST-SCANNED-INV-NP     TO ASC-TITLE          
                        WHEN ASC-ONLINE-SHEETS-NP                       INKCS046
                            MOVE PV-ST-ONLINE-SHEETS-NP   TO ASC-TITLE          
                        WHEN ASC-INVALIDS-NOT-CORR                      INKCS046
                            MOVE PV-ST-INVALIDS-NOT-CORR  TO ASC-TITLE          
                        WHEN ASC-STCKRM-WITHDRWL-NE                     INKCS046
                            MOVE PV-ST-STCKRM-WITHDRWL-NE TO ASC-TITLE          
                        WHEN ASC-SALES-ADJUSTS-NC                       INKCS046
                            MOVE PV-ST-SALES-ADJUSTS-NC   TO ASC-TITLE          
                        WHEN ASC-UNIT-BOOKINGS-NC                       INKCS046
                            MOVE PV-ST-UNIT-BOOKINGS-NC   TO ASC-TITLE          
                        WHEN ASC-UNIT-BOOKINGS-COMPLET                  INKCS046
                            MOVE PV-ST-UNIT-BOOKINGS-COMPLT                     
                                                          TO ASC-TITLE  INKCS046
                        WHEN OTHER                                      INKCS046
                             MOVE SPACES                  TO ASC-TITLE  INKCS046
                    END-EVALUATE                                        INKCS046
                    PERFORM 4000-BUILD-INITIAL-PANEL                            
                                                                        INKCS046
               WHEN DP020-NEXT-ACT-READ-MAP                             INKCS046
                    MOVE ASC-ITEM-AT-TOP-OF-PANEL                       INKCS046
                                         TO PV-TOP-ITEM                 INKCS046
                    PERFORM 2000-PROCESS-PANEL                          INKCS046
                                                                        INKCS046
               WHEN DP020-NEXT-ACT-RETURN                               INKCS046
                    MOVE ASC-ITEM-AT-TOP-OF-PANEL                       INKCS046
                                         TO PV-TOP-ITEM                 INKCS046
                    MOVE ZERO            TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS046
                    PERFORM 3000-EDIT-DATA-IN-COMMAREA                  INKCS046
                    PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                INKCS046
                                                                        INKCS046
               WHEN OTHER                                               INKCS046
                    SET DP013-LOGIC-ABEND TO TRUE                       INKCS046
                    SET DP013-NO-ROLLBACK TO TRUE                       INKCS046
                    MOVE '1000-CONTROL-PROCESSING'                      INKCS046
                                         TO DP013-PARAGRAPH             INKCS046
                    MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'   INKCS046
                                         TO DP013-MESSAGE-TEXT(1)       INKCS046
                    PERFORM DP013-0000-PROCESS-ABEND                    INKCS046
           END-EVALUATE.                                                INKCS046
                                                                        INKCS046
      *----------------------------------------------------------------*INKCS046
      * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *INKCS046
      * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *INKCS046
      * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *INKCS046
      * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *INKCS046
      * FUNCTION KEYS CAN GET THIS FAR.                                *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       2000-PROCESS-PANEL.                                              INKCS046
                                                                        INKCS046
           EVALUATE TRUE                                                INKCS046
               WHEN DP020-SRC-AID = DP016-CLEAR                         INKCS046
                   MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS046
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                   INKCS046
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS046
                                                                        INKCS046
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    INKCS046
                   PERFORM 2050-DELETE-LIST-QUEUE                       INKCS046
                   PERFORM 4000-BUILD-INITIAL-PANEL                     INKCS046
                                                                        INKCS046
               WHEN OTHER                                               INKCS046
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 INKCS046
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                   INKCS046
                   IF  PS-NO-ERROR                                      INKCS046
                       SET DP030-CONTINUE-GO                            INKCS046
                                       TO TRUE                          INKCS046
                       PERFORM 2100-CHECK-FUNCTION-KEY                  INKCS046
                   ELSE                                                 INKCS046
                       SET DP030-OVERRIDE-GO                            INKCS046
                                       TO TRUE                          INKCS046
                   END-IF                                               INKCS046
           END-EVALUATE.                                                INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       2050-DELETE-LIST-QUEUE.                                          INKCS046
                                                                        INKCS046
           EXEC CICS                                                    INKCS046
               DELETEQ TS                                               INKCS046
                   QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS046
                   RESP  (PV-CICS-RESP)                                 INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
           IF (PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS046
           OR  PV-CICS-RESP = DFHRESP(QIDERR))                          INKCS046
               CONTINUE                                                 INKCS046
           ELSE                                                         INKCS046
               SET DP013-CICS-ABEND      TO TRUE                        INKCS046
               SET DP013-NO-ROLLBACK     TO TRUE                        INKCS046
                                                                        INKCS046
               MOVE '2050-DELETE-LIST-QUEUE'                            INKCS046
                                         TO DP013-PARAGRAPH             INKCS046
               MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'    INKCS046
                                         TO DP013-MESSAGE-TEXT (1)      INKCS046
               PERFORM DP013-0000-PROCESS-ABEND                         INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
           MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.  INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*INKCS046
      * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *INKCS046
      * CICS ARCHITECTURE API.                                         *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       2100-CHECK-FUNCTION-KEY.                                         INKCS046
                                                                        INKCS046
           EVALUATE TRUE                                                INKCS046
22000                                                                   INKCS046
22000          WHEN DP020-FK-LOCAL-FUNC-10 (DP020-SRC-AID)                      
22000              PERFORM 6000-FORMAT-AND-SUBMIT                               
22000              PERFORM 2130-CHECK-FOR-JUMP-BROWSE                   INKCS046
22000              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS046
22000              MOVE PC-TSYMSG-00098 TO DP020-MSG-NUMBER                     
22000              SET DP020-MSG-INFORMATIONAL TO TRUE                          
                                                                                
               WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  INKCS046
                   MOVE +1               TO PV-TOP-ITEM                 INKCS046
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS046
                                                                        INKCS046
               WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   INKCS046
                   PERFORM 2110-BROWSE-BACKWARD                         INKCS046
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS046
                                                                        INKCS046
               WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   INKCS046
                   PERFORM 2120-BROWSE-FORWARD                          INKCS046
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS046
                                                                        INKCS046
               WHEN DP020-SRC-AID = DP016-ENTER                         INKCS046
                   PERFORM 2130-CHECK-FOR-JUMP-BROWSE                   INKCS046
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS046
                                                                        INKCS046
               WHEN OTHER                                               INKCS046
                   SET DP013-NO-ROLLBACK                                INKCS046
                       DP013-XCTL-DISPLAY-RESTART                       INKCS046
                       DP013-CICS-ABEND  TO TRUE                        INKCS046
                   MOVE '2100-CHECK-FUNCTION-KEY'                       INKCS046
                                         TO DP013-PARAGRAPH             INKCS046
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      INKCS046
                                         TO DP013-MESSAGE-TEXT (1)      INKCS046
                   PERFORM DP013-0000-PROCESS-ABEND                     INKCS046
           END-EVALUATE.                                                INKCS046
                                                                        INKCS046
      *----------------------------------------------------------------*INKCS046
      *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *INKCS046
      *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *INKCS046
      *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *INKCS046
      *    MESSAGE.                                                    *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       2110-BROWSE-BACKWARD.                                            INKCS046
                                                                        INKCS046
           SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL    INKCS046
               GIVING PV-TOP-ITEM.                                      INKCS046
                                                                        INKCS046
           IF  ((PV-TOP-ITEM < +1)                                      INKCS046
             OR (PV-TOP-ITEM = +1))                                     INKCS046
               MOVE +1                   TO PV-TOP-ITEM                 INKCS046
      *        -- TOP OF LIST --                                        INKCS046
               MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            INKCS046
               SET DP020-MSG-INFORMATIONAL TO TRUE                      INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *INKCS046
      *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *INKCS046
      *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       2120-BROWSE-FORWARD.                                             INKCS046
                                                                        INKCS046
           ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL              INKCS046
               GIVING PV-TOP-ITEM.                                      INKCS046
           IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                   INKCS046
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS046
                                         TO PV-TOP-ITEM                 INKCS046
      *        -- BOTTOM OF LIST --                                     INKCS046
               MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER            INKCS046
               SET DP020-MSG-INFORMATIONAL                              INKCS046
                                         TO TRUE                        INKCS046
           ELSE                                                         INKCS046
               COMPUTE PV-BOTTOM-ITEM =                                 INKCS046
                   PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                 INKCS046
               IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ            INKCS046
      *            -- BOTTOM OF LIST --                                 INKCS046
                   MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER            INKCS046
                   SET DP020-MSG-INFORMATIONAL                          INKCS046
                                         TO TRUE                        INKCS046
               END-IF                                                   INKCS046
           END-IF.                                                      INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *INKCS046
      *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *INKCS046
      *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *INKCS046
      *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *INKCS046
      *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *INKCS046
      *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       2130-CHECK-FOR-JUMP-BROWSE.                                      INKCS046
                                                                        INKCS046
           SET PS-NO-ERROR               TO TRUE.                       INKCS046
           IF (ASC-START-ITEM-REQUEST = SPACE)                          INKCS046
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS046
                                         TO PV-TOP-ITEM                 INKCS046
           ELSE                                                         INKCS046
               MOVE ASC-START-ITEM-REQUEST                              INKCS046
                                         TO DP010I-UNEDITED-FIELD       INKCS046
               MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS       INKCS046
               MOVE +0                   TO DP010I-MAXIMUM-DECIMALS     INKCS046
               SET DP010I-NEGATIVE-NOT-ALLOWED                          INKCS046
                                         TO TRUE                        INKCS046
263126         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263126              USING DP010I-NUMERIC-EDIT-AREA                              
                                                                        INKCS046
               IF  NOT DP010I-ERROR-DETECTED                            INKCS046
                   MOVE DP010I-NUMERIC-FIELD                            INKCS046
                                         TO  ASC-START-ITEM-REQUEST-N   INKCS046
                   IF  ASC-START-ITEM-REQUEST-N > ZERO                  INKCS046
                       IF  ASC-START-ITEM-REQUEST-N >                   INKCS046
                                                ASC-NUMBER-OF-ITEMS-READINKCS046
                           MOVE SPACE TO ASC-START-ITEM-REQUEST         INKCS046
                           COMPUTE PV-TOP-ITEM =                        INKCS046
                                   ASC-NUMBER-OF-ITEMS-READ             INKCS046
                                   - PC-ITEMS-PER-PANEL + 1             INKCS046
                           IF  PV-TOP-ITEM < +1                         INKCS046
                               MOVE +1   TO PV-TOP-ITEM                 INKCS046
                               MOVE PC-TSYMSG-00070                     INKCS046
                                         TO DP020-MSG-NUMBER            INKCS046
                               SET DP020-MSG-INFORMATIONAL              INKCS046
                                         TO TRUE                        INKCS046
                           END-IF                                       INKCS046
                       ELSE                                             INKCS046
                           MOVE ASC-START-ITEM-REQUEST-N                INKCS046
                                         TO PV-TOP-ITEM                 INKCS046
                           MOVE SPACE    TO ASC-START-ITEM-REQUEST      INKCS046
                           COMPUTE PV-BOTTOM-ITEM =                     INKCS046
                               PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1     INKCS046
                           IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READINKCS046
      *                        -- BOTTOM OF LIST --                     INKCS046
                               MOVE PC-TSYMSG-00070                     INKCS046
                                         TO DP020-MSG-NUMBER            INKCS046
                               SET DP020-MSG-INFORMATIONAL              INKCS046
                                         TO TRUE                        INKCS046
                           END-IF                                       INKCS046
                       END-IF                                           INKCS046
                   ELSE                                                 INKCS046
      *                --- VALUE OUT OF RANGE ----                      INKCS046
                       MOVE PC-TSYMSG-00101                             INKCS046
                                         TO DP020-MSG-NUMBER            INKCS046
                       SET DP030-SET-CURSOR-APPL-1                      INKCS046
                                         TO TRUE                        INKCS046
                       MOVE DP015-REVERSE                               INKCS046
                                         TO ASTRTITH                    INKCS046
                       MOVE DP015-RED    TO ASTRTITC                    INKCS046
                       MOVE DP015-UNP-NUM-BRT-OFF                       INKCS046
                                         TO ASTRTITA                    INKCS046
                       SET DP020-MSG-FATAL                              INKCS046
                                         TO TRUE                        INKCS046
                       SET PS-ERROR      TO TRUE                        INKCS046
                   END-IF                                               INKCS046
                                                                        INKCS046
               ELSE                                                     INKCS046
                   MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER            INKCS046
                   MOVE DP015-REVERSE    TO ASTRTITH                    INKCS046
                   MOVE DP015-RED        TO ASTRTITC                    INKCS046
                   MOVE DP015-UNP-NUM-BRT-OFF                           INKCS046
                                         TO ASTRTITA                    INKCS046
                   SET DP020-MSG-FATAL   TO TRUE                        INKCS046
                   SET PS-ERROR          TO TRUE                        INKCS046
               END-IF                                                   INKCS046
           END-IF.                                                      INKCS046
      *----------------------------------------------------------------*INKCS046
      *  MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE     *INKCS046
      *  SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM.  *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       2200-MOVE-SCREEN-TO-COMMAREA.                                    INKCS046
                                                                        INKCS046
           MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.             INKCS046
           MOVE +1                       TO PV-SUB.                     INKCS046
                                                                        INKCS046
            IF  ASTRTITL > ZERO                                         INKCS046
                MOVE ASTRTITI            TO ASC-START-ITEM-REQUEST      INKCS046
            ELSE                                                        INKCS046
                IF  ASTRTITF EQUAL DP015-ERASE-EOF                      INKCS046
                    INITIALIZE ASC-START-ITEM-REQUEST                   INKCS046
                END-IF                                                  INKCS046
            END-IF.                                                     INKCS046
                                                                        INKCS046
           PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS046
                                                                        INKCS046
      *----------------------------------------------------------------*INKCS046
      *  EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE       *INKCS046
      *  COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY).  *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       3000-EDIT-DATA-IN-COMMAREA.                                      INKCS046
                                                                        INKCS046
           SET PS-NO-ERROR               TO TRUE.                       INKCS046
           PERFORM 3200-RESET-ATTRIBUTES.                               INKCS046
           MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                     INKCS046
      **                                                                INKCS046
      * CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.              INKCS046
      * EDITTING IS DONE FROM THE BOTTOM UP.                            INKCS046
      **                                                                INKCS046
           COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                       INKCS046
               ASC-ITEMS-DISPLAYED - 1.                                 INKCS046
           PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *INKCS046
      *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*INKCS046
      *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *INKCS046
      *                                                                *INKCS046
      *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *INKCS046
      *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *INKCS046
      *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *INKCS046
      *    US IN THE 2ND BLOCK OF THE ARRAY.                           *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       3100-SET-SUBSCRIPTS.                                             INKCS046
                                                                        INKCS046
           COMPUTE ASC-ITEM-SUB =                                       INKCS046
               (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.        INKCS046
                                                                        INKCS046
           IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                         INKCS046
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS046
                                                                        INKCS046
               MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB               INKCS046
           ELSE                                                         INKCS046
               MOVE +1                     TO ASC-BLK-SUB               INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      *----------------------------------------------------------------*INKCS046
      *  RESET THE ATTRIBUTES OF ALL FIELDS ON THE MAP.                 INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       3200-RESET-ATTRIBUTES.                                           INKCS046
                                                                        INKCS046
           MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                   INKCS046
           MOVE DP015-GREEN              TO ASTRTITC.                   INKCS046
           MOVE DP015-UNDERLINE          TO ASTRTITH.                   INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *INKCS046
      *    INITIALIZE ALL COUNTERS AND FLAGS.                          *INKCS046
      *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *INKCS046
      *    DISPLAY THE PANEL TO USER.                                  *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       4000-BUILD-INITIAL-PANEL.                                        INKCS046
                                                                        INKCS046
           PERFORM 5120-DELETE-ASC-LISTQ.                               INKCS046
           INITIALIZE                  ASC-BLOCK-ENTRIES (1)            INKCS046
                                       ASC-BLOCK-ENTRIES (2).           INKCS046
           MOVE +1                 TO ASC-BLK-SUB.                      INKCS046
           SET ASC-ITEMS-LEFT-ON-DB                                     INKCS046
                                   TO TRUE.                             INKCS046
                                                                        INKCS046
           MOVE +0                 TO ASC-ITEM-SUB                      INKCS046
                                      ASC-NUMBER-OF-ITEMS-READ          INKCS046
                                      ASC-NUMBER-OF-SELECTED-ITEMS      INKCS046
                                      ASC-HIGHEST-BLOCK-WRITTEN         INKCS046
                                      ASC-START-OF-SELECTED-RANGE       INKCS046
                                      ASC-END-OF-SELECTED-RANGE         INKCS046
                                      ASC-ITEM-AT-TOP-OF-PANEL.         INKCS046
                                                                                
           EXEC SQL                                                             
               SET :PV-CURRENT-DATE = CURRENT DATE                              
           END-EXEC.                                                            
                                                                                
           PERFORM 4010-OPEN-CURSOR.                                    INKCS046
                                                                        INKCS046
           PERFORM 4300-READ-ITEMS-FROM-DB.                             INKCS046
                                                                        INKCS046
           PERFORM 4020-CLOSE-CURSOR.                                   INKCS046
                                                                        INKCS046
           MOVE +1                   TO  PV-TOP-ITEM.                   INKCS046
           IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                          INKCS046
               SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                    INKCS046
               SET DP020-MSG-FATAL   TO  TRUE                           INKCS046
               MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER               INKCS046
           ELSE                                                         INKCS046
               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     INKCS046
           END-IF.                                                      INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      * OPEN THE APPROPRIATE CURSOR.                                   *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       4010-OPEN-CURSOR.                                                INKCS046
                                                                        INKCS046
           PERFORM                                                      INKCS046
               WITH TEST AFTER                                          INKCS046
               VARYING PV-RETRY-COUNTER                                 INKCS046
               FROM 1 BY 1                                              INKCS046
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               INKCS046
                         OR (SQLCODE = ZERO))                           INKCS046
                                                                        INKCS046
               EVALUATE TRUE                                            INKCS046
                                                                        INKCS046
                   WHEN ASC-ALL-LOC-INQ                                 INKCS046
                        EXEC SQL                                        INKCS046
                             OPEN ALL-LOC-INQ-CSR                       INKCS046
                        END-EXEC                                        INKCS046
                                                                        INKCS046
                   WHEN ASC-SCANNED-INV-NP                              INKCS046
                        EXEC SQL                                        INKCS046
                             OPEN SCANNED-INV-NP-CSR                    INKCS046
                        END-EXEC                                        INKCS046
                                                                        INKCS046
                   WHEN ASC-ONLINE-SHEETS-NP                            INKCS046
                        EXEC SQL                                        INKCS046
                             OPEN ONLINE-SHEETS-CSR                     INKCS046
                        END-EXEC                                        INKCS046
                                                                        INKCS046
                   WHEN ASC-INVALIDS-NOT-CORR                           INKCS046
                        EXEC SQL                                        INKCS046
                             OPEN INVLD-NOT-CORR-CSR                    INKCS046
                        END-EXEC                                        INKCS046
                                                                        INKCS046
                   WHEN ASC-STCKRM-WITHDRWL-NE                          INKCS046
                        EXEC SQL                                        INKCS046
                             OPEN STCKRM-WITHDRW-CSR                    INKCS046
                        END-EXEC                                        INKCS046
                                                                        INKCS046
                   WHEN ASC-SALES-ADJUSTS-NC                            INKCS046
                        EXEC SQL                                        INKCS046
                             OPEN SALES-ADJS-NC-CSR                     INKCS046
                        END-EXEC                                        INKCS046
                                                                        INKCS046
                   WHEN ASC-UNIT-BOOKINGS-NC                            INKCS046
                        EXEC SQL                                        INKCS046
                             OPEN UNIT-BOOKNG-NC-CSR                    INKCS046
                        END-EXEC                                        INKCS046
                                                                        INKCS046
                   WHEN ASC-UNIT-BOOKINGS-COMPLET                       INKCS046
                        EXEC SQL                                        INKCS046
                             OPEN UNIT-BOOK-CMPL-CSR                    INKCS046
                        END-EXEC                                        INKCS046
                                                                        INKCS046
                   WHEN OTHER                                           INKCS046
                        SET DP013-LOGIC-ABEND TO TRUE                   INKCS046
                        SET DP013-NO-ROLLBACK TO TRUE                   INKCS046
                        MOVE '4010-OPEN-CURSOR' TO DP013-PARAGRAPH      INKCS046
                        MOVE 'INVALID INTER APPL FORMAT INDICATOR'      INKCS046
                                             TO DP013-MESSAGE-TEXT(1)   INKCS046
                        PERFORM DP013-0000-PROCESS-ABEND                INKCS046
               END-EVALUATE                                             INKCS046
                                                                        INKCS046
               EVALUATE TRUE                                            INKCS046
                   WHEN SQLCODE = ZERO                                  INKCS046
                   WHEN SQLCODE = -904                                  INKCS046
                   WHEN SQLCODE = -913                                  INKCS046
                       CONTINUE                                         INKCS046
                   WHEN SQLWARN0 NOT = SPACES                           INKCS046
                   WHEN SQLCODE < ZERO                                  INKCS046
                   WHEN SQLCODE > ZERO                                  INKCS046
                       MOVE '4010-OPEN-CURSOR'                          INKCS046
                                         TO DP013-PARAGRAPH             INKCS046
                       MOVE 'OPEN LINE CURSOR - '                       INKCS046
                                         TO DP013-MESSAGE-TEXT (1)      INKCS046
                       MOVE SQLCA        TO DP013-SQLCA                 INKCS046
                       MOVE 'TINVPAR '   TO DP013-DB2-TABLE-NAME (1)    INKCS046
                       MOVE 'XMT_LOC '   TO DP013-DB2-TABLE-NAME (2)    INKCS046
                       SET DP013-DB2-ABEND                              INKCS046
                           DP013-XCTL-DISPLAY-RESTART TO TRUE           INKCS046
                       PERFORM DP013-0000-PROCESS-ABEND                 INKCS046
               END-EVALUATE                                             INKCS046
           END-PERFORM.                                                 INKCS046
                                                                        INKCS046
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        INKCS046
               MOVE '4010-OPEN-CURSOR'   TO DP013-PARAGRAPH             INKCS046
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'   INKCS046
                                         TO DP013-MESSAGE-TEXT (1)      INKCS046
               MOVE SQLCA                TO DP013-SQLCA                 INKCS046
               MOVE 'TINVPAR '           TO DP013-DB2-TABLE-NAME (1)    INKCS046
               MOVE 'XMT_LOC '           TO DP013-DB2-TABLE-NAME (2)    INKCS046
               SET DP013-DB2-ABEND                                      INKCS046
                   DP013-XCTL-DISPLAY-RESTART                           INKCS046
                                         TO TRUE                        INKCS046
               PERFORM DP013-0000-PROCESS-ABEND                         INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      *                                                                *INKCS046
      * CLOSE THE APPROPRIATE CURSOR.                                  *INKCS046
      *                                                                *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       4020-CLOSE-CURSOR.                                               INKCS046
                                                                        INKCS046
           EVALUATE TRUE                                                INKCS046
                                                                        INKCS046
               WHEN ASC-ALL-LOC-INQ                                     INKCS046
                    EXEC SQL                                            INKCS046
                         CLOSE ALL-LOC-INQ-CSR                          INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-SCANNED-INV-NP                                  INKCS046
                    EXEC SQL                                            INKCS046
                         CLOSE SCANNED-INV-NP-CSR                       INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-ONLINE-SHEETS-NP                                INKCS046
                    EXEC SQL                                            INKCS046
                         CLOSE ONLINE-SHEETS-CSR                        INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-INVALIDS-NOT-CORR                               INKCS046
                    EXEC SQL                                            INKCS046
                         CLOSE INVLD-NOT-CORR-CSR                       INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-STCKRM-WITHDRWL-NE                              INKCS046
                    EXEC SQL                                            INKCS046
                         CLOSE STCKRM-WITHDRW-CSR                       INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-SALES-ADJUSTS-NC                                INKCS046
                    EXEC SQL                                            INKCS046
                         CLOSE SALES-ADJS-NC-CSR                        INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-UNIT-BOOKINGS-NC                                INKCS046
                    EXEC SQL                                            INKCS046
                         CLOSE UNIT-BOOKNG-NC-CSR                       INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-UNIT-BOOKINGS-COMPLET                           INKCS046
                    EXEC SQL                                            INKCS046
                         CLOSE UNIT-BOOK-CMPL-CSR                       INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN OTHER                                               INKCS046
                    SET DP013-LOGIC-ABEND TO TRUE                       INKCS046
                    SET DP013-NO-ROLLBACK TO TRUE                       INKCS046
                    MOVE '4020-CLOSE-CURSOR' TO DP013-PARAGRAPH         INKCS046
                    MOVE 'INVALID INTER APPL FORMAT INDICATOR'          INKCS046
                                         TO DP013-MESSAGE-TEXT(1)       INKCS046
                    PERFORM DP013-0000-PROCESS-ABEND                    INKCS046
           END-EVALUATE.                                                INKCS046
                                                                        INKCS046
           EVALUATE TRUE                                                INKCS046
               WHEN SQLCODE = ZERO                                      INKCS046
                   CONTINUE                                             INKCS046
               WHEN SQLWARN0 NOT = SPACES                               INKCS046
               WHEN SQLCODE < ZERO                                      INKCS046
               WHEN SQLCODE > ZERO                                      INKCS046
                   MOVE '4020-CLOSE-CURSOR'                             INKCS046
                                     TO DP013-PARAGRAPH                 INKCS046
                   MOVE 'CLOSE LINE CURSOR '                            INKCS046
                                     TO DP013-MESSAGE-TEXT (1)          INKCS046
                   MOVE SQLCA        TO DP013-SQLCA                     INKCS046
                   MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)        INKCS046
                   MOVE 'XMT_LOC '   TO DP013-DB2-TABLE-NAME (2)        INKCS046
                   SET DP013-DB2-ABEND                                  INKCS046
                       DP013-XCTL-DISPLAY-RESTART TO TRUE               INKCS046
                   PERFORM DP013-0000-PROCESS-ABEND                     INKCS046
           END-EVALUATE.                                                INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      *    SET UP THE HOST VARIABLES.                                  *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
      *----------------------------------------------------------------*INKCS046
      *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *INKCS046
      *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *INKCS046
      *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *INKCS046
      *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *INKCS046
      *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *INKCS046
      *                                                                *INKCS046
      *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *INKCS046
      *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *INKCS046
      *    UPON INITIALIZATION.                                        *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       4300-READ-ITEMS-FROM-DB.                                         INKCS046
                                                                        INKCS046
           PERFORM                                                      INKCS046
               VARYING PV-READ-COUNT                                    INKCS046
               FROM 1 BY 1                                              INKCS046
               UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                    INKCS046
                  OR (ASC-NO-ITEMS-LEFT-ON-DB))                         INKCS046
                                                                        INKCS046
               PERFORM 4310-FETCH-CURSOR                                INKCS046
                                                                        INKCS046
               IF  ASC-ITEMS-LEFT-ON-DB                                 INKCS046
                   PERFORM 4500-GET-TINENTY                                     
                   IF NOT ASC-ONLINE-SHEETS-NP                          INKCS046
                     OR (PS-TINENTY-FOUND                               INKCS046
                          OR (INVPAR-SCAN-INV-IND = 'N'                 INKCS046
W33304                        AND (INVPAR-SCHD-PHY-CNT-CDE = 'PI'               
W33304                        OR INVPAR-SCHD-PHY-CNT-CDE = 'NS')))              
                       IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                INKC
                           ADD +1        TO ASC-ITEM-SUB                    INKC
                       ELSE                                                 INKC
                           ADD +1        TO ASC-BLK-SUB                     INKC
                           MOVE +1       TO ASC-ITEM-SUB                    INKC
                                                                            INKC
                           IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY         INKC
                               MOVE +1   TO ASC-BLK-SUB                     INKC
                               SET ASC-BLOCK-MODIFIED (1)                   INKC
                                         TO TRUE                            INKC
                               PERFORM 5100-WRITE-TEMP-STORAGE              INKC
                               MOVE ASC-BLOCK-ENTRIES                       INKC
                                         (PC-MAX-BLOCKS-IN-ARRAY)           INKC
                                       TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)    INKC
                               ADD +1    TO ASC-BLK-SUB                     INKC
                               INITIALIZE ASC-BLOCK-ENTRIES(ASC-BLK-SUB)    INKC
                               SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)      INKC
                                         TO TRUE                            INKC
                           END-IF                                       INKCS046
                       END-IF                                           INKCS046
                                                                        INKCS046
                       ADD +1            TO ASC-NUMBER-OF-ITEMS-READ        INKC
                       MOVE ASC-NUMBER-OF-ITEMS-READ                    INKCS046
                                         TO ASC-LIST-ITEM-NUMBER        INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
W26600                 MOVE INVPAR-LOC-NBR                                      
                                         TO ASC-STORE-NUMBER                    
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
W26600                 MOVE XMT00001-LOC-10-ABBR-NM                             
                                         TO ASC-STORE-NAME                      
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                       PERFORM 4320-FIELD-FILL                                  
                                                                                
                   END-IF                                               INKCS046
               END-IF                                                   INKCS046
           END-PERFORM.                                                 INKCS046
                                                                        INKCS046
           IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                          INKCS046
               MOVE +1               TO ASC-BLK-SUB                     INKCS046
               PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS046
               ADD +1                TO ASC-BLK-SUB                     INKCS046
               PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS046
                                                                        INKCS046
               MOVE 1                TO ASC-BLK-SUB                     INKCS046
                                        PV-BLOCK-NUMBER                 INKCS046
               PERFORM 4410-READ-TEMP-STORAGE                           INKCS046
               IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1             INKCS046
                   MOVE 2            TO ASC-BLK-SUB                     INKCS046
                                        PV-BLOCK-NUMBER                 INKCS046
                   PERFORM 4410-READ-TEMP-STORAGE                       INKCS046
               END-IF                                                   INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      *    FETCH THE APPROPRIATE CURSOR.                               *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       4310-FETCH-CURSOR.                                               INKCS046
                                                                        INKCS046
           EVALUATE TRUE                                                INKCS046
                                                                        INKCS046
               WHEN ASC-ALL-LOC-INQ                                     INKCS046
                    EXEC SQL                                            INKCS046
                         FETCH ALL-LOC-INQ-CSR                          INKCS046
                         INTO                                           INKCS046
W26600                        :INVPAR-LOC-NBR                           INKCS046
                            , :INVPAR-ACTL-INV-DTE                      INKCS046
                            , :INVPAR-PLND-INV-DTE                      INKCS046
                            , :INVPAR-SCAN-INV-IND                      INKCS046
                            , :INVPAR-ACTL-UNIT-BK-DTE                  INKCS046
                            , :INVPAR-ACTL-FIN-BK-DTE                   INKCS046
                            , :INVPAR-ACTL-INV-RECD-DTE                 INKCS046
                            , :PV-STKRM-WITHD-DAYS-LEFT                 INKCS046
                            , :INVPAR-SLS-ADJ-CPLD-IND                  INKCS046
W33304                      , :INVPAR-SCHD-PHY-CNT-CDE                  INKCS046
                            , :INVPAR-SHEET-INV-CPLD-IND                INKCS046
                            , :INVPAR-UNT-BKG-STAT-CDE                  INKCS046
                            , :INVPAR-FIL-TOT-BAL-CDE                   INKCS046
23511C                      , :INVPAR-STKRM-WDRWL-IND                           
                            , :XMT00001-LOC-TYP-CDE                     INKCS046
W26600                      , :XMT00001-LOC-10-ABBR-NM:NI-LOC-10-ABBR-NMINKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-SCANNED-INV-NP                                  INKCS046
                    EXEC SQL                                            INKCS046
                         FETCH SCANNED-INV-NP-CSR                       INKCS046
                         INTO                                           INKCS046
W26600                        :INVPAR-LOC-NBR                           INKCS046
                            , :INVPAR-ACTL-INV-DTE                      INKCS046
                            , :INVPAR-PLND-INV-DTE                              
                            , :INVPAR-SCAN-INV-IND                      INKCS046
                            , :INVPAR-ACTL-UNIT-BK-DTE                  INKCS046
                            , :INVPAR-ACTL-FIN-BK-DTE                   INKCS046
                            , :INVPAR-ACTL-INV-RECD-DTE                 INKCS046
                            , :PV-STKRM-WITHD-DAYS-LEFT                 INKCS046
                            , :INVPAR-SLS-ADJ-CPLD-IND                  INKCS046
W33304                      , :INVPAR-SCHD-PHY-CNT-CDE                  INKCS046
                            , :INVPAR-SHEET-INV-CPLD-IND                INKCS046
                            , :INVPAR-UNT-BKG-STAT-CDE                  INKCS046
                            , :INVPAR-FIL-TOT-BAL-CDE                   INKCS046
23511C                      , :INVPAR-STKRM-WDRWL-IND                           
                            , :XMT00001-LOC-TYP-CDE                     INKCS046
W26600                      , :XMT00001-LOC-10-ABBR-NM:NI-LOC-10-ABBR-NMINKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-ONLINE-SHEETS-NP                                INKCS046
                    EXEC SQL                                            INKCS046
                         FETCH ONLINE-SHEETS-CSR                        INKCS046
                         INTO                                           INKCS046
W26600                        :INVPAR-LOC-NBR                           INKCS046
                            , :INVPAR-ACTL-INV-DTE                      INKCS046
                            , :INVPAR-PLND-INV-DTE                              
                            , :INVPAR-SCAN-INV-IND                      INKCS046
                            , :INVPAR-ACTL-UNIT-BK-DTE                  INKCS046
                            , :INVPAR-ACTL-FIN-BK-DTE                   INKCS046
                            , :INVPAR-ACTL-INV-RECD-DTE                 INKCS046
                            , :PV-STKRM-WITHD-DAYS-LEFT                 INKCS046
                            , :INVPAR-SLS-ADJ-CPLD-IND                  INKCS046
W33304                      , :INVPAR-SCHD-PHY-CNT-CDE                  INKCS046
                            , :INVPAR-SHEET-INV-CPLD-IND                INKCS046
                            , :INVPAR-UNT-BKG-STAT-CDE                  INKCS046
                            , :INVPAR-FIL-TOT-BAL-CDE                   INKCS046
23511C                      , :INVPAR-STKRM-WDRWL-IND                           
                            , :XMT00001-LOC-TYP-CDE                     INKCS046
W26600                      , :XMT00001-LOC-10-ABBR-NM                  INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-INVALIDS-NOT-CORR                               INKCS046
                    EXEC SQL                                            INKCS046
                         FETCH INVLD-NOT-CORR-CSR                       INKCS046
                         INTO                                           INKCS046
W26600                        :INVPAR-LOC-NBR                           INKCS046
                            , :INVPAR-ACTL-INV-DTE                      INKCS046
                            , :INVPAR-PLND-INV-DTE                              
                            , :INVPAR-SCAN-INV-IND                      INKCS046
                            , :INVPAR-ACTL-UNIT-BK-DTE                  INKCS046
                            , :INVPAR-ACTL-FIN-BK-DTE                   INKCS046
                            , :INVPAR-ACTL-INV-RECD-DTE                 INKCS046
                            , :PV-STKRM-WITHD-DAYS-LEFT                 INKCS046
                            , :INVPAR-SLS-ADJ-CPLD-IND                  INKCS046
W33304                      , :INVPAR-SCHD-PHY-CNT-CDE                  INKCS046
                            , :INVPAR-SHEET-INV-CPLD-IND                INKCS046
                            , :INVPAR-UNT-BKG-STAT-CDE                  INKCS046
                            , :INVPAR-FIL-TOT-BAL-CDE                   INKCS046
23511C                      , :INVPAR-STKRM-WDRWL-IND                           
                            , :XMT00001-LOC-TYP-CDE                     INKCS046
W26600                      , :XMT00001-LOC-10-ABBR-NM                  INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-STCKRM-WITHDRWL-NE                              INKCS046
                    EXEC SQL                                            INKCS046
                         FETCH STCKRM-WITHDRW-CSR                       INKCS046
                         INTO                                           INKCS046
W26600                        :INVPAR-LOC-NBR                           INKCS046
                            , :INVPAR-ACTL-INV-DTE                      INKCS046
                            , :INVPAR-PLND-INV-DTE                              
                            , :INVPAR-SCAN-INV-IND                      INKCS046
                            , :INVPAR-ACTL-UNIT-BK-DTE                  INKCS046
                            , :INVPAR-ACTL-FIN-BK-DTE                   INKCS046
                            , :INVPAR-ACTL-INV-RECD-DTE                 INKCS046
                            , :PV-STKRM-WITHD-DAYS-LEFT                 INKCS046
                            , :INVPAR-SLS-ADJ-CPLD-IND                  INKCS046
W33304                      , :INVPAR-SCHD-PHY-CNT-CDE                  INKCS046
                            , :INVPAR-SHEET-INV-CPLD-IND                INKCS046
                            , :INVPAR-UNT-BKG-STAT-CDE                  INKCS046
                            , :INVPAR-FIL-TOT-BAL-CDE                   INKCS046
23511C                      , :INVPAR-STKRM-WDRWL-IND                           
                            , :XMT00001-LOC-TYP-CDE                     INKCS046
W26600                      , :XMT00001-LOC-10-ABBR-NM                  INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-SALES-ADJUSTS-NC                                INKCS046
                    EXEC SQL                                            INKCS046
                         FETCH SALES-ADJS-NC-CSR                        INKCS046
                         INTO                                           INKCS046
W26600                        :INVPAR-LOC-NBR                           INKCS046
                            , :INVPAR-ACTL-INV-DTE                      INKCS046
                            , :INVPAR-PLND-INV-DTE                              
                            , :INVPAR-SCAN-INV-IND                      INKCS046
                            , :INVPAR-ACTL-UNIT-BK-DTE                  INKCS046
                            , :INVPAR-ACTL-FIN-BK-DTE                   INKCS046
                            , :INVPAR-ACTL-INV-RECD-DTE                 INKCS046
                            , :PV-STKRM-WITHD-DAYS-LEFT                 INKCS046
                            , :INVPAR-SLS-ADJ-CPLD-IND                  INKCS046
W33304                      , :INVPAR-SCHD-PHY-CNT-CDE                  INKCS046
                            , :INVPAR-SHEET-INV-CPLD-IND                INKCS046
                            , :INVPAR-UNT-BKG-STAT-CDE                  INKCS046
                            , :INVPAR-FIL-TOT-BAL-CDE                   INKCS046
23511C                      , :INVPAR-STKRM-WDRWL-IND                           
                            , :XMT00001-LOC-TYP-CDE                     INKCS046
W26600                      , :XMT00001-LOC-10-ABBR-NM                  INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-UNIT-BOOKINGS-NC                                INKCS046
                    EXEC SQL                                            INKCS046
                         FETCH UNIT-BOOKNG-NC-CSR                       INKCS046
                         INTO                                           INKCS046
W26600                        :INVPAR-LOC-NBR                           INKCS046
                            , :INVPAR-ACTL-INV-DTE                      INKCS046
                            , :INVPAR-PLND-INV-DTE                              
                            , :INVPAR-SCAN-INV-IND                      INKCS046
                            , :INVPAR-ACTL-UNIT-BK-DTE                  INKCS046
                            , :INVPAR-ACTL-FIN-BK-DTE                   INKCS046
                            , :INVPAR-ACTL-INV-RECD-DTE                 INKCS046
                            , :PV-STKRM-WITHD-DAYS-LEFT                 INKCS046
                            , :INVPAR-SLS-ADJ-CPLD-IND                  INKCS046
W33304                      , :INVPAR-SCHD-PHY-CNT-CDE                  INKCS046
                            , :INVPAR-SHEET-INV-CPLD-IND                INKCS046
                            , :INVPAR-UNT-BKG-STAT-CDE                  INKCS046
                            , :INVPAR-FIL-TOT-BAL-CDE                   INKCS046
23511C                      , :INVPAR-STKRM-WDRWL-IND                           
                            , :XMT00001-LOC-TYP-CDE                     INKCS046
W26600                      , :XMT00001-LOC-10-ABBR-NM                  INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN ASC-UNIT-BOOKINGS-COMPLET                           INKCS046
                    EXEC SQL                                            INKCS046
                         FETCH UNIT-BOOK-CMPL-CSR                       INKCS046
                         INTO                                           INKCS046
W26600                        :INVPAR-LOC-NBR                           INKCS046
                            , :INVPAR-ACTL-INV-DTE                      INKCS046
                            , :INVPAR-PLND-INV-DTE                              
                            , :INVPAR-SCAN-INV-IND                      INKCS046
                            , :INVPAR-ACTL-UNIT-BK-DTE                  INKCS046
                            , :INVPAR-ACTL-FIN-BK-DTE                   INKCS046
                            , :INVPAR-ACTL-INV-RECD-DTE                 INKCS046
                            , :PV-STKRM-WITHD-DAYS-LEFT                 INKCS046
                            , :INVPAR-SLS-ADJ-CPLD-IND                  INKCS046
W33304                      , :INVPAR-SCHD-PHY-CNT-CDE                  INKCS046
                            , :INVPAR-SHEET-INV-CPLD-IND                INKCS046
                            , :INVPAR-UNT-BKG-STAT-CDE                  INKCS046
                            , :INVPAR-FIL-TOT-BAL-CDE                   INKCS046
23511C                      , :INVPAR-STKRM-WDRWL-IND                           
                            , :XMT00001-LOC-TYP-CDE                     INKCS046
W26600                      , :XMT00001-LOC-10-ABBR-NM                  INKCS046
                    END-EXEC                                            INKCS046
                                                                        INKCS046
               WHEN OTHER                                               INKCS046
                    SET DP013-LOGIC-ABEND TO TRUE                       INKCS046
                    SET DP013-NO-ROLLBACK TO TRUE                       INKCS046
                    MOVE '4310-FETCH-CURSOR'                            INKCS046
                                          TO DP013-PARAGRAPH            INKCS046
                    MOVE 'INVALID INTER APPL FORMAT INDICATOR'          INKCS046
                                          TO DP013-MESSAGE-TEXT(1)      INKCS046
                    PERFORM DP013-0000-PROCESS-ABEND                    INKCS046
           END-EVALUATE.                                                INKCS046
                                                                        INKCS046
           EVALUATE TRUE                                                INKCS046
               WHEN SQLCODE = ZERO                                      INKCS046
               WHEN SQLCODE = +100                                      INKCS046
                    CONTINUE                                            INKCS046
               WHEN SQLWARN0 NOT = SPACE                                INKCS046
               WHEN SQLCODE NOT = ZERO                                  INKCS046
                    MOVE '4310-FETCH-CURSOR'                            INKCS046
                                     TO DP013-PARAGRAPH                 INKCS046
                    MOVE 'FETCH CURSOR'                                 INKCS046
                                     TO DP013-MESSAGE-TEXT (1)          INKCS046
                    MOVE SQLCA       TO DP013-SQLCA                     INKCS046
                    SET DP013-DB2-ABEND                                 INKCS046
                                     TO TRUE                            INKCS046
                    PERFORM DP013-0000-PROCESS-ABEND                    INKCS046
           END-EVALUATE.                                                INKCS046
                                                                        INKCS046
           IF  SQLCODE EQUAL +100                                       INKCS046
               SET ASC-NO-ITEMS-LEFT-ON-DB                              INKCS046
                                         TO TRUE                        INKCS046
           END-IF.                                                      INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      * FILL THE LINE FIELDS IN THE COMMAREA.                          *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       4320-FIELD-FILL.                                                 INKCS046
                                                                        INKCS046
      *    FILL LOCATION TYPE                                           INKCS046
W33304     IF INVPAR-SCHD-PHY-CNT-CDE = 'EI'                            INKCS046
JS0600         MOVE 'EST SH'                TO ASC-LOC-TYPE             INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
           ELSE                                                                 
W33304         IF INVPAR-SCHD-PHY-CNT-CDE = 'CL'                        INKCS046
                   MOVE 'CLOSED'            TO ASC-LOC-TYPE             INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
               ELSE                                                             
                   IF INVPAR-SCAN-INV-IND = 'Y'                         INKCS046
                       MOVE 'SCAN'          TO ASC-LOC-TYPE             INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                   ELSE                                                         
                       IF INVPAR-SCAN-INV-IND = 'N'                     INKCS046
W26600                     IF XMT00001-LOC-TYP-CDE  = 'CS'              INKCS046
W26600                                         OR 'DC' OR 'DA'          INKCS046
22960                                          OR 'RV' OR 'DS'                  
W26600                         MOVE XMT00001-LOC-TYP-CDE                INKCS046
                                            TO ASC-LOC-TYPE             INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                           ELSE                                                 
                               MOVE SPACES  TO ASC-LOC-TYPE             INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                           END-IF                                       INKCS046
                       ELSE                                                     
                           MOVE SPACES      TO ASC-LOC-TYPE             INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                       END-IF                                           INKCS046
                   END-IF                                               INKCS046
               END-IF                                                   INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      *    FILL ACTUAL INVENTORY DATE                                   INKCS046
           IF INVPAR-ACTL-INV-DTE = PC-INCOMPLETE-DTE                   INKCS046
               MOVE INVPAR-PLND-INV-DTE(6:5)                            INKCS046
                                 TO ASC-ACT-INV-DATE                    INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
           ELSE                                                                 
               MOVE INVPAR-ACTL-INV-DTE(6:5)                            INKCS046
                                 TO ASC-ACT-INV-DATE                    INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      *    FILL ACTUAL RECEIVED DATE                                    INKCS046
           IF INVPAR-ACTL-INV-DTE = PC-INCOMPLETE-DTE                   INKCS046
JS0300         MOVE SPACES       TO ASC-ACT-RECD-DATE                   INKCS046
JS0300                                       (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
JS0300     ELSE                                                                 
JS0300       IF INVPAR-ACTL-INV-DTE = PV-CURRENT-DATE                   INKCS046
W33304           IF (INVPAR-SCHD-PHY-CNT-CDE = 'PI' OR 'NS')            INKCS046
JS0300              AND INVPAR-SCAN-INV-IND = 'N'                       INKCS046
SJ0603                IF INVPAR-ACTL-INV-RECD-DTE < PC-INCOMPLETE-DTE   INKCS046
SJ0603                    MOVE INVPAR-ACTL-INV-RECD-DTE (6:5)           INKCS046
JS0300                                    TO ASC-ACT-RECD-DATE                  
SJ0603                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
SJ0603                ELSE                                              INKCS046
JS0300                    MOVE 'NOT PROC' TO ASC-ACT-RECD-DATE          INKCS046
JS0300                                       (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
SJ0603                END-IF                                            INKCS046
JS0300           ELSE                                                           
JS0300              MOVE SPACES       TO ASC-ACT-RECD-DATE              INKCS046
JS0300                                       (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
JS0300           END-IF                                                         
             ELSE                                                               
               IF INVPAR-ACTL-INV-RECD-DTE = PC-INCOMPLETE-DTE          INKCS046
W33304             IF (INVPAR-SCHD-PHY-CNT-CDE = 'PI' OR 'NS')          INKCS046
                     AND INVPAR-SCAN-INV-IND = 'Y'                      INKCS046
                       IF INVPAR-FIL-TOT-BAL-CDE = 'Y'                  INKCS046
                           MOVE 'BALANCE'   TO ASC-ACT-RECD-DATE        INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                       ELSE                                                     
                           IF INVPAR-FIL-TOT-BAL-CDE = 'N'              INKCS046
                               MOVE 'PEND TTL' TO ASC-ACT-RECD-DATE     INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                           ELSE                                                 
                               MOVE 'NOT RECD' TO ASC-ACT-RECD-DATE     INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                           END-IF                                               
                       END-IF                                                   
                   ELSE                                                         
W33304                 IF (INVPAR-SCHD-PHY-CNT-CDE = 'PI' OR 'NS')      INKCS046
                         AND INVPAR-SCAN-INV-IND = 'N'                  INKCS046
                           MOVE 'NOT PROC' TO ASC-ACT-RECD-DATE         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                       ELSE                                                     
W33304                     IF INVPAR-SCHD-PHY-CNT-CDE = 'EI' OR 'CL'    INKCS046
                               MOVE 'N/A'  TO ASC-ACT-RECD-DATE         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                           ELSE                                                 
                               MOVE 'ERR'  TO ASC-ACT-RECD-DATE         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                       END-IF                                                   
                   END-IF                                                       
               ELSE                                                             
                   MOVE INVPAR-ACTL-INV-RECD-DTE(6:5)                   INKCS046
                                            TO ASC-ACT-RECD-DATE        INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
               END-IF                                                   INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      *    FILL ON-LINE SHEET                                           INKCS046
W05091     IF INVPAR-SHEET-INV-CPLD-IND = 'S'                           INKCS046
W05091         MOVE 'SELECTED'  TO ASC-ON-LINE-SHEET                    INKCS046
W05091                                       (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
W05091     ELSE                                                                 
           IF INVPAR-ACTL-INV-DTE = PC-INCOMPLETE-DTE                   INKCS046
JS0400        AND INVPAR-SCAN-INV-IND = 'Y'                             INKCS046
W33304        AND (INVPAR-SCHD-PHY-CNT-CDE = 'PI' OR 'NS')              INKCS046
JS0400        AND PS-TINENTY-FOUND                                              
JS0400            MOVE 'STARTED'   TO ASC-ON-LINE-SHEET                         
JS0400                                   (ASC-BLK-SUB ASC-ITEM-SUB)             
JS0400     ELSE                                                                 
JS0400         IF INVPAR-ACTL-INV-DTE = PC-INCOMPLETE-DTE               INKCS046
JS0400             MOVE SPACES      TO ASC-ON-LINE-SHEET                        
JS0400                                   (ASC-BLK-SUB ASC-ITEM-SUB)             
               ELSE                                                             
                 IF INVPAR-SHEET-INV-CPLD-IND = 'Y'                     INKCS046
22000                MOVE PV-SQL-TINENTY-DATE-CHECK(6:5)                        
22000                    TO ASC-ON-LINE-SHEET(ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                 ELSE                                                           
                     IF INVPAR-SCAN-INV-IND = 'Y'                       INKCS046
                         IF PS-TINENTY-FOUND                                    
                             MOVE 'STARTED'   TO ASC-ON-LINE-SHEET      INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                         ELSE                                                   
                             MOVE SPACES      TO ASC-ON-LINE-SHEET      INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                         END-IF                                         INKCS046
                     ELSE                                                       
                        IF INVPAR-ACTL-INV-RECD-DTE = PC-INCOMPLETE-DTE INKCS046
                             IF PS-TINENTY-FOUND                                
                                 MOVE 'STARTED'                         INKCS046
                                            TO ASC-ON-LINE-SHEET        INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                             ELSE                                               
                                 MOVE 'NOT ENTRD'                       INKCS046
                                            TO ASC-ON-LINE-SHEET        INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                             END-IF                                     INKCS046
                         ELSE                                                   
22000                        IF PS-TINENTY-FOUND                                
22000                            MOVE PV-SQL-TINENTY-DATE-CHECK(6:5)            
22000                                TO ASC-ON-LINE-SHEET                       
22000                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
22000                        ELSE                                               
22000                            MOVE 'NONE'                                    
22000                                TO ASC-ON-LINE-SHEET                       
22000                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
22000                        END-IF                                             
                        END-IF                                          INKCS046
                     END-IF                                             INKCS046
                 END-IF                                                 INKCS046
             END-IF                                                     INKCS046
JS0400     END-IF.                                                      INKCS046
                                                                        INKCS046
      *    FILL SALES ADJUSTMENTS                                       INKCS046
W33304     IF INVPAR-SCHD-PHY-CNT-CDE = 'EI'                            INKCS046
W33304       OR INVPAR-SCHD-PHY-CNT-CDE = 'CL'                          INKCS046
             OR INVPAR-SCAN-INV-IND = 'N'                               INKCS046
               MOVE 'N/A'                   TO ASC-SALES-ADJUST         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
           ELSE                                                                 
               IF INVPAR-ACTL-INV-DTE = PC-INCOMPLETE-DTE               INKCS046
                 OR INVPAR-ACTL-INV-DTE = PV-CURRENT-DATE                       
JS0331           OR INVPAR-ACTL-INV-RECD-DTE = PC-INCOMPLETE-DTE        INKCS046
                   MOVE SPACES              TO ASC-SALES-ADJUST         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
               ELSE                                                             
                   IF INVPAR-SLS-ADJ-CPLD-IND = 'Y'                     INKCS046
22000                  PERFORM 4800-GET-TADJDTL                                 
22000                  MOVE PV-SQL-TADJDTL-DATE-CHECK(6:5)                      
22000                      TO ASC-SALES-ADJUST(ASC-BLK-SUB ASC-ITEM-SUB)INKCS046
                   ELSE                                                         
W33304                 IF (INVPAR-SCHD-PHY-CNT-CDE = 'PI' OR 'NS')      INKCS046
                         AND INVPAR-SCAN-INV-IND = 'Y'                  INKCS046
                         AND INVPAR-SLS-ADJ-CPLD-IND = 'N'              INKCS046
                           PERFORM 4600-GET-TINVDTL                             
                           IF PS-TINVDTL-FOUND                                  
                               MOVE 'WAIT-CORR'                         INKCS046
                                            TO ASC-SALES-ADJUST         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                           ELSE                                                 
JS0400                       PERFORM 4700-GET-TSTKRMW                           
JS0400                       IF PS-TSTKRMW-NOT-FOUND                            
                               AND PV-STKRM-WITHD-DAYS-LEFT > ZERO      INKCS046
23511C                         AND INVPAR-STKRM-WDRWL-IND = 'Y'                 
                                   MOVE 'WAIT-STRM'                     INKCS046
                                            TO ASC-SALES-ADJUST         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                              ELSE                                              
                               MOVE 'WAIT-CAND'                         INKCS046
                                            TO ASC-SALES-ADJUST         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                              END-IF                                            
                           END-IF                                               
                       ELSE                                                     
                           MOVE 'ERR'       TO ASC-SALES-ADJUST         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                   INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      *    FILL UNIT BOOKINGS                                           INKCS046
           IF INVPAR-SCAN-INV-IND = 'N'                                 INKCS046
PK1165*      AND (XMT00001-LOC-TYP-CDE = 'DC' OR 'DA' OR 'RV')                  
PK1165       AND (XMT00001-LOC-TYP-CDE = 'DC' OR 'DA')                          
               MOVE 'N/A'                   TO ASC-UNIT-BOOKING         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
           ELSE                                                                 
W33304         IF INVPAR-SCHD-PHY-CNT-CDE = 'CL'                        INKCS046
                   MOVE 'N/A'               TO ASC-UNIT-BOOKING         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
               ELSE                                                             
                   IF INVPAR-ACTL-INV-RECD-DTE = PC-INCOMPLETE-DTE      INKCS046
JS0500               AND INVPAR-ACTL-INV-DTE = PC-INCOMPLETE-DTE                
W33304               AND INVPAR-SCHD-PHY-CNT-CDE = 'CL'                         
                       MOVE SPACES          TO ASC-UNIT-BOOKING         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
JS0500             ELSE                                                         
JS0500             IF INVPAR-ACTL-INV-RECD-DTE = PC-INCOMPLETE-DTE      INKCS046
W33304               AND INVPAR-SCHD-PHY-CNT-CDE NOT = 'CL'                     
                       MOVE SPACES          TO ASC-UNIT-BOOKING         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                   ELSE                                                         
                       IF INVPAR-ACTL-UNIT-BK-DTE = PC-INCOMPLETE-DTE   INKCS046
                           IF INVPAR-SLS-ADJ-CPLD-IND NOT = 'Y'         INKCS046
W33304                       AND INVPAR-SCHD-PHY-CNT-CDE NOT = 'CL'             
                               MOVE 'WAIT-SLS ADJ'                      INKCS046
                                            TO ASC-UNIT-BOOKING         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                           ELSE                                                 
W33304                       IF INVPAR-UNT-BKG-STAT-CDE = 'SL'          INKCS046
                                 MOVE 'WAIT-NOT SEL'                    INKCS046
                                            TO ASC-UNIT-BOOKING         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                             ELSE                                               
W33304                         IF INVPAR-UNT-BKG-STAT-CDE = 'AP'        INKCS046
                                   MOVE 'SEL FOR BKNG'                  INKCS046
                                            TO ASC-UNIT-BOOKING         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                               ELSE                                             
W33304                           IF INVPAR-UNT-BKG-STAT-CDE = 'IP'      INKCS046
                                     MOVE 'BKNG IN PROC'                INKCS046
                                            TO ASC-UNIT-BOOKING         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                                 ELSE                                           
W33304                           IF INVPAR-UNT-BKG-STAT-CDE = 'IN'      INKCS046
JS0400                               MOVE 'WAIT-CAND   '                INKCS046
JS0400                                      TO ASC-UNIT-BOOKING         INKCS046
JS0400                                       (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                                 ELSE                                           
                                     MOVE 'ERROR       '                INKCS046
                                            TO ASC-UNIT-BOOKING         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                                 END-IF                                         
                               END-IF                                           
                             END-IF                                             
                           END-IF                                               
                       ELSE                                                     
                           MOVE INVPAR-ACTL-UNIT-BK-DTE(6:5)            INKCS046
                                            TO ASC-UNIT-BOOKING         INKCS046
                                             (ASC-BLK-SUB ASC-ITEM-SUB) INKCS046
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                   INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       4400-MOVE-COMMAREA-TO-SCREEN.                                    INKCS046
                                                                        INKCS046
           MOVE ASC-TITLE                TO  ATITLEO.                   INKCS046
           MOVE -1                       TO  ASTRTITL.                  INKCS046
           SET DP030-SET-CURSOR-APPL-1   TO  TRUE.                      INKCS046
                                                                        INKCS046
           IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))             INKCS046
               AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))          INKCS046
               OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))          INKCS046
                   MOVE +1    TO ASC-BLK-SUB                            INKCS046
                   PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS046
                   ADD +1     TO ASC-BLK-SUB                            INKCS046
                   PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS046
                   DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL             INKCS046
                       GIVING PV-BLOCK-NUMBER                           INKCS046
                       REMAINDER PV-REMAINDER                           INKCS046
                   IF  PV-REMAINDER > ZERO                              INKCS046
                       ADD +1 TO PV-BLOCK-NUMBER                        INKCS046
                   END-IF                                               INKCS046
                   MOVE +1    TO ASC-BLK-SUB                            INKCS046
                   PERFORM 4410-READ-TEMP-STORAGE                       INKCS046
                   ADD +1     TO ASC-BLK-SUB                            INKCS046
                                 PV-BLOCK-NUMBER                        INKCS046
                   PERFORM 4410-READ-TEMP-STORAGE                       INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
           MOVE +1            TO ASC-BLK-SUB.                           INKCS046
           COMPUTE ASC-ITEM-SUB =                                       INKCS046
               ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))              INKCS046
               + PC-ITEMS-PER-PANEL).                                   INKCS046
                                                                        INKCS046
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        INKCS046
               ADD +1         TO ASC-BLK-SUB                            INKCS046
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
           COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                           INKCS046
               ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                INKCS046
           PERFORM 4420-FORMAT-LIST-LINES                               INKCS046
               VARYING PV-SUB                                           INKCS046
               FROM PC-ITEMS-PER-PANEL BY -1                            INKCS046
               UNTIL PV-SUB < +1.                                       INKCS046
                                                                        INKCS046
           COMPUTE ASC-ITEMS-DISPLAYED =                                INKCS046
               ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).          INKCS046
           MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL.   INKCS046
           IF  PS-NO-ERROR                                              INKCS046
               MOVE PV-TOP-ITEM          TO ASTRTITO                    INKCS046
           END-IF.                                                      INKCS046
           MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                    INKCS046
           MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                    INKCS046
                                                                        INKCS046
           IF  DP020-MSG-NUMBER-X = SPACE                               INKCS046
               IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ  INKCS046
      *            --- MORE ITEMS TO DISPLAY ----                       INKCS046
                   MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER            INKCS046
                   SET DP020-MSG-INFORMATIONAL TO TRUE                  INKCS046
               END-IF                                                   INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      *----------------------------------------------------------------*INKCS046
      *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       4410-READ-TEMP-STORAGE.                                          INKCS046
                                                                        INKCS046
           EXEC CICS                                                    INKCS046
               READQ TS                                                 INKCS046
                   QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS046
                   INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS046
                   LENGTH (LENGTH OF ASC-BLOCK-ENTRIES(ASC-BLK-SUB))            
                   ITEM  (PV-BLOCK-NUMBER)                              INKCS046
                   RESP  (PV-CICS-RESP)                                 INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
           IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                       INKCS046
             OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                     INKCS046
               CONTINUE                                                 INKCS046
           ELSE                                                         INKCS046
               SET DP013-CICS-ABEND     TO TRUE                         INKCS046
               SET DP013-NO-ROLLBACK    TO TRUE                         INKCS046
               MOVE '4410-READ-TEMP-STORAGE'                            INKCS046
                                        TO DP013-PARAGRAPH              INKCS046
               MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'      INKCS046
                                        TO DP013-MESSAGE-TEXT(1)        INKCS046
               PERFORM DP013-0000-PROCESS-ABEND                         INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS046
                                                                        INKCS046
           IF  PV-CICS-RESP = DFHRESP (ITEMERR)                         INKCS046
               INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)               INKCS046
           END-IF.                                                      INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*INKCS046
      *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       4420-FORMAT-LIST-LINES.                                          INKCS046
                                                                        INKCS046
           IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ      INKCS046
               SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                INKCS046
               MOVE SPACE              TO MR-STORE         (PV-SUB)     INKCS046
                                          MR-STORE-NAME    (PV-SUB)     INKCS046
                                          MR-LOC-TYPE      (PV-SUB)     INKCS046
                                          MR-ACT-INV-DATE  (PV-SUB)     INKCS046
                                          MR-ACT-RECD-DATE (PV-SUB)     INKCS046
                                          MR-ON-LINE-SHEET (PV-SUB)     INKCS046
                                          MR-SALES-ADJUST  (PV-SUB)     INKCS046
                                          MR-UNIT-BOOKING  (PV-SUB)     INKCS046
           ELSE                                                         INKCS046
                                                                        INKCS046
               IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL           INKCS046
                   MOVE ASC-STORE-NUMBER  (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS046
                                       TO MR-STORE         (PV-SUB)     INKCS046
                   MOVE ASC-STORE-NAME    (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS046
                                       TO MR-STORE-NAME    (PV-SUB)     INKCS046
                   MOVE ASC-LOC-TYPE      (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS046
                                       TO MR-LOC-TYPE      (PV-SUB)     INKCS046
                   MOVE ASC-ACT-INV-DATE (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS046
                                       TO MR-ACT-INV-DATE  (PV-SUB)     INKCS046
                   MOVE ASC-ACT-RECD-DATE (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS046
                                       TO MR-ACT-RECD-DATE (PV-SUB)     INKCS046
                   MOVE ASC-ON-LINE-SHEET (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS046
                                       TO MR-ON-LINE-SHEET (PV-SUB)     INKCS046
                   MOVE ASC-SALES-ADJUST  (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS046
                                       TO MR-SALES-ADJUST  (PV-SUB)     INKCS046
                   MOVE ASC-UNIT-BOOKING  (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS046
                                       TO MR-UNIT-BOOKING  (PV-SUB)     INKCS046
                   MOVE DP015-BLUE     TO MR-STOREC        (PV-SUB)             
                                          MR-STORE-NAMEC   (PV-SUB)             
                                          MR-LOC-TYPEC     (PV-SUB)             
                                          MR-ACT-INV-DATEC (PV-SUB)             
                                          MR-ACT-RECD-DATEC (PV-SUB)            
                                          MR-ON-LINE-SHEETC (PV-SUB)            
                                          MR-SALES-ADJUSTC (PV-SUB)             
                                          MR-UNIT-BOOKINGC (PV-SUB)             
               END-IF                                                   INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
           SUBTRACT +1 FROM ASC-ITEM-SUB                                INKCS046
           IF  ASC-ITEM-SUB < + 1                                       INKCS046
               MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                  INKCS046
               MOVE +1                 TO ASC-BLK-SUB                   INKCS046
           END-IF.                                                      INKCS046
      /                                                                 INKCS046
      *----------------------------------------------------------------*   CL*17
      * DETERMINE IF A ROW EXISTS ON THE TINENTY TABLE FOR A STORE     *   CL*17
22000 * AND GET THE LATEST ENTRY DATE FOR REPORTING                    *   CL*17
      *----------------------------------------------------------------*   CL*17
       4500-GET-TINENTY.                                                   CL*17
                                                                           CL*17
W26600*        MOVE INVPAR-STORE-NBR TO PV-NUM-STORE.                      CL*17
W26600         MOVE INVPAR-LOC-NBR   TO INENTY-STR-NBR.                    CL*17
W26600*        MOVE PV-NUM-STORE     TO INENTY-STR-NBR.                    CL*17
                                                                           CL*17
               EXEC SQL                                                    CL*17
22000              SELECT MAX(DATE(LAST_UPD_TMST))                         CL*17
22000              INTO  :PV-SQL-TINENTY-DATE-CHECK                        CL*17
                   FROM  TINENTY                                           CL*17
                   WHERE STR_NBR = :INENTY-STR-NBR                         CL*17
               END-EXEC.                                                   CL*17
                                                                           CL*17
               EVALUATE TRUE                                               CL*17
                   WHEN SQLCODE = ZERO                                     CL*17
22000                  SET PS-TINENTY-FOUND TO TRUE                        CL*17
22000              WHEN SQLCODE = -305                                     CL*17
22000                  MOVE SPACES TO PV-SQL-TINENTY-DATE-CHECK                 
22000                  SET PS-TINENTY-NOT-FOUND TO TRUE                    CL*17
22000              WHEN SQLCODE = +100                                     CL*17
22000                  MOVE SPACES TO PV-SQL-TINENTY-DATE-CHECK                 
22000                  SET PS-TINENTY-NOT-FOUND TO TRUE                    CL*17
                   WHEN SQLWARN0 NOT = SPACES                              CL*17
                   WHEN SQLCODE NOT EQUAL ZERO                             CL*17
                       MOVE '4500-GET-TINENTY'                             CL*17
                                         TO DP013-PARAGRAPH                CL*17
22000                  MOVE 'CHECK/GET TINENTY DATE'                       CL*17
                                         TO DP013-MESSAGE-TEXT (1)         CL*17
                       MOVE SQLCA        TO DP013-SQLCA                    CL*17
                       MOVE 'TINENTY'    TO DP013-DB2-TABLE-NAME (1)       CL*17
                       SET DP013-DB2-ABEND                                 CL*17
                           DP013-XCTL-DISPLAY-RESTART                      CL*17
                                         TO TRUE                           CL*17
                       PERFORM DP013-0000-PROCESS-ABEND                    CL*17
               END-EVALUATE.                                               CL*17
      /                                                                 INKCS046
      *----------------------------------------------------------------*   CL*17
      * DETERMINE IF A ROW EXISTS ON THE TINVDTL TABLE FOR A STORE     *   CL*17
      *----------------------------------------------------------------*   CL*17
                                                                           CL*17
       4600-GET-TINVDTL.                                                   CL*17
                                                                           CL*17
               EXEC SQL                                                    CL*17
                   SELECT '1'                                              CL*17
                   INTO  :PV-LITERAL                                       CL*17
                   FROM  TINVDTL                                           CL*17
W26600             WHERE LOC_NBR         = :INVPAR-LOC-NBR                 CL*17
22960                AND INVALID_REC_IND = 'Y'                                  
               END-EXEC.                                                   CL*17
W26600*            WHERE STORE_NBR       = :INVPAR-STORE-NBR               CL*17
                                                                           CL*17
               EVALUATE TRUE                                               CL*17
                   WHEN SQLCODE = ZERO                                     CL*17
                   WHEN SQLCODE = -811                                     CL*17
                       SET PS-TINVDTL-FOUND TO TRUE                        CL*17
                   WHEN SQLCODE = +100                                     CL*17
                       SET PS-TINVDTL-NOT-FOUND TO TRUE                    CL*17
                   WHEN SQLWARN0 NOT = SPACES                              CL*17
                   WHEN SQLCODE NOT EQUAL ZERO                             CL*17
                       MOVE '4600-GET-TINVDTL'                             CL*17
                                         TO DP013-PARAGRAPH                CL*17
                       MOVE 'SELECT TINVDTL COUNT'                         CL*17
                                         TO DP013-MESSAGE-TEXT (1)         CL*17
                       MOVE SQLCA        TO DP013-SQLCA                    CL*17
                       MOVE 'TINVDTL'    TO DP013-DB2-TABLE-NAME (1)       CL*17
                       SET DP013-DB2-ABEND                                 CL*17
                           DP013-XCTL-DISPLAY-RESTART                      CL*17
                                         TO TRUE                           CL*17
                       PERFORM DP013-0000-PROCESS-ABEND                    CL*17
               END-EVALUATE.                                               CL*17
      /                                                                 INKCS046
      *----------------------------------------------------------------*   CL*17
      * DETERMINE IF A ROW EXISTS ON THE TSTKRMW TABLE FOR A STORE     *   CL*17
      *----------------------------------------------------------------*   CL*17
                                                                           CL*17
JS0400 4700-GET-TSTKRMW.                                                   CL*17
                                                                           CL*17
W26600*        MOVE INVPAR-STORE-NBR TO PV-NUM-STORE.                      CL*17
W26600*        MOVE PV-NUM-STORE     TO STKRMW-STORE-NBR.                  CL*17
W26600         MOVE INVPAR-LOC-NBR   TO STKRMW-LOC-NBR                     CL*17
                                                                           CL*17
               EXEC SQL                                                    CL*17
                   SELECT '1'                                              CL*17
                   INTO  :PV-LITERAL                                       CL*17
                   FROM  TSTKRMW                                           CL*17
                   WHERE LOC_NBR = :STKRMW-LOC-NBR                         CL*17
               END-EXEC.                                                   CL*17
W26600*            WHERE STORE_NBR = :STKRMW-STORE-NBR                     CL*17
                                                                           CL*17
               EVALUATE TRUE                                               CL*17
                   WHEN SQLCODE = ZERO                                     CL*17
                   WHEN SQLCODE = -811                                     CL*17
                       SET PS-TSTKRMW-FOUND TO TRUE                        CL*17
                   WHEN SQLCODE = +100                                     CL*17
                       SET PS-TSTKRMW-NOT-FOUND TO TRUE                    CL*17
                   WHEN SQLWARN0 NOT = SPACES                              CL*17
                   WHEN SQLCODE NOT EQUAL ZERO                             CL*17
                       MOVE '4700-GET-TSTKRMW'                             CL*17
                                         TO DP013-PARAGRAPH                CL*17
                       MOVE 'SELECT TSTKRMW COUNT'                         CL*17
                                         TO DP013-MESSAGE-TEXT (1)         CL*17
                       MOVE SQLCA        TO DP013-SQLCA                    CL*17
                       MOVE 'TSTKRMW'    TO DP013-DB2-TABLE-NAME (1)       CL*17
                       SET DP013-DB2-ABEND                                 CL*17
                           DP013-XCTL-DISPLAY-RESTART                      CL*17
                                         TO TRUE                           CL*17
                       PERFORM DP013-0000-PROCESS-ABEND                    CL*17
               END-EVALUATE.                                               CL*17
      /                                                                    CL*17
22000 *----------------------------------------------------------------*   CL*17
22000 * DETERMINE IF A ROW EXISTS ON THE TADJDTL TABLE FOR A STORE     *   CL*17
22000 * AND GET THE LATEST ENTRY DATE FOR REPORTING                    *   CL*17
22000 *----------------------------------------------------------------*   CL*17
22000  4800-GET-TADJDTL.                                                   CL*17
22000                                                                      CL*17
22000 *        MOVE INVPAR-STORE-NBR TO PV-NUM-STORE.                      CL*17
22000 *        MOVE PV-NUM-STORE     TO ADJDTL-STORE-NBR.                  CL*17
W26600         MOVE INVPAR-LOC-NBR   TO ADJDTL-LOC-NBR.                    CL*17
22000                                                                      CL*17
22000          EXEC SQL                                                    CL*17
22000              SELECT MAX(DATE(CRE_TMST))                              CL*17
22000              INTO  :PV-SQL-TADJDTL-DATE-CHECK                        CL*17
22000              FROM  TADJDTL                                           CL*17
W26600             WHERE LOC_NBR = :ADJDTL-LOC-NBR                         CL*17
22000                  AND ADJ_TYPE_CDE = 'SA'                                  
22000          END-EXEC.                                                   CL*17
22000 *            WHERE STORE_NBR = :ADJDTL-STORE-NBR                     CL*17
22000                                                                      CL*17
22000          EVALUATE TRUE                                               CL*17
22000              WHEN SQLCODE = ZERO                                     CL*17
22000                  CONTINUE                                            CL*17
22000              WHEN SQLCODE = -305                                     CL*17
22000                  MOVE SPACES TO PV-SQL-TADJDTL-DATE-CHECK            CL*17
22000              WHEN SQLCODE = +100                                     CL*17
22000                  MOVE SPACES TO PV-SQL-TADJDTL-DATE-CHECK            CL*17
22000              WHEN SQLWARN0 NOT = SPACES                              CL*17
22000              WHEN SQLCODE NOT EQUAL ZERO                             CL*17
22000                  MOVE '4800-GET-TADJDTL'                             CL*17
22000                                    TO DP013-PARAGRAPH                CL*17
22000                  MOVE 'CHECK/GET TADJDTL DATE'                       CL*17
22000                                    TO DP013-MESSAGE-TEXT (1)         CL*17
22000                  MOVE SQLCA        TO DP013-SQLCA                    CL*17
22000                  MOVE 'TADJDTL'    TO DP013-DB2-TABLE-NAME (1)       CL*17
22000                  SET DP013-DB2-ABEND                                 CL*17
22000                      DP013-XCTL-DISPLAY-RESTART                      CL*17
22000                                    TO TRUE                           CL*17
22000                  PERFORM DP013-0000-PROCESS-ABEND                    CL*17
22000          END-EVALUATE.                                               CL*17
22000                                                                   INKCS046
      *----------------------------------------------------------------*INKCS046
      *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *INKCS046
      *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
       5100-WRITE-TEMP-STORAGE.                                         INKCS046
                                                                        INKCS046
           MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                     INKCS046
                                         TO PV-LIST-ITEM-NUMBER.        INKCS046
           COMPUTE PV-BLOCK-NUMBER =                                    INKCS046
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS046
                                                                        INKCS046
           IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)             INKCS046
           OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)             INKCS046
               IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      INKCS046
                   PERFORM 5105-REWRITE-ASC-LISTQ                       INKCS046
               END-IF                                                   INKCS046
           ELSE                                                         INKCS046
               COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                       INKCS046
               ASC-HIGHEST-BLOCK-WRITTEN + 1                            INKCS046
               PERFORM 5110-WRITE-ASC-LISTQ                             INKCS046
               ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN   INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS046
                                                                        INKCS046
      ******************************************************************INKCS046
      ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **INKCS046
      ** SPECIFIC COMMAREA.                                           **INKCS046
      ******************************************************************INKCS046
                                                                        INKCS046
       5105-REWRITE-ASC-LISTQ.                                          INKCS046
                                                                        INKCS046
           EXEC CICS                                                    INKCS046
               WRITEQ TS                                                INKCS046
                   QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS046
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS046
                   LENGTH (LENGTH OF ASC-BLOCK-ENTRIES(ASC-BLK-SUB))            
                   ITEM  (PV-BLOCK-NUMBER)                              INKCS046
                   REWRITE                                              INKCS046
                   RESP  (PV-CICS-RESP)                                 INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
           IF  PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS046
               CONTINUE                                                 INKCS046
           ELSE                                                         INKCS046
               SET DP013-CICS-ABEND            TO TRUE                  INKCS046
               SET DP013-NO-ROLLBACK           TO TRUE                  INKCS046
               MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH       INKCS046
               MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'     INKCS046
                                               TO DP013-MESSAGE-TEXT(1) INKCS046
               PERFORM DP013-0000-PROCESS-ABEND                         INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      ******************************************************************INKCS046
      ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **INKCS046
      ** SPECIFIC COMMAREA.                                           **INKCS046
      ******************************************************************INKCS046
                                                                        INKCS046
       5110-WRITE-ASC-LISTQ.                                            INKCS046
                                                                        INKCS046
           EXEC CICS                                                    INKCS046
               WRITEQ TS                                                INKCS046
                   QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS046
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS046
                   LENGTH (LENGTH OF ASC-BLOCK-ENTRIES(ASC-BLK-SUB))            
                   ITEM  (PV-BLOCK-NUMBER)                              INKCS046
                   RESP  (PV-CICS-RESP)                                 INKCS046
           END-EXEC.                                                    INKCS046
           IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                      INKCS046
               SET DP013-CICS-ABEND      TO TRUE                        INKCS046
               SET DP013-NO-ROLLBACK     TO TRUE                        INKCS046
               MOVE '5110-WRITE-ASC-LISTQ'                              INKCS046
                                         TO DP013-PARAGRAPH             INKCS046
               MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           INKCS046
                                         TO DP013-MESSAGE-TEXT(1)       INKCS046
               PERFORM DP013-0000-PROCESS-ABEND                         INKCS046
           END-IF.                                                      INKCS046
                                                                        INKCS046
      ******************************************************************INKCS046
      ** DELETE THE LIST TEMP STORAGE QUEUE.                          **INKCS046
      ******************************************************************INKCS046
                                                                        INKCS046
       5120-DELETE-ASC-LISTQ.                                           INKCS046
                                                                        INKCS046
           EXEC CICS                                                    INKCS046
                DELETEQ TS                                              INKCS046
                   QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS046
                   RESP  (PV-CICS-RESP)                                 INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
           IF (PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS046
           OR  PV-CICS-RESP = DFHRESP (QIDERR))                         INKCS046
               CONTINUE                                                 INKCS046
           ELSE                                                         INKCS046
               SET DP013-CICS-ABEND      TO TRUE                        INKCS046
               SET DP013-NO-ROLLBACK     TO TRUE                        INKCS046
               MOVE '5120-DELETE-ASC-LISTQ'                             INKCS046
                                         TO DP013-PARAGRAPH             INKCS046
               MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           INKCS046
                                         TO DP013-MESSAGE-TEXT(1)       INKCS046
               PERFORM DP013-0000-PROCESS-ABEND                         INKCS046
           END-IF.                                                      INKCS046
      /                                                                 INKCS046
22000 ******************************************************************        
22000 ** FORMAT AND SUBMIT BATCH JOB.                                 **        
22000 ******************************************************************        
22000  6000-FORMAT-AND-SUBMIT.                                                  
22000                                                                           
22000      PERFORM 9500-ENQUEUE-INTERNAL-READER.                                
22000                                                                           
22000      EXEC CICS                                                            
22000        ASSIGN                                                             
22000          APPLID (PV-CICS-ID)                                              
22000      END-EXEC.                                                            
22000                                                                           
22000      IF PV-CICS-REGION-PRODUCTION                                         
22000          MOVE 'INK309'     TO JL-JCL-INK309-JOBNAME                       
22000          MOVE 'Q'          TO JL-JCL-INK309-JOBCLASS                      
22000          MOVE 'P'          TO JL-JCL-INK309-MSGCLASS                      
22000          MOVE SPACES       TO JL-JCL-INK309-NOTIFY-OPT                    
22000      ELSE                                                                 
22000          MOVE DP020-USERID TO JL-JCL-INK309-JOBNAME                       
22000                               JL-JCL-TEST-USER                            
22000          MOVE 'P'          TO JL-JCL-INK309-JOB-UNQ                       
22000          MOVE 'A'          TO JL-JCL-INK309-JOBCLASS                      
22000          MOVE 'X'          TO JL-JCL-INK309-MSGCLASS                      
22000          MOVE JL-JCL-TEST-NOTIFY TO JL-JCL-INK309-NOTIFY-OPT              
22000      END-IF.                                                              
22000      MOVE JL-JCL-INK309-LINE-1 TO TD-JCL-LINE.                            
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-LINE-2 TO TD-JCL-LINE.                            
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-LINE-3 TO TD-JCL-LINE.                            
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-SHORT     TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-LONG      TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-SHORT     TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      PERFORM DP017-0000-GET-CURR-DATE-TIME.                               
22000      MOVE DP017-EDITED-DATE-MMDDYYYY                                      
22000          TO JL-JCL-INK309-CMT-SUBDATE                                     
22000      MOVE DP017-EDITED-TIME-HMS                                           
22000          TO JL-JCL-INK309-CMT-SUBTIME                                     
22000      MOVE DP020-USERID TO JL-JCL-INK309-CMT-USRID                         
22000      MOVE PV-CICS-ID   TO JL-JCL-INK309-CMT-REGION.                       
22000      MOVE JL-JCL-INK309-CMT-USER-INFO TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-SHORT     TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-1         TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-2         TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-3         TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-SHORT     TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      EVALUATE TRUE                                                        
22000          WHEN ASC-ALL-LOC-INQ                                             
22000              MOVE PC-PRINT-LOCATION                                       
22000                  TO JL-JCL-INK309-REQ-OPTION                              
22000          WHEN ASC-SCANNED-INV-NP                                          
22000              MOVE PC-PRINT-SCANNED-INV                                    
22000                  TO JL-JCL-INK309-REQ-OPTION                              
22000          WHEN ASC-ONLINE-SHEETS-NP                                        
22000              MOVE PC-PRINT-SHEETS                                         
22000                  TO JL-JCL-INK309-REQ-OPTION                              
22000          WHEN ASC-INVALIDS-NOT-CORR                                       
22000              MOVE PC-PRINT-INVALIDS                                       
22000                  TO JL-JCL-INK309-REQ-OPTION                              
22000          WHEN ASC-STCKRM-WITHDRWL-NE                                      
22000              MOVE PC-PRINT-STOCKROOM                                      
22000                  TO JL-JCL-INK309-REQ-OPTION                              
22000          WHEN ASC-SALES-ADJUSTS-NC                                        
22000              MOVE PC-PRINT-SALES-ADJUSTMENT                               
22000                  TO JL-JCL-INK309-REQ-OPTION                              
22000          WHEN ASC-UNIT-BOOKINGS-NC                                        
22000              MOVE PC-PRINT-BOOKING-NOT-CPLD                               
22000                  TO JL-JCL-INK309-REQ-OPTION                              
22000          WHEN ASC-UNIT-BOOKINGS-COMPLET                                   
22000              MOVE PC-PRINT-BOOKING-CPLD                                   
22000                  TO JL-JCL-INK309-REQ-OPTION                              
22000      END-EVALUATE.                                                        
W26600     MOVE DP020-USERID                TO JL-REQ-USERID                    
22000      MOVE JL-JCL-INK309-LINE-14       TO JL-JCL-INK309-CMT-OPTION.        
22000      MOVE JL-JCL-INK309-CMT-4         TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-SHORT     TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-LONG      TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-SHORT     TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      IF PV-CICS-REGION-PRODUCTION                                         
22000          MOVE JL-JCL-INK309-LINE-10       TO TD-JCL-LINE                  
22000          PERFORM 8000-WRITE-TD-LINE                                       
22000      ELSE                                                                 
22000          MOVE JL-JCL-TEST-LINE-10         TO TD-JCL-LINE                  
22000          PERFORM 8000-WRITE-TD-LINE                                       
22000          MOVE JL-JCL-INK309-CMT-SHORT     TO TD-JCL-LINE                  
22000          PERFORM 8000-WRITE-TD-LINE                                       
22000                                                                           
22000          MOVE JL-JCL-TEST-LINE-11A        TO TD-JCL-LINE                  
22000          PERFORM 8000-WRITE-TD-LINE                                       
22000          MOVE JL-JCL-TEST-LINE-11B        TO TD-JCL-LINE                  
22000          PERFORM 8000-WRITE-TD-LINE                                       
22000          MOVE JL-JCL-TEST-LINE-11C        TO TD-JCL-LINE                  
22000          PERFORM 8000-WRITE-TD-LINE                                       
22000      END-IF.                                                              
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-SHORT     TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-LINE-11       TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-SHORT     TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      EXEC SQL                                                             
22000          SELECT DFLT_PRTR_ID                                              
22000          INTO  :USER-DFLT-PRTR-ID                                         
22000          FROM  TUSER                                                      
22000          WHERE USER_CODE = :DP020-USERID                                  
22000      END-EXEC.                                                            
22000      MOVE 'LOCAL'           TO JL-JCL-INK309-PRTDEST.                     
22000      EVALUATE TRUE                                                        
22000          WHEN SQLCODE = ZERO                                              
22000              IF USER-DFLT-PRTR-ID NOT = SPACES                            
22000                  MOVE USER-DFLT-PRTR-ID TO JL-JCL-INK309-PRTDEST          
22000              END-IF                                                       
22000          WHEN SQLCODE = +100                                              
22000              CONTINUE                                                     
22000          WHEN OTHER                                                       
22000              MOVE '6000-FORMAT-AND-SUBMIT'                                
22000                                TO DP013-PARAGRAPH                         
22000              MOVE 'SELECT DFLT-PRT-ID'                                    
22000                                TO DP013-MESSAGE-TEXT (1)                  
22000              MOVE SQLCA        TO DP013-SQLCA                             
22000              MOVE 'TUSER  '    TO DP013-DB2-TABLE-NAME (1)                
22000              SET DP013-DB2-ABEND                                          
22000                  DP013-XCTL-DISPLAY-RESTART                               
22000                                TO TRUE                                    
22000              PERFORM DP013-0000-PROCESS-ABEND                             
22000      END-EVALUATE.                                                        
22000                                                                           
22000      MOVE JL-JCL-INK309-LINE-12       TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-LINE-13       TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-LINE-14       TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-LINE-EOD      TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      MOVE JL-JCL-INK309-CMT-SHORT     TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22960      MOVE JL-JCL-INK309-LINE-EOF      TO TD-JCL-LINE.                     
22000      PERFORM 8000-WRITE-TD-LINE.                                          
22000                                                                           
22000      PERFORM 9600-DEQUEUE-INTERNAL-READER.                                
22000                                                                           
22000 ******************************************************************        
22000 *  WRITE A JCL LINE TO THE TRANSIENT DATA QUEUE FOR THE INTERNAL *        
22000 *  READER.  ON ANY ERROR, DEQUEUE THE INTERNAL READER AND ABEND. *        
22000 ******************************************************************        
22000  8000-WRITE-TD-LINE.                                                      
22000      EXEC CICS                                                            
22000          WRITEQ TD                                                        
22000              QUEUE(TD-QUEUE-NAME)                                         
22000              FROM(TD-JCL-LINE)                                            
22000              LENGTH(TD-JCL-LINE-LENGTH)                                   
22000              RESP(PV-CICS-RESP)                                           
22000      END-EXEC.                                                            
22000                                                                           
22000      IF PV-CICS-RESP NOT = DFHRESP(NORMAL)                                
22000          SET DP013-CICS-ABEND      TO TRUE                                
22000          SET DP013-NO-ROLLBACK     TO TRUE                                
22000          MOVE '8000-WRITE-TD-LINE' TO DP013-PARAGRAPH                     
22000          MOVE 'ATTEMPTING TO WRITE TO INTERNAL READER TD QUEUE'           
22000                                    TO DP013-MESSAGE-TEXT(1)               
22000          PERFORM 9600-DEQUEUE-INTERNAL-READER                             
22000          PERFORM DP013-0000-PROCESS-ABEND                                 
22000      END-IF.                                                              
22000                                                                           
22000 ******************************************************************        
22000 *  ENQUEUE THE TRANSIENT DATA QUEUE FOR THE INTERNAL READER.     *        
22000 *  THIS WILL PREVENT INTERLEAVING OF JCL SUBMITTED CONCURRENTLY  *        
22000 *  BY MULTIPLE USERS.  IF THE QUEUE IS BUSY, THIS PROGRAM WILL   *        
22000 *  WAIT TO ENQUEUE IT, BY NOT SPECIFICALLY HANDLING THE QBUSY    *        
22000 *  CONDITION.                                                    *        
22000 ******************************************************************        
22000  9500-ENQUEUE-INTERNAL-READER.                                            
22000      EXEC CICS                                                            
22000          ENQ                                                              
22000              RESOURCE(TD-QUEUE-NAME)                                      
22000              LENGTH(TD-QUEUE-NAME-LENGTH)                                 
22000              RESP(PV-CICS-RESP)                                           
22000      END-EXEC.                                                            
22000                                                                           
22000      IF PV-CICS-RESP NOT = DFHRESP(NORMAL)                                
22000          SET DP013-CICS-ABEND      TO TRUE                                
22000          SET DP013-NO-ROLLBACK     TO TRUE                                
22000          MOVE '9500-ENQUEUE-INTERNAL-READER'                              
22000                                    TO DP013-PARAGRAPH                     
22000          STRING 'ENQUEUE '            DELIMITED BY SIZE                   
22000                 TD-QUEUE-NAME         DELIMITED BY SIZE                   
22000                 ' (INTERNAL READER)'  DELIMITED BY SIZE                   
22000              INTO DP013-MESSAGE-TEXT (1)                                  
22000          PERFORM DP013-0000-PROCESS-ABEND                                 
22000      END-IF.                                                              
22000                                                                           
22000      SET PS-INTERNAL-READER-ENQUEUED TO TRUE.                             
22000                                                                           
22000 ******************************************************************        
22000 *  DEQUEUE THE TRANSIENT DATA QUEUE FOR THE INTERNAL READER.     *        
22000 *  THIS SHOULD BE DONE FOLLOWING A SUCCESSFUL REPORT REQUEST     *        
22000 *  SUBMISSION, A CANCELLATION OF THE CURRENT REPORT REQUEST, OR  *        
22000 *  ANY DETECTED ABENDS.  THIS WILL FREE UP THE INTERNAL READER   *        
22000 *  FOR THE NEXT USER.                                            *        
22000 ******************************************************************        
22000  9600-DEQUEUE-INTERNAL-READER.                                            
22000      SET PS-INTERNAL-READER-DEQUEUED TO TRUE.                             
22000                                                                           
22000      EXEC CICS                                                            
22000          DEQ                                                              
22000              RESOURCE(TD-QUEUE-NAME)                                      
22000              LENGTH(TD-QUEUE-NAME-LENGTH)                                 
22000              RESP(PV-CICS-RESP)                                           
22000      END-EXEC.                                                            
22000                                                                           
22000      IF PV-CICS-RESP NOT = DFHRESP(NORMAL)                                
22000          SET DP013-CICS-ABEND      TO TRUE                                
22000          SET DP013-NO-ROLLBACK     TO TRUE                                
22000          MOVE '9600-DEQUEUE-INTERNAL-READER'                              
22000                                    TO DP013-PARAGRAPH                     
22000          STRING 'DEQUEUE '            DELIMITED BY SIZE                   
22000                 TD-QUEUE-NAME         DELIMITED BY SIZE                   
22000                 ' (INTERNAL READER)'  DELIMITED BY SIZE                   
22000              INTO DP013-MESSAGE-TEXT (1)                                  
22000          PERFORM DP013-0000-PROCESS-ABEND                                 
22000      END-IF.                                                              
22000                                                                           
22000 *----------------------------------------------------------------*INKCS046
22000 *DP017-0000-GET-CURR-DATE-TIME                                   *INKCS046
22000 *----------------------------------------------------------------*INKCS046
22000      COPY DPPD017.                                                INKCS046
22000                                                                   INKCS046
      *----------------------------------------------------------------*INKCS046
      *    ABEND PROCESSOR MODULE                                      *INKCS046
      *----------------------------------------------------------------*INKCS046
                                                                        INKCS046
           COPY DPPD013.                                                INKCS046
