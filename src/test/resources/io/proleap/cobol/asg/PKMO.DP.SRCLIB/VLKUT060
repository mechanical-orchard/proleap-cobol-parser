000100 TITLE 'CN AUDIT FAILURE ANALYSIS EXTRACT'.                               
000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    VLKUT060.                                                 
000400 AUTHOR.        MIKE FRIESS.                                              
000500 DATE-WRITTEN.  SEPTEMBER 2010.                                           
000600 ENVIRONMENT DIVISION.                                                    
000700 INPUT-OUTPUT SECTION.                                                    
000800                                                                          
001600******************************************************************        
000800                                                                          
000900* THIS PROGRAM CN AUDIT FAILURE ANALYSIS EXTRACT.                         
      * /*VSH - CN FILE LAYOUT CHANGES                                          
001000* THE PROGRAM WAS MODIFIED TO COMPARE THE CORRECT QTY AGAINST             
      * RFID TAGGED QTY WHICH GETS MODFIED FOR DIFFERENT SCENARIOS              
      * SUCH RFID ONLY, COMBO AUDIT AND RFID AUDIT.                             
001100* */VSH - CN FILE LAYOUT CHANGES                                          
001600******************************************************************        
000900 FILE-CONTROL.                                                            
001000     SELECT VEND-VIOLATION-INPUT-FILE   ASSIGN TO INP01.                  
001100     SELECT AUD-EXTRACT-OUTPUT-FILE     ASSIGN TO OUT01.                  
001200                                                                          
001300 DATA DIVISION.                                                           
001400 FILE SECTION.                                                            
001500                                                                          
001600                                                                          
001700 FD  VEND-VIOLATION-INPUT-FILE                                            
001800     RECORDING MODE IS F                                                  
001900     LABEL RECORDS ARE STANDARD                                           
002000     RECORD CONTAINS 150                                                  
002100     BLOCK CONTAINS 0 RECORDS                                             
002200     DATA RECORD IS VEND-VIOLATION-INPUT-RECORD.                          
002300 01  VEND-VIOLATION-INPUT-RECORD      PIC X(150).                         
002400                                                                          
002500 FD  AUD-EXTRACT-OUTPUT-FILE                                              
002600     RECORDING MODE IS F                                                  
002700     LABEL RECORDS ARE STANDARD                                           
002800     RECORD CONTAINS 206                                                  
002900     BLOCK CONTAINS 0 RECORDS                                             
003000     DATA RECORD IS AUD-EXTRACT-OUTPUT-RECORD.                            
003100 01  AUD-EXTRACT-OUTPUT-RECORD        PIC X(206).                         
003200                                                                          
003300 WORKING-STORAGE SECTION.                                                 
003400                                                                          
003500 01  PV-ABEND-CODE                    PIC S9(04)   VALUE ZERO             
003600                                                   COMP SYNC.             
003700                                                                          
003800 01  PS-PROGRAM-SWITCHES.                                                 
003900     05  PS-END-EXTRACT-SW            PIC X(1)   VALUE 'N'.               
004000         88  PS-END-EXTRACT                      VALUE 'Y'.               
004300                                                                          
004400                                                                          
004500 01  PV-PROGRAM-VARIABLES.                                                
004700                                                                          
004800     05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
004900     05  PV-ERROR-MESSAGE-1           PIC X(60)   VALUE SPACES.           
005000     05  PV-ERROR-MESSAGE-2           PIC X(60)   VALUE SPACES.           
005010     05  PV-TMST                      PIC X(26)   VALUE SPACES.           
005100     05  PV-SQLCODE                   PIC 9(9)    VALUE ZERO.             
005700     05  PV-DISP-NBR                  PIC Z(8)9.                          
006200     05  PV-AUDIT-DURATION            PIC S9(9) COMP-3 VALUE ZERO.        
006250     05  PV-PREV-PO-NBR               PIC  9(9) VALUE ZERO.               
006251                                                                          
006252 01  PV-TRIM-ZERO-FIELDS.                                                 
006253     05  PV-COUNTER                  PIC S9(04)    VALUE +0               
006254                                                      COMP SYNC.          
006260     05  PV-TRIM-ZEROS               PIC S9(04)    VALUE +0               
006270                                                      COMP SYNC.          
006271     05  PV-MOVE-LENGTH              PIC S9(04)    VALUE +0               
006272                                                      COMP SYNC.          
006273     05  PV-INPUT-FLD-LENGTH         PIC S9(04)    VALUE +0               
006274                                                      COMP SYNC.          
006291     05  PV-WRK-TRIM-FLD             PIC X(20)     VALUE SPACES.          
006300                                                                          
006400 01  PV-COUNTERS COMP-3.                                                  
006500     05  PV-READ-COUNT                PIC S9(9)    VALUE ZERO.            
006600     05  PV-WRITE-COUNT               PIC S9(9)    VALUE ZERO.            
006700     05  PV-SKIP-INC-COUNT            PIC S9(9)    VALUE ZERO.            
006800                                                                          
006900 01  PC-PROGRAM-CONSTANTS.                                                
007000     05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'VLKUT060'.        
007100                                                                          
007300 01  PV-ABEND-FIELDS.                                                     
007400     05  PV-ABEND-PROGRAM             PIC X(8)   VALUE 'VLKUT060'.        
007500     05  PV-ABEND-PARAGRAPH           PIC X(50)  VALUE SPACES.            
007600     05  PV-ABEND-OPERATION           PIC X(50)  VALUE SPACES.            
007700     05  PV-ABEND-STRUCTURE-1         PIC X(15)  VALUE SPACES.            
007800     05  PV-ABEND-STRUCTURE-2         PIC X(15)  VALUE SPACES.            
007900     05  PV-ABEND-STRUCTURE-3         PIC X(15)  VALUE SPACES.            
008000     05  PV-ABEND-STATUS              PIC XX     VALUE SPACES.            
008100     05  PV-DB2-OPERATION-MESSAGE-1   PIC X(79)  VALUE SPACES.            
008200     05  PV-DB2-OPERATION-MESSAGE-2   PIC X(79)  VALUE SPACES.            
008300     05  PV-DB2-OPERATION-MESSAGE-3   PIC X(79)  VALUE SPACES.            
008400     05  PV-DB2-OPERATION-MESSAGE-4   PIC X(79)  VALUE SPACES.            
008500                                                                          
008600/                                                                         
008700*    VENDOR VIOLATION EXTRACT INPUT                                       
008900     COPY SURD033.                                                        
009000                                                                          
009100*    AUDIT EXTRACT OUTPUT                                                 
009200     COPY VLRD068.                                                        
009300                                                                          
010300*  USED FOR DB2 ERROR MESSAGE BUILDING                                    
010400     COPY DPWS004.                                                        
010500                                                                          
010600*   DB2 SQL COMMUNICATION AREA                                            
010700     EXEC SQL                                                             
010800          INCLUDE SQLCA                                                   
010900     END-EXEC.                                                            
010901                                                                          
010910*---------------------------------------------------------------*         
010920*    DB2 AREA FOR THE PURCHASE ORDER TABLE                      *         
010930*---------------------------------------------------------------*         
010940     EXEC SQL                                                             
010950          INCLUDE TPURCHS                                                 
010960     END-EXEC.                                                            
010970                                                                          
011000/                                                                         
011100 PROCEDURE DIVISION.                                                      
011200                                                                          
011300     PERFORM 1000-INIT.                                                   
011400                                                                          
011500     PERFORM 2000-PROCESS UNTIL PS-END-EXTRACT.                           
011600                                                                          
011700     PERFORM 9000-TERMINATE.                                              
011800                                                                          
011900     GOBACK.                                                              
012000                                                                          
012010********************************************************                  
012100 1000-INIT.                                                               
012110********************************************************                  
012200                                                                          
012300     OPEN INPUT  VEND-VIOLATION-INPUT-FILE                                
012400          OUTPUT AUD-EXTRACT-OUTPUT-FILE                                  
012500                                                                          
012604                                                                          
012610     EXEC SQL                                                             
012620         SET :PV-TMST = CURRENT TIMESTAMP                                 
012630     END-EXEC                                                             
012700                                                                          
012800     PERFORM 8000-READ-INPUT-AUDIT-REC.                                   
012900                                                                          
012910********************************************************                  
013000 2000-PROCESS.                                                            
013010********************************************************                  
013100                                                                          
013511     IF VL068-IN-PROCESS                                                  
013512       ADD 1 TO PV-SKIP-INC-COUNT                                         
013513     ELSE                                                                 
013514       PERFORM 2100-BUILD-EXTACT                                          
013515     END-IF.                                                              
013516                                                                          
013517     PERFORM 8000-READ-INPUT-AUDIT-REC.                                   
013518                                                                          
013519********************************************************                  
013520 2100-BUILD-EXTACT.                                                       
013521********************************************************                  
013522                                                                          
013523     INITIALIZE VL068-RECORD.                                             
013524                                                                          
013525*    *** KEEP THIS CODE TOGETHER ************************                 
013526     IF SU033-PO-NBR = PV-PREV-PO-NBR                                     
013527        CONTINUE                                                          
013528     ELSE                                                                 
013529        MOVE SU033-PO-NBR TO PV-PREV-PO-NBR                               
013530        PERFORM 2110-SELECT-PO-DATA                                       
013531     END-IF.                                                              
013532     MOVE PURCHS-DEPT-NBR            TO VL068-DEPT-NBR.                   
013533     MOVE PURCHS-PO-TYP-CDE          TO VL068-PO-TYP-CDE.                 
013534*    ****************************************************                 
013535                                                                          
013536     MOVE SU033-FISCAL-YEAR          TO VL068-DATE-CCYY.                  
013538     MOVE SU033-FISCAL-MONTH         TO VL068-DATE-MM.                    
013539                                                                          
013540     IF SU033-VENDOR-ID = ZERO                                            
013541       MOVE SPACES TO VL068-VENDOR-ID                                     
013542     ELSE                                                                 
013543       INITIALIZE PV-WRK-TRIM-FLD                                         
013544       MOVE SU033-VENDOR-ID TO PV-WRK-TRIM-FLD                            
013545       MOVE 9 TO PV-INPUT-FLD-LENGTH                                      
013546       INITIALIZE PV-TRIM-ZEROS                                           
013547       PERFORM 2120-TRIM-ZEROS                                            
013548       MOVE PV-WRK-TRIM-FLD(PV-COUNTER:PV-MOVE-LENGTH)                    
013549                         TO VL068-VENDOR-ID                               
013550     END-IF.                                                              
013551                                                                          
013552     MOVE SU033-PO-NBR               TO VL068-PO-NBR.                     
013553     MOVE SU033-PKGNG-TYPE           TO VL068-PKGNG-TYPE.                 
013554                                                                          
013555     IF SU033-RCVR-SEQ-NBR = ZERO                                         
013556       MOVE SPACES TO VL068-RCVR-SEQ-NBR                                  
013557     ELSE                                                                 
013558       INITIALIZE PV-WRK-TRIM-FLD                                         
013559       MOVE SU033-RCVR-SEQ-NBR TO PV-WRK-TRIM-FLD                         
013560       MOVE 9 TO PV-INPUT-FLD-LENGTH                                      
013561       INITIALIZE PV-TRIM-ZEROS                                           
013562       PERFORM 2120-TRIM-ZEROS                                            
013566       MOVE PV-WRK-TRIM-FLD(PV-COUNTER:PV-MOVE-LENGTH)                    
013567                         TO VL068-RCVR-SEQ-NBR                            
013568     END-IF.                                                              
013569                                                                          
013570     MOVE SU033-VENDOR-AUDIT-RANK    TO VL068-VENDOR-AUDIT-RANK.          
013571     MOVE SU033-AUD-CNTL-NBR         TO VL068-AUD-CNTL-NBR.               
013572     MOVE SU033-CRTNS-AUDITED        TO VL068-CRTNS-AUDITED.              
013573     MOVE SU033-CRTNS-W-PICK-ERR     TO VL068-CRTNS-W-PICK-ERR.           
013574     MOVE SU033-UNITS-AUDITED        TO VL068-UNITS-AUDITED.              
013575     MOVE SU033-UNITS-PICK-ERR       TO VL068-UNITS-PICK-ERR.             
013576     MOVE SU033-UNITS-PACK-ERR       TO VL068-UNITS-PACK-ERR.             
013577     MOVE SU033-PACKS-AUDITED        TO VL068-PACKS-AUDITED.              
013578     MOVE SU033-PACKS-W-PACK-ERR     TO VL068-PACKS-W-PACK-ERR.           
013580     MOVE SU033-FAILURE-ERROR-SW     TO VL068-FAILURE-ERROR-SW.           
013590     MOVE SU033-AUD-STAT-CDE         TO VL068-AUD-STAT-CDE.               
013591     IF SU033-PKGNG-TYPE = 'PP'                                           
013592       MOVE 1 TO VL068-UNITS-TICK-ERR                                     
      * /*VSH - CN FILE LAYOUT CHANGES                                          
           ELSE                                                                 
               IF SU033-PKGNG-TYPE = 'RF'                                       
                  MOVE SU033-UNITS-TICK-ERR TO VL068-UNITS-TICK-ERR             
               END-IF                                                           
001100* */VSH - CN FILE LAYOUT CHANGES                                          
013593     END-IF.                                                              
013594     MOVE 'X' TO VL068-ANCHOR-CONSTANT.                                   
013595                                                                          
013596     PERFORM 8100-WRITE-EXTRACT-REC.                                      
013700/                                                                         
013800********************************************************                  
013900 2110-SELECT-PO-DATA.                                                     
014000********************************************************                  
014010                                                                          
014020     MOVE SPACES TO PURCHS-DEPT-NBR                                       
014021                    PURCHS-PO-TYP-CDE.                                    
014030                                                                          
014300     EXEC SQL                                                             
014500          SELECT  DEPT_NBR                                                
014600                 ,PO_TYP_CDE                                              
015100            INTO :PURCHS-DEPT-NBR                                         
015200                ,:PURCHS-PO-TYP-CDE                                       
015700            FROM TPURCHS                                                  
016000           WHERE PO_NBR  = :SU033-PO-NBR                                  
016100             AND VER_STATUS_CDE = 'A'                                     
016700     END-EXEC.                                                            
016800                                                                          
016900     EVALUATE TRUE                                                        
017000          WHEN SQLCODE = ZERO                                             
017100              CONTINUE                                                    
017700                                                                          
017800***       WHEN SQLCODE = +100                                             
017900***           SET PS-DEPT-NOT-FOUND TO TRUE                               
017910                                                                          
018000          WHEN OTHER                                                      
018100              MOVE '2110-SELECT-PO-DATA'                                  
018200                                       TO PV-ABEND-PARAGRAPH              
018300              MOVE 'ERROR SELECTING PO - '                                
018400                             TO PV-DB2-OPERATION-MESSAGE-1                
018411              MOVE SU033-PO-NBR TO PV-DISP-NBR                            
018420              MOVE PV-DISP-NBR  TO PV-DB2-OPERATION-MESSAGE-2             
018500              MOVE 'TPURCHS'    TO PV-ABEND-STRUCTURE-1                   
018600              PERFORM 9900-DB2-ABEND                                      
018700      END-EVALUATE.                                                       
018800*                                                                         
018810********************************************************                  
018820 2120-TRIM-ZEROS.                                                         
018830********************************************************                  
018840                                                                          
018841     PERFORM VARYING PV-COUNTER FROM 1 BY 1                               
018843      UNTIL PV-WRK-TRIM-FLD(PV-COUNTER:1) > ZERO                          
018844*        *** SHOULD NEVER HAPPEN ***                                      
018845         OR PV-COUNTER > PV-INPUT-FLD-LENGTH                              
018846          ADD 1 TO PV-TRIM-ZEROS                                          
018847     END-PERFORM.                                                         
018849                                                                          
018850     SUBTRACT PV-TRIM-ZEROS FROM PV-INPUT-FLD-LENGTH                      
018851                                 GIVING PV-MOVE-LENGTH.                   
018860*                                                                         
018900********************************************************                  
026100 8000-READ-INPUT-AUDIT-REC.                                               
026200                                                                          
026300     READ VEND-VIOLATION-INPUT-FILE INTO                                  
026400           SU033-RECORD                                                   
026500     AT END                                                               
026600         SET PS-END-EXTRACT           TO TRUE                             
026700     NOT AT END                                                           
026800         ADD 1                        TO PV-READ-COUNT                    
026900     END-READ.                                                            
027000                                                                          
027010********************************************************                  
027100 8100-WRITE-EXTRACT-REC.                                                  
027200                                                                          
027300     ADD 1                         TO PV-WRITE-COUNT                      
027400                                                                          
027500     WRITE AUD-EXTRACT-OUTPUT-RECORD FROM VL068-RECORD.                   
027600/                                                                         
027610********************************************************                  
027700 9000-TERMINATE.                                                          
027800                                                                          
027900     CLOSE VEND-VIOLATION-INPUT-FILE                                      
028000           AUD-EXTRACT-OUTPUT-FILE                                        
028100                                                                          
028200     DISPLAY '************************'.                                  
028300     DISPLAY '******* VLKUT060 *******'.                                  
028400     DISPLAY '************************'.                                  
028500     DISPLAY SPACES                                                       
028600                                                                          
028700     MOVE PV-READ-COUNT               TO PV-DISP-NBR                      
028800     DISPLAY 'OLD AUDIT EXTRACTS READ      = ' PV-DISP-NBR                
028900                                                                          
029000     MOVE PV-WRITE-COUNT              TO PV-DISP-NBR                      
029100     DISPLAY 'SELECTED EXTRACTS WRITTEN    = ' PV-DISP-NBR                
029110     MOVE PV-SKIP-INC-COUNT           TO PV-DISP-NBR                      
029120     DISPLAY 'RECORDS SKIPPED              = ' PV-DISP-NBR                
029200                                                                          
029300     DISPLAY SPACES                                                       
029400     DISPLAY '************************'.                                  
029500                                                                          
029510********************************************************                  
029600 9900-DB2-ABEND.                                                          
029700                                                                          
029800     MOVE SQLCODE TO PV-SQLCODE.                                          
029900     DISPLAY '*** DB2 ERROR '.                                            
030000     DISPLAY '*** SQLCODE   = ' PV-SQLCODE.                               
030100     DISPLAY '*** PROGRAM   = ' PV-ABEND-PROGRAM.                         
030200     DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.                       
030300     DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.                       
030400     DISPLAY '*** MESSAGE   = ' PV-DB2-OPERATION-MESSAGE-1.               
030500     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-2.               
030600     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-3.               
030700     DISPLAY '*** TABLE 1   = ' PV-ABEND-STRUCTURE-1.                     
031000     DISPLAY '*** TABLE 2   = ' PV-ABEND-STRUCTURE-2                      
031200                                                                          
031300                                                                          
031400     COPY DPPD004.                                                        
031500                                                                          
031600     MOVE +4013                  TO PV-ABEND-CODE.                        
031700     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
031800                                                                          
031900*9200-ABEND.                                                              
032000******************************************************************        
032100*  PERFORMS A NON-SQL ABEND FOR THE PROGRAM                               
032200******************************************************************        
032300*    DISPLAY '***************************'.                               
032400*    DISPLAY '**       ABEND           **'.                               
032500*    DISPLAY '**  IN PROGRAM VLKUT060  **'.                               
032600*    DISPLAY '***************************'.                               
032700*    DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
032800*    DISPLAY '**                 ** = ' PV-ERROR-MESSAGE-1.               
032900*    DISPLAY '**                 ** = ' PV-ERROR-MESSAGE-2.               
033000*                                                                         
033100*    MOVE +4014                  TO PV-ABEND-CODE.                        
033200*    CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
033300                                                                          
