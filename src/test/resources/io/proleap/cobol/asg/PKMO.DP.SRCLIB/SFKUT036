       IDENTIFICATION DIVISION.                                         00010054
       PROGRAM-ID.    SFKUT036.                                         00020075
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030054
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040054
       DATE-WRITTEN.  05/16/2005.                                       00050054
       DATE-COMPILED.                                                   00060054
      ******************************************************************00070054
      **COMMON DATA LOOKUP                                              00080054
      ******************************************************************00090054
      **THIS PROGRAM WILL READ IN A FILE BY ITEM AND LOOK UP THE        00100070
      **     BUSINESS GROUP/DEPT/MAJ CLASS/SUB CLASS ASSOCIATED TO IT.  00110070
      ******************************************************************00120054
      **DATE:05/16/2005 MADE BY:JULIE TEWES INITIAL PROGRAM WRITE       00130054
      ******************************************************************00140054
       ENVIRONMENT DIVISION.                                            00150054
       CONFIGURATION SECTION.                                           00160054
       SOURCE-COMPUTER.    IBM-3090.                                    00170054
       OBJECT-COMPUTER.    IBM-3090.                                    00180054
       INPUT-OUTPUT SECTION.                                            00190054
       FILE-CONTROL.                                                    00200054
                                                                        00210054
           SELECT INPUT-FILE                                            00220054
                  ASSIGN TO INP01.                                      00230054
           SELECT OUTPUT-FILE                                           00240054
                  ASSIGN TO OUT01.                                      00250054
                                                                        00260054
       DATA DIVISION.                                                   00270054
       FILE SECTION.                                                    00280054
                                                                        00290054
       FD  INPUT-FILE                                                   00300054
           RECORDING MODE IS F                                          00310054
           LABEL RECORDS OMITTED                                        00320054
           BLOCK CONTAINS 0 RECORDS                                     00330054
           DATA RECORD IS INPUT-RECORD.                                 00340054
       01  INPUT-RECORD                        PIC X(050).              00350074
                                                                        00360054
       FD  OUTPUT-FILE                                                  00370054
           RECORDING MODE IS F                                          00380054
           LABEL RECORDS OMITTED                                        00390054
           BLOCK CONTAINS 0 RECORDS                                     00400054
           DATA RECORD IS OUTPUT-RECORD.                                00410054
       01  OUTPUT-RECORD                       PIC X(050).              00420074
                                                                        00430054
       WORKING-STORAGE SECTION.                                         00440054
      ******************************************************************00450054
      **PROGRAM SWITCHES                                                00460054
      ******************************************************************00470054
       01  PS-SWITCH-AREAS.                                             00480054
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)  VALUE 'N'.    00490054
               88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.    00500054
               88  PS-NOT-END-OF-INPUT-FILE               VALUE 'N'.    00510054
      ******************************************************************00520054
      **PROGRAM HOLDS                                                   00530054
      ******************************************************************00540054
       01  PH-PROGRAM-HOLDS.                                            00550054
           05  PH-INTR-UPC-ID                  PIC S9(09) COMP          00560070
                                                         VALUE -1.      00570081
      ******************************************************************00580054
      **DATE TIME FIELDS                                                00590054
      ******************************************************************00600054
       01 DF-DATE-TIME-FIELDS.                                          00610054
          05 DF-ACCEPT-DATE                    PIC X(06) VALUE SPACES.  00620054
          05 DF-ACCEPT-TIME                    PIC X(08) VALUE SPACES.  00630054
      ******************************************************************00640054
      **PROGRAM COUNTERS                                                00650054
      ******************************************************************00660054
       01  PC-PROGRAM-COUNTERS.                                         00670054
           05  PC-INPUT-RECORDS-READ           PIC 9(09) VALUE ZEROS.   00680057
           05  PC-OUTPUT-RECORDS-WRITTEN       PIC 9(09) VALUE ZEROS.   00690057
      ****************************************************************  00700054
      **ABEND INFORMATION                                               00710054
      ****************************************************************  00720054
       01  AC-ABEND-FIELDS.                                             00730054
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    00740054
                                                           COMP SYNC.   00750054
               88  AC-TABLE-ERROR                        VALUE +4004.   00760054
               88  AC-DB2-ERROR                          VALUE +4013.   00770054
       01  PV-MISC-ABEND-FIELDS.                                        00780054
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   00790054
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   00800054
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   00810054
           05  PV-DB2-OPERATION                PIC X(30) VALUE SPACE.   00820054
      ******************************************************************00830054
      **COPYBOOKS                                                       00840054
      ******************************************************************00850054
      ****INPUT RECORD LAYOUT                                           00860054
           COPY SURD062.                                                00870070
      ****OUTPUT RECORD LAYOUT                                          00880054
           COPY SFRD045.                                                00890070
      ****ABEND ROUTINE INFORMATION                                     00900054
           COPY SQLCA2.                                                 00910054
      ****ABEND ROUTINE INFORMATION                                     00920054
           COPY DPWS004.                                                00930054
      ******************************************************************00940054
      **TABLE INCLUSIONS                                                00950054
      ******************************************************************00960054
      ***XS DOMAIN SKU TABLE                                            00970054
           EXEC SQL                                                     00980054
                INCLUDE XST00010                                        00990054
           END-EXEC.                                                    01000054
      ***XS DOMAIN VENDOR STYLE TABLE                                   01010054
           EXEC SQL                                                     01020054
                INCLUDE XST00013                                        01030054
           END-EXEC.                                                    01040054
      ***XS DOMAIN DEPT TABLE                                           01050054
           EXEC SQL                                                     01060054
                INCLUDE XST00015                                        01070054
           END-EXEC.                                                    01080054
      *****DB2 TABLE INCLUSION                                          01090054
           EXEC SQL                                                     01100054
                INCLUDE SQLCA                                           01110054
           END-EXEC.                                                    01120054
                                                                        01130054
       PROCEDURE DIVISION.                                              01140054
      ******************************************************************01150054
       000-MAINLINE-PROCESSING.                                         01160054
                                                                        01170054
           PERFORM 1000-INITIALIZATION.                                 01180054
           PERFORM 2000-PROCESS-RECORDS                                 01190054
                   UNTIL PS-END-OF-INPUT-FILE.                          01200054
           PERFORM 9999-WRAPUP.                                         01210054
           GOBACK.                                                      01220054
                                                                        01230054
       EJECT.                                                           01240054
      ******************************************************************01250054
      **INITIALIZATION ROUTINE                                          01260054
      ******************************************************************01270054
       1000-INITIALIZATION.                                             01280054
                                                                        01290054
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01300054
                                                                        01310054
           OPEN INPUT  INPUT-FILE                                       01320054
                OUTPUT OUTPUT-FILE.                                     01330054
                                                                        01340054
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01350054
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01360054
                                                                        01370054
           PERFORM 9999-READ-INPUT-RECORD.                              01380054
                                                                        01390054
       EJECT.                                                           01400054
      ******************************************************************01410054
      **PROCESS INPUT RECORD TO CHECK ROWS                              01420054
      ******************************************************************01430054
       2000-PROCESS-RECORDS.                                            01440054
                                                                        01450054
           MOVE '2000-PROCESS-RECORDS'   TO PV-PARAGRAPH-NAME.          01460054
                                                                        01470054
           INITIALIZE SFRD045-REFORMAT-MANIFEST-FILE.                   01480070
                                                                        01490054
           IF  SU062-INTR-UPC-ID NOT EQUAL PH-INTR-UPC-ID               01500070
               PERFORM 2100-DATA-LOOKUP                                 01510054
               MOVE SU062-INTR-UPC-ID    TO PH-INTR-UPC-ID              01520070
      *    ELSE                                                         01521079
      *        IF SU062-INTR-UPC-ID = +0                                01522079
      *            MOVE +0               TO XST00013-DEPT-NBR           01523079
      *            MOVE +0               TO XST00013-MAJ-CL-NBR         01524079
      *            MOVE +0               TO XST00015-BUS-GP-NBR         01525079
      *        END-IF                                                   01526079
           END-IF.                                                      01530054
                                                                        01540054
           IF  SQLCODE = 0                                              01550056
      ****ONLY PROCESS INTR_UPC_ID IF IT WAS VALID IN 2100-             01560070
               MOVE SU062-INTR-UPC-ID    TO SFRD045-INTR-UPC-ID         01570070
               MOVE SU062-DEST-LOC-NBR   TO SFRD045-LOC-NBR             01580070
               MOVE XST00013-DEPT-NBR    TO SFRD045-DEPT-NBR            01590070
               MOVE SU062-SHPD-UNT-QTY   TO SFRD045-MNFST-QTY           01600071
               MOVE XST00015-BUS-GP-NBR  TO SFRD045-BUS-GP-NBR          01610070
               MOVE XST00013-MAJ-CL-NBR  TO SFRD045-MAJ-CL-NBR          01620070
           END-IF.                                                      01630070
                                                                        01640056
           PERFORM 9999-WRITE-OUTPUT-RECORD.                            01650056
           PERFORM 9999-READ-INPUT-RECORD.                              01660054
                                                                        01670054
       EJECT.                                                           01680054
      ******************************************************************01690054
      **LOOKUP DATA FOR SKU NBR                                         01700054
      ******************************************************************01710054
       2100-DATA-LOOKUP.                                                01720054
                                                                        01730054
           MOVE '2100-DATA-LOOKUP'    TO PV-PARAGRAPH-NAME.             01740054
                                                                        01750054
           MOVE SU062-INTR-UPC-ID     TO XST00010-INTR-UPC-ID.          01760070
                                                                        01770054
           EXEC SQL                                                     01780054
                SELECT B.DEPT_NBR                                       01790070
                      ,C.BUS_GP_NBR                                     01800066
                      ,B.MAJ_CL_NBR                                     01810068
                INTO   :XST00013-DEPT-NBR                               01820070
                      ,:XST00015-BUS-GP-NBR                             01830066
                      ,:XST00013-MAJ-CL-NBR                             01840068
                FROM   XST_SKU      A                                   01850054
                      ,XST_VND_STYL B                                   01860054
                      ,XST_DEPT     C                                   01870054
                WHERE A.STYL_ID     = B.STYL_ID                         01880070
                  AND B.DEPT_NBR    = C.DEPT_NBR                        01890070
                  AND A.INTR_UPC_ID = :SU062-INTR-UPC-ID                01900070
                WITH UR                                                 01910054
           END-EXEC.                                                    01920054
                                                                        01930054
           EVALUATE TRUE                                                01940054
              WHEN SQLCODE = +0                                         01950054
              WHEN SQLCODE = +100                                       01960065
                   CONTINUE                                             01970058
              WHEN OTHER                                                01980054
                   DISPLAY 'ERROR ON DATA LOOKUP....'                   01990054
                   DISPLAY 'INTR_UPC_ID ' SU062-INTR-UPC-ID             02000070
                   PERFORM 9999-DISPLAY-STATISTICS                      02010054
                   PERFORM 9999-SQL-ERROR                               02020054
           END-EVALUATE.                                                02030054
                                                                        02040054
       EJECT.                                                           02050054
      ******************************************************************02060054
      **READ INPUT RECORD                                               02070054
      ******************************************************************02080054
       9999-READ-INPUT-RECORD.                                          02090054
                                                                        02100054
           READ INPUT-FILE INTO SU062-STR-UNLOAD-ITEM-REC               02110070
                AT END                                                  02120054
                SET PS-END-OF-INPUT-FILE TO TRUE                        02130054
           END-READ.                                                    02140054
                                                                        02150054
           IF  PS-NOT-END-OF-INPUT-FILE                                 02160054
               ADD +1                      TO PC-INPUT-RECORDS-READ     02170054
           END-IF.                                                      02180054
                                                                        02190054
       EJECT.                                                           02200054
      ****************************************************************  02210054
      **WRITE OUTPUT RECORD                                             02220054
      ****************************************************************  02230054
       9999-WRITE-OUTPUT-RECORD.                                        02240054
                                                                        02250054
           WRITE OUTPUT-RECORD FROM SFRD045-REFORMAT-MANIFEST-FILE.     02260070
                                                                        02270054
           ADD +1              TO PC-OUTPUT-RECORDS-WRITTEN.            02280054
                                                                        02290054
       EJECT.                                                           02300054
      ******************************************************************02310054
      **CLOSE INPUT FILES.                                              02320054
      ******************************************************************02330054
       9999-WRAPUP.                                                     02340054
                                                                        02350054
           CLOSE INPUT-FILE                                             02360054
                 OUTPUT-FILE.                                           02370054
                                                                        02380054
           PERFORM 9999-DISPLAY-STATISTICS.                             02390054
                                                                        02400054
       EJECT.                                                           02410054
      ******************************************************************02420054
      **DISPLAY PROGRAM STATS                                           02430054
      ******************************************************************02440054
       9999-DISPLAY-STATISTICS.                                         02450054
                                                                        02460054
           DISPLAY '*************************************************'. 02470054
           DISPLAY '* PROGRAM...........: SFKUT036'.                    02480075
           DISPLAY '* DATE..............: ' DF-ACCEPT-DATE.             02490054
           DISPLAY '* TIME..............: ' DF-ACCEPT-TIME.             02500054
           DISPLAY '* RECORDS READ......: ' PC-INPUT-RECORDS-READ.      02510054
           DISPLAY '* OUTPUT RECORDS WRITTEN...: '                      02520054
                                              PC-OUTPUT-RECORDS-WRITTEN.02530054
           DISPLAY '*************************************************'. 02540054
                                                                        02550054
       EJECT.                                                           02560054
      ******************************************************************02570054
      * ABEND ROUTINE FOR BAD SQL CALLS                                *02580054
      ******************************************************************02590054
       9999-SQL-ERROR.                                                  02600054
                                                                        02610054
           DISPLAY '** ABEND     **'.                                   02620054
           DISPLAY '** PROGRAM   ** = SFKUT036'.                        02630075
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              02640054
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               02650054
      ***************************************************************** 02660054
      *    COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED  02670054
      *    TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.   02680054
      ***************************************************************** 02690054
           COPY DPPD004.                                                02700054
           SET AC-DB2-ERROR TO TRUE.                                    02710054
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         02720054
                                                                        02730054
       EJECT.                                                           02740054
