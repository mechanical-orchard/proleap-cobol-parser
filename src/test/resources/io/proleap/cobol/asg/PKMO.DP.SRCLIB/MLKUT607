       IDENTIFICATION DIVISION.                                         00010099
       PROGRAM-ID.    MLKUT607.                                         00020099
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030099
       DATE-WRITTEN.  08-03-07.                                         00040099
       DATE-COMPILED.                                                   00050099
      ******************************************************************00060099
      *                                                                 00070099
      * MLKUT607 - CREATE THE DATA INTEGRITY EXTRACT FILE FOR           00080099
      *            MLT_WKLY_VND.                                        00090099
      *                                                                 00100099
      * THIS PROGRAM WILL TAKE THE SORTED MLT_WKLY_VND FILE AND FORMAT  00110099
      * THE DATA TO DATA INTEGRITY EXTRACT COPYBOOK MLRD111 AND CREATE  00120099
      * DATA INTEGRITY EXTRACT FILE.                                    00130099
      *                                                                 00140099
      *CHANGE HISTORY:                                                  00150099
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00160099
      * ------- ---------- ----------- -------------------------------- 00170099
28582 * 28582   02-08-2007 ROD JANSSEN VENDOR LEVEL DETAIL LEDGER MOD.  00180099
      ******************************************************************00190099
                                                                        00200099
       ENVIRONMENT DIVISION.                                            00210099
       CONFIGURATION SECTION.                                           00220099
       SOURCE-COMPUTER.        IBM-3090.                                00230099
       OBJECT-COMPUTER.        IBM-3090.                                00240099
       INPUT-OUTPUT SECTION.                                            00250099
       FILE-CONTROL.                                                    00260099
                                                                        00270099
           SELECT DATA-INTEGRITY-CTL-FILE                               00280099
               ASSIGN TO INP01.                                         00290099
                                                                        00300099
           SELECT DATA-INTEGRITY-DATE-FILE                              00310099
               ASSIGN TO INP02.                                         00320099
                                                                        00330099
           SELECT DATA-INTEGRITY-EXTR-FILE                              00340099
               ASSIGN TO OUT01.                                         00350099
                                                                        00360099
       DATA DIVISION.                                                   00370099
       FILE SECTION.                                                    00380099
                                                                        00390099
       FD  DATA-INTEGRITY-CTL-FILE                                      00400099
           LABEL RECORDS ARE STANDARD                                   00410099
           RECORDING MODE IS F                                          00420099
           BLOCK CONTAINS 0 RECORDS.                                    00430099
       01  DATA-INTEGRITY-CTL-REC      PIC X(349).                      00440099
                                                                        00450099
       FD  DATA-INTEGRITY-DATE-FILE                                     00460099
           LABEL RECORDS ARE STANDARD                                   00470099
           RECORDING MODE IS F                                          00480099
           BLOCK CONTAINS 0 RECORDS.                                    00490099
       01  DATA-INTEGRITY-DATE-REC     PIC X(80).                       00500099
                                                                        00510099
       FD  DATA-INTEGRITY-EXTR-FILE                                     00520099
           RECORDING MODE IS F                                          00530099
           LABEL RECORDS ARE STANDARD                                   00540099
           BLOCK CONTAINS 0 RECORDS.                                    00550099
                                                                        00560099
       01  DATA-INTEGRITY-EXTR-REC     PIC X(120).                      00570099
                                                                        00580099
      ******************************************************************00590099
       WORKING-STORAGE SECTION.                                         00600099
      ******************************************************************00610099
                                                                        00620099
       01  PS-PROGRAM-SWITCHES.                                         00630099
           05  PS-END-OF-INPUT-SW      PIC X       VALUE 'N'.           00640099
               88  PS-END-OF-INPUT                 VALUE 'Y'.           00650099
               88  PS-END-OF-INPUT-NO              VALUE 'N'.           00660099
           05  PS-END-OF-DATE-FILE-SW  PIC X       VALUE 'N'.           00670099
               88  PS-END-OF-DATE-FILE             VALUE 'Y'.           00680099
               88  PS-END-OF-DATE-FILE-NO          VALUE 'N'.           00690099
           05  PS-MLT00015-DONE        PIC X       VALUE 'N'.           00700099
               88  PS-MLT00015-IS-DONE             VALUE 'Y'.           00710099
               88  PS-MLT00015-IS-NOT-DONE         VALUE 'N'.           00720099
                                                                        00730099
       01  PV-PROGRAM-VARIABLES.                                        00740099
           05  PV-PREV-FSCL-MN-END-DTE PIC X(10) VALUE SPACE.           00750099
           05  PV-PREV-DEPT-NBR        PIC S9(4)V COMP-3 VALUE ZEROS.   00760099
           05  PV-OPN-MONTH-FIRST-WEEK PIC X(10).                       00770099
           05  PV-DEPT-NBR             PIC 9(03)  VALUE ZEROS.          00780099
           05  PV-MAJ-CL-NBR           PIC 9(02)  VALUE ZEROS.          00790099
           05  PV-SUB-CL-NBR           PIC 9(02)  VALUE ZEROS.          00800099
           05  PV-LOC-NBR              PIC 9(04)  VALUE ZEROS.          00810099
           05  PV-FSCL-DTE             PIC X(10).                       00820099
           05  PV-FSCL-MN-END-DTE      PIC X(10).                       00830099
           05  PV-FSCL-MN-NBR          PIC X(02).                       00840099
           05  PV-PARTN-NBR            PIC S9(4)V COMP.                 00850099
                                                                        00860099
           05  PV-DKEY-NBR.                                             00870099
               10  PV-DKEY-WK-NBR      PIC X(02).                       00880099
               10  PV-DKEY-MN-NBR      PIC X(02).                       00890099
               10  FILLER              PIC X(11).                       00900099
                                                                        00910099
       01  PA-PROGRAM-ACCUMULATORS.                                     00920099
           05  PA-INPUT-FILES           PIC S9(13) VALUE ZEROS.         00930099
           05  PA-MLT00015-ROWS-FETCHED PIC S9(13) VALUE ZEROS.         00940099
           05  PA-TDTATTR-ROWS-SELECTED PIC S9(13) VALUE ZEROS.         00950099
           05  PA-OUTPUT-RECS-WRITTEN   PIC S9(13) VALUE ZEROS.         00960099
                                                                        00970099
       01  PC-PROGRAM-CONSTANTS.                                        00980099
           05  PC-MAX-TBL-ROWS         PIC S9(03)  VALUE +120.          00990099
           05  PC-Y                    PIC X       VALUE 'Y'.           01000099
                                                                        01010099
       01  TABLE-FOR-MONTH-DATA.                                        01020099
           05  TABLE-WEEKEND-ENTRIES   OCCURS 450 TIMES                 01030099
                                       INDEXED BY MTH-IDX.              01040099
               10  TBL-FSCL-DTE        PIC X(10).                       01050099
               10  TBL-MONTHEND-DTE    PIC X(10).                       01060099
               10  TBL-MONTHEND-NBR    PIC X(02).                       01070099
                                                                        01080099
       01  CD-COUNTER-DISPLAY.                                          01090099
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           01100099
                                                                        01110099
       01 ABEND-AREAS.                                                  01120099
           05 ABEND-CODE               PIC S9(04)  VALUE ZEROS          01130099
                                                   COMP SYNC.           01140099
               88  AC-BAD-DATA                     VALUE +4003.         01150099
               88  AC-DB2-ERROR                    VALUE +4013.         01160099
           05  AA-ABEND-LIT            PIC X(40)   VALUE                01170099
                   '*****       ABEND'.                                 01180099
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                01190099
                   '*****   PROGRAM: MLKUT607'.                         01200099
           05  AA-PARAGRAPH-LIT.                                        01210099
               10  FILLER              PIC X(17)   VALUE                01220099
                   '***** PARAGRAPH: '.                                 01230099
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        01240099
           05  AA-MESSAGE-LINE.                                         01250099
               10  FILLER              PIC X(06)   VALUE '*****'.       01260099
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        01270099
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                01280099
                   '*****    DB2 ERROR'.                                01290099
           05  AA-DB2-OPERATION-LIT.                                    01300099
               10  FILLER              PIC X(17)   VALUE                01310099
                   '***** OPERATION: '.                                 01320099
               10  AA-DB2-OPERATION.                                    01330099
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        01340099
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        01350099
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        01360099
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        01370099
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        01380099
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        01390099
                                                                        01400099
      *** DATA INTEGRITY CONTROL CARD                                   01410099
           COPY MLRD110.                                                01420099
                                                                        01430099
      *** DATA INTEGRITY MLT_WKLY_VND EXTRACT FILE                      01440099
           COPY MLRD111.                                                01450099
                                                                        01460099
      ******************************************************************01470099
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01480099
      ******************************************************************01490099
           COPY DPWS004.                                                01500099
                                                                        01510099
      ******************************************************************01520099
      *  DB2 ERRORS                                                     01530099
      ******************************************************************01540099
           COPY SQLCA2.                                                 01550099
                                                                        01560099
      ******************************************************************01570099
      *  DB2 COMMUNICATION AREA                                         01580099
      ******************************************************************01590099
           EXEC SQL                                                     01600099
                INCLUDE SQLCA                                           01610099
           END-EXEC.                                                    01620099
                                                                        01630099
      ******************************************************************01640099
      *  M/L DETAIL TABLE - PARTITION BALANCING TABLE                   01650099
      ******************************************************************01660099
           EXEC SQL                                                     01670099
               INCLUDE MLT00002                                         01680099
           END-EXEC.                                                    01690099
      *                                                                 01700099
      ******************************************************************01710099
      *  M/L DETAIL TABLE - PARTITION CONTROL TABLE                     01720099
      ******************************************************************01730099
           EXEC SQL                                                     01740099
               INCLUDE MLT00003                                         01750099
           END-EXEC.                                                    01760099
      *                                                                 01770099
      ******************************************************************01780099
      *  DB2 MLT_WKLY_VND TABLE LAYOUT                                  01790099
      ******************************************************************01800099
           EXEC SQL                                                     01810099
                INCLUDE MLT00015                                        01820099
           END-EXEC.                                                    01830099
      *                                                                 01840099
      ******************************************************************01850099
      *  DB2 TDTATTR TABLE LAYOUT                                       01860099
      ******************************************************************01870099
           EXEC SQL                                                     01880099
                INCLUDE TDTATTR                                         01890099
           END-EXEC.                                                    01900099
                                                                        01910099
      ******************************************************************01920099
       PROCEDURE DIVISION.                                              01930099
      ******************************************************************01940099
      ******************************************************************01960099
       0000-MAINLINE-MLKUT607.                                          01970099
      ******************************************************************01980099
      *                                                                 01990099
           PERFORM 1000-INITIALIZATION.                                 02000099
                                                                        02010099
           PERFORM 2000-MAIN-PROCESS.                                   02020099
                                                                        02030099
           PERFORM 8000-EOJ.                                            02040099
                                                                        02050099
           STOP RUN.                                                    02060099
                                                                        02070099
      ******************************************************************02080099
       1000-INITIALIZATION.                                             02090099
      ******************************************************************02100099
      *                                                                 02110099
           OPEN INPUT  DATA-INTEGRITY-CTL-FILE                          02120099
                       DATA-INTEGRITY-DATE-FILE.                        02130099
           OPEN OUTPUT DATA-INTEGRITY-EXTR-FILE.                        02140099
                                                                        02150099
           PERFORM 4000-READ-INPUT-FILE.                                02160099
           PERFORM 4100-READ-DATE-FILE.                                 02170099
                                                                        02180099
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                          02190099
           INITIALIZE PV-PROGRAM-VARIABLES.                             02200099
           INITIALIZE TABLE-FOR-MONTH-DATA.                             02210099
                                                                        02220099
      ******************************************************************02230099
       2000-MAIN-PROCESS.                                               02240099
      ******************************************************************02250099
      *                                                                 02260099
           PERFORM                                                      02270099
             UNTIL PS-END-OF-INPUT                                      02280099
               PERFORM 2100-GET-MONTH-DATA                              02290099
               PERFORM 5000-WRITE-EXTRACT                               02300099
               PERFORM 4000-READ-INPUT-FILE                             02310099
               ADD  1           TO PA-INPUT-FILES                       02320099
           END-PERFORM.                                                 02330099
                                                                        02340099
      ******************************************************************02350099
       2100-GET-MONTH-DATA.                                             02360099
      ******************************************************************02370099
                                                                        02380099
           SET MTH-IDX                 TO 1.                            02390099
           SEARCH TABLE-WEEKEND-ENTRIES                                 02400099
               AT END                                                   02410099
                   PERFORM 7400-SELECT-TDTATTR                          02420099
                   ADD 1               TO PA-TDTATTR-ROWS-SELECTED      02430099
                   MOVE MLT00015-FSCL-WK-END-DTE                        02440099
                                       TO PV-FSCL-DTE                   02450099
                   MOVE DTATTR-FSCL-MN-END-DTE                          02460099
                                       TO PV-FSCL-MN-END-DTE            02470099
                   MOVE DTATTR-FSCL-MN-NBR                              02480099
                                       TO PV-FSCL-MN-NBR                02490099
                   IF PA-TDTATTR-ROWS-SELECTED > PC-MAX-TBL-ROWS        02500099
                       CONTINUE                                         02510099
                   ELSE                                                 02520099
                       SET MTH-IDX     TO PA-TDTATTR-ROWS-SELECTED      02530099
                       MOVE MLT00015-FSCL-WK-END-DTE                    02540099
                                       TO TBL-FSCL-DTE     (MTH-IDX)    02550099
                       MOVE DTATTR-FSCL-MN-END-DTE                      02560099
                                       TO TBL-MONTHEND-DTE (MTH-IDX)    02570099
                       MOVE DTATTR-FSCL-MN-NBR                          02580099
                                       TO TBL-MONTHEND-NBR (MTH-IDX)    02590099
                   END-IF                                               02600099
               WHEN TBL-FSCL-DTE  (MTH-IDX) = MLT00015-FSCL-WK-END-DTE  02610099
                   MOVE TBL-MONTHEND-DTE (MTH-IDX)                      02620099
                                       TO PV-FSCL-MN-END-DTE            02630099
                   MOVE TBL-MONTHEND-NBR (MTH-IDX)                      02640099
                                       TO PV-FSCL-MN-NBR                02650099
           END-SEARCH.                                                  02660099
                                                                        02670099
      ******************************************************************02680099
       4000-READ-INPUT-FILE.                                            02690099
      ******************************************************************02700099
      *                                                                 02710099
           READ DATA-INTEGRITY-CTL-FILE                                 02720099
             INTO DCLMLT00015                                           02730099
               AT END                                                   02740099
                   SET PS-END-OF-INPUT    TO TRUE.                      02750099
                                                                        02760099
      ******************************************************************02770099
       4100-READ-DATE-FILE.                                             02780099
      ******************************************************************02790099
      *                                                                 02800099
           READ DATA-INTEGRITY-DATE-FILE                                02810099
             INTO ML110-DATA-INTEGRITY-CC-REC                           02820099
               AT END                                                   02830099
                   MOVE PC-Y        TO PS-END-OF-DATE-FILE-SW.          02840099
                                                                        02850099
      ******************************************************************02860099
       5000-WRITE-EXTRACT.                                              02870099
      ******************************************************************02880099
      *                                                                 02890099
           MOVE PV-FSCL-MN-END-DTE       TO ML111-FSCL-MN-END-DTE.      02900099
           MOVE PV-FSCL-MN-NBR           TO ML111-FSCL-MN-NBR.          02910099
           MOVE MLT00015-DEPT-NBR        TO PV-DEPT-NBR.                02920099
           MOVE PV-DEPT-NBR              TO ML111-DEPT-NBR.             02930099
           MOVE MLT00015-MAJ-CL-NBR      TO PV-MAJ-CL-NBR.              02940099
           MOVE PV-MAJ-CL-NBR            TO ML111-MAJ-CL-NBR.           02950099
           MOVE MLT00015-SUB-CL-NBR      TO PV-SUB-CL-NBR.              02960099
           MOVE PV-SUB-CL-NBR            TO ML111-SUB-CL-NBR.           02970099
           MOVE MLT00015-LOC-NBR         TO PV-LOC-NBR.                 02980099
           MOVE PV-LOC-NBR               TO ML111-STR-NBR.              02990099
                                                                        03000099
           COMPUTE ML111-SLS-RTL-AMT  = MLT00015-SLS-REG-RTL-AMT        03010099
                                      + MLT00015-SLS-PROMO-RTL-AMT      03020099
                                      + MLT00015-SLS-PROCLR-RTL-AMT     03030099
                                      + MLT00015-SLS-CLR-RTL-AMT        03040099
                                      + MLT00015-SLS-OTH-RTL-AMT.       03050099
           COMPUTE ML111-MKDN-RTL-AMT = MLT00015-MKDN-PLU-RTL-AMT       03060099
                                      + MLT00015-MKDN-RGST-RTL-AMT      03070099
                                      + MLT00015-MKDN-UMTCH-RTL-AMT     03080099
                                      + MLT00015-MKDN-BYR-RTL-AMT       03090099
                                      + MLT00015-MKDN-STR-RTL-AMT.      03100099
           MOVE MLT00015-MKUP-RTL-AMT     TO ML111-MKUP-RTL-AMT.        03110099
           MOVE MLT00015-XFER-IN-RTL-AMT  TO ML111-XFER-IN-RTL-AMT.     03120099
           MOVE MLT00015-XFER-OUT-RTL-AMT TO ML111-XFER-OUT-RTL-AMT.    03130099
           MOVE MLT00015-RCPT-RTL-AMT     TO ML111-RCPT-RTL-AMT.        03140099
           MOVE MLT00015-RCPT-NET-COST-AMT                              03150099
                                          TO ML111-RCPT-NET-COST-AMT.   03160099
           MOVE MLT00015-PADJ-RTL-AMT     TO ML111-PADJ-RTL-AMT.        03170099
           MOVE MLT00015-PADJ-NET-COST-AMT                              03180099
                                          TO ML111-PADJ-NET-COST-AMT.   03190099
           MOVE MLT00015-INV-ADJ-RTL-AMT  TO ML111-INV-ADJ-RTL-AMT.     03200099
           MOVE MLT00015-SHNK-RTL-AMT     TO ML111-SHNK-RTL-AMT.        03210099
                                                                        03220099
      *    ENDING INVENTORY IS ONLY FILLED IN ON THE RECORD FOR THE     03230099
      *    FIRST (MOST RECENT) FISCAL WEEK IN THE FISCAL MONTH.         03240099
      *    (THE FISCAL WEEKS ARE PROCESSED IN DESCENDING SEQUENCE.)     03250099
           IF  PV-FSCL-MN-END-DTE      = PV-PREV-FSCL-MN-END-DTE        03260099
           AND MLT00015-DEPT-NBR       = PV-PREV-DEPT-NBR               03270099
               MOVE ZEROS                  TO ML111-END-INV-RTL-AMT     03280099
           ELSE                                                         03290099
               MOVE MLT00015-END-INV-RTL-AMT TO ML111-END-INV-RTL-AMT   03300099
               MOVE PV-FSCL-MN-END-DTE     TO PV-PREV-FSCL-MN-END-DTE   03310099
               MOVE MLT00015-DEPT-NBR      TO PV-PREV-DEPT-NBR          03320099
           END-IF.                                                      03330099
                                                                        03340099
           MOVE SPACES                   TO ML111-DKEY-NBR.             03350099
                                                                        03360099
           WRITE DATA-INTEGRITY-EXTR-REC                                03370099
               FROM ML111-DATA-INTEGRITY-EXTR-REC.                      03380099
           ADD 1                         TO PA-OUTPUT-RECS-WRITTEN.     03390099
                                                                        03400099
      ******************************************************************03410099
       7400-SELECT-TDTATTR.                                             03420099
      ******************************************************************03430099
      *                                                                 03440099
           MOVE MLT00015-FSCL-WK-END-DTE TO PV-FSCL-DTE.                03450099
                                                                        03460099
           EXEC SQL                                                     03470099
               SELECT DISTINCT                                          03480099
                      FSCL_MN_NBR                                       03490099
                     ,FSCL_MN_END_DTE                                   03500099
                     ,FSCL_WK_NBR                                       03510099
                     ,FSCL_WK_END_DTE                                   03520099
               INTO  :DTATTR-FSCL-MN-NBR                                03530099
                    ,:DTATTR-FSCL-MN-END-DTE                            03540099
                    ,:DTATTR-FSCL-WK-NBR                                03550099
                    ,:DTATTR-FSCL-WK-END-DTE                            03560099
               FROM  MLT_PARTN_CNTL A                                   03570099
                     ,TDTATTR B                                         03580099
               WHERE A.FSCL_DTE = :PV-FSCL-DTE                          03590099
                 AND A.FSCL_DTE = B.KY_DTE                              03600099
                 AND A.TBL_TM_LVL_CDE = 'W'                             03610099
               WITH UR                                                  03620099
           END-EXEC.                                                    03630099
                                                                        03640099
           EVALUATE TRUE                                                03650099
               WHEN SQLCODE = 0                                         03660099
                   CONTINUE                                             03670099
               WHEN OTHER                                               03680099
                   MOVE '7400-SELECT-TDTATTR'                           03690099
                                       TO AA-PARAGRAPH-NAME             03700099
                   MOVE 'TDTATTR ROW NOT FOUND'                         03710099
                                       TO AA-MESSAGE                    03720099
                   MOVE 'SELECT'       TO AA-DB2-OPERATION              03730099
                   PERFORM Z998-SQL-ERROR                               03740099
           END-EVALUATE.                                                03750099
                                                                        03760099
      ******************************************************************03770099
       8000-EOJ.                                                        03780099
      ******************************************************************03790099
      *                                                                 03800099
           DISPLAY ' '.                                                 03810099
           DISPLAY '  PROGRAM MLKUT607 PROCESSING SUMMARY'.             03820099
           DISPLAY '  -----------------------------------'.             03830099
           DISPLAY ' '.                                                 03840099
      *                                                                 03850099
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.               03860099
           DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.       03870099
      *                                                                 03880099
           DISPLAY ' '.                                                 03890099
                                                                        03900099
           CLOSE DATA-INTEGRITY-CTL-FILE.                               03910099
           CLOSE DATA-INTEGRITY-DATE-FILE.                              03920099
           CLOSE DATA-INTEGRITY-EXTR-FILE.                              03930099
                                                                        03940099
      ******************************************************************03950099
       Z990-COMMON-ABEND-ROUTINE.                                       03960099
      ******************************************************************03970099
      *                                                                 03980099
           DISPLAY AA-ABEND-LIT.                                        03990099
           DISPLAY AA-PROGRAM-LIT.                                      04000099
           DISPLAY AA-PARAGRAPH-LIT.                                    04010099
           DISPLAY AA-MESSAGE-LINE.                                     04020099
           DISPLAY ABEND-CODE.                                          04030099
                                                                        04040099
           CALL 'ILBOABN0' USING ABEND-CODE.                            04050099
                                                                        04060099
      ******************************************************************04070099
       Z998-SQL-ERROR.                                                  04080099
      ******************************************************************04090099
      *                                                                 04100099
           CLOSE DATA-INTEGRITY-CTL-FILE                                04110099
                 DATA-INTEGRITY-EXTR-FILE.                              04120099
                                                                        04130099
           DISPLAY AA-ABEND-LIT.                                        04140099
           DISPLAY AA-DB2-ERROR-LIT.                                    04150099
           DISPLAY AA-PROGRAM-LIT.                                      04160099
           DISPLAY AA-PARAGRAPH-LIT.                                    04170099
           DISPLAY AA-MESSAGE-LINE.                                     04180099
           DISPLAY AA-DB2-OPERATION-LIT.                                04190099
           SET AC-DB2-ERROR TO TRUE.                                    04200099
           MOVE +4013                  TO ABEND-CODE.                   04210099
                                                                        04220099
           COPY DPPD004.                                                04230099
           CALL 'ILBOABN0' USING ABEND-CODE.                            04240099
                                                                        04250099
