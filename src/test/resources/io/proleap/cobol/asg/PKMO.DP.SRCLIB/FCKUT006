       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.      FCKUT006.                                               
       AUTHOR.          STEPHANIE CISKE.                                        
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    AUGUST 2010.                                            
       DATE-COMPILED.                                                           
      ****************************************************************          
      *       **********************************************                    
      *       **  THIS PROGRAM MUST BE COMPILED AS ORACLE **                    
      *       **********************************************                    
      *                                                                         
      * THIS PROGRAM CREATES A CONTROL CARD FOR EACH NEW STORE OPENING          
      * MONTH WITH THE FORMAT OF MONTH, MARKET CLUSTER, VSG, AND STORE          
      * NUMBER FOR EACH NEW STORE                                               
      *                                                                         
      *    WR/IR #          DATE       DESCRIPTION                     *        
      *    --------         ----       ------------------------------  *        
      *    PKE00000000####  08/12/2010  INITIAL IMPLEMENTATION                  
      ****************************************************************          
      ******************************************************************        
      * DATE:         07/30/2018                                       *        
      * WR/IR#:       CHG0204325                                       *        
      * PROGRAMMER:   COGNIZANT                                        *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ****************************************************************          
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
070697     SELECT ORACLE-LOGON-FILE                                             
070697          ASSIGN TO ORALOGON.                                             
                                                                                
           SELECT CONTROL-FILE                                                  
                ASSIGN TO INP03.                                                
                                                                                
           SELECT VPS-FILE-01                                                   
                ASSIGN TO OUT02.                                                
                                                                                
           SELECT DATE-CONTROL-FILE                                             
               ASSIGN TO OUT03.                                                 
                                                                                
      ****************************************************************          
       DATA DIVISION.                                                           
      ****************************************************************          
       FILE SECTION.                                                            
                                                                                
070697 FD  ORACLE-LOGON-FILE                                                    
070697     RECORDING MODE IS F                                                  
070697     LABEL RECORDS ARE STANDARD                                           
070697     BLOCK CONTAINS 0 RECORDS                                             
070697     RECORD CONTAINS 80 CHARACTERS                                        
070697     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070697                                                                          
070697 01  OL-ORACLE-LOGON-RECORD          PIC X(80).                           
                                                                                
       FD  CONTROL-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CONTROL-REC                     PIC X(2500).                         
                                                                                
                                                                                
       FD  VPS-FILE-01                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  VPS-REC-01                      PIC X(2500).                         
                                                                                
                                                                                
       FD  DATE-CONTROL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  DATE-CONTROL-RECORD             PIC X(80).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
070697 01  ABEND-CODE-1                  PIC S9(04)  COMP SYNC                  
070697                                               VALUE ZERO.                
070697     88  AC-CONTROL-CARD-ERROR                 VALUE +4003.               
070697     88  AC-ORACLE-ERROR                       VALUE +4013.               
                                                                                
       01  PV-LOGON-SYSIN.                                                      
           05  PV-SYSIN-ORACLE-ID          PIC X(10) VALUE SPACES.              
           05  FILLER                      PIC X(01) VALUE '*'.                 
           05  PV-SYSIN-PASSWD             PIC X(10) VALUE SPACES.              
                                                                                
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08)  VALUE 'FCKUT006'.           
           05  PC-FIELD-LENGTH           PIC S9(03) VALUE +10 COMP-3.           
           05  PC-COMMIT-INTERVAL        PIC  9(05) VALUE 1000.                 
070697     05  PC-AUTO-LOGON             PIC  X(01) VALUE '/'.                  
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-STORE-TMP                PIC X(05) VALUE SPACES.              
           05  PV-STORE            REDEFINES PV-STORE-TMP                       
                                           PIC 9(05).                           
           05  PV-ABEND-CODE               PIC S9(04) VALUE ZERO.               
           05  STR-TALLY                   PIC S9(02) VALUE ZERO.               
           05  TRIMMED-LEN                 PIC 9(05) VALUE ZERO.                
           05  PV-DB2-OPERATION            PIC X(30).                           
           05  PV-PARAGRAPH-NAME           PIC X(30).                           
           05  PV-MESSAGE                  PIC X(30).                           
070697     05  PV-ORACLE-USERID-LEN        PIC S9(4) VALUE ZEROS                
070697                                               COMP SYNC.                 
070697     05  PV-ORACLE-PASSWORD-LEN      PIC S9(4) VALUE ZEROS                
070697                                               COMP SYNC.                 
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
070697     05  PS-END-OF-LOGON-FILE-SW     PIC X(01)  VALUE 'N'.                
070697         88  PS-END-OF-LOGON-FILE               VALUE 'Y'.                
           05  PS-END-OF-DATA-SW           PIC X(01)  VALUE 'N'.                
               88  PS-END-OF-DATA                     VALUE 'Y'.                
       01  STR-GP-NM-TMP                   PIC X(20) VALUE ' '.                 
       01  PV-STORE-NBR-HOST               PIC S9(18) COMP-3.                   
                                                                                
       01 CC-ORACLE-CONNECTION.                                                 
           05 CC-ORACLE-USERID             PIC X(40).                           
              88 CC-AUTO-LOGON                       VALUE '/'.                 
           05 CC-ORACLE-PASSWORD           PIC X(40).                           
                                                                                
       01  WS-CONTROL-RECORD.                                                   
           05 MONTH                        PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(01) VALUE ' '.                 
           05 CNTL-CARD-1.                                                      
              07 PV-MNTH-BEG-DTE           PIC X(10) VALUE SPACES.              
              07 FILLER                    PIC X(09) VALUE '.00.00.00'.         
              07 FILLER                    PIC X(08) VALUE '        '.          
              07 PV-MNTH-END-DTE           PIC X(10) VALUE SPACES.              
              07 FILLER                    PIC X(09) VALUE '.00.00.00'.         
           05 FILLER                       PIC X(02) VALUE '  '.                
           05 CNTL-CARD-2.                                                      
              07 PV-MNTH-REC-DTE           PIC X(26) VALUE SPACES.              
              07 FILLER                    PIC X(01) VALUE ' '.                 
              07 PV-MNTH-SFT-DTE           PIC X(26) VALUE SPACES.              
           05 FILLER                       PIC X(01) VALUE ' '.                 
           05 STORE-COUNT                  PIC 9(04) VALUE 0.                   
           05 FILLER                       PIC X(01) VALUE ' '.                 
           05 STORE-ARR                    PIC X(09) OCCURS 1000                
                                               INDEXED BY IDX.                  
       01  WS-OUTPUT-RECORD.                                                    
           05 MONTH-ABRV                   PIC X(03) VALUE SPACES.              
           05 FILLER                       PIC X(01) VALUE ' '.                 
           05 MARKET-CLUSTER-VSG.                                               
              07 MARKET-CLUSTER            PIC X(02) VALUE SPACES.              
              07 FILLER                    PIC X(03) VALUE ' / '.               
              07 VSG                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(04) VALUE '    '.              
           05 STORE-NUMBER                 PIC X(05) VALUE SPACES.              
                                                                                
       01  WS-STRING-OUTPUT                 PIC X(40) VALUE SPACES.             
                                                                                
       01  LOOP-COUNTER.                                                        
           05  LOOP-COUNT                 PIC X(02) VALUE SPACES.               
           05  LOOP-COUNT-TMP     REDEFINES LOOP-COUNT                          
                                           PIC 9(02).                           
                                                                                
       01  WS-OUTPUT-CONTROL-FILE.                                              
           05 CNTL-FILE-STORES             PIC X(21)                            
                        VALUE '  INCLUDE=(17,5,ZD,EQ'.                          
           05 CNTL-FILE-NO-STORES               PIC X(28)                       
                        VALUE '  INCLUDE=(17,5,ZD,EQ,00000)'.                   
           05 CNTL-OUTPUT                  PIC X(70) VALUE SPACES.              
                                                                                
070697 01  WS-ERROR-FIELDS.                                                     
070697     05 WS-MSG-TEXT                  PIC X(512).                          
070697     05 WS-MAX-SIZE                  PIC S9(09) COMP                      
070697                                                 VALUE +512.              
070697     05 WS-MSG-LENGTH                PIC S9(09) COMP.                     
                                                                                
                                                                                
      *************************************************************             
      * COPYBOOK FOR NUMERIC EDIT ROUTINE                         *             
      *************************************************************             
           COPY DPWS010I.                                                       
      *--------------------------------------------------------------*          
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004              
      *--------------------------------------------------------------*          
           COPY DPWS004.                                                        
                                                                                
      ****************************************************************          
      * ORACLE DECLARE SECTION                                                  
      ****************************************************************          
           EXEC SQL                                                             
               BEGIN DECLARE SECTION                                            
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    ORACLE LOGON WORKING STORAGE COPYBOOK.                               
      *---------------------------------------------------------------*         
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
      *---------------------------------------------------------------*         
      * K_FC01.FCT_FLR_PLN_PROTYP ORACLE TABLE                                  
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE FCT00005                                                 
           END-EXEC.                                                            
      ******************************************************************        
      * DATE ATTRIBUTE TABLE TO FIND SEASON BEGIN AND END DATES                 
      ****************************************************************          
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               END DECLARE SECTION                                              
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCACOB                                                 
           END-EXEC.                                                            
      ****************************************************************          
      *  COMMUNICATION AREAS FOR DB2                                            
      ****************************************************************          
           COPY SQLCA2.                                                         
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
      ******************************************************************        
       0000-MAINLINE.                                                           
           DISPLAY ' '.                                                         
           DISPLAY '******************'.                                        
           DISPLAY '* BEGIN ' PC-PROGRAM-NAME ' *'.                             
           DISPLAY '******************'.                                        
           DISPLAY ' '.                                                         
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2500-SET-UP-DATE-CNTL-CARD.                                  
                                                                                
           PERFORM 2000-READ-CONTROL-CARD                                       
                UNTIL PS-END-OF-DATA.                                           
                                                                                
           PERFORM 9000-PROGRAM-END.                                            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '******************'.                                        
           DISPLAY '* END   ' PC-PROGRAM-NAME ' *'.                             
           DISPLAY '******************'.                                        
           DISPLAY ' '.                                                         
           STOP RUN.                                                            
      ******************************************************************        
      *  GET SEASON, AND ITS START AND END DATES                                
      ******************************************************************        
       1000-INITIALIZE.                                                         
           MOVE '1000-INITIALIZE                 ' TO PV-PARAGRAPH-NAME.        
                                                                                
           PERFORM 1200-CONNECT-TO-ORACLE.                                      
                                                                                
           OPEN INPUT CONTROL-FILE.                                             
           OPEN OUTPUT VPS-FILE-01.                                             
           OPEN OUTPUT DATE-CONTROL-FILE.                                       
                                                                                
      ****************************************************************          
      * IF USERNAME-LEN IS ZERO, THE LOGON ID IS MISSING FROM SYSIN.            
      * THEREFORE IT IS A PRODUCTION RUN WHERE THE LOGON INFO COMES             
      * FROM THE JCL                                                            
      ****************************************************************          
       1200-CONNECT-TO-ORACLE.                                                  
                                                                                
070697     OPEN INPUT ORACLE-LOGON-FILE.                                        
070697     READ ORACLE-LOGON-FILE                                               
070697         INTO CC-ORACLE-CONNECTION                                        
070697         AT END                                                           
070697             SET PS-END-OF-LOGON-FILE TO TRUE                             
070697     END-READ.                                                            
070697     CLOSE ORACLE-LOGON-FILE.                                             
070697                                                                          
070697     IF ((PS-END-OF-LOGON-FILE)                                           
070697             OR (CC-ORACLE-CONNECTION = SPACES))                          
070697         DISPLAY '** ABEND **'                                            
070697         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070697                   'NOT PROVIDED'                                         
070697         SET AC-CONTROL-CARD-ERROR TO TRUE                                
070697         CALL 'ILBOABN0' USING ABEND-CODE-1                               
070697     END-IF.                                                              
070697                                                                          
070697     IF CC-AUTO-LOGON                                                     
070697         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070697         EXEC SQL                                                         
070697              CONNECT :USERNAME                                           
070697         END-EXEC                                                         
070697     ELSE                                                                 
070697         INITIALIZE PV-ORACLE-USERID-LEN                                  
070697                    PV-ORACLE-PASSWORD-LEN                                
070697         INSPECT CC-ORACLE-USERID                                         
070697             TALLYING PV-ORACLE-USERID-LEN                                
070697             FOR CHARACTERS BEFORE INITIAL SPACE                          
070697         INSPECT CC-ORACLE-PASSWORD                                       
070697             TALLYING PV-ORACLE-PASSWORD-LEN                              
070697             FOR CHARACTERS BEFORE INITIAL SPACE                          
070697         MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                
070697                   USERNAME-ARR                                           
070697         MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                        
070697         MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO            
070697                   PASSWD-ARR                                             
070697         MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                        
070697         DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '            
070697                   USERNAME-ARR                                           
070697         EXEC SQL                                                         
070697           CONNECT :USERNAME IDENTIFIED BY :PASSWD                        
070697         END-EXEC                                                         
070697         INITIALIZE CC-ORACLE-PASSWORD                                    
070697                    PASSWD                                                
070697     END-IF.                                                              
070697                                                                          
070697     IF (SQLCODE NOT = 0)                                                 
070697         DISPLAY '1200-CONNECT-TO-ORACLE'                                 
070697         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070697         PERFORM 9100-ORACLE-ERROR                                        
070697     END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
                                                                                
      *****************************************************************         
      * READ INPUT CONTROL CARD CONTAINING THE MONTH/NEW STORE INFO             
      ******************************************************************        
       2000-READ-CONTROL-CARD.                                                  
           MOVE '2000-READ-CONTROL-CARD          ' TO PV-PARAGRAPH-NAME.        
                                                                                
           READ CONTROL-FILE INTO WS-CONTROL-RECORD                             
                 AT END SET PS-END-OF-DATA TO TRUE.                             
                                                                                
           MOVE SPACES TO DATE-CONTROL-RECORD.                                  
           ADD 1 TO LOOP-COUNT-TMP.                                             
                                                                                
           IF NOT STORE-COUNT = 0                                               
                                                                                
               STRING '  OUTFIL FILES=',                                        
                   MONTH,                                                       
                   ',' DELIMITED BY SIZE                                        
                        INTO CNTL-OUTPUT                                        
               END-STRING                                                       
                                                                                
                                                                                
               WRITE DATE-CONTROL-RECORD FROM CNTL-OUTPUT                       
               MOVE SPACES TO DATE-CONTROL-RECORD                               
               MOVE SPACES TO CNTL-OUTPUT                                       
                                                                                
               PERFORM 3000-GET-MONTH-ABRV                                      
                                                                                
               PERFORM VARYING IDX FROM 1 BY 1 UNTIL IDX > STORE-COUNT          
                                                                                
                  MOVE STORE-ARR (IDX) (1:4) TO STORE-NUMBER                    
                  MOVE STORE-ARR (IDX) (6:2) TO VSG                             
                  MOVE ZERO TO STR-TALLY                                        
                                                                                
                  INSPECT FUNCTION REVERSE(STORE-NUMBER) TALLYING               
                                          STR-TALLY FOR LEADING SPACES          
                  COMPUTE TRIMMED-LEN =                                         
                           FUNCTION LENGTH(STORE-NUMBER) - STR-TALLY            
                  MOVE STORE-NUMBER(1:TRIMMED-LEN) TO                           
                                        PV-STORE-NBR-HOST                       
                  MOVE PV-STORE-NBR-HOST TO PV-STORE                            
                                                                                
                  PERFORM 4000-GET-MARKET-CLUSTER-NAME                          
                                                                                
                  PERFORM 5000-GET-CLUSTR-ABRV                                  
                                                                                
                  PERFORM 8000-CREATE-CNTL-CARDS                                
                                                                                
               END-PERFORM                                                      
           ELSE                                                                 
              IF LOOP-COUNT-TMP < 13                                            
                  STRING '  OUTFIL FILES=',                                     
                      MONTH, ',' DELIMITED BY SIZE                              
                           INTO CNTL-OUTPUT                                     
                  END-STRING                                                    
                                                                                
                  WRITE DATE-CONTROL-RECORD FROM CNTL-OUTPUT                    
                  MOVE SPACES TO DATE-CONTROL-RECORD                            
                  MOVE SPACES TO CNTL-OUTPUT                                    
                                                                                
                  MOVE CNTL-FILE-NO-STORES TO CNTL-OUTPUT                       
                  WRITE DATE-CONTROL-RECORD FROM CNTL-OUTPUT                    
                  MOVE SPACES TO CNTL-OUTPUT                                    
                                                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * SET UP THE SORT CONTROL CARD                                            
      ******************************************************************        
       2500-SET-UP-DATE-CNTL-CARD.                                              
                                                                                
           MOVE '  SORT FIELDS=(1,15,CH,A,17,5,CH,A,32,2,CH,A,190,5,CH,'        
                   TO CNTL-OUTPUT.                                              
           WRITE DATE-CONTROL-RECORD FROM CNTL-OUTPUT.                          
           MOVE '               A,88,3,CH,A,92,3,CH,A,23,8,ZD,A)'               
                   TO CNTL-OUTPUT.                                              
           WRITE DATE-CONTROL-RECORD FROM CNTL-OUTPUT.                          
                                                                                
           MOVE SPACES TO CNTL-OUTPUT.                                          
           MOVE '  SUM FIELDS=NONE' TO CNTL-OUTPUT.                             
           WRITE DATE-CONTROL-RECORD FROM CNTL-OUTPUT.                          
                                                                                
           MOVE SPACES TO CNTL-OUTPUT.                                          
                                                                                
      ******************************************************************        
      * GET THE MONTH ABREVIATION FOR THE MONTH BEING PROCESSED                 
      ******************************************************************        
       3000-GET-MONTH-ABRV.                                                     
           MOVE '3000-GET-MONTH-ABRV. ' TO PV-PARAGRAPH-NAME.                   
           EXEC SQL                                                             
              SELECT DISTINCT SUBSTR(FSCL_MN_NM,1,3)                            
              INTO :MONTH-ABRV                                                  
              FROM TDTATTR                                                      
              WHERE FSCL_MN_BEG_DTE = :PV-MNTH-BEG-DTE                          
              AND FSCL_MN_END_DTE =   :PV-MNTH-END-DTE                          
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
           WHEN ZERO                                                            
                 CONTINUE                                                       
           WHEN OTHER                                                           
                 MOVE '3000-GET-MONTH-ABRV.'                                    
                                      TO PV-DB2-OPERATION                       
                 PERFORM 9999-SQL-ABEND                                         
           END-EVALUATE.                                                        
      ******************************************************************        
      * GET THE MARKET CLUSTER ASSOCIATED WITH THE STORE NUMBER                 
      ******************************************************************        
       4000-GET-MARKET-CLUSTER-NAME.                                            
           MOVE '4000-GET-MARKET-CLUSTER-NAME' TO PV-PARAGRAPH-NAME.            
           EXEC SQL                                                             
              SELECT DISTINCT B.STR_GP_NM                                       
              INTO :STR-GP-NM-TMP                                               
              FROM XMT_STR_GP_MBSHP A                                           
                   ,XMT_STR_GP B                                                
              WHERE A.STR_GP_TYP_CDE = B.STR_GP_TYP_CDE                         
              AND A.STR_GP_ID = B.STR_GP_ID                                     
              AND A.STR_GP_TYP_CDE =  7000                                      
              AND A.EFF_BEG_DTE <= CURRENT_DATE                                 
              AND (A.EFF_END_DTE IS NULL OR                                     
                   CURRENT_DATE <= A.EFF_END_DTE)                               
              AND A.LOC_NBR = :PV-STORE-NBR-HOST                                
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
           WHEN ZERO                                                            
                 CONTINUE                                                       
           WHEN OTHER                                                           
                 MOVE '4000-GET-MARKET-CLUSTER-NAME.'                           
                                      TO PV-DB2-OPERATION                       
                 PERFORM 9999-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * GET THE MARKET CLUSTER ABBREVIATION                                     
      ******************************************************************        
       5000-GET-CLUSTR-ABRV.                                                    
           MOVE '5000-GET-CLUSTR-ABRV.' TO PV-PARAGRAPH-NAME.                   
                                                                                
           EXEC SQL                                                             
            SELECT DISTINCT SUBSTR                                              
                  (DISP_CLUSTR_NM, LENGTH(DISP_CLUSTR_NM) -1, 2)                
              INTO :MARKET-CLUSTER                                              
              FROM CUT_CLUSTR                                                   
              WHERE CLUSTR_NM = :STR-GP-NM-TMP                                  
              AND OWNR_TXT = 'CORP'                                             
              AND DISP_CLUSTR_NM IS NOT NULL                                    
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
           WHEN ZERO                                                            
                 CONTINUE                                                       
           WHEN OTHER                                                           
                 MOVE '3000-GET-MONTH-ABRV.'                                    
                                      TO PV-DB2-OPERATION                       
                 PERFORM 9999-SQL-ABEND                                         
           END-EVALUATE.                                                        
      ******************************************************************        
      * CREATE THE CONTROL CARDS                                                
      ******************************************************************        
       8000-CREATE-CNTL-CARDS.                                                  
                                                                                
           MOVE PV-STORE-TMP TO STORE-NUMBER.                                   
           WRITE VPS-REC-01 FROM WS-OUTPUT-RECORD.                              
                                                                                
           IF IDX = 1                                                           
               STRING CNTL-FILE-STORES, ',',                                    
                      STORE-NUMBER,                                             
                      ',OR,' DELIMITED BY SIZE                                  
                      INTO CNTL-OUTPUT                                          
               END-STRING                                                       
           END-IF                                                               
           IF IDX = STORE-COUNT                                                 
               STRING '           17,5,ZD,EQ,'                                  
                      STORE-NUMBER,                                             
                      ')' DELIMITED BY SIZE                                     
                      INTO CNTL-OUTPUT                                          
               END-STRING                                                       
           END-IF                                                               
                                                                                
           IF IDX > 1 AND IDX < STORE-COUNT                                     
               STRING '           17,5,ZD,EQ,'                                  
                      STORE-NUMBER,                                             
                      ',OR,' DELIMITED BY SIZE                                  
                      INTO CNTL-OUTPUT                                          
               END-STRING                                                       
           END-IF                                                               
                                                                                
           WRITE DATE-CONTROL-RECORD FROM CNTL-OUTPUT.                          
           MOVE SPACES TO DATE-CONTROL-RECORD.                                  
           MOVE SPACES TO CNTL-OUTPUT.                                          
                                                                                
      **************************************************                        
      *  DISPLAY OUTPUT FILE COUNTS AT END OF PROGRAM                           
      **************************************************                        
       9000-PROGRAM-END.                                                        
           CLOSE VPS-FILE-01.                                                   
           CLOSE CONTROL-FILE.                                                  
           CLOSE DATE-CONTROL-FILE.                                             
                                                                                
      ******************************************************************        
      *   FORMAT AN ORACLE ERROR MESSAGE AND ABEND THE PROGRAM. *               
      ******************************************************************        
       9100-ORACLE-ERROR.                                                       
                                                                                
           MOVE SQLCODE    TO SQLCODE2.                                         
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           DISPLAY '** ABEND **'                                                
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
                                                                                
           MOVE SPACES TO WS-MSG-TEXT.                                          
      *    GET FULL TEXT OF ERROR MESSAGE                                       
           CALL 'SQLGLM' USING WS-MSG-TEXT,                                     
                               WS-MAX-SIZE,                                     
                               WS-MSG-LENGTH.                                   
                                                                                
           DISPLAY 'MESSAGE TEXT      ** = ' WS-MSG-TEXT.                       
                                                                                
           EXEC SQL                                                             
            ROLLBACK WORK RELEASE                                               
           END-EXEC.                                                            
                                                                                
           SET AC-ORACLE-ERROR   TO TRUE                                        
           CALL 'ILBOABN0' USING ABEND-CODE-1.                                  
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
        9999-SQL-ABEND.                                                         
            MOVE SQLCODE        TO  SQLCODE2.                                   
            MOVE SQLERRD(3)     TO  SQLERRD3.                                   
            DISPLAY '***************************'                               
            DISPLAY '**       ABEND           **'                               
            DISPLAY '**  IN PROGRAM ' PC-PROGRAM-NAME '  **'.                   
            DISPLAY '***************************'                               
            DISPLAY '** OPERATION **      = ' PV-DB2-OPERATION                  
            DISPLAY 'PARAGRAPH            = ' PV-PARAGRAPH-NAME                 
            DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                         
            DISPLAY '** SQLCODE **        = ' SQLCODE2.                         
            MOVE +4013         TO PV-ABEND-CODE                                 
            CALL 'ILBOABN0' USING PV-ABEND-CODE.                                
      ****************************************************************          
      *  COPY BOOK CONTAINS THE PROCEDURE DIVISION STATEMENTS      *            
      *    THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         *          
      *    "DSNTIAR" AND CONVERT THE SQLCA INFORMATION INTO A        *          
      *    READABLE FORMAT.                                          *          
      ****************************************************************          
           COPY DPPD004.                                                        
                                                                                
