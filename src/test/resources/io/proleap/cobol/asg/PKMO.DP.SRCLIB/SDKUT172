       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    SDKUT172.                                         00020001
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  10/08/2012.                                       00050001
       DATE-COMPILED.                                                   00060000
                                                                        00061001
      ******************************************************************00070000
      * DELTA PROCESSING FOR PAYROLL WEEKLY STORE ADJUSTMENTS           00080013
      *    COMPARES PREVIOUS VS CURRENT EXTRACT FILES AND WRITES        00090013
      *    A CHANGE RECORD FOR ADDS, UPDATES, AND DELETES.              00100013
      ******************************************************************00110000
                                                                        00131005
       ENVIRONMENT DIVISION.                                            00140000
       CONFIGURATION SECTION.                                           00150000
       SOURCE-COMPUTER.    IBM-3090.                                    00160000
       OBJECT-COMPUTER.    IBM-3090.                                    00170000
       INPUT-OUTPUT SECTION.                                            00180000
       FILE-CONTROL.                                                    00190000
                                                                        00200000
           SELECT CURR-EXTRACT-FILE                                     00210005
                  ASSIGN TO INP01.                                      00220000
           SELECT PREV-EXTRACT-FILE                                     00230005
                  ASSIGN TO INP02.                                      00240000
           SELECT DELTA-FILE                                            00270003
                  ASSIGN TO OUT01.                                      00280000
           SELECT DELTA-COUNT-FILE                                      00281003
                  ASSIGN TO OUT02.                                      00282002
                                                                        00290000
       DATA DIVISION.                                                   00300000
       FILE SECTION.                                                    00310000
                                                                        00320000
       FD  CURR-EXTRACT-FILE                                            00330005
           RECORDING MODE IS F                                          00340000
           LABEL RECORDS OMITTED                                        00350000
           BLOCK CONTAINS 0 RECORDS                                     00360000
           DATA RECORD IS CURR-EXTRACT-RECORD.                          00370005
       01  CURR-EXTRACT-RECORD                 PIC X(80).               00380005
                                                                        00390000
       FD  PREV-EXTRACT-FILE                                            00400005
           RECORDING MODE IS F                                          00410000
           LABEL RECORDS OMITTED                                        00420000
           BLOCK CONTAINS 0 RECORDS                                     00430000
           DATA RECORD IS PREV-EXTRACT-RECORD.                          00440005
       01  PREV-EXTRACT-RECORD                 PIC X(80).               00450005
                                                                        00460000
       FD  DELTA-FILE                                                   00513003
           RECORDING MODE IS F                                          00514000
           LABEL RECORDS OMITTED                                        00515000
           BLOCK CONTAINS 0 RECORDS                                     00516000
           DATA RECORD IS DELTA-RECORD.                                 00517005
       01  DELTA-RECORD                        PIC X(41).               00518014
                                                                        00518102
       FD  DELTA-COUNT-FILE                                             00518203
           RECORDING MODE IS F                                          00518302
           LABEL RECORDS OMITTED                                        00518402
           BLOCK CONTAINS 0 RECORDS                                     00518502
           DATA RECORD IS DELTA-COUNT-RECORD.                           00518603
       01  DELTA-COUNT-RECORD                  PIC 9(07).               00518703
                                                                        00519000
       WORKING-STORAGE SECTION.                                         00520000
      ******************************************************************00530000
      **PROGRAM SWITCHES                                                00540000
      ******************************************************************00550000
       01  PS-SWITCH-AREAS.                                             00560000
           05  PS-END-OF-CUR-FILE-SW           PIC X(01)  VALUE 'N'.    00570000
               88  PS-END-OF-CUR-FILE                     VALUE 'Y'.    00580000
               88  PS-NOT-END-OF-CUR-FILE                 VALUE 'N'.    00590000
           05  PS-END-OF-PRV-FILE-SW           PIC X(01)  VALUE 'N'.    00600000
               88  PS-END-OF-PRV-FILE                     VALUE 'Y'.    00610000
               88  PS-NOT-END-OF-PRV-FILE                 VALUE 'N'.    00620000
      ******************************************************************00630000
      **DATE TIME FIELDS                                                00640000
      ******************************************************************00650000
       01 DF-DATE-TIME-FIELDS.                                          00660000
          05 DF-ACCEPT-DATE                    PIC X(06) VALUE SPACES.  00670000
          05 DF-ACCEPT-TIME                    PIC X(08) VALUE SPACES.  00680000
      ******************************************************************00690000
      **PROGRAM ACCUMULATORS (COUNTERS)                                 00700000
      ******************************************************************00710000
       01  PA-PROGRAM-ACCUMULATORS.                                     00720000
           05  PA-CUR-READ                     PIC 9(07) VALUE ZEROS.   00730005
           05  PA-PRV-READ                     PIC 9(07) VALUE ZEROS.   00740005
           05  PA-MATCH-COUNT                  PIC 9(07) VALUE ZEROS.   00780105
           05  PA-DATE-OOB-COUNT               PIC 9(07) VALUE ZEROS.   00780205
           05  PA-WRITE-ADD-COUNT              PIC 9(07) VALUE ZEROS.   00780405
           05  PA-WRITE-UPD-COUNT              PIC 9(07) VALUE ZEROS.   00780505
           05  PA-WRITE-DLT-COUNT              PIC 9(07) VALUE ZEROS.   00780605
           05  PA-TTL-WRITE-COUNT              PIC 9(07) VALUE ZEROS.   00780705
                                                                        00780805
      ******************************************************************00780905
      **PROGRAM VARIABLES                                               00781005
      ******************************************************************00781105
       01  PV-PROGRAM-VARIABLES.                                        00781205
           05  PV-COUNT-RECORD.                                         00781305
               10  PV-RECORDS-WRITTEN-TO-FILE  PIC 9(07) VALUE ZEROES.  00781413
                                                                        00781505
       01  PV-FISCAL-PERIOD-DATES.                                      00781613
           05  PV-CUR-FP-1ST-DTE               PIC X(10) VALUE SPACES.  00781713
           05  PV-CUR-FP-LAST-DTE              PIC X(10) VALUE SPACES.  00781813
           05  PV-PRV-FP-1ST-DTE               PIC X(10) VALUE SPACES.  00781913
           05  PV-PRV-FP-LAST-DTE              PIC X(10) VALUE SPACES.  00782013
           05  PV-NXT-FP-1ST-DTE               PIC X(10) VALUE SPACES.  00782113
           05  PV-NXT-FP-LAST-DTE              PIC X(10) VALUE SPACES.  00782213
                                                                        00782313
      ******************************************************************00782413
      **PRIMARY KEYS USED TO MATCH UP RECORDS                           00782513
      ******************************************************************00782613
       01  PV-PRIMARY-KEYS.                                             00782713
           05  PV-CUR-KEY.                                              00782813
               10  PV-CUR-STR-NBR              PIC 9(04) VALUE ZEROES.  00782913
               10  PV-CUR-WK-END-DTE           PIC X(10) VALUE SPACES.  00783013
           05  PV-PRV-KEY.                                              00786000
               10  PV-PRV-STR-NBR              PIC 9(04) VALUE ZEROES.  00786113
               10  PV-PRV-WK-END-DTE           PIC X(10) VALUE SPACES.  00786213
                                                                        00786305
      ******************************************************************00786405
      *               SYSTEM TIME AND DATE VARIABLES                    00786505
      ******************************************************************00786605
       01  SYS-DATE-TIME.                                               00786705
           05  SYS-DATE                        PIC X(08) VALUE SPACES.  00786813
           05  SYS-TIME                        PIC X(08) VALUE SPACES.  00786913
                                                                        00787005
       01  ST-BEG-TIME.                                                 00787105
           05  ST-BEG-HR                       PIC 9(02) VALUE ZEROS.   00788013
           05  FILLER                          PIC X(01) VALUE ':'.     00789013
           05  ST-BEG-MN                       PIC 9(02) VALUE ZEROS.   00789113
                                                                        00789205
       01  ST-END-TIME.                                                 00789305
           05  ST-END-HR                       PIC 9(02) VALUE ZEROS.   00789413
           05  FILLER                          PIC X(01) VALUE ':'.     00789513
           05  ST-END-MN                       PIC 9(02) VALUE ZEROS.   00789613
                                                                        00789705
       01  SD-EDIT-DATE.                                                00789805
           05  SD-MONTH                        PIC 9(02) VALUE ZEROS.   00789913
           05  FILLER                          PIC X(01) VALUE '/'.     00790013
           05  SD-DAY                          PIC 9(02) VALUE ZEROS.   00790113
           05  FILLER                          PIC X(01) VALUE '/'.     00790213
           05  SD-CENT                         PIC 9(02) VALUE ZEROS.   00790313
           05  SD-YEAR                         PIC 9(02) VALUE ZEROS.   00790413
                                                                        00790505
      ****************************************************************  00791800
      **ABEND INFORMATION                                               00791900
      ****************************************************************  00792000
       01  AC-ABEND-FIELDS.                                             00793000
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    00800000
                                                           COMP SYNC.   00810000
               88  AC-DATE-ROUTINE-ERROR                 VALUE +4019.   00830111
                                                                        00831005
       01  PV-MISC-ABEND-FIELDS.                                        00840000
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   00850000
           05  PV-OPERATION-MSG                PIC X(40) VALUE SPACE.   00860011
                                                                        00870013
      ******************************************************************00890000
      **COPYBOOKS                                                       00900000
      ******************************************************************00910000
      ****BOTH INPUT FILES USE THE SAME COPYBOOK, THIS MAKES THEM UNIQUE00930000
           COPY SDRD171 REPLACING 'SDRD171-' BY 'SD171-CUR-'.           00940009
           COPY SDRD171 REPLACING 'SDRD171-' BY 'SD171-PRV-'.           00950009
                                                                        00951013
      ****OUTPUT RECORD LAYOUT ALSO USES THE SAME COPYBOOK, BUT SETS    00960013
      *   THE APPROPRIATE CHANGE CODE (A/U/D) WHEN WRITING BACK OUT.    00961013
           COPY SDRD171 REPLACING 'SDRD171-' BY 'SD171-OUT-'.           00970009
                                                                        00970106
      ******************************************************************00971006
      * PARAMETER LIST FOR THE CALENDAR ROUTINE                         00972006
      ******************************************************************00973006
           COPY DPWS500.                                                00974006
                                                                        00975006
                                                                        00980000
       PROCEDURE DIVISION.                                              00990000
      ******************************************************************01000000
       0000-MAINLINE.                                                   01010000
                                                                        01020000
           PERFORM 1000-INITIALIZATION.                                 01030000
                                                                        01031000
           PERFORM 2000-PROCESS-RECORDS                                 01032000
                   UNTIL PS-END-OF-CUR-FILE                             01033000
                      OR PS-END-OF-PRV-FILE.                            01034000
                                                                        01035000
           PERFORM 2010-PROCESS-EXTRA-RECORDS                           01036017
                   UNTIL PS-END-OF-CUR-FILE                             01037000
                     AND PS-END-OF-PRV-FILE.                            01038000
                                                                        01039000
           PERFORM 9000-WRAP-UP.                                        01040016
           GOBACK.                                                      01050000
                                                                        01060000
      ******************************************************************01070000
      **INITIALIZATION ROUTINE                                          01080000
      ******************************************************************01090000
       1000-INITIALIZATION.                                             01100000
                                                                        01110000
           DISPLAY '*************************************'.             01111013
           DISPLAY '* SDKUT172 - SDT_WKLY_STR_ADJ       *'.             01112013
           DISPLAY '*            DELTA FILE PROCESSING  *'.             01113013
           DISPLAY '*************************************'.             01114013
           DISPLAY '* PAYROLL WEEKLY STORE ADJUSTMENTS  *'.             01115013
           DISPLAY '*************************************'.             01116013
           DISPLAY '*'.                                                 01117013
           DISPLAY '* - PROCESSING...'.                                 01118013
                                                                        01119013
           OPEN INPUT  CURR-EXTRACT-FILE                                01120005
                       PREV-EXTRACT-FILE.                               01130005
           OPEN OUTPUT DELTA-FILE.                                      01150005
                                                                        01151005
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01170000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01180000
                                                                        01180105
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 01181005
                                                                        01182005
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            01183005
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            01184005
                                                                        01185005
           MOVE SYS-DATE (1:2) TO SD-CENT.                              01186005
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              01187005
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             01188005
           MOVE SYS-DATE (7:2) TO SD-DAY.                               01189005
                                                                        01189105
           PERFORM 1100-DETERMINE-DATES.                                01189205
                                                                        01190000
           PERFORM 4000-READ-CUR-EXTR-RECORD.                           01210002
           PERFORM 4100-READ-PRV-EXTR-RECORD.                           01220002
                                                                        01230000
      ******************************************************************01240005
      * DETERMINE FISCAL MONTH START / END DATES                        01241005
      ******************************************************************01242005
       1100-DETERMINE-DATES.                                            01243005
                                                                        01244005
           MOVE '1100-DETERMINE-DATES' TO PV-PARAGRAPH-NAME.            01245005
           DISPLAY 'SYSTEM DATE: ' SD-EDIT-DATE.                        01246005
                                                                        01247005
      ***GET START/END DATES OF CURRENT FISCAL MONTH                    01247105
                                                                        01247205
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01247305
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       01247405
           SET DPG52-LK-DTE-GREG-CC-FMT  TO TRUE.                       01247505
           MOVE SD-EDIT-DATE             TO DPG52-GREG-CC-DATE-FMT.     01247605
                                                                        01247705
           DISPLAY 'DPG52-GREG-CC-DATE = ' DPG52-GREG-CC-DATE-FMT.      01247805
                                                                        01247905
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                01248005
                                                   DPG52                01248105
                                                   DPG53                01248205
                                                   DPG54                01248305
                                                   DPG55                01248405
                                                   DPG56.               01248505
                                                                        01248605
           EVALUATE TRUE                                                01248705
               WHEN DPG54-NO-ERROR                                      01248805
                   MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO PV-CUR-FP-1ST-DTE 01248905
                   MOVE DPG56-LAST-FP-DB2-ISO-FMT TO PV-CUR-FP-LAST-DTE 01249005
               WHEN OTHER                                               01249105
                   DISPLAY 'ERROR GETTING FISCAL MONTH START DATE'      01249205
                   PERFORM Z5000-ERROR-ROUTINE                          01249305
           END-EVALUATE.                                                01249405
                                                                        01249505
      ***GET START/END DATES OF PREVIOUS FISCAL MONTH                   01249605
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01249705
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       01249805
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                       01249905
           MOVE PV-CUR-FP-1ST-DTE        TO DPG52-DB2-ISO-DATE-FMT.     01250005
           SET DPG51-DECREMENT-DATE      TO TRUE.                       01250105
           MOVE 1                        TO DPG51-INCR-DECR-DAYS-9.     01250205
                                                                        01250305
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                01250405
                                                   DPG52                01250505
                                                   DPG53                01250605
                                                   DPG54                01250705
                                                   DPG55                01250805
                                                   DPG56.               01250905
                                                                        01251005
           EVALUATE TRUE                                                01251105
               WHEN DPG54-NO-ERROR                                      01251205
                   MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO PV-PRV-FP-1ST-DTE 01251305
                   MOVE DPG56-LAST-FP-DB2-ISO-FMT  TO PV-PRV-FP-LAST-DTE01251405
               WHEN OTHER                                               01251505
                   DISPLAY 'ERROR GETTING PREV FISCAL MONTH DATES'      01251605
                   PERFORM Z5000-ERROR-ROUTINE                          01251705
           END-EVALUATE.                                                01251805
                                                                        01251905
      ***GET START/END DATES OF NEXT FISCAL MONTH                       01252005
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01252105
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       01252205
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                       01252305
           MOVE PV-CUR-FP-LAST-DTE       TO DPG52-DB2-ISO-DATE-FMT.     01252405
           SET DPG51-INCREMENT-DATE      TO TRUE.                       01252505
           MOVE 1                        TO DPG51-INCR-DECR-DAYS-9.     01252605
                                                                        01252705
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                01252805
                                                   DPG52                01252905
                                                   DPG53                01253005
                                                   DPG54                01253105
                                                   DPG55                01253205
                                                   DPG56.               01253305
                                                                        01253405
           EVALUATE TRUE                                                01253505
               WHEN DPG54-NO-ERROR                                      01253605
                   MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO PV-NXT-FP-1ST-DTE 01253705
                   MOVE DPG56-LAST-FP-DB2-ISO-FMT  TO PV-NXT-FP-LAST-DTE01253805
               WHEN OTHER                                               01253905
                   DISPLAY 'ERROR GETTING NEXT FISCAL MONTH DATES'      01254005
                   PERFORM Z5000-ERROR-ROUTINE                          01254105
           END-EVALUATE.                                                01254205
                                                                        01254305
           DISPLAY 'PAYROLL WKLY ADJ BEGIN DATE: ' PV-PRV-FP-1ST-DTE.   01255205
           DISPLAY '........THROUGH ENDING DATE: ' PV-NXT-FP-LAST-DTE.  01255305
           DISPLAY '...ONLY COMPARING RECORDS IN THIS RANGE.'.          01255405
           DISPLAY '...'.                                               01255505
                                                                        01255605
      ******************************************************************01255705
      **COMPARES THE INPUT FILES AND IDENTIFIES ADDS, DELETES, AND CHG  01255805
      ******************************************************************01256000
       2000-PROCESS-RECORDS.                                            01260000
                                                                        01270000
           EVALUATE TRUE                                                01280000
      *--> KEY DATA MATCHES, SO CHECK NON-KEY DATA VALUES               01281005
              WHEN PV-CUR-KEY EQUAL PV-PRV-KEY                          01282000
                   IF SD171-CUR-PYRL-ADJ-AMT NOT EQUAL TO               01282102
                      SD171-PRV-PYRL-ADJ-AMT                            01282202
                      OR SD171-CUR-PYRL-ADJ-HR-QTY NOT EQUAL TO         01282302
                         SD171-PRV-PYRL-ADJ-HR-QTY                      01282402
      *--> CURRENT FILE MATCHES ON KEY, BUT NON-KEY DOESN'T MATCH       01282505
                     PERFORM 2200-WRITE-CURRENT-UPDATE                  01282705
                   ELSE                                                 01282802
                     ADD +1 TO PA-MATCH-COUNT                           01283005
                   END-IF                                               01284002
                   PERFORM 4000-READ-CUR-EXTR-RECORD                    01285007
                   PERFORM 4100-READ-PRV-EXTR-RECORD                    01286007
      *--> CURRENT FILE HAS AN ADDITIONAL RECORD, SO LIST AS 'A'        01287005
              WHEN PV-CUR-KEY LESS THAN PV-PRV-KEY                      01288000
                   PERFORM 2100-WRITE-CURRENT-ADD                       01289005
                   PERFORM 4000-READ-CUR-EXTR-RECORD                    01290007
      *--> CURRENT FILE IS MISSING A PREV RECORD, SO LIST AS 'D'        01291005
              WHEN PV-CUR-KEY GREATER THAN PV-PRV-KEY                   01291202
                   PERFORM 2300-WRITE-PREVIOUS-DELETE                   01291305
                   PERFORM 4100-READ-PRV-EXTR-RECORD                    01291407
           END-EVALUATE.                                                01291502
                                                                        01291602
      ******************************************************************01291702
      * 2010-PROCESS-EXTRA-RECORDS.                                     01291817
      *     PROCESSES THRU END OF FILE FOR REMAINING RECORDS.           01291902
      ******************************************************************01292002
       2010-PROCESS-EXTRA-RECORDS.                                      01292117
                                                                        01293000
      * WHEN CUR EXTRACT FILE STILL HAS RECORDS... COUNT AS 'A'         01293105
           IF  PS-NOT-END-OF-CUR-FILE                                   01293200
               PERFORM 2100-WRITE-CURRENT-ADD                           01293305
               PERFORM 4000-READ-CUR-EXTR-RECORD                        01293407
           END-IF.                                                      01293500
                                                                        01293600
      * WHEN PRV EXTRACT FILE STILL HAS RECORDS... COUNT AS 'D'         01293705
           IF  PS-NOT-END-OF-PRV-FILE                                   01293902
               PERFORM 2300-WRITE-PREVIOUS-DELETE                       01294005
               PERFORM 4100-READ-PRV-EXTR-RECORD                        01294107
           END-IF.                                                      01294302
                                                                        01297000
      ******************************************************************01400000
      **WRITE CURRENT RECORD - ADD                                      01410005
      ******************************************************************01420000
       2100-WRITE-CURRENT-ADD.                                          01430005
                                                                        01440000
           MOVE SD171-CUR-EXTRACT-RECORD TO SD171-OUT-EXTRACT-RECORD.   01450005
           MOVE 'A' TO SD171-OUT-CHANGE-ACTN-CDE.                       01460005
                                                                        01480000
           IF SD171-OUT-WK-END-DTE >= PV-PRV-FP-1ST-DTE                 01481012
            AND SD171-OUT-WK-END-DTE <= PV-NXT-FP-LAST-DTE              01482012
              WRITE DELTA-RECORD FROM SD171-OUT-EXTRACT-RECORD          01490012
              ADD +1 TO PA-WRITE-ADD-COUNT                              01491012
              ADD +1 TO PA-TTL-WRITE-COUNT                              01500012
           ELSE                                                         01510012
              ADD +1 TO PA-DATE-OOB-COUNT                               01510112
              DISPLAY 'SKIPPED; DATE OOB: ' SD171-OUT-EXTRACT-RECORD    01510212
           END-IF.                                                      01510312
                                                                        01511005
      ******************************************************************01520005
      **WRITE CURRENT RECORD - UPDATE                                   01530005
      ******************************************************************01540005
       2200-WRITE-CURRENT-UPDATE.                                       01541005
                                                                        01542005
           MOVE SD171-CUR-EXTRACT-RECORD TO SD171-OUT-EXTRACT-RECORD.   01543005
           MOVE 'U' TO SD171-OUT-CHANGE-ACTN-CDE.                       01544005
                                                                        01545005
           IF SD171-OUT-WK-END-DTE >= PV-PRV-FP-1ST-DTE                 01545112
            AND SD171-OUT-WK-END-DTE <= PV-NXT-FP-LAST-DTE              01545212
              WRITE DELTA-RECORD FROM SD171-OUT-EXTRACT-RECORD          01546012
              ADD +1 TO PA-WRITE-UPD-COUNT                              01546112
              ADD +1 TO PA-TTL-WRITE-COUNT                              01547012
           ELSE                                                         01548012
              ADD +1 TO PA-DATE-OOB-COUNT                               01548112
              DISPLAY 'SKIPPED; DATE OOB: ' SD171-OUT-EXTRACT-RECORD    01548212
           END-IF.                                                      01548312
                                                                        01548405
      ******************************************************************01549005
      **WRITE PREVIOUS RECORD - DELETE                                  01549105
      ******************************************************************01549205
       2300-WRITE-PREVIOUS-DELETE.                                      01549305
                                                                        01549405
           MOVE SD171-PRV-EXTRACT-RECORD TO SD171-OUT-EXTRACT-RECORD.   01549705
           MOVE 'D' TO SD171-OUT-CHANGE-ACTN-CDE.                       01549805
                                                                        01549912
           IF SD171-OUT-WK-END-DTE >= PV-PRV-FP-1ST-DTE                 01550012
            AND SD171-OUT-WK-END-DTE <= PV-NXT-FP-LAST-DTE              01550112
              WRITE DELTA-RECORD FROM SD171-OUT-EXTRACT-RECORD          01550212
              ADD +1 TO PA-WRITE-DLT-COUNT                              01550312
              ADD +1 TO PA-TTL-WRITE-COUNT                              01551012
           ELSE                                                         01551112
              ADD +1 TO PA-DATE-OOB-COUNT                               01551212
              DISPLAY 'SKIPPED; DATE OOB: ' SD171-OUT-EXTRACT-RECORD    01551312
           END-IF.                                                      01552012
                                                                        01560000
      ******************************************************************01640000
      **READ CURR FILE RECORD                                           01650005
      ******************************************************************01660000
       4000-READ-CUR-EXTR-RECORD.                                       01670007
                                                                        01680000
           READ CURR-EXTRACT-FILE                                       01690005
              INTO SD171-CUR-EXTRACT-RECORD                             01700005
                 AT END                                                 01710000
                 SET PS-END-OF-CUR-FILE TO TRUE                         01720000
           END-READ.                                                    01730000
                                                                        01740000
      * MOVE KEYS                                                       01741005
           IF  PS-NOT-END-OF-CUR-FILE                                   01750000
               ADD +1                       TO PA-CUR-READ              01760000
               MOVE SD171-CUR-STR-NBR       TO PV-CUR-STR-NBR           01761011
               MOVE SD171-CUR-WK-END-DTE    TO PV-CUR-WK-END-DTE        01762011
           END-IF.                                                      01766000
                                                                        01767000
      ******************************************************************01768000
      **READ PRICE CHANGE RECORD FROM THE PREVIOUS REPORT FILE          01769000
      ******************************************************************01770000
       4100-READ-PRV-EXTR-RECORD.                                       01780007
                                                                        01781000
           READ PREV-EXTRACT-FILE                                       01781105
              INTO SD171-PRV-EXTRACT-RECORD                             01781205
                 AT END                                                 01781300
                 SET PS-END-OF-PRV-FILE TO TRUE                         01781400
           END-READ.                                                    01781500
                                                                        01781600
      * MOVE KEYS                                                       01781705
           IF  PS-NOT-END-OF-PRV-FILE                                   01781800
               ADD +1                       TO PA-PRV-READ              01781900
               MOVE SD171-PRV-STR-NBR       TO PV-PRV-STR-NBR           01782011
               MOVE SD171-PRV-WK-END-DTE    TO PV-PRV-WK-END-DTE        01782111
           END-IF.                                                      01782700
                                                                        01782800
      ******************************************************************01782906
      * DATE-ERROR-ROUTINE.                                             01783006
      ******************************************************************01784006
       Z5000-ERROR-ROUTINE.                                             01785006
                                                                        01786006
           PERFORM 9999-DISPLAY-STATISTICS.                             01786106
                                                                        01786206
           DISPLAY 'PARAGRAPH: ' PV-PARAGRAPH-NAME.                     01786310
           DISPLAY 'ERROR: ' PV-OPERATION-MSG.                          01786410
                                                                        01786706
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         01786811
                                                                        01786906
      ******************************************************************01787000
      **CLOSE FILES                                                     01788000
      ******************************************************************01789000
       9000-WRAP-UP.                                                    01790016
                                                                        01791003
      *    UPDATE THE DELTA COUNT FILE WITH NUMBER OF RECORDS WRITTEN   01791105
           OPEN OUTPUT DELTA-COUNT-FILE.                                01791205
           WRITE DELTA-COUNT-RECORD FROM PA-TTL-WRITE-COUNT.            01792010
           CLOSE DELTA-COUNT-FILE.                                      01793005
                                                                        01800000
           CLOSE CURR-EXTRACT-FILE                                      01810005
                 PREV-EXTRACT-FILE                                      01820005
                 DELTA-FILE.                                            01830005
                                                                        01850000
           PERFORM 9999-DISPLAY-STATISTICS.                             01860000
                                                                        01870000
      ******************************************************************01880000
      **DISPLAY PROGRAM STATS                                           01890000
      ******************************************************************01900000
       9999-DISPLAY-STATISTICS.                                         01910000
                                                                        01920000
           DISPLAY '*************************************************'. 01930000
           DISPLAY '* PROGRAM...........: SDKUT172'.                    01940001
           DISPLAY '* DATE..............: ' DF-ACCEPT-DATE.             01950000
           DISPLAY '* TIME..............: ' DF-ACCEPT-TIME.             01960000
           DISPLAY '*************************************************'. 01970000
           DISPLAY '* CUR INP RECORDS READ: ' PA-CUR-READ.              01980000
           DISPLAY '* PRV INP RECORDS READ: ' PA-PRV-READ.              01990000
           DISPLAY '*************************************************'. 02000000
           DISPLAY '* DELTA PROCESSING...'.                             02010002
           DISPLAY '* - ADD RECORDS COUNT: ' PA-WRITE-ADD-COUNT.        02020010
           DISPLAY '* - UPD RECORDS COUNT: ' PA-WRITE-UPD-COUNT.        02021010
           DISPLAY '* - DLT RECORDS COUNT: ' PA-WRITE-DLT-COUNT.        02030010
           DISPLAY '*************************************************'. 02070000
           DISPLAY '* - TOTAL WRITE COUNT: ' PA-TTL-WRITE-COUNT.        02070105
           DISPLAY '*************************************************'. 02071005
           DISPLAY '* MATCHED RECORDS, NOT NEEDED: ' PA-MATCH-COUNT.    02080004
           DISPLAY '* SKIPPED DUE TO DATE OOB....: ' PA-DATE-OOB-COUNT. 02081004
           DISPLAY '*************************************************'. 02090000
                                                                        02100000
      ******************************************************************02110000
