      ***************************************************************** 00010001
       IDENTIFICATION DIVISION.                                         00020001
      ***************************************************************** 00030001
                                                                        00040001
       PROGRAM-ID.             XMKUP002.                                00050006
       AUTHOR.                 PAUL KEBER.                              00060006
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070001
       DATE-WRITTEN            11/29/2005.                              00080001
       DATE-COMPILED.                                                   00090001
                                                                        00100001
      ******************************************************************00110007
      *    XMKUP002 - RELOAD THE XMT_TKT_FMT TABLE.                     00120007
      ******************************************************************00110007
      *  THIS PROGRAM WILL DELETE ALL EXISTING ROWS IN THE XMT_TKT_FMT  00120007
      *  TABLE, THEN IT WILL INSERT ROWS USING DATA FROM THE TICKET     00120007
      *  FORMAT EXTRACT FILE CREATED IN XLK067.                         00120007
      ******************************************************************00240001
      *---------------------------------------------------------------*         
      *    WR/PITS#     DATE        DESCRIPTION                       *         
      *    --------   ----------   ---------------------------------  *         
      *    WR29552    11/29/2005   INITIAL CREATION.                  *         
      *---------------------------------------------------------------*         
                                                                        00250001
      ***************************************************************** 00260001
       ENVIRONMENT DIVISION.                                            00270001
      ***************************************************************** 00280001
                                                                        00290001
       CONFIGURATION SECTION.                                           00300001
                                                                        00310001
       SOURCE-COMPUTER.        IBM-3090.                                00320001
       OBJECT-COMPUTER.        IBM-3090.                                00330001
                                                                        00340001
       INPUT-OUTPUT SECTION.                                            00350001
                                                                        00360001
       FILE-CONTROL.                                                    00370001
                                                                        00380001
           SELECT TKT-FMT-EXTRACT-FILE                                  00390008
               ASSIGN TO INP01.                                         00400008
                                                                        00410001
      ******************************************************************00420001
       DATA DIVISION.                                                   00430001
      ******************************************************************00440001
                                                                        00450001
       FILE SECTION.                                                    00460001
                                                                        00470001
       FD  TKT-FMT-EXTRACT-FILE                                         00480008
           RECORDING MODE IS F                                          00490001
           BLOCK CONTAINS 0 RECORDS                                     00500001
           LABEL RECORDS ARE STANDARD.                                  00510001
                                                                        00520001
       01  TKT-FMT-EXTRACT-RECORD    PIC X(100).                        00530008
                                                                        00540001
      ******************************************************************00550001
       WORKING-STORAGE SECTION.                                         00560001
      ******************************************************************00570001
                                                                        00580001
       01  FILLER                           PIC X(50)  VALUE            00610001
           ' *** WORKING STORAGE STARTS HERE FOR XMKUP002 *** '.        00620006
                                                                        00630007
                                                                        00640007
       01  ABEND-CODE                       PIC S9(04) VALUE +0 COMP.   00650007
           88  PV-ABEND-4001                   VALUE +4001.                     
      *        EMPTY TICKET FORMAT EXTRACT FILE                                 
           88  PV-ABEND-4013                   VALUE +4013.                     
      *        DB2 SQL ABEND                                                    
                                                                        00680007
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECORDS-READ              PIC 9(07)  VALUE ZERO.              
           05  PA-ROWS-INSERTED             PIC 9(07)  VALUE ZERO.              
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                        00720007
           05  PC-CURRENT-PROGRAM-NAME      PIC X(8)   VALUE 'XMKUP002'.00730007
           05  PC-MAXIMUM-RETRY-COUNT       PIC S9(04) VALUE +2 COMP.           
                                                                        00740007
       01  PS-PROGRAM-SWITCHES.                                         00750007
           05  PS-END-OF-FILE-SW            PIC X(1)   VALUE 'N'.       00770007
               88  PS-NOT-END-OF-FILE                  VALUE 'N'.       00780007
               88  PS-END-OF-FILE                      VALUE 'Y'.       00790007
                                                                        00800007
       01  PV-PROGRAM-VARIABLES.                                        00750007
           05  PV-RETRY-COUNT               PIC S9(04) VALUE +0 COMP.           
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.            
           05  PV-DB2-OPERATION-ATTEMPTED   PIC  X(30) VALUE SPACES.            
           05  PV-SQLCODE-DISPLAY           PIC +(8)9  VALUE ZEROES.    00830000
                                                                        00800007
      ******************************************************************00900007
      *  COPYBOOK FOR TICKET FORMAT EXTRACT FILE                        00910007
      ******************************************************************00920007
           COPY XLRD150.                                                00930007
                                                                        00940007
      ******************************************************************00810007
      *  DB2 DCLGEN FOR TICKET FORMAT TABLE                             00820007
      *  (XMT_TKT_FMT)                                                  00830008
      ******************************************************************00840007
           EXEC SQL                                                     00860007
               INCLUDE XMT00018                                         00870008
           END-EXEC.                                                    00880007
                                                                        00890007
      ******************************************************************00950007
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                00960007
      ******************************************************************00970007
                                                                        00980007
           EXEC SQL                                                     00990007
               INCLUDE SQLCA                                            01000007
           END-EXEC.                                                    01010007
                                                                        01020007
      ******************************************************************01030007
      *  DB2 FORMATTED ERROR MESSAGE                                   *01040007
      ******************************************************************01050007
           COPY DPWS004.                                                01060007
                                                                        01070007
      ******************************************************************01080009
       PROCEDURE DIVISION.                                              01090009
      ******************************************************************01100009
                                                                        01110009
       0000-MAINLINE.                                                   01120010
           MOVE '0000-MAINLINE' TO PV-PARAGRAPH-NAME.                           
                                                                        01130009
           OPEN INPUT TKT-FMT-EXTRACT-FILE.                             01140009
                                                                        01150009
           PERFORM 4000-READ-EXTRACT-FILE.                              01160010
                                                                        01170009
           IF PS-END-OF-FILE                                            01170009
               DISPLAY 'ABEND 4001 - EMPTY TICKET FORMAT EXTRACT FILE'          
               SET PV-ABEND-4001 TO TRUE                                        
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                        01170009
           PERFORM 5000-DELETE-TKT-FMT-TABLE.                           01180009
                                                                        01170009
           PERFORM 2000-PROCESS-EXTRACT-FILE                            01180009
               UNTIL PS-END-OF-FILE.                                    01190009
                                                                        01200009
           PERFORM 7000-COMMIT.                                                 
                                                                                
           DISPLAY '********************************************'.              
           DISPLAY '**** RUN STATISTICS FOR PROGRAM XMKUP002 ***'.              
           DISPLAY '********************************************'.              
           DISPLAY 'INPUT RECORDS READ: ' PA-RECORDS-READ.                      
           DISPLAY 'ROWS INSERTED:      ' PA-ROWS-INSERTED.                     
           DISPLAY '********************************************'.              
                                                                                
           CLOSE TKT-FMT-EXTRACT-FILE.                                  01210009
                                                                        01220009
           GOBACK.                                                      01230009
                                                                        01240009
      ******************************************************************01350009
      * 2000-PROCESS-EXTRACT-FILE.                                      01360009
      *  PERFORM MAIN FILE PROCESSING.                                  01370009
      ******************************************************************01380009
       2000-PROCESS-EXTRACT-FILE.                                       01390009
                                                                        01400009
           IF XL150-EXTERNAL                                            01400009
               MOVE XL150-TKT-FMT-CDE   TO XMT00018-TKT-FMT-CDE                 
               MOVE XL150-TKT-TYP-CDE   TO XMT00018-TKT-TYP-CDE                 
               MOVE XL150-TKT-FMT-DESC  TO XMT00018-TKT-FMT-DESC                
               MOVE XL150-TKT-FMT-NBR   TO XMT00018-TKT-FMT-NBR                 
               PERFORM 6000-INSERT-TKT-FMT-TABLE                                
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-EXTRACT-FILE.                                      
                                                                                
      ******************************************************************01250009
      * 4000-READ-EXTRACT-FILE.                                         01260010
      *  READ TICKET FORMAT EXTRACT FILE                                01270009
      ******************************************************************01280009
       4000-READ-EXTRACT-FILE.                                          01290010
                                                                        01300009
           READ TKT-FMT-EXTRACT-FILE INTO XL150-TICKET-FMT-REC          01310009
               AT END                                                   01320009
                   SET PS-END-OF-FILE TO TRUE                           01330009
               NOT AT END                                               01320009
                   ADD 1 TO PA-RECORDS-READ                                     
           END-READ.                                                            
                                                                                
      ******************************************************************01250009
      * 5000-DELETE-TKT-FMT-TABLE.                                      01260010
      *  DELETE ALL ROWS FROM THE XMT_TKT_FMT TABLE                     01270009
      ******************************************************************01280009
       5000-DELETE-TKT-FMT-TABLE.                                       01290010
           MOVE '5000-DELETE-TKT-FMT-TABLE' TO PV-PARAGRAPH-NAME.               
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR  SQLCODE = +100                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   DELETE FROM XMT_TKT_FMT                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'DELETE XMT_TKT_FMT TABLE'                          
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'DELETE XMT_TKT_FMT TABLE'                                  
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************01250009
      * 6000-INSERT-TKT-FMT-TABLE.                                      01260010
      *  INSERT A ROW INTO THE XMT_TKT_FMT TABLE                        01270009
      ******************************************************************01280009
       6000-INSERT-TKT-FMT-TABLE.                                       01290010
           MOVE '6000-INSERT-TKT-FMT-TABLE' TO PV-PARAGRAPH-NAME.               
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   INSERT INTO XMT_TKT_FMT                                      
                          (TKT_FMT_CDE                                          
                         , TKT_TYP_CDE                                          
                         , TKT_FMT_DESC                                         
                         , TKT_FMT_NBR)                                         
                   VALUES (:XMT00018-TKT-FMT-CDE                                
                         , :XMT00018-TKT-TYP-CDE                                
                         , :XMT00018-TKT-FMT-DESC                               
                         , :XMT00018-TKT-FMT-NBR)                               
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD 1 TO PA-ROWS-INSERTED                                
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'INSERT XMT_TKT_FMT TABLE'                          
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'INSERT XMT_TKT_FMT TABLE'                                  
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  7000-COMMIT.                                                           
      *  COMMIT ALL UPDATES.                                           *        
      ******************************************************************        
       7000-COMMIT.                                                             
           MOVE '7000-COMMIT' TO PV-PARAGRAPH-NAME.                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                CONTINUE                                                        
             WHEN OTHER                                                         
               DISPLAY '7000-COMMIT '                                           
               MOVE 'COMMIT' TO PV-DB2-OPERATION-ATTEMPTED                      
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  ABEND THE PROGRAM IN THE EVENT THAT A SQL ERROR OR WARNING    *        
      *  CODE OCCURS.                                                  *        
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
                                                                                
           DISPLAY 'XMKUP002 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                           
           DISPLAY '****************************'.                              
           DISPLAY 'INPUT RECORDS READ: ' PA-RECORDS-READ.                      
           DISPLAY 'ROWS INSERTED:      ' PA-ROWS-INSERTED.                     
           DISPLAY '****************************'.                              
                                                                                
           COPY DPPD004.                                                        
                                                                                
           SET PV-ABEND-4013 TO TRUE.                                           
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
       9999-ABEND.                                                      07730094
           DISPLAY '** XMKUP002 - ABEND'.                                       
           DISPLAY '** PARAGRAPH = ' PV-PARAGRAPH-NAME.                         
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
