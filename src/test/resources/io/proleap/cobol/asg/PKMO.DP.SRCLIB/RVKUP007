      ******************************************************************00010001
       IDENTIFICATION DIVISION.                                         00020001
      ******************************************************************00030001
                                                                        00040001
       PROGRAM-ID.   RVKUP007.                                          00050001
       AUTHOR.       CHRIS STRODTHOFF.                                  00060001
       INSTALLATION. KOHLS DEPARTMENT STORES.                           00070001
       DATE-WRITTEN. OCTOBER 2011.                                      00080001
       DATE-COMPILED.                                                   00090001
                                                                        00100001
      ******************************************************************00110001
      * NAME: RTV GENCO RVTW_DFCTOR_ERR TABLE CLEAN UP PROGRAM.         00120014
      ******************************************************************00130014
      * NARRATIVE: THIS PROGRAM READS IN A SORTED FILE AND PERFORMS     00140014
      * DELETES OF RVTW_DFCTOR_ERR TABLE BASED ON MATCHES TO            00150014
      * RV018-DFCT-ORD-ID.                                              00160014
      ******************************************************************00170001
                                                                        00180001
      ******************************************************************00190003
      *  CHANGE LOG.                                                    00200003
      *  DATE         CHANGED BY      DESC                              00210003
      *  -----------  --------------  ----------------------------------00220003
      *  10/30/2011   C. STRODTHOFF - GENCO / RTV UPGRADE. NEW PROGRAM  00230019
      *                               TO CLEAN UP RVTW_DFCTOR_ERR TABLE.00240019
      *                               PRJ ID 3219 - WR40134.            00250019
      ******************************************************************00260003
      ******************************************************************00270001
                                                                        00280003
       ENVIRONMENT DIVISION.                                            00290001
      ******************************************************************00300001
                                                                        00310001
       CONFIGURATION SECTION.                                           00320001
                                                                        00330001
       SOURCE-COMPUTER.        IBM-3090.                                00340001
       OBJECT-COMPUTER.        IBM-3090.                                00350001
                                                                        00360001
       INPUT-OUTPUT SECTION.                                            00370001
                                                                        00380001
       FILE-CONTROL.                                                    00390001
                                                                        00400001
           SELECT RTV-ERRSUM-FILE                                       00410004
               ASSIGN TO INP01.                                         00420001
                                                                        00430001
      ******************************************************************00440001
       DATA DIVISION.                                                   00450001
      ******************************************************************00460001
                                                                        00470001
       FILE SECTION.                                                    00480004
                                                                        00490004
       FD  RTV-ERRSUM-FILE                                              00500004
           RECORDING MODE IS F                                          00510004
           LABEL RECORDS ARE STANDARD                                   00520004
           BLOCK CONTAINS 0 RECORDS.                                    00530004
                                                                        00540004
       01  RTV-ERRSUM-RECORD               PIC X(254).                  00550004
                                                                        00560004
      ******************************************************************00570004
       WORKING-STORAGE SECTION.                                         00580004
      ******************************************************************00590004
                                                                        00600004
      ******************************************************************00610004
      *  COMMUNICATIONS AREA FOR DB2                                    00620004
      ******************************************************************00630004
           EXEC SQL                                                     00640004
                INCLUDE SQLCA                                           00650004
           END-EXEC.                                                    00660004
                                                                        00670004
      ******************************************************************00680004
      *  DB2 TABLE DECLARATION - RTVW 3RD PARTY ERROR.                  00690004
      *      (RVTW_DFCTOR_ERR).                                         00700004
      ******************************************************************00710004
           EXEC SQL                                                     00720004
                INCLUDE RVT00006                                        00730004
           END-EXEC.                                                    00740004
                                                                        00750004
      ******************************************************************00760004
      * INPUT LAYOUT - RV018-HDR-ERROR-RECORD.                          00770004
      ******************************************************************00780004
           COPY RVWS018.                                                00790004
                                                                        00800004
      ******************************************************************00810010
      *  DB2 FORMATTED MESSAGE AREA                                     00820010
      ******************************************************************00830010
           COPY DPWS004.                                                00840010
                                                                        00850010
                                                                        00860005
       01  ABEND-CODE                      PIC S9(04)    VALUE +0       00870005
                                                         COMP SYNC.     00880005
       01  PC-PROGRAM-CONSTANTS.                                        00890010
           05  PC-PROGRAM-NAME             PIC X(08)  VALUE 'RVKUP007'. 00900010
           05  PC-MAX-RETRIES              PIC S9(03) VALUE +5 COMP-3.  00910010
                                                                        00920010
       01  PA-PROGRAM-ACCUMULATORS.                                     00930005
           05  PA-TOTALS.                                               00940005
               10 PA-TOT-RECS-READ          PIC S9(09) VALUE +0 COMP-3. 00950005
               10 PA-TOT-ERR-DELETED        PIC S9(09) VALUE +0 COMP-3. 00960005
                                                                        00970000
       01  PS-PROGRAM-SWITCHES.                                         00980005
           05  PS-END-OF-ERR-INPUT          PIC  X(01)    VALUE ' '.    00990009
               88  PS-END-OF-ERR-INPUT-YES                VALUE 'Y'.    01000009
               88  PS-END-OF-ERR-INPUT-NO                 VALUE 'N'.    01010009
                                                                        01020005
       01  PV-PROGRAM-VARIABLES.                                        01030005
           05  PV-DFCT-ORD-ID               PIC X(12).                  01040021
           05  PV-ERROR-RECORD              PIC X(254).                 01050015
           05  PV-RETRY-COUNTER             PIC S9(04) VALUE +0 COMP    01060010
                                                                SYNC.   01070010
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.     01080010
           05  PV-DB2-FUNCTION              PIC  X(30) VALUE SPACES.    01090010
           05  PV-DB2-TABLE-NAME            PIC  X(30) VALUE SPACES.    01100010
           05  PV-ABEND-MESSAGE             PIC  X(30) VALUE SPACE.     01110011
           05  PV-DFCT-ORD-ID-DISP          PIC  X(12) VALUE SPACES.    01120021
                                                                        01130005
       01  PV-ABEND-VARIABLES.                                          01140005
           05  PV-ABEND-CODE              PIC S9(04) VALUE +0 COMP SYNC.01150005
                                                                        01160005
      ******************************************************************01170005
       LINKAGE SECTION.                                                 01180005
      ******************************************************************01190005
                                                                        01200005
      ******************************************************************01210005
       PROCEDURE DIVISION.                                              01220005
      ******************************************************************01230005
      ******************************************************************01240005
      *  0000-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE PROGRAM.  *01250005
      ******************************************************************01260005
       0000-PROGRAM-MAINLINE.                                           01270005
                                                                        01280005
           DISPLAY '************ START OF RVKUP007 ***************'     01290005
           PERFORM 1000-INITIALIZE.                                     01300005
                                                                        01310005
      *PROCESS INPUT FILES TO EOF                                       01320005
           PERFORM 1100-PROCESS-DELETES                                 01330005
             UNTIL PS-END-OF-ERR-INPUT-YES.                             01340008
                                                                        01350005
      *END OF INPUT PROCESSING.                                         01360005
           PERFORM 8000-EOJ-ROUTINE.                                    01370005
                                                                        01380005
           DISPLAY '************  END OF RVKUP007  ***************'     01390005
                                                                        01400020
           GOBACK.                                                      01410020
                                                                        01420005
      ******************************************************************01430005
      * INITIALIZE VARIABLES.                                           01440005
      ******************************************************************01450005
       1000-INITIALIZE.                                                 01460005
                                                                        01470005
           OPEN INPUT  RTV-ERRSUM-FILE.                                 01480005
                                                                        01490005
           MOVE +0        TO PA-TOT-RECS-READ                           01500006
                             PA-TOT-ERR-DELETED.                        01510006
                                                                        01520006
           SET PS-END-OF-ERR-INPUT-NO  TO TRUE.                         01530006
                                                                        01540006
      **PRIME READ OF ERRSUMM FILE                                      01550005
           PERFORM 6000-READ-RTV-ERR-FILE.                              01560005
                                                                        01570005
      ******************************************************************01580005
      * PERFORM MAIN DELETE PROCESS UNTIL EOF                           01590005
      * EOJ TASKS                                                       01600005
      ******************************************************************01610005
       1100-PROCESS-DELETES.                                            01620005
                                                                        01630005
           IF  RV018-HEADER-REC OR                                      01640013
               RV018-DETAIL-REC                                         01650013
               PERFORM 7000-DELETE-ERR-RECORDS                          01660013
           ELSE                                                         01670015
               PERFORM 7100-DELETE-SUM-RECORDS                          01680015
           END-IF.                                                      01690013
                                                                        01700008
           PERFORM 6000-READ-RTV-ERR-FILE.                              01710007
                                                                        01720008
      ******************************************************************01730005
      * READ THE RTV ERRSUM FILE                                       *01740005
      ******************************************************************01750005
       6000-READ-RTV-ERR-FILE.                                          01760005
                                                                        01770005
           READ RTV-ERRSUM-FILE                                         01780006
               INTO RV018-HDR-ERROR-RECORD                              01790006
               AT END                                                   01800005
                 SET PS-END-OF-ERR-INPUT-YES TO TRUE.                   01810006
                                                                        01820005
           IF PS-END-OF-ERR-INPUT-NO                                    01830006
              ADD +1  TO PA-TOT-RECS-READ                               01840005
           END-IF.                                                      01850005
                                                                        01860005
      ******************************************************************01870007
      * DELETE HEADER AND DETAIL ERROR RECORDS  (RVTW_DFCTOR_ERR)       01880015
      ******************************************************************01890007
       7000-DELETE-ERR-RECORDS.                                         01900007
           MOVE RV018-DFCT-ORD-ID-X           TO PV-DFCT-ORD-ID.        01910010
                                                                        01920009
           PERFORM                                                      01930007
             WITH TEST AFTER                                            01940007
             VARYING PV-RETRY-COUNTER FROM 1 BY 1                       01950007
             UNTIL   PV-RETRY-COUNTER > PC-MAX-RETRIES  OR              01960007
                     SQLCODE = +0 OR +100                               01970017
                                                                        01980007
           EXEC SQL                                                     01990007
               DELETE FROM RVTW_DFCTOR_ERR                              02000007
               WHERE SUBSTR(RTV_3RD_PTY_ERR,3,12) =  :PV-DFCT-ORD-ID    02010021
           END-EXEC                                                     02020007
                                                                        02030007
               EVALUATE TRUE                                            02040007
                   WHEN SQLCODE = ZERO                                  02050007
                       ADD +1   TO PA-TOT-ERR-DELETED                   02060007
                                                                        02070007
                   WHEN SQLCODE LESS THAN ZERO                          02080007
                   WHEN SQLWARN0 NOT EQUAL SPACE                        02090008
                       DISPLAY '**************************************' 02100007
                       DISPLAY '** ABEND                            **' 02110007
                       DISPLAY '** PROGRAM = RVKUP007               **' 02120007
                       DISPLAY '**************************************' 02130007
                       MOVE '7000-DELETE-ERR-RECORDS'                   02140007
                                              TO PV-PARAGRAPH-NAME      02150011
                       MOVE 'DELETE'          TO PV-DB2-FUNCTION        02160007
                       MOVE RV018-DFCT-ORD-ID-X                         02170007
                                              TO PV-DFCT-ORD-ID-DISP    02180007
                       STRING 'DFCT-ORD-ID: '  DELIMITED BY SIZE        02190007
                             PV-DFCT-ORD-ID-DISP DELIMITED BY SIZE      02200007
                                              INTO PV-ABEND-MESSAGE     02210012
                       MOVE 'RVTW_DFCTOR_ERR' TO PV-DB2-TABLE-NAME      02220007
                       PERFORM 9100-SQL-ABEND                           02230008
               END-EVALUATE                                             02240007
           END-PERFORM.                                                 02250007
                                                                        02260007
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                    02270007
               (SQLCODE NOT = +0 AND                                    02280017
                SQLCODE NOT = +100)                                     02290017
              DISPLAY '********************************************'    02300007
              DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO     **'    02310007
              DISPLAY '********************************************'    02320007
              MOVE '7000-DELETE-ERR-RECORDS'  TO PV-PARAGRAPH-NAME      02330012
              MOVE 'DELETE'                   TO PV-DB2-FUNCTION        02340012
              MOVE RV018-DFCT-ORD-ID-X        TO PV-DFCT-ORD-ID-DISP    02350012
                   STRING 'DFCT-ORD-ID: '  DELIMITED BY SIZE            02360012
                   PV-DFCT-ORD-ID-DISP DELIMITED BY SIZE                02370012
                                              INTO PV-ABEND-MESSAGE     02380012
              MOVE 'RVTW_DFCTOR_ERR'          TO PV-DB2-TABLE-NAME      02390012
              PERFORM 9100-SQL-ABEND                                    02400012
           END-IF.                                                      02410007
                                                                        02420006
      ******************************************************************02430015
      * DELETE SUMMARY ERROR RECORDS  (RVTW_DFCTOR_ERR)                 02440015
      ******************************************************************02450015
       7100-DELETE-SUM-RECORDS.                                         02460015
           MOVE RV018-HDR-ERROR-RECORD        TO PV-ERROR-RECORD.       02470015
                                                                        02480015
           PERFORM                                                      02490015
             WITH TEST AFTER                                            02500015
             VARYING PV-RETRY-COUNTER FROM 1 BY 1                       02510015
             UNTIL   PV-RETRY-COUNTER > PC-MAX-RETRIES  OR              02520015
                     SQLCODE = +0 OR +100                               02530017
                                                                        02540015
           EXEC SQL                                                     02550015
               DELETE FROM RVTW_DFCTOR_ERR                              02560015
               WHERE SUBSTR(RTV_3RD_PTY_ERR,1,254)                      02570015
                                                 =  :PV-ERROR-RECORD    02580015
           END-EXEC                                                     02590015
                                                                        02600015
               EVALUATE TRUE                                            02610015
                   WHEN SQLCODE = ZERO                                  02620015
                       ADD +1   TO PA-TOT-ERR-DELETED                   02630015
                                                                        02640015
                   WHEN SQLCODE LESS THAN ZERO                          02650015
                   WHEN SQLWARN0 NOT EQUAL SPACE                        02660015
                       DISPLAY '**************************************' 02670015
                       DISPLAY '** ABEND                            **' 02680015
                       DISPLAY '** PROGRAM = RVKUP007               **' 02690015
                       DISPLAY '**************************************' 02700015
                       MOVE '7100-DELETE-SUM-RECORDS'                   02710015
                                              TO PV-PARAGRAPH-NAME      02720015
                       MOVE 'DELETE'          TO PV-DB2-FUNCTION        02730015
                       MOVE 'SUMMARY RECORD ERROR'                      02740015
                                              TO PV-ABEND-MESSAGE       02750015
                       MOVE 'RVTW_DFCTOR_ERR' TO PV-DB2-TABLE-NAME      02760015
                       PERFORM 9100-SQL-ABEND                           02770015
               END-EVALUATE                                             02780015
           END-PERFORM.                                                 02790015
                                                                        02800015
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                    02810015
               (SQLCODE NOT = +0 AND                                    02820017
                SQLCODE NOT = +100)                                     02830017
              DISPLAY '********************************************'    02840015
              DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO     **'    02850015
              DISPLAY '********************************************'    02860015
              MOVE '7100-DELETE-SUM-RECORDS'  TO PV-PARAGRAPH-NAME      02870015
              MOVE 'DELETE'                   TO PV-DB2-FUNCTION        02880015
              MOVE 'SUMMARY RECORD ERROR'     TO PV-ABEND-MESSAGE       02890015
              MOVE 'RVTW_DFCTOR_ERR'          TO PV-DB2-TABLE-NAME      02900015
              PERFORM 9100-SQL-ABEND                                    02910015
           END-IF.                                                      02920015
                                                                        02930015
       8000-EOJ-ROUTINE.                                                02940015
                                                                        02950006
           CLOSE RTV-ERRSUM-FILE.                                       02960006
                                                                        02970006
           DISPLAY '***********************************************'.   02980006
           DISPLAY '**    * RVKUP007 SUMMARY STATISTICS *        **'.   02990006
           DISPLAY '**                                           **'.   03000006
           DISPLAY '**      ---------------------------          **'.   03010006
           DISPLAY '**           GENCO RTV DELETES               **'.   03020006
           DISPLAY '**      ---------------------------          **'.   03030006
           DISPLAY '** TOT RECS READ          :' PA-TOT-RECS-READ.      03040016
           DISPLAY '** SUCCESSFUL DELETE CALLS:' PA-TOT-ERR-DELETED.    03050016
           DISPLAY '***********************************************'.   03060006
                                                                        03070006
      ******************************************************************03080007
      * PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL    *03090007
      * ERROR CODE OR WARNING OCCURS.                                  *03100008
      ******************************************************************03110007
       9100-SQL-ABEND.                                                  03120007
      ******************************************************************03130007
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *03140007
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *03150007
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *03160007
      * FORMAT.                                                        *03170007
      ******************************************************************03180007
                                                                        03190007
           COPY DPPD004.                                                03200007
                                                                        03210007
           DISPLAY '***********************************'.               03220007
           DISPLAY '      GENCO RTV LOAD ABEND         '.               03230007
           DISPLAY '***********************************'.               03240007
           DISPLAY '* CURRENT STATS AT TIME OF ABEND  *'.               03250007
           DISPLAY '***********************************'.               03260007
           DISPLAY '** TOT RECS READ       :' PA-TOT-RECS-READ.         03270007
           DISPLAY '***********************************************'.   03280007
           DISPLAY '** ABEND PARAGRAPH **  ' PV-PARAGRAPH-NAME.         03290007
           DISPLAY '** DB2 TABLE NAME  **  ' PV-DB2-TABLE-NAME.         03300012
           DISPLAY '** DB2 FUNCTION    **  ' PV-DB2-FUNCTION.           03310012
           DISPLAY '** ERROR MESSAGE   **  ' PV-ABEND-MESSAGE.          03320007
           DISPLAY '***********************************************'.   03330007
           DISPLAY ' '.                                                 03340007
           MOVE +4013 TO ABEND-CODE.                                    03350007
           CALL 'ILBOABN0' USING ABEND-CODE.                            03360007
                                                                        03370007
      ******************************************************************03380007
      *                       END OF PROGRAM                            03390007
      ******************************************************************03400007
