      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
      *                                                                         
       PROGRAM-ID.    FSKRP040.                                                 
       AUTHOR.        JILL LIN.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  04-09-2010.                                               
       DATE-COMPILED.                                                           
      *                                                                         
      ******************************************************************        
      *       FSKRP040 - DAILY RECEIPT EXCEPTION REPORT                         
      ******************************************************************        
      *                                                                         
      *   THIS PROGRAM WILL PRINT AN EXCEPTION REPORT FOR ALL                   
      *   RECEIPT TRANS RECORDS THAT CONTAIN EFC2 LOCATION.                     
      *                                                                         
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT RECEIPT-EXCEPT-FILE ASSIGN TO INP01.                          
                                                                                
           SELECT PRINT-REPORT        ASSIGN TO RPT01.                          
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      *****************************************************************         
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  RECEIPT-EXCEPT-FILE                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RECEIPT-EXCEPT-RECORD          PIC X(200).                           
                                                                                
      *                                                                         
       FD  PRINT-REPORT                                                         
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PRINT-LINE                     PIC X(132).                           
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
      ******************************************************                    
      ** FS RECEIPT EXCEPTION FILE LAYOUT                                       
      ******************************************************                    
           COPY FSRD036.                                                        
      *                                                                         
      ******************************************************                    
      ** STANDARD HEADER LINE - KOHLS 132 COL                                   
      ******************************************************                    
           COPY DPWS132.                                                        
      *                                                                         
       01  HEADING-LINE-2.                                                      
           05  FILLER                       PIC X(53)  VALUE SPACES.            
           05  FILLER                       PIC X(26)  VALUE                    
               ' FS RECEIPT EFC EXCEPTION '.                                    
           05  FILLER                       PIC X(53)  VALUE SPACES.            
      *                                                                         
       01  HEADING-LINE-3.                                                      
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  FILLER                       PIC X(14)  VALUE                    
               'ADJ SRC PGM NM'.                                                
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  FILLER                       PIC X(08)  VALUE                    
               'BTCH NBR'.                                                      
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  FILLER                       PIC X(07)  VALUE                    
               'LOC NBR'.                                                       
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  FILLER                       PIC X(15)  VALUE                    
               'ITEM NUMBER    '.                                               
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(09)  VALUE                    
               'PO NBR   '.                                                     
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(09)  VALUE                    
               'RCVR NBR '.                                                     
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(10)  VALUE                    
               'RCPT DTE  '.                                                    
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(10)  VALUE                    
               'VRFD DTE  '.                                                    
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(12)  VALUE                    
               'TOT RCPT QTY'.                                                  
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  FILLER                       PIC X(12)  VALUE                    
               'LOC RCPT QTY'.                                                  
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  FILLER                       PIC X(09)  VALUE                    
               'ENRTE IND'.                                                     
      *                                                                         
       01  DETAIL-LINE.                                                         
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  DL-RCPT-SRC-PGM-NM           PIC X(08)  VALUE SPACES.            
           05  FILLER                       PIC X(08)  VALUE SPACES.            
           05  DL-BTCH-NBR                  PIC ZZZ9   VALUE ZEROES.            
           05  FILLER                       PIC X(06)  VALUE SPACES.            
           05  DL-LOC-NBR                   PIC ZZZ9   VALUE ZEROES.            
           05  FILLER                       PIC X(05)  VALUE SPACES.            
           05  DL-ITM-NBR                   PIC ZZZZZZZZZZZZZZ9                 
                                                       VALUE ZEROES.            
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  DL-PO-NBR                    PIC ZZZZZZZZ9                       
                                                       VALUE ZEROES.            
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  DL-RCVR-NBR                  PIC ZZZZZZZZ9                       
                                                       VALUE ZEROES.            
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  DL-RCPT-DTE                  PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  DL-VRFD-DTE                  PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  DL-TOT-RCPT-QTY              PIC --------9                       
                                                       VALUE ZEROES.            
           05  FILLER                       PIC X(05)  VALUE SPACES.            
           05  DL-LOC-RCPT-QTY              PIC --------9                       
                                                       VALUE ZEROES.            
           05  FILLER                       PIC X(05)  VALUE SPACES.            
           05  DL-ENRTE-IND                 PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(08)  VALUE SPACES.            
      *                                                                         
       01  BLANK-LINE.                                                          
           05  FILLER                       PIC X(132) VALUE SPACES.            
      *                                                                         
       01  END-OF-REPORT-LINE.                                                  
           05  FILLER                       PIC X(25)      VALUE                
               ' ** END OF REPORT **     '.                                     
      *                                                                         
       01  PROGRAM-SWITCHES.                                                    
           05  PS-EOF-RCPT-ECPT-FILE       PIC  X(01)    VALUE 'N'.             
               88  EOF-RCPT-ECPT-FILE                    VALUE 'Y'.             
           05  PS-FIRST-TIME-THRU          PIC  X(01)    VALUE 'Y'.             
               88  FIRST-TIME-THRU                       VALUE 'Y'.             
      *                                                                         
       01  ABEND-CODE                       PIC S9(04)     VALUE +0             
                                                           COMP SYNC.           
      *                                                                         
       01  PC-CONSTANTS.                                                        
           05  PC-PROGRAM-NAME             PIC X(08)  VALUE 'FSKRP040'.         
           05  PC-1                        PIC S9(03) VALUE +1                  
                                                      COMP-3.                   
           05  PC-2                        PIC S9(03) VALUE +2                  
                                                      COMP-3.                   
           05  PC-MAX-LINES                PIC S9(3)  VALUE +57                 
                                                      COMP-3.                   
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-RCPT-ECPT-REC-COUNT      PIC S9(09) COMP-3 VALUE +0.          
           05  PV-RECS-PROCESSED           PIC S9(09) COMP-3 VALUE +0.          
           05  PV-DISP-RECS-PROC           PIC ZZZ,ZZZ,ZZ9.                     
      *                                                                         
           05  PV-TRANS-DATE.                                                   
               10  PV-TRANS-MONTH          PIC X(02).                           
               10  FILLER                  PIC X(01)     VALUE '/'.             
               10  PV-TRANS-DAY            PIC X(02).                           
               10  FILLER                  PIC X(01)     VALUE '/'.             
               10  PV-TRANS-CCYY           PIC X(04).                           
      *                                                                         
           05  PV-PAGE-CNT                 PIC S9(5)     VALUE +0               
                                                         COMP-3.                
           05  PV-LINE-CNT                 PIC S9(3)     VALUE +0               
                                                         COMP-3.                
           05  PV-PARAGRAPH-NAME           PIC X(30)     VALUE SPACES.          
      *                                                                         
       01  CD-CPU-DATE                      PIC 9(08).                          
       01  FILLER REDEFINES CD-CPU-DATE.                                        
           05  CD-CPU-YEAR                  PIC 9(04).                          
           05  CD-CPU-MON                   PIC 9(02).                          
           05  CD-CPU-DAY                   PIC 9(02).                          
      *                                                                         
       01  CT-CPU-TIME.                                                         
           05  CT-CPU-HR                    PIC 9(02)     VALUE ZEROS.          
           05  CT-CPU-MIN                   PIC 9(02)     VALUE ZEROS.          
           05  CT-CPU-SEC                   PIC 9(02)     VALUE ZEROS.          
           05  CT-CPU-HSEC                  PIC 9(02)     VALUE ZEROS.          
      *                                                                         
       EJECT                                                                    
      *                                                                         
       LINKAGE SECTION.                                                         
      *****************************************************************         
      *PROCEDURE DIVISION.                                                      
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *                                                                         
       A100-MAINLINE-PROCESSING.                                                
      *                                                                         
           PERFORM B100-INITIALIZATION.                                         
                                                                                
           PERFORM B200-PROCESS-RCPT-ECPT-FILE                                  
                   UNTIL EOF-RCPT-ECPT-FILE.                                    
      *                                                                         
           PERFORM B300-END-ROUTINE.                                            
      *                                                                         
           STOP RUN.                                                            
      *                                                                         
       EJECT                                                                    
      *****************************************************************         
      *  - GET SYSTEM DATE & TIME AND MOVE TO REPORT HEADER                     
      *  - INITIAL READ INPUT EXCEPTION FILE                                    
      *****************************************************************         
       B100-INITIALIZATION.                                                     
      *                                                                         
           ACCEPT CT-CPU-TIME FROM TIME.                                        
           ACCEPT CD-CPU-DATE FROM DATE YYYYMMDD.                               
      *                                                                         
           MOVE CT-CPU-HR     TO DP132-RUN-HOUR.                                
           MOVE CT-CPU-MIN    TO DP132-RUN-MINUTE.                              
      *                                                                         
           MOVE CD-CPU-MON    TO DP132-RUN-MONTH.                               
           MOVE CD-CPU-DAY    TO DP132-RUN-DAY.                                 
           MOVE CD-CPU-YEAR   TO DP132-RUN-YEAR.                                
      *                                                                         
           MOVE 1  TO DP132-REPORT-NUMBER                                       
                      DP132-PAGE-NUMBER.                                        
           MOVE PC-PROGRAM-NAME TO DP132-PROGRAM-NAME.                          
      *                                                                         
           OPEN INPUT  RECEIPT-EXCEPT-FILE                                      
                OUTPUT PRINT-REPORT.                                            
      *                                                                         
           PERFORM Z100-READ-RCPT-ECPT-FILE.                                    
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *  - PRINT DETAIL LINE                                                    
      *****************************************************************         
       B200-PROCESS-RCPT-ECPT-FILE.                                             
      *                                                                         
           IF EOF-RCPT-ECPT-FILE                                                
               CONTINUE                                                         
           ELSE                                                                 
               IF FIRST-TIME-THRU                                               
                   PERFORM Z400-PRINT-REPORT-HEADERS                            
                   MOVE 'N' TO PS-FIRST-TIME-THRU                               
               END-IF                                                           
               PERFORM C400-FORMAT-DETAIL-LINE                                  
           END-IF.                                                              
                                                                                
           PERFORM Z100-READ-RCPT-ECPT-FILE.                                    
       EJECT                                                                    
      *****************************************************************         
      *  - PRINT CONTROL TOTALS, CLOSE FILES                                    
      *****************************************************************         
       B300-END-ROUTINE.                                                        
      *                                                                         
           WRITE PRINT-LINE FROM END-OF-REPORT-LINE                             
               AFTER ADVANCING PC-2.                                            
      *                                                                         
           MOVE PV-RECS-PROCESSED TO PV-DISP-RECS-PROC.                         
           DISPLAY '*************************************************'.         
           DISPLAY 'FSKRP040 - RECORDS PROCESSED: ' PV-DISP-RECS-PROC.          
           DISPLAY '*************************************************'.         
      *                                                                         
           CLOSE RECEIPT-EXCEPT-FILE                                            
                 PRINT-REPORT.                                                  
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *  - MOVE DATA TO DETAIL LINE & PRINT                                     
      *****************************************************************         
       C400-FORMAT-DETAIL-LINE.                                                 
      *                                                                         
           MOVE FS036-RCPT-SRC-PGM-NM TO DL-RCPT-SRC-PGM-NM.                    
           MOVE FS036-BTCH-NBR        TO DL-BTCH-NBR.                           
           MOVE FS036-LOC-NBR         TO DL-LOC-NBR.                            
           MOVE FS036-ITM-NBR         TO DL-ITM-NBR.                            
           MOVE FS036-PO-NBR          TO DL-PO-NBR.                             
           MOVE FS036-RCVR-NBR        TO DL-RCVR-NBR.                           
           MOVE FS036-RCPT-DTE        TO DL-RCPT-DTE.                           
           MOVE FS036-VRFD-DTE        TO DL-VRFD-DTE.                           
           MOVE FS036-TOT-RCPT-QTY    TO DL-TOT-RCPT-QTY.                       
           MOVE FS036-LOC-RCPT-QTY    TO DL-LOC-RCPT-QTY.                       
           MOVE FS036-ENRTE-IND       TO DL-ENRTE-IND.                          
      *                                                                         
           PERFORM D100-PRINT-DETAIL-LINE.                                      
           ADD +1 TO PV-RECS-PROCESSED.                                         
                                                                                
       EJECT                                                                    
      *****************************************************************         
       D100-PRINT-DETAIL-LINE.                                                  
      *                                                                         
           ADD PC-2 TO PV-LINE-CNT.                                             
      *                                                                         
           IF PV-LINE-CNT > PC-MAX-LINES                                        
              PERFORM Z400-PRINT-REPORT-HEADERS                                 
              ADD PC-2 TO PV-LINE-CNT                                           
           END-IF.                                                              
      *                                                                         
           WRITE PRINT-LINE FROM DETAIL-LINE                                    
               AFTER ADVANCING PC-2.                                            
      *                                                                         
           MOVE SPACES TO PRINT-LINE.                                           
      *                                                                         
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *  - READ CONTROL CARD CONTAINING PROCESSING DATE                         
      *****************************************************************         
       Z100-READ-RCPT-ECPT-FILE.                                                
      *                                                                         
           READ RECEIPT-EXCEPT-FILE INTO FS036-RECEIPT-RECORD                   
                AT END SET EOF-RCPT-ECPT-FILE TO TRUE.                          
      *                                                                         
           IF NOT EOF-RCPT-ECPT-FILE                                            
               ADD +1 TO PV-RCPT-ECPT-REC-COUNT                                 
           ELSE                                                                 
               IF PV-RCPT-ECPT-REC-COUNT = 0                                    
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PROGRAM    = ' PC-PROGRAM-NAME                   
                   DISPLAY '** PARAGRAPH  = Z100-READ-RCPT-ECPT-FILE'           
                   DISPLAY                                                      
                       '** MESSAGE    = INPUT EXCEPTION FILE NOT FOUND'         
                   MOVE +4003      TO ABEND-CODE                                
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *****************************************************************         
       Z400-PRINT-REPORT-HEADERS.                                               
      *                                                                         
           ADD PC-1 TO PV-PAGE-CNT.                                             
           MOVE PV-PAGE-CNT TO DP132-PAGE-NUMBER.                               
      *                                                                         
           WRITE PRINT-LINE FROM DP132-STANDRD-HEADER-132-COLS                  
               AFTER ADVANCING PAGE.                                            
      *                                                                         
           WRITE PRINT-LINE FROM HEADING-LINE-2                                 
               AFTER ADVANCING PC-1.                                            
      *                                                                         
           WRITE PRINT-LINE FROM HEADING-LINE-3                                 
               AFTER ADVANCING PC-2.                                            
      *                                                                         
           MOVE ZERO TO PV-LINE-CNT.                                            
                                                                                
      *****************************************************************         
