      ****************************************************************  00010000
       IDENTIFICATION DIVISION.                                         00020000
      ****************************************************************  00030000
                                                                        00040000
       PROGRAM-ID.             SAKUT132.                                00050070
       AUTHOR.                 COGNIZANT.                               00060001
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            NOV 2015.                                00080070
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ****************************************************************  00110000
      *  THIS PROGRAM TO FIND MISSING TRNSACTIONS - BOPUS RETURN        00130274
      ****************************************************************  00133000
      * PRGMR: COGNIZANT                                                00150001
      * WHY:   NEW PROGRAM TO VERIFY ECOM TRANSACTIONS.                 00160000
      ******************************************************************00173000
      ****************************************************************  00188053
       ENVIRONMENT DIVISION.                                            00190000
      ****************************************************************  00200000
                                                                        00210000
       CONFIGURATION SECTION.                                           00220000
                                                                        00230000
       SOURCE-COMPUTER.        IBM-3090.                                00240000
                                                                        00250000
       OBJECT-COMPUTER.        IBM-3090.                                00260000
                                                                        00270000
       INPUT-OUTPUT SECTION.                                            00280000
                                                                        00290000
       FILE-CONTROL.                                                    00300000
                                                                        00310001
           SELECT ECOM-EXT-FILE ASSIGN TO INP01.                        00331061
           SELECT ECOM-MISS-FILE ASSIGN TO OUT01.                       00332061
                                                                        00350000
      ***************************************************************** 00360000
       DATA DIVISION.                                                   00370000
      ***************************************************************** 00380000
                                                                        00390000
       FILE SECTION.                                                    00400000
                                                                        00410000
                                                                        00480000
       FD  ECOM-EXT-FILE                                                00490001
           RECORDING MODE IS F                                          00491000
           LABEL RECORDS ARE STANDARD                                   00492000
           BLOCK CONTAINS 0 RECORDS                                     00493000
           RECORD CONTAINS 80 CHARACTERS                                00494071
           DATA RECORD IS ECOM-EXT-RECORD.                              00495001
                                                                        00496000
       01  ECOM-EXT-RECORD                PIC X(80).                    00497071
                                                                        00498000
       FD  ECOM-MISS-FILE                                               00499054
           RECORDING MODE IS F                                          00499154
           LABEL RECORDS ARE STANDARD                                   00499254
           BLOCK CONTAINS 0 RECORDS                                     00499354
           RECORD CONTAINS 80 CHARACTERS                                00499471
           DATA RECORD IS ECOM-EXT-RECORD.                              00499554
                                                                        00499654
       01  ECOM-MISS-RECORD                PIC X(80).                   00499771
                                                                        00499854
      ***************************************************************** 00500000
       WORKING-STORAGE SECTION.                                         00510000
      ***************************************************************** 00520000
       01  PV-ECOM-RECORD.                                              00530026
           05  PV-ECOM-SLS-DATE.                                        00540026
               10  PV-ECOM-SLS-CCYY        PIC X(4).                    00550026
               10  FILLER                  PIC X(1) VALUE '-'.          00560026
               10  PV-ECOM-SLS-MM          PIC X(2).                    00570026
               10  FILLER                  PIC X(1) VALUE '-'.          00580026
               10  PV-ECOM-SLS-DD          PIC X(2).                    00590026
           05  FILLER                      PIC X(1) VALUE SPACE.        00600026
           05  PV-ECOM-ORD-DATE.                                        00610026
                10  PV-ECOM-ORD-CCYY       PIC X(4).                    00620026
                10  FILLER                 PIC X(1) VALUE '-'.          00630026
                10  PV-ECOM-ORD-MM         PIC X(2).                    00640026
                10  FILLER                 PIC X(1) VALUE '-'.          00650026
                10  PV-ECOM-ORD-DD         PIC X(2).                    00660026
           05  FILLER                      PIC X(1) VALUE SPACE.        00670026
           05  PV-ECOM-STR-NBR             PIC 9(05).                   00680026
           05  FILLER                      PIC X(16) VALUE SPACE.       00690054
           05  PV-ECOM-RGST-ID             PIC 9(03).                   00700054
           05  PV-ECOM-TRN-NBR             PIC 9(4).                    00720026
           05  FILLER                      PIC X(1) VALUE SPACE.        00730026
       01  PS-PROGRAM-SWITCHES.                                         00790800
           05  PS-END-OF-FILE-SW           PIC X(01) VALUE 'N'.         00800054
               88  PS-END-OF-FILE-YES                VALUE 'Y'.         00810054
               88  PS-END-OF-FILE-NO                 VALUE 'N'.         00820054
                                                                        00980000
       01  PC-CONSTANTS.                                                00990000
           05  PC-CURRENT-PROGRAM-NAME       PIC  X(08) VALUE           01000000
                                                        'SAKUT132'.     01010054
           05  PC-ROW-NOT-FOUND              PIC S9(04) VALUE +100      01020000
                                                        COMP SYNC.      01030000
                                                                        01050037
                                                                        01140000
       01  PV-MISC-FIELDS.                                              01270000
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   01280000
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   01290000
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   01300000
           05  PV-DB2-OPERATION            PIC  X(30)    VALUE SPACE.   01310000
           05  PV-WRITE-CNT                PIC  9(07)    VALUE ZERO.    01311030
           05  PV-READ-CNT                 PIC  9(07)    VALUE ZERO.    01312069
       01  AC-ABEND-CODE                   PIC S9(4) VALUE ZEROS        01320030
                                                      COMP SYNC.        01330018
           88  AC-DB2-ERROR                           VALUE +4013.      01430018
                                                                        01480018
                                                                        01480100
      ***************************************************************** 01510000
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            01520000
      ***************************************************************** 01530000
            COPY DPWS004.                                               01540000
      ***************************************************************** 01560000
      *  DB2 COMMUNICATION AREA.                                        01570000
      ***************************************************************** 01580000
           EXEC SQL                                                     01590000
                INCLUDE SQLCA                                           01600000
           END-EXEC.                                                    01610000
                                                                        01620000
      ***************************************************************** 01772000
      *  DB2 TABLE TEPPART - PARTITION TABLE                            01780000
      ***************************************************************** 01790000
           EXEC SQL                                                     01800000
                INCLUDE TEPPART                                         01810000
           END-EXEC.                                                    01820000
      ***************************************************************** 01821004
      *  DB2 TABLE TEPTRN  - TRANSACTION TABLE                          01822004
      ***************************************************************** 01823004
           EXEC SQL                                                     01824004
                INCLUDE TEPTRN                                          01825004
           END-EXEC.                                                    01826004
      ***************************************************************** 02350000
      * PROCEDURE DIVISION.                                           * 02360000
      ***************************************************************** 02370000
       PROCEDURE DIVISION.                                              02380000
                                                                        02390000
      ***************************************************************** 02400000
      * 0000-MAINLINE-PARAGRAPH                                         02410000
      ***************************************************************** 02420000
       0000-MAINLINE-PARAGRAPH.                                         02430000
                                                                        02440000
           PERFORM 1000-INITIALIZE-PROGRAM.                             02450000
           PERFORM 2000-PROCESS-PARA                                    02451004
           UNTIL PS-END-OF-FILE-YES.                                    02452054
           PERFORM 9999-END-OF-ROUTINE.                                 02452228
           GOBACK.                                                      02453015
                                                                        02454015
           EJECT                                                        02455015
      ***************************************************************** 02860000
      * 1000-INITIALIZE-PROGRAM                                         02870000
      *     PROCESS CONTROL CARD                                        02880000
      ***************************************************************** 02890000
       1000-INITIALIZE-PROGRAM.                                         02900000
                                                                        02910000
           OPEN INPUT   ECOM-EXT-FILE.                                  02920054
           OPEN OUTPUT  ECOM-MISS-FILE.                                 02930054
           PERFORM 3000-READ-INPUT-FILE.                                03102055
                                                                        03109155
      ***************************************************************** 08575812
      * 2000-PROCESS-PARA                                               08575912
      ***************************************************************** 08576012
       2000-PROCESS-PARA.                                               08576112
             MOVE PV-ECOM-SLS-DATE TO EPPART-SLS-DTE.                   08576268
             MOVE PV-ECOM-STR-NBR TO EPTRN-STR-NBR.                     08576364
             MOVE PV-ECOM-TRN-NBR  TO EPTRN-TRN-NBR.                    08576468
             MOVE PV-ECOM-RGST-ID  TO EPTRN-RGST-ID.                    08576564
             PERFORM 3100-CHECK-MATCH.                                  08576864
             IF SQLCODE = PC-ROW-NOT-FOUND                              08576964
                 PERFORM  4000-WRTITE-ECOM-MISS                         08577064
             END-IF.                                                    08577164
             PERFORM  3000-READ-INPUT-FILE.                             08577264
      * 2000-PROCESS-PARA-EXIT.                                         08577312
      ***************************************************************** 08577475
      * 3000-READ-INPUT-FILE.                                           08577575
      ***************************************************************** 08577675
       3000-READ-INPUT-FILE.                                            08577775
           INITIALIZE PV-ECOM-RECORD.                                   08577875
                                                                        08577975
           READ ECOM-EXT-FILE                                           08578075
              INTO PV-ECOM-RECORD                                       08578175
                 AT END                                                 08578275
                 SET PS-END-OF-FILE-YES TO TRUE                         08578375
           END-READ.                                                    08578475
                                                                        08578575
           IF  PS-END-OF-FILE-NO                                        08578675
               ADD +1                       TO PV-READ-CNT              08578775
           END-IF.                                                      08578875
                                                                        08578975
      ***************************************************************** 08579012
      * 3100-CHECK-MATCH                                                08579155
      ***************************************************************** 08579212
       3100-CHECK-MATCH.                                                08579355
            EXEC SQL                                                    08579412
              SELECT TRN_NBR INTO :EPTRN-TRN-NBR                        08579558
              FROM                                                      08579658
                  TEPTRN T,                                             08579756
                  TEPPART P                                             08579856
              WHERE P.SLS_DTE =:EPPART-SLS-DTE                          08579956
              AND T.PARTN_NBR = P.PARTN_NBR                             08580056
              AND T.STR_NBR = :EPTRN-STR-NBR                            08580156
              AND T.RGST_ID = :EPTRN-RGST-ID                            08580260
              AND T.TRN_NBR = :EPTRN-TRN-NBR                            08580356
            END-EXEC.                                                   08580413
                                                                        08580513
           EVALUATE TRUE                                                08580613
               WHEN SQLCODE = +0                                        08580713
               WHEN SQLCODE = +100                                      08580813
               WHEN SQLCODE = -811                                      08580956
                    CONTINUE                                            08581056
               WHEN SQLWARN0 NOT EQUAL SPACE                            08581156
               WHEN SQLCODE NOT EQUAL ZERO                              08581256
                    MOVE '3000-FETCH-CURSOR' TO  PV-PARAGRAPH-NAME      08581356
                    MOVE 'ERROR ON FETCHING' TO   PV-DB2-OPERATION      08581456
                    PERFORM 9900-SQL-ABEND-ROUTINE                      08581556
           END-EVALUATE.                                                08581656
      * 3000-FETCH-CURSOR-EXIT.                                         08581756
      ***************************************************************** 08581856
      *  4000-WRTITE-ECOM-EXTRACT                                       08581956
      ***************************************************************** 08582056
       4000-WRTITE-ECOM-MISS.                                           08582166
            WRITE ECOM-MISS-RECORD FROM PV-ECOM-RECORD.                 08582265
            ADD 1 TO PV-WRITE-CNT.                                      08582356
      *4000-WRTITE-ECOM-EXTRACT-EXIT.                                   08583056
      ***************************************************************** 08583756
      * 9900-SQL-ABEND-ROUTINE                                          08583856
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.08583956
      ***************************************************************** 08584056
       9900-SQL-ABEND-ROUTINE.                                          08585056
           DISPLAY '** ABEND     **'.                                   08590000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        08600000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              08610000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               08620000
      ***************************************************************** 08680000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     08690000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      08700000
      ***************************************************************** 08710000
            COPY DPPD004.                                               08720000
                                                                        08730000
           SET AC-DB2-ERROR TO TRUE.                                    08740000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         08750000
                                                                        08760000
      ***************************************************************** 08770028
      * 9999-END-OF-ROUTINE                                             08780028
      ***************************************************************** 08810028
        9999-END-OF-ROUTINE.                                            08810131
           CLOSE ECOM-EXT-FILE ECOM-MISS-FILE.                          08811059
           DISPLAY 'NUMBER OF RECORDS READ   :' PV-READ-CNT.            08811173
           DISPLAY 'NUMBER OF RECORDS WRITTEN:' PV-WRITE-CNT.           08811269
           DISPLAY 'END OF SAKUT132 PROGRAM'.                           08812070
      *9999-END-OF-ROUTINE-EXIT                                         08820028
