00001 *************************                                         07/10/92
00002  IDENTIFICATION DIVISION.                                         DPKUT017
00003 *************************                                            LV001
00004  PROGRAM-ID.      DPKUT017.                                       DPKUT017
00005  AUTHOR.          D.MEDORA, CAP GEMINI AMERICA.                   DPKUT017
00006  INSTALLATION.    KOHLS DEPARTMENT STORES.                        DPKUT017
00007  DATE-WRITTEN.    AUGUST 18, 1988.                                DPKUT017
00008  DATE-COMPILED.                                                   DPKUT017
00009 *                                                                 DPKUT017
00010 *             THIS PROGRAM USES THE OUTPUT FROM UTILITY IEBPTPCH, DPKUT017
00011 *             THAT CONTAINS THE PKMO.CONTROL DATA.  THE PROGRAM   DPKUT017
00012 *             WILL SEARCH FOR DB2  AND DYL280 PROGRAM NAMES AND   DPKUT017
00013 *             CREATE THE A FILE TO BE USED TO MATCH AGAINST THE   DPKUT017
00014 *             PROCXREF FILE IN PROGRAM DPKUT002.                  DPKUT017
00015 *                                                                 DPKUT017
00016 *************************                                         DPKUT017
00017 * --> CHANGES:                                                    DPKUT017
00018 *************************                                         DPKUT017
00019 *     4/10/89 COLLEEN KOBE (STRATAGEM)                            DPKUT017
00020 *             ADDED CHECK FOR IMS/DB2 PROGRAMS AND WROTE OUT      DPKUT017
00021 *             CONTROL FILE RECORD FOR THOSE. ALSO ADDED PLAN TO   DPKUT017
00022 *             OUTPUT RECORD FOR DB2 PROGRAMS.                     DPKUT017
00023 *************************                                         DPKUT017
00024  ENVIRONMENT DIVISION.                                            DPKUT017
00025 *************************                                         DPKUT017
00026  CONFIGURATION SECTION.                                           DPKUT017
00027  SOURCE-COMPUTER. IBM-3090.                                       DPKUT017
00028  OBJECT-COMPUTER. IBM-3090.                                       DPKUT017
00029                                                                   DPKUT017
00030  INPUT-OUTPUT SECTION.                                            DPKUT017
00031  FILE-CONTROL.                                                    DPKUT017
00032      SELECT CONTROL-IEBPTPCH-FILE      ASSIGN TO INP01.           DPKUT017
00033      SELECT CONTROL-PROGRAM-FILE       ASSIGN TO OUT01.           DPKUT017
00034                                                                   DPKUT017
00035 ***************                                                   DPKUT017
00036  DATA DIVISION.                                                   DPKUT017
00037 ***************                                                   DPKUT017
00038  FILE SECTION.                                                    DPKUT017
00039  FD  CONTROL-IEBPTPCH-FILE                                        DPKUT017
00040                          BLOCK CONTAINS 0 RECORDS                 DPKUT017
00041                          RECORD CONTAINS 81 CHARACTERS            DPKUT017
00042                          LABEL RECORDS ARE STANDARD               DPKUT017
00043                          RECORDING MODE F                         DPKUT017
00044                          DATA RECORD IS CONTROL-IEBPTPCH-RECORD.  DPKUT017
00045  01  CONTROL-IEBPTPCH-RECORD PIC X(81).                           DPKUT017
00046                                                                   DPKUT017
00047  FD  CONTROL-PROGRAM-FILE                                         DPKUT017
00048                          BLOCK CONTAINS 0 RECORDS                 DPKUT017
00049                          RECORD CONTAINS 28 CHARACTERS            DPKUT017
00050                          LABEL RECORDS ARE STANDARD               DPKUT017
00051                          RECORDING MODE F                         DPKUT017
00052                          DATA RECORD IS CONTROL-PROGRAM-RECORD.   DPKUT017
00053  01  CONTROL-PROGRAM-RECORD  PIC X(28).                           DPKUT017
00054                                                                   DPKUT017
00055      EJECT                                                        DPKUT017
00056                                                                   DPKUT017
00057  WORKING-STORAGE SECTION.                                         DPKUT017
00058                                                                   DPKUT017
00059 **   CONTROL-IEBPTPCH-FILE LAYOUT (INPUT)    **                   DPKUT017
00060                                                                   DPKUT017
00061 *    COPY DPRD003I.                                               DPKUT017
00062      COPY DPRD003I.                                               DPKUT017
00063                                                                   DPKUT017
00064 **   JOBNAME-PROCNAME-FILE LAYOUT (OUTPUT)  **                    DPKUT017
00065                                                                   DPKUT017
00066 *    COPY DPRD004O.                                               DPKUT017
00067      COPY DPRD004O.                                               DPKUT017
00068                                                                   DPKUT017
00069  01  PROGRAM-SWITCHES.                                            DPKUT017
00070      05  PS-NO                   PIC X          VALUE 'N'.        DPKUT017
00071      05  PS-YES                  PIC X          VALUE 'Y'.        DPKUT017
00072      05  PS-CONTROL-EOF          PIC X          VALUE 'N'.        DPKUT017
00073          88  CONTROL-EOF                        VALUE 'Y'.        DPKUT017
00074      05  PS-FIRST-TIME           PIC X          VALUE 'Y'.        DPKUT017
00075          88  FIRST-TIME                         VALUE 'Y'.        DPKUT017
00076      05  PS-WRITE                PIC X          VALUE 'N'.        DPKUT017
00077                                                                   DPKUT017
00078      EJECT                                                        DPKUT017
00079                                                                   DPKUT017
00080  01  ABEND-CODE                  PIC S9(04)     VALUE +0     COMP.DPKUT017
00081  01  ABEND-4007                  PIC S9(04)     VALUE +4007  COMP.DPKUT017
00082                                                                   DPKUT017
00083  01  WS-CONTROL-NAME             PIC X(08)      VALUE SPACES.     DPKUT017
00084  01  WS-PROGRAM-NAME             PIC X(08)      VALUE SPACES.     DPKUT017
00085  01  WS-TYPE                     PIC X(04)      VALUE SPACES.     DPKUT017
00086  01  WS-PLAN-NAME                PIC X(08)      VALUE SPACES.     DPKUT017
00087                                                                   DPKUT017
00088  01  UNSTRING-PARTS.                                              DPKUT017
00089      05  WS-PART1                PIC X(10).                       DPKUT017
00090      05  WS-PART2                PIC X(10).                       DPKUT017
00091      05  WS-PART3                PIC X(10).                       DPKUT017
00092      05  WS-PART4                PIC X(10).                       DPKUT017
00093      05  WS-PART5                PIC X(10).                       DPKUT017
00094      05  WS-PART6                PIC X(10).                       DPKUT017
00094      05  WS-PART7                PIC X(10).                       DPKUT017
00095                                                                   DPKUT017
00096      EJECT                                                        DPKUT017
00097 ********************                                              DPKUT017
00098  PROCEDURE DIVISION.                                              DPKUT017
00099 ********************                                              DPKUT017
00100                                                                   DPKUT017
00101                                                                   DPKUT017
00102      OPEN INPUT  CONTROL-IEBPTPCH-FILE                            DPKUT017
00103           OUTPUT CONTROL-PROGRAM-FILE.                            DPKUT017
00104                                                                   DPKUT017
00105                                                                   DPKUT017
00106      MOVE SPACES TO UNSTRING-PARTS.                               DPKUT017
00107      MOVE 'N' TO PS-WRITE.                                        DPKUT017
00108                                                                   DPKUT017
00109                                                                   DPKUT017
00110      PERFORM 9100-READ-CONTROL-IEBPTPCH-REC.                      DPKUT017
00111      IF DP003I-MEMBER-NAME EQUAL TO 'MEMBER NAME'                 DPKUT017
00112          MOVE DP003I-CONTROL-NAME TO WS-CONTROL-NAME              DPKUT017
00113      ELSE                                                         DPKUT017
00114          DISPLAY 'PROGRAM DPKUT017 IS ABENDING'                   DPKUT017
00115          DISPLAY 'INPUT FILE OUT OF SEQUENCE'                     DPKUT017
00116          MOVE ABEND-4007 TO ABEND-CODE                            DPKUT017
00117          CALL 'ILBOABN0' USING ABEND-CODE                         DPKUT017
00118      END-IF.                                                      DPKUT017
00119                                                                   DPKUT017
00120                                                                   DPKUT017
00121      PERFORM 1000-PROCESS-CONTROL-FILE THRU  1000-EXIT            DPKUT017
00122              UNTIL CONTROL-EOF.                                   DPKUT017
00123                                                                   DPKUT017
00124                                                                   DPKUT017
00125      IF PS-WRITE  EQUAL TO  'Y'                                   DPKUT017
00126          PERFORM 8000-BUILD-CONTROL-PROGRAM THRU 8000-EXIT        DPKUT017
00127      END-IF.                                                      DPKUT017
00128                                                                   DPKUT017
00129                                                                   DPKUT017
00130      CLOSE       CONTROL-IEBPTPCH-FILE                            DPKUT017
00131                  CONTROL-PROGRAM-FILE.                            DPKUT017
00132                                                                   DPKUT017
00133                                                                   DPKUT017
00134      MOVE ZEROES TO RETURN-CODE.                                  DPKUT017
00135      GOBACK.                                                      DPKUT017
00136                                                                   DPKUT017
00137      EJECT                                                        DPKUT017
00138                                                                   DPKUT017
00139 ******************************************************************DPKUT017
00140 *** 1000 THIS PARAGRAPH PROCESSES THE INPUT FILE FROM "IEBPTPCH"  DPKUT017
00141 ***      BY TAKING THE MEMBER NAME FROM THE PDS AS THE JOBNAME.   DPKUT017
00142 ***      IT THEN ELIMINATES UNNECCESSARY JCL LINES.  VALID JCL    DPKUT017
00143 ***      LINES ARE UNSTRUNG TO FIND JOB AND EXEC DATA.            DPKUT017
00144 ******************************************************************DPKUT017
00145  1000-PROCESS-CONTROL-FILE.                                       DPKUT017
00146                                                                   DPKUT017
00147      IF DP003I-MEMBER-NAME EQUAL TO 'MEMBER NAME'                 DPKUT017
00148          IF FIRST-TIME                                            DPKUT017
00149              MOVE 'N' TO PS-FIRST-TIME                            DPKUT017
00150          ELSE                                                     DPKUT017
00151              IF  PS-WRITE  EQUAL TO  'Y'                          DPKUT017
00152                  PERFORM 8000-BUILD-CONTROL-PROGRAM THRU 8000-EXITDPKUT017
00153              END-IF                                               DPKUT017
00154              MOVE DP003I-CONTROL-NAME TO WS-CONTROL-NAME          DPKUT017
00155              MOVE SPACES           TO WS-PROGRAM-NAME             DPKUT017
00156              MOVE SPACES           TO WS-PLAN-NAME                DPKUT017
00157              MOVE SPACES           TO WS-TYPE                     DPKUT017
00158              MOVE SPACES           TO UNSTRING-PARTS              DPKUT017
00159              MOVE 'N' TO PS-WRITE                                 DPKUT017
00160          END-IF                                                   DPKUT017
00161      ELSE                                                         DPKUT017
00162          PERFORM 1100-UNSTRING-CONTROL-LINE THRU 1100-EXIT        DPKUT017
00163      END-IF.                                                      DPKUT017
00164                                                                   DPKUT017
00165      PERFORM 9100-READ-CONTROL-IEBPTPCH-REC.                      DPKUT017
00166                                                                   DPKUT017
00167  1000-EXIT.                                                       DPKUT017
00168      EXIT.                                                        DPKUT017
00169      EJECT                                                        DPKUT017
00170                                                                   DPKUT017
00171 ******************************************************************DPKUT017
00172 *** 1100 UNSTRINGS THE CONTROL LINE TO FIND DBII AND DYL280 NAMES.DPKUT017
00173 ******************************************************************DPKUT017
00174  1100-UNSTRING-CONTROL-LINE.                                      DPKUT017
00175                                                                   DPKUT017
00176      INSPECT DP003I-CONTROL-LINE                                  DPKUT017
00177          REPLACING ALL ',' BY ' '.                                DPKUT017
00178                                                                   DPKUT017
00179      INSPECT DP003I-CONTROL-LINE                                  DPKUT017
00180          REPLACING ALL '(' BY ' '.                                DPKUT017
00181                                                                   DPKUT017
00182      INSPECT DP003I-CONTROL-LINE                                  DPKUT017
00183          REPLACING ALL ')' BY ' '.                                DPKUT017
00184                                                                   DPKUT017
00185      MOVE SPACES TO UNSTRING-PARTS.                               DPKUT017
00186                                                                   DPKUT017
00187      UNSTRING DP003I-CONTROL-LINE                                 DPKUT017
00188        DELIMITED BY ALL SPACES                                    DPKUT017
00189        INTO WS-PART1                                              DPKUT017
00190             WS-PART2                                              DPKUT017
00191             WS-PART3                                              DPKUT017
00192             WS-PART4                                              DPKUT017
00193             WS-PART5                                              DPKUT017
00194             WS-PART6                                              DPKUT017
00194             WS-PART7                                              DPKUT017
00195      END-UNSTRING.                                                DPKUT017
00196                                                                   DPKUT017
00197      IF WS-PART1  EQUAL TO  'OPTION'  AND                         DPKUT017
00198         WS-PART2  EQUAL TO  'RESTORE'                             DPKUT017
00199          MOVE 'Y' TO PS-WRITE                                     DPKUT017
00200          MOVE WS-PART3 TO WS-PROGRAM-NAME                         DPKUT017
00201          MOVE 'DYL '   TO WS-TYPE                                 DPKUT017
00202      END-IF.                                                      DPKUT017
00203                                                                   DPKUT017
00204      IF WS-PART1  EQUAL TO  SPACES    AND                         DPKUT017
00205         WS-PART2  EQUAL TO  'OPTION'  AND                         DPKUT017
00206         WS-PART3  EQUAL TO  'RESTORE'                             DPKUT017
00207          MOVE 'Y' TO PS-WRITE                                     DPKUT017
00208          MOVE WS-PART4 TO WS-PROGRAM-NAME                         DPKUT017
00209          MOVE 'DYL '   TO WS-TYPE                                 DPKUT017
00210      END-IF.                                                      DPKUT017
00211                                                                   DPKUT017
00212      IF WS-PART1  EQUAL TO  'RUN'     AND                         DPKUT017
00213         (WS-PART2  EQUAL TO  'PROG' OR 'PROGRAM')                 DPKUT017
00214          MOVE 'Y' TO PS-WRITE                                     DPKUT017
00215          MOVE WS-PART3 TO WS-PROGRAM-NAME                         DPKUT017
00216          MOVE 'DB2 '   TO WS-TYPE                                 DPKUT017
00217          IF WS-PART4 EQUAL TO 'PLAN'                              DPKUT017
00218             MOVE WS-PART5 TO WS-PLAN-NAME                         DPKUT017
00219          END-IF                                                   DPKUT017
00220      END-IF.                                                      DPKUT017
00221                                                                   DPKUT017
00222      IF WS-PART1  EQUAL TO  SPACES    AND                         DPKUT017
00223         WS-PART2  EQUAL TO  'RUN'     AND                         DPKUT017
00224         (WS-PART3  EQUAL TO  'PROG' OR 'PROGRAM')                 DPKUT017
00225          MOVE 'Y' TO PS-WRITE                                     DPKUT017
00226          MOVE WS-PART4 TO WS-PROGRAM-NAME                         DPKUT017
00227          MOVE 'DB2 '   TO WS-TYPE                                 DPKUT017
00228          IF WS-PART5 EQUAL TO 'PLAN'                              DPKUT017
00229             MOVE WS-PART6 TO WS-PLAN-NAME                         DPKUT017
00230          END-IF                                                   DPKUT017
00231      END-IF.                                                      DPKUT017
00232                                                                   DPKUT017
00233      IF WS-PART1  EQUAL TO  SPACES    AND                         DPKUT017
00234         WS-PART2  EQUAL TO  'PLAN'                                DPKUT017
00235          MOVE 'Y' TO PS-WRITE                                     DPKUT017
00236          MOVE WS-PART3 TO WS-PLAN-NAME                            DPKUT017
00237          MOVE 'DB2 '   TO WS-TYPE                                 DPKUT017
00238      END-IF.                                                      DPKUT017
00239                                                                   DPKUT017
00240      IF WS-PART1  EQUAL TO ('DB2T' OR 'DB2P') AND                 DPKUT017
00241         WS-PART2  EQUAL TO  'SYS1'            AND                 DPKUT017
00242         WS-PART3  EQUAL TO  'DSNMIN10'                            DPKUT017
00243          MOVE 'Y' TO PS-WRITE                                     DPKUT017
00244          MOVE WS-PART7 TO WS-PROGRAM-NAME                         DPKUT017
00245          MOVE WS-PART6 TO WS-PLAN-NAME                            DPKUT017
00246          MOVE 'IMSD'   TO WS-TYPE                                 DPKUT017
00247      END-IF.                                                      DPKUT017
00248                                                                   DPKUT017
00249  1100-EXIT.                                                       DPKUT017
00250      EXIT.                                                        DPKUT017
00251                                                                   DPKUT017
00252                                                                   DPKUT017
00253                                                                   DPKUT017
00254  8000-BUILD-CONTROL-PROGRAM.                                      DPKUT017
00255                                                                   DPKUT017
00256      MOVE WS-CONTROL-NAME             TO  DP004O-CONTROL-NAME.    DPKUT017
00257      MOVE WS-PROGRAM-NAME             TO  DP004O-PROGRAM-NAME.    DPKUT017
00258      MOVE WS-TYPE                     TO  DP004O-TYPE.            DPKUT017
00259      MOVE WS-PLAN-NAME                TO  DP004O-PLAN-NAME.       DPKUT017
00260                                                                   DPKUT017
00261      INSPECT DP004O-PLAN-NAME                                     DPKUT017
00262          REPLACING ALL ')' BY ' '.                                DPKUT017
00263                                                                   DPKUT017
00264      PERFORM 9200-WRITE-CONTROL-PROGRAM  THRU  9200-EXIT.         DPKUT017
00265                                                                   DPKUT017
00266  8000-EXIT.                                                       DPKUT017
00267      EXIT.                                                        DPKUT017
00268      EJECT                                                        DPKUT017
00269                                                                   DPKUT017
00270 ******************************************************************DPKUT017
00271 *** 9100 THIS PARAGRAPH READS THE INPUT FILE FROM "IEBPTPCH".     DPKUT017
00272 ******************************************************************DPKUT017
00273  9100-READ-CONTROL-IEBPTPCH-REC.                                  DPKUT017
00274                                                                   DPKUT017
00275      READ CONTROL-IEBPTPCH-FILE INTO                              DPKUT017
00276                   DP003I-CONTROL-IEBPTPCH-RECORD                  DPKUT017
00277          AT END                                                   DPKUT017
00278              MOVE PS-YES          TO PS-CONTROL-EOF.              DPKUT017
00279                                                                   DPKUT017
00280  9100-EXIT.                                                       DPKUT017
00281      EXIT.                                                        DPKUT017
00282                                                                   DPKUT017
00283 ******************************************************************DPKUT017
00284 *** 9200 THIS PARAGRAPH READS THE INPUT FILE FROM "IEBPTPCH".     DPKUT017
00285 ******************************************************************DPKUT017
00286  9200-WRITE-CONTROL-PROGRAM.                                      DPKUT017
00287                                                                   DPKUT017
00288      WRITE CONTROL-PROGRAM-RECORD  FROM                           DPKUT017
00289            DP004O-RECORD.                                         DPKUT017
00290                                                                   DPKUT017
00291  9200-EXIT.                                                       DPKUT017
00292      EXIT.                                                        DPKUT017
00293                                                                   DPKUT017
