000100 TITLE 'RESTORE PC.MERCHANDISE.ADJ.Q'.                                    
000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    PCKUT002.                                                 
000400 AUTHOR.        DENNIS STEFFEN.                                           
000500 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000600 DATE-WRITTEN.  EARLY WINTER 2004.                                        
000700 DATE-COMPILED.                                                           
000800                                                                          
000900*     INPUT:                                                              
001000*        MESSAGE QUEUE EXTRACT                                            
001100*        FILE CONTAINING QUE MANAGER NAME                                 
001200*        FILE CONTAINING QUE NAME                                         
001300*     OUTPUT:                                                             
001400*        PC.MERCHANDISE.ADJ.Q                                             
001500*                                                                         
001600 ENVIRONMENT DIVISION.                                                    
001700 CONFIGURATION SECTION.                                                   
001800 SOURCE-COMPUTER.  IBM-3090.                                              
001900 OBJECT-COMPUTER.  IBM-3090.                                              
002000                                                                          
002100 INPUT-OUTPUT SECTION.                                                    
002200                                                                          
002300 FILE-CONTROL.                                                            
002400                                                                          
002500*    THIS CONTAINS THE MESSAGES TO BE RESTORED                            
002600     SELECT EXTRACT-FILE                                                  
002700             ASSIGN TO INP01.                                             
002800                                                                          
002900     SELECT QMGR-FILE                                                     
003000             ASSIGN TO INP02.                                             
003100                                                                          
003200     SELECT QNAME-FILE                                                    
003300             ASSIGN TO INP03.                                             
003400                                                                          
003500 DATA DIVISION.                                                           
003600 FILE SECTION.                                                            
003700                                                                          
003800 FD  EXTRACT-FILE                                                         
003900     RECORDING MODE F                                                     
004000     BLOCK CONTAINS 0 RECORDS                                             
004100     LABEL RECORDS ARE OMITTED.                                           
004200 01  EXTRACT-REC                      PIC X(0500).                        
004300                                                                          
004400 FD  QMGR-FILE                                                            
004500     RECORDING MODE F                                                     
004600     BLOCK CONTAINS 0 RECORDS                                             
004700     LABEL RECORDS ARE OMITTED.                                           
004800 01  QMGR-CARD                        PIC  X(80).                         
004900                                                                          
005000 FD  QNAME-FILE                                                           
005100     RECORDING MODE F                                                     
005200     BLOCK CONTAINS 0 RECORDS                                             
005300     LABEL RECORDS ARE OMITTED.                                           
005400 01  QNAME-CARD                       PIC  X(80).                         
005500                                                                          
005600/                                                                         
005700 WORKING-STORAGE SECTION.                                                 
005800                                                                          
005900 01  PS-SWITCHES.                                                         
006000     05  PS-ERROR-SW                  PIC X(01) VALUE 'N'.                
006100         88  PS-ERROR-NO                        VALUE 'N'.                
006200         88  PS-ERROR-YES                       VALUE 'Y'.                
006300     05  PS-END-OF-EXTRACT-SW         PIC X(01) VALUE 'N'.                
006400         88  PS-END-OF-EXTRACT-NO               VALUE 'N'.                
006500         88  PS-END-OF-EXTRACT-YES              VALUE 'Y'.                
006600     05  PS-PACK-SW                   PIC X(01) VALUE SPACE.              
006700         88  PS-PACK-NO                         VALUE SPACE.              
006800         88  PS-PACK-YES                        VALUE 'P'.                
006900                                                                          
007000 01  PV-QMGR-CARD.                                                        
007100     05  PV-QMGR-NAME              PIC X(48).                             
007200     05  FILLER                    PIC X(32).                             
007300                                                                          
007400 01  PV-QNAME-CARD.                                                       
007500     05  PV-QNAME                  PIC X(48).                             
007600     05  FILLER                    PIC X(32).                             
007700                                                                          
007800*    IS001-MERCH-ADJUST-RECORD.                                           
007900     COPY ISRD001.                                                        
008000                                                                          
008100 01  PV-CRAPOLA.                                                          
008200     05  PV-START                     PIC S9(05) COMP-3                   
008300                                             VALUE ZERO.                  
008400     05  PV-LENGTH                    PIC S9(05) COMP-3                   
008500                                             VALUE ZERO.                  
008600     05  PV-COMMIT                    PIC S9(09) COMP-3                   
008700                                             VALUE ZERO.                  
008800     05  PV-TOTAL-PUT-COUNT           PIC S9(09) COMP-3                   
008900                                             VALUE ZERO.                  
009000     05  PV-READ-COUNT                PIC S9(09) COMP-3                   
009100                                             VALUE ZERO.                  
009200     05  PV-DISP-NUM                  PIC ZZZ,ZZZ,ZZ9.                    
009300                                                                          
009400     05  ERROR-MESSAGE                PIC X(48) VALUE SPACES.             
009500     05  PV-MQ-TEXT-REASON            PIC X(30) VALUE SPACES.             
009600     05  PV-PARAGRAPH                 PIC X(48) VALUE SPACES.             
009700*   MQ-SERIES PARAMETER VARIABLES                                         
009800                                                                          
009900 01  QM-NAME                          PIC X(48).                          
010000 01  QNAME                            PIC X(48).                          
010100 01  MSGBUFFER                        PIC X(500) VALUE SPACES.            
010200 01  PV-MSGLENGTH                     PIC S9(9) BINARY.                   
010300 01  MQ-REASON-TEXT                   PIC X(30) VALUE SPACES.             
010400 01  HOLD-REASON           PIC S9(9) BINARY.                              
010500 01  HOLD-COMPLETION-CODE  PIC S9(9) BINARY.                              
010600                                                                          
010700*    MQSERIES API FIELDS                                                  
010800                                                                          
010900 01  HCONN                   PIC S9(9) BINARY VALUE 0.                    
011000 01  PQ-HANDLE               PIC S9(9) BINARY VALUE 0.                    
011100 01  OPEN-OPTIONS            PIC S9(9) BINARY.                            
011200 01  COMPLETION-CODE         PIC S9(9) BINARY.                            
011300 01  REASON                  PIC S9(9) BINARY.                            
011400 01  CON-REASON              PIC S9(9) BINARY.                            
011500*                                                                         
011600*    API CONTROL BLOCKS                                                   
011700*                                                                         
011800 01  MQM-OBJECT-DESCRIPTOR.                                               
011900     COPY CMQODV.                                                         
012000 01  MQM-MESSAGE-DESCRIPTOR.                                              
012100     COPY CMQMDV.                                                         
012200 01  MQM-PUT-MESSAGE-OPTIONS.                                             
012300     COPY CMQPMOV.                                                        
012400*                                                                         
012500*    MQV CONTAINS CONSTANTS (FOR FILLING IN THE CONTROL BLOCKS)           
012600*    AND RETURN CODES (FOR TESTING THE RESULT OF A CALL)                  
012700*                                                                         
012800 01  MQM-CONSTANTS.                                                       
012900     COPY CMQV.                                                           
013000                                                                          
013100 01  PV-ABEND-VARIABLES.                                                  
013200     05  PV-ABEND-CODE              PIC S9(04) VALUE +0 COMP SYNC.        
013300         88 ABEND-4001-WRITE-ERROR                   VALUE +4001.         
013400         88 ABEND-4002-READ-ERROR                    VALUE +4002.         
013500         88 ABEND-4003-CTRL-CARD-ERROR               VALUE +4003.         
013600         88 ABEND-4004-TABLE-ERROR                   VALUE +4004.         
013700         88 ABEND-4005-OPEN-ERROR                    VALUE +4005.         
013800         88 ABEND-4006-CLOSE-ERROR                   VALUE +4006.         
013900         88 ABEND-4007-SEQUENCE-ERROR                VALUE +4007.         
014000         88 ABEND-4008-DATA-ERROR                    VALUE +4008.         
014100         88 ABEND-4009-KEY-DATA-ERROR                VALUE +4009.         
014200         88 ABEND-4013-DB2-ERROR                     VALUE +4013.         
014300         88 ABEND-4019-CAL-SUB-ERROR                 VALUE +4019.         
014400         88 ABEND-4020-MQ-ERROR                      VALUE +4020.         
014500         88 ABEND-4444-FORCED-ABEND                  VALUE +4444.         
014600                                                                          
014700                                                                          
014800 01  PE-MQ-ABEND-VARIABLES.                                               
014900     10  PE-MQ-QMANAGER           PIC X(05) VALUE SPACES.                 
015000     10  PE-MQ-QNAME              PIC X(30) VALUE SPACES.                 
015100     10  PE-MQ-COMPLETION-CODE    PIC 9(04) VALUE ZEROS.                  
015200     10  PE-MQ-REASON-CODE        PIC 9(04) VALUE ZEROS.                  
015300                                                                          
015400 01  MQ-CONSTANTS.                                                        
015500     05 WS-MQCONN            PIC X(8)  VALUE 'CSQBCONN'.                  
015600     05 WS-MQOPEN            PIC X(8)  VALUE 'CSQBOPEN'.                  
015700     05 WS-MQINQ             PIC X(8)  VALUE 'CSQBINQ'.                   
015800     05 WS-MQPUT             PIC X(8)  VALUE 'CSQBPUT'.                   
015900     05 WS-MQCLOSE           PIC X(8)  VALUE 'CSQBCLOS'.                  
016000     05 WS-MQDISC            PIC X(8)  VALUE 'CSQBDISC'.                  
016100     05 WS-MQCHECK           PIC X(8)  VALUE 'MQR2C'.                     
016200     05 WS-MQCMIT            PIC X(8)  VALUE 'CSQBCOMM'.                  
016300     05 WS-MQBACK            PIC X(8)  VALUE 'CSQBBACK'.                  
016400                                                                          
016500************************************************************              
016600* END OF MQ-SERIES STUFF                                                  
016700************************************************************              
016800                                                                          
016900 LINKAGE SECTION.                                                         
017000 01  RUN-PARMS.                                                           
017100     05  PARM-LENGTH                  PIC S9(04) COMP.                    
017200     05  PARM-USERID                  PIC X(07).                          
017300/                                                                         
017400 PROCEDURE DIVISION USING RUN-PARMS.                                      
017500                                                                          
017600     PERFORM 1000-INITIALIZE.                                             
017700                                                                          
017800     PERFORM 2000-PROCESS                                                 
017900         UNTIL PS-END-OF-EXTRACT-YES                                      
018000               OR PS-ERROR-YES                                            
018100                                                                          
018200     PERFORM 9999-END.                                                    
018300                                                                          
018400     GOBACK.                                                              
018500                                                                          
018600 1000-INITIALIZE.                                                         
018700                                                                          
018800     DISPLAY SPACES                                                       
018900     DISPLAY '***********  PCKUT002  ***************'                     
019000                                                                          
019100     OPEN INPUT                                                           
019200            EXTRACT-FILE                                                  
019300            QMGR-FILE                                                     
019400            QNAME-FILE                                                    
019500                                                                          
019600     READ QMGR-FILE                                                       
019700         INTO PV-QMGR-CARD                                                
019800     AT END                                                               
019900         DISPLAY 'ERROR - QMGR NAME MISSING'                              
020000         SET PS-ERROR-YES             TO TRUE                             
020100     END-READ                                                             
020200                                                                          
020300     IF PS-ERROR-NO                                                       
020400         READ QNAME-FILE                                                  
020500             INTO PV-QNAME-CARD                                           
020600         AT END                                                           
020700             DISPLAY 'ERROR - QUEUE NAME MISSING'                         
020800             SET PS-ERROR-YES         TO TRUE                             
020900         END-READ                                                         
021000     END-IF                                                               
021100                                                                          
021200     IF PS-ERROR-NO                                                       
021300         PERFORM 1100-CONNECT                                             
021400     END-IF                                                               
021500                                                                          
021600     IF PS-ERROR-NO                                                       
021700         PERFORM 1200-OPEN-OUTPUT-QUE                                     
021800*    ELSE                                                                 
021900*        PERFORM 1300-DISCONNECT-QMGR                                     
022000     END-IF.                                                              
022100                                                                          
022200     IF PS-ERROR-NO                                                       
022300         PERFORM 8000-READ-EXTRACT                                        
022400     END-IF.                                                              
022500/                                                                         
022600 1100-CONNECT.                                                            
022700*    CONNECT TO THE QUEUE MANAGER                                         
022800*                                                                         
022900     MOVE PV-QMGR-NAME TO QM-NAME.                                        
023000     CALL WS-MQCONN USING QM-NAME                                         
023100                         HCONN                                            
023200                         COMPLETION-CODE                                  
023300                         REASON.                                          
023400*                                                                         
023500*    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE                      
023600*    AND EXIT                                                             
023700                                                                          
023800     MOVE REASON TO CON-REASON.                                           
023900     IF COMPLETION-CODE = MQCC-OK                                         
024000         DISPLAY 'MQCONN SUCCESSFUL   TO '  QM-NAME                       
024100     ELSE                                                                 
024200         MOVE 'MQCONN ERROR'          TO ERROR-MESSAGE                    
024300         PERFORM 9100-DISPLAY-ERROR-MESSAGE                               
024400         SET PS-ERROR-YES             TO TRUE                             
024500     END-IF.                                                              
024600/                                                                         
024700 1200-OPEN-OUTPUT-QUE.                                                    
024800*    OPEN THE QUEUE FOR OUTPUT                                            
024900                                                                          
025000     COMPUTE OPEN-OPTIONS = MQOO-OUTPUT +                                 
025100                            MQOO-FAIL-IF-QUIESCING +                      
025200                            MQOO-SET-IDENTITY-CONTEXT.                    
025300     MOVE PV-QNAME                 TO MQOD-OBJECTNAME.                    
025400     MOVE QM-NAME                  TO MQOD-OBJECTQMGRNAME.                
025500                                                                          
025600     CALL WS-MQOPEN USING HCONN                                           
025700                         MQM-OBJECT-DESCRIPTOR                            
025800                         OPEN-OPTIONS                                     
025900                         PQ-HANDLE                                        
026000                         COMPLETION-CODE                                  
026100                         REASON.                                          
026200*                                                                         
026300*    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                            
026400*    AND EXIT.                                                            
026500*                                                                         
026600     IF COMPLETION-CODE = MQCC-OK                                         
026700         DISPLAY PV-QNAME  ' OPENED SUCCESSFULLY FOR OUTPUT'              
026800     ELSE                                                                 
026900         SET PS-ERROR-YES             TO TRUE                             
027000         CALL WS-MQCHECK USING REASON                                     
027100                              PV-MQ-TEXT-REASON                           
027200         MOVE 'MQOPEN ERROR'          TO ERROR-MESSAGE                    
027300         PERFORM 9100-DISPLAY-ERROR-MESSAGE                               
027400*        PERFORM 1300-DISCONNECT-QMGR                                     
027500     END-IF.                                                              
027600                                                                          
027700 1300-DISCONNECT-QMGR.                                                    
027800                                                                          
027900     CALL WS-MQDISC USING HCONN                                           
028000                          COMPLETION-CODE                                 
028100                          REASON                                          
028200     IF COMPLETION-CODE = MQCC-OK                                         
028300         DISPLAY 'MQDISC SUCCESSFUL'                                      
028400     ELSE                                                                 
028500         SET PS-ERROR-YES             TO TRUE                             
028600         MOVE 'MQDISC ERROR'          TO ERROR-MESSAGE                    
028700         PERFORM 9100-DISPLAY-ERROR-MESSAGE                               
028800     END-IF.                                                              
028900                                                                          
029000 1400-CLOSE-QUE.                                                          
029100     CALL WS-MQCLOSE USING HCONN                                          
029200                          PQ-HANDLE                                       
029300                          MQCO-NONE                                       
029400                          COMPLETION-CODE                                 
029500                          REASON.                                         
029600     IF COMPLETION-CODE = MQCC-OK                                         
029700        DISPLAY 'MQCLOSE SUCCESSFUL'                                      
029800     ELSE                                                                 
029900         SET PS-ERROR-YES             TO TRUE                             
030000         CALL WS-MQCHECK USING REASON                                     
030100                              PV-MQ-TEXT-REASON                           
030200         MOVE 'MQCLOSE ERROR'  TO ERROR-MESSAGE                           
030300         PERFORM 9100-DISPLAY-ERROR-MESSAGE                               
030400     END-IF.                                                              
030500/                                                                         
030600 2000-PROCESS.                                                            
030700                                                                          
030800     INITIALIZE MSGBUFFER                                                 
030900     MOVE LENGTH OF IS001-MERCH-ADJUST-RECORD                             
031000                                        TO PV-MSGLENGTH                   
031100     MOVE IS001-MERCH-ADJUST-RECORD TO MSGBUFFER                          
031200     PERFORM 8200-PUT                                                     
031300                                                                          
031400     PERFORM 8000-READ-EXTRACT.                                           
031500/                                                                         
031600 3600-MQ-COMMIT.                                                          
031700                                                                          
031800     CALL WS-MQCMIT USING HCONN                                           
031900                          COMPLETION-CODE                                 
032000                          REASON.                                         
032100                                                                          
032200     IF COMPLETION-CODE = MQCC-OK                                         
032300         CONTINUE                                                         
032400     ELSE                                                                 
032500         CALL WS-MQCHECK USING REASON                                     
032600                               MQ-REASON-TEXT                             
032700        MOVE '3600-COMMIT-MQ'   TO PV-PARAGRAPH                           
032800        MOVE 'MQCMIT ERROR'     TO ERROR-MESSAGE                          
032900        MOVE COMPLETION-CODE    TO HOLD-COMPLETION-CODE                   
033000        MOVE REASON             TO HOLD-REASON                            
033100        PERFORM 3700-BACK-OUT-MQ                                          
033200        MOVE HOLD-COMPLETION-CODE TO COMPLETION-CODE                      
033300                                        PE-MQ-COMPLETION-CODE             
033400        MOVE HOLD-REASON          TO REASON                               
033500                                        PE-MQ-REASON-CODE                 
033600        PERFORM 9200-MQ-ABEND                                             
033700     END-IF.                                                              
033800                                                                          
033900 3700-BACK-OUT-MQ.                                                        
034000                                                                          
034100     CALL WS-MQBACK USING HCONN                                           
034200                          COMPLETION-CODE                                 
034300                          REASON.                                         
034400                                                                          
034500     IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
034600         CALL WS-MQCHECK USING REASON                                     
034700                            MQ-REASON-TEXT                                
034800        MOVE '3700-BACK-OUT-MQ'  TO PV-PARAGRAPH                          
034900        MOVE 'MQBACK ERROR'      TO ERROR-MESSAGE                         
035000        PERFORM 9200-MQ-ABEND                                             
035100     END-IF.                                                              
035200/                                                                         
035300 8000-READ-EXTRACT.                                                       
035400                                                                          
035500     READ EXTRACT-FILE                                                    
035600         INTO IS001-MERCH-ADJUST-RECORD                                   
035700     AT END                                                               
035800         SET PS-END-OF-EXTRACT-YES    TO TRUE                             
035900     NOT AT END                                                           
036000         SET PS-END-OF-EXTRACT-NO     TO TRUE                             
036100         ADD 1                        TO PV-READ-COUNT                    
036200     END-READ.                                                            
036300/                                                                         
036400 8200-PUT.                                                                
036500                                                                          
036600     MOVE MQMI-NONE                   TO MQMD-MSGID                       
036700                                                                          
036800     IF PARM-LENGTH = ZERO                                                
036900         MOVE MQCI-NONE               TO MQMD-CORRELID                    
037000     ELSE                                                                 
037100         MOVE PARM-USERID             TO MQMD-CORRELID                    
037200     END-IF                                                               
037300                                                                          
037400     MOVE 'USERIDENTFIR'              TO MQMD-USERIDENTIFIER              
037500     MOVE 'PUTAPPLNAME'               TO MQMD-PUTAPPLNAME                 
037600     MOVE 'APPLIDENTITYDATA'          TO MQMD-APPLIDENTITYDATA            
037700                                                                          
037800     MOVE '9'                         TO MQMD-PRIORITY                    
037900     MOVE MQFMT-STRING                TO MQMD-FORMAT                      
038000     MOVE MQPER-PERSISTENT            TO MQMD-PERSISTENCE                 
038100     COMPUTE MQPMO-OPTIONS  = MQPMO-SYNCPOINT +                           
038200                              MQPMO-SET-IDENTITY-CONTEXT                  
038300                                                                          
038400     CALL WS-MQPUT USING HCONN                                            
038500                        PQ-HANDLE                                         
038600                        MQMD                                              
038700                        MQPMO                                             
038800                        PV-MSGLENGTH                                      
038900                        MSGBUFFER                                         
039000                        COMPLETION-CODE                                   
039100                        REASON                                            
039200                                                                          
039300     IF COMPLETION-CODE = MQCC-OK                                         
039400        ADD 1                         TO PV-COMMIT                        
039500                                         PV-TOTAL-PUT-COUNT               
039600            IF PV-COMMIT > 100                                            
039700                PERFORM 3600-MQ-COMMIT                                    
039800                MOVE ZERO             TO PV-COMMIT                        
039900            END-IF                                                        
040000     ELSE                                                                 
040100         SET PS-ERROR-YES             TO TRUE                             
040200         CALL WS-MQCHECK USING REASON                                     
040300                               PV-MQ-TEXT-REASON                          
040400         MOVE 'MQPUT ERROR'           TO ERROR-MESSAGE                    
040500         PERFORM 9100-DISPLAY-ERROR-MESSAGE                               
040600     END-IF.                                                              
040700/                                                                         
040800 9100-DISPLAY-ERROR-MESSAGE.                                              
040900*                                                                         
041000     DISPLAY '**** ABEND *************************************'.          
041100     DISPLAY '* ', 'PCKUT002'                                             
041200     DISPLAY '* ', ERROR-MESSAGE.                                         
041300     DISPLAY '* COMPLETION CODE : ', COMPLETION-CODE.                     
041400     DISPLAY '* REASON CODE     : ', REASON.                              
041500     DISPLAY '* TEXT REASON     : ', PV-MQ-TEXT-REASON.                   
041600     DISPLAY '************************************************'.          
041700*    MOVE +4020 TO PV-ABEND-CODE.                                         
041800*    CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
041900*                                                                         
042000****************************************************************          
042100** PERFORM MQ-SERIES ABEND PROCESSING                                     
042200****************************************************************          
042300 9200-MQ-ABEND.                                                           
042400                                                                          
042500*    DISPLAY '**** ABEND *************************************'.          
042600*    DISPLAY '** MQSERIES ERROR **'.                                      
042700*    DISPLAY '** PROGRAM:    ', 'PCKUT002'.                               
042800*    DISPLAY '** ABEND CODE: ', PV-ABEND-CODE.                            
042900*    DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH.                             
043000*    DISPLAY '** MESSAGE:    ', ERROR-MESSAGE.                            
043100*    DISPLAY '** COMP CODE:  ', PE-MQ-COMPLETION-CODE.                    
043200*    DISPLAY '** RSN CODE:   ', PE-MQ-REASON-CODE.                        
043300*    DISPLAY '** RSN TEXT:   ', MQ-REASON-TEXT.                           
043400*                                                                         
043500                                                                          
043600     MOVE +4013 TO PV-ABEND-CODE.                                         
043700     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
043800/                                                                         
043900 9999-END.                                                                
044000                                                                          
044100     PERFORM 3600-MQ-COMMIT                                               
044200                                                                          
044300     MOVE PV-READ-COUNT               TO PV-DISP-NUM                      
044400     DISPLAY ' EXTRACTS READ    = ' PV-DISP-NUM                           
044500                                                                          
044600     MOVE PV-TOTAL-PUT-COUNT         TO PV-DISP-NUM                       
044700     DISPLAY ' QUE RECS WRITTEN = ' PV-DISP-NUM                           
044800                                                                          
044900     PERFORM 1400-CLOSE-QUE.                                              
045000                                                                          
045100     PERFORM 1300-DISCONNECT-QMGR                                         
045200                                                                          
045300     CLOSE  EXTRACT-FILE                                                  
045400            QMGR-FILE                                                     
045500            QNAME-FILE.                                                   
