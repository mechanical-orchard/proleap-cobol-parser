       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.             XSKRP018.                                00020000
       AUTHOR.                 KOHLS DEPARTMENT STORES.                 00030000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00040000
       DATE-WRITTEN.           01/29/2003.                              00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      ***XS DOMAIN - XST_DEPT REPORT                                    00080000
      ******************************************************************00090000
      **THIS PROGRAM FORMATS XST_DEPT ERROR DATA INTO A REPORT          00100000
      ******************************************************************00110000
      *DATE:01/29/2003   MADE BY:CHRIS ALBRIGHT INITIAL WRITE           00120000
      ******************************************************************00130008
092404**09/24/2004 CHANGED BY:GAYLE HAUPT  WR27107   FIELDS ADDED:      00140008
092404**     DMA-NBR, STR-ZN-CDE, BYR-NBR, CONT-CLR-EXCLN-IND           00150008
      ******************************************************************00160000
       ENVIRONMENT DIVISION.                                            00170000
       CONFIGURATION SECTION.                                           00180000
       SOURCE-COMPUTER.                        IBM-3090.                00190000
       OBJECT-COMPUTER.                        IBM-3090.                00200000
       INPUT-OUTPUT SECTION.                                            00210000
       FILE-CONTROL.                                                    00220000
                                                                        00230000
           SELECT INPUT-FILE                                            00240000
                  ASSIGN TO INP01.                                      00250000
           SELECT REPORT-FILE                                           00260000
                  ASSIGN TO RPT01.                                      00270000
                                                                        00280000
       DATA DIVISION.                                                   00290000
       FILE SECTION.                                                    00300000
                                                                        00310000
       FD  INPUT-FILE                                                   00320000
           RECORDING MODE IS F                                          00330000
           LABEL RECORDS ARE STANDARD                                   00340000
           BLOCK CONTAINS 0 CHARACTERS.                                 00350000
       01  INPUT-RECORD                    PIC X(164).                  00360008
                                                                        00370000
       FD  REPORT-FILE                                                  00380000
           RECORDING MODE IS F                                          00390000
           LABEL RECORDS ARE STANDARD                                   00400000
           BLOCK CONTAINS 0 CHARACTERS.                                 00410000
       01  REPORT-RECORD                   PIC X(133).                  00420000
                                                                        00430000
       WORKING-STORAGE SECTION.                                         00440000
      ******************************************************************00450000
      *SWITCHES                                                         00460000
      ******************************************************************00470000
       01  PS-PROGRAM-SWITCHES.                                         00480000
           05  PS-END-OF-INPUT-FILE-SW    PIC X(01)   VALUE 'N'.        00490000
               88  PS-EOF-INPUT-FILE                  VALUE 'Y'.        00500000
               88  PS-NOT-EOF-INPUT-FILE              VALUE 'N'.        00510000
      ******************************************************************00520000
      **TIME FIELDS                                                     00530000
      ******************************************************************00540000
       01  DF-DATETIME-FIELDS.                                          00550000
           05  DF-ACCEPT-TIME.                                          00560000
               10  DF-ACCEPT-HR             PIC 9(02) VALUE ZEROS.      00570000
               10  DF-ACCEPT-MIN            PIC 9(02) VALUE ZEROS.      00580000
               10  DF-ACCEPT-SEC            PIC 9(02) VALUE ZEROS.      00590000
               10  DF-ACCEPT-HSEC           PIC 9(02) VALUE ZEROS.      00600000
           05  DF-ACCEPT-DATE.                                          00610000
               10  DF-ACCEPT-YR             PIC 9(02) VALUE ZEROS.      00620000
               10  DF-ACCEPT-MO             PIC 9(02) VALUE ZEROS.      00630000
               10  DF-ACCEPT-DY             PIC 9(02) VALUE ZEROS.      00640000
      ******************************************************************00650000
      *PROGRAM CONSTANTS                                                00660000
      ******************************************************************00670000
       01  PC-PROGRAM-CONSTANTS.                                        00680000
           05  PC-PROGRAM-NAME            PIC X(08) VALUE 'XSKRP018'.   00690000
      ******************************************************************00700000
      **PROGRAM COUNTS                                                  00710000
      ******************************************************************00720000
       01  PC-PROGRAM-COUNTERS.                                         00730000
           05  PC-LINE-COUNT              PIC 9(03) VALUE ZEROS.        00740000
           05  PC-PAGE-COUNT              PIC 9(03) VALUE ZEROS.        00750000
           05  PC-RECORDS-READ            PIC 9(06) VALUE ZEROS.        00760000
      ***************************************************************** 00770000
      **ABEND CODE FIELDS                                               00780000
      ***************************************************************** 00790000
       01  ABEND-CODE                     PIC S9(4)                     00800000
                                                 VALUE ZEROS COMP SYNC. 00810000
       01  PV-MISC-ABEND-FIELDS.                                        00820000
           05  PV-PARAGRAPH-NAME          PIC  X(30)    VALUE SPACE.    00830000
           05  PV-OPERATION-MSG-1         PIC  X(40)    VALUE SPACE.    00840000
           05  PV-OPERATION-MSG-2         PIC  X(40)    VALUE SPACE.    00850000
           05  PV-DB2-OPERATION           PIC  X(30)    VALUE SPACES.   00860000
      ***************************************************************** 00870000
      **REPORT HEADINGS                                                 00880000
      ***************************************************************** 00890000
       01  REPORT-HEADING-1.                                            00900000
           05  FILLER                     PIC  X(56)  VALUE SPACES.     00910000
           05  FILLER                     PIC  X(22)                    00920000
                                 VALUE 'XS DOMAIN ERROR REPORT'.        00930000
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00940000
       01  REPORT-HEADING-2.                                            00950000
           05  FILLER                     PIC  X(54)  VALUE SPACES.     00960000
           05  FILLER                     PIC  X(25)                    00970000
                                 VALUE 'XST_DEPT INTEGRITY REPORT'.     00980000
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00990000
       01  REPORT-HEADING-3.                                            01000000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01010000
           05  FILLER                     PIC  X(08)                    01020000
                                 VALUE 'DEPT_NBR'.                      01030000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01040000
           05  FILLER                     PIC  X(10)                    01050000
                                 VALUE 'BUS_GP_NBR'.                    01060000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01070000
           05  FILLER                     PIC  X(07)                    01080000
                                 VALUE 'DEPT_NM'.                       01090000
           05  FILLER                     PIC  X(15)  VALUE SPACES.     01100009
           05  FILLER                     PIC  X(12)                    01110000
                                 VALUE 'MDSE_LNE_CDE'.                  01120000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01130009
092404     05  FILLER                     PIC  X(07)                    01140009
092404                           VALUE 'DMA_NBR'.                       01150009
092404     05  FILLER                     PIC  X(02)  VALUE SPACES.     01160009
092404     05  FILLER                     PIC  X(10)                    01170009
092404                           VALUE 'STR_ZN_CDE'.                    01180009
092404     05  FILLER                     PIC  X(02)  VALUE SPACES.     01190009
092404     05  FILLER                     PIC  X(07)                    01200009
092404                           VALUE 'BYR_NBR'.                       01210009
092404     05  FILLER                     PIC  X(02)  VALUE SPACES.     01220010
092404     05  FILLER                     PIC  X(18)                    01230009
092404                           VALUE 'CONT_CLR_EXCLN_IND'.            01240009
       01  REPORT-HEADING-4.                                            01250000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01260000
           05  FILLER                     PIC  X(08)  VALUE ALL '-'.    01270000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01280000
           05  FILLER                     PIC  X(10)  VALUE ALL '-'.    01290000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01300000
           05  FILLER                     PIC  X(20)  VALUE ALL '-'.    01310009
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01320000
           05  FILLER                     PIC  X(12)  VALUE ALL '-'.    01330009
092404     05  FILLER                     PIC  X(02)  VALUE SPACES.     01340009
092404     05  FILLER                     PIC  X(07)  VALUE ALL '-'.    01350009
092404     05  FILLER                     PIC  X(02)  VALUE SPACES.     01360009
092404     05  FILLER                     PIC  X(10)  VALUE ALL '-'.    01370009
092404     05  FILLER                     PIC  X(02)  VALUE SPACES.     01380009
092404     05  FILLER                     PIC  X(07)  VALUE ALL '-'.    01390009
092404     05  FILLER                     PIC  X(02)  VALUE SPACES.     01400009
092404     05  FILLER                     PIC  X(18)  VALUE ALL '-'.    01410009
      ***************************************************************** 01420000
      **REPORT DETAIL                                                   01430000
      ***************************************************************** 01440000
       01  REPORT-DETAIL-1.                                             01450000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01460000
           05  RD-XS-DEPT-NBR             PIC  ZZZ9.                    01470000
           05  FILLER                     PIC  X(06)  VALUE SPACES.     01480000
           05  RD-XS-BUS-GP-NBR           PIC  ZZ9.                     01490000
           05  FILLER                     PIC  X(09)  VALUE SPACES.     01500000
           05  RD-XS-BUS-DEPT-NM          PIC  X(20).                   01510009
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01520000
           05  RD-XS-MERCH-LNE-CDE        PIC  X(12).                   01530009
092404     05  FILLER                     PIC  X(02)  VALUE SPACES.     01540009
092404     05  RD-XS-DMA-NBR              PIC  ZZZ9.                    01550009
092404     05  FILLER                     PIC  X(05)  VALUE SPACES.     01560009
092404     05  RD-XS-STR-ZN-CDE           PIC  X(05).                   01570009
092404     05  FILLER                     PIC  X(07)  VALUE SPACES.     01580009
092404     05  RD-XS-BYR-NBR              PIC  ZZZ9.                    01590009
092404     05  FILLER                     PIC  X(05)  VALUE SPACES.     01600009
092404     05  RD-XS-CONT-CLR-EXCLN-IND   PIC  X(01).                   01610009
                                                                        01620000
       01  REPORT-DETAIL-2.                                             01630000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01640000
           05  RD-VSM-DEPT-NBR            PIC  ZZZ9.                    01650000
           05  FILLER                     PIC  X(06)  VALUE SPACES.     01660000
           05  RD-VSM-BUS-GP-NBR          PIC  ZZ9.                     01670000
           05  FILLER                     PIC  X(09)  VALUE SPACES.     01680000
           05  RD-VSM-BUS-DEPT-NM         PIC  X(20).                   01690009
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01700000
           05  RD-VSM-MERCH-LNE-CDE       PIC  X(12).                   01710009
092404     05  FILLER                     PIC  X(02)  VALUE SPACES.     01720009
092404     05  RD-VSM-DMA-NBR             PIC  ZZZ9.                    01730009
092404     05  FILLER                     PIC  X(05)  VALUE SPACES.     01740009
092404     05  RD-VSM-STR-ZN-CDE          PIC  X(05).                   01750009
092404     05  FILLER                     PIC  X(07)  VALUE SPACES.     01760009
092404     05  RD-VSM-BYR-NBR             PIC  ZZZ9.                    01770009
092404     05  FILLER                     PIC  X(05)  VALUE SPACES.     01780009
092404     05  RD-VSM-CONT-CLR-EXCLN-IND  PIC  X(01).                   01790009
                                                                        01800000
       01  RF-END-OF-REPORT.                                            01810000
           05  FILLER                     PIC X(55)  VALUE SPACES.      01820009
           05  FILLER                     PIC X(21)                     01830000
                                 VALUE '***END OF REPORT***'.           01840000
           05  FILLER                     PIC X(56)  VALUE SPACES.      01850009
      ***************************************************************** 01860000
      **COPYBOOKS                                                       01870000
      ***************************************************************** 01880000
      ****ABEND ROUTINE INFORMATION                                     01890000
           COPY DPWS900.                                                01900000
      ****ABEND ROUTINE INFORMATION                                     01910000
           COPY DPWS004.                                                01920008
      ***STANDARD REPORT HEADER FOR A 132 COLUMN REPORT.                01930000
           COPY DPWS132O.                                               01940000
      ***INPUT RECORD                                                   01950000
           COPY XSRD006.                                                01960000
                                                                        01970000
       PROCEDURE DIVISION.                                              01980000
      ******************************************************************01990000
       0000-MAINLINE.                                                   02000000
                                                                        02010000
           PERFORM 1000-INITIALIZATION.                                 02020000
           PERFORM 2000-PROCESS-FILES                                   02030000
                   UNTIL PS-EOF-INPUT-FILE.                             02040000
           PERFORM 9999-WRAPUP.                                         02050000
           GOBACK.                                                      02060000
                                                                        02070000
       EJECT.                                                           02080000
      ******************************************************************02090000
      *INITIALIZATION ROUTINE                                           02100000
      ******************************************************************02110000
       1000-INITIALIZATION.                                             02120000
                                                                        02130000
           OPEN INPUT   INPUT-FILE                                      02140000
                OUTPUT  REPORT-FILE.                                    02150000
                                                                        02160000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             02170000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             02180000
                                                                        02190000
           PERFORM 1100-SETUP-HEADINGS.                                 02200000
                                                                        02210000
           PERFORM 9999-READ-INPUT-FILE.                                02220000
                                                                        02230000
       EJECT.                                                           02240000
      ******************************************************************02250000
      *SETS UP ALL HEADING STATIC VARIABLES FOR THE REPORT DURATION     02260000
      ******************************************************************02270000
       1100-SETUP-HEADINGS.                                             02280000
                                                                        02290000
           MOVE '1200-SETUP-HEADINGS' TO PV-PARAGRAPH-NAME.             02300000
                                                                        02310000
           MOVE DF-ACCEPT-YR          TO DP132O-RUN-YEAR.               02320000
           MOVE DF-ACCEPT-MO          TO DP132O-RUN-MONTH.              02330000
           MOVE DF-ACCEPT-DY          TO DP132O-RUN-DAY.                02340000
           MOVE DF-ACCEPT-HR          TO DP132O-RUN-HOUR.               02350000
           MOVE DF-ACCEPT-MIN         TO DP132O-RUN-MINUTE.             02360000
                                                                        02370000
           MOVE PC-PROGRAM-NAME  TO DP132O-PROGRAM-NAME.                02380000
           MOVE 1                TO DP132O-REPORT-NUMBER.               02390000
                                                                        02400000
           PERFORM 4000-PRINT-HEADINGS.                                 02410000
                                                                        02420000
       EJECT.                                                           02430000
      ******************************************************************02440000
      *PROCESS BOTH FILES                                               02450000
      ******************************************************************02460000
       2000-PROCESS-FILES.                                              02470000
                                                                        02480000
           MOVE '2000-PROCESS-FILES' TO PV-PARAGRAPH-NAME.              02490000
                                                                        02500000
           PERFORM 4100-PRINT-DETAIL.                                   02510000
                                                                        02520000
           IF  PC-LINE-COUNT > 60                                       02530000
               PERFORM 4000-PRINT-HEADINGS                              02540000
           END-IF.                                                      02550000
                                                                        02560000
           PERFORM 9999-READ-INPUT-FILE.                                02570000
                                                                        02580000
       EJECT.                                                           02590000
      ******************************************************************02600000
      *PRINTS REPORT HEADINGS AT TOP OF PAGE                            02610000
      ******************************************************************02620000
       4000-PRINT-HEADINGS.                                             02630000
                                                                        02640000
           MOVE '4000-PRINT-HEADINGS' TO PV-PARAGRAPH-NAME.             02650000
                                                                        02660000
           INITIALIZE PC-LINE-COUNT.                                    02670000
           ADD +1                     TO PC-PAGE-COUNT.                 02680000
           MOVE PC-PAGE-COUNT         TO DP132O-PAGE-NUMBER.            02690000
                                                                        02700000
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      02710000
                                                  AFTER ADVANCING PAGE. 02720000
           WRITE REPORT-RECORD FROM REPORT-HEADING-1                    02730000
                                               AFTER ADVANCING 2 LINES. 02740000
           WRITE REPORT-RECORD FROM REPORT-HEADING-2                    02750000
                                               AFTER ADVANCING 1 LINES. 02760000
           WRITE REPORT-RECORD FROM REPORT-HEADING-3                    02770000
                                               AFTER ADVANCING 3 LINES. 02780000
           WRITE REPORT-RECORD FROM REPORT-HEADING-4                    02790000
                                               AFTER ADVANCING 1 LINES. 02800000
                                                                        02810000
           ADD +7                     TO PC-LINE-COUNT.                 02820000
                                                                        02830000
       EJECT.                                                           02840000
      ******************************************************************02850000
      *PRINTS DETAIL REPORT                                             02860000
      ******************************************************************02870000
       4100-PRINT-DETAIL.                                               02880000
                                                                        02890000
           MOVE '4100-PRINT-DETAIL'      TO PV-PARAGRAPH-NAME.          02900009
                                                                        02910000
           MOVE XSRD006-XS-DEPT-NBR      TO RD-XS-DEPT-NBR.             02920009
           MOVE XSRD006-XS-BUS-GP-NBR    TO RD-XS-BUS-GP-NBR.           02930009
           MOVE XSRD006-XS-BUS-DEPT-NM   TO RD-XS-BUS-DEPT-NM.          02940009
           MOVE XSRD006-XS-MERCH-LNE-CDE TO RD-XS-MERCH-LNE-CDE.        02950000
092404     MOVE XSRD006-XS-DMA-NBR       TO RD-XS-DMA-NBR.              02960009
092404     MOVE XSRD006-XS-STR-ZN-CDE    TO RD-XS-STR-ZN-CDE.           02970009
092404     MOVE XSRD006-XS-BYR-NBR       TO RD-XS-BYR-NBR.              02980009
092404     MOVE XSRD006-XS-CONT-CLR-EXCLN-IND                           02990009
092404                                   TO RD-XS-CONT-CLR-EXCLN-IND    03000009
                                                                        03010000
           MOVE XSRD006-VSM-DEPT-NBR     TO RD-VSM-DEPT-NBR.            03020009
           MOVE XSRD006-VSM-BUS-GP-NBR   TO RD-VSM-BUS-GP-NBR.          03030009
           MOVE XSRD006-VSM-BUS-DEPT-NM  TO RD-VSM-BUS-DEPT-NM.         03040009
           MOVE XSRD006-VSM-MERCH-LEN-CDE                               03050009
                                         TO RD-VSM-MERCH-LNE-CDE.       03060009
092404     MOVE XSRD006-VSM-DMA-NBR      TO RD-VSM-DMA-NBR.             03070009
092404     MOVE XSRD006-VSM-STR-ZN-CDE   TO RD-VSM-STR-ZN-CDE.          03080009
092404     MOVE XSRD006-VSM-BYR-NBR      TO RD-VSM-BYR-NBR.             03090009
092404     MOVE XSRD006-VSM-CON-CLR-EXCLN-IND                           03100009
092404                                   TO RD-VSM-CONT-CLR-EXCLN-IND.  03110009
                                                                        03120000
           WRITE REPORT-RECORD FROM REPORT-DETAIL-1                     03130000
                                            AFTER ADVANCING 2 LINES.    03140000
           WRITE REPORT-RECORD FROM REPORT-DETAIL-2                     03150000
                                            AFTER ADVANCING 1 LINE.     03160000
                                                                        03170000
           ADD +3                        TO PC-LINE-COUNT.              03180009
                                                                        03190000
       EJECT.                                                           03200000
      ******************************************************************03210000
      *READS INPUT FILE                                                 03220000
      ******************************************************************03230000
       9999-READ-INPUT-FILE.                                            03240000
                                                                        03250000
           READ INPUT-FILE INTO XSRD006-DEPT-ERR-RECORD                 03260000
               AT END SET PS-EOF-INPUT-FILE TO TRUE                     03270000
           END-READ.                                                    03280000
                                                                        03290000
           ADD +1  TO PC-RECORDS-READ.                                  03300000
                                                                        03310000
       EJECT.                                                           03320000
      ******************************************************************03330000
      *PROGRAM END PROCESSING                                           03340000
      ******************************************************************03350000
       9999-WRAPUP.                                                     03360000
                                                                        03370000
           WRITE REPORT-RECORD FROM RF-END-OF-REPORT                    03380000
                                    AFTER ADVANCING 2 LINES.            03390000
                                                                        03400000
           CLOSE INPUT-FILE                                             03410000
                 REPORT-FILE.                                           03420000
                                                                        03430000
           DISPLAY '*************************************************'. 03440000
           DISPLAY '* PROGRAM..................: ' PC-PROGRAM-NAME.     03450000
           DISPLAY '* RUN DATE.................: ' DF-ACCEPT-DATE.      03460000
           DISPLAY '* RUN TIME.................: ' DF-ACCEPT-TIME.      03470000
           DISPLAY '*--------- COUNTS FOR THIS RUN ---------'.          03480000
           DISPLAY '* RECORDS READ........: ' PC-RECORDS-READ.          03490000
           DISPLAY '*************************************************'. 03500000
                                                                        03510000
       EJECT.                                                           03520000
                                                                        03530000
       EJECT.                                                           03540000
