      ****************************************************************  00010000
       IDENTIFICATION DIVISION.                                         00020000
      ****************************************************************  00030000
                                                                        00040000
       PROGRAM-ID.             SVKUT010.                                00050000
       AUTHOR.                 DAN PAYNTER.                             00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            APRIL 2022.                              00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ****************************************************************  00110000
      *                                                                 00120000
      *   ***  RESET STORED VALUE CARD FRAUD ATTEMPTS TABLE ***         00130000
      *                                                                 00140000
      *  RELEASE LOCKS ON STORED VALUE CARDS BY COPYING DATA FROM       00150000
      *  SVT_CARD_FAILED_ATTEMPTS DB2 TABLE TO THE HISTORY VERSION OF   00160000
      *  THE SAME TABLE (SVT_CARD_FAILED_ATTEMPTS_HIST).                00170000
      *  THIS PARTICULAR PROGRAM MERELY EXTRACTS THE RECORDS FROM THE   00180000
      *  SVT_CARD_FAILED_ATTEMPTS DB2 TABLE TO A FILE SO THEY CAN BE    00190000
      *  INSERTED INTO THE HISTORY TABLE.                               00200000
      *                                                                 00230000
      ****************************************************************  00270000
      *                                                                 00280000
      * CHANGE LOG: CHG0270185                                          00290006
      * DATE:  2022-05-17                                               00300006
      * PRGMR: DAN PAYNTER                                              00310000
      * WHY:   NEW PROGRAM TO RELEASE LOCKED STORED VALUE CARDS         00320000
      ******************************************************************00330000
       ENVIRONMENT DIVISION.                                            00750000
      ****************************************************************  00760000
                                                                        00770000
       CONFIGURATION SECTION.                                           00780000
                                                                        00790000
       SOURCE-COMPUTER.        IBM-3090.                                00800000
                                                                        00810000
       OBJECT-COMPUTER.        IBM-3090.                                00820000
                                                                        00830000
       INPUT-OUTPUT SECTION.                                            00840000
                                                                        00850000
       FILE-CONTROL.                                                    00860000
                                                                        00870000
           SELECT FAIL-UPDT-FILE ASSIGN TO OUT01.                       00890003
                                                                        00900000
      ***************************************************************** 00910000
       DATA DIVISION.                                                   00920000
      ***************************************************************** 00930000
                                                                        00940000
       FILE SECTION.                                                    00950000
                                                                        01030000
       FD  FAIL-UPDT-FILE                                               01040003
           RECORDING MODE IS F                                          01050000
           LABEL RECORDS ARE STANDARD                                   01060000
           BLOCK CONTAINS 0 RECORDS                                     01070000
           RECORD CONTAINS 100 CHARACTERS                               01080003
           DATA RECORD IS FAIL-UPDT-RECORD.                             01090003
                                                                        01100000
       01  FAIL-UPDT-RECORD                PIC X(100).                  01110003
                                                                        01120000
      ***************************************************************** 01130000
       WORKING-STORAGE SECTION.                                         01140000
      ***************************************************************** 01150000
       01  FILLER                            PIC X(50)  VALUE           01160000
           ' *** WORKING STORAGE STARTS HERE FOR SVKUT010 *** '.        01170000
                                                                        01180000
      ***************************************************************** 01270000
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)             01280000
      ***************************************************************** 01290000
           COPY DPWS500.                                                01300000
                                                                        01310000
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      01320000
                                                        COMP SYNC.      01330000
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    01340000
           88  AC-DB2-ERROR                             VALUE +4013.    01350000
           88  ABEND-4019-CAL-RTN-ERROR                 VALUE +4019.    01360000
                                                                        01370000
       01  PS-PROGRAM-SWITCHES.                                         01400000
           05  PS-END-OF-FLD-CSR-SW          PIC  X(01) VALUE 'N'.      01490003
               88  PS-END-OF-FLD-CSR-N                  VALUE 'N'.      01500003
               88  PS-END-OF-FLD-CSR-Y                  VALUE 'Y'.      01510003
                                                                        01550000
       01  PC-CONSTANTS.                                                01560000
           05  PC-CURRENT-PROGRAM-NAME       PIC  X(08) VALUE           01570000
                                                        'SVKUT010'.     01580000
           05  PC-ROW-NOT-FOUND              PIC S9(04) VALUE +100      01590000
                                                        COMP SYNC.      01600000
           05  PC-MULTIPLE-ROWS-FOUND        PIC S9(04) VALUE -811      01610000
                                                        COMP SYNC.      01620000
                                                                        01660000
       01  PV-MISC-COUNT-FIELDS            COMP-3.                      01760000
           05  PV-FAIL-CURSOR-CNT          PIC S9(07)    VALUE ZERO.    01770003
                                                                        01800000
       01  PV-ABEND-DISPLAYS.                                           01860000
           05  PV-ABEND-CRD-NBR            PIC  9(12)    VALUE ZERO.    01870003
           05  PV-ABEND-TRAN-TYPE-CODE     PIC  X(02)    VALUE ZERO.    01880003
                                                                        01910000
       01  PV-MISC-FIELDS.                                              01920000
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   01930000
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   01940000
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   01950000
           05  PV-DB2-OPERATION            PIC  X(30)    VALUE SPACE.   01960000
           05  PV-EDIT-MASK                PIC  Z,ZZZ,ZZ9.              01970000
                                                                        01980000
      ***************************************************************** 01990000
      *  WS FIELDS FOR SVT_CARD_FAILED_ATTEMPTS DELETES AND             02000005
      *  SVT_CARD_FAILED_ATTEMPTS_HIST INSERTS                          02001005
      ***************************************************************** 02010000
           COPY SVRD024.                                                02020002
                                                                        02030000
      ***************************************************************** 02040000
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            02050000
      ***************************************************************** 02060000
           COPY DPWS004.                                                02070000
                                                                        02080000
      ***************************************************************** 02210000
      *  DB2 COMMUNICATION AREA.                                        02220000
      ***************************************************************** 02230000
           EXEC SQL                                                     02240000
                INCLUDE SQLCA                                           02250000
           END-EXEC.                                                    02260000
                                                                        02270000
      ***************************************************************** 02271000
      *  STORED VALUE FRAUD PARAMETERS TABLE                            02280006
      ***************************************************************** 02281000
           EXEC SQL                                                     02290006
               INCLUDE SVT00018                                         02300006
           END-EXEC.                                                    02310006
                                                                        02320006
      ***************************************************************** 02321000
      *  STORED VALUE FAILED ATTEMPTS TABLE                             02330006
      ***************************************************************** 02331000
           EXEC SQL                                                     02340006
               INCLUDE SVT00019                                         02341006
           END-EXEC.                                                    02342006
                                                                        02343006
                                                                        02910000
      ***************************************************************** 02920000
      *  FLD-CURSOR -- CURSOR TO RETRIEVE ALL FAILED ATTEMPT STORED     02930000
      *  VALUE RECORDS                                                  02940000
      ***************************************************************** 02950000
           EXEC SQL                                                     02960000
             DECLARE FLD-CURSOR CURSOR FOR                              02970000
                                                                        02980000
            SELECT                                                      03000000
                   FLD.CRD_NBR                                          03010000
                  ,FLD.TRAN_TYPE_CODE                                   03011000
                  ,FLD.CARD_LOCK_INDICATOR                              03012000
                  ,FLD.FAIL_COUNT                                       03013000
              FROM SVT_CARD_FAILED_ATTEMPTS FLD                         03080000
            ORDER BY                                                    03090003
                   FLD.CRD_NBR                                          03100003
           END-EXEC.                                                    03940000
                                                                        04260000
      ***************************************************************** 04270000
      * PROCEDURE DIVISION.                                           * 04280000
      ***************************************************************** 04290000
       PROCEDURE DIVISION.                                              04300000
                                                                        04310000
      ***************************************************************** 04320000
      * 0000-MAINLINE-PARAGRAPH                                         04330000
      ***************************************************************** 04340000
       0000-MAINLINE-PARAGRAPH.                                         04350000
                                                                        04360000
           PERFORM 1000-INITIALIZE-PROGRAM.                             04370000
                                                                        04380000
           PERFORM 2000-OPEN-FLD-CURSOR.                                04390003
                                                                        04400000
           PERFORM 2100-PROCESS-FLD-ATT                                 04410003
               UNTIL PS-END-OF-FLD-CSR-Y.                               04420003
                                                                        04430000
           PERFORM 2200-CLOSE-FAIL-CURSOR.                              04440003
                                                                        04450000
           DISPLAY '****************************************'           04460000
           DISPLAY '*************** SVKUT010 ***************'           04470000
           DISPLAY '****************************************'           04480000
           MOVE PV-FAIL-CURSOR-CNT       TO PV-EDIT-MASK.               04530003
           INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'.         04540000
           DISPLAY 'FAILED ATT ROWS LOCATED........'                    04550003
               PV-EDIT-MASK.                                            04560000
           DISPLAY '****************************************'           04570000
                                                                        04580000
           CLOSE FAIL-UPDT-FILE.                                        04590003
           STOP RUN.                                                    04600000
                                                                        04610000
      ***************************************************************** 04620000
      * 1000-INITIALIZE-PROGRAM                                         04630000
      *     PROCESS CONTROL CARD                                        04640000
      ***************************************************************** 04650000
       1000-INITIALIZE-PROGRAM.                                         04660000
                                                                        04670000
           OPEN OUTPUT FAIL-UPDT-FILE.                                  04690003
                                                                        04700000
           SET PS-END-OF-FLD-CSR-N TO TRUE.                             04710003
                                                                        04730000
           MOVE ZERO                     TO PV-FAIL-CURSOR-CNT.         04740003
                                                                        04770000
      *1000-INITIALIZE-PROGRAM-EXIT.                                    04850000
                                                                        05430000
      ***************************************************************** 06050000
      * 2000-OPEN-FLD-CURSOR                                            06060003
      *     OPEN THE FLD-CURSOR.                                        06070003
      ***************************************************************** 06080000
       2000-OPEN-FLD-CURSOR.                                            06090003
                                                                        06100000
           EXEC SQL                                                     06110000
                OPEN FLD-CURSOR                                         06120003
           END-EXEC.                                                    06130000
                                                                        06140000
           EVALUATE TRUE                                                06150000
               WHEN SQLCODE = ZERO                                      06160000
                   CONTINUE                                             06170000
               WHEN SQLWARN0 NOT = SPACES                               06180000
               WHEN SQLCODE NOT = ZEROES                                06190000
                   MOVE '2000-OPEN-FLD-CURSOR'                          06200003
                                             TO PV-PARAGRAPH-NAME       06210000
                   MOVE 'ERROR ON OPEN OF FLD-CURSOR'                   06220003
                                             TO PV-DB2-OPERATION        06230000
                   PERFORM 9100-SQL-ABEND                               06240000
           END-EVALUATE.                                                06250000
                                                                        06260000
      *2000-OPEN-FLD-CURSOR-EXIT.                                       06270003
                                                                        06280000
      ***************************************************************** 06290000
      * 2100-PROCESS-FLD-ATT.                                           06300003
      *     PROCESS EACH ROW IN THE TRN CURSOR.                         06310000
      ***************************************************************** 06320000
       2100-PROCESS-FLD-ATT.                                            06330003
                                                                        06340000
           PERFORM 2150-FETCH-FAIL-CURSOR.                              06350003
                                                                        06360000
           IF PS-END-OF-FLD-CSR-N                                       06370003
               PERFORM 8500-WRITE-FAIL-UPDT-RECORD                      06380004
           END-IF.                                                      06490000
                                                                        06500000
      *2100-PROCESS-FLD-ATT-EXIT.                                       06510003
                                                                        06520000
      ***************************************************************** 06530000
      * 2150-FETCH-FAIL-CURSOR                                          06540003
      *     FETCH THE FAIL-CURSOR                                       06550003
      ***************************************************************** 06560000
       2150-FETCH-FAIL-CURSOR.                                          06570003
                                                                        06580000
           EXEC SQL                                                     06590000
                FETCH FLD-CURSOR                                        06600003
                 INTO :SVT00019-CRD-NBR                                 06610003
                     ,:SVT00019-TRAN-TYPE-CODE                          06620003
                     ,:SVT00019-CARD-LOCK-INDICATOR                     06630003
                     ,:SVT00019-FAIL-COUNT                              06640003
           END-EXEC.                                                    06680000
                                                                        06690000
           EVALUATE TRUE                                                06700000
               WHEN SQLCODE = ZERO                                      06710000
                   ADD 1                     TO PV-FAIL-CURSOR-CNT      06720003
               WHEN SQLCODE = PC-ROW-NOT-FOUND                          06730000
                   SET PS-END-OF-FLD-CSR-Y                              06740003
                                             TO TRUE                    06750000
               WHEN SQLWARN0 NOT = SPACES                               06760000
               WHEN SQLCODE NOT = ZEROES                                06770000
                   MOVE '2150-FETCH-FAIL-CURSOR'                        06780003
                                             TO PV-PARAGRAPH-NAME       06790000
                   MOVE 'ERROR ON FETCH OF FAIL-CURSOR'                 06800003
                                             TO PV-DB2-OPERATION        06810000
                   PERFORM 9100-SQL-ABEND                               06830000
           END-EVALUATE.                                                06840000
                                                                        06850000
      *2150-FETCH-FAIL-CURSOR-EXIT.                                     06860003
                                                                        06882300
      ***************************************************************** 06883000
      * 2200-CLOSE-FAIL-CURSOR                                          06890003
      *     CLOSE THE FAIL-CURSOR                                       06900003
      ***************************************************************** 06910000
       2200-CLOSE-FAIL-CURSOR.                                          06920003
                                                                        06930000
           EXEC SQL                                                     06940000
                CLOSE FLD-CURSOR                                        06950003
           END-EXEC.                                                    06960000
                                                                        06970000
           EVALUATE TRUE                                                06980000
               WHEN SQLCODE = ZERO                                      06990000
                    CONTINUE                                            07000000
               WHEN SQLWARN0 NOT = SPACES                               07010000
               WHEN SQLCODE NOT = ZEROES                                07020000
                   MOVE '2200-CLOSE-FAIL-CURSOR'                        07030003
                                             TO PV-PARAGRAPH-NAME       07040000
                   MOVE 'ERROR ON CLOSE OF FLD-CURSOR'                  07050003
                                             TO PV-DB2-OPERATION        07060000
                   PERFORM 9100-SQL-ABEND                               07070000
           END-EVALUATE.                                                07080000
                                                                        07090000
      ***************************************************************** 09770000
      * 8500-WRITE-FAIL-UPDT-RECORD                                     09780003
      *     WRITE THE OUTPUT RECORD WITH INSERT/DELETE DETAILS.         09790003
      ***************************************************************** 09800000
       8500-WRITE-FAIL-UPDT-RECORD.                                     09810003
                                                                        09820000
           INITIALIZE SV024-CARD-UPDT-RECORD.                           09830003
           SET SV024-DELETE-RECORD TO TRUE.                             09840003
           IF SVT00019-CARD-LOCK-INDICATOR = 'Y'                        09841003
               SET SV024-DEL-AND-INSRT-REC TO TRUE                      09842003
           END-IF.                                                      09843002
           MOVE SVT00019-CRD-NBR        TO SV024-CRD-NBR.               09850003
           MOVE SVT00019-TRAN-TYPE-CODE TO SV024-TRAN-TYPE-CODE.        09860003
           MOVE SVT00019-CARD-LOCK-INDICATOR                            09870002
                                        TO SV024-CARD-LOCK-INDICATOR.   09871003
           MOVE SVT00019-FAIL-COUNT     TO SV024-FAIL-COUNT.            09880003
                                                                        10020000
           WRITE FAIL-UPDT-RECORD                                       10030003
               FROM SV024-CARD-UPDT-RECORD.                             10040003
                                                                        10050000
      *8500-WRITE-FAIL-UPDT-RECORD-EXIT.                                10060003
                                                                        10070000
      ***************************************************************** 10080000
      * 9100-SQL-ABEND                                                  10090000
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.10100000
      ***************************************************************** 10110000
       9100-SQL-ABEND.                                                  10120000
           DISPLAY '** ABEND     **'.                                   10130000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        10140000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              10150000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               10160000
           DISPLAY '** CARD NBR  ** = ' PV-ABEND-CRD-NBR.               10170003
           DISPLAY '** TRN TYP CDE ** = ' PV-ABEND-TRAN-TYPE-CODE.      10180003
                                                                        10210000
      ***************************************************************** 10220000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     10230000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      10240000
      ***************************************************************** 10250000
           COPY DPPD004.                                                10260000
                                                                        10270000
           SET AC-DB2-ERROR TO TRUE.                                    10280000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         10290000
                                                                        10300000
      ***************************************************************** 10310000
      * 9999-ABEND                                                      10320000
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  10330000
      ***************************************************************** 10340000
       9999-ABEND.                                                      10350000
           DISPLAY '** ABEND           **'.                             10360000
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  10370000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        10380000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       10390000
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       10400000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         10410000
