000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.             VLKUT037.                                        
000400 AUTHOR.                 DAN HINTZ.                                       
000500 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000600 DATE-WRITTEN.           FEBRUARY 2005.                                   
000700 DATE-COMPILED.                                                           
000800                                                                          
000900*                                                                         
001000*  PROGRAM NARRATIVE:                                                     
001100*  THIS PROGRAM WILL CREATE A FILE CONTAINING VENDOR CONTACT              
001200*  INFORMATION FOR COMPLIANCE NETWORK                                     
001400*                                                                         
001400*                                                                         
002800 ENVIRONMENT DIVISION.                                                    
002900                                                                          
003000 CONFIGURATION SECTION.                                                   
003100                                                                          
003200 SOURCE-COMPUTER.                        IBM-3090.                        
003300 OBJECT-COMPUTER.                        IBM-3090.                        
003400                                                                          
003500 INPUT-OUTPUT SECTION.                                                    
003600                                                                          
003700 FILE-CONTROL.                                                            
003800                                                                          
003900     SELECT  VL-VND-CONTACT-FILE                                          
004000         ASSIGN TO OUT01.                                                 
004100                                                                          
003900     SELECT  VL-MARKER-FILE                                               
004000         ASSIGN TO OUT02.                                                 
004100                                                                          
004500******************************************************************        
004600 DATA DIVISION.                                                           
004700******************************************************************        
004800                                                                          
004900 FILE SECTION.                                                            
005000*                                                                         
005100 FD  VL-VND-CONTACT-FILE                                                  
005200     RECORDING MODE IS F                                                  
005300     LABEL RECORDS ARE STANDARD                                           
005400     BLOCK CONTAINS 0 RECORDS.                                            
005500 01  VL-VND-CONTACT-RECORD            PIC  X(350).                        
005600*                                                                         
005100 FD  VL-MARKER-FILE                                                       
005200     RECORDING MODE IS F                                                  
005300     LABEL RECORDS ARE STANDARD                                           
005400     BLOCK CONTAINS 0 RECORDS.                                            
005500 01  VL-MARKER-RECORD                 PIC  X(78).                         
005600*                                                                         
006300*                                                                         
006500 WORKING-STORAGE SECTION.                                                 
000250***************************************************************** 00025015
000260*    INPUT CONTROL CARD                                           00026015
000270***************************************************************** 00027015
000054 01  CC-CONTROL-CARD.                                                     
000055     05  CC-SCHEDULE-DATE.                                                
000056         10  CC-CCYY                 PIC X(04).                           
000057         10  CC-MM                   PIC X(02).                           
000058         10  CC-DD                   PIC X(02).                           
006300*                                                                         
013800*****************************************************************         
013800*    PROGRAM CONSTANTS                                                    
013800*****************************************************************         
013600 01  PC-CONSTANTS.                                                        
013700     05  PC-CURRENT-PGM-VLKUT037      PIC  X(08) VALUE 'VLKUT037'.        
013700     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
013700                                                 COMP SYNC.               
013800                                                                          
013800*****************************************************************         
013800*    PROGRAM SWITCHES                                                     
013800*****************************************************************         
013900 01  PS-SWITCHES.                                                         
014000     05  PS-END-CURSOR-SWITCH         PIC  X(01) VALUE 'N'.               
014100         88  PS-END-CSR                          VALUE 'Y'.               
014100         88  PS-NOT-END-CSR                      VALUE 'N'.               
014200                                                                          
013800*****************************************************************         
013800*    PROGRAM VARIABLES                                                    
013800*****************************************************************         
008000 01  PV-VARIABLES.                                                        
013700     05  PV-RETRY-COUNT               PIC S9(04) VALUE +0                 
006800                                                 COMP SYNC.               
006700     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
006800                                                 COMP SYNC.               
011000     05  PV-PARAGRAPH                 PIC  X(30) VALUE SPACES.            
010800     05  PV-DB2-FUNCTION              PIC  X(30) VALUE SPACES.            
010900     05  PV-ABEND-TABLE-1             PIC  X(20) VALUE SPACES.            
010900     05  PV-ABEND-TABLE-2             PIC  X(20) VALUE SPACES.            
010900     05  PV-ABEND-TABLE-3             PIC  X(20) VALUE SPACES.            
010900     05  PV-ABEND-TABLE-4             PIC  X(20) VALUE SPACES.            
010900     05  PV-ERROR-MSG                 PIC  X(60) VALUE SPACES.            
009700     05  PV-RECS-WRITTEN              PIC  9(09) VALUE ZERO.              
009700     05  PV-ENT-ID-X.                                                     
009700         10  PV-ENT-ID                PIC  9(09) VALUE ZERO.              
009000     05  PV-RUN-DATE-CN               PIC  X(08) VALUE SPACES.            
010100     05  PV-RUN-TIME-CN               PIC  X(06) VALUE SPACES.            
010500***** FILE NAME CONSISTS OF RUN-DATE-CN_CONTACTS.DAT                      
010600*****     EXAMPLE: 20050317_CONTACTS.DAT                                  
010700     05  PV-FILE-NAME.                                                    
010800         10  PV-CREATE-DATE           PIC  X(08) VALUE SPACES.            
010900         10  PV-UNDERSCORE            PIC  X(01) VALUE '_'.               
011000         10  PV-CONTACTS-DAT          PIC  X(12) VALUE SPACES.            
011100                                                                          
011200***  MARKER FILE LAYOUT                                                   
011300     COPY VLRD029.                                                        
011400                                                                          
014600                                                                          
015400***  VENDOR CONTACT FILE LAYOUT                                           
014700     COPY VLRD037.                                                        
014800                                                                          
000418***  DATE ROUTINE COPYBOOK                                        00041815
000419     COPY DPWS500.                                                00041915
000420                                                                  00042015
015400***  DB2 ABEND COPYBOOK                                                   
014700     COPY DPWS004.                                                        
014800                                                                          
014900     EXEC SQL                                                             
015000         INCLUDE SQLCA                                                    
015100     END-EXEC.                                                            
015200                                                                          
015300     COPY SQLCA2.                                                         
015400                                                                          
034300*---------------------------------------------------------------*         
034400*    DCLGEN FOR THE EXTERNAL ENTERPRISE TABLE (TEXTENT)         *         
034600*---------------------------------------------------------------*         
016300     EXEC SQL                                                             
016400         INCLUDE TEXTENT                                                  
016500     END-EXEC.                                                            
016600                                                                          
034300*---------------------------------------------------------------*         
034400*    DCLGEN FOR THE ENTERPRISE NAME TABLE (TENTNME)             *         
034600*---------------------------------------------------------------*         
016300     EXEC SQL                                                             
016400         INCLUDE TENTNME                                                  
016500     END-EXEC.                                                            
016600                                                                          
034300*---------------------------------------------------------------*         
034400*    DCLGEN FOR THE ENTERPRISE REPRESENTATIVE TABLE (TENTREP)   *         
034600*---------------------------------------------------------------*         
016300     EXEC SQL                                                             
016400         INCLUDE TENTREP                                                  
016500     END-EXEC.                                                            
016600                                                                          
034300*---------------------------------------------------------------*         
034400*    DCLGEN FOR THE BUSINESS LOCATION TABLE (TBUSLOC)           *         
034600*---------------------------------------------------------------*         
016300     EXEC SQL                                                             
016400         INCLUDE TBUSLOC                                                  
016500     END-EXEC.                                                            
016600                                                                          
034300*---------------------------------------------------------------*         
034400*    DCLGEN FOR THE BUSINESS FUNCTION TABLE (TBUSFNC)           *         
034600*---------------------------------------------------------------*         
016300     EXEC SQL                                                             
016400         INCLUDE TBUSFNC                                                  
016500     END-EXEC.                                                            
016600                                                                          
016600***  EXTRACT CURSOR                                                       
016600     EXEC SQL                                                             
016600         DECLARE EXTRACT-CSR CURSOR FOR                                   
               SELECT                                                           
                      A.ENT_ID                                                  
                    , B.ENT_NAME_DESC                                           
                    , C.FIRST_NAME                                              
                    , C.LAST_NAME                                               
                    , C.TITLE_NAME                                              
                    , C.TEL1_NBR                                                
                    , C.FAX_NBR                                                 
                    , C.EMAIL_ADDR_ID                                           
                    , D.ADDRESS_LINE1_DESC                                      
                    , D.ADDRESS_LINE2_DESC                                      
                    , D.CITY_NAME                                               
                    , D.STATE_CDE                                               
                    , D.ZIP_CDE                                                 
                    , E.FUNCTION_NAME                                           
                 FROM TEXTENT A                                                 
                    , TENTNME B                                                 
                    , TENTREP C                                                 
                    , TBUSLOC D                                                 
                    , TBUSFNC E                                                 
                WHERE                                                           
                      A.STATUS_CDE = 'A'                                        
                  AND A.ENT_ID = B.ENT_ID                                       
                  AND B.TYPE_CDE = '01'                                         
                  AND A.ENT_ID = C.ENT_ID                                       
                  AND C.FUNCTION_ID = 15                                        
                  AND C.ENT_ID = D.ENT_ID                                       
                  AND C.FUNCTION_ID = D.FUNCTION_ID                             
                  AND (C.FIRST_NAME > ' ' OR C.LAST_NAME > ' ')                 
                  AND C.ENT_BUS_LOC_ID = D.SEQ_NBR                              
                  AND C.FUNCTION_ID = E.FUNCTION_ID                             
             GROUP BY                                                           
                      A.ENT_ID                                                  
                    , B.ENT_NAME_DESC                                           
                    , C.FIRST_NAME                                              
                    , C.LAST_NAME                                               
                    , C.TITLE_NAME                                              
                    , C.TEL1_NBR                                                
                    , C.FAX_NBR                                                 
                    , C.EMAIL_ADDR_ID                                           
                    , D.ADDRESS_LINE1_DESC                                      
                    , D.ADDRESS_LINE2_DESC                                      
                    , D.CITY_NAME                                               
                    , D.STATE_CDE                                               
                    , D.ZIP_CDE                                                 
                    , E.FUNCTION_NAME                                           
             ORDER BY                                                           
                      A.ENT_ID                                                  
              WITH UR                                                           
016600     END-EXEC.                                                            
016600                                                                          
016700 LINKAGE SECTION.                                                         
016800                                                                          
016900 PROCEDURE DIVISION.                                                      
017000*****************************************************************         
017200*  MAINLINE LOGIC FOR PROGRAM                                             
017000*****************************************************************         
017100 0000-MAINLINE.                                                           
017500     PERFORM 1000-INITIALIZE.                                             
017600                                                                          
017700     PERFORM 2000-PROCESS                                                 
017800         UNTIL PS-END-CSR.                                                
017900                                                                          
018100     PERFORM 9000-EOJ.                                                    
018200                                                                          
018200     STOP RUN.                                                            
018200                                                                          
018200                                                                          
017000*****************************************************************         
017200*  INITIALIZATION PARAGRAPH                                               
017000*****************************************************************         
018600 1000-INITIALIZE.                                                         
020200     OPEN OUTPUT VL-VND-CONTACT-FILE                                      
017400                 VL-MARKER-FILE.                                          
017500                                                                          
000126     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
000127                                                                          
000128     DISPLAY 'CONTROL CARD ====>  ' CC-CONTROL-CARD.                      
000129                                                                          
000130     PERFORM 1100-VALIDATE-CONTROL-CARD.                                  
000131                                                                          
000132******* TIME IS CONTAINED CURRENT-DATE IN POSITION 9 FOR 8                
000133     MOVE FUNCTION CURRENT-DATE(9:8) TO PV-RUN-TIME-CN.                   
000135                                                                          
000136     DISPLAY '******  RUN TIME  => '  PV-RUN-TIME-CN.                     
000137                                                                          
020300     PERFORM 7000-OPEN-EXTRACT-CURSOR.                                    
020400     PERFORM 7200-FETCH-EXTRACT-CURSOR.                                   
022400                                                                          
022400                                                                          
000142******************************************************************        
000143**  VALIDATE THE DATE FROM THE SCHEDULE                                   
000144******************************************************************        
000145 1100-VALIDATE-CONTROL-CARD.                                              
000146     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
000147     SET DPG51-FISCAL-454-CALENDAR                                        
000148         DPG51-DO-NOT-INCR-DECR-DATE                                      
000149         DPG52-LK-DTE-ISO           TO TRUE.                              
000150                                                                          
000151     MOVE CC-SCHEDULE-DATE     TO DPG52-LK-DATE-INPUT.                    
000152                                                                          
000153     CALL DP500-KOHLS-CALENDAR-ROUTINE USING                              
000154             DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
000155                                                                          
000156     IF DPG54-NO-ERROR                                                    
000157         MOVE CC-SCHEDULE-DATE     TO PV-RUN-DATE-CN                      
000158     ELSE                                                                 
000159         DISPLAY '** ABEND **'                                            
000160         DISPLAY '** PROGRAM = VLKUT037 -- CALLED MODULE '                
000161                  'DPKUT500 **'                                           
000162         DISPLAY '** PARAGRAPH = 1100-VALIDATE-CONTROL-CARD **'           
000163         DISPLAY '** INVALID SCHEDULE DATE: ' CC-SCHEDULE-DATE            
000164         DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '                
000165         DISPLAY DPG54-ERROR-MESSAGE                                      
000166         MOVE +4003 TO PV-ABEND-CODE                                      
000167         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
000168     END-IF.                                                              
000169                                                                          
000169                                                                          
017000*****************************************************************         
017200* PROCESS DATA FROM CURSOR AND WRITE OUTPUT RECORD                        
017000*****************************************************************         
024200 2000-PROCESS.                                                            
002800     MOVE 'A'                     TO VL037-ACD-INDICATOR.                 
002800     MOVE EXTENT-ENT-ID           TO PV-ENT-ID.                           
000011     MOVE PV-ENT-ID-X             TO VL037-VENDOR-NUMBER.                 
000011     MOVE ENTREP-FIRST-NAME       TO VL037-FIRST-NAME.                    
000011     MOVE ENTREP-LAST-NAME        TO VL037-LAST-NAME.                     
002800     MOVE BUSLOC-ADDRESS-LINE1-DESC                                       
002800                                  TO VL037-ADDRESS-LINE-1.                
002800     MOVE BUSLOC-ADDRESS-LINE2-DESC                                       
                                        TO VL037-ADDRESS-LINE-2.                
000011     MOVE BUSLOC-CITY-NAME        TO VL037-CITY.                          
000011     MOVE BUSLOC-STATE-CDE        TO VL037-STATE.                         
000011     MOVE BUSLOC-ZIP-CDE          TO VL037-ZIP-CODE.                      
000011     MOVE ENTREP-TEL1-NBR         TO VL037-PHONE-NUMBER.                  
000011     MOVE ENTREP-FAX-NBR          TO VL037-FAX-NUMBER.                    
000011     MOVE ENTREP-EMAIL-ADDR-ID    TO VL037-EMAIL-ADDRESS.                 
000011     MOVE SPACES                  TO VL037-WEB-ADDRESS                    
000011                                     VL037-DEPARTMENT-NUMBER.             
000011     MOVE 'V'                     TO VL037-CONTACT-TYPE.                  
000011                                                                          
000011     IF  ENTREP-TITLE-NAME = 'RCMS CONTACT'                               
000011         MOVE 'RCMS CONTACT'      TO VL037-CONTACT-CATEGORY               
000011     ELSE                                                                 
000011         MOVE BUSFNC-FUNCTION-NAME                                        
                                        TO VL037-CONTACT-CATEGORY               
000011     END-IF.                                                              
000011                                                                          
002800     MOVE 'X'                     TO VL037-ANCHOR-CONSTANT.               
002800                                                                          
002800     PERFORM 8000-WRITE-OUTPUT.                                           
026000                                                                          
026200     PERFORM 7200-FETCH-EXTRACT-CURSOR.                                   
021700                                                                          
042800                                                                          
026400******************************************************************        
026600* OPEN CURSOR                                                             
026700******************************************************************        
060600 7000-OPEN-EXTRACT-CURSOR.                                                
000910                                                                  00090315
000904     PERFORM                                                      00097115
000905         WITH TEST AFTER                                          00097215
000906         VARYING PV-RETRY-COUNT                                   00097315
000907         FROM 1 BY 1                                              00097415
000908         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                 00097515
000909                   OR (SQLCODE = ZERO))                           00097615
000910                                                                  00090315
060700         EXEC SQL                                                         
060800              OPEN EXTRACT-CSR                                            
060900         END-EXEC                                                         
061000                                                                          
061100         EVALUATE TRUE                                                    
061300             WHEN SQLCODE = +0                                            
001036             WHEN SQLCODE = -904                                  00098315
001037             WHEN SQLCODE = -911                                  00098415
001038             WHEN SQLCODE = -913                                  00098515
061300                 CONTINUE                                                 
061200             WHEN SQLWARN0 NOT EQUAL SPACE                                
061300             WHEN SQLCODE NOT EQUAL ZERO                                  
061400                 DISPLAY '** ABEND **'                                    
061500                 DISPLAY '** PROGRAM = VLKUT037 **'                       
061600                 DISPLAY '** PARAGRAPH = '                                
                               '7000-OPEN-EXTRACT-CURSOR **'                    
118726                 MOVE 'OPEN CURSOR'   TO PV-ERROR-MSG                     
118726                 MOVE 'TEXTENT'       TO PV-ABEND-TABLE-1                 
118726                 MOVE 'TENTNME'       TO PV-ABEND-TABLE-2                 
118726                 MOVE 'TENTREP'       TO PV-ABEND-TABLE-3                 
118726                 MOVE 'TBUSLOC'       TO PV-ABEND-TABLE-4                 
061700                 PERFORM 9200-SQL-ERROR                                   
061800         END-EVALUATE                                                     
000997     END-PERFORM.                                                 00099715
000998                                                                  00099815
000999     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          00099915
001000         DISPLAY '** ABEND **'                                    00100015
001010         DISPLAY '** PROGRAM = VLKUT037 **'                       00101015
001011         DISPLAY '** PARAGRAPH =  7000-OPEN-EXTRACT-CURSOR **'    00101115
001012         MOVE 'OPEN CURSOR MAX RETRIES'                           00101215
001013                                      TO PV-ERROR-MSG             00101315
118726         MOVE 'TEXTENT'               TO PV-ABEND-TABLE-1                 
118726         MOVE 'TENTNME'               TO PV-ABEND-TABLE-2                 
118726         MOVE 'TENTREP'               TO PV-ABEND-TABLE-3                 
118726         MOVE 'TBUSLOC'               TO PV-ABEND-TABLE-4                 
001015         PERFORM 9200-SQL-ERROR                                   00101515
001016     END-IF.                                                      00101615
001017                                                                  00101715
001017                                                                  00101715
062000******************************************************************        
062100* CLOSE CURSOR                                                            
062200******************************************************************        
062300 7100-CLOSE-EXTRACT-CURSOR.                                               
062700                                                                          
001024     PERFORM                                                      00102415
001025         WITH TEST AFTER                                          00102515
001026         VARYING PV-RETRY-COUNT                                   00102615
001027         FROM 1 BY 1                                              00102715
001028         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                 00102815
001029                   OR (SQLCODE = ZERO))                           00102915
062400         EXEC SQL                                                         
062500             CLOSE EXTRACT-CSR                                            
062600         END-EXEC                                                         
062700                                                                          
062800         EVALUATE TRUE                                                    
061300             WHEN SQLCODE = +0                                            
001036             WHEN SQLCODE = -904                                  00098315
001037             WHEN SQLCODE = -911                                  00098415
001038             WHEN SQLCODE = -913                                  00098515
061300                 CONTINUE                                                 
062900             WHEN SQLWARN0 NOT EQUAL SPACE                                
063000             WHEN SQLCODE NOT EQUAL ZERO                                  
063100                 DISPLAY '** ABEND **'                                    
063200                 DISPLAY '** PROGRAM = VLKUT037 **'                       
063300                 DISPLAY '** PARAGRAPH = '                                
                               '7100-CLOSE-EXTRACT-CURSOR**'                    
118726                 MOVE 'CLOSE CURSOR'  TO PV-ERROR-MSG                     
118726                 MOVE 'TEXTENT'       TO PV-ABEND-TABLE-1                 
118726                 MOVE 'TENTNME'       TO PV-ABEND-TABLE-2                 
118726                 MOVE 'TENTREP'       TO PV-ABEND-TABLE-3                 
118726                 MOVE 'TBUSLOC'       TO PV-ABEND-TABLE-4                 
063400                 PERFORM 9200-SQL-ERROR                                   
063500         END-EVALUATE                                                     
001050     END-PERFORM.                                                 00099715
001051                                                                  00099815
001052     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          00104615
063100         DISPLAY '** ABEND **'                                            
063200         DISPLAY '** PROGRAM = VLKUT037 **'                               
063300         DISPLAY '** PARAGRAPH = 7100-CLOSE-EXTRACT-CURSOR**'             
118726         MOVE 'CLOSE CURSOR'          TO PV-ERROR-MSG                     
118726         MOVE 'TEXTENT'               TO PV-ABEND-TABLE-1                 
118726         MOVE 'TENTNME'               TO PV-ABEND-TABLE-2                 
118726         MOVE 'TENTREP'               TO PV-ABEND-TABLE-3                 
118726         MOVE 'TBUSLOC'               TO PV-ABEND-TABLE-4                 
063400         PERFORM 9200-SQL-ERROR                                           
001072     END-IF.                                                      00107215
001073                                                                  00107315
063600                                                                          
063700******************************************************************        
063800*  FETCH CURSOR                                                           
063900******************************************************************        
064000 7200-FETCH-EXTRACT-CURSOR.                                               
001078     PERFORM                                                      00102415
001079         WITH TEST AFTER                                          00102515
001080         VARYING PV-RETRY-COUNT                                   00102615
001081         FROM 1 BY 1                                              00102715
001082         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                 00102815
001083                   OR (SQLCODE = ZERO) OR (SQLCODE = +100))       00102915
063600                                                                          
064100         EXEC SQL                                                         
064200              FETCH EXTRACT-CSR                                           
064400              INTO                                                        
                          :EXTENT-ENT-ID                                        
                        , :ENTNME-ENT-NAME-DESC                                 
                        , :ENTREP-FIRST-NAME                                    
                        , :ENTREP-LAST-NAME                                     
                        , :ENTREP-TITLE-NAME                                    
                        , :ENTREP-TEL1-NBR                                      
                        , :ENTREP-FAX-NBR                                       
                        , :ENTREP-EMAIL-ADDR-ID                                 
                        , :BUSLOC-ADDRESS-LINE1-DESC                            
                        , :BUSLOC-ADDRESS-LINE2-DESC                            
                        , :BUSLOC-CITY-NAME                                     
                        , :BUSLOC-STATE-CDE                                     
                        , :BUSLOC-ZIP-CDE                                       
                        , :BUSFNC-FUNCTION-NAME                                 
065100         END-EXEC                                                         
065200                                                                          
065300         EVALUATE TRUE                                                    
065400             WHEN SQLCODE = ZERO                                          
001036             WHEN SQLCODE = -904                                  00098315
001037             WHEN SQLCODE = -911                                  00098415
001038             WHEN SQLCODE = -913                                  00098515
065500                 CONTINUE                                                 
065600             WHEN SQLCODE = +100                                          
065700                 SET PS-END-CSR       TO TRUE                             
065800             WHEN SQLWARN0 NOT EQUAL SPACE                                
065900             WHEN SQLCODE NOT EQUAL ZERO                                  
066000                 DISPLAY '** ABEND **'                                    
066100                 DISPLAY '** PROGRAM = VLKUT037 **'                       
066200                 DISPLAY '** PARAGRAPH = '                                
                               '7200-FETCH-EXTRACT-CURSOR**'                    
118726                 MOVE 'FETCH CURSOR'  TO PV-ERROR-MSG                     
118726                 MOVE 'TEXTENT'       TO PV-ABEND-TABLE-1                 
118726                 MOVE 'TENTNME'       TO PV-ABEND-TABLE-2                 
118726                 MOVE 'TENTREP'       TO PV-ABEND-TABLE-3                 
118726                 MOVE 'TBUSLOC'       TO PV-ABEND-TABLE-4                 
066300                 PERFORM 9200-SQL-ERROR                                   
066400         END-EVALUATE                                                     
001161     END-PERFORM.                                                 00099715
001163                                                                  00117015
001164     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          00104615
066000         DISPLAY '** ABEND **'                                            
066100         DISPLAY '** PROGRAM = VLKUT037 **'                               
066200         DISPLAY '** PARAGRAPH = 7200-FETCH-EXTRACT-CURSOR**'             
118726         MOVE 'FETCH CURSOR'          TO PV-ERROR-MSG                     
118726         MOVE 'TEXTENT'               TO PV-ABEND-TABLE-1                 
118726         MOVE 'TENTNME'               TO PV-ABEND-TABLE-2                 
118726         MOVE 'TENTREP'               TO PV-ABEND-TABLE-3                 
118726         MOVE 'TBUSLOC'               TO PV-ABEND-TABLE-4                 
066300         PERFORM 9200-SQL-ERROR                                           
001176     END-IF.                                                      00107215
001177                                                                  00117015
066500                                                                          
051200*****************************************************************         
051200*  WRITE OUTPUT FILE                                                      
051200*****************************************************************         
050800 8000-WRITE-OUTPUT.                                                       
050900     WRITE VL-VND-CONTACT-RECORD FROM VL037-VENDOR-CONTACT-RECORD.        
051000                                                                          
051100     ADD 1                TO PV-RECS-WRITTEN.                             
002800                                                                          
035800*****************************************************************         
035900*  CREATE MARKER FILE                                                     
036000*****************************************************************         
036100 8500-CREATE-MARKER.                                                      
036200     MOVE PV-RUN-DATE-CN  TO PV-CREATE-DATE.                              
036300     MOVE 'CONTACTS.DAT'  TO PV-CONTACTS-DAT.                             
036400     MOVE PV-FILE-NAME    TO VL029-FILE-NAME.                             
036500     MOVE PV-RECS-WRITTEN TO VL029-RECORD-COUNT.                          
036600     MOVE PV-RUN-DATE-CN  TO VL029-BUILD-DATE.                            
036700     MOVE PV-RUN-TIME-CN  TO VL029-BUILD-TIME.                            
036800     MOVE 'X'             TO VL029-ANCHOR-CONSTANT.                       
036900                                                                          
037000     WRITE VL-MARKER-RECORD FROM VL029-MARKER-RECORD.                     
037100                                                                          
002800                                                                          
051200*****************************************************************         
051200*  END OF JOB PROCESSING                                                  
051200*****************************************************************         
051400 9000-EOJ.                                                                
020300     PERFORM 7100-CLOSE-EXTRACT-CURSOR.                                   
051800                                                                          
051800     DISPLAY 'RECORDS WRITTEN ===>  ' PV-RECS-WRITTEN.                    
051800                                                                          
051800     PERFORM 8500-CREATE-MARKER.                                          
051800                                                                          
051600     CLOSE VL-VND-CONTACT-FILE                                            
051600           VL-MARKER-FILE.                                                
002800                                                                          
052500                                                                          
051200*****************************************************************         
051200*  PARAGRAPH EXECUTED WHEN SERIOUS DB2 ERROR OCCURRED                     
051200*****************************************************************         
052600 9200-SQL-ERROR.                                                          
052700     CLOSE VL-VND-CONTACT-FILE                                            
052700           VL-MARKER-FILE.                                                
052900                                                                          
053000     MOVE SQLCODE                     TO SQLCODE2.                        
053100     MOVE SQLERRD(3)                  TO SQLERRD3.                        
053200                                                                          
053300     DISPLAY '** ABEND **         ** = SQLERROR'.                         
053400     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PGM-VLKUT037.                
053500     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH.                           
053600     DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                        
118726     DISPLAY '**           ** = ' PV-ABEND-TABLE-1.                       
118726     DISPLAY '**           ** = ' PV-ABEND-TABLE-2.                       
118726     DISPLAY '**           ** = ' PV-ABEND-TABLE-3.                       
118726     DISPLAY '**           ** = ' PV-ABEND-TABLE-4.                       
053700     DISPLAY '**           ** = ' PV-ERROR-MSG                            
053800                                                                          
053900     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
054000     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
054100     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
054200     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
054300                                                                          
054400     COPY DPPD004.                                                        
054500                                                                          
054600     MOVE +4013                       TO PV-ABEND-CODE.                   
054700     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
054800                                                                          
054800*************  END OF VLKUT037  ********************************          
