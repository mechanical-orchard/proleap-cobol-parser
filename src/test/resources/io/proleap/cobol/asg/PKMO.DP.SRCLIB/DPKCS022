      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.                DPKCS022.                                     
       AUTHOR.                    DWYNE WALLACE.                                
       INSTALLATION.              KOHLS DEPARTMENT STORES.                      
       DATE-WRITTEN.              SEPTEMBER 1990.                               
       DATE-COMPILED.                                                           
       SKIP2                                                                    
      ******************************************************************        
      *  CICS PROGRAM - DPKCS022                                       *        
      *  TRANSACTION ID. - Z022                                        *        
      *                                                                *        
      *                DELETE MNEMONIC PROGRAM                         *        
      *                                                                *        
      *  THIS TRANSACTION PROCESSES ALL DELETE REQUESTS FOR THE TMNEMO-*        
      *  NIC DB2 DATABASE. THIS DATABASE IS USED TO CONTROL NAVIGATION *        
      *  WITHIN CICS.                                                  *        
      *                                                                *        
      *  PF KEY PROCESSING:                                            *        
      *                                                                *        
      *         ENTER - EDIT SCREEN                                    *        
      *         PA2   - TOGGLE FUNCTION KEY LINES                      *        
      *         PF5   - REFRESH                                        *        
      *         PF14  - DELETE MNEMONIC FROM TMNEMONIC                 *        
      *         CLEAR - RESET SCREEN TO LAST DISPLAY                   *        
      *                                                                *        
      *  NAVIGATION PF KEY PROCESSING:                                 *        
      *                                                                *        
      *         *SEE MNEMONIC FILE FOR NAVIGATION PF KEY DEFINITIONS.  *        
      *                                                                *        
      *  CHANGED 4/92 BY M. FURLICK PER W/R 1937.                      *        
      *         ADD PROGRAM ID AND GLOBAL ACCESS INDICATOR             *        
      *         TO SCREEN.                                             *        
      *         ALLOW FOR "DETAIL SCROLLING" BETWEEN SELECTED ITEMS.   *        
      *                                                                *        
      *    03/14/96  TKMA235  ADD LOGIC FOR CICS24X7 PROCESSING.       *        
      *                       1) MODIFIED MAP (DPKM022) AND PGM TO     *        
      *                          SET THE UPD-VSAM-IND, DISPLAY IT ON   *        
      *                          THE SCREEN, AND TO ADD IT TO THE      *        
      *                          DB2 TMNEMONIC TABLE.                  *        
      *                       2) MODIFIED MAP (DPKM022) AND PGM TO     *        
      *                          DISPLAY PROGRAM-NAME, FROM THE        *        
      *                          DB2 TMNEMONIC TABLE.                  *        
      *                       3) AS PART OF THE CHANGES TO SUPPORT THE *        
      *                          CICS24X7 PROJECT, THIS WILL NO LONGER *        
      *                          BE A TRUE DELETE OF ROWS FROM THE     *        
      *                          TMNEMONIC, TCICSROUTE, AND TMENUITEM  *        
      *                          DB2 TABLES. INSTEAD, THIS WILL NOW BE *        
      *                          A "SOFT" DELETE; IN OTHER WORDS, THE  *        
      *                          RECORD WILL BE MARKED FOR DELETION,   *        
      *                          BUT WILL NOT BE DELETED AT THIS TIME. *        
      *                          THE ROW FOR THIS MNEMONIC ON THE      *        
      *                          'TMENMONIC' TABLE WILL HAVE THE       *        
      *                          UPD-VSAM-IND SET TO 'D', AND THIS WILL*        
      *                          TRIGGER TRUE DELETION OF ALL          *        
      *                          CORRESPONDING ROWS ON THE ABOVE LISTED*        
      *                          DB2 TABLES, IN THE NEW PROGRAM        *        
      *                          DPKCS033.                             *        
      *    09/09/99  TKMA464  ADD ACCURATE LOCATION REQUIRED INDICATOR.*        
      ******************************************************************        
      *                                                                *        
      ******************************************************************        
       SKIP2                                                                    
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
       EJECT                                                                    
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       SKIP2                                                                    
       WORKING-STORAGE SECTION.                                                 
       SKIP1                                                                    
      *********************                                                     
      * PROGRAM CONSTANTS *                                                     
      *********************                                                     
       SKIP1                                                                    
       01  PROGRAM-CONSTANT-AREA.                                               
           05  PC-MAXIMUM-COMMAREA-LENGTH                                       
                                       PIC S9(4)      VALUE +4072               
                                                      COMP SYNC.                
           05  PC-CURRENT-MAP-NAME     PIC X(8)       VALUE 'DP022A  '.         
           05  PC-CURRENT-MAPSET-NAME  PIC X(8)       VALUE 'DPKM022 '.         
           05  PC-CURRENT-PROGRAM-NAME PIC X(8)       VALUE 'DPKCS022'.         
           05  PC-NAVIGATOR-TRAN-ID    PIC X(4)       VALUE 'Z008'.             
           05  PC-LEAVE-SYSTEM-LENGTH  PIC S9(4)      VALUE +80                 
                                                      COMP SYNC.                
           05  PC-FORMAT-CODE          PIC X(4)       VALUE 'CMIA'.             
           05  PC-ACKNWLDGE-CODE       PIC X(4)       VALUE 'CMIA'.             
           05  PC-COMM-LENGTH          PIC 9(4)       VALUE 2866.               
           05  PC-D-DELETE-REQUESTED   PIC X(1)       VALUE 'D'.                
           05  PC-TSYMSG-MESSAGE-NUMBERS.                                       
               10  PC-TSYMSG-00070     PIC 9(5)       VALUE 00070.              
               10  PC-TSYMSG-00115     PIC 9(5)       VALUE 00115.              
               10  PC-TSYMSG-00148     PIC 9(5)       VALUE 00148.              
               10  PC-TSYMSG-00149     PIC 9(5)       VALUE 00149.              
               10  PC-TSYMSG-01238     PIC 9(5)       VALUE 01238.              
       SKIP2                                                                    
      *********************                                                     
      * PROGRAM VARIABLES *                                                     
      *********************                                                     
       SKIP1                                                                    
       01  PROGRAM-VARIABLE-AREA.                                               
           05  PV-CICS-RESPONSE        PIC S9(8)      VALUE ZERO                
                                                      COMP SYNC.                
           05  PV-RESP                 PIC S9(8)      VALUE ZERO                
                                                      COMP SYNC.                
           05  PV-TERMINAL             PIC X(4)       VALUE SPACES.             
           05  PV-LEAVE-SYSTEM-MSG     PIC X(80)      VALUE SPACES.             
           05  PV-HOLD-MESSAGE         PIC X(78)      VALUE SPACES.             
           05  PV-HOLD-SEVERITY-IND    PIC X(1)       VALUE SPACES.             
           05  PV-MNEMONIC             PIC X(8)       VALUE SPACES.             
           05  PV-COMM-LENGTH-ALPHA    PIC X(4).                                
           05  PV-COMM-LENGTH-NUMERIC  REDEFINES                                
               PV-COMM-LENGTH-ALPHA    PIC 9(4).                                
           05  PV-FORMAT-CODES         PIC X(4)       VALUE SPACES.             
               88  PV-ACCEPTED-FORMAT-CODE            VALUE 'CMIA'              
                                                            'FKEY'              
                                                            'MITM'.             
               88  PV-CMIA-FORMAT                     VALUE 'CMIA'.             
               88  PV-FKEY-FORMAT                     VALUE 'FKEY'.             
               88  PV-MITM-FORMAT                     VALUE 'MITM'.             
       EJECT                                                                    
      ********************                                                      
      * PROGRAM SWITCHES *                                                      
      ********************                                                      
       SKIP1                                                                    
       01  PROGRAM-SWITCHES.                                                    
           05  PS-MAP-SEND-TYPE        PIC X          VALUE '1'.                
               88  PS-SEND-MAP-N-DATA-FRSET           VALUE '1'.                
               88  PS-SEND-MAP-N-DATA-FRSET-CPOSN     VALUE '2'.                
               88  PS-SEND-DATA-ONLY-FRSET            VALUE '3'.                
               88  PS-SEND-DATA-ONLY                  VALUE '4'.                
               88  PS-SEND-DATA-ONLY-ERASEAUP         VALUE '5'.                
               88  PS-SEND-MAP-ONLY-FRSET             VALUE '6'.                
           05  PS-LOCAL-FUNCTION-KEY-IND                                        
                                       PIC X          VALUE 'N'.                
               88  PS-LOCAL-FUNCTION-NO               VALUE 'N'.                
               88  PS-LOCAL-FUNCTION-YES              VALUE 'Y'.                
           05  PS-ROW-FOUND-IND        PIC X          VALUE 'N'.                
               88  PS-ROW-FOUND-NO                    VALUE 'N'.                
               88  PS-ROW-FOUND-YES                   VALUE 'Y'.                
           05  PS-NEW-SCREEN-SENT-SW   PIC X          VALUE 'N'.                
               88  PS-NEW-SCREEN-SENT-NO              VALUE 'N'.                
               88  PS-NEW-SCREEN-SENT-YES             VALUE 'Y'.                
           05  PS-NEW-MNEMONIC-REQ-SW  PIC X          VALUE 'N'.                
               88  PS-NEW-MNEMONIC-REQ-NO             VALUE 'N'.                
               88  PS-NEW-MNEMONIC-REQ-YES            VALUE 'Y'.                
       EJECT                                                                    
      ******************                                                        
      * ERROR MESSAGES *                                                        
      ******************                                                        
       SKIP1                                                                    
       01  ERROR-MESSAGES.                                                      
           05  DEPENDENT-ROW-MSG           PIC X(78)      VALUE                 
               'MNEMONIC CURRENTLY EXISTS ON A MENU OR ASSIGNED TO A USE        
      -        'R, CANNOT DELETE ROW  '.                                        
           05  TIMESTAMP-ERROR-MSG         PIC X(78)      VALUE                 
               'DATABASE TIMESTAMP INCONSISTENCY FOUND                          
      -        '                      '.                                        
           05  INVALID-NEXT-APPL-MSG       PIC X(78)      VALUE                 
               'NEXT APPLICATION CODE INVALID                                   
      -        '                      '.                                        
           05  PM-PROGRAM-MESSAGE-22          PIC X(78)    VALUE                
           'LOGIC ERROR IN TRYING TO READ MD LIST QUEUE - RESP = '.             
           05  FILLER REDEFINES PM-PROGRAM-MESSAGE-22.                          
               10  FILLER                     PIC X(61).                        
               10  PM-RESP-22                 PIC 9999.                         
               10  FILLER                     PIC X(12).                        
       EJECT                                                                    
      **************************                                                
      * DB2 COMMUNICATION AREA *                                                
      **************************                                                
       SKIP1                                                                    
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
       SKIP2                                                                    
      ***********************************                                       
      * DB2 SQL RETURN CODE DEFINITIONS *                                       
      *    USING '88' LEVEL             *                                       
      ***********************************                                       
       SKIP1                                                                    
       01  SQL-RETURN-CODE             PIC S9(9)      COMP-4.                   
           88  SQL-SUCCESSFUL                         VALUE +0.                 
           88  SQL-ROW-FOUND                          VALUE +0.                 
           88  SQL-ROW-NOT-FOUND                      VALUE +100.               
           88  SQL-WARNING                            VALUE +100                
                                                            +558                
                                                            +560.               
           88  SQL-DUPLICATE-ROW                      VALUE -803.               
           88  SQL-MORE-THAN-1-ROW                    VALUE -811.               
           88  SQL-NOT-AVAILABLE                      VALUE -911.               
           88  SQL-DB2-DOWN                           VALUE -923.               
           88  SQL-NULL-VALUE                         VALUE -305.               
           88  SQL-DEPENDENT-ROW                      VALUE -532.               
       EJECT                                                                    
      **************************************                                    
      * MNEMONIC TSQ "B" RECORD LAYOUT.    *                                    
      **************************************                                    
       SKIP1                                                                    
           COPY DPWS002.                                                        
       EJECT                                                                    
      **************************************                                    
      * STANDARD ATTENTION IDENTIFIER LIST *                                    
      **************************************                                    
       SKIP1                                                                    
           COPY DFHAID.                                                         
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WORKING STORAGE INCLUDE MEMBER USED WITH INCLUDE DPPD005    *        
      *----------------------------------------------------------------*        
           COPY DPWS006.                                                        
       EJECT                                                                    
      **************************************************************            
      * WORKING-STORAGE LAYOUT USED TO LOAD THE FUNCTION KEY LINES *            
      **************************************************************            
           COPY DPWS009.                                                        
       EJECT                                                                    
      ***************************                                               
      * COMMON ABEND DATA AREAS *                                               
      ***************************                                               
       SKIP1                                                                    
           COPY DPWS013.                                                        
       EJECT                                                                    
      *****************************                                             
      * ARCHITECTURE MESSAGE AREA *                                             
      *****************************                                             
       SKIP1                                                                    
           COPY DPRD507I.                                                       
       EJECT                                                                    
      *********************************************************                 
      * KOHL'S DEFINED:                                       *                 
      * STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST *                 
      *********************************************************                 
       SKIP1                                                                    
           COPY DPWS015.                                                        
       EJECT                                                                    
      **************************************                                    
      * KOHL'S:                            *                                    
      * STANDARD ATTENTION IDENTIFIER LIST *                                    
      **************************************                                    
       SKIP1                                                                    
           COPY DPWS016.                                                        
       EJECT                                                                    
      ***************************************************************           
      * WORKING-STORAGE LAYOUT USED TO GET THE SYSTEM DATE AND TIME *           
      ***************************************************************           
           COPY DPWS017.                                                        
       EJECT                                                                    
      *******************************                                           
      * STANDARD COMMUNICATION AREA *                                           
      *******************************                                           
       SKIP1                                                                    
           COPY DPWS020.                                                        
           05  FILLER                  REDEFINES                                
               DP020-VARIABLE-COMMAREA.                                         
      ***************************************                                   
      * CICSARC INTER APPLICATION DATA AREA *                                   
      ***************************************                                   
               10  INTER-APPL-COMM-AREA                                         
                                       PIC X(200).                              
           COPY DPWS050.                                                        
           COPY DPWS051.                                                        
           COPY DPWS052.                                                        
           COPY DPWS055.                                                        
      **********************************                                        
      * APPLICATION SPECIFIC COMM AREA *                                        
      **********************************                                        
               10  APPL-SPECIFIC-COMM-AREA                                      
                                       PIC X(2865).                             
               10  DP022-APPL-SPECIFIC-COMM-AREA                                
                                       REDEFINES                                
                   APPL-SPECIFIC-COMM-AREA.                                     
                   15  FILLER          PIC X(02).                               
                   15  DP022-CURRENT-SCREEN-IMAGE.                              
                       20  DP022-SCREEN-TRAN-ID                                 
                                       PIC X(4).                                
                       20  DP022-SCREEN-MNEMONIC                                
                                       PIC X(8).                                
                       20  DP022-SCREEN-MNEMONIC-TITLE                          
                                       PIC X(30).                               
                       20  DP022-SCREEN-AVAILABIL-IND                           
                                       PIC X.                                   
                       20  DP022-SCREEN-COMMAREA-FORMAT                         
                                       PIC X.                                   
                       20  DP022-SCREEN-SAVE-COMM-IND                           
                                       PIC X.                                   
                       20  DP022-SCREEN-SAVE-COMM-LENGTH                        
                                       PIC X(4).                                
                       20  DP022-SCREEN-GLOBAL-ACCESS                           
                                       PIC X(1).                                
                       20  DP022-SCREEN-PROGRAM                                 
                                       PIC X(8).                                
                       20  DP022-SCREEN-TYPE-CODE                               
                                       PIC X(1).                                
                       20  DP022-UPD-VSAM-IND                                   
                                       PIC X(1).                                
                       20  DP022-SCREEN-DB2-PKG-CDE                             
                                       PIC X(1).                                
                       20  DP022-SCREEN-CMNT-TXT                                
                                       PIC X(40).                               
                       20  DP022-ACCURATE-LOCATION-REQ                          
                                       PIC X(1).                                
                   15  DP022-TRAN-DEFINED-CICS                                  
                                       PIC X.                                   
                   15  DP022-SELECT-MAINT-RESULT                                
                                       PIC X.                                   
                       88  DP022-RESULT-IS-CREATE      VALUE '1'.               
                       88  DP022-RESULT-IS-UPDATE      VALUE '2'.               
                       88  DP022-RESULT-IS-DELETE      VALUE '3'.               
                       88  DP022-RESULT-IS-READ        VALUE '4'.               
                       88  DP022-RESULT-IS-ERROR       VALUE '5'.               
                   15  DP022-INTER-APPL-COMMAREA.                               
                       20  DP022-INTER-APPL-FORMAT-CODE                         
                                       PIC X(04).                               
                       20  DP022-INTER-APPL-ACKNLG-CODE                         
                                       PIC X(04).                               
                       20  DP022-MITM-AREA.                                     
                           25  DP022-MITM-MENU-NAME                             
                                       PIC X(08).                               
                           25  DP022-MITM-ITEM-MNEMONIC                         
                                       PIC X(08).                               
                           25  DP022-MITM-SEQUENCE-NUMBER                       
                                       PIC S9(4)      COMP.                     
                           25  DP022-MITM-TRAN-ID                               
                                       PIC X(04).                               
                           25  DP022-MITM-TITLE                                 
                                       PIC X(30).                               
                           25  DP022-MITM-ITEM-TITLE                            
                                       PIC X(30).                               
                           25  DP022-MITM-MAINTENANCE-RESULT                    
                                       PIC X.                                   
                               88  DP022-MITM-RESULT-IS-CREATE                  
                                       VALUE '1'.                               
                               88  DP022-MITM-RESULT-IS-UPDATE                  
                                       VALUE '2'.                               
                               88  DP022-MITM-RESULT-IS-DELETE                  
                                       VALUE '3'.                               
                               88  DP022-MITM-RESULT-IS-READ                    
                                       VALUE '4'.                               
                               88  DP022-MITM-RESULT-IS-ERROR                   
                                       VALUE '5'.                               
                           25  FILLER  PIC X(109).                              
                       20  DP022-FKEY-AREA                                      
                                       REDEFINES                                
                           DP022-MITM-AREA.                                     
                           25  DP022-FKEY-MNEMONIC                              
                                       PIC X(08).                               
                           25  DP022-FKEY-FUNCTION-KEY                          
                                       PIC S9(04)     COMP.                     
                           25  DP022-FKEY-FUNCT-KEY-MNEMONIC                    
                                       PIC X(08).                               
                           25  DP022-FKEY-MAINTENANCE-RESULT                    
                                       PIC X.                                   
                               88  DP022-FKEY-RESULT-IS-CREATE                  
                                       VALUE '1'.                               
                               88  DP022-FKEY-RESULT-IS-UPDATE                  
                                       VALUE '2'.                               
                               88  DP022-FKEY-RESULT-IS-DELETE                  
                                       VALUE '3'.                               
                               88  DP022-FKEY-RESULT-IS-READ                    
                                       VALUE '4'.                               
                               88  DP022-FKEY-RESULT-IS-ERROR                   
                                       VALUE '5'.                               
                           25  FILLER  PIC X(173).                              
                       20  DP022-CMIA-AREA                                      
                                       REDEFINES                                
                           DP022-FKEY-AREA.                                     
                           25  DP022-CMIA-MNEMONIC                              
                                       PIC X(08).                               
                           25  DP022-CMIA-TRAN-ID                               
                                       PIC X(04).                               
                           25  DP022-CMIA-TITLE                                 
                                       PIC X(30).                               
                           25  DP022-CMIA-MAINTENANCE-RESULT                    
                                       PIC X.                                   
                               88  DP022-CMIA-RESULT-IS-CREATE                  
                                       VALUE '1'.                               
                               88  DP022-CMIA-RESULT-IS-UPDATE                  
                                       VALUE '2'.                               
                               88  DP022-CMIA-RESULT-IS-DELETE                  
                                       VALUE '3'.                               
                               88  DP022-CMIA-RESULT-IS-READ                    
                                       VALUE '4'.                               
                               88  DP022-CMIA-RESULT-IS-ERROR                   
                                       VALUE '5'.                               
                           25  FILLER  PIC X(149).                              
                   15  DP022-TIMESTAMP     PIC X(26).                           
                   15  DP022-LAST-MNEMONIC PIC X(08).                           
                   15  DP022-MN-ITEM       PIC S9(4)   COMP.                    
                   15  DP022-MN-SUB        PIC S9(4)   COMP.                    
                   15  DP022-SAVE-INTER-APPL-COMMAREA                           
                                           PIC X(2000).                         
                   15  DP022-SAVE-TSQB-RECORD                                   
                                           PIC X(200).                          
       EJECT                                                                    
      **************************************************************            
      * PARAMETER LIST USED TO CALL THE 'TRANSLATE AID' SUBROUTINE *            
      **************************************************************            
           COPY DPWS045.                                                        
        EJECT                                                                   
      ********************************                                          
      * DSECT FOR THE DPKM010 MAPSET *                                          
      ********************************                                          
       SKIP1                                                                    
           COPY DPKM022.                                                        
       SKIP2                                                                    
      ************************************************                          
      * REDEFINES OF DP010AI MAPSET OF DETAIL FIELDS *                          
      ************************************************                          
       01  MD-MAP-DATA-AREA            REDEFINES                                
           DP022AO.                                                             
           05  FILLER                  PIC X(1000).                             
       EJECT                                                                    
      ******************************                                            
      * DCLGEN FOR TABLE TMNEMONIC *                                            
      ******************************                                            
       SKIP1                                                                    
           EXEC SQL                                                             
               INCLUDE TMNMNC                                                   
           END-EXEC.                                                            
       EJECT                                                                    
      ******************************                                            
      * DCLGEN FOR TABLE TMENUITEM *                                            
      ******************************                                            
       SKIP1                                                                    
           EXEC SQL                                                             
               INCLUDE TMNUITM                                                  
           END-EXEC.                                                            
       EJECT                                                                    
      *******************************                                           
      * DCLGEN FOR TABLE TCICSRTE   *                                           
      *******************************                                           
       SKIP1                                                                    
           EXEC SQL                                                             
               INCLUDE TCICSRTE                                                 
           END-EXEC.                                                            
       EJECT                                                                    
       LINKAGE SECTION.                                                         
       SKIP2                                                                    
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * A000-DELETE-MNEMONIC                                           *        
      * (Z022-00 DELETE MNEMONIC)                                      *        
      * MAINLINE SECTION.                                              *        
      ******************************************************************        
                                                                                
                                                                                
                                                                                
           PERFORM DP015-0000-CHECK-COMMAREA.                                   
                                                                                
      *****************************************************************         
      * THE VARIABLE 'RETURN-WITH-TRAN-ID' WILL BE SET TO 'Y' IN      *         
      * U13-CHECK-COMMAREA IF A BAD COMMAREA OR NO COMMAREA WAS       *         
      * ENCOUNTERED. IF THAT HAPPENS U13-CHECK-COMMAREA STARTS THE    *         
      * NAVIGATION TRANSACTION AND SETS THIS INDICATOR TO 'Y'. CHECK- *         
      * ING FOR A 'Y' HERE WILL PREVENT THIS TRANSACTION FROM CONTINU-*         
      * ING TO EXECUTE IF THE NAVIGATOR HAS BEEN REQUESTED TO START.  *         
      *****************************************************************         
                                                                                
           IF  DP020-SRC-RETURN-W-TRAN-ID                                       
               PERFORM A050-CONTROL-PROCESSING                                  
           END-IF.                                                              
                                                                                
           PERFORM DP003-0000-RETURN-TO-CICS.                                   
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A050-CONTROL-PROCESSING                                        *        
      * (Z022-01 CONTROL PROCESSING)                                   *        
      ******************************************************************        
                                                                                
       A050-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   INITIALIZE DP022-APPL-SPECIFIC-COMM-AREA                     
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   PERFORM A240-GET-FIRST-MNEMONIC                              
                   PERFORM A250-SEND-INITIAL-SCREEN                             
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   MOVE -1             TO ANAVKEYL                              
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   PERFORM A470-SET-COMMAND-ATTRIBUTES                          
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-ERROR                                        
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                       TO TRUE                                  
                   PERFORM A470-SET-COMMAND-ATTRIBUTES                          
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   MOVE PV-HOLD-MESSAGE                                         
                                       TO DP020-MSG-MESSAGE                     
                   MOVE PV-HOLD-SEVERITY-IND                                    
                                       TO DP020-MSG-SEVERITY-IND                
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                       TO TRUE                                  
                   PERFORM A470-SET-COMMAND-ATTRIBUTES                          
                   PERFORM A100-PROCESS-MAP                                     
                                                                                
               WHEN OTHER                                                       
                   SET  DP013-OTHER-ABEND                                       
                                       TO TRUE                                  
                   MOVE 'A050-CONTROL-PROCESSING       '                        
                                       TO DP013-PARAGRAPH                       
                   MOVE INVALID-NEXT-APPL-MSG                                   
                                       TO DP013-MESSAGE-TEXT (1)                
                   MOVE DP020-NEXT-APPL-ACTIVITY                                
                                       TO DP013-MESSAGE-TEXT (2)                
                   PERFORM DP013-0000-PROCESS-ABEND                             
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
      ******************************************************************        
      * A100-PROCESS-MAP                                               *        
      * (Z010-03 PROCESS-MAP)                                          *        
      ******************************************************************        
                                                                                
       A100-PROCESS-MAP.                                                        
                                                                                
           IF  EIBAID  =  DFHPA2                                                
      ***************************************                                   
      * PA2 KEY  -  CHANGE PFKEYS DISPLAYED *                                   
      ***************************************                                   
               MOVE DP016-PA2          TO DP020-SRC-AID                         
               PERFORM DP009-0000-LOAD-FK-LINES                                 
               PERFORM DP018-0000-GET-CURR-TIME                                 
               SET PS-SEND-DATA-ONLY                                            
                                       TO TRUE                                  
               MOVE -1                 TO ANAVKEYL                              
               PERFORM DP008-0000-WRITE-PANEL                                   
           ELSE                                                                 
               IF  EIBAID  =  DFHCLEAR                                          
                   MOVE DP016-CLEAR      TO DP020-SRC-AID                       
                   MOVE -1               TO ANAVKEYL                            
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                         TO TRUE                                
                   PERFORM A550-DISPLAY-PANEL                                   
               ELSE                                                             
                   PERFORM DP007-0000-GET-PANEL                                 
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   PERFORM A300-CHECK-LOCAL-PF-KEYS                             
                   IF  PS-LOCAL-FUNCTION-NO                                     
                       PERFORM DP015-1000-START-NAVIGATOR                       
                   ELSE                                                         
                       IF  ((DP020-SRC-AID  =  DP016-ENTER)    AND              
                            (DP020-NAV-MNEMONIC  NOT =  SPACES  AND             
                                                        LOW-VALUES))            
                           PERFORM DP015-1000-START-NAVIGATOR                   
                       ELSE                                                     
                           MOVE -1         TO ANAVKEYL                          
                           PERFORM A350-LOCAL-PF-KEY-CONTROL                    
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A200-PROCESS-INTER-APPL-COMM                                   *        
      * (Z022-05 PROCESS INTER-APPL COMMAREA)                          *        
      * THE PURPOSE OF THIS MODULE IS TO MANIPULATE THE INTER-APPL-    *        
      * COMMAREA UNDER THE FOUR POSSIBLE CYCLES OF THIS TRANSACTION:   *        
      *                                                                *        
      *        1. DP020-NEXT-ACT-INITIAL                               *        
      *        2. DP020-NEXT-ACT-RETURN                                *        
      *        3. DP020-NEXT-ACT-READ-MAP                              *        
      *        4. DP020-NEXT-ACT-ERROR-RETURN                          *        
      *                                                                *        
      * THREE DATA AREAS ARE NEEDED TO CORRECTLY USE THE INTER-APPL-   *        
      * COMMAREA:                                                      *        
      *                                                                *        
      *        1. THE INTER-APPL-COMMAREA PORTION OF THE STANDARD-COMM-*        
      *           AREA                                                 *        
      *        2. AN AREA OF THE APPLICATION COMMAREA THAT WILL BE USED*        
      *           TO STORE THE CONTENTS OF THE INTER-APPL-COMMAREA SENT*        
      *           TO THIS PROGRAM. (NOTE: ONLY SAVE THOSE FIELDS       *        
      *           RELAVENT TO THIS PROGRAM).                           *        
      *        3. AN AREA OF THE APPLICATION COMMAREA THAT WILL BE USED*        
      *           TO HOLD THE DATA RELAVENT TO THE CURRENT OBJECT BEING*        
      *           PROCESSED BY THIS PROGRAM.                           *        
      ******************************************************************        
                                                                                
       A200-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           EVALUATE TRUE                                                        
                                                                                
           WHEN DP020-NEXT-ACT-INITIAL                                          
                MOVE DP020-INTER-APPL-FORMAT-CODE TO PV-FORMAT-CODES            
                MOVE DP020-INTER-APPL-DATA-AREA                                 
                     TO  DP022-SAVE-INTER-APPL-COMMAREA                         
                                                                                
           WHEN DP020-NEXT-ACT-RETURN                                           
                MOVE DP022-SAVE-INTER-APPL-COMMAREA                             
                     TO  DP020-INTER-APPL-DATA-AREA                             
                                                                                
           WHEN DP020-NEXT-ACT-READ-MAP                                         
                IF  DP020-FK-GO-TO-TRAN (DP020-SRC-AID)                         
                    PERFORM A800-SETUP-INTER-APPL                               
                ELSE                                                            
                    IF  DP020-FK-RETURN (DP020-SRC-AID)                         
                        IF  DP020-INT-APPL-FORMAT-IND EQUAL '2'                 
                            PERFORM B300-REWRITE-TSQB                           
                        END-IF                                                  
                        PERFORM A850-RESET-INITIAL-INTER-APPL                   
                    END-IF                                                      
                END-IF                                                          
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
      ******************************************************************        
      * INITIALIZE ITEM AND SUB FIELDS, GET FIRST MNEMONIC FOR         *        
      * PROCESSING.                                                    *        
      ******************************************************************        
                                                                                
       A240-GET-FIRST-MNEMONIC.                                                 
                                                                                
           IF  DP020-INT-APPL-FORMAT-IND EQUAL '1'                              
               MOVE DP055-MNEMONIC   TO  DP022-SCREEN-MNEMONIC                  
           ELSE                                                                 
               IF  DP020-INT-APPL-FORMAT-IND EQUAL '2'                          
                   MOVE 1            TO  DP022-MN-ITEM                          
                                         DP022-MN-SUB                           
                   PERFORM B100-READ-LIST-QUEUE                                 
                   MOVE DP002-MNEMONIC (1)                                      
                                     TO  DP022-SCREEN-MNEMONIC                  
                   SET DP002-INQ (1) TO  TRUE                                   
                   MOVE DP002-ARCH-MAINT-TSQB-REC                               
                                     TO  DP022-SAVE-TSQB-RECORD                 
               ELSE                                                             
                   MOVE DP020-NAV-KEY-DATA                                      
                                     TO  DP022-SCREEN-MNEMONIC                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A250-SEND-INITIAL-SCREEN                                       *        
      * (Z022-06 SEND INITIAL SCREEN)                                  *        
      * RETRIEVE THE DATA FROM THE DATABASE, FORMAT DATA ON SCREEN, AND*        
      * DISPLAY THE SCREEN TO THE USER.                                *        
      ******************************************************************        
                                                                                
       A250-SEND-INITIAL-SCREEN.                                                
                                                                                
           PERFORM A500-RETRIEVE-DATA-FOR-SCREEN.                               
                                                                                
           IF  NOT DP020-NEXT-ACT-APPL-ERROR                                    
               PERFORM A550-DISPLAY-PANEL                                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A300-CHECK-LOCAL-PF-KEYS                                       *        
      * (Z010-09 CHECK FOR LOCAL PF KEYS)                              *        
      *                                                                *        
      * LOCAL FUNCTION KEYS:                                           *        
      *      PF5   (DP020-SRC-AID = 05)  - REFRESH SCREEN              *        
      *      PF16  (DP020-SRC-AID = 16)  - DELETE ROW FROM TMNEMONIC   *        
      *      PA2   (DP020-SRC-AID = 26)  - TOGGLE THE FUNCTION KEY     *        
      *                                    LINES ON SCREEN             *        
      *      CLEAR (DP020-SRC-AID = 27)  - REFRESH SCREEN              *        
      *      ENTER (DP020-SRC-AID = 28)  - EDIT SCREEN                 *        
      ******************************************************************        
                                                                                
       A300-CHECK-LOCAL-PF-KEYS.                                                
                                                                                
           SET PS-LOCAL-FUNCTION-NO    TO TRUE.                                 
                                                                                
           IF (DP020-FK-LOCAL-KEY (DP020-SRC-AID)      OR                       
               DP020-FK-CONFIRM (DP020-SRC-AID)        OR                       
               DP020-FK-REFRESH (DP020-SRC-AID)        OR                       
               DP020-FK-PREV-PAGE (DP020-SRC-AID)      OR                       
               DP020-FK-NEXT-PAGE (DP020-SRC-AID)      OR                       
               DP020-FK-FIRST-PAGE (DP020-SRC-AID)     OR                       
               DP020-FK-NEXT-SELECTION (DP020-SRC-AID) OR                       
               DP020-FK-DESELECT (DP020-SRC-AID))                               
               SET PS-LOCAL-FUNCTION-YES                                        
                                       TO TRUE                                  
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A350-LOCAL-PF-KEY-CONTROL                                      *        
      * (Z010-10 LOCAL PF KEY CONTROL)                                 *        
      ******************************************************************        
                                                                                
       A350-LOCAL-PF-KEY-CONTROL.                                               
                                                                                
           EVALUATE DP020-SRC-AID                                               
                                                                                
               WHEN DP016-PF5                                                   
                   PERFORM A250-SEND-INITIAL-SCREEN                             
                                                                                
               WHEN DP016-PF20                                                  
                   IF  DP020-INT-APPL-FORMAT-IND EQUAL '2'                      
                       SET PS-SEND-MAP-N-DATA-FRSET                             
                                     TO  TRUE                                   
                       PERFORM B200-GET-NEXT-MNEMONIC                           
                       IF (DP020-MSG-NUMBER NUMERIC                             
                       AND DP020-MSG-NUMBER = PC-TSYMSG-00070)                  
      *                    *********************************************        
      *                    * PC-TSYMSG-00070:                                   
      *                    * BOTTOM OF LIST                                     
      *                    *********************************************        
                           SET PS-SEND-DATA-ONLY-FRSET TO TRUE                  
                           PERFORM A550-DISPLAY-PANEL                           
                       ELSE                                                     
                           PERFORM A250-SEND-INITIAL-SCREEN                     
                       END-IF                                                   
                   ELSE                                                         
                       MOVE PC-TSYMSG-00070                                     
                                     TO  DP020-MSG-NUMBER                       
      *                *************************************************        
      *                * PC-TSYMSG-00070:                                       
      *                * BOTTOM OF LIST                                         
      *                *************************************************        
                       SET DP020-MSG-INFORMATIONAL                              
                                     TO  TRUE                                   
                       PERFORM A550-DISPLAY-PANEL                               
                   END-IF                                                       
                                                                                
               WHEN  DP016-PF16                                                 
                   PERFORM A400-PROCESS-DELETE                                  
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN  DP016-PA2                                                  
                   SET PS-SEND-DATA-ONLY                                        
                                       TO TRUE                                  
                   PERFORM DP009-0000-LOAD-FK-LINES                             
                   PERFORM DP018-0000-GET-CURR-TIME                             
                   MOVE -1             TO ANAVKEYL                              
                   PERFORM DP008-0000-WRITE-PANEL                               
                                                                                
               WHEN  DP016-CLEAR                                                
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   MOVE -1             TO ANAVKEYL                              
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN OTHER                                                       
                   PERFORM A650-MOVE-SCREEN-TO-COMM                             
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
      ******************************************************************        
      * A400-PROCESS-DELETE                                            *        
      * (Z022-12 PROCESS DELETE TO DATABASE)                           *        
      * ALL DB2 DATABASE MAINTENANCE USES 'TIMESTAMP' VALIDATION SYNTAX*        
      * TO ENSURE THAT THE SET OF DATA BEING UPDATED IS IN THE SAME    *        
      * CONDITION AT THE TIME OF UPDATE THAT IT WAS IN WHEN THE SET OF *        
      * DATA WAS RETRIEVED FROM THE DATABASE FOR PROCESSING. THIS IS A *        
      * RECOGNITION OF THE POTENTIAL FOR CONCURRENT UPDATING ON ALL    *        
      * RELATIONS WITHIN THE DB2 ENVIRONMENT.                          *        
      ******************************************************************        
                                                                                
       A400-PROCESS-DELETE.                                                     
                                                                                
           MOVE DP022-SCREEN-MNEMONIC  TO TMNEMONIC-MNEMONIC-CODE.              
           MOVE DP022-TIMESTAMP        TO TMNEMONIC-LAST-UPDATE-TMESTP.         
                                                                                
           PERFORM X910-UPDATE-TMNEMONIC-ROW.                                   
                                                                                
           IF  SQL-ROW-NOT-FOUND                                                
               MOVE DP015-PRO-BRT-OFF  TO AMNEMA                                
               MOVE DP015-RED          TO AMNEMC                                
               MOVE DP015-REVERSE      TO AMNEMH                                
               MOVE DP015-PRO-NOR-OFF  TO ATRANIDA                              
               MOVE DP015-BLUE         TO ATRANIDC                              
               MOVE DP015-PRO-NOR-OFF  TO AFORMATA                              
               MOVE DP015-BLUE         TO AFORMATC                              
               MOVE DP015-PRO-NOR-OFF  TO ASAVCOMA                              
               MOVE DP015-BLUE         TO ASAVCOMC                              
               MOVE DP015-PRO-NOR-OFF  TO AAVAILA                               
               MOVE DP015-BLUE         TO AAVAILC                               
               MOVE DP015-PRO-NOR-OFF  TO ACOMLENA                              
               MOVE DP015-BLUE         TO ACOMLENC                              
               MOVE DP015-PRO-NOR-OFF  TO ATRNTLEA                              
               MOVE DP015-BLUE         TO ATRNTLEC                              
               MOVE DP015-PRO-NOR-OFF  TO APKGA                                 
               MOVE DP015-BLUE         TO APKGC                                 
               MOVE DP015-PRO-NOR-OFF  TO ACMNTA                                
               MOVE DP015-BLUE         TO ACMNTC                                
               MOVE SPACES             TO DP022-TIMESTAMP                       
               MOVE PC-TSYMSG-00148    TO DP020-MSG-NUMBER                      
      *        *********************************************************        
      *        * PC-TSYMSG-00148:                                               
      *        * DATA DOES NOT EXIST                                            
      *        *********************************************************        
               SET DP020-MSG-FATAL TO TRUE                                      
               SET DP022-RESULT-IS-ERROR TO TRUE                                
           ELSE                                                                 
               IF SQL-SUCCESSFUL                                                
                   MOVE PC-TSYMSG-01238                                         
                                       TO  DP020-MSG-NUMBER                     
      *            *****************************************************        
      *            * PC-TSYMSG-01238:                                           
      *            * REQUEST SUCCESSFUL; ROW MARKED FOR DELETION                
      *            *****************************************************        
                   SET DP020-MSG-INFORMATIONAL                                  
                                       TO TRUE                                  
      *            MOVE PC-D-DELETE-REQUESTED                                   
                   MOVE TMNEMONIC-UPD-VSAM-IND                                  
                                       TO DP022-UPD-VSAM-IND                    
                                          AUVINDO                               
                                                                                
                   IF  DP020-INT-APPL-FORMAT-IND EQUAL '2'                      
                       MOVE DP022-SAVE-TSQB-RECORD                              
                                       TO DP002-ARCH-MAINT-TSQB-REC             
                       SET DP002-DEL (DP022-MN-SUB)                             
                                       TO  TRUE                                 
                       MOVE DP002-ARCH-MAINT-TSQB-REC                           
                                     TO  DP022-SAVE-TSQB-RECORD                 
                   ELSE                                                         
                       IF  DP020-INT-APPL-FORMAT-IND EQUAL '1'                  
                           SET DP055-DELETED TO  TRUE                           
                           MOVE DP020-INTER-APPL-DATA-AREA                      
                                     TO  DP022-SAVE-INTER-APPL-COMMAREA         
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      *****************************************************************         
      *     EVERY TIME IN SET THE COMMAND FIELD TO EITHER RED         *         
      *     OR GREEN. THIS IS BECAUSE IT IS UNEDITTED IN THE          *         
      *     PROGRAM AND IT NEEDS RESETTING.                           *         
      *****************************************************************         
       A470-SET-COMMAND-ATTRIBUTES.                                             
           IF DP020-NEXT-ACT-ERROR                                              
               MOVE DP020-MSG-MESSAGE                                           
                                       TO PV-HOLD-MESSAGE                       
               MOVE DP020-MSG-SEVERITY-IND                                      
                                       TO PV-HOLD-SEVERITY-IND                  
               IF DP020-NAV-MNEMONIC NOT EQUAL TO SPACES                        
                   MOVE DP015-UNP-ALP-BRT-MDT                                   
                                       TO ANAVCMDA                              
                   MOVE DP015-RED                                               
                                       TO ANAVCMDC                              
                   MOVE DP015-REVERSE                                           
                                       TO ANAVCMDH                              
               ELSE                                                             
                   MOVE SPACES         TO ANAVCMDO                              
                                          DP020-NAV-MNEMONIC                    
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ANAVCMDA                              
                   MOVE DP015-GREEN                                             
                                       TO ANAVCMDC                              
                   MOVE DP015-UNDERLINE                                         
                                       TO ANAVCMDH                              
               END-IF                                                           
           ELSE                                                                 
               IF (EIBAID NOT EQUAL TO DFHPA2 AND                               
                                       DFHCLEAR)                                
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ANAVCMDA                              
                   MOVE DP015-GREEN                                             
                                       TO ANAVCMDC                              
                   MOVE DP015-UNDERLINE                                         
                                       TO ANAVCMDH                              
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A500-RETRIEVE-DATA-FOR-SCREEN                                  *        
      * (Z022-07 RETRIEVE DATA FOR SCREEN                              *        
      ******************************************************************        
                                                                                
       A500-RETRIEVE-DATA-FOR-SCREEN.                                           
                                                                                
           IF  DP020-SRC-AID  =  DP016-CLEAR                                    
               CONTINUE                                                         
           ELSE                                                                 
               MOVE DP022-SCREEN-MNEMONIC                                       
                                       TO TMNEMONIC-MNEMONIC-CODE               
                                                                                
               PERFORM X900-READ-TMNEMONIC-ROW                                  
                                                                                
               IF  SQL-ROW-FOUND                                                
                   MOVE TMNEMONIC-TRANS-ID-CODE                                 
                                       TO DP022-SCREEN-TRAN-ID                  
                   MOVE TMNEMONIC-PANEL-TITLE-NAME                              
                                       TO DP022-SCREEN-MNEMONIC-TITLE           
                   MOVE TMNEMONIC-AVAIL-INDCT                                   
                                       TO DP022-SCREEN-AVAILABIL-IND            
                   IF  TMNEMONIC-CMAREA-FRMT-CODE EQUAL 'K'                     
                       MOVE 'Y'        TO DP022-SCREEN-COMMAREA-FORMAT          
                   ELSE                                                         
                       MOVE TMNEMONIC-CMAREA-FRMT-CODE                          
                                       TO DP022-SCREEN-COMMAREA-FORMAT          
                   END-IF                                                       
                   MOVE TMNEMONIC-SAVE-CMAREA-INDCT                             
                                       TO DP022-SCREEN-SAVE-COMM-IND            
                   MOVE TMNEMONIC-CMAREA-LNGTH-MSRMT                            
                                       TO PV-COMM-LENGTH-NUMERIC                
                   MOVE PV-COMM-LENGTH-ALPHA                                    
                                       TO DP022-SCREEN-SAVE-COMM-LENGTH         
                   MOVE TMNEMONIC-LAST-UPDATE-TMESTP                            
                                       TO DP022-TIMESTAMP                       
                   MOVE TMNEMONIC-GLBL-ACCS-IND                                 
                                       TO DP022-SCREEN-GLOBAL-ACCESS            
                   MOVE TMNEMONIC-PGM-NAME                                      
                                       TO DP022-SCREEN-PROGRAM                  
                   MOVE TMNEMONIC-SCRN-TYP-CDE                                  
                                       TO DP022-SCREEN-TYPE-CODE                
                   MOVE TMNEMONIC-DB2-PKG-CDE                                   
                                       TO DP022-SCREEN-DB2-PKG-CDE              
                   MOVE TMNEMONIC-CMNT-TXT                                      
                                       TO DP022-SCREEN-CMNT-TXT                 
                   MOVE TMNEMONIC-ACRT-LOC-REQR-IND                             
                                       TO DP022-ACCURATE-LOCATION-REQ           
                   MOVE TMNEMONIC-UPD-VSAM-IND                                  
                                       TO DP022-UPD-VSAM-IND                    
                   MOVE LOW-VALUES     TO ANAVKEYO                              
               ELSE                                                             
                   MOVE PC-TSYMSG-00148                                         
                                       TO DP020-MSG-NUMBER                      
      *            *****************************************************        
      *            * PC-TSYMSG-00148:                                           
      *            * DATA DOES NOT EXIST                                        
      *            *****************************************************        
                   SET  DP020-MSG-FATAL                                         
                                       TO TRUE                                  
                   SET DP020-NEXT-ACT-APPL-ERROR                                
                                       TO TRUE                                  
                                                                                
                   IF  DP020-INT-APPL-FORMAT-IND EQUAL '2'                      
                       PERFORM B300-REWRITE-TSQB                                
                   END-IF                                                       
                                                                                
                   PERFORM A850-RESET-INITIAL-INTER-APPL                        
                   PERFORM DP015-1000-START-NAVIGATOR                           
               END-IF                                                           
               MOVE -1                 TO ANAVKEYL                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A550-DISPLAY-PANEL                                             *        
      * (Z022-02 DISPLAY PANEL)                                        *        
      ******************************************************************        
                                                                                
       A550-DISPLAY-PANEL.                                                      
                                                                                
           IF  PS-SEND-DATA-ONLY-FRSET                                          
               PERFORM DP018-0000-GET-CURR-TIME                                 
           ELSE                                                                 
               PERFORM DP017-0000-GET-CURR-DATE-TIME                            
               PERFORM DP002-0000-FORMAT-TITLE-LINES                            
           END-IF.                                                              
                                                                                
           IF  (    PS-NEW-MNEMONIC-REQ-YES                                     
               OR   PS-SEND-MAP-N-DATA-FRSET                                    
               )                                                                
               PERFORM A600-LOAD-DETAIL-DATA                                    
           END-IF.                                                              
                                                                                
           PERFORM DP005-0000-FORMAT-MSG-LINES.                                 
                                                                                
           IF  (    PS-SEND-MAP-N-DATA-FRSET                                    
               OR   DP020-SRC-AID  =  DP016-PA2                                 
               )                                                                
      ***************************************                                   
      * PA2 KEY  -  CHANGE PFKEYS DISPLAYED *                                   
      ***************************************                                   
               PERFORM DP009-0000-LOAD-FK-LINES                                 
           END-IF.                                                              
                                                                                
           PERFORM DP008-0000-WRITE-PANEL.                                      
       EJECT                                                                    
      ******************************************************************        
      * A600-LOAD-DETAIL-DATA                                          *        
      * (Z022-08 LOAD DETAIL DATA)                                     *        
      ******************************************************************        
                                                                                
       A600-LOAD-DETAIL-DATA.                                                   
                                                                                
           MOVE DP022-SCREEN-MNEMONIC  TO AMNEMO.                               
           MOVE DP022-SCREEN-TRAN-ID   TO ATRANIDO.                             
           MOVE DP022-SCREEN-MNEMONIC-TITLE                                     
                                       TO ATRNTLEO.                             
           MOVE DP022-SCREEN-AVAILABIL-IND                                      
                                       TO AAVAILO.                              
           MOVE DP022-SCREEN-COMMAREA-FORMAT                                    
                                       TO AFORMATO.                             
           MOVE DP022-SCREEN-SAVE-COMM-IND                                      
                                       TO ASAVCOMO.                             
           MOVE DP022-SCREEN-SAVE-COMM-LENGTH                                   
                                       TO ACOMLENO.                             
           MOVE DP022-SCREEN-GLOBAL-ACCESS                                      
                                       TO AGLBACCO.                             
           MOVE DP022-SCREEN-PROGRAM                                            
                                       TO APROGO.                               
           MOVE DP022-SCREEN-TYPE-CODE                                          
                                       TO ASCRTYPO.                             
           MOVE DP022-SCREEN-DB2-PKG-CDE                                        
                                       TO APKGO.                                
           MOVE DP022-SCREEN-CMNT-TXT                                           
                                       TO ACMNTO.                               
           MOVE DP022-ACCURATE-LOCATION-REQ                                     
                                       TO ALOCO.                                
           MOVE DP022-UPD-VSAM-IND     TO AUVINDO.                              
       EJECT                                                                    
      ******************************************************************        
      * A650-MOVE-SCREEN-TO-COMM                                       *        
      * (Z022-13 MOVE SCREEN TO COMMAREA)                              *        
      * IN ORDER TO MINIMIZE DATA TRANSMISSIONS, ALL SCREEN VARIABLES  *        
      * WILL BE STORED IN THE COMMAREA. ALL EDITING AND DATABASE I/O   *        
      * WILL BE FROM THESE FIELDS IN THE APPLICATION SPECIFIC COMMAREA.*        
      * ONLY ATTRIBUTE CHANGES WILL BE SENT TO THE TERMINAL AS LONG AS *        
      * THE CURRENT MAP IS STILL BEING DISPLAYED. THIS MODULE OVERLAYS *        
      * ANY COMMAREA SCREEN VARIABLE THAT HAS CHANGED (EITHER A LENGTH *        
      * GREATER THAN ZERO OR EOF HAS BEEN PRESSED) WITH THE INPUT FIELD*        
      * FROM THE SCREEN. IF ERASE-EOF HAS BEEN PRESSED WITHIN THE      *        
      * MNEMONIC FIELD ON THE SCREEN IT IS NECESSARY TO SEND AN INITIAL*        
      * SCREEN. THIS IS DONE BECAUSE MNEMONIC IS THE KEY TO THE TMNEMO-*        
      *NIC TABLE THAT THE USER IS TRYING TO ADD A ROW TO. IF THIS PROG-*        
      * RAM HAS NO MNEMONIC, IT CAN'T ADD A ROW. THE SWITCH 'PS-NEW-   *        
      * SCREEN-SENT-SW' IS SET TO A 'Y' IN THIS SITUATION SO THAT FUR- *        
      * THER PROCESSING OF THE SCREEN IS NOT ATTEMPTED (SEE MODULE A100*        
      * PROCESS-MAP FOR WHERE THIS SWITCH IS INTERROGATED AND PROCESS- *        
      * ING BYPASSED IF IT IS OTHER THAN 'N'.                                   
      ******************************************************************        
                                                                                
       A650-MOVE-SCREEN-TO-COMM.                                                
                                                                                
           SET PS-NEW-SCREEN-SENT-NO   TO TRUE.                                 
           IF  (    ANAVKEYI       NOT =   SPACES                               
               AND  ANAVKEYI       NOT =   LOW-VALUES                           
               AND  ANAVCMDL           =   ZEROS                                
               AND  DP020-SRC-AID      =   DP016-ENTER                          
               )                                                                
               SET PS-NEW-SCREEN-SENT-YES                                       
                                       TO TRUE                                  
               INITIALIZE DP022-CURRENT-SCREEN-IMAGE                            
               MOVE ANAVKEYI           TO PV-MNEMONIC                           
                                          DP022-SCREEN-MNEMONIC                 
               MOVE LOW-VALUES         TO ANAVKEYO                              
               MOVE SPACES             TO DP022-SELECT-MAINT-RESULT             
               SET PS-SEND-MAP-N-DATA-FRSET                                     
                                       TO TRUE                                  
               PERFORM A250-SEND-INITIAL-SCREEN                                 
           ELSE                                                                 
               SET PS-SEND-DATA-ONLY-FRSET                                      
                                       TO TRUE                                  
               PERFORM A550-DISPLAY-PANEL                                       
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A800-SETUP-INTER-APPL                                          *        
      * (Z022-16 SETUP INTER-APPL)                                     *        
      *                                                                *        
      * WHENEVER THIS TRANSACTION IS ABOUT TO NAVIGATE TO ANOTHER      *        
      * TRANSACTION, THIS MODULE WILL FORMAT THE INTER APPLICATION     *        
      * COMMAREA WITH THE DATA SAVED FOR THE CURRENT OBJECT BEING PRO- *        
      * CESSED BY THIS TRANSACTION.                                    *        
      ******************************************************************        
                                                                                
       A800-SETUP-INTER-APPL.                                                   
                                                                                
           MOVE DP020-INTER-APPL-DATA-AREA                                      
                                    TO DP022-SAVE-INTER-APPL-COMMAREA.          
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A850-RESET-INITIAL-INTER-APPL                                  *        
      * (Z022-17 RESET INITIAL INTER-APPL)                             *        
      *                                                                *        
      * WHEN THIS MODULE IS ABOUT TO RETURN TO THE MODULE THAT INVOKED *        
      * IT, IT WILL RESET THE COMMAREA TO THE CONTENTS THAT WERE SENT  *        
      * TO IT ALONG WITH ANY UPDATES TO THE OBJECT SENT TO IT, ALONG   *        
      * WITH AN ACKNOWLEDGEMENT CODE.                                  *        
      ******************************************************************        
                                                                                
       A850-RESET-INITIAL-INTER-APPL.                                           
                                                                                
           MOVE DP022-SAVE-INTER-APPL-COMMAREA                                  
                                     TO   DP020-INTER-APPL-DATA-AREA.           
                                                                                
           IF  DP020-INT-APPL-FORMAT-IND EQUAL '2'                              
                                                                                
               IF  DP020-NEXT-ACT-APPL-ERROR                                    
                   MOVE DP022-SAVE-TSQB-RECORD                                  
                                     TO DP002-ARCH-MAINT-TSQB-REC               
                   SET DP002-ERR (DP022-MN-ITEM)                                
                                     TO  TRUE                                   
                   MOVE DP002-ARCH-MAINT-TSQB-REC                               
                                     TO  DP022-SAVE-TSQB-RECORD                 
                   PERFORM B300-REWRITE-TSQB                                    
               END-IF                                                           
                                                                                
               COMPUTE DP055-NUMBER-PROCESSED-ITEMS =                           
                     ((DP022-MN-ITEM - 1) * DP002-MAX-ITEMS)                    
                     + DP022-MN-SUB                                             
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      **********************************                                        
      * READ A TSQB RECORD.            *                                        
      **********************************                                        
                                                                                
       B100-READ-LIST-QUEUE.                                                    
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
               QUEUE(DP055-TSQB-QUEUE-NAME)                                     
               INTO(DP002-ARCH-MAINT-TSQB-REC)                                  
               LENGTH(DP055-TSQB-QUEUE-LENGTH)                                  
               ITEM(DP022-MN-ITEM)                                              
               RESP(PV-RESP)                                                    
           END-EXEC.                                                            
                                                                                
           IF  PV-RESP EQUAL DFHRESP(NORMAL)                                    
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PV-RESP          TO  PM-RESP-22                             
               SET DP013-LOGIC-ABEND TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE 'B100-READ-LIST-QUEUE'                                      
                                     TO  DP013-PARAGRAPH                        
               MOVE PM-PROGRAM-MESSAGE-22                                       
                                     TO  DP013-MESSAGE-TEXT (1)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE TEMP STORAGE QUEUE, GET THE NEXT MNEMONIC.         *        
      *    IF AT END, ISSUE MESSAGE, OTHERWISE CONTINUE PROCESSING.    *        
      *                                                                *        
      *    PERFORMED BY 3000                                           *        
      *----------------------------------------------------------------*        
                                                                                
       B200-GET-NEXT-MNEMONIC.                                                  
                                                                                
           ADD 1                     TO  DP022-MN-SUB.                          
           IF  DP022-MN-SUB GREATER THAN DP002-MAX-ITEMS                        
               PERFORM B300-REWRITE-TSQB                                        
               ADD 1                 TO  DP022-MN-ITEM                          
               MOVE 1                TO  DP022-MN-SUB                           
               PERFORM B100-READ-LIST-QUEUE                                     
               MOVE DP002-ARCH-MAINT-TSQB-REC                                   
                                     TO  DP022-SAVE-TSQB-RECORD                 
           ELSE                                                                 
               MOVE DP022-SAVE-TSQB-RECORD                                      
                                     TO  DP002-ARCH-MAINT-TSQB-REC              
           END-IF.                                                              
                                                                                
           IF  DP002-MNEMONIC (DP022-MN-SUB) GREATER THAN SPACES                
               MOVE DP002-MNEMONIC (DP022-MN-SUB)                               
                                     TO  DP022-SCREEN-MNEMONIC                  
               SET DP002-INQ (DP022-MN-SUB)                                     
                                     TO  TRUE                                   
               MOVE DP002-ARCH-MAINT-TSQB-REC                                   
                                     TO  DP022-SAVE-TSQB-RECORD                 
           ELSE                                                                 
               SUBTRACT 1          FROM  DP022-MN-SUB                           
               MOVE PC-TSYMSG-00070  TO  DP020-MSG-NUMBER                       
      *        *********************************************************        
      *        * PC-TSYMSG-00070:                                               
      *        * BOTTOM OF LIST                                                 
      *        *********************************************************        
               SET DP020-MSG-INFORMATIONAL                                      
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    UPDATE THE LIST QUEUE TO REFLECT INQUIRE STATUS.            *        
      *                                                                *        
      *    PERFORMED BY 2150, 3100.                                    *        
      *----------------------------------------------------------------*        
                                                                                
       B300-REWRITE-TSQB.                                                       
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                QUEUE(DP055-TSQB-QUEUE-NAME)                                    
                FROM(DP022-SAVE-TSQB-RECORD)                                    
                LENGTH(DP055-TSQB-QUEUE-LENGTH)                                 
                ITEM(DP022-MN-ITEM)                                             
                RESP(PV-RESP)                                                   
                REWRITE                                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-RESP NOT EQUAL DFHRESP(NORMAL)                                
               MOVE PV-RESP          TO  PM-RESP-22                             
               SET DP013-LOGIC-ABEND TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE 'B300-REWRITE-TSQB'                                         
                                     TO  DP013-PARAGRAPH                        
               MOVE PM-PROGRAM-MESSAGE-22                                       
                                     TO  DP013-MESSAGE-TEXT (1)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      **********************************                                        
      * FETCH ROW FROM TABLE TMNEMONIC *                                        
      **********************************                                        
                                                                                
       X900-READ-TMNEMONIC-ROW.                                                 
                                                                                
             EXEC SQL                                                           
                 SELECT       MNEMONIC_CODE,                                    
                              TRANS_ID_CODE,                                    
                              AVAIL_INDCT,                                      
                              CMAREA_FRMT_CODE,                                 
                              SAVE_CMAREA_INDCT,                                
                              CMAREA_LNGTH_MSRMT,                               
                              PANEL_TITLE_NAME,                                 
                              LAST_UPDATE_TMESTP,                               
                              GLBL_ACCS_IND,                                    
                              PGM_NAME,                                         
                              SCRN_TYP_CDE,                                     
                              UPD_VSAM_IND,                                     
                              DB2_PKG_CDE,                                      
                              CMNT_TXT,                                         
                              ACRT_LOC_REQR_IND                                 
                       INTO   :TMNEMONIC-MNEMONIC-CODE,                         
                              :TMNEMONIC-TRANS-ID-CODE,                         
                              :TMNEMONIC-AVAIL-INDCT,                           
                              :TMNEMONIC-CMAREA-FRMT-CODE,                      
                              :TMNEMONIC-SAVE-CMAREA-INDCT,                     
                              :TMNEMONIC-CMAREA-LNGTH-MSRMT,                    
                              :TMNEMONIC-PANEL-TITLE-NAME,                      
                              :TMNEMONIC-LAST-UPDATE-TMESTP,                    
                              :TMNEMONIC-GLBL-ACCS-IND,                         
                              :TMNEMONIC-PGM-NAME,                              
                              :TMNEMONIC-SCRN-TYP-CDE,                          
                              :TMNEMONIC-UPD-VSAM-IND,                          
                              :TMNEMONIC-DB2-PKG-CDE,                           
                              :TMNEMONIC-CMNT-TXT,                              
                              :TMNEMONIC-ACRT-LOC-REQR-IND                      
                       FROM   TMNEMONIC                                         
                       WHERE  MNEMONIC_CODE  =  :TMNEMONIC-MNEMONIC-CODE        
             END-EXEC.                                                          
                                                                                
      ***************************                                               
      * CHECK SQLCODE OF SELECT *                                               
      ***************************                                               
                                                                                
             MOVE SQLCODE              TO SQL-RETURN-CODE.                      
                                                                                
             IF  (    SQL-SUCCESSFUL                                            
                 OR   SQL-ROW-NOT-FOUND                                         
                 )                                                              
                 CONTINUE                                                       
             ELSE                                                               
                 SET  DP013-DB2-ABEND  TO TRUE                                  
                 MOVE 'X900-READ-TMNEMONIC-ROW       '                          
                                       TO DP013-PARAGRAPH                       
                 MOVE SQLCA            TO DP013-SQLCA                           
                 MOVE 'TMNEMONIC         '                                      
                                       TO DP013-DB2-TABLE-NAME (1)              
                 PERFORM DP013-0000-PROCESS-ABEND                               
             END-IF.                                                            
       EJECT                                                                    
      ***********************************                                       
      * DELETE ROW FROM TABLE TMNEMONIC *                                       
      ***********************************                                       
       X910-UPDATE-TMNEMONIC-ROW.                                               
             MOVE PC-D-DELETE-REQUESTED TO TMNEMONIC-UPD-VSAM-IND.              
                                                                                
             EXEC SQL                                                           
                 UPDATE                                                         
                        TMNEMONIC                                               
                   SET                                                          
                        UPD_VSAM_IND       =                                    
                                          :TMNEMONIC-UPD-VSAM-IND,              
                        LAST_UPDATE_TMESTP =                                    
                                           CURRENT TIMESTAMP                    
                  WHERE                                                         
                         (MNEMONIC_CODE     =                                   
                                  :TMNEMONIC-MNEMONIC-CODE)                     
      *                           :TMNEMONIC-MNEMONIC-CODE                      
      *                        AND                                              
      *                        LAST_UPDATE_TMESTP =                             
      *                           :TMNEMONIC-LAST-UPDATE-TMESTP)                
             END-EXEC.                                                          
                                                                                
      ***************************                                               
      * CHECK SQLCODE OF UPDATE *                                               
      ***************************                                               
                                                                                
             MOVE SQLCODE              TO SQL-RETURN-CODE.                      
             IF  SQL-SUCCESSFUL                                                 
             OR  SQL-ROW-NOT-FOUND                                              
                 CONTINUE                                                       
             ELSE                                                               
                 SET  DP013-DB2-ABEND  TO TRUE                                  
                 MOVE 'X910-UPDATE-TMNEMONIC-ROW     '                          
                                       TO DP013-PARAGRAPH                       
                 MOVE SQLCA            TO DP013-SQLCA                           
                 MOVE 'UPDATE (SOFT DELETE) FAILED FOR:'                        
                                       TO DP013-DB2-TABLE-NAME (1)              
                 MOVE TMNEMONIC-MNEMONIC-CODE                                   
                                       TO DP013-DB2-TABLE-NAME (2)              
                 PERFORM DP013-0000-PROCESS-ABEND                               
             END-IF.                                                            
       EJECT                                                                    
      **************************************                                    
      * COPY DP002-0000-FORMAT-TITLE-LINES *                                    
      **************************************                                    
           COPY DPPD022.                                                        
       EJECT                                                                    
      **********************************                                        
      * COPY DP003-0000-RETURN-TO-CICS *                                        
      **********************************                                        
           COPY DPPD003.                                                        
       EJECT                                                                    
      ************************************                                      
      * COPY DP005-0000-FORMAT-MSG-LINES *                                      
      ************************************                                      
           COPY DPPD005.                                                        
       EJECT                                                                    
      *****************************                                             
      * COPY DP007-0000-GET-PANEL *                                             
      *****************************                                             
           COPY DPPD007.                                                        
       EJECT                                                                    
      *******************************                                           
      * COPY DP008-0000-WRITE-PANEL *                                           
      *******************************                                           
           COPY DPPD008.                                                        
       EJECT                                                                    
      *********************************                                         
      * COPY DP009-0000-LOAD-FK-LINES *                                         
      *********************************                                         
           COPY DPPD009.                                                        
       EJECT                                                                    
      *********************************                                         
      * COPY DP013-0000-PROCESS-ABEND *                                         
      *********************************                                         
           COPY DPPD013.                                                        
       EJECT                                                                    
      **********************************                                        
      * COPY DP015-0000-CHECK-COMMAREA *                                        
      **********************************                                        
           COPY DPPD015.                                                        
       EJECT                                                                    
      **************************************                                    
      * COPY DP017-0000-GET-CURR-DATE-TIME *                                    
      **************************************                                    
           COPY DPPD017.                                                        
       EJECT                                                                    
      *********************************                                         
      * COPY DP018-0000-GET-CURR-TIME *                                         
      *********************************                                         
           COPY DPPD018.                                                        
