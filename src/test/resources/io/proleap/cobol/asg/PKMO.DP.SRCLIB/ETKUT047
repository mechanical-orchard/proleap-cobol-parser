00001  IDENTIFICATION DIVISION.                                         07/13/95
00002  PROGRAM-ID.    ETKUT047.                                         ETKUT047
00003  AUTHOR.        STEVE FLUNKER.                                       LV003
00004  INSTALLATION.  KOHLS DEPARTMENT STORES.                          ETKUT047
00005  DATE-WRITTEN.  APRIL-95.                                         ETKUT047
00006  DATE-COMPILED.                                                   ETKUT047
00007 ******************************************************************ETKUT047
00008 *     ETKUT047 - EXTRACT EDI 855 POA'S TO BE REVALIDATED          ETKUT047
00009 *                                                                 ETKUT047
00010 *     THIS PROGRAM WILL EXTRACT ALL 855 POA'S THAT ARE FLAGGED    ETKUT047
00011 *     NOT TO BE RELEASED AND WRITE THE DATA FOR THESE DOCUMENTS   ETKUT047
00012 *     TO A FLAT FILE. THIS FILE WILL THEN BE FED IN TO THE        ETKUT047
00013 *     REVALIDATION PROGRAM TO EDIT ANY CHANGES MADE ONLINE.       ETKUT047
00014 ******************************************************************ETKUT047
00015 *                                                                 ETKUT047
00016  ENVIRONMENT DIVISION.                                            ETKUT047
00017 *                                                                 ETKUT047
00018  CONFIGURATION SECTION.                                           ETKUT047
00019 *                                                                 ETKUT047
00020  SOURCE-COMPUTER.        IBM-3090.                                ETKUT047
00021  OBJECT-COMPUTER.        IBM-3090.                                ETKUT047
00022 *                                                                 ETKUT047
00023 *                                                                 ETKUT047
00024  INPUT-OUTPUT SECTION.                                            ETKUT047
00025  FILE-CONTROL.                                                    ETKUT047
00026 *                                                                 ETKUT047
00027      SELECT POA-OUTPUT-FILE                                       ETKUT047
00028          ASSIGN TO OUT01.                                         ETKUT047
00029 *                                                                 ETKUT047
00030 ******************************************************************ETKUT047
00031  DATA DIVISION.                                                   ETKUT047
00032 *                                                                 ETKUT047
00033 *                                                                 ETKUT047
00034  FILE SECTION.                                                    ETKUT047
00035                                                                   ETKUT047
00036 *                                                                 ETKUT047
00037  FD  POA-OUTPUT-FILE                                              ETKUT047
00038      RECORDING MODE IS F                                          ETKUT047
00039      BLOCK CONTAINS 0 RECORDS.                                    ETKUT047
00040  01  POA-OUTPUT-RECORD               PIC  X(384).                 ETKUT047
00041 *                                                                 ETKUT047
00042                                                                   ETKUT047
00043 ******************************************************************ETKUT047
00044  WORKING-STORAGE SECTION.                                         ETKUT047
00045 ******************************************************************ETKUT047
00046 *                                                                 ETKUT047
00047 *                                                                 ETKUT047
00048  01  ABEND-CODE                      PIC S9(04) VALUE ZERO        ETKUT047
00049                                                 COMP SYNC.        ETKUT047
00050 *                                                                 ETKUT047
00051  01  PV-PROGRAM-VARIABLES.                                        ETKUT047
00052      05  PV-OUT-DATA-AREA.                                        ETKUT047
00053          10  FILLER                  PIC S9(04) COMP.             ETKUT047
00054          10  PV-OUT-DATA             PIC X(350).                  ETKUT047
00055 *                                                                 ETKUT047
00056  01  WS-OUTPUT-RECORD.                                            ETKUT047
00057      05  WS-OUT-TP-DKEY              PIC S9(09) COMP.             ETKUT047
00058      05  WS-OUT-DKEY-TMST            PIC X(26).                   ETKUT047
00059      05  WS-OUT-SEQ-NBR              PIC S9(09) COMP.             ETKUT047
00060      05  WS-OUT-DATA                 PIC X(350).                  ETKUT047
00061                                                                   ETKUT047
00062 *  DOCUMENTS ON HOLD TABLE                                        ETKUT047
00063      EXEC SQL                                                     ETKUT047
00064          INCLUDE TFGTRHL                                          ETKUT047
00065      END-EXEC.                                                    ETKUT047
00066 *                                                                 ETKUT047
00067 *  DOCUMENT ON HOLD DETAIL DATA TABLE                             ETKUT047
00068      EXEC SQL                                                     ETKUT047
00069          INCLUDE TFGTRDT                                          ETKUT047
00070      END-EXEC.                                                    ETKUT047
00071 *                                                                 ETKUT047
00072 *  DB2 COMMUNICATION AREA                                         ETKUT047
00073 *                                                                 ETKUT047
00074      EXEC SQL                                                     ETKUT047
00075         INCLUDE SQLCA                                             ETKUT047
00076      END-EXEC.                                                    ETKUT047
00077 *                                                                 ETKUT047
00078 *  DB2 COMMUNICATION AREA 2                                       ETKUT047
00079 *                                                                 ETKUT047
00080      COPY SQLCA2.                                                 ETKUT047
00081 *                                                                 ETKUT047
00082 *  DB2 DOCUMENT CURSOR                                            ETKUT047
00083 *  SELECTS DOCUMENTS AND THE DATA THAT IS TO BE RELEASED          ETKUT047
00084 *                                                                 ETKUT047
00085      EXEC SQL                                                     ETKUT047
00086         DECLARE DOC_CURSOR CURSOR FOR                             ETKUT047
00087           SELECT A.TP_DKEY,                                       ETKUT047
00088                  A.TP_DKEY_TMST,                                  ETKUT047
00089                  A.TRN_SET_CDE,                                   ETKUT047
00090                  B.TRN_SET_SEQ_NBR,                               ETKUT047
00091                  B.TRN_SET_DATA                                   ETKUT047
00092             FROM TFGTRHL A, TFGTRDT B                             ETKUT047
00093             WHERE A.RLSE_IND     = 'N'            AND             ETKUT047
00094                   B.TP_DKEY      = A.TP_DKEY      AND             ETKUT047
00095                   B.TP_DKEY_TMST = A.TP_DKEY_TMST AND             ETKUT047
00096                   A.TRN_SET_CDE  = '855'                          ETKUT047
00097             ORDER BY A.TP_DKEY,                                   ETKUT047
00098                      A.TP_DKEY_TMST,                              ETKUT047
00099                      B.TRN_SET_SEQ_NBR                            ETKUT047
00100      END-EXEC.                                                    ETKUT047
00101                                                                   ETKUT047
00102 *                                                                 ETKUT047
00103 *                                                                 ETKUT047
00104  PROCEDURE DIVISION.                                              ETKUT047
00105                                                                   ETKUT047
00106 *                                                                 ETKUT047
00107  A100-MAIN-MODULE.                                                ETKUT047
00108 ******************************************************************ETKUT047
00109 *    OPEN CURSOR TO FIND ALL DOCUMENTS TO BE REVALIDATED.         ETKUT047
00110 *    DO SOME HOUSEKEEPING.                                        ETKUT047
00111 *    PROCESS ALL EXTRACTED DOCUMENTS AND WRITE THEM TO            ETKUT047
00112 *    OUTPUT FILE.                                                 ETKUT047
00113 *    CLOSE FILE AND CURSOR.                                       ETKUT047
00114 ******************************************************************ETKUT047
00115                                                                   ETKUT047
00116      PERFORM Z000-OPEN-CURSOR.                                    ETKUT047
00117                                                                   ETKUT047
00118      PERFORM B100-INITIALIZE-DUTIES.                              ETKUT047
00119                                                                   ETKUT047
00120      PERFORM B200-PROCESS-DOCUMENT                                ETKUT047
00121          UNTIL SQLCODE = +100                                     ETKUT047
00122                                                                   ETKUT047
00123      PERFORM Z200-CLOSE-FILE.                                     ETKUT047
00124      PERFORM Z300-CLOSE-CURSOR.                                   ETKUT047
00125                                                                   ETKUT047
00126      STOP RUN.                                                    ETKUT047
00127                                                                   ETKUT047
00128  B100-INITIALIZE-DUTIES.                                          ETKUT047
00129                                                                   ETKUT047
00130 ******************************************************************ETKUT047
00131 *    OPEN OUTPUT FILE                                             ETKUT047
00132 *    FETCH FIRST DOCUMENT TO BE REVALIDATED                       ETKUT047
00133 *                                                                 ETKUT047
00134 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUT047
00135 ******************************************************************ETKUT047
00136                                                                   ETKUT047
00137      OPEN OUTPUT POA-OUTPUT-FILE.                                    CL**2
00138                                                                   ETKUT047
00139      PERFORM Z100-FETCH-DOC-CSR.                                  ETKUT047
00140                                                                   ETKUT047
00141  B200-PROCESS-DOCUMENT.                                           ETKUT047
00142                                                                   ETKUT047
00143 ******************************************************************ETKUT047
00144 *    DETERMINE WHICH EDI DOCUMENT IT IS AND WRITE THE DATA FOR    ETKUT047
00145 *    THAT DOCUMENT TO THE APPROPRIATE OUTPUT FILE.                ETKUT047
00146 *                                                                 ETKUT047
00147 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUT047
00148 ******************************************************************ETKUT047
00149                                                                   ETKUT047
00150      MOVE FGTRHL-TP-DKEY            TO  WS-OUT-TP-DKEY.           ETKUT047
00151      MOVE FGTRHL-TP-DKEY-TMST       TO  WS-OUT-DKEY-TMST.         ETKUT047
00152      MOVE FGTRDT-TRN-SET-SEQ-NBR    TO  WS-OUT-SEQ-NBR.           ETKUT047
00153      MOVE FGTRDT-TRN-SET-DATA       TO  PV-OUT-DATA-AREA.         ETKUT047
00154      MOVE PV-OUT-DATA               TO  WS-OUT-DATA.              ETKUT047
00155                                                                   ETKUT047
00156      PERFORM C100-WRITE-POA.                                         CL**3
00157                                                                   ETKUT047
00158      PERFORM Z100-FETCH-DOC-CSR.                                  ETKUT047
00159                                                                   ETKUT047
00160  C100-WRITE-POA.                                                     CL**3
00161                                                                   ETKUT047
00162 ******************************************************************ETKUT047
00163 *    WRITE A RECORD TO THE 855 POA OUTPUT FILE                       CL**3
00164 *                                                                 ETKUT047
00165 *    PERFORMED BY B200-PROCESS-DOCUMENT.                          ETKUT047
00166 ******************************************************************ETKUT047
00167                                                                   ETKUT047
00168      WRITE POA-OUTPUT-RECORD                                         CL**2
00169          FROM WS-OUTPUT-RECORD.                                   ETKUT047
00170                                                                   ETKUT047
00171  Z000-OPEN-CURSOR.                                                ETKUT047
00172                                                                   ETKUT047
00173 ******************************************************************ETKUT047
00174 *    OPEN THE DOCUMENT CURSOR                                     ETKUT047
00175 *                                                                 ETKUT047
00176 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUT047
00177 ******************************************************************ETKUT047
00178                                                                   ETKUT047
00179      EXEC SQL                                                     ETKUT047
00180          OPEN DOC_CURSOR                                          ETKUT047
00181      END-EXEC.                                                    ETKUT047
00182                                                                   ETKUT047
00183      IF SQLCODE < 0                                               ETKUT047
00184          DISPLAY '** ABEND **'                                    ETKUT047
00185          DISPLAY '** OPEN CSR ERROR FOR TFGTRHL/TFGTRDT TABLE **' ETKUT047
00186          PERFORM Z900-SQL-ERROR                                   ETKUT047
00187      ELSE                                                         ETKUT047
00188          IF SQLCODE > +100                                        ETKUT047
00189              DISPLAY '** ABEND **'                                ETKUT047
00190              DISPLAY '** OPEN CSR WARNING TFGTRHL/TFGTRDT TABLE'  ETKUT047
00191              PERFORM Z900-SQL-WARNING                             ETKUT047
00192          END-IF                                                   ETKUT047
00193      END-IF.                                                      ETKUT047
00194                                                                   ETKUT047
00195  Z100-FETCH-DOC-CSR.                                              ETKUT047
00196                                                                   ETKUT047
00197 ******************************************************************ETKUT047
00198 *    FETCH THE DOCUMENTS USING THE DOC CURSOR                     ETKUT047
00199 *                                                                 ETKUT047
00200 *    PERFORMED BY B100-INITIALIZE-DUTIES.                         ETKUT047
00201 *    PERFORMED BY B200-PROCESS-DOCUMENT.                          ETKUT047
00202 ******************************************************************ETKUT047
00203                                                                   ETKUT047
00204      EXEC SQL                                                     ETKUT047
00205          FETCH DOC_CURSOR                                         ETKUT047
00206          INTO  :FGTRHL-TP-DKEY,                                   ETKUT047
00207                :FGTRHL-TP-DKEY-TMST,                              ETKUT047
00208                :FGTRHL-TRN-SET-CDE,                               ETKUT047
00209                :FGTRDT-TRN-SET-SEQ-NBR,                           ETKUT047
00210                :FGTRDT-TRN-SET-DATA                               ETKUT047
00211      END-EXEC.                                                    ETKUT047
00212                                                                   ETKUT047
00213      IF SQLCODE < 0                                               ETKUT047
00214          DISPLAY '** ABEND **'                                    ETKUT047
00215          DISPLAY '** FETCH ERROR ON TFGTRHL/TFGTRDT TABLE'        ETKUT047
00216          PERFORM Z900-SQL-ERROR                                   ETKUT047
00217      ELSE                                                         ETKUT047
00218          IF SQLCODE > +100                                        ETKUT047
00219              DISPLAY '** ABEND **'                                ETKUT047
00220              DISPLAY '** FETCH WARNING ON TFGTRHL/TFGTRDT TABLE'  ETKUT047
00221              PERFORM Z900-SQL-WARNING                             ETKUT047
00222          END-IF                                                   ETKUT047
00223      END-IF.                                                      ETKUT047
00224                                                                   ETKUT047
00225  Z200-CLOSE-FILE.                                                 ETKUT047
00226                                                                   ETKUT047
00227 ******************************************************************ETKUT047
00228 *    CLOSE THE OUTPUT FILE                                        ETKUT047
00229 *                                                                 ETKUT047
00230 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUT047
00231 ******************************************************************ETKUT047
00232                                                                   ETKUT047
00233      CLOSE POA-OUTPUT-FILE.                                          CL**2
00234                                                                   ETKUT047
00235  Z300-CLOSE-CURSOR.                                               ETKUT047
00236                                                                   ETKUT047
00237 ******************************************************************ETKUT047
00238 *    OPEN THE DOCUMENT CURSOR                                     ETKUT047
00239 *                                                                 ETKUT047
00240 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUT047
00241 ******************************************************************ETKUT047
00242                                                                   ETKUT047
00243      EXEC SQL                                                     ETKUT047
00244          CLOSE DOC_CURSOR                                         ETKUT047
00245      END-EXEC.                                                    ETKUT047
00246                                                                   ETKUT047
00247      IF SQLCODE < 0                                               ETKUT047
00248          DISPLAY '** ABEND **'                                    ETKUT047
00249          DISPLAY '** CLOSE CSR ERROR FOR TFGTRHL/TFGTRDT TABLES'  ETKUT047
00250          PERFORM Z900-SQL-ERROR                                   ETKUT047
00251      ELSE                                                         ETKUT047
00252          IF SQLCODE > +100                                        ETKUT047
00253              DISPLAY '** ABEND **'                                ETKUT047
00254              DISPLAY '** CLOSE CSR WARNING TFGTRHL/TFGTRDT TABLES'ETKUT047
00255              PERFORM Z900-SQL-WARNING                             ETKUT047
00256          END-IF                                                   ETKUT047
00257      END-IF.                                                      ETKUT047
00258                                                                   ETKUT047
00259  Z900-SQL-WARNING.                                                ETKUT047
00260                                                                   ETKUT047
00261 ******************************************************************ETKUT047
00262 *     SQL WARNING PROCESSING.                                    *ETKUT047
00263 ******************************************************************ETKUT047
00264                                                                   ETKUT047
00265      MOVE SQLERRD(3) TO SQLERRD3.                                 ETKUT047
00266      MOVE SQLCODE    TO SQLCODE2.                                 ETKUT047
00267      DISPLAY '** ABEND **       ** = SQLWARNING'.                 ETKUT047
00268      DISPLAY '** PROGRAM        ** = ETKUT047'.                   ETKUT047
00269      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  ETKUT047
00270      DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  ETKUT047
00271      DISPLAY '** SQLWARN0       ** = ' SQLWARN0.                  ETKUT047
00272      DISPLAY '** SQLWARN1       ** = ' SQLWARN1.                  ETKUT047
00273      DISPLAY '** SQLWARN2       ** = ' SQLWARN2.                  ETKUT047
00274      DISPLAY '** SQLWARN3       ** = ' SQLWARN3.                  ETKUT047
00275      DISPLAY '** SQLWARN4       ** = ' SQLWARN4.                  ETKUT047
00276      DISPLAY '** SQLWARN5       ** = ' SQLWARN5.                  ETKUT047
00277      DISPLAY '** SQLWARN6       ** = ' SQLWARN6.                  ETKUT047
00278      DISPLAY '** SQLWARN7       ** = ' SQLWARN7.                  ETKUT047
00279                                                                   ETKUT047
00280      MOVE +4013 TO ABEND-CODE.                                    ETKUT047
00281      CALL 'ILBOABN0' USING ABEND-CODE.                            ETKUT047
00282                                                                   ETKUT047
00283                                                                   ETKUT047
00284  Z900-SQL-ERROR.                                                  ETKUT047
00285                                                                   ETKUT047
00286 ******************************************************************ETKUT047
00287 *     SQL ERROR PROCESSING.                                       ETKUT047
00288 ******************************************************************ETKUT047
00289                                                                   ETKUT047
00290      MOVE SQLCODE    TO SQLCODE2.                                 ETKUT047
00291      MOVE SQLERRD(3) TO SQLERRD3.                                 ETKUT047
00292      DISPLAY '** ABEND **       ** = SQLERROR'.                   ETKUT047
00293      DISPLAY '** PROGRAM        ** = ETKUT047'.                   ETKUT047
00294      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  ETKUT047
00295      DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  ETKUT047
00296      DISPLAY '** SQLERRM        ** = ' SQLERRM.                   ETKUT047
00297      DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  ETKUT047
00298                                                                   ETKUT047
00299      MOVE +4013 TO ABEND-CODE.                                    ETKUT047
00300      CALL 'ILBOABN0' USING ABEND-CODE.                            ETKUT047
00301                                                                   ETKUT047
