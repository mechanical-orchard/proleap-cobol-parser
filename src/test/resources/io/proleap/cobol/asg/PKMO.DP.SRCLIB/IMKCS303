      ******************************************************************IMKUP006
       IDENTIFICATION DIVISION.                                         09/16/94
      ******************************************************************IMKUP006
                                                                           LV020
       PROGRAM-ID.             IMKCS303.                                   CL**1
       AUTHOR.                 PAUL KEBER.                                 CL**1
       INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL**1
       DATE-WRITTEN.           02/27/2002.                                 CL**1
       DATE-COMPILED.                                                      CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      *  IMKCS303 - I301 ITEM MESSAGE BRIDGE RUN STATUS CHECKER            CL**1
      ******************************************************************   CL**1
      *          CICS COBOL II                                          00110072
      ******************************************************************   CL**1
      * THIS IS A CICS PROGRAM WHICH WILL CHECK TO SEE IF THE ITEM         CL**1
      * MESSAGE BRIDGE TO MQSERIES QUEUE DUMP TRANSACTION (I301) IS        CL**1
      * RUNNING IN THE CICS REGION.  IF IT IS NOT, THE PROGRAM WILL        CL**1
      * DELAY FOR 15 MINUTES AND THEN CHECK AGAIN.  IF I301 IS STILL       CL**1
      * NOT RUNNING, AN OPERATOR CONSOLE MESSAGE IS WRITTEN (IF CICS       CL**1
      * PRODUCTION) TO INFORM THE OPERATORS.                                    
      *                                                               *         
      * CHANGE LOG:                                                   *         
      *                                                               *         
      * 07/06/22    JEAN KURK                       CHG0271667        *         
      *             REMOVE INPUT-OUTPUT, FILE-CONTROL, FILE IN ORDER  *         
      *             TO RECOMPILE FOR UPCOMING COBOL UPGRADE           *         
      ******************************************************************   CL**1
                                                                        00180000
      ******************************************************************00190000
       ENVIRONMENT DIVISION.                                            00200000
      ******************************************************************00210000
                                                                        00220000
       CONFIGURATION SECTION.                                           00230000
                                                                        00240000
       SOURCE-COMPUTER.        IBM-3090.                                00250000
       OBJECT-COMPUTER.        IMB-3090.                                00260000
                                                                        00270000
      ******************************************************************00320000
       DATA DIVISION.                                                   00330000
      ******************************************************************00340000
                                                                        00350000
                                                                        00380017
       WORKING-STORAGE SECTION.                                         00390000
                                                                        00460000
       01  PC-PROGRAM-CONSTANTS.                                        00930000
           05  PC-RETRY-DELAY-MINUTES  PIC S9(9)    VALUE +15  COMP.    01030069
           05  PC-MAX-CICS-TASKS       PIC S9(9)    VALUE +500 COMP.    01510000
           05  PC-BRIDGE-DOWN-MSG-LEN  PIC S9(8)    VALUE +122 COMP.            
           05  PC-CURRENT-PROGRAM-NAME PIC X(8)     VALUE 'IMKCS303'.   00940073
           05  PC-VSM-BRIDGE-DOWN-MSG  PIC X(122)   VALUE               00940073
               'OPER034 - VSM BRIDGE TO ITEM HUB TRANSACTION (I301) IS N        
      -        'OT RUNNING - CONTACT APPL IM GROUP RESPONSIBLE FOR CICS 01120074
      -        'TRANS I301'.                                            01120074
           05  PC-MAX-TASKS-ERROR-MSG  PIC X(44)    VALUE               00940073
               'GREATER THAN 500 CICS TASKS - TABLE OVERFLOW'.                  
           05  PC-SYS-ID               PIC X(04).                       01200073
               88  PC-KOHLS-TEST-SYSTEM             VALUE 'CIT1'.       01210073
               88  PC-KOHLS-ACC-TEST-SYSTEM         VALUE 'CIQA',       01220073
                                                          'CIQT',       01230073
                                                          'CILA',       01240073
                                                          'CILT'.       01250073
               88  PC-KOHLS-QA-SYSTEM               VALUE 'CIQA',       01260073
                                                          'CIQT'.       01270073
               88  PC-KOHLS-LEARNING-SYSTEM         VALUE 'CILA',       01280073
                                                          'CILT'.       01290073
               88  PC-KOHLS-PROD-SYSTEM             VALUE 'CIPS',       01300073
                                                          'CIP1',       01310073
                                                          'CIP3',       01320073
                                                          'CIP4',       01330073
                                                          'CIP6'.       01340073
                                                                        01350000
       01  PS-PROGRAM-SWITCHES.                                         00800000
           05  PS-I301-FOUND-SW          PIC X(01)  VALUE 'N'.          00810059
               88  PS-I301-FOUND                    VALUE 'Y'.          00820059
               88  PS-I301-NOT-FOUND                VALUE 'N'.          00820059
                                                                        01350000
       01  PV-PROGRAM-VARIABLES.                                        01360000
           05  PV-TASKS-LIST-ADDR        PIC S9(9)  VALUE +0 COMP.      01510000
           05  PV-TRANS-LIST-ADDR        PIC S9(9)  VALUE +0 COMP.      01510000
           05  PV-NR-CICS-TASKS          PIC S9(9)  VALUE +0 COMP.      01510000
           05  PV-SUB                    PIC S9(4)  VALUE +0 COMP.              
           05  PV-CICS-RESPONSE          PIC S9(4)  VALUE +0 COMP.              
           05  PV-CURRENT-SYSID          PIC X(4)   VALUE SPACES.       01370073
           05  PV-PARAGRAPH              PIC X(48)  VALUE SPACES.       01420000
                                                                        01840000
       01  PV-DT-DATE-TIME-AREA.                                                
           05  PV-DT-CURRENT-ABSTIME     PIC S9(15) VALUE +0 COMP-3.            
           05  PV-DT-FORMAT-DATE-MMDDYY  PIC X(8)   VALUE SPACES.               
           05  PV-DT-CURRENT-TIME        PIC X(8)   VALUE SPACES.               
                                                                                
       01  PV-LOG-VSM-BRIDGE-DOWN-MSG.                                          
           05  FILLER                    PIC X(56)  VALUE                       
             'VSM BRIDGE TO ITEM HUB TRANSACTION (I301) IS NOT RUNNING'.        
                                                                                
       01  PV-SL-SYSTEM-LOG-MSG-AREA.                                           
           05  PV-SL-SYS-LOG-QUEUE-NAME  PIC X(4)   VALUE 'CSSL'.               
           05  PV-SL-SYS-LOG-MSG-LENGTH  PIC S9(4)  VALUE +128                  
                                                    COMP SYNC.                  
           05  PV-SL-SYSTEM-LOG-MESSAGE.                                        
               10  PV-SL-MESSAGE-DATE    PIC X(8)   VALUE SPACES.               
               10  FILLER                PIC X(1)   VALUE SPACE.                
               10  PV-SL-MESSAGE-TIME    PIC X(8)   VALUE SPACES.               
               10  FILLER                PIC X(1)   VALUE SPACE.                
               10  PV-SL-PROGRAM-ID      PIC X(8)   VALUE SPACES.               
               10  FILLER                PIC X(1)   VALUE SPACE.                
               10  PV-SL-SYS-LOG-MESSAGE-DTL                                    
                                         PIC X(101) VALUE SPACES.               
                                                                        00480000
      *****************************************************************         
      *    ABEND PROCESSING COPYBOOK.                                           
      *****************************************************************         
           COPY DPWS013.                                                        
                                                                        00750011
                                                                        01860054
       LINKAGE SECTION.                                                 01850054
                                                                        01860054
       01 DFHCOMMAREA                 PIC X(01).                        01870073
                                                                        01860054
       01 PV-CICS-TRANS-LIST.                                           01870073
           05 PV-CICS-TL-TRANS  OCCURS 500 TIMES  PIC X(4).                  018
                                                                        01890054
      ******************************************************************01900000
       PROCEDURE DIVISION.                                              01910073
      ******************************************************************01920000
                                                                        01930000
       0000-MAINLINE.                                                   01940000
                                                                        01950000
           PERFORM 1000-ITEM-BRIDGE-CHECK.                              01960054
                                                                        01950000
           IF PS-I301-NOT-FOUND                                         01950000
               EXEC CICS                                                        
                   DELAY FOR MINUTES(PC-RETRY-DELAY-MINUTES)                    
               END-EXEC                                                         
               PERFORM 1000-ITEM-BRIDGE-CHECK                           01960054
           END-IF.                                                              
                                                                        01950000
           IF PS-I301-NOT-FOUND                                         01950000
               PERFORM 2000-ITEM-BRIDGE-DOWN                                0196
           END-IF.                                                              
                                                                        02010074
           PERFORM 9990-EXEC-CICS-RETURN.                               02020074
                                                                        02050085
      ******************************************************************06290074
      *  CHECK IF THE VSM TO RMS ITEM BRIDGE TRANS (I301) IS NOT RUNNING02090074
      ******************************************************************06290074
       1000-ITEM-BRIDGE-CHECK.                                          02070074
                                                                        02110074
           EXEC CICS                                                    02120074
               INQUIRE TASK LIST                                        02130074
                            LISTSIZE(PV-NR-CICS-TASKS)                  02130074
                            SET(PV-TASKS-LIST-ADDR)                     02130074
                            SETTRANSID(PV-TRANS-LIST-ADDR)              02130074
           END-EXEC.                                                    02140074
                                                                        02150074
           EXEC CICS                                                    02120074
               ADDRESS SET(ADDRESS OF PV-CICS-TRANS-LIST)               02130074
                       USING(PV-TRANS-LIST-ADDR)                        02130074
           END-EXEC.                                                    02140074
                                                                        02150074
           IF PV-NR-CICS-TASKS GREATER THAN PC-MAX-CICS-TASKS           02180074
               MOVE '1000-ITEM-BRIDGE-CHECK' TO PV-PARAGRAPH            02510074
               PERFORM 9900-PROGRAM-ABEND                               06490074
           END-IF.                                                              
                                                                        02150074
           PERFORM 1100-CICS-TRANS-CHECK                                        
               VARYING PV-SUB                                           02150074
               FROM 1 BY 1                                              02150074
                  UNTIL PV-SUB GREATER THAN PV-NR-CICS-TASKS            02150074
                     OR PS-I301-FOUND.                                  02150074
                                                                        02050085
      ******************************************************************06290074
      *  CHECK IF THE VSM TO RMS ITEM BRIDGE TRANS (I301) IS RUNNING    02090074
      ******************************************************************06290074
       1100-CICS-TRANS-CHECK.                                           02070074
                                                                        02110074
           IF PV-CICS-TL-TRANS (PV-SUB) EQUAL 'I301'                    02180074
               SET PS-I301-FOUND TO TRUE                                06490074
           END-IF.                                                              
                                                                        02150074
      ******************************************************************06290074
      *    WRITE ITEM BRIDGE DOWN MESAGE                                02090074
      ******************************************************************06290074
       2000-ITEM-BRIDGE-DOWN.                                           02070074
                                                                        02110074
           MOVE PV-LOG-VSM-BRIDGE-DOWN-MSG                                      
               TO PV-SL-SYS-LOG-MESSAGE-DTL.                                    
           PERFORM 9000-SYSTEM-LOG-MESSAGE.                                     
                                                                        02110074
           EXEC CICS                                                    02120074
               ASSIGN SYSID (PV-CURRENT-SYSID)                          02130074
           END-EXEC.                                                    02140074
                                                                        02150074
           MOVE PV-CURRENT-SYSID TO PC-SYS-ID.                          02160074
                                                                        02170074
           IF PC-KOHLS-PROD-SYSTEM                                      02180074
               EXEC CICS WRITE OPERATOR                                 06490074
                         TEXT (PC-VSM-BRIDGE-DOWN-MSG)                  06490074
                         TEXTLENGTH (PC-BRIDGE-DOWN-MSG-LEN)            06490074
                         CRITICAL                                       06490074
               END-EXEC                                                 06490074
           END-IF.                                                              
                                                                        02280074
      ******************************************************************        
      *  POST PRE-FORMATTED MESSAGES TO THE CICS SYSTEM LOG.  ENQUEUE  *        
      *  THE LOG TO ENSURE THAT ALL MESSAGES FOR THE SAME ERROR ARE    *        
      *  CONSECUTIVE IN THE LOG (MESSAGES WILL BE POSTED EVEN IF THE   *        
      *  ENQUEUE IS NOT SUCCESSFUL).  POST THE MESSAGES AND DEQUEUE    *        
      *  THE CICS SYSTEM LOG.                                          *        
      ******************************************************************        
       9000-SYSTEM-LOG-MESSAGE.                                                 
                                                                                
      *  FORMAT THE CURRENT DATE AND TIME, USING THE ABSOLUTE TIME.             
           EXEC CICS                                                            
               ASKTIME                                                          
                   ABSTIME (PV-DT-CURRENT-ABSTIME)                              
           END-EXEC.                                                            
                                                                                
           EXEC CICS                                                            
               FORMATTIME                                                       
                   ABSTIME  (PV-DT-CURRENT-ABSTIME)                             
                   MMDDYY   (PV-DT-FORMAT-DATE-MMDDYY)                          
                   DATESEP  ('/')                                               
                   TIME     (PV-DT-CURRENT-TIME)                                
                   TIMESEP  (':')                                               
           END-EXEC.                                                            
                                                                                
           MOVE PV-DT-FORMAT-DATE-MMDDYY TO  PV-SL-MESSAGE-DATE.                
           MOVE PV-DT-CURRENT-TIME       TO  PV-SL-MESSAGE-TIME.                
           MOVE PC-CURRENT-PROGRAM-NAME  TO  PV-SL-PROGRAM-ID.                  
                                                                                
      *    WRITE A MESSAGE TO THE CICS SYSTEM LOG FOR THE CURRENT CICS          
      *    REGION.  ALL EXCEPTIONAL CONDITIONS WILL BE IGNORED.                 
           EXEC CICS                                                            
               WRITEQ TD                                                        
                   QUEUE  (PV-SL-SYS-LOG-QUEUE-NAME)                            
                   FROM   (PV-SL-SYSTEM-LOG-MESSAGE)                            
                   LENGTH (PV-SL-SYS-LOG-MSG-LENGTH)                            
                   RESP   (PV-CICS-RESPONSE)                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************06290074
      * ABEND THE PROGRAM                                               06460074
      ******************************************************************06290074
       9900-PROGRAM-ABEND.                                              06480074
                                                                        06490074
           SET DP013-LINK-RETURN                                                
               DP013-OTHER-ABEND                                                
               DP013-NO-ROLLBACK  TO TRUE.                                      
           MOVE PV-PARAGRAPH      TO DP013-PARAGRAPH.                           
           MOVE PC-MAX-TASKS-ERROR-MSG                                          
                                  TO DP013-MESSAGE-TEXT (1).                    
                                                                        06600074
           PERFORM DP014-0000-PROCESS-ABEND.                                    
           PERFORM 9990-EXEC-CICS-RETURN.                               06600074
                                                                        06490074
       9990-EXEC-CICS-RETURN.                                           06600074
           EXEC CICS                                                    06610085
               RETURN                                                   06620085
           END-EXEC.                                                    06630085
                                                                        06600074
      ******************************************************************06290074
      *    NON-CICS ARCHITECTURE ABEND ROUTINE CALL                             
      ******************************************************************06290074
                                                                                
           COPY DPPD014.                                                        
