      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
       PROGRAM-ID.             PCKUT085.                                        
       AUTHOR.                 V.SUDHAN.                                        
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           NOVEMBER, 2009.                                  
       DATE-COMPILED.                                                           
      *****************************************************************         
      *                                                               *         
      *  THIS PROGRAM WILL UPDATE THE RECORDS WHICH ARE PROCESSED BY  *         
      *  PCKUT055 AND TRIM THE PROCESSED RECORDS WHICH ARE MORE THAN  *         
      *  370 DAYS OLD.  OUTPUT WILL HAVE RECORDS WHICH ARE LESS THAN  *         
      *  370 DAYS OLD AND WHICH ARE PROCESSED BY PCKUT055 AND YET TO  *         
      *  BE PROCESSED.                                                *         
      *                                                               *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT PC-STORES-PURGE-EXT-FILE                                      
                  ASSIGN TO IN01.                                               
      *                                                                         
           SELECT PC-STORES-PURGE-TODAY-EXT                                     
                  ASSIGN TO IN02.                                               
      *                                                                         
           SELECT PC-STORES-PURGE-TRIM-FILE                                     
                  ASSIGN TO OUT01.                                              
      *                                                                         
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  PC-STORES-PURGE-EXT-FILE                                             
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0  RECORDS.                                           
       01  PC-STORES-PURGE-EXT-REC         PIC  X(080).                         
      *                                                                         
       FD  PC-STORES-PURGE-TODAY-EXT                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0  RECORDS.                                           
       01  PC-STORES-PURGE-TODAY-REC       PIC  X(080).                         
      *                                                                         
       FD  PC-STORES-PURGE-TRIM-FILE                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0  RECORDS.                                           
       01  PC-STORES-PURGE-TRIM-REC        PIC  X(080).                         
      *                                                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  ABEND-CODE                      PIC S9(04)  VALUE +0                 
                                                       COMP SYNC.               
      *                                                                         
       01  PROGRAM-SWITCHES.                                                    
           05  PS-EOF-STORE-EXT-SW         PIC  X(01)  VALUE 'N'.               
               88  PS-EOF-STORE-EXT                    VALUE 'Y'.               
               88  PS-NOT-EOF-STORE-EXT                VALUE 'N'.               
      *                                                                         
           05  PS-EOF-STORE-TODAY-EXT-SW   PIC  X(01)  VALUE 'N'.               
               88  PS-EOF-STORE-TODAY-EXT              VALUE 'Y'.               
               88  PS-NOT-EOF-STORE-TODAY-EXT          VALUE 'N'.               
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)  VALUE                    
                                                       'PCKUT085'.              
           05  PC-DAYS-TO-DECREMENT        PIC S9(03)  VALUE +370.              
           05  PC-CURRENT-DATE.                                                 
               10  PC-CURRENT-CC           PIC 9(2)    VALUE ZERO.              
               10  PC-CURRENT-YEAR         PIC 9(2)    VALUE ZERO.              
               10  PC-CURRENT-MONTH        PIC 9(2)    VALUE ZERO.              
               10  PC-CURRENT-DAY          PIC 9(2)    VALUE ZERO.              
                                                                                
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PROCESS-DATE             PIC  X(06).                          
           05  PV-DATE-RETURNED-FROM-ROUTINE                                    
                                           PIC  9(08).                          
           05  PV-READ-COUNTER-1           PIC  9(09)  VALUE ZERO.              
           05  PV-READ-COUNTER-2           PIC  9(09)  VALUE ZERO.              
           05  PV-STORES-SKIP-CNT          PIC  9(09)  VALUE ZERO.              
           05  PV-TOTAL-CNT                PIC  9(09)  VALUE ZERO.              
           05  PV-TABLE-LOAD-CNT           PIC  9(09)  VALUE ZERO.              
           05  PV-TABLE-MAX                PIC  9(5)   VALUE 10000.             
           05  PV-PARAGRAPH-NAME           PIC  X(30)  VALUE SPACES.            
           05  PV-DATE-TRIM                PIC  X(10)  VALUE SPACES.            
           05  PV-DATE.                                                         
               10  PV-DATE-CCYY            PIC  9(04)  VALUE ZEROES.            
               10  PV-DATE-MM              PIC  9(02)  VALUE ZEROES.            
               10  PV-DATE-DD              PIC  9(02)  VALUE ZEROES.            
           05  PV-SUB                      PIC S9(04)  VALUE +0                 
                                                       COMP SYNC.               
      *                                                                         
       01  PV-TABLE-VARIABLES.                                                  
           05  PV-STORES-DATA-TABLE.                                            
               10  PV-TABLE-SIZE           PIC  9(5).                           
               10  PV-STORES-DTL           OCCURS 10000 TIMES                   
                                           DEPENDING ON PV-TABLE-SIZE           
                                           INDEXED BY PV-STORE-IDX.             
                   15  PV-STORE-START-NUM  PIC 9(04).                           
                   15  PV-STORE-END-NUM    PIC 9(04).                           
                   15  PV-PURGE-DATE       PIC X(10).                           
                   15  PV-OFFSET-DATE      PIC 9(04).                           
                   15  PV-PROCESSED-REC-SW PIC X(01).                           
      *                                                                         
           COPY PCRD067.                                                        
      *                                                                         
           COPY DPWS500.                                                        
      *                                                                         
       PROCEDURE DIVISION.                                                      
      *                                                                         
      ******************************************************************        
      **  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                    **        
      ******************************************************************        
      *                                                                         
       0000-MAINLINE-PROCESSING.                                                
      *                                                                         
           PERFORM 1000-INITIALIZATION.                                         
      *                                                                         
           PERFORM 2000-PROCESS-STORES.                                         
      *                                                                         
           PERFORM 3000-END-OF-JOB-ROUTINE.                                     
      *                                                                         
           STOP RUN.                                                            
      *                                                                         
      ******************************************************************        
      ** PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.              **        
      ** 01.ACCEPTS THE CURRENT DATE FROM SYSTEM DATE.                **        
      ** 02.OPENS THE INPUT & OUTPUT FILES.                           **        
      ** 03.READS THE PURGE EXTRACT RECORDS FILE AND LOAD THEM IN TO  **        
      **    INTERNAL TABLE.                                           **        
      ** 04.READS THE PURGE EXTRACT PROCESSED BY PCKUT055 TODAY       **        
      ******************************************************************        
      *                                                                         
       1000-INITIALIZATION.                                                     
      *                                                                         
           DISPLAY '** PROGRAM PCKUT085 EXECUTION STARTED **'.                  
      *                                                                         
           ACCEPT PV-PROCESS-DATE FROM DATE.                                    
            MOVE PV-PROCESS-DATE(1:2) TO PC-CURRENT-YEAR.                       
            MOVE PV-PROCESS-DATE(3:2) TO PC-CURRENT-MONTH.                      
            MOVE PV-PROCESS-DATE(5:2) TO PC-CURRENT-DAY.                        
            IF PC-CURRENT-YEAR < 99                                             
              MOVE 20 TO PC-CURRENT-CC                                          
            ELSE                                                                
              MOVE 21 TO PC-CURRENT-CC                                          
            END-IF                                                              
            DISPLAY '** PROGRAM PCKUT085 PROCESSING DATE : **'                  
                   PC-CURRENT-DATE.                                             
            PERFORM 1100-CALCULATE-MINUS-370DAYS.                               
            DISPLAY '** PROCESSING DATE - 370 DAYS IS    : **'                  
                   PV-DATE-RETURNED-FROM-ROUTINE.                               
      *                                                                         
            OPEN INPUT  PC-STORES-PURGE-EXT-FILE                                
                        PC-STORES-PURGE-TODAY-EXT                               
                 OUTPUT PC-STORES-PURGE-TRIM-FILE.                              
      *                                                                         
            PERFORM 1200-LOAD-INTERNAL-TABLE.                                   
            PERFORM 1300-READ-EXT-TODAY-FILE.                                   
            PERFORM 1400-UPDATE-PROCESS-REC-SW.                                 
      *                                                                         
      ******************************************************************        
      **     THIS PARAGRAPH CALLS THE KOHL'S DATE ROUTINE AND SUBTRACT**        
      **  370 DAYS TO THE CURRENT DATE WHICH WAS READ FROM THE SYSTEM **        
      **  DATE.                                                       **        
      ******************************************************************        
      *                                                                         
       1100-CALCULATE-MINUS-370DAYS.                                            
      *                                                                         
           INITIALIZE DPG51 DPG52 DPG53                                         
                      DPG54 DPG55 DPG56.                                        
                                                                                
           SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                              
           SET DPG51-DECREMENT-DATE       TO TRUE.                              
           MOVE PC-DAYS-TO-DECREMENT      TO DPG51-INCR-DECR-DAYS-9.            
           MOVE ZEROES                    TO DPG51-INCR-DECR-BUS-DAYS-9.        
                                                                                
           MOVE PC-CURRENT-DATE           TO DPG52-LK-DATE-INPUT.               
           SET DPG52-LK-DTE-ISO           TO TRUE.                              
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
             USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
               WHEN DPG54-DATE-INVALID                                          
                   DISPLAY ' '                                                  
                   DISPLAY '*********** ABEND *************************'        
                   DISPLAY '** PROGRAM   **  = ' PC-CURRENT-PROGRAM-NAME        
                   DISPLAY '** PARAGRAPH **  = 2000-PROCESS-STORES **'          
                   DISPLAY '** BAD CALL TO CALENDAR MODULE '                    
                   DISPLAY '** ADDING 370 DAYS TO PROCESS DATE    '             
                   DISPLAY '** ' DPG54-ERROR-MESSAGE  PC-CURRENT-DATE           
                   DISPLAY '*******************************************'        
                   DISPLAY ' '                                                  
                   PERFORM 3000-END-OF-JOB-ROUTINE                              
                   MOVE +4019 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
           END-EVALUATE.                                                        
      *                                                                         
           MOVE DPG55-DB2-ISO-DATE     TO PV-DATE-RETURNED-FROM-ROUTINE.        
      *                                                                         
      ******************************************************************        
      ** THI PARAGRAPH READS THE STORES DATA PURGE EXTRACT FILE AND   **        
      ** AND LOAD THEM IN TO AN INTERNAL TABLE. THE INTERNAL TABLE    **        
      ** LIMIT IS 10000. IF IT GOES BEYOND PROGRAM WILL ABEND.        **        
      ******************************************************************        
      *                                                                         
       1200-LOAD-INTERNAL-TABLE.                                                
      *                                                                         
           SET PS-NOT-EOF-STORE-EXT TO TRUE.                                    
           MOVE ZEROES TO PV-STORES-DATA-TABLE                                  
           MOVE ZEROES TO PV-TABLE-SIZE                                         
      *                                                                         
           PERFORM 8000-READ-EXTRACT-FILE VARYING PV-STORE-IDX                  
               FROM 1 BY 1                                                      
                   UNTIL PV-STORE-IDX > PV-TABLE-MAX    OR                      
                         PS-EOF-STORE-EXT.                                      
               IF PV-STORE-IDX > PV-TABLE-MAX                                   
                  MOVE '1200-LOAD-INTERNAL-TABLE'                               
                            TO PV-PARAGRAPH-NAME                                
                  DISPLAY '  '                                                  
                  DISPLAY '*********** ABEND ************************'          
                  DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME          
                  DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME                
                  DISPLAY '** TABLE OVERFLOW ON PV-STORES-DTL        '          
                  DISPLAY '** INCREASE PV-STORES-DTL  MAX LIMIT      '          
                  DISPLAY '** CURRENT LIMIT IS ** ' PV-TABLE-MAX                
                  DISPLAY '******************************************'          
                  DISPLAY '  '                                                  
                  MOVE +4004 TO ABEND-CODE                                      
                  CALL 'ILBOABN0' USING ABEND-CODE                              
               END-IF.                                                          
      *                                                                         
      ******************************************************************        
      ** THIS PARAGRAPGH READS THE INPUT STORES DATA PURGE EXTRACT'S  **        
      ** TODAY'S FILE IN PCRD067 LAYOUT.                              **        
      ******************************************************************        
      *                                                                         
       1300-READ-EXT-TODAY-FILE.                                                
      *                                                                         
           INITIALIZE PC067-STORE-DATA-PURGE-EXTRACT.                           
      *                                                                         
           READ PC-STORES-PURGE-TODAY-EXT INTO                                  
                PC067-STORE-DATA-PURGE-EXTRACT                                  
                AT END                                                          
                 SET PS-EOF-STORE-TODAY-EXT TO TRUE                             
              NOT AT END                                                        
                 ADD +1 TO PV-READ-COUNTER-2                                    
           END-READ.                                                            
      *                                                                         
      ******************************************************************        
      **  THIS PARAGRAPH UPDATE THE PROCESSED RECORD SWITCH IN THE    **        
      **  STORES DATA PURGE EXTRACT FILE FOR THE RECORD WHICH IS      **        
      **  PROCESSED TODAY BY PCKUT055.                                **        
      ******************************************************************        
      *                                                                         
       1400-UPDATE-PROCESS-REC-SW.                                              
      *                                                                         
              PERFORM  VARYING PV-SUB  FROM 1 BY 1  UNTIL                       
                      PV-SUB > PV-TABLE-MAX  OR                                 
                      PV-PURGE-DATE(PV-SUB) = ZEROES                            
               IF   ((PV-STORE-START-NUM(PV-SUB) EQUAL                          
                      PC067-STORE-START-NUM) AND                                
                     (PV-STORE-END-NUM(PV-SUB)   EQUAL                          
                      PC067-STORE-END-NUM) AND                                  
                     (PV-PURGE-DATE(PV-SUB)      EQUAL                          
                      PC067-PURGE-DATE) AND                                     
                     (PV-OFFSET-DATE(PV-SUB)     EQUAL                          
                      PC067-OFFSET-DATE))                                       
                      MOVE 'Y'             TO                                   
                         PV-PROCESSED-REC-SW(PV-SUB)                            
                      ADD +1 TO PV-SUB                                          
               END-IF                                                           
              END-PERFORM.                                                      
      *                                                                         
      ******************************************************************        
      **  THIS PARAGRAPH COMPARES THE CURRENT DATE - 370 WITH THE DATE**        
      **  IN FILE AND IF THE GREATER THAN OR EQUAL TO THE CURRENT     **        
      **  AND IF THE GREATER THAN OR EQUAL TO THE CURRENT DATE THE    **        
      **  THAT RECORD WILL BE KEPT. ELSE IT WILL BE TRIMMED FROM THE  **        
      **  STORE DATA PURGE EXTRACT FILE.                              **        
      ******************************************************************        
      *                                                                         
       2000-PROCESS-STORES.                                                     
      *                                                                         
           PERFORM VARYING PV-STORE-IDX FROM 1 BY 1                             
                   UNTIL PV-PURGE-DATE(PV-STORE-IDX) = ZEROES                   
                   OR PV-STORE-IDX > PV-TABLE-MAX                               
             INITIALIZE PC067-STORE-DATA-PURGE-EXTRACT                          
             MOVE PV-PURGE-DATE(PV-STORE-IDX) TO  PV-DATE-TRIM                  
             MOVE PV-DATE-TRIM(1:4)           TO  PV-DATE-CCYY                  
             MOVE PV-DATE-TRIM(6:2)           TO  PV-DATE-MM                    
             MOVE PV-DATE-TRIM(9:2)           TO  PV-DATE-DD                    
             IF (PV-DATE-RETURNED-FROM-ROUTINE >=  PV-DATE) AND                 
                 PV-PROCESSED-REC-SW(PV-STORE-IDX) EQUAL 'Y'                    
                 ADD +1 TO PV-STORES-SKIP-CNT                                   
             ELSE                                                               
               MOVE  PV-STORE-START-NUM(PV-STORE-IDX) TO                        
                     PC067-STORE-START-NUM                                      
               MOVE  PV-STORE-END-NUM(PV-STORE-IDX)   TO                        
                     PC067-STORE-END-NUM                                        
               MOVE  PV-PURGE-DATE(PV-STORE-IDX)      TO                        
                     PC067-PURGE-DATE                                           
               MOVE  PV-OFFSET-DATE(PV-STORE-IDX)     TO                        
                     PC067-OFFSET-DATE                                          
               MOVE  PV-PROCESSED-REC-SW(PV-STORE-IDX)                          
                                                      TO                        
                     PC067-PROCESSED-REC-SW                                     
               PERFORM 8100-WRITE-EXTRACT-RECORD                                
             END-IF                                                             
           END-PERFORM.                                                         
      *                                                                         
      ******************************************************************        
      **  THIS PARAGRAPH CLOSES THE INPUT & OUTPT FILES AND THEN      **        
      **  DISPLAYS THE RECORD COUNT IN THE SYSOUT.                    **        
      ******************************************************************        
      *                                                                         
       3000-END-OF-JOB-ROUTINE.                                                 
      *                                                                         
           CLOSE PC-STORES-PURGE-EXT-FILE                                       
                 PC-STORES-PURGE-TODAY-EXT                                      
                 PC-STORES-PURGE-TRIM-FILE.                                     
      *                                                                         
           DISPLAY '                                                   '        
           DISPLAY '** PCKUT085-TOTAL NO OF STORE EXTRACT RECORDS READ:'        
                    PV-READ-COUNTER-1.                                          
           DISPLAY '** PCKUT085-TOTAL NO OF STORE EXT LOADED IN TABLE :'        
                    PV-TABLE-LOAD-CNT                                           
           DISPLAY '** PCKUT085-TOTAL NO OF STORE EXTRACT TRIMMED     :'        
                   PV-STORES-SKIP-CNT.                                          
           DISPLAY '** PCKUT085-TOTAL NO OF STORE EXTRACT OUT WRITTEN :'        
                   PV-TOTAL-CNT.                                                
           DISPLAY '                                        '.                  
           DISPLAY '** PROGRAM PCKUT085 EXECUTION ENDED   **'.                  
      *                                                                         
      ******************************************************************        
      ** THIS PARAGRAPGH READS THE INPUT STORES DATA PURGE EXTRACT IN **        
      ** PCRD067 LAYOUT.                                              **        
      ******************************************************************        
      *                                                                         
       8000-READ-EXTRACT-FILE.                                                  
      *                                                                         
           INITIALIZE   PC067-STORE-DATA-PURGE-EXTRACT.                         
      *                                                                         
           READ PC-STORES-PURGE-EXT-FILE INTO                                   
                PC067-STORE-DATA-PURGE-EXTRACT                                  
                AT END                                                          
                 SET PS-EOF-STORE-EXT TO TRUE                                   
              NOT AT END                                                        
                   ADD +1 TO PV-READ-COUNTER-1                                  
                   MOVE PC067-STORE-START-NUM  TO                               
                                    PV-STORE-START-NUM(PV-STORE-IDX)            
                   MOVE PC067-STORE-END-NUM    TO                               
                                    PV-STORE-END-NUM(PV-STORE-IDX)              
                   MOVE PC067-PURGE-DATE       TO                       X)      
                                    PV-PURGE-DATE(PV-STORE-IDX)                 
                   MOVE PC067-OFFSET-DATE      TO                       DX)     
                                    PV-OFFSET-DATE(PV-STORE-IDX)                
                   MOVE PC067-PROCESSED-REC-SW TO                               
                        PV-PROCESSED-REC-SW(PV-STORE-IDX)                       
                   ADD 1 TO PV-TABLE-LOAD-CNT                                   
                            PV-TABLE-SIZE                                       
           END-READ.                                                            
      *                                                                         
      ******************************************************************        
      **  THIS PARAGRAPH WRITES THE RECORDS IN TO THE OUTPUT FILE     **        
      **  IN PCRD067 LAYOUT.                                          **        
      ******************************************************************        
      *                                                                         
       8100-WRITE-EXTRACT-RECORD.                                               
      *                                                                         
            WRITE PC-STORES-PURGE-TRIM-REC                                      
             FROM PC067-STORE-DATA-PURGE-EXTRACT.                               
              ADD 1 TO PV-TOTAL-CNT.                                            
      *                                                                         
