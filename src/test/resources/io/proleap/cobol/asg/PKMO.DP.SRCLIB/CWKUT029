      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT029.                                                
       AUTHOR.         CARL RICHTER.                                            
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   05/15/03.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  DUMMY SKU UPC EXTRACT PROGRAM                                          
      *                                                                         
      *  CHANGE LOG:                                                            
      *  10/01/2008   MARIBETH ZAMORA   PROJECT ID 1526 / WR 32284              
      *               CHANGED FOR THE AD ROTATION PROJECT                       
      *                                                                         
      *  CHANGE LOG:                                                            
      *  04/03/2009   LUKE SCHULTE      PROJECT ID 2313                         
      *               CHANGED FOR THE QUANTUM PROJECT                           
      *                                                                         
      *  02/14/2023   JEAN KURK         CHG0281369                              
      *               CHANGE CALL TO UPC CHECK DIGIT ROUTINE DYNAMIC            
      *                                                                         
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CW-CURRENT-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CW-PREVIOUS-FILE                                              
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT CW-CHANGE-FILE                                                
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CW-CURRENT-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-CURRENT-RECORD                  PIC X(343).                       
                                                                                
       FD  CW-PREVIOUS-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-PREVIOUS-RECORD                 PIC X(343).                       
                                                                                
       FD  CW-CHANGE-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-CHANGE-RECORD                   PIC X(344).                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CURRENT-REC.                                                         
           05  CR-CURR-KEY                    PIC   X(12).                      
           05  CR-CURR-DATA                   PIC   X(331).                     
                                                                                
       01  PREVIOUS-REC.                                                        
           05  PR-PREV-KEY                    PIC   X(12).                      
           05  PR-PREV-DATA                   PIC   X(331).                     
                                                                                
       01  CHANGE-OUTPUT-REC.                                                   
           05  COR-CHANGE-KEY                 PIC   X(12).                      
           05  COR-CHANGE-DATA                PIC   X(331).                     
           05  COR-CHANGE-INDICATOR           PIC   X(01).                      
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'CWKUT029'.            
           05  PC-OPERCO                 PIC X(02) VALUE '03'.                  
                                                                                
       01  PS-RUN-TYPE                   PIC X     VALUE 'Y'.                   
           88  PS-INITIAL-RUN                      VALUE 'Y'.                   
           88  PS-NOT-INITIAL-RUN                  VALUE 'N'.                   
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-PRE-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-CUR-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-OUTPUT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-INSERT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-UPDATE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-DELETE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-NO-CHANGE-RECORDS      PIC  9(9)       VALUE ZERO.            
                                                                                
       01  ABEND-CODE                      PIC S9(04)    VALUE ZERO             
                                                         COMP SYNC.             
      ****                                                                      
      *   UPC CHECK DIGIT PARAMETER LIST                                        
      ****                                                                      
                                                                                
           COPY DPWS040I.                                                       
                                                                                
      ****                                                                      
      *   DUMMY SKU/DUMMY UPC RECORD LAYOUT.                                    
      ****                                                                      
                                                                                
           COPY CWRD002.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL CR-CURR-KEY   = HIGH-VALUES                                
                     AND                                                        
                     PR-PREV-KEY   = HIGH-VALUES.                               
                                                                                
           IF  PS-INITIAL-RUN                                                   
               PERFORM 4000-ADD-EXTRA-DEPTS                                     
           END-IF.                                                              
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  CW-CURRENT-FILE                                          
                       CW-PREVIOUS-FILE                                         
                OUTPUT CW-CHANGE-FILE.                                          
                                                                                
           SET PS-INITIAL-RUN  TO  TRUE.                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 8000-READ-CURRENT.                                           
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
                                                                                
           INITIALIZE     CHANGE-OUTPUT-REC.                                    
                                                                                
           IF CR-CURR-KEY  =  PR-PREV-KEY                                       
               PERFORM 2100-UPDATE-OR-NOCHANGE                                  
                                                                                
           ELSE                                                                 
               IF CR-CURR-KEY  <  PR-PREV-KEY                                   
                   PERFORM 2200-INSERT-CURRENT                                  
                                                                                
               ELSE                                                             
                   PERFORM 2300-DELETE-PREVIOUS                                 
                                                                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  CHECKS SECOND KEY FOR EQUALITY                                         
      *  IF DATA IS NOT EQUAL, THEN RECORD IS AN UPDATE                         
      ******************************************************************        
       2100-UPDATE-OR-NOCHANGE.                                                 
                                                                                
           IF CR-CURR-DATA  =  PR-PREV-DATA                                     
                                                                                
               ADD +1                 TO PA-NO-CHANGE-RECORDS                   
                                                                                
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
                                                                                
           ELSE                                                                 
                                                                                
               MOVE CR-CURR-KEY       TO COR-CHANGE-KEY                         
               MOVE CR-CURR-DATA      TO COR-CHANGE-DATA                        
               MOVE 'U'               TO COR-CHANGE-INDICATOR                   
                                                                                
               ADD +1                 TO PA-UPDATE-RECORDS                      
                                                                                
               PERFORM 7100-WRITE                                               
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  INSERT FROM CURRENT RECORD                                             
      ******************************************************************        
       2200-INSERT-CURRENT.                                                     
                                                                                
           MOVE CR-CURR-KEY   TO COR-CHANGE-KEY                                 
           MOVE CR-CURR-DATA  TO COR-CHANGE-DATA                                
           MOVE 'I'           TO COR-CHANGE-INDICATOR                           
                                                                                
           ADD +1             TO PA-INSERT-RECORDS                              
                                                                                
           PERFORM 7100-WRITE                                                   
           PERFORM 8000-READ-CURRENT.                                           
                                                                                
                                                                                
      ******************************************************************        
      *  DELETE FROM PREVIOUS RECORD                                            
      ******************************************************************        
       2300-DELETE-PREVIOUS.                                                    
                                                                                
           MOVE PR-PREV-KEY   TO COR-CHANGE-KEY                                 
           MOVE PR-PREV-DATA  TO COR-CHANGE-DATA                                
           MOVE 'D'           TO COR-CHANGE-INDICATOR                           
                                                                                
           ADD +1             TO PA-DELETE-RECORDS                              
                                                                                
           PERFORM 7100-WRITE                                                   
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      *  ADD DEPTS 981 AND 983                                                  
      *  THIS IS ONLY DONE DURING THE INITIAL INSERT                            
      ******************************************************************        
       4000-ADD-EXTRA-DEPTS.                                                    
                                                                                
      *                                                                         
      **** DEPT 981                                                             
      *                                                                         
           INITIALIZE CW002-RECORD                                              
           MOVE       '0981'               TO   CW002-DEPT-NBR                  
           MOVE       98100000             TO   CW002-SKU-NBR                   
                                                                                
           PERFORM    4100-MOVE-DEFAULT-VALUES                                  
           PERFORM    4200-CALL-UPC-CHECK                                       
                                                                                
           MOVE       CW002-RECORD         TO   CHANGE-OUTPUT-REC               
           MOVE       'I'                  TO   COR-CHANGE-INDICATOR            
           PERFORM    7100-WRITE.                                               
                                                                                
      *                                                                         
      **** DEPT 983                                                             
      *                                                                         
           INITIALIZE CW002-RECORD                                              
           MOVE       '0983'               TO   CW002-DEPT-NBR                  
           MOVE       98300000             TO   CW002-SKU-NBR                   
                                                                                
           PERFORM    4100-MOVE-DEFAULT-VALUES                                  
           PERFORM    4200-CALL-UPC-CHECK                                       
                                                                                
           MOVE       CW002-RECORD         TO   CHANGE-OUTPUT-REC               
           MOVE       'I'                  TO   COR-CHANGE-INDICATOR            
           PERFORM    7100-WRITE.                                               
                                                                                
                                                                                
      ******************************************************************        
      *  MOVE DEFAULTS FOR DEPTS 981 AND 983                                    
      ******************************************************************        
       4100-MOVE-DEFAULT-VALUES.                                                
                                                                                
           MOVE '0000'                  TO  CW002-MAJ-CL-NBR                    
           MOVE '0000'                  TO  CW002-SUB-CL-NBR                    
                                                                                
           MOVE '0000000000'            TO  CW002-RMS-STYL-ID.                  
           MOVE '00'                    TO  CW002-SKU-STAT-CDE.                 
           MOVE '0000000001'            TO  CW002-KOHLS-SIZE-CDE.               
           MOVE '999'                   TO  CW002-NRF-CLOR-CDE.                 
           MOVE '0 '                    TO  CW002-SIZE-UM-CDE                   
                                            CW002-WT-UM-CDE.                    
           MOVE 'ASSORTED'              TO  CW002-CSTM-CLOR-DESC                
                                            CW002-CUSTFACG-CLOR-DESC.           
           MOVE 'I'                     TO  CW002-UPC-TYP-CDE.                  
           MOVE 'G'                     TO  CW002-UPC-SRC-CDE.                  
           MOVE '9999-09-09'            TO  CW002-CERTD-DTE.                    
           MOVE '1900-01-01'            TO  CW002-EFF-BEG-DTE.                  
                                                                                
                                                                                
      ******************************************************************        
      *  CALL UPC CHECK USING THE DEPT NUMBER (981 AND 982)                     
      *  TO GET THE UPC-NBR                                                     
      ******************************************************************        
       4200-CALL-UPC-CHECK.                                                     
                                                                                
           SET  DP040I-GEN-INTERNAL-UPC-OPTION TO TRUE.                    CL**2
                                                                                
           MOVE CW002-DEPT-NBR           TO DP040I-DEPARTMENT-NUMBER.           
           MOVE '0000'                   TO DP040I-CLASS-NUMBER.                
           MOVE '000'                    TO DP040I-SEQUENCE-NUMBER.             
                                                                                
           CALL DP040I-UPC-CHECK-DIGIT-WO-IUPC                                  
                           USING DP040I-UPC-CHECK-DIGIT-PARMS.                  
                                                                                
           IF DP040I-NO-ERROR-DETECTED                                          
               MOVE DP040I-UPC-CODE      TO CW002-UPC-NBR                       
                                                                                
           ELSE                                                                 
                                                                                
               DISPLAY '**** WARNING ****'                                      
               DISPLAY 'BAD RETURN CODE FROM UPC CHECK DIGIT ROUTINE'           
               DISPLAY 'FOR SKU NUMBER: ' DP040I-SKU-NUMBER                     
               DISPLAY 'UPC CHECK ERR : ' DP040I-ERROR-INDICATOR                
               MOVE +4001 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE THE OUTPUT RECORD                                                
      ******************************************************************        
       7100-WRITE.                                                              
                                                                                
           WRITE  CW-CHANGE-RECORD FROM CHANGE-OUTPUT-REC.                      
           ADD +1            TO PA-OUTPUT-RECORDS.                              
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE CURRENT FILE.                                        *        
      ******************************************************************        
       8000-READ-CURRENT.                                                       
                                                                                
           READ CW-CURRENT-FILE                                                 
               INTO CURRENT-REC                                                 
               AT END                                                           
                   MOVE HIGH-VALUES  TO  CR-CURR-KEY                            
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-CUR-RECORDS                     
           END-READ.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE PREVIOUS FILE.                                                
      ******************************************************************        
       8100-READ-PREVIOUS.                                                      
                                                                                
           READ CW-PREVIOUS-FILE                                                
               INTO PREVIOUS-REC                                                
               AT END                                                           
                   MOVE HIGH-VALUES       TO  PR-PREV-KEY                       
               NOT AT END                                                       
                   ADD 1                  TO  PA-INPUT-RECORDS                  
                   ADD 1                  TO  PA-INPUT-PRE-RECORDS              
                   SET PS-NOT-INITIAL-RUN TO  TRUE                              
           END-READ.                                                            
                                                                                
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
           DISPLAY '   '.                                                       
                                                                                
           DISPLAY 'INPUT RECS READ    : ' PA-INPUT-RECORDS.                    
           DISPLAY 'INPUT PREVIOUS RECS: ' PA-INPUT-PRE-RECORDS.                
           DISPLAY 'INPUT CURRENT RECS : ' PA-INPUT-CUR-RECORDS.                
           DISPLAY '   '.                                                       
           DISPLAY 'OUTPUT RECS WRITTEN: ' PA-OUTPUT-RECORDS.                   
           DISPLAY 'INSERT RECS WRITTEN: ' PA-INSERT-RECORDS.                   
           DISPLAY 'UPDATE RECS WRITTEN: ' PA-UPDATE-RECORDS.                   
           DISPLAY 'DELETE RECS WRITTEN: ' PA-DELETE-RECORDS.                   
           DISPLAY 'NO CHANGE RECORDS  : ' PA-NO-CHANGE-RECORDS.                
                                                                                
           CLOSE CW-CURRENT-FILE                                                
                 CW-PREVIOUS-FILE                                               
                 CW-CHANGE-FILE.                                                
                                                                                
      ******************************************************************        
      ******************************************************************        
